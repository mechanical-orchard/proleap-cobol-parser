000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MAKUT041.                                                 
000300 AUTHOR.        TESS BESTE.                                               
000400 DATE-WRITTEN.  10/10/1999.                                               
000500                                                                          
000600*----------------------------------------------------------------*        
000700*            MA - CREATE RCP DATA LOAD FILE                               
000800*----------------------------------------------------------------*        
000900* THIS PROGRAM WAS COPIED FROM PROGRAM MAKUT026.                          
001000*                                                                         
001100* THIS PROGRAM WILL CREATE A LOAD FILE THAT WILL BE USED TO LOAD          
001200* ORACLE TABLE TMARCPX.  IT WILL READ AN EXTRACT FILE CONTAINING          
001300* THE CURRENTLY EFFECTIVE REGIONAL CLEARANCE PRICING DATA THAT            
001400* WAS EXTRACTED FROM FASHION SALES VSM.  FOR EACH RECORD ON THE           
001500* EXTRACT FILE, THE SKU NUMBER WILL BE CONVERTED TO THE ITEM              
001600* CLUSTER ID, THE STORE NUMBER WILL BE CONVERTED TO THE ORG               
001700* CLUSTER ID, AND THE RECORD WILL BE WRITTEN TO THE OUTPUT LOAD           
001800* FILE.                                                                   
001900*                                                                         
002000* THE EXTRACT FILE IS IN SEQUENCE BY SKU NUMBER, STORE NUMBER.            
002100* TO CONVERT THE SKU NUMBERS, EACH TIME THERE IS A BREAK ON               
002200* SKU NUMBER, THE SKU NUMBER WILL BE USED TO RETRIEVE THE ITEM            
002300* CLUSTER ID FROM ORACLE TABLE TITMCLT.  TO CONVERT THE STORE             
002400* NUMBER, THE ORG CLUSTER IDS FOR ALL STORES WILL BE RETRIEVED            
002500* FROM ORACLE TABLE TORGCLT AND TABLED IN WORKING STORAGE.  FOR           
002600* EACH EXTRACT RECORD, THE STORE NUMBER WILL THEN BE LOOKED UP            
002700* IN THE WORKING STORAGE TABLE TO GET ITS ORG CLUSTER ID. R               
002800*                                                                         
002900* FRANK SANTANDREA 02/25/2000                                             
003000* ADDED THE END DATE AND STATUS CODES AS PART OF THE SECOND               
003100* PHASE FOR RCP.                                                          
003200* ADDED HIER_CDE OF 'CO' TO BRING BACK THE CORP ORG-ID.                   
003300* FIXED LOGIC PROBLEM FOR FINDING THE CORP ORG-ID.                        
003400*                                                                         
003500* ANN SUN 03/20/2000                                                      
003600* ADDED THE PV-INITIAL-END-TMST AS '0000-00-00-11.59.59'.                 
003700* CHANGED TO INITIALZE THE MA041-EFF-END-TMST FROM                        
003800* PV-INITIAL-END-TMST.                                                    
003900*                                                                         
004000* ANN SUN 09/11/2000                                                      
004100* SINCE THE RESTART LOGIC HAS NEVER WORKED, WE TEMPORAILY                 
004200* BLOCKED OUT THE LOGIC TO SET RUN-IN-PROCESS TO TRUE.                    
004300* ADDED SET THE RUN-COMPELTE AS TRUE. WHEN WE HAVE TIME,                  
004400* WE SHOULD FIX THE RESTART LOGIC FOR THIS PROGRAM.                       
004500*                                                                         
004600* DONALD TOMLINSON  10/24/2001                                            
004700* REMOVED ALL ORACLE LOGIC FOR OBTAINING TITMCLT AND TORGCLT              
004800* TABLES AND REPLACED WITH EXTRACT FILES OF THESE TWO TABLES.             
004900* NO CHECKPOINT LOGIC EXISTS ANYMORE ALSO. PROGRAM WILL RUN               
005000* MUCH FASTER NOW. REPLACED HARD CODED TORGCLT AND TITMCLT                
005010* EXTRACT WORKING STORAGE DEFINITIONS WITH MARD042 & MARD043              
005020* COPY MEMBERS.                                                           
005030*                                                                         
005040* NICK MOULTON 06/17/2003                                                 
005050* NON-INTELLIGENT SKU CHANGES.                                            
005060*                                                                         
DGM001* DAVE METZGER 12/01/2004 WR25478                                         
DGM001* CHANGED CODE TO ACCOMODATE A 4 BYTES STORE NUMBER.                      
DGM001* RCP EXTRACT INCREASED FROM 40 TO 60 (MARD040)                           
216116*                                                                         
216116* 01/18/2019  JIM HUNTER                  CHG0216116                      
216116*    MAINFRAME REFACTORING - INCREASE ORG-CLUSTER-ID (STORE)              
216116*    OCCURS FROM 3000 TO 9999.                                            
258201*                                                                         
258201* 04/28/2021  JIM HUNTER                  CHG0258201                      
258201*    BYPASSING DISPLAY IN PARA. 2200-GET-ITEM-CLUSTER-INFO                
258201*    AFTER 5,000 ERRORS HAVE BEEN DISPLAYED THAT IS CURRENTLY             
258201*    WRITING UP TO 27 MILLION RECORDS TO SYSOUT DAILY.                    
005100*----------------------------------------------------------------*        
005200                                                                          
005300 ENVIRONMENT DIVISION.                                                    
005400 INPUT-OUTPUT SECTION.                                                    
005500 FILE-CONTROL.                                                            
005600                                                                          
005700     SELECT RCP-DATA-EXTRACT-FILE                                         
005800         ASSIGN TO INP01.                                                 
005900                                                                          
006000     SELECT RCP-DATA-LOAD-FILE                                            
006100         ASSIGN TO IO02.                                                  
006200                                                                          
006300     SELECT TITMCLT-EXTRACT-FILE                                          
006400         ASSIGN TO INP03.                                                 
006500                                                                          
006600     SELECT TORGCLT-EXTRACT-FILE                                          
006700         ASSIGN TO INP04.                                                 
006800*                                                                         
006900*                                                                         
007000 DATA DIVISION.                                                           
007100 FILE SECTION.                                                            
007200 FD  RCP-DATA-EXTRACT-FILE                                                
007300     RECORDING MODE IS F                                                  
007400     BLOCK CONTAINS 0 RECORDS.                                            
DGM001 01  RCP-DATA-EXTRACT-RECORD         PIC  X(60).                          
007600*                                                                         
007700*                                                                         
007800 FD  RCP-DATA-LOAD-FILE                                                   
007900     RECORDING MODE IS F                                                  
008000     BLOCK CONTAINS 0 RECORDS.                                            
008100 01  RCP-DATA-LOAD-RECORD            PIC  X(63).                          
008200*                                                                         
008300*                                                                         
008400 FD  TITMCLT-EXTRACT-FILE                                                 
008500     RECORDING MODE IS F                                                  
008600     BLOCK CONTAINS 0 RECORDS.                                            
008700 01  TITMCLT-EXTRACT-RECORD          PIC  X(25).                          
008800*                                                                         
008900*                                                                         
009000 FD  TORGCLT-EXTRACT-FILE                                                 
009100     RECORDING MODE IS F                                                  
009200     BLOCK CONTAINS 0 RECORDS.                                            
009300 01  TORGCLT-EXTRACT-RECORD          PIC  X(13).                          
009400*                                                                         
009500*                                                                         
009600 WORKING-STORAGE SECTION.                                                 
009700                                                                          
009800 01  INPUT-FILE-STATUS           PIC X(2) VALUE SPACES.                   
009900                                                                          
010000 01  PROGRAM-CONSTANTS.                                                   
010100     05  PC-I                    PIC  X(01) VALUE 'I'.                    
010200     05  PC-MULT-VALUES          PIC S9(04) VALUE -2112 COMP.             
010300     05  PC-COMA                 PIC  X(01) VALUE ','.                    
010400                                                                          
010500 01  PROGRAM-VARIABLES.                                                   
010600     05  PV-EXTRACT-NO-ITEM-COUNT   PIC S9(09) VALUE +0 COMP.             
010700     05  PV-RCP-DATA-LOAD-REC-COUNT PIC S9(09) VALUE +0 COMP.             
010800     05  PV-SKU-NO-ITEM-COUNT       PIC S9(09) VALUE +0 COMP.             
010900     05  PV-RCP-DATA-EXTRACT-REC-COUNT                                    
011000                                    PIC S9(09) VALUE +0 COMP.             
011100     05  PV-STORE-NO-ORG-UNIT-COUNT PIC S9(09) VALUE +0 COMP.             
011200     05  PV-TITMCLT-EXTRACT-REC-COUNT                                     
011300                                    PIC S9(09) VALUE +0 COMP.             
011400     05  PV-TORGCLT-EXTRACT-REC-COUNT                                     
011500                                    PIC S9(09) VALUE +0 COMP.             
011600     05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP.             
011700     05  PV-SUB                     PIC S9(04) VALUE +0 COMP.             
011800     05  PV-SUB-ORG-CLUSTER         PIC S9(04) VALUE +0 COMP.             
011900     05  PV-SUB-STORE               PIC S9(04) VALUE +0 COMP.             
012000     05  PV-ABEND-PARAGRAPH         PIC X(30)  VALUE SPACES.              
012100     05  PV-HOLD-ITEM-NAME          PIC X(15)  VALUE SPACES.              
DGM001     05  PV-RESTART-STORE           PIC X(04)  VALUE SPACES.              
012300     05  PV-INITIAL-STRT-TMST       PIC X(19)      VALUE                  
012400                                    '0000-00-00-00.00.00'.                
012500     05  PV-INITIAL-END-TMST        PIC X(19)      VALUE                  
012600                                    '0000-00-00-23.59.59'.                
012700     05  PV-ITEM-CLUSTER-NAME       PIC X(20)  VALUE SPACES.              
012800     05  PV-ALL-NINES               PIC X(15)  VALUE                      
012900                                          '999999999999999'.              
013000                                                                          
013100 01  PROGRAM-SWITCHES.                                                    
013200     05  PS-RCP-DATA-LOAD-EOF-SW       PIC X  VALUE 'N'.                  
013300         88 PS-RCP-DATA-LOAD-EOF              VALUE 'Y'.                  
013400     05  PS-END-OF-RCP-DATA-EXTRACT-SW PIC X  VALUE 'N'.                  
013500         88 PS-END-OF-RCP-DATA-EXTRACT        VALUE 'Y'.                  
013600     05  PS-END-OF-TORGCLT-EXTRACT-SW  PIC X  VALUE 'N'.                  
013700         88 PS-END-OF-TORGCLT-EXTRACT         VALUE 'Y'.                  
013800     05  PS-END-OF-TITMCLT-EXTRACT-SW  PIC X  VALUE 'N'.                  
013900         88 PS-END-OF-TITMCLT-EXTRACT         VALUE 'Y'.                  
014000     05  PS-ITEM-CLUSTR-FOUND-SW       PIC X  VALUE 'N'.                  
014100         88 PS-ITEM-CLUSTR-ID-NOT-FOUND       VALUE 'N'.                  
014200         88 PS-ITEM-CLUSTR-ID-FOUND           VALUE 'Y'.                  
014300                                                                          
014400 01  PV-ORG-CLUSTER-NAME.                                                 
DGM001     05  PV-ORG-CLUSTER-NAME-NUM PIC 9(04)   VALUE ZERO.                  
014600                                                                          
014700                                                                          
014800 01  ERROR-CODE                  PIC S9(04) VALUE +0 COMP.                
014900     88  ERROR-1001                         VALUE -1001.                  
015000     88  ERROR-1002                         VALUE -1002.                  
015100     88  ERROR-1003                         VALUE -1003.                  
015200     88  ERROR-1004                         VALUE -1004.                  
015300     88  ERROR-1005                         VALUE -1005.                  
015400     88  ERROR-1006                         VALUE -1006.                  
015500     88  ERROR-1007                         VALUE -1007.                  
015600     88  ERROR-1008                         VALUE -1008.                  
015700     88  ERROR-1009                         VALUE -1009.                  
015800     88  ERROR-1011                         VALUE -1011.                  
015900     88  ERROR-1012                         VALUE -1012.                  
016000     88  ERROR-1014                         VALUE -1014.                  
016100     88  ERROR-1016                         VALUE -1016.                  
016200     88  ERROR-1017                         VALUE -1017.                  
016300                                                                          
016400 01  ERROR-1004-MESSAGE.                                                  
016500     05  FILLER                  PIC X(45)      VALUE                     
016600             'RESTART ORG UNIT CLUSTER ID NOT FOUND.  ID = '.             
016700     05  E1004M-ORG-CLUSTR-ID    PIC 9(09)      VALUE ZERO.               
016800                                                                          
016900 01  ERROR-1011-MESSAGE.                                                  
017000     05  FILLER                  PIC X(41)                                
017100             VALUE 'RESTART ITEM CLUSTER ID NOT FOUND.  ID = '.           
017200     05  E1011M-ITEM-CLUSTR-ID   PIC 9(09)      VALUE ZERO.               
017300                                                                          
017400 01  ERROR-MESSAGE.                                                       
017500     05  FILLER                  PIC X(29)                                
017600             VALUE '*** ERROR IN PROGRAM MAKUT041'.                       
017700     05  FILLER                  PIC X(14)                                
017800                                 VALUE '; ERROR CODE ='.                  
017900     05  EM-ERROR-CODE           PIC --------9  VALUE ZERO.               
018000     05  FILLER                  PIC X(15)                                
018100                                 VALUE '; ERROR DESC = '.                 
018200     05  EM-ERROR-DESC           PIC X(65)   VALUE SPACES.                
018300                                                                          
018400 01  LOG-MESSAGE.                                                         
018500     05  FILLER                  PIC X(23)                                
018600             VALUE '*** PROGRAM MAKUT041 - '.                             
018700     05  LM-MESSAGE              PIC X(109)  VALUE SPACES.                
018800                                                                          
018900 01  ITEM-NOT-FOUND-LOG-MSG.                                              
019000     05  FILLER                  PIC X(37)                                
019100             VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.                   
019200     05  INF-LM-ITEM-NAME        PIC X(20)   VALUE SPACES.                
019300     05  INF-LM-SKU              PIC X(15)   VALUE SPACES.                
019400                                                                          
019500 01  INVALID-STORE-MSG.                                                   
019600     05  FILLER                  PIC X(35)                                
019700             VALUE 'INVALID ORG CLUSTER NAME (STORE) = '.                 
019800     05  ISM-ORG-CLUSTR-NAME     PIC X(20)      VALUE SPACES.             
019900     05  FILLER                  PIC X(43)                                
020000             VALUE ' RETURNED FROM TORGCLT WITH A CLUSTER ID = '.         
020100     05  ISM-ORG-CLUSTR-ID       PIC X(10)      VALUE SPACES.             
020200                                                                          
020300 01  MULT-ITEMS-FOUND-LOG-MSG.                                            
020400     05  FILLER                  PIC X(43)                                
020500             VALUE 'MULTIPLE ITEM CLUSTER ROWS FOUND FOR SKU = '.         
020600     05  MIF-LM-SKU              PIC X(15)   VALUE SPACES.                
020700     05  MIF-LM-ITEM-NAME        PIC X(20)   VALUE SPACES.                
020800                                                                          
020900                                                                          
021000 01  RUN-SUMMARY-LOG-MSG1.                                                
021100     05  FILLER                  PIC X(35)                                
021200             VALUE 'RCP DATA EXTRACT RECORDS READ = '.                    
021300     05  RSLM1-RCP-DATA-EXTRACT-RECS PIC Z(08)9   VALUE ZERO.             
021400                                                                          
021500 01  RUN-SUMMARY-LOG-MSG2.                                                
021600     05  FILLER                  PIC X(33)                                
021700             VALUE 'RCP DATA LOAD RECORDS WRITTEN = '.                    
021800     05  RSLM2-RCP-DATA-LOAD-RECS PIC Z(08)9 VALUE ZERO.                  
021900                                                                          
022000 01  RUN-SUMMARY-LOG-MSG3.                                                
022100     05  FILLER                  PIC X(37)                                
022200             VALUE 'NUMBER OF SKUS WITH NO MATCHING ITEM '.               
022300     05  FILLER                  PIC X(13)                                
022400             VALUE 'CLUSTER ID = '.                                       
022500     05  RSLM3-SKU-NO-ITEM-RECS  PIC Z(08)9  VALUE ZERO.                  
022600                                                                          
022700 01  RUN-SUMMARY-LOG-MSG5.                                                
022800     05  FILLER                  PIC X(43)                                
022900             VALUE 'NUMBER OF RCP DATA EXTRACT RECORDS DROPPED '.         
023000     05  FILLER                  PIC X(39)                                
023100             VALUE 'DUE TO NO MATCHING ITEM CLUSTER ID= '.                
023200     05  RSLM5-EXTRACT-NO-ITEM-RECS                                       
023300                                 PIC Z(08)9  VALUE ZERO.                  
023400                                                                          
023500 01  RUN-SUMMARY-LOG-MSG6.                                                
023600     05  FILLER                  PIC X(38)                                
023700             VALUE 'NUMBER OF RCP DATA EXTRACT RECORDS DRO'.              
023800     05  FILLER                  PIC X(38)                                
023900             VALUE 'PPED DUE TO NO MATCHING ORG UNIT = '.                 
024000     05  RSLM6-STORE-NO-ORG-RECS PIC Z(08)9  VALUE ZERO.                  
024100                                                                          
024200 01  RUN-SUMMARY-LOG-MSG7.                                                
024300     05  FILLER                  PIC X(34)                                
024400             VALUE 'NUMBER OF TORGCLT EXTRACT RECORDS '.                  
024500     05  RSLM7-TORGCLT-EXTRACT-RECS                                       
024600                                 PIC Z(08)9  VALUE ZERO.                  
024700                                                                          
024800 01  RUN-SUMMARY-LOG-MSG8.                                                
024900     05  FILLER                  PIC X(34)                                
025000             VALUE 'NUMBER OF TITMCLT EXTRACT RECORDS '.                  
025100     05  RSLM8-TITMCLT-EXTRACT-RECS                                       
025200                                 PIC Z(08)9  VALUE ZERO.                  
025300                                                                          
025400                                                                          
025500*----------------------------------------------------------------*        
025600* ORG-CLUSTER-TABLE LOADED FROM MA042-ORG-CLUSTR-RECORD IN                
025700* 1200-TABLE-ORG-CLUST-ROWS PARAGRAPH.                                    
025800*----------------------------------------------------------------*        
025900 01  ORG-CLUSTER-TABLE.                                                   
216116*    05  ORG-CLUSTER-ID    OCCURS 3000 TIMES  PIC S9(9)  COMP.            
216116     05  ORG-CLUSTER-ID    OCCURS 9999 TIMES  PIC S9(9)  COMP.            
026100                                                                          
026200*----------------------------------------------------------------*        
026300* RECORD LAYOUT FOR THE RCP DATA EXTRACT FILE.                   *        
026400*----------------------------------------------------------------*        
026500     COPY MARD040.                                                        
026600                                                                          
026700*----------------------------------------------------------------*        
026800* RECORD LAYOUT FOR THE RCP DATA LOAD FILE.                      *        
026900*----------------------------------------------------------------*        
027000     COPY MARD041.                                                        
027100                                                                          
027200*----------------------------------------------------------------*        
027300* COBOL DECLARATION FOR TORGCLT EXTRACT FILE                              
027400*----------------------------------------------------------------*        
027410     COPY MARD042.                                                        
027900                                                                        32
028000*----------------------------------------------------------------*        
028100* COBOL DECLARATION FOR TITMCLT EXTRACT FILE                              
028200*----------------------------------------------------------------*        
028210     COPY MARD043.                                                        
028700                                                                          
028800 EJECT                                                                    
028900 PROCEDURE DIVISION.                                                      
029000                                                                          
029100 0000-MAINLINE.                                                           
029200                                                                          
029300     DISPLAY 'MAKUT041 - START'.                                          
029400     PERFORM 1000-SET-UP.                                                 
029500                                                                          
029600     PERFORM 2000-PROCESS-INPUT.                                          
029700                                                                          
029800     PERFORM 3000-WRAP-UP.                                                
029900                                                                          
030000     DISPLAY 'MAKUT041 - SUCCESSFUL END'.                                 
030100     MOVE ZERO TO RETURN-CODE.                                            
030200     GOBACK.                                                              
030300                                                                          
030400                                                                          
030410*  THIS PARAGRAPH LOADS THE INTERNAL ORG CLUSTER TABLE AND OPENS          
030420*  ALL FILES.                                                             
030500 1000-SET-UP.                                                             
030900                                                                          
031000     MOVE '1000-SETUP' TO PV-ABEND-PARAGRAPH.                             
031400                                                                          
034200     INITIALIZE ORG-CLUSTER-TABLE.                                        
034300                                                                          
034400     OPEN INPUT TITMCLT-EXTRACT-FILE                                      
034500                TORGCLT-EXTRACT-FILE.                                     
034600                                                                          
034700     PERFORM 1110-READ-TORGCLT-EXTRACT.                                   
034800                                                                          
034900     IF PV-TORGCLT-EXTRACT-REC-COUNT = +0                                 
035000*      EMPTY FILE ERROR                                                   
035100         SET ERROR-1003          TO TRUE                                  
035200         DISPLAY PV-ABEND-PARAGRAPH                                       
035300         MOVE ERROR-CODE         TO EM-ERROR-CODE                         
035400         MOVE 'EMPTY ORG UNIT CLUSTER TABLE'  TO EM-ERROR-DESC            
035500         PERFORM 9000-HANDLE-ERROR                                        
035600     END-IF.                                                              
035700                                                                          
035800     PERFORM 1200-TABLE-ORG-CLUST-ROWS                                    
035900         UNTIL PS-END-OF-TORGCLT-EXTRACT.                                 
036000                                                                          
036100                                                                          
036200     DISPLAY 'MAKUT041 - NORMAL RUN'                                      
036300     OPEN INPUT RCP-DATA-EXTRACT-FILE                                     
036400          OUTPUT RCP-DATA-LOAD-FILE.                                      
036500                                                                          
036600                                                                          
036800******************************************************************        
036900* 1110-READ-TORGCLT-EXTRACT.                                     *        
037000*                                                                *        
037100******************************************************************        
037200 1110-READ-TORGCLT-EXTRACT.                                               
037300                                                                          
037400                                                                          
037500     READ TORGCLT-EXTRACT-FILE INTO MA042-ORG-CLUSTR-RECORD               
037600         AT END      SET PS-END-OF-TORGCLT-EXTRACT TO TRUE                
037700         NOT AT END  ADD +1 TO PV-TORGCLT-EXTRACT-REC-COUNT.              
037800                                                                          
037810                                                                          
037900*****************************************************************         
038000* 1200-TABLE-ORG-CLUST-ROWS.                                    *         
038100*                                                               *         
038200*  THIS PARAGRAPH LOADS THE CLUSTER IDS INTO THE ORG CLUSTER    *         
038300*  TABLE FROM THE TORGCLT-EXTRACT-RECORD.                       *         
038400*****************************************************************         
038600 1200-TABLE-ORG-CLUST-ROWS.                                               
038700                                                                          
038800     MOVE MA042-ORG-CLUSTR-STORE TO PV-ORG-CLUSTER-NAME.                  
038900     IF PV-ORG-CLUSTER-NAME-NUM NUMERIC                                   
039000         MOVE PV-ORG-CLUSTER-NAME-NUM                                     
039100                               TO PV-SUB-ORG-CLUSTER                      
039200         MOVE MA042-ORG-CLUSTR-ID                                         
039300               TO ORG-CLUSTER-ID (PV-SUB-ORG-CLUSTER).                    
039400                                                                          
039500     PERFORM 1110-READ-TORGCLT-EXTRACT.                                   
039700                                                                          
039800                                                                          
039900*****************************************************************         
040000* 2000-PROCESS-INPUT.                                                     
040100*                                                                         
040200*  THIS PROCEDURE READS EACH RCP DATA EXTRACT RECORD.  THE FILE           
040300*  IS SORTED IN SKU, STORE SEQUENCE.  IF A NEW SKU IS FOUND,              
040400*  THE MATCHING ITEM CLUSTER ID IS OBTAINED BY DOING A SELECT ON          
040500*  THE ITEM CLUSTER TABLE.  THE MATCHING ORG UNIT CLUSTER ID              
040600*  IS OBTAINED FROM THE ORG-CLUSTER-ID TABLE.  THE TWO IDS AND            
040700*  THE STATUS CHANGE DATE ARE WRITTEN TO THE RCP DATA LOAD FILE.          
040800*****************************************************************         
041000 2000-PROCESS-INPUT.                                                      
041100                                                                          
041200     MOVE '2000-PROCESS-INPUT' TO PV-ABEND-PARAGRAPH.                     
041300                                                                          
041400     PERFORM 2050-READ-RCP-DATA-EXTRACT-REC.                              
041600     IF  PS-END-OF-RCP-DATA-EXTRACT                                       
041700     AND PV-RCP-DATA-EXTRACT-REC-COUNT = ZERO                             
042400         DISPLAY ' INPUT FILE IS EMPTY.'                                  
042500     END-IF.                                                              
042600                                                                          
042700     PERFORM 2100-PROCESS-RCP-DATA-EXTRACT                                
042800         UNTIL PS-END-OF-RCP-DATA-EXTRACT.                                
042900                                                                          
043000     IF PV-RCP-DATA-LOAD-REC-COUNT = ZERO                                 
043700         DISPLAY 'OUTPUT FILE IS EMPTY.'                                  
043800     END-IF.                                                              
043900                                                                          
044000                                                                          
044010*    THIS PARAGRAPH READS EACH RCP DATA EXTRACT EXTRACT RECORD            
044100 2050-READ-RCP-DATA-EXTRACT-REC.                                          
044200                                                                          
044500     READ RCP-DATA-EXTRACT-FILE INTO MA040-RCP-DATA                       
044600         AT END      SET PS-END-OF-RCP-DATA-EXTRACT TO TRUE               
044700         NOT AT END  ADD +1 TO PV-RCP-DATA-EXTRACT-REC-COUNT.             
044800                                                                          
044900                                                                          
044910*    THIS PARAGRAPH PROCESSES EACH RCP DATA EXTRACT RECORD,               
044920*    CREATING AN RCP DATA LOAD OUTPUT RECORD.                             
045000 2100-PROCESS-RCP-DATA-EXTRACT.                                           
045400                                                                          
045500     MOVE SPACES         TO PV-HOLD-ITEM-NAME.                            
045600     SET PS-ITEM-CLUSTR-ID-FOUND TO TRUE.                                 
045700     PERFORM 2200-GET-ITEM-CLUSTER-INFO                                   
045800         UNTIL MA040-ITM-NAME = PV-HOLD-ITEM-NAME                         
045900         OR PS-ITEM-CLUSTR-ID-NOT-FOUND.                                  
046000                                                                          
046100     IF PS-ITEM-CLUSTR-ID-FOUND                                           
046200***** IF THE STORE NUMBER IS 0000 - USE THE 0090 AS THE                   
046300***** INDEX.                                                              
DGM001         IF MA040-STR-NBR = '0000'                                        
DGM001            MOVE '0090'                TO PV-SUB-STORE                    
046600         ELSE                                                             
046700            MOVE MA040-STR-NBR        TO PV-SUB-STORE                     
046800         END-IF                                                           
046900         MOVE ORG-CLUSTER-ID (PV-SUB-STORE)                               
047000                                      TO MA041-ORG-CLUSTR-ID              
047100         IF MA041-ORG-CLUSTR-ID NOT = ZERO                                
047200*                                                                         
047300* ADDED TWO NEW COLUMNS AND CHANGED THE NAME OF ONE.                      
047400*                                                                         
047500*            MOVE MA040-STAT-CHG-DTE  TO MA041-STAT-CHG-DTE               
047600             MOVE PV-INITIAL-STRT-TMST                                    
047700                                      TO MA041-EFF-BEG-TMST               
047800             MOVE MA040-STAT-BEG-DTE  TO MA041-EFF-BEG-TMST(1:10)         
047900             MOVE SPACES TO MA041-EFF-END-TMST                            
048000             IF MA040-STAT-END-DTE NOT EQUAL TO SPACES                    
048200                MOVE PV-INITIAL-END-TMST                                  
048210                                      TO MA041-EFF-END-TMST               
048300                MOVE MA040-STAT-END-DTE                                   
048400                                      TO MA041-EFF-END-TMST(1:10)         
048500             END-IF                                                       
048600             MOVE MA040-STAT-CDE      TO MA041-SKU-STAT-CDE               
048700             WRITE RCP-DATA-LOAD-RECORD                                   
048800                   FROM MA041-RCP-LOAD                                    
048900             ADD +1 TO PV-RCP-DATA-LOAD-REC-COUNT                         
049000         ELSE                                                             
049100*          NO ORG UNIT CLUSTER ROW FOUND FOR THIS STORE                   
049200             ADD +1                 TO PV-STORE-NO-ORG-UNIT-COUNT         
049300         END-IF                                                           
049400     ELSE                                                                 
049500         ADD +1                 TO PV-EXTRACT-NO-ITEM-COUNT               
049600     END-IF.                                                              
049700                                                                          
049800     PERFORM 2050-READ-RCP-DATA-EXTRACT-REC.                              
049900                                                                          
050000                                                                          
050010*    THIS PARAGRAPH OBTAINS THE ITEM CLUSTER ID FOR A SPECIFIC SKU        
050020*    AND MOVES IT TO THE RCP DATA LOAD OUTPUT RECORD.                     
050100 2200-GET-ITEM-CLUSTER-INFO.                                              
050500                                                                          
050600     IF MA040-ITM-NAME = MA043-ITEM-CLUSTR-SKU                            
050700*      ITEM CLUSTER ROW FOUND FOR THIS SKU                                
050800         MOVE MA043-ITEM-CLUSTR-SKU  TO PV-HOLD-ITEM-NAME                 
050900         MOVE MA043-ITEM-CLUSTR-ID   TO MA041-ITEM-CLUSTR-ID              
051000     ELSE                                                                 
051100         IF MA040-ITM-NAME > MA043-ITEM-CLUSTR-SKU                        
051200             PERFORM 2275-READ-TITMCLT-RECORD                             
051300         ELSE                                                             
051400*      NO ITEM CLUSTER ROW FOUND FOR THIS SKU                             
051500             ADD +1 TO PV-SKU-NO-ITEM-COUNT                               
051600             MOVE MA040-ITM-NAME         TO INF-LM-ITEM-NAME              
051700             MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
258201**           DISPLAY LOG-MESSAGE UPON SYSOUT                              
258201             IF PV-SKU-NO-ITEM-COUNT < 5001                               
258201                 DISPLAY ITEM-NOT-FOUND-LOG-MSG                           
258201             END-IF                                                       
051900             SET PS-ITEM-CLUSTR-ID-NOT-FOUND TO TRUE                      
052000         END-IF                                                           
052100     END-IF.                                                              
052200                                                                          
052300                                                                          
052400*  THIS PARAGRAPH WILL READ THE EXTRACT FILE OF THE ORACLE TABLE          
052500*  TITMCLT.                                                               
052600 2275-READ-TITMCLT-RECORD.                                                
052700                                                                          
052800     READ TITMCLT-EXTRACT-FILE INTO MA043-ITEM-CLUSTR-RECORD              
052900         AT END      SET PS-ITEM-CLUSTR-ID-NOT-FOUND TO TRUE              
053000                     MOVE PV-ALL-NINES   TO MA043-ITEM-CLUSTR-SKU         
053100         NOT AT END  ADD +1 TO PV-TITMCLT-EXTRACT-REC-COUNT.              
053200                                                                          
053210                                                                          
053220*  THIS PARAGRAPH WRITE SUMMARY INFORMATION TO THE LOG FILE AND           
053230*  CLOSES THE INPUT AND OUTPUT FILES.                                     
053300 3000-WRAP-UP.                                                            
053800                                                                          
053900     CLOSE RCP-DATA-EXTRACT-FILE                                          
054000           RCP-DATA-LOAD-FILE                                             
054100           TITMCLT-EXTRACT-FILE                                           
054200           TORGCLT-EXTRACT-FILE.                                          
054300                                                                          
054400     MOVE PV-RCP-DATA-EXTRACT-REC-COUNT                                   
054500                                   TO RSLM1-RCP-DATA-EXTRACT-RECS.        
054600     MOVE RUN-SUMMARY-LOG-MSG1     TO LM-MESSAGE.                         
054700     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
054800                                                                          
054900     MOVE PV-RCP-DATA-LOAD-REC-COUNT                                      
055000                                   TO RSLM2-RCP-DATA-LOAD-RECS.           
055100     MOVE RUN-SUMMARY-LOG-MSG2     TO LM-MESSAGE.                         
055200     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
055300                                                                          
055400     MOVE PV-SKU-NO-ITEM-COUNT    TO RSLM3-SKU-NO-ITEM-RECS.              
055500     MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                          
055600     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
055700                                                                          
055800     MOVE PV-EXTRACT-NO-ITEM-COUNT                                        
055900                                  TO RSLM5-EXTRACT-NO-ITEM-RECS.          
056000     MOVE RUN-SUMMARY-LOG-MSG5    TO LM-MESSAGE.                          
056100     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
056200                                                                          
056300     MOVE PV-STORE-NO-ORG-UNIT-COUNT                                      
056400                                  TO RSLM6-STORE-NO-ORG-RECS.             
056500     MOVE RUN-SUMMARY-LOG-MSG6    TO LM-MESSAGE.                          
056600     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
056700                                                                          
056800     MOVE PV-TORGCLT-EXTRACT-REC-COUNT                                    
056900                                  TO RSLM7-TORGCLT-EXTRACT-RECS.          
057000     MOVE RUN-SUMMARY-LOG-MSG7    TO LM-MESSAGE.                          
057100     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
057200                                                                          
057300     MOVE PV-TITMCLT-EXTRACT-REC-COUNT                                    
057400                                  TO RSLM8-TITMCLT-EXTRACT-RECS.          
057500     MOVE RUN-SUMMARY-LOG-MSG8    TO LM-MESSAGE.                          
057600     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
057700                                                                          
057800                                                                          
057810*  THIS PARAGRAPH WRITES AN ERROR MESSAGE TO THE SYSTEM LOG AND           
057820*  ENDS THE PROGRAM WITH AN ERROR CODE IN THE RETURN-CODE VALUE.          
057900 9000-HANDLE-ERROR.                                                       
058300                                                                          
058400     MOVE ERROR-MESSAGE TO LOG-MESSAGE.                                   
058500     MOVE ERROR-CODE    TO PV-ABEND-CODE.                                 
058600                                                                          
058700     DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
058800                                                                          
058900     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
059000                                                                          
