       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT133.                                       
       AUTHOR.                  JON FIEDLER.                                    
       DATE-WRITTEN.            02/21/2001.                                     
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM CHECKS THAT THE TEXT MESSAGE NUMBER INCREASES              
      * SEQUENTIALLY WITH NO GAPS BETWEEN NUMBERS. IF GAPS OCCUR, THERE         
      * IS A POSSIBILITY THAT A DB2 INSERT ERROR HAS OCCURED. THIS              
      * PROGRAM SETS A RETURN CODE TO EMAIL EDISUPPORT IF GAPS ARE              
      * FOUND.                                                                  
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT LAST-MSG-TXT-NBR-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  LAST-MSG-TXT-NBR-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  LAST-MSG-TXT-NBR-RECORD          PIC  X(80).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  WS-TETTXHD-END-SW            PIC X       VALUE 'N'.              
               88  NOT-END-OF-TETTXHD                   VALUE 'N'.              
               88  END-OF-TETTXHD                       VALUE 'Y'.              
           05  WS-END-OF-MSG-DKEY-FILE-SW   PIC  X      VALUE 'N'.              
               88  END-OF-MSG-DKEY-FILE                 VALUE 'Y'.              
           05  WS-ERRORS-FOUND              PIC X       VALUE 'N'.              
               88  ERRORS-FOUND                         VALUE 'Y'.              
                                                                                
       01  PV-PARAGRAPH-NAME                PIC X(30).                          
       01  PV-DB2-OPER-ATTEMPTED            PIC X(30).                          
       01  PV-LAST-TXT-MSG-NBR-FILE.                                            
           05 PV-LAST-TXT-MSG-NBR           PIC 9(09).                          
       01  PV-LAST-TXT-MSG-NBR-CMP-FILE.                                        
           05 PV-LAST-TXT-MSG-NBR-CMP       PIC S9(09) COMP.                    
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
              INCLUDE TETTXHD                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    TXT_MSG_NBR CURSOR                                                   
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE TXT_MSG_NBR_CSR CURSOR FOR                                 
               SELECT TXT_MSG_DKEY_ID                                           
               FROM   TETTXHD T                                                 
               WHERE  TXT_MSG_DKEY_ID >= :PV-LAST-TXT-MSG-NBR-CMP               
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN I-O LAST-MSG-TXT-NBR-FILE.                                      
                                                                                
           PERFORM 2000-READ-CONTROL-CARD.                                      
                                                                                
           MOVE PV-LAST-TXT-MSG-NBR TO PV-LAST-TXT-MSG-NBR-CMP.                 
                                                                                
           PERFORM 3000-OPEN-TXT-MSG-NBR-NMCURSOR.                              
                                                                                
           PERFORM 3100-FETCH-TXT-MSG-NBR                                       
               UNTIL END-OF-TETTXHD.                                            
                                                                                
           PERFORM 3200-CLOSE-TXT-MSG-NBR-CURSOR.                               
                                                                                
           CLOSE LAST-MSG-TXT-NBR-FILE.                                         
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ***********************************************************               
      * CHECK THAT THE NEW TEXT MESSAGE NUMBERS ARE IN SEQUENCE.                
      ***********************************************************               
       1000-CHECK-SEQ-TXT-MSG-NBR.                                              
                                                                                
           MOVE PV-LAST-TXT-MSG-NBR TO PV-LAST-TXT-MSG-NBR-CMP.                 
                                                                                
           IF PV-LAST-TXT-MSG-NBR-CMP = ETTXHD-TXT-MSG-DKEY-ID                  
              CONTINUE                                                          
           ELSE                                                                 
              SET ERRORS-FOUND TO TRUE                                          
           END-IF.                                                              
                                                                                
           ADD 1 TO PV-LAST-TXT-MSG-NBR.                                        
                                                                                
       2000-READ-CONTROL-CARD.                                                  
                                                                                
           READ LAST-MSG-TXT-NBR-FILE                                           
               INTO PV-LAST-TXT-MSG-NBR-FILE                                    
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * UPDATE CONTROL CARD WITH THE LAST TEXT MESSAGE NUMBER ADDED.            
      ******************************************************************        
       2500-WRITE-UPDTE-TO-CNTRL-CRD.                                           
                                                                                
           MOVE ETTXHD-TXT-MSG-DKEY-ID TO PV-LAST-TXT-MSG-NBR.                  
                                                                                
           REWRITE LAST-MSG-TXT-NBR-RECORD                                      
               FROM PV-LAST-TXT-MSG-NBR-FILE.                                   
                                                                                
      *****************************************************************         
      *    OPEN TEXT MESSAGE NUMBER CURSOR                                      
      *****************************************************************         
       3000-OPEN-TXT-MSG-NBR-NMCURSOR.                                          
                                                                                
           MOVE '3000-'                TO PV-PARAGRAPH-NAME.                    
           MOVE 'OPEN TXT MSG NBR CSR' TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           EXEC SQL                                                             
              OPEN TXT_MSG_NBR_CSR                                              
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET END-OF-TETTXHD TO TRUE                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    FETCH TEXT MESSAGE NUMBER                                            
      *****************************************************************         
       3100-FETCH-TXT-MSG-NBR.                                                  
                                                                                
           MOVE '3100-'                     TO PV-PARAGRAPH-NAME.               
           MOVE 'FETCH TEXT MESSAGE NUMBER' TO PV-DB2-OPER-ATTEMPTED.           
                                                                                
           EXEC SQL                                                             
             FETCH TXT_MSG_NBR_CSR                                              
             INTO  :ETTXHD-TXT-MSG-DKEY-ID                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   PERFORM 1000-CHECK-SEQ-TXT-MSG-NBR                           
               WHEN SQLCODE = +100                                              
                   SET END-OF-TETTXHD TO TRUE                                   
                   PERFORM 2500-WRITE-UPDTE-TO-CNTRL-CRD                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    CLOSE TEXT MESSAGE NUMBER CURSOR                                     
      *****************************************************************         
       3200-CLOSE-TXT-MSG-NBR-CURSOR.                                           
                                                                                
           MOVE '3200-'            TO PV-PARAGRAPH-NAME.                        
           MOVE 'CLOSE TXT MSG NBR CSR' TO PV-DB2-OPER-ATTEMPTED.               
                                                                                
           EXEC SQL                                                             
              CLOSE TXT_MSG_NBR_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 4000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4000-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
