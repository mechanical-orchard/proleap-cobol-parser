00001  IDENTIFICATION DIVISION.                                         05/07/91
00002 *                                                                 MPKCS018
00003  PROGRAM-ID.             MPKCS018.                                   LV002
00004  AUTHOR.                 WILLIAM B. BURT.                         MPKCS018
00005  INSTALLATION.           KOHLS.                                   MPKCS018
00006  DATE-WRITTEN            MARCH, 1990.                             MPKCS018
00007  DATE-COMPILED.                                                   MPKCS018
00008 *                                                                 MPKCS018
00009 ******************************************************************MPKCS018
00010 * THIS PROGRAM DISPLAYS THE MERCHANDISE IDENTIFICATION MENU AND   MPKCS018
00011 * AND PASSES CONTROL TO THE PROGRAM WHICH PROCESSES THE FUNCTION  MPKCS018
00012 * SELECTED.                                                       MPKCS018
00013 ******************************************************************MPKCS018
00014 *                                                                 MPKCS018
00015                                                                   MPKCS018
00016 ******************************************************************MPKCS018
00017 ***             MAINTENANCE LOG                                  *MPKCS018
00018 ******************************************************************MPKCS018
00019 **    DATE   | INIT |       DESCRIPTION / COMMENTS               |MPKCS018
00020 ******************************************************************MPKCS018
00021 **           |      |                                            |MPKCS018
00022 ** 05/06/91  | GCJ  | CHANGED PGM TO RETURN TO CORPORATE MENU    |MPKCS018
00023 **           |      | WHEN F3 IS PRESSED (WR# 1401).             |MPKCS018
00024 **           |      |                                            |MPKCS018
00025 **-----------|------|--------------------------------------------|MPKCS018
00026 **           |      |                                            |MPKCS018
00027 **           |      |                                            |MPKCS018
00028 ******************************************************************MPKCS018
00029                                                                   MPKCS018
00030  EJECT                                                            MPKCS018
00031  ENVIRONMENT DIVISION.                                            MPKCS018
00032 *                                                                 MPKCS018
00033  DATA DIVISION.                                                   MPKCS018
00034 *                                                                 MPKCS018
00035  WORKING-STORAGE SECTION.                                         MPKCS018
00036 *                                                                 MPKCS018
00037  01  PC-PROGRAM-CONSTANTS.                                        MPKCS018
00038      05  PC-QUESTION-MARK.                                        MPKCS018
00039          10  FILLER                      PIC  X(01)  VALUE '?'.   MPKCS018
00040          10  FILLER                      PIC  X(01)  VALUE        MPKCS018
00041                                                      LOW-VALUE.   MPKCS018
00042      05  PC-MPKCS019-COMMAREA-LENGTH     PIC S9(04)  VALUE +1     MPKCS018
00043                                                      COMP.        MPKCS018
00044      05  PC-MP-MENU-TRANID               PIC  X(04)  VALUE 'M018'.MPKCS018
00045      05  PC-UPC-TICKET-VERIFICATION      PIC  9(02)  VALUE 01.    MPKCS018
00046                                                                   MPKCS018
00047  01  PV-PROGRAM-VARIABLES.                                        MPKCS018
00048      05  PV-CODE-EDIT                    PIC  X(02)  VALUE SPACE. MPKCS018
00049          88  PV-VALID-CODE                           VALUE  '01'. MPKCS018
00050      05  PV-MESSAGE-AREA                 PIC  X(80)  VALUE SPACE. MPKCS018
00051      05  PV-COMMAREA                     PIC  X(01)  VALUE SPACE. MPKCS018
00052      05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS  MPKCS018
00053                                                      COMP SYNC.   MPKCS018
00054                                                                   MPKCS018
00055  01  ST-SYSTEM-TIME-AREA.                                         MPKCS018
00056      05  ST-ABSTIME                      PIC S9(16)  VALUE +0     MPKCS018
00057                                                      COMP SYNC.   MPKCS018
00058      05  ST-TIME-EDIT.                                            MPKCS018
00059          10  ST-TIME-HHMM                PIC  X(05)  VALUE SPACE. MPKCS018
00060          10  FILLER                      PIC  X(03)  VALUE SPACE. MPKCS018
00061                                                                   MPKCS018
00062 *                                                                 MPKCS018
00063 *  THE SYMBOLIC DESCRIPTION MAP                                   MPKCS018
00064 *                                                                 MPKCS018
00065      COPY MPKM018.                                                MPKCS018
00066                                                                   MPKCS018
00067 *                                                                 MPKCS018
00068 *  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST   MPKCS018
00069 *                                                                 MPKCS018
00070      COPY DPWS015.                                                MPKCS018
00071                                                                   MPKCS018
00072 *                                                                 MPKCS018
00073 *  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                      MPKCS018
00074 *                                                                 MPKCS018
00075      COPY DFHAID.                                                 MPKCS018
00076                                                                   MPKCS018
00077 ******************************************************************MPKCS018
00078  LINKAGE SECTION.                                                 MPKCS018
00079 ******************************************************************MPKCS018
00080  01  DFHCOMMAREA                   PIC  X(01).                    MPKCS018
00081                                                                   MPKCS018
00082 ******************************************************************MPKCS018
00083  PROCEDURE DIVISION.                                              MPKCS018
00084 ******************************************************************MPKCS018
00085  0000-PROGRAM-MAINLINE.                                           MPKCS018
00086      MOVE LOW-VALUES TO MP018AI.                                  MPKCS018
00087                                                                   MPKCS018
00088      EVALUATE TRUE                                                MPKCS018
00089          WHEN EIBCALEN = ZERO                                     MPKCS018
00090          WHEN EIBAID = DFHCLEAR                                   MPKCS018
00091              PERFORM 2100-UPDATE-SCREEN-HEADER                    MPKCS018
00092              PERFORM 8000-SEND-MAP                                MPKCS018
00093              PERFORM 8200-RETURN-TRANID                           MPKCS018
00094          WHEN EIBAID = DFHENTER                                   MPKCS018
00095              PERFORM 1000-RECEIVE-MAP                             MPKCS018
00096              PERFORM 1100-EDIT-SELECTION                          MPKCS018
00097          WHEN EIBAID = DFHPF3                                     MPKCS018
00098              PERFORM 8300-RETURN                                  MPKCS018
00099          WHEN OTHER                                               MPKCS018
00100              MOVE 'UNRECOGNIZED PF KEY' TO AERRMSGO               MPKCS018
00101              PERFORM 2100-UPDATE-SCREEN-HEADER                    MPKCS018
00102              PERFORM 8100-SEND-DATA-ONLY                          MPKCS018
00103              PERFORM 8200-RETURN-TRANID                           MPKCS018
00104      END-EVALUATE.                                                MPKCS018
00105                                                                   MPKCS018
00106      GOBACK.                                                      MPKCS018
00107 *                                                                 MPKCS018
00108  1000-RECEIVE-MAP.                                                MPKCS018
00109      EXEC CICS                                                    MPKCS018
00110          RECEIVE MAP    ('MP018A')                                MPKCS018
00111                  MAPSET ('MPKM018')                               MPKCS018
00112                  RESP   (PV-CICS-RESPONSE)                        MPKCS018
00113      END-EXEC.                                                    MPKCS018
00114                                                                   MPKCS018
00115      IF PV-CICS-RESPONSE = DFHRESP(MAPFAIL)                       MPKCS018
00116          PERFORM 2000-MAPFAIL                                     MPKCS018
00117      END-IF.                                                      MPKCS018
00118 *                                                                 MPKCS018
00119  1100-EDIT-SELECTION.                                             MPKCS018
00120      IF  AFUNCL > ZERO                                            MPKCS018
00121          MOVE AFUNCI                    TO PV-CODE-EDIT           MPKCS018
00122          IF  PV-VALID-CODE                                        MPKCS018
00123              PERFORM 2200-TRANSFER-CONTROL                        MPKCS018
00124          ELSE                                                     MPKCS018
00125              MOVE 'INVALID FUNCTION -- PLEASE REENTER'            MPKCS018
00126                                         TO AERRMSGO               MPKCS018
00127              MOVE DP015-UNP-NUM-BRT-MDT TO AFUNCA                 MPKCS018
00128              MOVE DP015-RED             TO AFUNCC                 MPKCS018
00129              MOVE DP015-REVERSE         TO AFUNCH                 MPKCS018
00130              MOVE -1                    TO AFUNCL                 MPKCS018
00131              PERFORM 8100-SEND-DATA-ONLY                          MPKCS018
00132              PERFORM 8200-RETURN-TRANID                           MPKCS018
00133          END-IF                                                   MPKCS018
00134      ELSE                                                         MPKCS018
00135          MOVE 'ENTER A VALID FUNCTION'  TO AERRMSGO               MPKCS018
00136          MOVE PC-QUESTION-MARK             TO AFUNCO              MPKCS018
00137          MOVE DP015-UNP-NUM-BRT-OFF     TO AFUNCA                 MPKCS018
00138          MOVE DP015-RED                 TO AFUNCC                 MPKCS018
00139          MOVE DP015-REVERSE             TO AFUNCH                 MPKCS018
00140          MOVE -1                        TO AFUNCL                 MPKCS018
00141          PERFORM 8100-SEND-DATA-ONLY                              MPKCS018
00142          PERFORM 8200-RETURN-TRANID                               MPKCS018
00143      END-IF.                                                      MPKCS018
00144 *                                                                 MPKCS018
00145  2000-MAPFAIL.                                                    MPKCS018
00146      MOVE 'ENTER A VALID FUNCTION'  TO AERRMSGO.                  MPKCS018
00147      MOVE PC-QUESTION-MARK          TO AFUNCO.                    MPKCS018
00148      MOVE DP015-UNP-NUM-BRT-OFF     TO AFUNCA.                    MPKCS018
00149      MOVE DP015-RED                 TO AFUNCC.                    MPKCS018
00150      MOVE DP015-REVERSE             TO AFUNCH.                    MPKCS018
00151      MOVE -1                        TO AFUNCL.                    MPKCS018
00152      PERFORM 2100-UPDATE-SCREEN-HEADER.                           MPKCS018
00153      PERFORM 8100-SEND-DATA-ONLY.                                 MPKCS018
00154      PERFORM 8200-RETURN-TRANID.                                  MPKCS018
00155 *                                                                 MPKCS018
00156  2100-UPDATE-SCREEN-HEADER.                                       MPKCS018
00157      EXEC CICS                                                    MPKCS018
00158          ASKTIME                                                  MPKCS018
00159          ABSTIME (ST-ABSTIME)                                     MPKCS018
00160      END-EXEC.                                                    MPKCS018
00161                                                                   MPKCS018
00162      EXEC CICS                                                    MPKCS018
00163          FORMATTIME                                               MPKCS018
00164          ABSTIME (ST-ABSTIME)                                     MPKCS018
00165          MMDDYY  (ADATEO)                                         MPKCS018
00166          DATESEP                                                  MPKCS018
00167          TIME    (ST-TIME-EDIT)                                   MPKCS018
00168          TIMESEP                                                  MPKCS018
00169      END-EXEC.                                                    MPKCS018
00170                                                                   MPKCS018
00171      MOVE ST-TIME-HHMM  TO ATIMEO.                                MPKCS018
00172      MOVE EIBTRMID      TO ATERMO.                                MPKCS018
00173 *                                                                 MPKCS018
00174  2200-TRANSFER-CONTROL.                                           MPKCS018
00175      EVALUATE TRUE                                                MPKCS018
00176          WHEN AFUNCI = PC-UPC-TICKET-VERIFICATION                 MPKCS018
00177              MOVE 'Y' TO PV-COMMAREA                              MPKCS018
00178              EXEC CICS                                            MPKCS018
00179                  XCTL  PROGRAM   ('MPKCS019')                     MPKCS018
00180                        COMMAREA  (PV-COMMAREA)                    MPKCS018
00181                        LENGTH    (PC-MPKCS019-COMMAREA-LENGTH)    MPKCS018
00182                        RESP      (PV-CICS-RESPONSE)               MPKCS018
00183              END-EXEC                                             MPKCS018
00184      END-EVALUATE.                                                MPKCS018
00185                                                                   MPKCS018
00186      IF PV-CICS-RESPONSE = DFHRESP(PGMIDERR)                      MPKCS018
00187          PERFORM 3000-PROGRAM-ID-ERROR                            MPKCS018
00188      END-IF.                                                      MPKCS018
00189 *                                                                 MPKCS018
00190  3000-PROGRAM-ID-ERROR.                                           MPKCS018
00191      MOVE 'PROGRAM ID ERROR' TO AERRMSGO.                         MPKCS018
00192      PERFORM 8100-SEND-DATA-ONLY.                                 MPKCS018
00193      PERFORM 8200-RETURN-TRANID.                                  MPKCS018
00194 *                                                                 MPKCS018
00195  8000-SEND-MAP.                                                   MPKCS018
00196      EXEC CICS                                                    MPKCS018
00197          SEND MAP    ('MP018A')                                   MPKCS018
00198               MAPSET ('MPKM018')                                  MPKCS018
00199               ERASE                                               MPKCS018
00200               FORMFEED                                            MPKCS018
00201      END-EXEC.                                                    MPKCS018
00202 *                                                                 MPKCS018
00203  8100-SEND-DATA-ONLY.                                             MPKCS018
00204      MOVE -1            TO AFUNCL.                                MPKCS018
00205      EXEC CICS                                                    MPKCS018
00206          SEND MAP       ('MP018A')                                MPKCS018
00207               MAPSET    ('MPKM018')                               MPKCS018
00208               CURSOR                                              MPKCS018
00209               DATAONLY                                            MPKCS018
00210               FORMFEED                                            MPKCS018
00211      END-EXEC.                                                    MPKCS018
00212 *                                                                 MPKCS018
00213  8200-RETURN-TRANID.                                              MPKCS018
00214      EXEC CICS                                                    MPKCS018
00215          RETURN TRANSID  (PC-MP-MENU-TRANID)                      MPKCS018
00216                 COMMAREA (PV-COMMAREA)                            MPKCS018
00217                 LENGTH   (1)                                      MPKCS018
00218      END-EXEC.                                                    MPKCS018
00219 *                                                                 MPKCS018
00220  8300-RETURN.                                                     MPKCS018
00221      MOVE 'M018 - MERCHANDISE IDENTIFICATION MENU TERMINATED'        CL**2
00222                    TO PV-MESSAGE-AREA.                               CL**2
00223      EXEC CICS                                                    MPKCS018
00224          SEND FROM (PV-MESSAGE-AREA)                              MPKCS018
00225               LENGTH (80)                                         MPKCS018
00226               ERASE                                               MPKCS018
00227      END-EXEC.                                                    MPKCS018
00228                                                                   MPKCS018
00229      EXEC CICS                                                    MPKCS018
00230          START                                                    MPKCS018
00231               TRANSID ('MENU')                                    MPKCS018
00232               TERMID  (EIBTRMID)                                  MPKCS018
00233      END-EXEC.                                                    MPKCS018
00234                                                                   MPKCS018
00235      EXEC CICS                                                    MPKCS018
00236          RETURN                                                   MPKCS018
00237      END-EXEC.                                                    MPKCS018
