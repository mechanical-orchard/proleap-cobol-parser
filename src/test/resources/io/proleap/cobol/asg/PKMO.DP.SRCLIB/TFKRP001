       IDENTIFICATION DIVISION.                                         00010045
                                                                        00020045
       PROGRAM-ID.      TFKRP001.                                       00030099
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00040046
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050045
       DATE-WRITTEN.    08/02/2005.                                     00060099
       DATE-COMPILED.                                                   00070045
                                                                        00080045
      ******************************************************************00090045
      * INVENTORY SNAPSHOT REPORT                                       00100099
      ******************************************************************00110045
      **DATE:08/02/2005 MADE BY: RAY GRAF   INITIAL PROGRAM WRITE       00120099
      ******************************************************************00130046
      ******************************************************************00131099
SR3403**DATE:03/10/2012 MADE BY: ANUJ GUPTA LARGE RETAIL EXPANSION      00132099
      ******************************************************************00133099
                                                                        00140045
       ENVIRONMENT DIVISION.                                            00150045
       CONFIGURATION SECTION.                                           00160045
       SOURCE-COMPUTER.  IBM-3090.                                      00170045
       OBJECT-COMPUTER.  IBM-3090.                                      00180045
       INPUT-OUTPUT SECTION.                                            00190045
       FILE-CONTROL.                                                    00200045
                                                                        00210045
           SELECT INPUT-FILE    ASSIGN TO INP01.                        00220052
           SELECT REPORT-FILE   ASSIGN TO OUT01.                        00240047
                                                                        00250045
       DATA DIVISION.                                                   00260045
       FILE SECTION.                                                    00270045
                                                                        00280045
       FD  INPUT-FILE                                                   00290052
           RECORDING MODE IS F                                          00300099
           LABEL RECORDS OMITTED                                        00310046
           BLOCK CONTAINS 0 RECORDS                                     00320046
           DATA RECORD IS INPUT-RECORD.                                 00330052
SR3403 01  INPUT-RECORD                        PIC X(079).              00341099
                                                                        00350046
       FD  REPORT-FILE                                                  00430046
           RECORDING MODE IS F                                          00440099
           LABEL RECORDS OMITTED                                        00450046
           RECORD CONTAINS 132 CHARACTERS                               00461099
           DATA RECORD IS REPORT-RECORD.                                00470046
       01  REPORT-RECORD                       PIC X(132).              00480099
                                                                        00490045
       WORKING-STORAGE SECTION.                                         00500047
                                                                        00501099
      ******************************************************************00510047
      **PROGRAM SWITCHES                                                00520047
      ******************************************************************00530047
       01  PS-PROGRAM-SWITCHES.                                         00540052
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00550052
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00560052
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00570052
                                                                        00650045
      ******************************************************************00660047
      **PROGRAM CONSTANTS                                               00670047
      ******************************************************************00680047
       01  PC-PROGRAM-CONSTANTS.                                        00690045
           05  PC-PROGRAM-NAME             PIC X(08) VALUE 'TFKRP001'.  00700099
           05  PC-MAX-LINES                PIC 9(02) VALUE 60.          00710099
           05  PC-INPUT-RECORDS-READ       PIC 9(09) VALUE ZEROS.       00720064
           05  PC-REPORT-RECORDS-WRITTEN   PIC 9(09) VALUE ZEROS.       00740064
           05  PC-REPORT-LINE-COUNTER      PIC 9(03) VALUE ZEROS.       00760064
           05  PC-PAGE-COUNTER             PIC 9(03) VALUE ZEROS.       00770064
           05  PC-TRANSFER-TOTAL-DESC      PIC X(15) VALUE              00780099
                                                     'TRANSFER TOTAL '. 00781099
           05  PC-STORE-TOTAL-DESC         PIC X(15) VALUE              00781199
                                                     '   STORE TOTAL '. 00781299
                                                                        00782099
           05  PC-REPORT-TOTAL-DESC        PIC X(15) VALUE              00782199
                                                     '  REPORT TOTAL '. 00782299
                                                                        00783099
      ******************************************************************00850047
      **PROGRAM VARIABLES                                               00860047
      ******************************************************************00870047
       01  PV-PROGRAM-VARIABLES.                                        00880045
           05  PV-COUNTER                  PIC  9(02) VALUE ZEROES.     00911099
           05  PV-READ-COUNT               PIC S9(09) VALUE +0          00911299
                                                      COMP-3.           00911399
                                                                        00912099
           05  PV-FORMAT-DATE.                                          00913099
               10  FILLER                  PIC  9(02)  VALUE 20.        00914099
               10  PV-FORMAT-YY            PIC  9(02)  VALUE ZEROS.     00916099
               10  FILLER                  PIC  X(01)  VALUE '-'.       00917099
               10  PV-FORMAT-MM            PIC  9(02)  VALUE ZEROS.     00918099
               10  FILLER                  PIC  X(01)  VALUE '-'.       00919099
               10  PV-FORMAT-DD            PIC  9(02)  VALUE ZEROS.     00919199
           05  PV-DB2-NULL-IND             PIC S9(04)  VALUE ZEROS      00919299
                                                       COMP SYNC.       00919399
                                                                        00920051
      ******************************************************************00930051
      **PROGRAM HOLDS                                                   00940051
      ******************************************************************00950051
       01  PH-PROGRAM-HOLDS.                                            00960099
           05  PH-TO-PSTG-LOC-NBR           PIC S9(04)    COMP          00970099
                                                          VALUE ZEROS.  00971099
           05  PH-FR-LOC-NBR                PIC S9(04)    COMP          00980099
                                                          VALUE ZEROS.  00981099
           05  PH-XFER-SEQ-NBR              PIC S9(09)    COMP          00990099
                                                          VALUE ZEROS.  01150099
           05  PH-EXTENDED-RETAIL           PIC 9(09)V99  VALUE ZEROS.  01150199
           05  PH-TRANSFER-QTY-TOTAL        PIC S9(06)    COMP          01150299
                                                          VALUE ZEROS.  01150399
           05  PH-STORE-QTY-TOTAL           PIC S9(06)    COMP          01151099
                                                          VALUE ZEROS.  01152099
           05  PH-REPORT-QTY-TOTAL          PIC S9(06)    COMP          01153099
                                                          VALUE ZEROS.  01154099
           05  PH-TRANSFER-EXT-RETAIL-TOTAL PIC 9(09)V99  VALUE ZEROS.  01170099
           05  PH-STORE-EXT-RETAIL-TOTAL    PIC 9(09)V99  VALUE ZEROS.  01190099
           05  PH-REPORT-EXT-RETAIL-TOTAL   PIC 9(09)V99  VALUE ZEROS.  01201099
                                                                        01202099
      ******************************************************************01210047
      **DATE/TIME                                                       01220047
      ******************************************************************01230047
       01  WS-DATE-TIME.                                                01240048
           05  WS-DATE.                                                 01250048
               10 WS-YEAR                  PIC 9(2) VALUE ZEROES.       01260048
               10 WS-MONTH                 PIC 9(2) VALUE ZEROES.       01270048
               10 WS-DAY                   PIC 9(2) VALUE ZEROES.       01280048
           05  WS-TIME.                                                 01290048
               10 WS-HOUR                  PIC 9(2) VALUE ZEROES.       01300048
               10 WS-MINUTE                PIC 9(2) VALUE ZEROES.       01310048
               10 WS-SEC                   PIC 9(2) VALUE ZEROES.       01320048
               10 WS-HSEC                  PIC 9(2) VALUE ZEROES.       01330048
                                                                        01340045
      ******************************************************************01420045
      **INPUT COPYBOOK                                                  01430099
      ******************************************************************01440045
           COPY TFRD060.                                                01450099
      ******************************************************************01540059
      **VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         01550059
      ******************************************************************01560059
           COPY DPWS004.                                                01570059
      ******************************************************************01590099
      * PARAMETER LIST FOR THE CALENDAR ROUTINE                         01600099
      ******************************************************************01610099
           COPY DPWS500.                                                01611099
                                                                        01616099
      ******************************************************************01740045
      ** LAYOUT FOR INVENTORY SNAPSHOT REPORT                           01750099
      ******************************************************************01760045
                                                                        01760199
       01  HL-HEADER-LINE0.                                             01760999
           05  FILLER                            PIC X(02) VALUE SPACE. 01761199
           05  HL0-RUN-DATE.                                            01761299
               10  HL0-RUN-MONTH                 PIC 9(02) VALUE ZERO.  01761399
               10  FILLER                        PIC X(01) VALUE '/'.   01761499
               10  HL0-RUN-DAY                   PIC 9(02) VALUE ZERO.  01761599
               10  FILLER                        PIC X(01) VALUE '/'.   01761699
               10  HL0-RUN-YEAR                  PIC 9(02) VALUE ZERO.  01761799
           05  FILLER                            PIC X(02) VALUE SPACE. 01761899
           05  HL0-RUN-TIME.                                            01761999
               10  HL0-RUN-HOUR                  PIC 9(02) VALUE ZERO.  01762099
               10  FILLER                        PIC X(01) VALUE ':'.   01762199
               10  HL0-RUN-MINUTE                PIC 9(02) VALUE ZERO.  01762299
           05  FILLER                            PIC X(27) VALUE SPACE. 01762399
           05  FILLER                            PIC X(45)              01762499
               VALUE 'K O H L S   D E P A R T M E N T   S T O R E S'.   01762599
           05  FILLER                            PIC X(21) VALUE SPACE. 01762699
           05  HL0-PROGRAM-NAME                  PIC X(08) VALUE        01762799
                                                           'TFKRP001'.  01762899
           05  FILLER                            PIC X(01) VALUE '-'.   01762999
           05  HL0-REPORT-NUMBER                 PIC 9(01) VALUE ZERO.  01763099
           05  HL0-REPORT-NUMBER-CHAR REDEFINES                         01763199
               HL0-REPORT-NUMBER                 PIC X(01).             01763299
           05  FILLER                            PIC X(07) VALUE        01763499
                                                           '  PAGE '.   01763599
           05  HL0-PAGE-NUMBER                   PIC ZZZZ9 VALUE ZERO.  01763699
                                                                        01764099
       01  HL-HEADER-LINE1.                                             01770099
           05  FILLER                      PIC  X(45) VALUE SPACES.     01780099
           05  FILLER                      PIC  X(38) VALUE             01790099
                         'TRANSFERS INVENTORY SNAPSHOT REPORT - '.      01800099
           05  HL1-INV-REPORT-DATE         PIC  X(10) VALUE SPACES.     01810099
                                                                        01820045
       01  HL-HEADER-LINE2.                                             01821099
           05  FILLER                      PIC  X(57) VALUE SPACES.     01822099
           05  FILLER                      PIC  X(18) VALUE             01823099
                         'INVENTORY SNAPSHOT'.                          01824099
                                                                        01826099
       01  HL-HEADER-LINE3.                                             01830099
           05  FILLER                      PIC  X(02) VALUE SPACES.     01840099
           05  FILLER                      PIC  X(14) VALUE             01850099
                         'TO STORE NBR: '.                              01860099
           05  HL3-TO-LOC-NBR              PIC  9(04) VALUE ZEROES.     01870099
           05  FILLER                      PIC  X(01) VALUE SPACES.     01880045
           05  HL3-TO-LOC-NM               PIC  X(20) VALUE SPACES.     01890099
           05  FILLER                      PIC  X(88) VALUE SPACES.     01900099
                                                                        01910045
       01  HL-HEADER-LINE4.                                             01911099
           05  FILLER                      PIC  X(16) VALUE             01912099
                         'INVENTORY DATE: '.                            01913099
           05  HL4-INVENTORY-DATE          PIC  X(10) VALUE SPACES.     01913199
                                                                        01914099
       01  HL-HEADER-LINE5.                                             01920099
           05  FILLER                      PIC  X(57) VALUE SPACES.     01930099
           05  FILLER                      PIC  X(08) VALUE 'EXTENDED'. 01950099
                                                                        01951099
       01  HL-HEADER-LINE6.                                             01960099
           05  FILLER                      PIC  X(43)                   01961099
               VALUE 'TRANSFER NBR DEPT SUBCLASS SKU        QTY  '.     01962099
           05  FILLER                      PIC  X(21)                   01963099
               VALUE 'RETAIL         RETAIL'.                           01964099
                                                                        01970047
       01  HL-DOTTED-HEADER-LINE.                                       02210047
           05  FILLER                      PIC  X(43)                   02220099
               VALUE '------------ ---- -------- ---------- ---- '.     02230099
           05  FILLER                      PIC  X(26)                   02240099
               VALUE '---------- ---------------'.                      02250099
                                                                        02310099
       01  DL-DETAIL-LINE1.                                             02390047
           05  DL1-TRANSFER-FROM-LOC       PIC  9(04)  VALUE ZEROES.    02410099
           05  FILLER                      PIC  X(01)  VALUE '-'.       02411099
           05  DL1-TRANSFER-SEQ-NBR        PIC  9(06)  VALUE ZEROES.    02420099
           05  FILLER                      PIC  X(02)  VALUE SPACES.    02421099
           05  DL1-DEPT-NBR                PIC  9(04)  VALUE ZEROES.    02430099
           05  FILLER                      PIC  X(03)  VALUE SPACES.    02440099
           05  DL1-SUBCLASS                PIC  9(04)  VALUE ZEROES.    02450099
           05  FILLER                      PIC  X(03)  VALUE SPACES.    02460099
           05  DL1-SKU                     PIC  9(08)  VALUE ZEROES.    02470099
           05  FILLER                      PIC  X(04)  VALUE SPACES.    02480099
           05  DL1-QTY                     PIC  ZZ9    VALUE ZEROES.    02490099
           05  FILLER                      PIC  X(02)  VALUE SPACES.    02500047
           05  DL1-RETAIL                  PIC  ZZ,ZZ9.99               02510099
                                                       VALUE ZEROES.    02511099
           05  FILLER                      PIC  X(02)  VALUE SPACES.    02520099
           05  DL1-EXTENDED-RETAIL         PIC  ZZZ,ZZZ,ZZ9.99          02530099
                                                       VALUE ZEROES.    02540099
                                                                        02550048
       01  TL-TOTAL-LINE.                                               02560099
           05  FILLER                      PIC  X(20)   VALUE SPACES.   02570099
           05  TL-DESC                     PIC  X(15)   VALUE SPACES.   02590099
           05  TL-QTY                      PIC  ZZZ,ZZ9 VALUE ZEROES.   02591099
           05  FILLER                      PIC  X(13)   VALUE SPACES.   02591199
           05  TL-EXTENDED-RETAIL          PIC  ZZZ,ZZZ,ZZ9.99          02591299
                                                        VALUE ZEROES.   02592099
                                                                        02593099
       01  BL-BLANK-LINE.                                               02940099
           05  FILLER                      PIC  X(132) VALUE SPACES.    02950099
                                                                        02950199
       01  ND-NO-DATA-LINE.                                             02951099
           05  FILLER                      PIC  X(050) VALUE SPACES.    02952099
           05  FILLER                      PIC  X(082)                  02953099
               VALUE '****  NO DATA TO PROCESS FOR THIS REPORT  ***'.   02954099
                                                                        02960099
      ****************************************************************  03390099
      **ABEND INFORMATION                                               03400099
      ****************************************************************  03410099
       01  AC-ABEND-FIELDS.                                             03420099
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    03430099
                                                          COMP SYNC.    03440099
               88  AC-TABLE-ERROR                         VALUE +4004.  03450099
               88  AC-DB2-ERROR                           VALUE +4013.  03460099
                                                                        03461099
       01  PV-MISC-ABEND-FIELDS.                                        03470099
           05  PV-PARAGRAPH-NAME               PIC X(30)  VALUE SPACE.  03480099
           05  PV-OPERATION-MSG-1              PIC X(40)  VALUE SPACE.  03481099
           05  PV-OPERATION-MSG-2              PIC X(40)  VALUE SPACE.  03482099
           05  PV-DB2-OPERATION                PIC X(30)  VALUE SPACE.  03483099
                                                                        03484099
       PROCEDURE DIVISION.                                              03490045
      ******************************************************************03500045
      **MAINLINE                                                        03510047
      ******************************************************************03520045
       0000-MAINLINE-PROCESSING.                                        03530047
                                                                        03540049
           PERFORM 1000-INITIALIZATION.                                 03550049
           PERFORM 2000-PROCESS-RECORDS                                 03560074
                   UNTIL PS-END-OF-INPUT-FILE                           03570099
           PERFORM 9999-WRAPUP.                                         03580049
           GOBACK.                                                      03590049
                                                                        03600049
       EJECT.                                                           03610049
                                                                        03620050
      ******************************************************************03630049
      **INITIALIZATION AREA                                             03640049
      ******************************************************************03650049
       1000-INITIALIZATION.                                             03660049
                                                                        03670049
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             03680049
                                                                        03690049
           OPEN INPUT INPUT-FILE                                        03700052
               OUTPUT REPORT-FILE.                                      03720050
                                                                        03730050
      **RETRIEVE DATE FROM SYSTEM                                       03740049
           ACCEPT WS-DATE FROM DATE.                                    03750050
           ACCEPT WS-TIME FROM TIME.                                    03760050
                                                                        03770049
           PERFORM 8000-READ-INPUT-FILE.                                03780053
                                                                        03810049
           IF  PS-NOT-END-OF-INPUT-FILE                                 03820049
               PERFORM 1100-HEADING-PREP                                03830049
               PERFORM 2100-WRITE-REPORT-HEADINGS                       03890077
               MOVE TFRD060-TO-PSTG-LOC-NBR TO PH-TO-PSTG-LOC-NBR       03891099
               MOVE TFRD060-FR-LOC-NBR      TO PH-FR-LOC-NBR            03892099
               MOVE TFRD060-XFER-SEQ-NBR    TO PH-XFER-SEQ-NBR          03900099
           END-IF.                                                      03920049
                                                                        03930049
       EJECT.                                                           03940049
                                                                        03950050
      ******************************************************************03960049
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       03970049
      ******************************************************************03980049
       1100-HEADING-PREP.                                               03990049
                                                                        04000049
           MOVE '1100-HEADING-PREP'       TO PV-PARAGRAPH-NAME.         04010099
                                                                        04020049
      ***PREPS KOHLS STANDARD HEADING                                   04030049
           MOVE WS-MONTH                  TO HL0-RUN-MONTH.             04070099
           MOVE WS-DAY                    TO HL0-RUN-DAY.               04080099
           MOVE WS-YEAR                   TO HL0-RUN-YEAR.              04090099
                                                                        04100050
           MOVE WS-HOUR                   TO HL0-RUN-HOUR.              04110099
           MOVE WS-MINUTE                 TO HL0-RUN-MINUTE.            04120099
                                                                        04121099
       EJECT.                                                           04123799
                                                                        04123899
      ******************************************************************04170049
      **PROCESS INPUT FILE                                              04180077
      ******************************************************************04190049
       2000-PROCESS-RECORDS.                                            04200049
                                                                        04210049
           MOVE '2000-PROCESS-RECORDS'  TO PV-PARAGRAPH-NAME.           04220049
                                                                        04230049
           EVALUATE TRUE                                                04240049
              WHEN TFRD060-TO-PSTG-LOC-NBR = PH-TO-PSTG-LOC-NBR         04250099
                   IF  (TFRD060-FR-LOC-NBR   NOT = PH-FR-LOC-NBR)       04290099
                   OR  (TFRD060-XFER-SEQ-NBR NOT = PH-XFER-SEQ-NBR)     04291099
                       PERFORM 2300-WRITE-TRANSFER-NBR-TOTAL            04310099
                       MOVE TFRD060-FR-LOC-NBR   TO PH-FR-LOC-NBR       04320099
                       MOVE TFRD060-XFER-SEQ-NBR TO PH-XFER-SEQ-NBR     04330099
                   END-IF                                               04350049
                                                                        04350199
                   IF  PC-REPORT-LINE-COUNTER > 60                      04351099
                       PERFORM 2100-WRITE-REPORT-HEADINGS               04353099
                   END-IF                                               04354099
                                                                        04355099
                   PERFORM 2200-WRITE-REPORT-DETAIL                     04360077
              WHEN TFRD060-TO-PSTG-LOC-NBR NOT = PH-TO-PSTG-LOC-NBR     04380099
                   PERFORM 2300-WRITE-TRANSFER-NBR-TOTAL                04390099
                   PERFORM 2400-WRITE-STORE-NBR-TOTAL                   04400099
                   INITIALIZE PC-PAGE-COUNTER                           04400199
                   PERFORM 2100-WRITE-REPORT-HEADINGS                   04500077
                   PERFORM 2200-WRITE-REPORT-DETAIL                     04520077
                   MOVE TFRD060-TO-PSTG-LOC-NBR TO PH-TO-PSTG-LOC-NBR   04521099
                   MOVE TFRD060-FR-LOC-NBR      TO PH-FR-LOC-NBR        04522099
                   MOVE TFRD060-XFER-SEQ-NBR    TO PH-XFER-SEQ-NBR      04523099
                                                                        04551099
           END-EVALUATE.                                                04560049
                                                                        04570049
           PERFORM 8000-READ-INPUT-FILE.                                04580054
                                                                        04590049
       EJECT.                                                           04600049
                                                                        04610053
      ***************************************************************** 04620049
      **WRITE REPORT HEADINGS                                           04630099
      ***************************************************************** 04640049
       2100-WRITE-REPORT-HEADINGS.                                      04650077
                                                                        04660049
           MOVE '2100-WRITE-REPORT-HEADINGS'  TO PV-PARAGRAPH-NAME.     04670077
                                                                        04680049
           INITIALIZE PC-REPORT-LINE-COUNTER.                           04690049
                                                                        04700049
           ADD +1                    TO PC-PAGE-COUNTER.                04710099
           MOVE PC-PAGE-COUNTER      TO HL0-PAGE-NUMBER.                04720099
                                                                        04770049
           WRITE REPORT-RECORD FROM HL-HEADER-LINE0                     04810099
               AFTER ADVANCING PAGE.                                    04820099
                                                                        04821099
           MOVE TFRD060-ACTL-INV-DATE TO HL1-INV-REPORT-DATE.           04830099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE1.                    04840099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE2.                    04860099
                                                                        04870199
           MOVE TFRD060-TO-PSTG-LOC-NBR TO HL3-TO-LOC-NBR.              04871099
           MOVE TFRD060-TO-LOC-NM       TO HL3-TO-LOC-NM.               04872099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE3                     04880099
               AFTER ADVANCING 2 LINES.                                 04890099
                                                                        04900053
           MOVE TFRD060-ACTL-INV-DATE TO HL4-INVENTORY-DATE.            04901099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE4.                    04910099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE5                     04930099
               AFTER ADVANCING 2 LINES.                                 04940099
           WRITE REPORT-RECORD FROM HL-HEADER-LINE6.                    04950099
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE.              04970099
                                                                        04990049
           ADD +10             TO PC-REPORT-LINE-COUNTER.               05000099
                                                                        05010049
       EJECT.                                                           05020049
                                                                        05030053
      ***************************************************************** 05040049
      **WRITE REPORT DETAIL LINE                                        05050077
      ***************************************************************** 05060049
       2200-WRITE-REPORT-DETAIL.                                        05070077
                                                                        05080049
           MOVE '2200-WRITE-REPORT-DETAIL' TO PV-PARAGRAPH-NAME.        05090077
                                                                        05100049
           INITIALIZE DL-DETAIL-LINE1.                                  05110054
                                                                        05120049
           MOVE TFRD060-FR-LOC-NBR        TO DL1-TRANSFER-FROM-LOC.     05200099
           MOVE TFRD060-XFER-SEQ-NBR      TO DL1-TRANSFER-SEQ-NBR.      05210099
           MOVE TFRD060-DEPT-NBR          TO DL1-DEPT-NBR.              05220099
           MOVE TFRD060-SUB-CL-NBR        TO DL1-SUBCLASS.              05230099
           MOVE TFRD060-SKU-NBR           TO DL1-SKU.                   05240099
           MOVE TFRD060-ITM-QTY           TO DL1-QTY.                   05250099
           MOVE TFRD060-ITM-RTL-PRCE-AMT  TO DL1-RETAIL.                05251099
                                                                        05251199
           COMPUTE PH-EXTENDED-RETAIL                                   05251299
             = TFRD060-ITM-QTY * TFRD060-ITM-RTL-PRCE-AMT.              05251399
           MOVE PH-EXTENDED-RETAIL        TO DL1-EXTENDED-RETAIL.       05252099
                                                                        05260049
           WRITE REPORT-RECORD FROM DL-DETAIL-LINE1.                    05280099
           ADD +1                         TO PC-REPORT-LINE-COUNTER.    05300099
                                                                        05390078
           COMPUTE PH-TRANSFER-QTY-TOTAL                                05400099
             = PH-TRANSFER-QTY-TOTAL        + TFRD060-ITM-QTY.          05410099
           COMPUTE PH-TRANSFER-EXT-RETAIL-TOTAL                         05420099
             = PH-TRANSFER-EXT-RETAIL-TOTAL + PH-EXTENDED-RETAIL.       05430099
                                                                        05480049
           ADD +1                         TO PC-REPORT-RECORDS-WRITTEN. 05490099
                                                                        05510082
       EJECT.                                                           05520049
                                                                        05521099
      ***************************************************************** 05780078
      **WRITE TRANSFER NUMBER TOTAL                                     05790099
      ***************************************************************** 05800078
       2300-WRITE-TRANSFER-NBR-TOTAL.                                   05810099
                                                                        05820078
           MOVE '2300-WRITE-TRANSFER-NBR-TOTAL' TO PV-PARAGRAPH-NAME.   05830099
                                                                        05840078
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE.              05850099
                                                                        05870078
           INITIALIZE TL-TOTAL-LINE.                                    05880099
           MOVE PC-TRANSFER-TOTAL-DESC       TO TL-DESC.                05890099
           MOVE PH-TRANSFER-QTY-TOTAL        TO TL-QTY.                 05900099
           MOVE PH-TRANSFER-EXT-RETAIL-TOTAL TO TL-EXTENDED-RETAIL.     05910099
                                                                        05930199
           WRITE REPORT-RECORD FROM TL-TOTAL-LINE.                      06000099
                                                                        06020078
           COMPUTE PH-STORE-QTY-TOTAL                                   06021099
            = PH-STORE-QTY-TOTAL + PH-TRANSFER-QTY-TOTAL.               06022099
           COMPUTE PH-STORE-EXT-RETAIL-TOTAL                            06022199
            = PH-STORE-EXT-RETAIL-TOTAL + PH-TRANSFER-EXT-RETAIL-TOTAL. 06022299
                                                                        06023099
           INITIALIZE PH-TRANSFER-QTY-TOTAL                             06030099
                      PH-TRANSFER-EXT-RETAIL-TOTAL.                     06040099
                                                                        06070079
           ADD +2                        TO PC-REPORT-LINE-COUNTER.     06080079
                                                                        06090099
       EJECT.                                                           06140078
                                                                        06145399
      ***************************************************************** 06145499
      **WRITE STORE NUMBER TOTAL                                        06145599
      ***************************************************************** 06145699
       2400-WRITE-STORE-NBR-TOTAL.                                      06145799
                                                                        06145899
           MOVE '2400-WRITE-STORE-NBR-TOTAL' TO PV-PARAGRAPH-NAME.      06145999
                                                                        06146099
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE               06147099
               AFTER ADVANCING 2 LINES.                                 06147199
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE.              06147299
                                                                        06148099
           INITIALIZE TL-TOTAL-LINE.                                    06149099
           MOVE PC-STORE-TOTAL-DESC       TO TL-DESC.                   06150099
           MOVE PH-STORE-QTY-TOTAL        TO TL-QTY.                    06160099
           MOVE PH-STORE-EXT-RETAIL-TOTAL TO TL-EXTENDED-RETAIL.        06170099
                                                                        06180099
           WRITE REPORT-RECORD FROM TL-TOTAL-LINE.                      06190099
                                                                        06200099
           COMPUTE PH-REPORT-QTY-TOTAL                                  06210099
             = PH-REPORT-QTY-TOTAL + PH-STORE-QTY-TOTAL.                06220099
           COMPUTE PH-REPORT-EXT-RETAIL-TOTAL                           06230099
             = PH-REPORT-EXT-RETAIL-TOTAL + PH-STORE-EXT-RETAIL-TOTAL.  06240099
                                                                        06250099
           INITIALIZE PH-STORE-QTY-TOTAL                                06260099
                      PH-STORE-EXT-RETAIL-TOTAL.                        06270099
                                                                        06280099
           ADD +4                        TO PC-REPORT-LINE-COUNTER.     06290099
                                                                        06300099
       EJECT.                                                           06310099
                                                                        06311099
      ***************************************************************** 06320099
      **WRITE REPORT NUMBER TOTAL                                       06330099
      ***************************************************************** 06340099
       2500-WRITE-REPORT-NBR-TOTAL.                                     06350099
                                                                        06360099
           MOVE '2500-WRITE-REPORT-NBR-TOTAL' TO PV-PARAGRAPH-NAME.     06370099
                                                                        06380099
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE               06381099
               AFTER ADVANCING 2 LINES.                                 06382099
           WRITE REPORT-RECORD FROM HL-DOTTED-HEADER-LINE.              06383099
                                                                        06400099
           INITIALIZE TL-TOTAL-LINE.                                    06410099
           MOVE PC-REPORT-TOTAL-DESC       TO TL-DESC.                  06420099
           MOVE PH-REPORT-QTY-TOTAL        TO TL-QTY.                   06430099
           MOVE PH-REPORT-EXT-RETAIL-TOTAL TO TL-EXTENDED-RETAIL.       06440099
                                                                        06450099
           WRITE REPORT-RECORD FROM TL-TOTAL-LINE.                      06460099
                                                                        06470099
       EJECT.                                                           06580099
                                                                        06620099
      ******************************************************************07642053
      * READ INPUT FILE FOR SUMMARY DETAIL REPORT DATA                  07650099
      ******************************************************************07660053
       8000-READ-INPUT-FILE.                                            07670053
                                                                        07680053
           READ INPUT-FILE INTO TFRD060-INV-SNAPSHOT-EXTRACT            07690099
               AT END                                                   07700053
                   SET PS-END-OF-INPUT-FILE  TO TRUE                    07710053
               NOT AT END                                               07720053
                   ADD +1 TO PC-INPUT-RECORDS-READ.                     07730053
                                                                        07740053
       EJECT.                                                           07750053
                                                                        07760053
      ****************************************************************  07900049
      *END OF PROGRAM ROUTINE                                           07910049
      ****************************************************************  07920049
       9999-WRAPUP.                                                     07930049
                                                                        07940049
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            07950090
               PERFORM 2300-WRITE-TRANSFER-NBR-TOTAL                    07970099
               PERFORM 2400-WRITE-STORE-NBR-TOTAL                       07971099
               PERFORM 2500-WRITE-REPORT-NBR-TOTAL                      07972099
           END-IF.                                                      08000090
                                                                        08010049
           IF PC-INPUT-RECORDS-READ = 0                                 08011099
               PERFORM 1100-HEADING-PREP                                08011199
               ADD +1                    TO PC-PAGE-COUNTER             08011299
               MOVE PC-PAGE-COUNTER      TO HL0-PAGE-NUMBER             08011399
               WRITE REPORT-RECORD FROM HL-HEADER-LINE0                 08011499
                   AFTER ADVANCING PAGE                                 08011599
               WRITE REPORT-RECORD FROM HL-HEADER-LINE1                 08011699
               WRITE REPORT-RECORD FROM ND-NO-DATA-LINE                 08011799
                   AFTER ADVANCING 2 LINES                              08011899
           END-IF.                                                      08012099
                                                                        08013099
           CLOSE INPUT-FILE                                             08020049
                 REPORT-FILE.                                           08040049
                                                                        08050049
           PERFORM 9999-DISPLAY-STATISTICS.                             08060049
                                                                        08070049
       EJECT.                                                           08080049
                                                                        08090054
      ****************************************************************  08100049
      *DISPLAY PROGRAM STATISTICS                                       08110049
      ****************************************************************  08120049
       9999-DISPLAY-STATISTICS.                                         08130049
                                                                        08140049
           DISPLAY '*************************************************'. 08150049
           DISPLAY '* PROGRAM...............: ' PC-PROGRAM-NAME.        08160064
           DISPLAY '* DATE..................: ' WS-DATE.                08170054
           DISPLAY '* TIMESTAMP.............: ' WS-TIME.                08180054
           DISPLAY '*************************************************'. 08190049
           DISPLAY '* INPUT RECORDS READ.........: '                    08200049
                                          PC-INPUT-RECORDS-READ.        08210049
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    08240049
                                          PC-REPORT-RECORDS-WRITTEN.    08250049
           DISPLAY '*************************************************'. 08280049
                                                                        08290049
       EJECT.                                                           08300049
                                                                        08310050
