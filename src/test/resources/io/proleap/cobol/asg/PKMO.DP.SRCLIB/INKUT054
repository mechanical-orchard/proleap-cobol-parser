       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUT054.                                        
       AUTHOR.                 JIMMY/INFOSYS.                                   
       INSTALLATION.           KOHLS.                                           
       DATE-WRITTEN.           08-27-08.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *     PROGRAM TO EXTRACT DATA FOR CREATING CUMULATIVE REPORT.    *        
      ******************************************************************        
      * THIS PROGRAM EXTRACTS DATA FOR CREATING CUMULATIVE REPORT THAT *        
      * NEED TO BREAK AND TOTAL BY CYCLE. OUTPUT FILE FROM THIS        *        
      * PROGRAM WILL BE USED IN NEW PROGRAMS INKRP054 AND INKRP055.    *        
      *                                                                *        
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES         *        
      * ------- ---------- ------------ -------------------------------*        
      * W33304  08-27-2008 JIMMY/        NEW PROGRAM CREATED AS PART   *        
      *                    INFOSYS       OF HOLDING INVENTORY LEDGER   *        
      *                                  OPEN - HILO                   *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT MERCH-EDIT-IN-FILE         ASSIGN TO INP01.                   
           SELECT MERCH-EDIT-OUT-FILE        ASSIGN TO OUT01.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  MERCH-EDIT-IN-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
                                                                                
       01  EDIFL-RPT1-RECORD                PIC  X(80).                         
                                                                                
       FD  MERCH-EDIT-OUT-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 110 CHARACTERS.                                      
                                                                                
       01  EDOFL-RPT1-RECORD                PIC  X(110).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *    CONSTANTS ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC  X(08)   VALUE 'INKUT054'.         
           05  PC-STATUS-CODE            PIC  X(02)   VALUE 'IN'.               
           05  PC-MAX-RETRIES            PIC S9(04)   VALUE +3                  
                                                      COMP SYNC.                
                                                                                
      ******************************************************************        
      *    VARIABLES ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-STORE-NBR                 PIC S9(04) COMP.                    
           05  PV-TEMP-STORE-NBR            PIC 9(05)    VALUE ZEROES.          
           05  PV-OPER-ATTEMPTED            PIC X(30)    VALUE SPACES.          
           05  PV-PARAGRAPH-NAME            PIC X(30)    VALUE SPACES.          
           05  PV-RETRY-COUNT               PIC S9(04)   VALUE ZERO             
                                                         COMP SYNC.             
       01  PV-HOLD-STORE-NBR                PIC S9(04) COMP.                    
                                                                                
      ******************************************************************        
      *    PROGRAM SWITCHES ARE DECLARED HERE                          *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MERCH-EDIT-IN-FILE-SW     PIC X(01)    VALUE 'N'.             
               88  PS-MERCH-EDIT-IN-EOF-YES              VALUE 'Y'.             
               88  PS-MERCH-EDIT-IN-EOF-NO               VALUE 'N'.             
           05  PS-STORE-FOUND-SW            PIC X(01)    VALUE 'N'.             
               88  PS-STORE-FOUND-YES                    VALUE 'Y'.             
               88  PS-STORE-FOUND-NO                     VALUE 'N'.             
                                                                                
      ******************************************************************        
      *    ACCUMULATORS ARE DECLARED HERE                              *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INP-RECS-CNT              PIC 9(09)    VALUE ZEROES.          
           05  PA-TABLE-ACS-CNT             PIC 9(09)    VALUE ZEROES.          
           05  PA-OUT-WRITE-CNT             PIC 9(09)    VALUE ZEROES.          
                                                                                
      ******************************************************************        
      *    ABEND CODE DECLARATION                                      *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04)   VALUE +0               
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      * INPUT FILE COPY MEMBER                                                  
      ******************************************************************        
           COPY MLRD025.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE COPY MEMBER                                                 
      ******************************************************************        
           COPY INRD035.                                                        
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINVPAR                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINCNTL                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    USED FOR SQL COMMUNICATION AREA.                            *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                               *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  PROGRAM PROCESSING STARTS HERE                                *        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATIONS.                                        
           PERFORM 3000-READ-MERCH-EDIT-IN-FILE.                                
           PERFORM 2000-PROCESS-FILE                                            
              UNTIL PS-MERCH-EDIT-IN-EOF-YES.                                   
           PERFORM 5000-END-OF-JOB.                                             
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   INITIALIZATION PARA                                          *        
      ******************************************************************        
                                                                                
       1000-INITIALIZATIONS.                                                    
                                                                                
           OPEN INPUT  MERCH-EDIT-IN-FILE                                       
                OUTPUT MERCH-EDIT-OUT-FILE.                                     
           INITIALIZE  ML025-RPT1-RECORD                                        
                       IN035-RPT1-RECORD.                                       
           SET PS-MERCH-EDIT-IN-EOF-NO      TO   TRUE.                          
                                                                                
           INITIALIZE                       PV-PROGRAM-VARIBLES                 
                                            PV-HOLD-STORE-NBR                   
                                            DCLTINVPAR                          
                                            DCLTINCNTL.                         
                                                                                
      ******************************************************************        
      *   MAIN PROCESSING PARA                                         *        
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           IF  ML025-RPT1-DETAIL-REC                                            
               SET IN035-RPT1-DETAIL-REC    TO TRUE                             
               MOVE ML025-RPT1-STORE-NBR    TO IN035-RPT1-STORE-NBR             
                                               PV-TEMP-STORE-NBR                
               MOVE PV-TEMP-STORE-NBR       TO PV-STORE-NBR                     
               MOVE ML025-RPT1-TRANS-CDE    TO IN035-RPT1-TRANS-CDE             
               MOVE ML025-RPT1-FIN-LIAB-DTE TO IN035-RPT1-FIN-LIAB-DTE          
               MOVE ML025-RPT1-FSCL-YR-PER  TO IN035-RPT1-FSCL-YR-PER           
               MOVE ML025-RPT1-SKU          TO IN035-RPT1-SKU                   
               MOVE ML025-RPT1-EXTD-RTL-AMT TO IN035-RPT1-EXTD-RTL-AMT          
               MOVE ML025-RPT1-EXTD-COST-AMT                                    
                                            TO IN035-RPT1-EXTD-COST-AMT         
               MOVE ML025-RPT1-ML-IND       TO IN035-RPT1-ML-IND                
               MOVE ML025-RPT1-INV-IND      TO IN035-RPT1-INV-IND               
               MOVE ML025-RPT1-DOC-5-NBRX   TO IN035-RPT1-DOC-5-NBRX            
               MOVE ML025-RPT1-BATCH-NBR    TO IN035-RPT1-BATCH-NBR             
               MOVE ML025-RPT1-XFER-DOC-NBR TO IN035-RPT1-XFER-DOC-NBR          
               MOVE ML025-RPT1-SYST-ORIG-CDE                                    
                                            TO IN035-RPT1-SYST-ORIG-CDE         
               MOVE ML025-RPT1-DEPT-CLASS   TO IN035-RPT1-DEPT-CLASS            
                                                                                
               IF  PV-STORE-NBR NOT = PV-HOLD-STORE-NBR                         
                   PERFORM 2100-SELECT-SORT-DATA                                
                   ADD 1                TO PA-TABLE-ACS-CNT                     
                   IF  PS-STORE-FOUND-YES                                       
                       MOVE INVPAR-INV-ID                                       
                                        TO IN035-INV-ID                         
                       MOVE INCNTL-OPN-FSCL-MN-DTE                              
                                        TO IN035-IN-OPN-FSCL-MN-DTE             
                       MOVE INCNTL-OPN-FSCL-MN-NBR                              
                                        TO                                      
                                        IN035-RPT1-INV-FSCL-PER-CDE             
                       MOVE INVPAR-FINCL-INV-STAT-CDE                           
                                        TO IN035-FINCL-INV-STAT-CDE             
                   ELSE                                                         
                       MOVE ZEROES      TO IN035-INV-ID                         
                       MOVE SPACES      TO IN035-IN-OPN-FSCL-MN-DTE             
                       MOVE SPACES      TO                                      
                                        IN035-RPT1-INV-FSCL-PER-CDE             
                       MOVE SPACES      TO IN035-FINCL-INV-STAT-CDE             
                   END-IF                                                       
               END-IF                                                           
               MOVE PV-STORE-NBR        TO PV-HOLD-STORE-NBR                    
               PERFORM 4000-WRITE-SORT-DATA-FILE                                
           ELSE                                                                 
               MOVE ML025-RPT1-CNTL-CARD-RECORD                                 
                                            TO                                  
                                         IN035-RPT1-CNTL-CARD-RECORD            
               PERFORM 4000-WRITE-SORT-DATA-FILE                                
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-MERCH-EDIT-IN-FILE.                                
                                                                                
      ******************************************************************        
      *   SELECTS THE SORT DATA FROM TABLES                            *        
      ******************************************************************        
       2100-SELECT-SORT-DATA.                                                   
                                                                                
           MOVE ZERO TO PV-RETRY-COUNT.                                         
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
                                                                                
           EXEC SQL                                                             
               SELECT A.INV_ID,                                                 
                      A.FINCL_INV_STAT_CDE,                                     
                      B.OPN_FSCL_MN_DTE,                                        
                      B.OPN_FSCL_MN_NBR                                         
                 INTO :INVPAR-INV-ID,                                           
                      :INVPAR-FINCL-INV-STAT-CDE,                               
                      :INCNTL-OPN-FSCL-MN-DTE,                                  
                      :INCNTL-OPN-FSCL-MN-NBR                                   
                 FROM TINVPAR A,                                                
                      TINCNTL B                                                 
                WHERE A.LOC_NBR = :PV-STORE-NBR                                 
                  AND A.LOC_INV_STAT_CDE = :PC-STATUS-CODE                      
                  AND A.ACTL_FIN_BK_DTE = '9999-09-09'                          
                  AND B.ACTV_IND = 'Y'                                          
                  AND A.INV_ID = B.INV_ID                                       
                 WITH UR                                                        
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                    SET PS-STORE-FOUND-YES  TO TRUE                             
               WHEN +100                                                        
                    SET PS-STORE-FOUND-NO   TO TRUE                             
               WHEN -904                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   MOVE '2100-SELECT-SORT-DATA'                                 
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'ERROR IN SELECT - SORT_DATA'                           
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '2100-SELECT-SORT-DATA' TO PV-PARAGRAPH-NAME                
               MOVE 'TINVPAR MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   READS INPUT FILE                                             *        
      ******************************************************************        
       3000-READ-MERCH-EDIT-IN-FILE.                                            
                                                                                
           READ MERCH-EDIT-IN-FILE          INTO ML025-RPT1-RECORD              
           AT END                                                               
               SET PS-MERCH-EDIT-IN-EOF-YES TO TRUE                             
           NOT AT END                                                           
               ADD 1                        TO PA-INP-RECS-CNT                  
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *   WRITES INTO THE OUTPUT FILE                                  *        
      ******************************************************************        
       4000-WRITE-SORT-DATA-FILE.                                               
                                                                                
           WRITE EDOFL-RPT1-RECORD          FROM IN035-RPT1-RECORD.             
           ADD 1                            TO   PA-OUT-WRITE-CNT.              
                                                                                
      ******************************************************************        
      *   END OF JOB PROCESSING                                        *        
      ******************************************************************        
       5000-END-OF-JOB.                                                         
                                                                                
           DISPLAY 'NUMBER OF RECORDS READ FROM INPUT FILE         : '          
                   PA-INP-RECS-CNT.                                             
           DISPLAY 'NUMBER OF ACCESS TO THE TABLES TINVPAR/TINCNTL : '          
                   PA-TABLE-ACS-CNT.                                            
           DISPLAY 'NUMBER OF RECORDS WRITTEN TO OUTPUT FILE       : '          
                   PA-OUT-WRITE-CNT.                                            
                                                                                
           CLOSE MERCH-EDIT-IN-FILE                                             
                 MERCH-EDIT-OUT-FILE.                                           
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                           *        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           CLOSE MERCH-EDIT-IN-FILE                                             
                 MERCH-EDIT-OUT-FILE.                                           
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = '  PC-PROGRAM-NAME.                     
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED.                   
           DISPLAY '**  PARAGRAPH **  = '  PV-PARAGRAPH-NAME.                   
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                  *        
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           MOVE +4013                       TO ABEND-CODE.                      
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
      *                 END OF PROGRAM INKUT054                        *        
      ******************************************************************        
