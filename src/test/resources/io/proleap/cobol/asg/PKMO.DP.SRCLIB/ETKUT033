       ID  DIVISION.                                                            
       PROGRAM-ID.         ETKUT033.                                            
       AUTHOR.             STEVE FLUNKER.                                       
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       APR 1995.                                            
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ETKUT033 - PO/UPC RECYCLCE UTILITY (TRAN PREP FOR INSERT)              
      *  WORK REQUEST #  - 3044                                                 
      ******************************************************************        
      *  THIS PROGRAM WILL CREATE A FILE WHICH HAS DATA TO BE INSERTED          
      *  BY THE NEXT PROGRAM.  THIS WILL ALLOW FOR CHECKPOINT RESTART           
      *  ON THE INSERT.                                                         
************************************************************************        
JF0503*  DATE  05/16/03                                                         
JF0503*  CHANGE MADE:                                                           
JF0503*   UPDATED PROGRAM SO IT IS COMPLIANT WITH NI-SKU OBJECTIVES.            
JF0503*  MADE BY: JON FIEDLER             WR# : 22777                           
************************************************************************        
************************************************************************        
WG0106*  DATE  01/26/06                                                         
WG0106*  CHANGE MADE:                                                           
WG0106*   HANDLE MULTIPLE TRADING PARTNER ID'S                                  
WG0106*  MADE BY: WADE GROVE              WR# : 27805                           
************************************************************************        
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS990 TO MAKE CALL TO DPKUT900 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
************************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  PO-INPUT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT  PO-OUTPUT-FILE                                               
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PO-INPUT-FILE                                                        
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS PO-INPUT-RECORD.                                      
                                                                                
       01  PO-INPUT-RECORD             PIC X(350).                              
                                                                                
       FD  PO-OUTPUT-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS PO-OUTPUT-RECORD.                                     
                                                                                
       01  PO-OUTPUT-RECORD             PIC X(350).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PO-INSERT-RECORD.                                                    
           05 I-TABLE-NAME              PIC X(07).                              
           05 I-TP-DKEY                 PIC S9(9) COMP.                         
           05 I-DATA-AREA               PIC X(354).                             
           05 I-TFGTRHL-AREA  REDEFINES I-DATA-AREA.                            
              10  I-TRN-SET-CDE         PIC X(3).                               
              10  I-TRN-DTL-TXT         PIC 9(9).                               
              10  I-RLSE-IND            PIC X(01).                              
              10  I-RLSE-DTE            PIC X(10).                              
              10  I-USER-ID             PIC X(08).                              
              10  FILLER                PIC X(323).                             
           05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                            
              10  I-PO-NBR              PIC 9(9).                               
              10  I-DEPT-NBR            PIC X(03).                              
              10  I-PO-RLSE-IND         PIC X(01).                              
              10  FILLER                PIC X(341).                             
           05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                            
              10  I-TRN-SET-SEQ-NBR     PIC S9(9) COMP.                         
              10  I-TRN-SET-DATA        PIC X(350).                             
                                                                                
       01  WS-WORKING-STORAGE.                                                  
           05  WS-NOSKU.                                                        
               10  FILLER                 PIC X(05) VALUE 'NOSKU'.              
               10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.                 
                                                                                
           05  WS-HEADER-RECORD.                                                
               10  WS-PO-NBR              PIC 9(09) VALUE ZERO.                 
               10  WS-LINE-NBR            PIC 9(06) VALUE ZERO.                 
               10  WS-DEPT-NBR            PIC X(03) VALUE SPACES.               
               10  WS-STATUS              PIC X(01) VALUE SPACE.                
               10  FILLER                 PIC X(28) VALUE SPACES.               
               10  WS-SHIP-DATE           PIC X(06) VALUE SPACES.               
               10  WS-INTERCHANGE-ID-QUAL PIC X(02) VALUE SPACES.               
               10  WS-INTERCHANGE-ID      PIC X(15) VALUE SPACES.               
               10  FILLER                 PIC X(280) VALUE SPACES.              
           05  WS-TP-ID-QUAL              PIC X(02) VALUE SPACES.               
           05  WS-TP-ID                   PIC X(15) VALUE SPACES.               
           05  WS-SEQ-NBR                 PIC S9(9) COMP.                       
           05  WS-PO002-I-UPC             PIC S9(15)V COMP-3.                   
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CHECKPOINT-TYPE           PIC  X     VALUE SPACE.             
               88  CONTROL-BREAK                       VALUE 'C'.               
               88  LOGICAL-TRANSACTION                 VALUE 'L'.               
               88  TIME-INTERVAL                       VALUE 'T'.               
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.               
               88  NO-COMMIT                           VALUE ' '.               
           05  PV-DB2-KEYS.                                                     
               10  PV-DB2-STR-NBR           PIC  X(03).                         
               10  PV-DB2-DOC-NBR           PIC S9(07) COMP-3.                  
               10  PV-DB2-DEPT-NBR          PIC  X(03).                         
               10  PV-DB2-CL-NBR            PIC  X(03).                         
               10  FILLER                   REDEFINES                           
                   PV-DB2-CL-NBR.                                               
                   15  PV-DB2-CL-NBR-1      PIC  X.                             
                   15  PV-DB2-CL-NBR-2-3    PIC  X(02).                         
               10  PV-DB2-SEQ-NBR           PIC  X(03).                         
               10  PV-DB2-ROW-NBR           PIC S9(04) COMP.                    
               10  PV-DB2-INV-DKEY          PIC S9(09) COMP.                    
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.              
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.             
                                                                                
      ******************************************************************        
      * TRANSACTION PREPARATION MODULE LINKAGE SECTION.                         
      ******************************************************************        
                                                                                
260107     COPY DPWS990.                                                        
                                                                                
           COPY DPLS000.                                                        
                                                                                
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03                   
                                                    COMP-3.                     
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'ETKUT033'.        
           05  PC-TRAN-PREP-FUNC-CODES.                                         
               10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.           
               10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.           
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.           
           05  PC-JOB-NAME                  PIC  X(08) VALUE 'ETK160  '.        
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'ETKUP033'.        
           05  PC-OPERCO-NBR                PIC  X(02) VALUE '03'.              
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE             PIC X     VALUE 'N'.                  
               88  PS-EOF                           VALUE 'Y'.                  
           05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.                  
               88  ONE-DEPT                         VALUE 'Y'.                  
               88  MULTIPLE-DEPTS                   VALUE 'N'.                  
           05  PS-CATALOG-ERROR           PIC X     VALUE 'N'.                  
               88  CATALOG-ERROR                    VALUE 'Y'.                  
               88  CATALOG-IS-OK                    VALUE 'N'.                  
           05  PS-KEEP-THIS-PO            PIC X     VALUE 'Y'.                  
               88  KEEP-THIS-PO                     VALUE 'Y'.                  
               88  KICK-OUT-THIS-PO                 VALUE 'N'.                  
           05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.                  
               88  ALL-GOOD-UPCS                    VALUE 'Y'.                  
               88  NOT-ALL-GOOD-UPCS                VALUE 'N'.                  
      *                                                                         
      *****************************************************************         
      *****  TO BE PASSED FILE RECORD LAYOUT.                                   
      *****************************************************************         
      *                                                                         
           COPY PORD002.                                                        
           EJECT                                                                
      *                                                                         
      ******************************************************************        
      *  SQL ABEND WORKING STORAGE                                              
      ******************************************************************        
           COPY DPWS004.                                                        
           EJECT                                                                
      *                                                                         
      *****************************************************************         
      *****  DB2 UPC LOOKUP TABLE DCLGEN                                        
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TUNUPCS                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *****  DB2 PO ON HOLD TABLE DCLGEN                                        
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TTRPOHL                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *****  DB2 EDI FUNCTIONAL GROUP ON HOLD TABLE                             
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TFGTRHL                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *****  DB2 EDI FUNCTIONAL GROUP ON HOLD DETAIL TABLE                      
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TFGTRDT                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *****  DB2 EDI TRADING PARTNER TABLE DCLGEN                               
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TTRDPRT                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *****  DB2 EDI TRADING PARTNER TRANSMISSION TABLE DCLGEN                  
      *****************************************************************         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TTRDTRN                                                  
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      * DB2 COMMUNICATIONS AREA                                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
           COPY SQLCA2.                                                         
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
           EJECT                                                                
       LINKAGE SECTION.                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      * MAIN-LINE LOGIC.                                                        
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
      *                                                                         
           DISPLAY 'ETKUT033 PRODUCTION'.                                       
                                                                                
           OPEN INPUT  PO-INPUT-FILE.                                           
           OPEN OUTPUT PO-OUTPUT-FILE.                                          
      *                                                                         
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.                      
           PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                              
           MOVE DP000-CKPT-TYPE-CODE TO PV-CHECKPOINT-TYPE.                     
                                                                                
           PERFORM Z100-READ-INPUT-FILE.                                        
      *                                                                         
           PERFORM B100-PROCESS-PO-FILE                                         
                   UNTIL PS-EOF.                                                
      *                                                                         
           CLOSE PO-INPUT-FILE                                                  
                 PO-OUTPUT-FILE.                                                
                                                                                
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
           PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                              
      *                                                                         
           IF CATALOG-ERROR                                                     
              MOVE 4060 TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
      *                                                                         
      ******************************************************************        
      *  PROCESS ALL PO RECORDS FROM INPUT FILE.                                
      ******************************************************************        
       B100-PROCESS-PO-FILE.                                                    
      *                                                                         
            IF PO002-HEADER-RECORD                                              
               SET KEEP-THIS-PO TO TRUE                                         
               IF PO002-NEW-PO-GOOD                                             
                  PERFORM Z200-WRITE-OUTPUT-FILE                                
                  PERFORM Z100-READ-INPUT-FILE                                  
                  PERFORM UNTIL PS-EOF OR PO002-HEADER-RECORD                   
                     PERFORM Z200-WRITE-OUTPUT-FILE                             
                     PERFORM Z100-READ-INPUT-FILE                               
                  END-PERFORM                                                   
               ELSE                                                             
                  PERFORM C100-FIND-DATALESS-KEY                                
WG0106            IF SQLCODE = +000 OR                                          
WG0106               SQLCODE = -811                                             
                     CONTINUE                                                   
                  ELSE                                                          
WG0106               IF SQLCODE = +100                                          
                        SET KICK-OUT-THIS-PO TO TRUE                            
                        DISPLAY '**** '                                         
                        DISPLAY '**** PO WAS KICKED OUT DUE TO'                 
                        DISPLAY '**** INVALID TRADING PARTNERSHIP'              
                        DISPLAY '**** PO: ' PO002-H-PO-NBR                      
                        DISPLAY '**** '                                         
                     END-IF                                                     
                  END-IF                                                        
                  IF KEEP-THIS-PO                                               
                     SET LOGICAL-TRANSACTION TO TRUE                            
                     PERFORM C200-INSERT-TFGTRHL-ROW                            
                     PERFORM Z300-WRITE-INSERT-RECORD                           
                                                                                
                     SET NO-COMMIT TO TRUE                                      
                                                                                
                     PERFORM C300-INSERT-TFGPOHL-ROW                            
                     PERFORM Z300-WRITE-INSERT-RECORD                           
                     MOVE +1 TO WS-SEQ-NBR                                      
                     PERFORM C400-INSERT-TFGTRDT-ROW                            
                     PERFORM Z300-WRITE-INSERT-RECORD                           
                     PERFORM Z100-READ-INPUT-FILE                               
                                                                                
                     PERFORM UNTIL PO002-HEADER-RECORD OR                       
                                   PS-EOF                                       
                        IF PO002-ITEM-RECORD                                    
      * IF NOT NOSKU                                                            
JF0503                     IF PO002-I-SKU(1:5) NOT = 'NOSKU'                    
                              CONTINUE                                          
                           ELSE                                                 
                              MOVE PO002-I-UPC TO WS-PO002-I-UPC                
                              PERFORM C500-SELECT-TUNUPCS                       
                              IF SQLCODE = +000                                 
                                 IF UNUPCS-TRANS-IND = 'Y'                      
                                    SET CATALOG-ERROR TO TRUE                   
                                 END-IF                                         
                              END-IF                                            
                           END-IF                                               
                        END-IF                                                  
                        ADD +1 TO WS-SEQ-NBR                                    
                        PERFORM C400-INSERT-TFGTRDT-ROW                         
                        PERFORM Z300-WRITE-INSERT-RECORD                        
                        PERFORM Z100-READ-INPUT-FILE                            
                                                                                
                     END-PERFORM                                                
                  ELSE                                                          
                     PERFORM Z100-READ-INPUT-FILE                               
                     PERFORM UNTIL PO002-HEADER-RECORD OR                       
                                   PS-EOF                                       
                           PERFORM Z100-READ-INPUT-FILE                         
                     END-PERFORM                                                
                  END-IF                                                        
               END-IF                                                           
            ELSE                                                                
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  RECORDS IN THE INPUT FILE WERE'                     
               DISPLAY '**  OUT OF SEQUENCE.    '                               
               DISPLAY '**  PROGRAM   **  =  ETKUT033'                          
               DISPLAY '**  PARAGRAPH **  = B100-PROCESS-FILE'                  
               MOVE +4007          TO ABEND-CODE                                
               CALL 'ILBOABN0'  USING ABEND-CODE                                
            END-IF.                                                             
      *                                                                         
      ******************************************************************        
      *  FIND THE DATALESS KEY FOR THIS TRADING PARTNER                         
      ******************************************************************        
       C100-FIND-DATALESS-KEY.                                                  
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100                                      
                         OR SQLCODE = -811)                                     
      *                                                                         
               EXEC SQL                                                         
                  SELECT A.TP_DKEY                                              
                  INTO :TRDPRT-TP-DKEY                                          
                  FROM TTRDPRT A, TTRDTRN B                                     
                  WHERE A.VEND_INCHG_ID      = :PO002-H-INTERCHANGE-ID          
                    AND A.VEND_INCHG_ID_DESC                                    
                          = :PO002-H-INTERCHANGE-ID-QUAL                        
                    AND A.INACTV_DTE         > CURRENT DATE                     
                    AND B.TP_DKEY            = A.TP_DKEY                        
                    AND B.TRAN_SET_CDE       = '855'                            
                    AND B.INACTV_DTE         > CURRENT DATE                     
                    AND B.PROD_TEST_IND      = 'P'                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -811                                          
                   WHEN SQLCODE = 100                                           
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'C100-FIND-DATALESS-KEY'                            
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FIND DATALESS KEY'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'C100-FIND-DATALESS-KEY'                                    
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'VERIFY RETRIES EXCEEDED'                                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTRHL TABLE        
      ******************************************************************        
       C200-INSERT-TFGTRHL-ROW.                                                 
      *                                                                         
           MOVE SPACES TO PO-INSERT-RECORD.                                     
           MOVE 'TFGTRHL'      TO I-TABLE-NAME.                                 
           MOVE TRDPRT-TP-DKEY TO I-TP-DKEY.                                    
           MOVE '855'          TO I-TRN-SET-CDE.                                
           MOVE PO002-H-PO-NBR TO I-TRN-DTL-TXT.                                
           MOVE 'N'            TO I-RLSE-IND.                                   
           MOVE '9999-09-09'   TO I-RLSE-DTE.                                   
           MOVE 'ETKUP033'     TO I-USER-ID.                                    
      *                                                                         
      ******************************************************************        
      *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTPOL TABLE        
      ******************************************************************        
       C300-INSERT-TFGPOHL-ROW.                                                 
      *                                                                         
           MOVE SPACES           TO I-DATA-AREA.                                
           MOVE 'TTRPOHL'        TO I-TABLE-NAME.                               
           MOVE PO002-H-PO-NBR   TO I-PO-NBR.                                   
           MOVE PO002-H-DEPT-NBR TO I-DEPT-NBR.                                 
           MOVE 'N'              TO I-PO-RLSE-IND.                              
      *                                                                         
      ******************************************************************        
      *  WRITE RECORD TO INPUT FILE FOR INSERTION INTO THE TFGTRDT TABLE        
      ******************************************************************        
       C400-INSERT-TFGTRDT-ROW.                                                 
      *                                                                         
           MOVE SPACES              TO I-DATA-AREA.                             
           MOVE 'TFGTRDT'           TO I-TABLE-NAME.                            
           MOVE WS-SEQ-NBR          TO I-TRN-SET-SEQ-NBR.                       
           MOVE PO002-EDI855-RECORD TO I-TRN-SET-DATA.                          
      *                                                                         
      ******************************************************************        
      *  FIND IF THE UPC REQUEST IS BACK FROM THE CATALOG                       
      ******************************************************************        
       C500-SELECT-TUNUPCS.                                                     
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                   SELECT A.TRANS_IND                                           
                   INTO :UNUPCS-TRANS-IND                                       
                   FROM TUNUPCS A                                               
                   WHERE A.UPC_CDE = :WS-PO002-I-UPC                            
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 100                                           
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'C500-SELECT-TUNUPCS'                               
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FIND UPC ON CATALOG'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'C500-SELECT-TUNUPCS'                                       
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'VERIFY RETRIES EXCEEDED'                                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * Z100-READ-INPUT-FILE                                                    
      ******************************************************************        
       Z100-READ-INPUT-FILE.                                                    
      *                                                                         
           READ PO-INPUT-FILE INTO                                              
                PO002-EDI855-RECORD                                             
                  AT END SET PS-EOF TO TRUE.                                    
      *                                                                         
      ******************************************************************        
      * Z200-WRITE-OUTPUT-FILE                                                  
      ******************************************************************        
       Z200-WRITE-OUTPUT-FILE.                                                  
      *                                                                         
           WRITE PO-OUTPUT-RECORD FROM                                          
                PO002-EDI855-RECORD.                                            
      *                                                                         
      ******************************************************************        
      * Z300-WRITE-INSERT-RECORD                                                
      ******************************************************************        
       Z300-WRITE-INSERT-RECORD.                                                
      *                                                                         
      *    *--------------------------------------------------------            
      *    * THERE ARE NO CONTROL BREAKS IN THIS PROGRAM.  TAKING               
      *    * CHECKPOINTS BY CONTROL BREAK HAS THE SAME EFFECT AS                
      *    * TAKING CHECKPOINTS BY LOGICAL TRANSACTION.                         
      *    *--------------------------------------------------------            
           IF LOGICAL-TRANSACTION                                               
           OR CONTROL-BREAK                                                     
               MOVE 'Y' TO DP000-CHECKPOINT-IND                                 
           ELSE                                                                 
               MOVE 'N' TO DP000-CHECKPOINT-IND                                 
           END-IF.                                                              
                                                                                
           MOVE LENGTH OF PO-INSERT-RECORD                                      
                                        TO DP000-TRANS-REC-LENGTH.              
           MOVE PO-INSERT-RECORD        TO DP000-TRANS-REC.                     
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                     
           PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                              
                                                                                
      ******************************************************************        
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION              
      * PREPARATION MODULE.                                                     
      ******************************************************************        
       Z400-BUILD-COMM-AREA-TRAN-PREP.                                          
                                                                                
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.                      
           MOVE PC-CKPT-PLAN-NAME       TO DP000-PLAN-NAME.                     
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
                                                                                
      ******************************************************************        
      *     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
      ******************************************************************        
                                                                                
           COPY DPPD000.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  ETKUT033'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
