       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    POKUP052.                                         00020000
       AUTHOR.        RYAN STAUFFACHER.                                 00030000
       DATE-WRITTEN.  04/04.                                            00040000
      ******************************************************************00050000
      * THIS PROGRAM WILL HANDLE THE EDI870 PROCESS AND WILL SEND       00060000
      * MESSAGES TO THE UPDATE API AND WAIT FOR A RESPONSE.             00070000
      ******************************************************************00080000
C98147*   CHANGE LOG                                                    00090008
C98147******************************************************************00100008
C98147* 11/04/14 MADE BY: LINDA FRIESS               CHG0098147         00110008
C98147* CHANGES: EXPANDED PO NUMBER TO 8 DIGITS. UPDATED PORD033        00120008
C98147*          COPYBOOK.                                              00130008
C98147***************************************************************** 00140008
       ENVIRONMENT DIVISION.                                            00150000
       INPUT-OUTPUT SECTION.                                            00160000
                                                                        00170000
       FILE-CONTROL.                                                    00180000
           SELECT INPUT-FILE                                            00190000
                 ASSIGN TO INP01.                                       00200000
                                                                        00210000
           SELECT CONTROL-OUTPUT-QMGR                                   00220000
                 ASSIGN TO INP02.                                       00230000
                                                                        00240000
           SELECT CONTROL-OUTPUT-QMGR-API                               00250000
                 ASSIGN TO INP03.                                       00260000
                                                                        00270000
           SELECT CONTROL-OUTPUT-QUEUE                                  00280000
                 ASSIGN TO INP04.                                       00290000
                                                                        00300000
       DATA DIVISION.                                                   00310000
       FILE SECTION.                                                    00320000
                                                                        00330000
       FD  INPUT-FILE                                                   00340000
           RECORDING MODE IS F                                          00350000
           LABEL RECORDS ARE STANDARD                                   00360000
           RECORD CONTAINS 200 CHARACTERS                               00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           DATA RECORD IS INPUT-RECORD.                                 00390000
                                                                        00400000
       01  INPUT-RECORD                     PIC X(200).                 00410000
                                                                        00420000
       FD  CONTROL-OUTPUT-QMGR                                          00430000
           RECORDING MODE F                                             00440000
           BLOCK CONTAINS 0 RECORDS                                     00450000
           LABEL RECORDS ARE OMITTED.                                   00460000
       01  CONTROL-OUTPUT-QMGR-REC          PIC  X(80).                 00470000
                                                                        00480000
       FD  CONTROL-OUTPUT-QMGR-API                                      00490000
           RECORDING MODE F                                             00500000
           BLOCK CONTAINS 0 RECORDS                                     00510000
           LABEL RECORDS ARE OMITTED.                                   00520000
       01  CONTROL-OUTPUT-QMGR-REC-API      PIC  X(80).                 00530000
                                                                        00540000
       FD  CONTROL-OUTPUT-QUEUE                                         00550000
           RECORDING MODE F                                             00560000
           BLOCK CONTAINS 0 RECORDS                                     00570000
           LABEL RECORDS ARE OMITTED.                                   00580000
       01  CONTROL-OUTPUT-QUEUE-REC-API     PIC  X(80).                 00590000
                                                                        00600000
       WORKING-STORAGE SECTION.                                         00610000
      *    API CONTROL BLOCKS                                           00620000
                                                                        00630000
       01  MQM-OBJECT-DESCRIPTOR.                                       00640000
           COPY CMQODV.                                                 00650000
       01  MQM-MESSAGE-DESCRIPTOR.                                      00660000
           COPY CMQMDV.                                                 00670000
       01  MQM-GET-MESSAGE-OPTIONS.                                     00680000
           COPY CMQGMOV.                                                00690000
       01  MQM-PUT-MESSAGE-OPTIONS.                                     00700000
           COPY CMQPMOV.                                                00710000
                                                                        00720000
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   00730000
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          00740000
                                                                        00750000
       01  MQM-CONSTANTS.                                               00760000
           COPY CMQV.                                                   00770000
      *                                                                 00780000
       01  MQ-WORKING-STORAGE.                                          00790000
           05  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.      00800000
           05  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.      00810000
           05  WS-MQGET                PIC X(8)  VALUE 'CSQBGET'.       00820000
           05  WS-MQPUT                PIC X(8)  VALUE 'CSQBPUT'.       00830000
           05  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.      00840000
           05  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.      00850000
           05  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.      00860000
           05  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.      00870000
           05  WS-SYSWAIT              PIC X(08) VALUE 'SYSWAIT'.       00880000
           05  WS-MQCHECK              PIC X(08) VALUE 'MQR2C'.         00890000
                                                                        00900000
       01  PC-PROGRAM-CONSTANTS.                                        00910000
           05  PC-DELAY-INTERVAL.                                       00920000
               10  FILLER              PIC X(2)     VALUE SPACES.       00930000
               10  PC-DELAY-TIME       PIC 9(8)     VALUE 0500.         00940000
           05  PC-MQ-SERIES-ERROR      PIC X(01)    VALUE 'M'.          00950000
           05  PC-MAX-RETRIES          PIC S9(04)   VALUE +3.           00960000
           05  PC-MAX-PUTS             PIC S9(04)   VALUE +4500.        00970000
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'POKUP052'.   00980000
           05  PC-ED870-APPL-ID        PIC X(08)    VALUE 'EDI870'.     00990001
           05  PC-ZERO-LOC             PIC X(04)    VALUE '0000'.       01000000
           05  PC-ZERO-QTY             PIC X(09)    VALUE '000000000'.  01010000
                                                                        01020000
       01  WS-PROGRAM-SWITCHES.                                         01030000
           05  PS-MQPUT-SUCCESSFUL-SW       PIC X     VALUE 'N'.        01040000
               88  PS-MQPUT-SUCCESSFUL                VALUE 'Y'.        01050000
           05  PS-MQPUT-ERROR-SW            PIC X     VALUE 'N'.        01060000
               88  PS-MQPUT-ERROR                     VALUE 'Y'.        01070000
           05  PS-MQ-CONNECTED-SW           PIC  X(01)    VALUE 'N'.    01080000
               88  PS-MQ-CONNECTED                        VALUE 'Y'.    01090000
               88  PS-MQ-NOT-CONNECTED                    VALUE 'N'.    01100000
           05  PS-MQ-OPENED-SW              PIC  X(01)    VALUE 'N'.    01110000
               88  PS-MQ-OPENED                           VALUE 'Y'.    01120000
               88  PS-MQ-NOT-OPENED                       VALUE 'N'.    01130000
           05  PS-END-OF-INPUT-FILE         PIC  X(01)    VALUE 'Y'.    01140000
               88  PS-MORE-INPUT                          VALUE 'Y'.    01150000
               88  PS-NO-MORE-INPUT                       VALUE 'N'.    01160000
           05  PS-FIRST-MESSAGE-SW          PIC  X(01)    VALUE 'Y'.    01170000
               88  PS-FIRST-MESSAGE                       VALUE 'Y'.    01180000
               88  PS-NOT-FIRST-MESSAGE                   VALUE 'N'.    01190000
           05  PS-EMPTY-INPUT-FILE-SW       PIC  X(01)    VALUE 'N'.    01200000
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    01210000
               88  PS-NOT-EMPTY-INPUT-FILE                VALUE 'N'.    01220000
370482     05  PS-RUN-TYPE-SW                PIC  X(01)    VALUE ' '.   01230000
370483         88  PS-NORMAL-RUN                           VALUE '0'.   01240000
370484         88  PS-RESTART-RUN                          VALUE '9'.   01250000
           05  PS-FIRST-COMMIT-SW      PIC X       VALUE 'N'.           01260000
               88  PS-FIRST-COMMIT                 VALUE 'Y'.           01270000
           05  PS-COMMIT-TAKEN-SW      PIC X       VALUE 'N'.           01280000
               88  PS-COMMIT-NOT-TAKEN             VALUE 'N'.           01290000
               88  PS-COMMIT-TAKEN                 VALUE 'Y'.           01300000
                                                                        01310000
       01  PV-PROGRAM-VARIABLES.                                        01320000
           05  PV-CKPT-STAT-CODE           PIC X(01)  VALUE SPACE.      01330000
           05  PV-870-MSGS-READ            PIC S9(9)  COMP VALUE 0.     01340000
           05  PV-MQPUT-RETRY-COUNTER      PIC S9(4)  COMP VALUE 0.     01350000
           05  PV-MQPUT-COUNTER-COMMIT     PIC S9(9)  COMP VALUE 0.     01360000
           05  PV-ROW-COUNT                PIC S9(9)  COMP VALUE 0.     01370000
           05  PV-MQPUT-COUNTER-TOTAL      PIC S9(9)  COMP VALUE 0.     01380000
           05  PV-MSGBUFFER-LENGTH         PIC S9(9)  BINARY.           01390000
           05  PV-COMPLETION-CODE          PIC S9(9)  COMP.             01400000
           05  PV-REASON                   PIC S9(9)  COMP.             01410000
           05  PV-HCONN                    PIC S9(09) BINARY VALUE 0.   01420000
           05  PV-PQ-HANDLE                PIC S9(09) BINARY VALUE 0.   01430000
           05  PV-OPEN-OPTIONS             PIC S9(09) BINARY.           01440000
           05  PV-CON-REASON               PIC S9(09) BINARY.           01450000
           05  PV-STRING-CLAUSE.                                        01460000
               10  FILLER                  PIC X(201).                  01470000
           05  PV-STRING-LENGTH            PIC S9(09)   VALUE +0 COMP.  01480000
           05  PV-MQ-REASON-TEXT           PIC X(30)  VALUE SPACES.     01490000
           05  PV-MQ-OUTPUT-RECORD         PIC X(30)  VALUE SPACES.     01500000
           05  PV-ERROR.                                                01510000
               10  PV-ERROR-LITERAL        PIC X(14)  VALUE SPACES.     01520000
               10  PV-ERROR-VARIABLE       PIC X(34)  VALUE SPACES.     01530000
           05  PV-ERROR-MESSAGE            PIC X(48)  VALUE SPACES.     01540000
           05  PV-ERROR-MESSAGE2           PIC X(48)  VALUE SPACES.     01550000
           05  PV-SAVE-COMPLETION-CODE     PIC S9(09) BINARY.           01560000
           05  PV-SAVE-REASON              PIC S9(09) BINARY.           01570000
           05  PV-PUT-COUNT                PIC S9(09) COMP.             01580000
           05  PV-870-MSGS-PROCESSED       PIC S9(09)  VALUE +0  COMP.  01590000
C98147     05  PV-HOLD-PO-NUMBER           PIC 9(08)  VALUE ZERO.       01600008
           05  PV-HOLD-DC                  PIC 9(05).                   01610000
           05  PV-HOLD-SKU                 PIC 9(08).                   01620000
           05  PV-STORE-NBR-X.                                          01630000
               10 FILLER                   PIC X(01).                   01640000
               10 PV-STORE-X               PIC X(04).                   01650000
           05  PV-DC-NBR-X.                                             01660000
               10 FILLER                   PIC X(01).                   01670000
               10 PV-DC-X                  PIC X(04).                   01680000
           05  PV-PO-NBR-X.                                             01690000
               10 FILLER                   PIC X(01) VALUE SPACE.       01700000
               10 PV-PO-NBR-X-7            PIC X(07).                   01710005
           05  PV-ADJUSTED-QTY-X           PIC X(10).                   01720000
                                                                        01730000
371169 01  CHECKPOINT-RESTART-INFO.                                     01740000
           05  CKR-AREA-LEN                PIC S9(04) VALUE +218 COMP.  01750000
           05  CKR-RESTART-VAR.                                         01760000
371170       10  CKR-TOT-PUT-REC-COUNT       PIC 9(09)  VALUE  0.       01770000
371170       10  CKR-MESSAGES-READ           PIC 9(09)  VALUE  0.       01780000
             10  CKR-LAST-VALID-RECORD.                                 01790000
C98147           15  CKR-PO-NUMBER                     PIC  9(08).      01800008
                 15  CKR-DEPT-NUMBER                   PIC  X(03).      01810000
                 15  CKR-PO-DATE.                                       01820000
                     20  CKR-CCYY.                                      01830000
                         25  CKR-CC                    PIC  X(02).      01840000
                         25  CKR-YY                    PIC  X(02).      01850000
                     20  CKR-MM                        PIC  X(02).      01860000
                     20  CKR-DD                        PIC  X(02).      01870000
                 15  CKR-SKU                           PIC  9(08).      01880000
                 15  CKR-ITEM-UPC                      PIC  9(15).      01890000
                 15  CKR-DC                            PIC  9(05).      01900000
                 15  CKR-LOCATION                      PIC  9(05).      01910000
                 15  CKR-QTY-ORDERED                   PIC S9(09).      01920000
                 15  CKR-UOM                           PIC  X(02).      01930000
                 15  CKR-ACTION-CODE                   PIC  X(02).      01940000
                 15  CKR-QTY-ADJUSTED                  PIC S9(09).      01950000
                 15  CKR-VENDOR-OSR-REF                PIC  X(30).      01960000
                 15  CKR-VENDOR-OSR-DATE               PIC  X(08).      01970000
                 15  CKR-VENDOR-INTERNAL-NUMBER        PIC  X(30).      01980000
                 15  CKR-870-REJECTION-REASONS.                         01990000
                     20  CKR-PO-DOES-NOT-EXIST-SW      PIC  X(01).      02000000
                     20  CKR-PO-NOT-ACTIVE-SW          PIC  X(01).      02010000
                     20  CKR-UNMATCHED-DEPT-SW         PIC  X(01).      02020000
                     20  CKR-DTL-REJECTION-REASONS     PIC  X(02).      02030000
C98147           15  FILLER                            PIC  X(53).      02040008
       01  WS-ABEND-FIELDS.                                             02050000
           05  WS-PROGRAM-ID                PIC X(8)  VALUE 'POKUP052'. 02060000
           05  PV-PARAGRAPH-NAME            PIC X(50) VALUE SPACES.     02070000
           05  WS-ABEND-CODE                PIC S9(4) COMP SYNC         02080000
                                                      VALUE ZERO.       02090000
               88  WS-CONTROL-CARD-ERROR              VALUE +4003.      02100000
               88  WS-DB2-ABEND                       VALUE +4013.      02110000
               88  WS-MQ-ERROR                        VALUE +4020.      02120000
               88  WS-PO056-ERROR                     VALUE +4030.      02130000
                                                                        02140000
       01  WS-ACCUMULATORS.                                             02150000
           05  PV-RETRY-COUNTER            PIC S9(04) COMP VALUE ZERO.  02160000
                                                                        02170000
       01  WS-UPDATE-API-MESSAGE.                                       02180000
           05  FILLER                      PIC X(10) VALUE 'PO_ALLOC'.  02190000
           05  UAM-APPL-REQUEST-ID.                                     02200000
               10 FILLER                   PIC X(06) VALUE 'EDI870'.    02210000
               10 UAM-AR-MESSAGE-TYPE      PIC X(02).                   02220000
                  88 UAM-AR-DC-MESSAGE     VALUE 'DC'.                  02230000
                  88 UAM-AR-STORE-MESSAGE  VALUE 'ST'.                  02240000
               10 UAM-AR-SKU               PIC X(08) VALUE SPACES.      02250000
               10 UAM-AR-STORE             PIC X(04) VALUE SPACES.      02260000
               10 UAM-AR-DC                PIC X(04) VALUE SPACES.      02270000
           05  FILLER                      PIC X(10) VALUE 'PO_EDI_870'.02280000
           05  UAM-PO-NUMBER               PIC X(08) VALUE SPACES.      02290000
           05  UAM-MESSAGE-TYPE            PIC X(02) VALUE SPACES.      02300000
               88 UAM-DC-MESSAGE           VALUE 'DC'.                  02310000
               88 UAM-STORE-MESSAGE        VALUE 'ST'.                  02320000
           05  FILLER                      PIC X(08) VALUE 'POKUP052'.  02330000
           05  UAM-SKU                     PIC X(08) VALUE SPACES.      02340000
           05  UAM-DC                      PIC X(04) VALUE SPACES.      02350000
           05  UAM-STORE                   PIC X(04) VALUE SPACES.      02360000
           05  UAM-ADJUSTED-QTY            PIC X(09) VALUE SPACES.      02370000
       01  CC-CONTROL-OUTPUT-QMGR.                                      02380000
           05  CC-OUTPUT-QMGR-NAME          PIC X(48).                  02390000
           05  FILLER                       PIC X(32).                  02400000
                                                                        02410000
       01  CC-CONTROL-OUTPUT-QMGR-API.                                  02420000
           05  CC-OUTPUT-QMGR-NAME-API      PIC X(48).                  02430000
           05  FILLER                       PIC X(32).                  02440000
                                                                        02450000
       01  CC-CONTROL-OUTPUT-QUEUE.                                     02460000
           05  CC-OUTPUT-QUEUE-NAME-API     PIC X(48).                  02470000
           05  FILLER                       PIC X(32).                  02480000
                                                                        02490000
      *  INPUT AND OUTPUT COPYBOOK                                      02500000
           COPY PORD033.                                                02510000
                                                                        02520000
216900******************************************************************02530000
216901*  DB2 TABLE DECLARATION - CHECKPOINT RESTART INFO TBL(TCKRSTINF) 02540000
216902*  THIS TABLE IS USED TO STORE WORKING STOREAGE INFORMATION FOR   02550000
216903*  CHECKPOINT RESTARTS.                                           02560000
216904******************************************************************02570000
216905     EXEC SQL                                                     02580000
216906         INCLUDE TCKRSTIN                                         02590000
216907     END-EXEC.                                                    02600000
216908*                                                                 02610000
216909******************************************************************02620000
216910*  DB2 TABLE DECLARATION - CHECKPOINT RESTART CONTROL (TCKRSTCNTL)02630000
216911*  THIS TABLE IS USED FOR CHECKPOINT RESTARTS.                    02640000
216912******************************************************************02650000
216913     EXEC SQL                                                     02660000
216914         INCLUDE TCKRSTCN                                         02670000
216915     END-EXEC.                                                    02680000
007600*                                                                 02690000
007700******************************************************************02700000
007800*  DB2 FORMATTED MESSAGE AREA                                     02710000
007900******************************************************************02720000
008000     COPY DPWS004.                                                02730000
008100*                                                                 02740000
010100******************************************************************02750000
010200*  COMMUNICATIONS AREA FOR DB2                                    02760000
010300******************************************************************02770000
010400     EXEC SQL                                                     02780000
010500          INCLUDE SQLCA                                           02790000
010600     END-EXEC.                                                    02800000
                                                                        02810000
       PROCEDURE DIVISION.                                              02820000
      ******************************************************************02830000
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM.                   02840000
      ******************************************************************02850000
       0000-MAINLINE.                                                   02860000
           OPEN INPUT  INPUT-FILE                                       02870000
                       CONTROL-OUTPUT-QMGR                              02880000
                       CONTROL-OUTPUT-QMGR-API                          02890000
                       CONTROL-OUTPUT-QUEUE.                            02900000
                                                                        02910000
           PERFORM 0100-INITIALIZATION.                                 02920000
                                                                        02930000
           PERFORM 1000-PROCESS-INPUT                                   02940000
            UNTIL PS-NO-MORE-INPUT.                                     02950000
           PERFORM 0110-END-PROCESSING.                                 02960000
                                                                        02970000
      ******************************************************************02980000
      * 0100-INITIALIZE.  DO THE NECESSARY INITIALIZATION              *02990000
      ******************************************************************03000000
       0100-INITIALIZATION.                                             03010000
           PERFORM 0200-MQ-INITIALIZATION.                              03020000
638297     PERFORM 7000-GET-CHECKPOINT-CNTL.                            03030000
638298     MOVE PV-CKPT-STAT-CODE       TO                              03040000
638299         PS-RUN-TYPE-SW.                                          03050000
638300                                                                  03060000
           IF PS-RESTART-RUN                                            03070000
               PERFORM 7100-GET-CHECKPOINT-INFO                         03080000
               MOVE TCKRSTINF-WORK-STRG-DESC                            03090000
                                      TO CHECKPOINT-RESTART-INFO        03100000
      *        RE-ESTABLISH COUNTS FOR PROCESSED INPUT                  03110000
               MOVE CKR-MESSAGES-READ TO PV-870-MSGS-PROCESSED          03120000
               DISPLAY '*** RESTART-LAST 870 TRANS COMMIT: '            03130000
               DISPLAY 'INPUT MESSAGE TO START WITH'                    03140000
                       ' PO:  '  CKR-PO-NUMBER                          03150000
                       ' SKU: '  CKR-SKU                                03160000
                       ' DC:  '  CKR-DC                                 03170000
                       ' LOC: '  CKR-LOCATION                           03180000
                       ' QTY: '  CKR-QTY-ORDERED                        03190000
               DISPLAY '*** RECORDS PROCESSED: '                        03200000
                        CKR-MESSAGES-READ                               03210000
           ELSE                                                         03220000
               SET PS-FIRST-COMMIT TO TRUE                              03230000
               INITIALIZE CKR-LAST-VALID-RECORD                         03240000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  03250000
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' MQ PUTS.'03260000
           END-IF.                                                      03270000
                                                                        03280000
           IF PS-RESTART-RUN                                            03290000
               PERFORM 9000-READ-INPUT-FILE                             03300000
                   UNTIL PV-870-MSGS-READ > CKR-MESSAGES-READ - 1       03310000
                     OR  PS-NO-MORE-INPUT                               03320000
           ELSE                                                         03330000
               PERFORM 9000-READ-INPUT-FILE                             03340000
           END-IF.                                                      03350000
                                                                        03360000
           IF  PS-NO-MORE-INPUT                                         03370000
               IF PS-RESTART-RUN                                        03380000
                   DISPLAY 'NO RECORDS PROCESSED AFTER RESTART'         03390000
                   SET PS-COMMIT-TAKEN TO TRUE                          03400000
               ELSE                                                     03410000
                   DISPLAY 'NO RECORDS IN INPUT FILE'                   03420000
                   SET PS-EMPTY-INPUT-FILE TO TRUE                      03430000
               END-IF                                                   03440000
           END-IF.                                                      03450000
      ******************************************************************03460000
      * 0110-END PROCESSING.  DO THE NECESSARY END OF PROGRAM          *03470000
      *      PROCESSING.                                               *03480000
      ******************************************************************03490000
       0110-END-PROCESSING.                                             03500000
           IF PS-NOT-EMPTY-INPUT-FILE                                   03510000
              PERFORM 1200-SEND-DC-MESSAGE                              03520000
           END-IF.                                                      03530000
                                                                        03540000
           DISPLAY PV-PUT-COUNT ' MESSAGES WERE PUT TO THE QUEUE.'.     03550000
                                                                        03560000
           CLOSE INPUT-FILE                                             03570000
                 CONTROL-OUTPUT-QMGR                                    03580000
                 CONTROL-OUTPUT-QMGR-API                                03590000
                 CONTROL-OUTPUT-QUEUE.                                  03600000
                                                                        03610000
           PERFORM 6000-COMMIT-PROCESSING.                              03620000
                                                                        03630000
           IF PS-MQ-OPENED                                              03640000
               PERFORM 0250-MQ-CLOSE-QUEUE                              03650000
           END-IF.                                                      03660000
                                                                        03670000
           IF PS-MQ-CONNECTED                                           03680000
               PERFORM 0260-MQ-DISCONNECT-QMGR                          03690000
           END-IF.                                                      03700000
                                                                        03710000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       03720000
           PERFORM 7200-UPDATE-TCKRSTCNTL.                              03730000
                                                                        03740000
           GOBACK.                                                      03750000
                                                                        03760000
      ***************************************************************** 03770000
      * DO THE NECESSARY MQ INITIALIZATION                            * 03780000
      ***************************************************************** 03790000
       0200-MQ-INITIALIZATION.                                          03800000
           PERFORM 0210-READ-CONTROL-FILES.                             03810000
           PERFORM 0220-MQ-CONNECT-QMGR.                                03820000
           IF PS-MQ-CONNECTED                                           03830000
              PERFORM 0230-MQ-OPEN-QUEUE                                03840000
           END-IF.                                                      03850000
                                                                        03860000
      ***************************************************************** 03870000
      * READ THE INFORMATION IN THE CONTROL FILES.                    * 03880000
      ***************************************************************** 03890000
       0210-READ-CONTROL-FILES.                                         03900000
           READ CONTROL-OUTPUT-QMGR INTO CC-CONTROL-OUTPUT-QMGR         03910000
               AT END                                                   03920000
                 DISPLAY WS-PROGRAM-ID                                  03930000
                   ' CONTROL CARD MISSING - FOR OUTPUT QMGR NAME'       03940000
                 SET WS-CONTROL-CARD-ERROR TO TRUE                      03950000
                 CALL 'ILBOABN0' USING WS-ABEND-CODE.                   03960000
                                                                        03970000
           READ CONTROL-OUTPUT-QMGR-API                                 03980000
                 INTO CC-CONTROL-OUTPUT-QMGR-API                        03990000
               AT END                                                   04000000
                 DISPLAY WS-PROGRAM-ID                                  04010000
                   ' CONTROL CARD MISSING - FOR OUTPUT QMGR-API NAME'   04020000
                 SET WS-CONTROL-CARD-ERROR TO TRUE                      04030000
                 CALL 'ILBOABN0' USING WS-ABEND-CODE.                   04040000
                                                                        04050000
           READ CONTROL-OUTPUT-QUEUE                                    04060000
                 INTO CC-CONTROL-OUTPUT-QUEUE                           04070000
               AT END                                                   04080000
                 DISPLAY WS-PROGRAM-ID                                  04090000
                   ' CONTROL CARD MISSING - FOR OUTPUT QUEUE NAME'      04100000
                 SET WS-CONTROL-CARD-ERROR TO TRUE                      04110000
                 CALL 'ILBOABN0' USING WS-ABEND-CODE.                   04120000
                                                                        04130000
      ***************************************************************** 04140000
      *    CONNECT TO THE QUEUE MANAGER                                 04150000
      ***************************************************************** 04160000
       0220-MQ-CONNECT-QMGR.                                            04170000
           CALL WS-MQCONN USING CC-OUTPUT-QMGR-NAME                     04180000
                                PV-HCONN                                04190000
                                PV-COMPLETION-CODE                      04200000
                                PV-REASON.                              04210000
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE              04220000
      *    AND EXIT                                                     04230000
           MOVE PV-REASON TO PV-CON-REASON.                             04240000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04250000
               DISPLAY 'MQCONN ERROR: ' CC-OUTPUT-QMGR-NAME             04260000
               SET PS-MQ-NOT-CONNECTED        TO TRUE                   04270000
               MOVE '0220-MQ-CONNECT-QMGR'   TO PV-PARAGRAPH-NAME       04280000
               MOVE 'ERROR WHEN CONNECTING ' TO PV-ERROR-MESSAGE        04290000
               PERFORM 99998-MQ-ABEND                                   04300000
           ELSE                                                         04310000
               SET PS-MQ-CONNECTED        TO TRUE                       04320000
               DISPLAY 'MQCONN SUCCESSFUL TO ', CC-OUTPUT-QMGR-NAME     04330000
           END-IF.                                                      04340000
                                                                        04350000
      ******************************************************************04360000
      * OPEN THE UPDATE QUEUE                                           04370000
      ******************************************************************04380000
       0230-MQ-OPEN-QUEUE.                                              04390000
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                      04400000
                                     MQOO-FAIL-IF-QUIESCING +           04410003
                                     MQOO-SET-IDENTITY-CONTEXT          04420003
           MOVE CC-OUTPUT-QUEUE-NAME-API TO MQOD-OBJECTNAME.            04430000
           MOVE CC-OUTPUT-QMGR-NAME-API TO MQOD-OBJECTQMGRNAME.         04440000
      *                                                                 04450000
           CALL WS-MQOPEN USING PV-HCONN                                04460000
                                MQM-OBJECT-DESCRIPTOR                   04470000
                                PV-OPEN-OPTIONS                         04480000
                                PV-PQ-HANDLE                            04490000
                                PV-COMPLETION-CODE                      04500000
                                PV-REASON.                              04510000
      *                                                                 04520000
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    04530000
      *    AND EXIT.                                                    04540000
      *                                                                 04550000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04560000
              DISPLAY 'MQOPEN ERROR: ' CC-OUTPUT-QUEUE-NAME-API         04570000
              SET PS-MQ-NOT-OPENED          TO TRUE                     04580000
              MOVE '0230-MQ-OPEN-QUEUE'   TO PV-PARAGRAPH-NAME          04590000
              MOVE 'ERROR WHEN OPENING' TO PV-ERROR-MESSAGE             04600000
              PERFORM 99998-MQ-ABEND                                    04610000
           ELSE                                                         04620000
              SET PS-MQ-OPENED              TO TRUE                     04630000
              DISPLAY 'MQOPEN SUCCESSFUL FOR ' , MQOD-OBJECTQMGRNAME    04640000
              DISPLAY '                      ' , MQOD-OBJECTNAME        04650000
           END-IF.                                                      04660000
                                                                        04670000
      ******************************************************************04680000
      *  CLOSE THE OUTPUT MESSAGE QUEUE.                               *04690000
      ******************************************************************04700000
       0250-MQ-CLOSE-QUEUE.                                             04710000
           CALL WS-MQCLOSE USING PV-HCONN                               04720000
                                 PV-PQ-HANDLE                           04730000
                                 MQCO-NONE                              04740000
                                 PV-COMPLETION-CODE                     04750000
                                 PV-REASON.                             04760000
                                                                        04770000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   04780000
               DISPLAY 'MQCLOSE ERROR: ' CC-OUTPUT-QUEUE-NAME-API       04790000
               MOVE '0250-MQ-CLOSE-QUEUE'   TO PV-PARAGRAPH-NAME        04800000
               MOVE 'ERROR WHEN CLOSING' TO PV-ERROR-MESSAGE            04810000
               PERFORM 99998-MQ-ABEND                                   04820000
           ELSE                                                         04830000
              DISPLAY 'SUCCESSFUL CLOSE FOR OUTPUT QUEUE'               04840000
           END-IF.                                                      04850000
                                                                        04860000
      ******************************************************************04870000
      *  DISCONNECT FROM THE MQ QUEUE MANAGER                          *04880000
      ******************************************************************04890000
       0260-MQ-DISCONNECT-QMGR.                                         04900000
           IF PV-CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED      04910000
              CALL WS-MQDISC USING PV-HCONN                             04920000
                                   PV-COMPLETION-CODE                   04930000
                                   PV-REASON                            04940000
               IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN               04950000
                   DISPLAY 'MQDISC ERROR: ' CC-OUTPUT-QMGR-NAME         04960000
                   MOVE '0260-MQ-DISCONNECT-QMGR'  TO PV-PARAGRAPH-NAME 04970000
                   MOVE 'ERROR WHEN DISCONNECTING' TO PV-ERROR-MESSAGE  04980000
                   PERFORM 99998-MQ-ABEND                               04990000
               ELSE                                                     05000000
                   DISPLAY 'MQDISC SUCCESSFUL TO ', CC-OUTPUT-QMGR-NAME 05010000
               END-IF                                                   05020000
           ELSE                                                         05030000
               DISPLAY 'MQDISC SUCCESSFUL TO ', CC-OUTPUT-QMGR-NAME     05040000
           END-IF.                                                      05050000
      ******************************************************************05060000
      * PROCESS AN INPUT MESSAGE                                       *05070000
      ******************************************************************05080000
       1000-PROCESS-INPUT.                                              05090000
           IF PS-FIRST-MESSAGE                                          05100000
              MOVE PO033-PO-NUMBER TO PV-HOLD-PO-NUMBER                 05110000
              MOVE PO033-DC        TO PV-HOLD-DC                        05120000
              MOVE PO033-SKU       TO PV-HOLD-SKU                       05130000
              SET PS-NOT-FIRST-MESSAGE TO TRUE                          05140000
           END-IF.                                                      05150000
                                                                        05160000
           IF (PO033-PO-NUMBER NOT EQUAL PV-HOLD-PO-NUMBER) OR          05170000
              (PO033-DC        NOT EQUAL PV-HOLD-DC)        OR          05180000
              (PO033-SKU       NOT EQUAL PV-HOLD-SKU)                   05190000
              PERFORM 1200-SEND-DC-MESSAGE                              05200000
              MOVE PO033-PO-NUMBER TO PV-HOLD-PO-NUMBER                 05210000
              MOVE PO033-DC        TO PV-HOLD-DC                        05220000
              MOVE PO033-SKU       TO PV-HOLD-SKU                       05230000
           END-IF.                                                      05240000
                                                                        05250000
           PERFORM 1100-SEND-STORE-MESSAGE.                             05260000
                                                                        05270000
           PERFORM 9000-READ-INPUT-FILE.                                05280000
      ******************************************************************05290000
      * CREATE AND SEND A STORE MESSAGE TO THE UPDATE API.             *05300000
      ******************************************************************05310000
       1100-SEND-STORE-MESSAGE.                                         05320000
           MOVE PO033-DC            TO PV-DC-NBR-X.                     05330000
           MOVE PO033-LOCATION      TO PV-STORE-NBR-X.                  05340000
C98147     MOVE PO033-PO-NUMBER     TO PV-PO-NBR-X.                     05350008
           MOVE PO033-QTY-ADJUSTED  TO PV-ADJUSTED-QTY-X.               05360000
           SET UAM-AR-STORE-MESSAGE TO TRUE.                            05370000
           MOVE PO033-SKU           TO UAM-AR-SKU.                      05380000
           MOVE PV-STORE-X          TO UAM-AR-STORE.                    05390000
           MOVE PV-DC-X             TO UAM-AR-DC.                       05400000
           MOVE PV-PO-NBR-X         TO UAM-PO-NUMBER.                   05410000
           SET UAM-STORE-MESSAGE    TO TRUE.                            05420000
           MOVE PV-DC-X             TO UAM-DC.                          05430000
           MOVE PO033-SKU           TO UAM-SKU.                         05440000
           MOVE PV-STORE-X          TO UAM-STORE.                       05450000
           MOVE PV-ADJUSTED-QTY-X   TO UAM-ADJUSTED-QTY.                05460000
                                                                        05470000
           PERFORM 5000-PUT-MQ-MESSAGE.                                 05480000
      ******************************************************************05490000
      * CREATE AND SEND A DC MESSAGE TO THE UPDATE API.                *05500000
      ******************************************************************05510000
       1200-SEND-DC-MESSAGE.                                            05520000
           MOVE PV-HOLD-DC          TO PV-DC-NBR-X.                     05530000
C98147     MOVE PV-HOLD-PO-NUMBER   TO PV-PO-NBR-X.                     05540008
           SET UAM-AR-DC-MESSAGE    TO TRUE.                            05550000
           MOVE PV-HOLD-SKU         TO UAM-AR-SKU.                      05560000
           MOVE PC-ZERO-LOC         TO UAM-AR-STORE.                    05570000
           MOVE PV-DC-X             TO UAM-AR-DC.                       05580000
           MOVE PV-PO-NBR-X         TO UAM-PO-NUMBER.                   05590000
           SET UAM-DC-MESSAGE       TO TRUE.                            05600000
           MOVE PV-DC-X             TO UAM-DC.                          05610000
           MOVE PV-HOLD-SKU         TO UAM-SKU.                         05620000
           MOVE PC-ZERO-LOC         TO UAM-STORE.                       05630000
           MOVE PC-ZERO-QTY         TO UAM-ADJUSTED-QTY.                05640000
                                                                        05650000
           PERFORM 5000-PUT-MQ-MESSAGE.                                 05660000
      ******************************************************************05670000
      * SETUP THE MQPUT AND CALL THE NECESSARY FUNCTION TO DO THE PUT  *05680000
      ******************************************************************05690000
       5000-PUT-MQ-MESSAGE.                                             05700000
           INITIALIZE PS-MQPUT-SUCCESSFUL-SW                            05710000
                      PV-MQPUT-RETRY-COUNTER                            05720000
                      PV-MQ-OUTPUT-RECORD.                              05730000
                                                                        05740000
           MOVE LENGTH OF WS-UPDATE-API-MESSAGE TO PV-MSGBUFFER-LENGTH. 05750000
           MOVE CC-OUTPUT-QMGR-NAME-API         TO MQOD-OBJECTQMGRNAME. 05760000
           MOVE CC-OUTPUT-QUEUE-NAME-API        TO MQOD-OBJECTNAME.     05770000
           MOVE MQMI-NONE                       TO MQMD-MSGID.          05780000
           MOVE MQFMT-STRING                    TO MQMD-FORMAT.         05790000
           MOVE MQPER-PERSISTENT                TO MQMD-PERSISTENCE.    05800000
           MOVE PC-ED870-APPL-ID               TO MQMD-APPLIDENTITYDATA.05810001
           COMPUTE MQPMO-OPTIONS =  MQPMO-SYNCPOINT +                   05820002
                                    MQPMO-SET-IDENTITY-CONTEXT          05830002
                                                                        05840000
           INITIALIZE PS-MQPUT-SUCCESSFUL-SW.                           05850000
                                                                        05860000
           PERFORM 5100-MQ-PUT-API-MSG                                  05870000
             UNTIL PS-MQPUT-SUCCESSFUL                                  05880000
                OR PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES.             05890000
           IF PS-MQPUT-SUCCESSFUL                                       05900000
             CONTINUE                                                   05910000
           ELSE                                                         05920000
              SET PS-MQPUT-ERROR        TO TRUE                         05930000
              MOVE PV-COMPLETION-CODE      TO PV-SAVE-COMPLETION-CODE   05940000
              MOVE PV-REASON               TO PV-SAVE-REASON            05950000
              PERFORM 5300-BACK-OUT-MQ                                  05960000
              MOVE PV-SAVE-COMPLETION-CODE TO PV-COMPLETION-CODE        05970000
              MOVE PV-SAVE-REASON          TO PV-REASON                 05980000
              MOVE 'MQ PUT ERROR:'       TO PV-ERROR-LITERAL            05990000
              MOVE CC-OUTPUT-QMGR-NAME   TO PV-ERROR-VARIABLE           06000000
              MOVE PV-ERROR              TO PV-ERROR-MESSAGE            06010000
              MOVE 'PV-COMPLETION-CODE ' TO PV-ERROR-LITERAL            06020000
              MOVE PV-COMPLETION-CODE    TO PV-ERROR-VARIABLE           06030000
              MOVE PV-ERROR              TO PV-ERROR-MESSAGE2           06040000
              MOVE '5100-PUT-MQ-MSG'     TO PV-PARAGRAPH-NAME           06050000
              PERFORM 99998-MQ-ABEND                                    06060000
           END-IF.                                                      06070000
                                                                        06080000
      ******************************************************************06090000
      * DO THE ACTUAL MQPUT                                            *06100000
      ******************************************************************06110000
       5100-MQ-PUT-API-MSG.                                             06120000
           MOVE WS-UPDATE-API-MESSAGE TO PV-STRING-CLAUSE.              06130000
           MOVE MQCC-OK TO PV-COMPLETION-CODE.                          06140000
           CALL WS-MQPUT USING PV-HCONN                                 06150000
                               PV-PQ-HANDLE                             06160000
                               MQM-MESSAGE-DESCRIPTOR                   06170000
                               MQM-PUT-MESSAGE-OPTIONS                  06180000
                               PV-MSGBUFFER-LENGTH                      06190000
                               PV-STRING-CLAUSE                         06200000
                               PV-COMPLETION-CODE                       06210000
                               PV-REASON.                               06220000
                                                                        06230000
      *        IF PUT FAILED THEN DISPLAY ERROR MESSAGE                 06240000
      *        AND BREAK OUT OF LOOP                                    06250000
           IF (PV-COMPLETION-CODE = MQCC-OK)                            06260000
             SET PS-MQPUT-SUCCESSFUL TO TRUE                            06270000
             ADD 1 TO PV-PUT-COUNT                                      06280000
             ADD 1 TO PV-MQPUT-COUNTER-COMMIT                           06290000
             ADD 1 TO PV-MQPUT-COUNTER-TOTAL                            06300000
             IF (PV-MQPUT-COUNTER-COMMIT >                              06310000
                 TCKRSTCNTL-CKPT-FRQNCY-QTY) AND                        06320000
                (UAM-AR-DC-MESSAGE)                                     06330000
                MOVE PV-870-MSGS-READ TO CKR-MESSAGES-READ              06340000
                INITIALIZE PV-MQPUT-COUNTER-COMMIT                      06350000
                PERFORM 6000-COMMIT-PROCESSING                          06360000
             END-IF                                                     06370000
           ELSE                                                         06380000
             IF PV-REASON = MQRC-PAGESET-FULL OR MQRC-Q-FULL            06390000
               ADD 1 TO PV-MQPUT-RETRY-COUNTER                          06400000
               IF PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES               06410000
                 CONTINUE                                               06420000
               ELSE                                                     06430000
      **         WAIT FOR 5 SECONDS AND TRY AGAIN                       06440000
                 CALL WS-SYSWAIT USING PC-DELAY-INTERVAL                06450000
               END-IF                                                   06460000
             ELSE                                                       06470000
               COMPUTE PV-MQPUT-RETRY-COUNTER = PC-MAX-RETRIES + 1      06480000
             END-IF                                                     06490000
           END-IF.                                                      06500000
      ******************************************************************06510000
      *  COMMIT MQ-API PROCESSING SINCE THE POINT OF THE LAST COMMIT   *06520000
      ******************************************************************06530000
       5200-COMMIT-MQ-MESSAGE.                                          06540000
                                                                        06550000
           CALL WS-MQCMIT USING PV-HCONN                                06560000
                                PV-COMPLETION-CODE                      06570000
                                PV-REASON.                              06580000
                                                                        06590000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   06600000
              MOVE '5200-COMMIT-MQ-MESSAGE' TO PV-PARAGRAPH-NAME        06610000
              MOVE 'MQCMIT ERROR'          TO PV-ERROR-MESSAGE          06620000
              MOVE SPACES                  TO PV-ERROR-MESSAGE2         06630000
              MOVE PV-COMPLETION-CODE      TO PV-SAVE-COMPLETION-CODE   06640000
              MOVE PV-REASON               TO PV-SAVE-REASON            06650000
              PERFORM 5300-BACK-OUT-MQ                                  06660000
              MOVE PV-SAVE-COMPLETION-CODE TO PV-COMPLETION-CODE        06670000
              MOVE PV-SAVE-REASON          TO PV-REASON                 06680000
              PERFORM 99998-MQ-ABEND                                    06690000
           END-IF.                                                      06700000
                                                                        06710000
      ******************************************************************06720000
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT      *06730000
      ******************************************************************06740000
       5300-BACK-OUT-MQ.                                                06750000
                                                                        06760000
           CALL WS-MQBACK USING PV-HCONN                                06770000
                                PV-COMPLETION-CODE                      06780000
                                PV-REASON.                              06790000
                                                                        06800000
           IF (PV-COMPLETION-CODE NOT = MQCC-OK) THEN                   06810000
              MOVE '5300-BACK-OUT-MQ' TO PV-PARAGRAPH-NAME              06820000
              MOVE 'MQBACK ERROR'      TO PV-ERROR-MESSAGE              06830000
              MOVE SPACES              TO PV-ERROR-MESSAGE2             06840000
              PERFORM 99998-MQ-ABEND                                    06850000
           ELSE                                                         06860000
              DISPLAY 'BACKOUT SUCCESSFUL'                              06870000
           END-IF.                                                      06880000
      *                                                                 06890000
      ******************************************************************06900000
      * 6000-COMMIT-PROCESSING.                                        *06910000
      * COMPARE COMMIT TIMESTAMP TO CURRENT TO DETERMINE IF COMMIT REQD*06920000
      ******************************************************************06930000
       6000-COMMIT-PROCESSING.                                          06940000
           MOVE '*6000-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06950000
           PERFORM 5200-COMMIT-MQ-MESSAGE                               06960000
           MOVE PO033-ORDER-QTY-UPDATE-RECORD                           06970000
                                        TO CKR-LAST-VALID-RECORD        06980000
           MOVE PV-870-MSGS-READ        TO CKR-MESSAGES-READ            06990000
           MOVE PV-MQPUT-COUNTER-TOTAL  TO CKR-TOT-PUT-REC-COUNT        07000000
      * MOVE COMMIT INFO TO WORKING STORAGE ROW                         07010000
           MOVE CHECKPOINT-RESTART-INFO TO TCKRSTINF-WORK-STRG-DESC     07020000
           SET PS-COMMIT-TAKEN TO TRUE                                  07030000
           IF PS-FIRST-COMMIT                                           07040000
              MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                     07050000
              PERFORM 7200-UPDATE-TCKRSTCNTL                            07060000
              MOVE 'N' TO PS-FIRST-COMMIT-SW                            07070000
           END-IF.                                                      07080000
           PERFORM 6100-COMMIT-CHANGES.                                 07090000
           DISPLAY 'COMMIT SUCCESSFUL'.                                 07100000
      *                                                                 07110000
      ******************************************************************07120000
      * 6100-COMMIT-CHANGES.                                            07130000
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.             07140000
      ******************************************************************07150000
       6100-COMMIT-CHANGES.                                             07160000
      *                                                                 07170000
           EXEC SQL                                                     07180000
             UPDATE TCKRSTINF                                           07190000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07200000
              WHERE PLAN_NAME      = :PC-PROGRAM-NAME                   07210000
           END-EXEC.                                                    07220000
      *                                                                 07230000
           EVALUATE TRUE                                                07240000
               WHEN SQLWARN0 NOT EQUAL SPACE                            07250000
               WHEN SQLCODE NOT EQUAL ZERO                              07260000
                   DISPLAY '** ABEND **'                                07270000
                   DISPLAY '** PROGRAM = POKUP052 **'                   07280000
                   DISPLAY '** PARAGRAPH = 6100-COMMIT-CHANGES**'       07290000
                   DISPLAY '** UPDATING RESTART INFORMATION*'           07300000
                   PERFORM 9100-SQL-ABEND                               07310000
           END-EVALUATE.                                                07320000
      *                                                                 07330000
           EXEC SQL                                                     07340000
               COMMIT                                                   07350000
           END-EXEC.                                                    07360000
      *                                                                 07370000
           EVALUATE TRUE                                                07380000
               WHEN SQLWARN0 NOT EQUAL SPACE                            07390000
               WHEN SQLCODE NOT EQUAL ZERO                              07400000
                   DISPLAY '** ABEND **'                                07410000
                   DISPLAY '** PROGRAM = POKUP052 **'                   07420000
                   DISPLAY '** PARAGRAPH = 6100-COMMIT-CHANGES**'       07430000
                   DISPLAY '** COMMITTING RESTART INFORMATION*'         07440000
                   PERFORM 9100-SQL-ABEND                               07450000
           END-EVALUATE.                                                07460000
                                                                        07470000
645131******************************************************************07480000
645132*    SELECT CHECKPOINT RESTART CONTROL INFO.                     *07490000
645133******************************************************************07500000
645134 7000-GET-CHECKPOINT-CNTL.                                        07510000
645135                                                                  07520000
645136     EXEC SQL                                                     07530000
645137      SELECT CKPT_STAT_CODE                                       07540000
                  ,CKPT_FRQNCY_QTY                                      07550000
645138       INTO :PV-CKPT-STAT-CODE                                    07560000
                 ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                           07570000
645139       FROM TCKRSTCNTL                                            07580000
645140       WHERE PLAN_NAME = :PC-PROGRAM-NAME                         07590000
645141     END-EXEC.                                                    07600000
645142                                                                  07610000
645143                                                                  07620000
645144     EVALUATE TRUE                                                07630000
645145         WHEN SQLWARN0 NOT EQUAL SPACE                            07640000
645146         WHEN SQLCODE NOT EQUAL ZERO                              07650000
645147             DISPLAY '** ABEND **'                                07660000
645148             DISPLAY '** PROGRAM = POKUP052 **'                   07670000
645149             DISPLAY '** PARAGRAPH = 7000-GET-CHECKPOINT-CNTL**'  07680000
645150             DISPLAY '** CHECKING IF RUN IS NORMAL OR RESTART*'   07690000
645151             PERFORM 9100-SQL-ABEND                               07700000
645152     END-EVALUATE.                                                07710000
645153                                                                  07720000
645154 EJECT                                                            07730000
645173******************************************************************07740000
645174*    SELECT CHECKPOINT RESTART WORKING STORAGE INFORMATION.      *07750000
645175******************************************************************07760000
645176 7100-GET-CHECKPOINT-INFO.                                        07770000
645177                                                                  07780000
645178     EXEC SQL                                                     07790000
645179      SELECT WORK_STRG_DESC                                       07800000
645180       INTO :TCKRSTINF-WORK-STRG-DESC                             07810000
645181       FROM TCKRSTINF                                             07820000
645182      WHERE PLAN_NAME = :PC-PROGRAM-NAME                          07830000
645183     END-EXEC.                                                    07840000
645184                                                                  07850000
645185     EVALUATE TRUE                                                07860000
645186         WHEN SQLWARN0 NOT EQUAL SPACE                            07870000
645187         WHEN SQLCODE NOT EQUAL ZERO                              07880000
645188             DISPLAY '** ABEND **'                                07890000
645189             DISPLAY '** PROGRAM = POKUP052 **'                   07900000
645190             DISPLAY '** PARAGRAPH = 7100-GET-CHECKPOINT-INFO**'  07910000
645191             DISPLAY '** GETTING RESTART WORKING STORAGE INFO**'  07920000
645192             PERFORM 9100-SQL-ABEND                               07930000
645193     END-EVALUATE.                                                07940000
645194                                                                  07950000
645195 EJECT                                                            07960000
645196******************************************************************07970000
645197*    UPDATES THE CHECKPOINT RESTART CONTROL TABLE.  THE STATUS   *07980000
645198*    CODE IS SET TO '9' (RUN IN PROGRESS) OR '0' (RUN COMPLETE). *07990000
645199******************************************************************08000000
645200 7200-UPDATE-TCKRSTCNTL.                                          08010000
645202     EXEC SQL                                                     08020000
645203       UPDATE TCKRSTCNTL                                          08030000
645204       SET CKPT_STAT_CODE   = :TCKRSTCNTL-CKPT-STAT-CODE          08040000
645205       WHERE PLAN_NAME      = :PC-PROGRAM-NAME                    08050000
645206     END-EXEC.                                                    08060000
645207                                                                  08070000
645208     EVALUATE TRUE                                                08080000
645209         WHEN SQLWARN0 NOT EQUAL SPACE                            08090000
645210         WHEN SQLCODE NOT EQUAL ZERO                              08100000
645211             DISPLAY '** ABEND **'                                08110000
645212             DISPLAY '** PROGRAM = POKUP052 **'                   08120000
645213             DISPLAY '** PARAGRAPH = 7200-UPDATE-TCKRSTCNTL**'    08130000
645214             DISPLAY '** GETTING RESTART INFORMATION AFTER ABEND*'08140000
645215             PERFORM 9100-SQL-ABEND                               08150000
645216     END-EVALUATE.                                                08160000
645217                                                                  08170000
645218 EJECT                                                            08180000
      ******************************************************************08190000
      * READ THE NEXT RECORD FROM THE INPUT                            *08200000
      ******************************************************************08210000
       9000-READ-INPUT-FILE.                                            08220000
           READ INPUT-FILE INTO PO033-ORDER-QTY-UPDATE-RECORD           08230000
                AT END  SET PS-NO-MORE-INPUT TO TRUE.                   08240000
           IF NOT PS-NO-MORE-INPUT                                      08250000
              ADD 1 TO PV-870-MSGS-READ                                 08260000
           END-IF.                                                      08270000
                                                                        08280000
646488 9100-SQL-ABEND.                                                  08290000
646489******************************************************************08300000
646490* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    08310000
646491* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         08320000
646492* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     08330000
646493* FORMAT.                                                         08340000
646494******************************************************************08350000
           PERFORM 5300-BACK-OUT-MQ.                                    08360000
646496     COPY DPPD004.                                                08370000
646497                                                                  08380000
646498     MOVE +4013 TO WS-ABEND-CODE.                                 08390000
646499     CALL 'ILBOABN0' USING WS-ABEND-CODE.                         08400000
                                                                        08410000
      ******************************************************************08420000
      * DO THE NECCESARY PROCESSING TO ABEND BECAUSE OF AN MQ ERROR    *08430000
      ******************************************************************08440000
       99998-MQ-ABEND.                                                  08450000
           SET WS-MQ-ERROR TO TRUE.                                     08460000
           CALL WS-MQCHECK USING PV-REASON                              08470000
                                 PV-MQ-REASON-TEXT.                     08480000
                                                                        08490000
           DISPLAY '                 '.                                 08500000
                                                                        08510000
           DISPLAY '**** ABEND *************************************'.  08520000
           DISPLAY '** MQSERIES ERROR **'.                              08530000
           DISPLAY '** PROGRAM:    ', PC-PROGRAM-NAME.                  08540000
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH-NAME.                08550000
           DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE.                 08560000
           IF PV-ERROR-MESSAGE2 NOT EQUAL SPACES                        08570000
               DISPLAY '** MESSAGE:    ', PV-ERROR-MESSAGE2             08580000
           END-IF.                                                      08590000
           DISPLAY '** COMP CODE:  ', PV-COMPLETION-CODE.               08600000
           DISPLAY '** RSN CODE:   ', PV-REASON.                        08610000
           DISPLAY '** RSN TEXT:   ', PV-MQ-REASON-TEXT.                08620000
           DISPLAY '************************************************'.  08630000
           CALL 'ILBOABN0' USING WS-ABEND-CODE.                         08640000
