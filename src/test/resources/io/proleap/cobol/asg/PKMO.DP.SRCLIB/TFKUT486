      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             TFKUT486.                                00050000
       AUTHOR.                 JACK KRAMER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            DECEMBER 96.                             00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *  THIS PROGRAM WILL EXTRACT TRANSFER DATA FOR THE SPECIFIED      00120015
      * PERIOD FOR TRANSFERS THAT WERE MISDIRECTED - RECEIVED AT A STORE00130015
      * OTHER THAN THE TRANSFER 'TO' STORE. DATA IS WRITTEN TO AN       00140000
      * OUTPUT SEQUENTIAL FILE AND PASSED TO A SORT AND REPORT PROGRAM. 00141000
      ******************************************************************00150000
100404** 10/04/2004 CHANGED BY: RAY GRAF                                        
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                        
      *****************************************************************         
022712** 02/27/2012 CHANGED BY: CHERI PARMLEY            PRJ#3403               
022712**     COMPILE FOR CHANGES TO TABLE TTFITEM                               
      *****************************************************************         
                                                                        00160000
      ******************************************************************00170000
       ENVIRONMENT DIVISION.                                            00180000
      ******************************************************************00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
                                                                        00220000
       SOURCE-COMPUTER.        IBM-3090.                                00230000
       OBJECT-COMPUTER.        IBM-3090.                                00240000
                                                                        00250000
       INPUT-OUTPUT SECTION.                                            00260000
                                                                        00270000
       FILE-CONTROL.                                                    00280000
                                                                        00280115
           SELECT PROCESS-DATES-FILE                                    00281015
               ASSIGN TO INP01.                                         00290015
                                                                        00293000
           SELECT MISDIRECT-FILE                                        00294000
               ASSIGN TO OUT01                                          00295000
               FILE STATUS IS PV-FILE-STATUS.                           00296000
      *                                                                 00350000
      ******************************************************************00360000
       DATA DIVISION.                                                   00370000
      ******************************************************************00380000
                                                                        00390000
       FILE SECTION.                                                    00400000
                                                                        00400115
       FD  PROCESS-DATES-FILE                                           00400215
           RECORDING MODE IS F                                          00400315
           LABEL RECORDS ARE STANDARD                                   00400415
           BLOCK CONTAINS 0 RECORDS.                                    00400515
                                                                        00401015
       01  PROCESS-DATES-RECORD                PIC  X(16).              00410015
                                                                        00480000
       FD  MISDIRECT-FILE                                               00490000
           LABEL RECORDS ARE STANDARD                                   00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS                                     00520000
           RECORD CONTAINS 60 CHARACTERS                                00530013
           DATA RECORD IS MISDIRECT-FILE-RECORD.                        00540000
                                                                        00540111
       01  MISDIRECT-FILE-RECORD               PIC  X(60).              00540213
                                                                        00550000
       WORKING-STORAGE SECTION.                                         00760000
                                                                        00770000
      *                                                                 00780000
      *---------------------------------------------------------------* 00790000
      * CALENDAR ROUTINE WORK AREA.                                     00800000
      *---------------------------------------------------------------* 00810000
           COPY DPWS500.                                                00820000
      *                                                                         
      *  RECORD LAYOUT FOR THE PROCESS DATES FILE                               
      *                                                                         
           COPY TFRD029.                                                        
                                                                        00971000
      *---------------------------------------------------------------* 00972000
      * MISDIRECT TRANSFER RECORD                                       00973000
      *---------------------------------------------------------------* 00974000
           COPY TFRD032.                                                00975000
                                                                        01030000
       01  CD-CURRENT-DATE.                                                     
           05  CD-CURRENT-YEAR                 PIC  9(02)  VALUE ZEROES.        
           05  CD-CURRENT-MONTH                PIC  9(02)  VALUE ZEROES.        
           05  CD-CURRENT-DAY                  PIC  9(02)  VALUE ZEROES.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                         01040000
           05  PS-MORE-TRANSFER-SW     PIC X(01)    VALUE 'Y'.          01200000
               88  PS-MORE-TRANSFERS                VALUE 'Y'.          01210000
               88  PS-NO-MORE-TRANSFERS             VALUE 'N'.          01220000
                                                                        01290000
       01  PC-PROGRAM-CONSTANTS.                                        01300000
           05  PC-MAX-RETRIES          PIC S9(4)    VALUE +3            01310000
                                       COMP SYNC.                       01320000
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'TFKUT486'.   01350000
                                                                        01500000
       01  PV-PROGRAM-VARIABLES.                                        01510000
           05  PV-FILE-STATUS          PIC X(02)  VALUE '00'.           01520000
100404*    05  PV-DC                   PIC X(03)  VALUE SPACE.          01530011
100404     05  PV-DC                   PIC 9(05)  VALUE ZEROS.          01530011
           05  PV-DB2-ISO-DATE         PIC X(10)   VALUE SPACE.         01600000
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          01670000
                                       COMP SYNC.                       01680000
           05  PV-PARAGRAPH-NAME       PIC X(30)   VALUE SPACE.         01680111
           05  PV-DB2-OPERATION        PIC X(30)   VALUE SPACE.         01680200
           05  PV-XFER-QTY             PIC S9(07)    VALUE +0 COMP-3.   01680300
           05  PV-XFER-PRICE           PIC S9(09)V99 VALUE +0 COMP-3.   01680400
           05  PV-XFER-QTY-IND         PIC S9(04)  VALUE +0 COMP SYNC.  01680500
           05  PV-XFER-PRICE-IND       PIC S9(04)  VALUE +0 COMP SYNC.  01680600
           05  PV-DATE.                                                 01680713
               10  PV-DATE-CC          PIC X(02).                       01680813
               10  PV-DATE-YY          PIC X(02).                       01680913
               10                      PIC X(01).                       01681013
               10  PV-DATE-MM          PIC X(02).                       01681113
               10                      PIC X(01).                       01681213
               10  PV-DATE-DD          PIC X(02).                       01681313
           05  PV-BEG-TIMESTAMP.                                        01681413
               10  PV-BEG-CC                   PIC  X(02)  VALUE SPACE. 01410201
               10  PV-BEG-YY                   PIC  X(02)  VALUE SPACE. 01410201
               10  FILLER                      PIC  X(01)  VALUE '-'.   01410201
               10  PV-BEG-MM                   PIC  X(02)  VALUE SPACE. 01410201
               10  FILLER                      PIC  X(01)  VALUE '-'.   01410201
               10  PV-BEG-DD                   PIC  X(02)  VALUE SPACE. 01410201
               10                      PIC X(16) VALUE                  01681613
                                       '-00.00.00.000000'.              01681713
           05  PV-BEG-TMST REDEFINES                                    01681813
                 PV-BEG-TIMESTAMP      PIC X(26).                       01681913
           05  PV-END-TIMESTAMP.                                        01682013
               10  PV-END-CC                   PIC  X(02)  VALUE SPACE. 01410201
               10  PV-END-YY                   PIC  X(02)  VALUE SPACE. 01410201
               10  FILLER                      PIC  X(01)  VALUE '-'.   01410201
               10  PV-END-MM                   PIC  X(02)  VALUE SPACE. 01410201
               10  FILLER                      PIC  X(01)  VALUE '-'.   01410201
               10  PV-END-DD                   PIC  X(02)  VALUE SPACE. 01410201
               10                      PIC X(16) VALUE                  01682213
                                       '-23.59.59.999999'.              01682313
           05  PV-END-TMST REDEFINES                                    01682413
                 PV-END-TIMESTAMP      PIC X(26).                       01683013
      *---------------------------------------------------------------* 02350000
      *    DB2 AREA FOR TTFMSTR (TRANSFER TABLE)                        02360000
      *---------------------------------------------------------------* 02370000
           EXEC SQL                                                     02380000
                INCLUDE TTFMSTR                                         02390000
           END-EXEC.                                                    02400000
      *---------------------------------------------------------------* 02401000
      *    DB2 AREA FOR TTFSTAT (TRANSFER STATUS TABLE)                 02402000
      *---------------------------------------------------------------* 02403000
           EXEC SQL                                                     02404000
                INCLUDE TTFSTAT                                         02405000
           END-EXEC.                                                    02406000
      *---------------------------------------------------------------* 02407000
      *    DB2 AREA FOR TTFRCPT (TRANSFER RECEIPT TABLE)                02408000
      *---------------------------------------------------------------* 02409000
           EXEC SQL                                                     02409100
                INCLUDE TTFRCPT                                         02409200
           END-EXEC.                                                    02409300
      *---------------------------------------------------------------* 02410000
      *    DB2 AREA FOR TTFITEM (TRANSFER ITEM TABLE)                   02420000
      *---------------------------------------------------------------* 02430000
           EXEC SQL                                                     02440000
                INCLUDE TTFITEM                                         02450000
           END-EXEC.                                                    02460000
      *---------------------------------------------------------------* 02710000
100404*    DB2 AREA FOR XST_LOC TABLE                                 * 02720000
      *---------------------------------------------------------------* 02730000
           EXEC SQL                                                     02740000
100404          INCLUDE XST00001                                        02750000
           END-EXEC.                                                    02760000
      *---------------------------------------------------------------* 02710000
100404*    DB2 AREA FOR XST_DC_LOC_ASGMT TABLE                        * 02720000
      *---------------------------------------------------------------* 02730000
           EXEC SQL                                                     02740000
100404          INCLUDE XST00020                                        02750000
           END-EXEC.                                                    02760000
      *---------------------------------------------------------------* 03020000
      *     CURSOR TO RETRIEVE MISDIRECTED TRANSFERS                  * 03030000
      *---------------------------------------------------------------* 03040000
           EXEC SQL                                                     03050000
                DECLARE TRANSFER-CSR CURSOR FOR                         03060000
                SELECT                                                  03070000
                      M.XFER_DKEY_ID                                    03080000
                     ,M.FR_STR_NBR                                      03081000
                     ,M.XFER_SEQ_NBR                                    03090000
                     ,M.CMPT_XFER_END_TMST                              03091000
                     ,M.TO_STR_NBR                                      03092000
                     ,R.STR_NBR                                         03093000
                     ,R.RCV_XFER_BEG_TMST                               03093113
                     ,S.XFER_STAT_CDE                                   03094011
                FROM                                                    03205000
                      TTFMSTR M                                         03206000
                     ,TTFRCPT R                                         03207000
                     ,TTFSTAT S                                         03208000
                WHERE M.CMPT_XFER_END_TMST BETWEEN :PV-BEG-TMST    AND  03208200
                                                   :PV-END-TMST    AND  03208300
                      M.XFER_DKEY_ID       = R.XFER_DKEY_ID        AND  03208401
                      M.XFER_DKEY_ID       = S.XFER_DKEY_ID        AND  03208500
                      R.RCV_XFER_BEG_TMST  = S.XFER_STAT_TMST      AND  03208600
                      S.XFER_STAT_CDE      IN ('IR', 'PR')         AND  03208700
                      M.TO_STR_NBR         ^= R.STR_NBR                 03208801
                ORDER BY                                                03209000
                      M.FR_STR_NBR                                      03209101
                     ,M.XFER_SEQ_NBR                                    03209201
                     ,S.XFER_STAT_CDE                                   03209301
                WITH UR                                                         
           END-EXEC.                                                    03209400
      *---------------------------------------------------------------* 03210700
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                03210800
      *---------------------------------------------------------------* 03210900
           EXEC SQL                                                     03211000
               INCLUDE SQLCA                                            03211100
           END-EXEC.                                                    03211200
      *---------------------------------------------------------------* 03211301
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           03211400
      *  SQL CODES FOR DISPLAY                                          03211500
      *---------------------------------------------------------------* 03211601
           COPY SQLCA2.                                                 03211800
                                                                        03211900
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          03212000
                                       COMP SYNC.                       03212100
      *                                                                 03212200
      ******************************************************************03212300
       PROCEDURE DIVISION.                                              03213000
      ******************************************************************03214000
       0000-MAINLINE.                                                   03215000
                                                                        03220000
           OPEN INPUT  PROCESS-DATES-FILE                               03230000
                OUTPUT MISDIRECT-FILE.                                  03231011
           PERFORM 7000-READ-PROCESS-DATES-FILE.                        01880203
           DISPLAY 'TF029-PROCESS-DATES-RECORD = '                      01880303
               TF029-PROCESS-DATES-RECORD.                              01880403
           MOVE TF029-PROCESS-START-MONTH    TO PV-BEG-MM.                      
           MOVE TF029-PROCESS-START-DAY      TO PV-BEG-DD.                      
           MOVE TF029-PROCESS-START-YEAR     TO PV-BEG-YY.                      
           MOVE TF029-PROCESS-START-CENTURY  TO PV-BEG-CC.                      
           MOVE TF029-PROCESS-END-MONTH      TO PV-END-MM.                      
           MOVE TF029-PROCESS-END-DAY        TO PV-END-DD.                      
           MOVE TF029-PROCESS-END-YEAR       TO PV-END-YY.                      
           MOVE TF029-PROCESS-END-CENTURY    TO PV-END-CC.                      
           PERFORM 1000-CALENDAR-ROUTINE.                               03270001
           PERFORM 1100-OPEN-XFER-CURSOR.                               03675311
           PERFORM 2000-READ-TRANSFERS.                                 03675411
           PERFORM 1200-CLOSE-XFER-CURSOR.                              03675511
                                                                        03675600
           CLOSE PROCESS-DATES-FILE                                     03675700
                 MISDIRECT-FILE.                                        03675800
           STOP RUN.                                                    03675900
                                                                        03676000
       1000-CALENDAR-ROUTINE.                                           03676101
                                                                        03676200
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03676300
           ACCEPT CD-CURRENT-DATE  FROM  DATE.                                  
           MOVE CD-CURRENT-MONTH     TO DPG52-GREG-DATE-MO.             04165001
           MOVE CD-CURRENT-DAY       TO DPG52-GREG-DATE-DY.             04165001
           MOVE CD-CURRENT-YEAR      TO DPG52-GREG-DATE-YR.             04165001
      *  CALL THE CALENDAR ROUTINE WITH CURRENT DATE TO FIND CURRENT    04163501
      *  DATE IN ISO FORMAT                                             04163601
           SET DPG51-ACTUAL-CALENDAR-ONLY                               04163811
               DPG51-DO-NOT-INCR-DECR-DATE                              04163901
               DPG52-LK-DTE-GREG        TO TRUE.                        04164001
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52          04220000
                                                   DPG53 DPG54          04230000
                                                   DPG55 DPG56.         04240000
           EVALUATE TRUE                                                04250000
               WHEN DPG54-DATE-VALID                                    04260000
                   MOVE DPG55-DB2-ISO-DATE-FMT  TO PV-DB2-ISO-DATE      04271011
               WHEN OTHER                                               04280000
                   DISPLAY '** ABEND **'                                04290000
                   DISPLAY '** PGM = ' PC-PROGRAM-NAME                  04300000
                   DISPLAY '** 1000-CALENDAR-ROUTINE - 1 **'            04310000
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  04320000
                   DISPLAY '** ERROR MESSAGE = '                        04330000
                           DPG54-ERROR-MESSAGE                          04340000
                   DISPLAY '** DPG51 = ' DPG51                          04350000
                   DISPLAY '** DPG52 = ' DPG52                          04360000
                   DISPLAY '** DPG53 = ' DPG53                          04370000
                   DISPLAY '** DPG54 = ' DPG54                          04380000
                   MOVE +4019 TO ABEND-CODE                             04390000
                   CALL 'ILBOABN0' USING ABEND-CODE                     04400000
           END-EVALUATE.                                                04410000
                                                                        04700111
      ******************************************************************04700200
      *  OPEN THE TRANSFER CURSOR.                                      04700300
      ******************************************************************04700400
       1100-OPEN-XFER-CURSOR.                                           04700500
           EXEC SQL                                                     04760011
                OPEN TRANSFER-CSR                                       04770000
           END-EXEC.                                                    04780011
                                                                        04790011
           EVALUATE TRUE                                                04800011
               WHEN SQLCODE = ZERO                                      04810011
                    CONTINUE                                            04820011
               WHEN OTHER                                               04830011
                   MOVE '1100-OPEN-XFER-CURSOR' TO PV-PARAGRAPH-NAME    04850011
                   MOVE 'OPEN TRANSFER CURSOR'   TO PV-DB2-OPERATION    04860011
                   PERFORM 9100-SQL-ERROR                               04870011
           END-EVALUATE.                                                04880011
      *************************************************************     04890011
      * CLOSE TRANSFER CURSOR                                           04900011
      *************************************************************     04910011
       1200-CLOSE-XFER-CURSOR.                                          04920011
           EXEC SQL                                                     04930011
                CLOSE TRANSFER-CSR                                      04940000
           END-EXEC.                                                    04950011
      ******************************************************************05220011
      * CONTROL THE PROCESSING OF THE TRANSFER CURSOR                   05230011
      ******************************************************************05240011
       2000-READ-TRANSFERS.                                             05250011
           PERFORM                                                      05260011
               UNTIL (PS-NO-MORE-TRANSFERS)                             05270011
                                                                        05280011
               PERFORM 2100-FETCH-TRANSFER                              05290011
                                                                        05300011
               IF  PS-MORE-TRANSFERS                                    05310011
                   PERFORM 3000-PROCESS-TRANSFER                        05330011
               END-IF                                                   05340011
           END-PERFORM.                                                 05350011
       EJECT                                                            05410011
      *----------------------------------------------------------------*05420011
      *    FETCH THE NEXT TRANSFER.                                     05430011
      *----------------------------------------------------------------*05440011
       2100-FETCH-TRANSFER.                                             05450011
           PERFORM                                                      05460011
               WITH TEST AFTER                                          05470011
               VARYING PV-RETRY-COUNT                                   05480011
               FROM 1 BY 1                                              05490011
               UNTIL PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES         05500011
                  OR SQLCODE = 0                                        05510011
                  OR SQLCODE = +100                                     05520011
               EXEC SQL                                                 05530011
                    FETCH TRANSFER-CSR                                  05540011
                    INTO  :TFMSTR-XFER-DKEY-ID                          05540111
                         ,:TFMSTR-FR-STR-NBR                            05540200
                         ,:TFMSTR-XFER-SEQ-NBR                          05540300
                         ,:TFMSTR-CMPT-XFER-END-TMST                    05540400
                         ,:TFMSTR-TO-STR-NBR                            05540500
                         ,:TFRCPT-STR-NBR                               05540600
                         ,:TFRCPT-RCV-XFER-BEG-TMST                     05540713
                         ,:TFSTAT-XFER-STAT-CDE                         05540813
               END-EXEC                                                 05650011
                                                                        05660011
               EVALUATE TRUE                                            05670011
                   WHEN SQLCODE = ZERO                                  05680011
                   WHEN SQLCODE = -904                                  05690011
                   WHEN SQLCODE = -913                                  05700011
                        CONTINUE                                        05710011
                   WHEN SQLCODE = +100                                  05720011
                        SET PS-NO-MORE-TRANSFERS  TO TRUE               05730011
                   WHEN OTHER                                           05740011
                        MOVE '2100-FETCH-TRANSFER'                      05760011
                                     TO  PV-PARAGRAPH-NAME              05770011
                        MOVE 'FETCH FROM TRANSFER CURSOR'               05780011
                                            TO PV-DB2-OPERATION         05790011
                       PERFORM 9100-SQL-ERROR                           05800011
               END-EVALUATE                                             05810011
           END-PERFORM.                                                 05820011
           IF  PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES               05830011
               MOVE '2100-FETCH-TRANSFER'   TO  PV-PARAGRAPH-NAME       05840011
               MOVE 'MAX RETRIES FETCHING FROM TRANSFER CURSOR'         05850011
                                            TO PV-DB2-OPERATION         05860011
               PERFORM 9100-SQL-ERROR                                   05870011
           END-IF.                                                      05880011
      *----------------------------------------------------------------*07342300
      *  CONTROL THE PROCESSING OF THE TRANSFER DATA.                  *07342400
      *----------------------------------------------------------------*07343000
       3000-PROCESS-TRANSFER.                                           07344011
           PERFORM 4000-READ-TRANSFER-ITEM.                             07440003
           PERFORM 5000-GET-DIST-FACILITY.                              07450011
           MOVE SPACES                  TO TF032-XFER-MISDIRECT-RECORD. 07460011
           MOVE TFMSTR-FR-STR-NBR       TO TF032-FROM-STR-NBR.          07470011
           MOVE TFMSTR-XFER-SEQ-NBR     TO TF032-XFER-SEQ-NBR.          07480011
100404*    MOVE PV-DC                   TO TF032-DIST-FACILITY-X.       07490011
100404     MOVE PV-DC                   TO TF032-DIST-FACILITY.         07490011
           MOVE TFMSTR-CMPT-XFER-END-TMST                               07500011
                                        TO PV-DATE.                     07510013
           MOVE PV-DATE-CC              TO TF032-COMPLETE-CC.           07511013
           MOVE PV-DATE-YY              TO TF032-COMPLETE-YY.           07512013
           MOVE PV-DATE-MM              TO TF032-COMPLETE-MM.           07513013
           MOVE PV-DATE-DD              TO TF032-COMPLETE-DD.           07514013
           MOVE TFRCPT-RCV-XFER-BEG-TMST                                07515013
                                        TO PV-DATE.                     07516013
           MOVE PV-DATE-CC              TO TF032-RECEIVE-CC.            07517013
           MOVE PV-DATE-YY              TO TF032-RECEIVE-YY.            07518013
           MOVE PV-DATE-MM              TO TF032-RECEIVE-MM.            07519013
           MOVE PV-DATE-DD              TO TF032-RECEIVE-DD.            07519113
           MOVE TFSTAT-XFER-STAT-CDE    TO TF032-XFER-STATUS.           07520011
           MOVE TFRCPT-STR-NBR          TO TF032-RECV-STR-NBR.          07530011
           MOVE TFMSTR-TO-STR-NBR       TO TF032-TO-STR-NBR.            07540011
           MOVE PV-XFER-QTY             TO TF032-XFER-TOT-UNITS.        07550011
           MOVE PV-XFER-PRICE           TO TF032-XFER-TOT-RETAIL.       07560011
           WRITE MISDIRECT-FILE-RECORD FROM TF032-XFER-MISDIRECT-RECORD.07560111
      *----------------------------------------------------------------*07560200
      *  GET THE TOTAL XFER QTY & RETAIL.                              *07560300
      *----------------------------------------------------------------*07560400
       4000-READ-TRANSFER-ITEM.                                         07560503
           PERFORM WITH TEST AFTER                                      07560603
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       07560711
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                  07560811
                    OR SQLCODE = ZERO                                   07560903
                    OR SQLCODE = +100                                   07570003
               EXEC SQL                                                 07580003
                   SELECT SUM(XFER_PRIC_AMT * XFER_QTY)                 07590011
                         ,SUM(XFER_QTY)                                 07591003
                       INTO :PV-XFER-PRICE                              07600011
                            :PV-XFER-PRICE-IND                          07600111
                           ,:PV-XFER-QTY                                07601011
                            :PV-XFER-QTY-IND                            07602011
                       FROM TTFITEM                                     07610011
                       WHERE XFER_DKEY_ID  = :TFMSTR-XFER-DKEY-ID       07620011
               END-EXEC                                                 07630003
               EVALUATE TRUE                                            07640003
                   WHEN SQLCODE = ZERO                                  07690003
                   WHEN SQLCODE = +100                                  07690111
                   WHEN SQLCODE = -904                                  07690203
                   WHEN SQLCODE = -913                                  07690303
                       CONTINUE                                         07690407
                   WHEN OTHER                                           07710003
                       MOVE '4000-READ-TRANSFER-ITEM'                   07740003
                                         TO PV-PARAGRAPH-NAME           07750003
                       MOVE 'SUMMARIZE DATA FROM TTFITEM'               07760011
                                         TO PV-DB2-OPERATION            07770003
                       PERFORM 9100-SQL-ERROR                           07780011
               END-EVALUATE                                             07840003
           END-PERFORM.                                                 07850003
      *                                                                 07861003
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          07862003
               MOVE '4000-READ-TRANSFER-ITEM'                           07862111
                                        TO PV-PARAGRAPH-NAME            07862203
               MOVE 'MAX RETRIES - SUM DATA FROM TTFITEM'               07862311
                                         TO PV-DB2-OPERATION            07862403
               PERFORM 9100-SQL-ERROR                                   07862511
           END-IF.                                                      07869503
           IF PV-XFER-PRICE-IND < 0                                     07869611
               MOVE ZEROES            TO PV-XFER-PRICE                  07869704
           END-IF                                                       07869811
           IF PV-XFER-QTY-IND < 0                                       07869911
               MOVE ZEROES            TO PV-XFER-QTY                    07870004
           END-IF.                                                      07870111
       EJECT                                                            07870205
      *----------------------------------------------------------------*07870300
      *  FIND THE DISTRIBUTION FACILITY FOR THE FROM STORE.            *07870400
      *----------------------------------------------------------------*07870500
       5000-GET-DIST-FACILITY.                                          07870603
100404     MOVE TFMSTR-FR-STR-NBR         TO XST00001-LOC-NBR.          07870711
           PERFORM WITH TEST AFTER                                      07870803
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       07870911
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                  07871011
                    OR SQLCODE = ZERO                                   07871103
                    OR SQLCODE = +100                                   07871203
               EXEC SQL                                                 07871311
100404           SELECT  B.DC_NBR                                               
100404             INTO  :XST00020-DC-NBR                               07872011
100404             FROM  XST_LOC            A                                   
100404                  ,XST_DC_LOC_ASGMT   B                                   
100404            WHERE  A.LOC_NBR      = :XST00001-LOC-NBR                     
100404              AND  A.LOC_NBR      = B.LOC_NBR                             
100404              AND  B.EFF_BEG_DTE  =                                       
100404                        (SELECT MAX(EFF_BEG_DTE)                          
100404                           FROM XST_DC_LOC_ASGMT                          
100404                          WHERE LOC_NBR = B.LOC_NBR)                      
               END-EXEC                                                 07878203
               EVALUATE TRUE                                            07878303
                   WHEN SQLCODE = ZERO                                  07878403
                   WHEN SQLCODE = +100                                  07878503
                   WHEN SQLCODE = -904                                  07878603
                   WHEN SQLCODE = -913                                  07878703
                       CONTINUE                                         07878807
                   WHEN OTHER                                           07878903
                       MOVE '5000-GET-DIST-FACILITY'                    07879203
                                         TO PV-PARAGRAPH-NAME           07879303
                       MOVE 'GET DC NUMBER FROM THE STORE TABLE'        07879411
                                         TO PV-DB2-OPERATION            07879503
                       PERFORM 9100-SQL-ERROR                           07879611
               END-EVALUATE                                             07879703
           END-PERFORM.                                                 07879803
      *                                                                 07879903
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          07880003
               MOVE '5000-GET-DIST-FACILITY'                            07880103
                                        TO PV-PARAGRAPH-NAME            07880203
               MOVE 'MAX RETRIES READING THE STORE TABLE'               07880311
                                         TO PV-DB2-OPERATION            07880403
               PERFORM 9100-SQL-ERROR                                   07880511
           END-IF.                                                      07880603
                                                                                
100404     IF SQLCODE = 0                                                       
100404         MOVE XST00020-DC-NBR TO PV-DC                                    
100404     END-IF.                                                      10620000
      *                                                                 10760200
      *----------------------------------------------------------------*03250105
      *  READ THE PROCESS DATES FILE FOR BEGIN AND END DATES           *03250205
      *----------------------------------------------------------------*03250305
       7000-READ-PROCESS-DATES-FILE.                                    01880203
           READ PROCESS-DATES-FILE                                      03250505
               INTO TF029-PROCESS-DATES-RECORD                          03250605
           END-READ.                                                    03251005
                                                                        10760700
      ******************************************************************10850000
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *10860000
      *  PROGRAM.                                                      *10870000
      ******************************************************************10880000
       9100-SQL-ERROR.                                                  10890000
           MOVE SQLCODE TO SQLCODE2.                                    10900000
           MOVE SQLERRD(3) TO SQLERRD3.                                 10910000
           DISPLAY '** ABEND **'.                                       10920000
           DISPLAY 'TFKUT486 - SQL ERROR'.                              10930000
           DISPLAY '       SQLCODE = ' SQLCODE2.                        10940000
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        10950000
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        10960000
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        10970000
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        10980000
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        10990000
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        11000000
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        11010000
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        11020000
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        11030000
                                                                        11040000
           MOVE +4013 TO ABEND-CODE.                                    11050000
           CALL 'ILBOABN0' USING ABEND-CODE.                            11060000
