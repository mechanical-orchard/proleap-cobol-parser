000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    APKUT183.                                                 
000300 AUTHOR.        MARK BURNSIDE                                             
000400 DATE-WRITTEN.  08/04/1999.                                               
000500 DATE-COMPILED.                                                           
000600*****************************************************************         
000700*      DETERMINE IF MULTIPLE PERIODS TO PROCESS                 *         
000800*****************************************************************         
000900*  COBOL II WITH SQL                                            *         
001000*****************************************************************         
001100*  PURPOSE:  READ THE TAPCNTL DB2 TABLE AND EVALUATE THE        *         
001200*            MULTIPLE POSTING PERIODS INDICATOR.  THE           *         
001300*            MULT_PSTG_PER_IND IS THE TAPCNTL FIELD THAT IS     *         
001400*            EVALUATED.  IF THE MULT_PSTG_PER_IND FIELD IS A    *         
001500*            'Y' THIS PROGRAM WILL END NORMALLY.  OTHERWISE     *         
001600*            THIS PROGRAM WILL END WITH A RETURN CODE OF 4090   *         
001700*            PREVENTING ANY SUCCEEDING PROCESSES IN THE CURRENT *         
001800*            JOB FROM PROCESSING.                               *         
001900*****************************************************************         
002000*  DB2 TABLES: TAPCNTL                                          *         
002100*                                                               *         
002200*****************************************************************         
002300                                                                          
002400 ENVIRONMENT DIVISION.                                                    
002500 CONFIGURATION SECTION.                                                   
002600 SOURCE-COMPUTER. IBM-3090.                                               
002700 OBJECT-COMPUTER. IBM-3090.                                               
002800                                                                          
002900 INPUT-OUTPUT SECTION.                                                    
003000 FILE-CONTROL.                                                            
003100                                                                          
003200 DATA DIVISION.                                                           
003300 FILE SECTION.                                                            
003400                                                                          
003500 WORKING-STORAGE SECTION.                                                 
003600                                                                          
003700 01  PROGRAM-CONSTANTS.                                                   
003800     05  PC-PROGRAM-NAME             PIC X(08) VALUE 'APKUT183'.          
003900                                                                          
004000*--------------------------------------------------------------*          
004100*     TAPCNTL DCLGEN                                           *          
004200*--------------------------------------------------------------*          
004300     EXEC SQL                                                             
004400         INCLUDE TAPCNTL                                                  
004500     END-EXEC.                                                            
004600 EJECT                                                                    
004700                                                                          
004800*--------------------------------------------------------------*          
004900*  COMMUNICATIONS AREA2 FOR DB2                                *          
005000*--------------------------------------------------------------*          
005100     COPY SQLCA2.                                                         
005200*--------------------------------------------------------------*          
005300*     SQL COMMUNICATIONS AREA DCLGEN                           *          
005400*--------------------------------------------------------------*          
005500     EXEC SQL                                                             
005600         INCLUDE SQLCA                                                    
005700     END-EXEC.                                                            
005800 EJECT                                                                    
005900                                                                          
006000 01  ABEND-CODE            PIC S9(04)  VALUE ZEROS COMP SYNC.             
006100     88  AC-DB2-ERROR                        VALUE +4013.                 
006200     88  AC-UNSUCCESSFUL-SELECT              VALUE +4015.                 
006300                                                                          
006400 01  ABEND-AREAS.                                                         
006500     05  AA-ABEND-LIT            PIC  X(40)  VALUE                        
006600             '*****       ABEND'.                                         
006700     05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                        
006800             '*****   PROGRAM: APKUT183'.                                 
006900     05  AA-PARAGRAPH-LIT.                                                
007000         10  FILLER              PIC  X(17)  VALUE                        
007100             '***** PARAGRAPH: '.                                         
007200         10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.                
007300     05  AA-MESSAGE-LINE.                                                 
007400         10  FILLER              PIC  X(06)  VALUE '*****'.               
007500         10  AA-MESSAGE          PIC  X(127) VALUE SPACES.                
007600         10  FILLER REDEFINES AA-MESSAGE.                                 
007700             15 AA-MESSAGE-1     PIC  X(45).                              
007800             15 AA-MESSAGE-2     PIC  X(45).                              
007900             15 AA-MESSAGE-3     PIC  X(37).                              
008000     05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                        
008100             '*****    DB2 ERROR'.                                        
008200     05  AA-DB2-OPERATION-LIT.                                            
008300         10  FILLER              PIC  X(17)  VALUE                        
008400             '***** OPERATION: '.                                         
008500         10  AA-DB2-OPERATION.                                            
008600             15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.                
008700             15  AA-DB2-OP-2     PIC  X(25)  VALUE SPACES.                
008800     05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.                
008900     05  AA-DB2-TABLE-2          PIC  X(08)  VALUE SPACES.                
009000     05  AA-DB2-TABLE-3          PIC  X(08)  VALUE SPACES.                
009100 EJECT                                                                    
009200                                                                          
009300     COPY DPWS004.                                                        
009400 EJECT                                                                    
009500                                                                          
009600                                                                          
009700 PROCEDURE DIVISION.                                                      
009800 A100-MAINLINE-ROUTINE.                                                   
009900                                                                          
010000     PERFORM B100-INITIALIZATION.                                         
010100                                                                          
010200     PERFORM B200-EVALUATE-MULT-POST.                                     
010300                                                                          
010400     PERFORM B300-TERMINATION.                                            
010500     GOBACK.                                                              
010600                                                                          
010700 EJECT                                                                    
010800                                                                          
010900*--------------------------------------------------------------*          
011000*    INITIALIZATION PROCESSING                                 *          
011100*--------------------------------------------------------------*          
011200 B100-INITIALIZATION.                                                     
011300     DISPLAY ' '.                                                         
011400     DISPLAY '**************************************************'.        
011500     DISPLAY '*        START  PROGRAM:  APKUT183               *'.        
011600     DISPLAY '**************************************************'.        
011700     DISPLAY ' '.                                                         
011800                                                                          
011900 EJECT.                                                                   
012000                                                                          
012100                                                                          
012200*---------------------------------------------------------------*         
012300*    THIS ROUTINE WILL EVALUATE THE MULTIPLE POSTING INDICATOR. *         
012400*---------------------------------------------------------------*         
012500 B200-EVALUATE-MULT-POST.                                                 
012600     PERFORM S100-SELECT-AP-CONTROL.                                      
012700     DISPLAY 'BTCH_PRC_DTE       ' APCNTL-BTCH-PRC-DTE.                   
012800     DISPLAY 'MULT_PSTG_PER_IND  ' APCNTL-MULT-PSTG-PER-IND.              
012900                                                                          
013000     IF APCNTL-MULT-PSTG-PER-IND NOT = 'Y'                                
013100        MOVE 4090 TO RETURN-CODE                                          
013200     END-IF.                                                              
013300                                                                          
013400 EJECT.                                                                   
013500                                                                          
013600*---------------------------------------------------------------*         
013700*  END OF PROGRAM PROCESSING.                                   *         
013800*---------------------------------------------------------------*         
013900 B300-TERMINATION.                                                        
014000                                                                          
014100     DISPLAY ' '.                                                         
014200     DISPLAY '**************************************************'.        
014300     DISPLAY '*               END APKUT183                     *'.        
014400     DISPLAY '**************************************************'.        
014500     DISPLAY ' '.                                                         
014600                                                                          
014700 EJECT.                                                                   
014800                                                                          
014900*--------------------------------------------------------------*          
015000* THIS ROUTINE DOES A DB2 CALL TO THE TAPCNTL TABLE TO RETRIEVE*          
015100* THE BATCH PROCESS DATE AND THE MULTIPLE PERIOD POSTING       *          
015200* INDICATOR.                                                   *          
015300*--------------------------------------------------------------*          
015400 S100-SELECT-AP-CONTROL.                                                  
015500     EXEC SQL                                                             
015600        SELECT BTCH_PRC_DTE                                               
015700             , MULT_PSTG_PER_IND                                          
015800          INTO :APCNTL-BTCH-PRC-DTE                                       
015900             , :APCNTL-MULT-PSTG-PER-IND                                  
016000          FROM TAPCNTL                                                    
016100     END-EXEC.                                                            
016200                                                                          
016300     EVALUATE TRUE                                                        
016400         WHEN SQLCODE = ZERO                                              
016500             CONTINUE                                                     
016600         WHEN OTHER                                                       
016700             SET AC-UNSUCCESSFUL-SELECT TO TRUE                           
016800             MOVE 'S100-SELECT-AP-CONTROL' TO AA-PARAGRAPH-NAME           
016900             MOVE 'UNSUCCESSFUL SELECT'    TO AA-DB2-OPERATION            
017000             MOVE 'TAPCNTL'                TO AA-DB2-TABLE-1              
017100             MOVE SPACES                   TO AA-DB2-TABLE-2              
017200             MOVE SPACES                   TO AA-DB2-TABLE-3              
017300             PERFORM Z998-DB2-ABEND                                       
017400     END-EVALUATE.                                                        
017500 EJECT                                                                    
017600                                                                          
017700*---------------------------------------------------------------          
017800*     DB2 ABEND ROUTINE                                                   
017900*---------------------------------------------------------------          
018000 Z998-DB2-ABEND.                                                          
018100                                                                          
018200     PERFORM B300-TERMINATION.                                            
018300                                                                          
018400     DISPLAY AA-ABEND-LIT.                                                
018500     DISPLAY AA-DB2-ERROR-LIT.                                            
018600     DISPLAY AA-PROGRAM-LIT.                                              
018700     DISPLAY AA-PARAGRAPH-LIT.                                            
018800     DISPLAY AA-DB2-OPERATION-LIT.                                        
018900     DISPLAY AA-DB2-TABLE-1.                                              
019000     DISPLAY AA-DB2-TABLE-2.                                              
019100     DISPLAY AA-DB2-TABLE-3.                                              
019200     SET AC-DB2-ERROR TO TRUE.                                            
019300                                                                          
019400     COPY DPPD004.                                                        
019500                                                                          
019600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
019700 EJECT                                                                    
019800                                                                          
