       ID  DIVISION.                                                    03/16/94
00002  PROGRAM-ID.         INKRP174.                                    INKRP174
00003  AUTHOR.             SCOTT SWESSEL.                                       
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKRP174
00005  DATE-WRITTEN.       10/98.                                       INKRP174
00006  DATE-COMPILED.                                                   INKRP174
00007                                                                   INKRP174
00008 *----------------------------------------------------------------*INKRP174
00009 *                                                                *INKRP174
00010 *    READ THE POS DAILY SALES AND CREATE THE TIMESTAMPS          *INKRP174
00011 *    FOR AFTER INVENTORY SALES REPORT.                           *INKRP174
00012 *                                                                *INKRP174
00013 *----------------------------------------------------------------*INKRP174
RB1002*    12/09/2002  RONNA BUTZ   WR24029                                     
RB1002*    NO PROGRAM CHANGES MADE, BUT COPYBOOK INRD017 CHANGED                
RB1002*    AND THIS PROGRAM NEEDS TO BE RECOMPILED.                             
RB1002*****************************************************************         
W21732* 11/19/2002        CHANGED BY SUKUMAR,IGSI    WR# W21732        *        
W21732* CHANGED CURRENT DEPT/CLASS/SEQ NUMBER TO NON-INTELLIGENT SKU.  *        
W21732*----------------------------------------------------------------*        
W26600* 06/07/2004        CHANGED BY D. THEEL        WR# W26600        *        
W26600* INCREASED STORE FROM X(03) TO 9(05) AND S9(4) COMP AND         *        
W26600* INCREASED INPUT FILE LRECL TO X(50) FOR IMRD017 CHANGES.       *        
W21732*----------------------------------------------------------------*        
00014                                                                   INKRP174
00015  ENVIRONMENT DIVISION.                                            INKRP174
00016  CONFIGURATION SECTION.                                           INKRP174
00017  INPUT-OUTPUT SECTION.                                            INKRP174
00018  FILE-CONTROL.                                                    INKRP174
00019                                                                   INKRP174
00020      SELECT DAILY-SALES-INPUT                                             
00021          ASSIGN TO INP01.                                         INKRP174
00025                                                                   INKRP174
00026      SELECT INV-SLS-AFT-REPORT                                    INKRP174
00027          ASSIGN TO RPT01.                                         INKRP174
00028                                                                   INKRP174
00029  DATA DIVISION.                                                   INKRP174
00030  FILE SECTION.                                                    INKRP174
00031                                                                   INKRP174
00032  FD  DAILY-SALES-INPUT                                                    
00033      LABEL RECORDS ARE STANDARD                                   INKRP174
00034      RECORDING MODE IS F                                          INKRP174
00035      BLOCK CONTAINS 0 RECORDS.                                    INKRP174
00036  01  DAILY-SALES-INPUT-REC         PIC X(50).                     INKRP174
00037                                                                   INKRP174
00044  FD  INV-SLS-AFT-REPORT                                           INKRP174
00045      LABEL RECORDS ARE STANDARD                                   INKRP174
00046      RECORDING MODE IS F                                          INKRP174
00047      BLOCK CONTAINS 0 RECORDS.                                    INKRP174
00048  01  INV-SLS-AFT-REPORT-REC        PIC X(132).                    INKRP174
00049                                                                   INKRP174
00050  WORKING-STORAGE SECTION.                                         INKRP174
00051                                                                   INKRP174
00052  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.INKRP174
00053      88  ABEND-4003                          VALUE +4003.         INKRP174
00054      88  ABEND-4013                          VALUE +4013.         INKRP174
00055                                                                   INKRP174
00056  01  PV-PROGRAM-VARIABLES.                                        INKRP174
           05  PV-FIRST-TIME           PIC X(01)    VALUE 'Y'.                  
           05  PV-HOLD-ITEM.                                                    
W21732         07  PV-HOLD-SKU         PIC X(08)    VALUE SPACES.               
           05  PV-ITEM.                                                         
W21732         07  PV-SKU              PIC X(08)    VALUE SPACES.               
           05  PV-HOLD-CALC            PIC 9(06)    VALUE ZEROES.               
W26600*    05  PV-HOLD-STORE           PIC  X(03)   VALUE SPACES.               
W26600     05  PV-HOLD-STORE           PIC S9(4)    COMP VALUE +0.              
           05  PV-HOLD-DATE            PIC X(04).                               
           05  PV-HOLD-DATEA REDEFINES PV-HOLD-DATE.                            
               07  PV-HOLD-MM          PIC X(02).                               
               07  PV-HOLD-DD          PIC X(02).                               
           05  PV-HOLD-TIME            PIC X(06).                               
           05  PV-HOLD-TIMEA REDEFINES PV-HOLD-TIME.                            
               07  PV-HH               PIC X(02).                               
               07  PV-MIN              PIC X(02).                               
               07  PV-SS               PIC X(02).                               
           05  PV-HOLD-DATE2           PIC X(04).                               
           05  PV-HOLD-DATEB REDEFINES PV-HOLD-DATE2.                           
               07  PV-MONTH            PIC X(02).                               
               07  PV-DAY              PIC X(02).                               
00057      05  PV-REC-IN               PIC S9(07)   COMP-3 VALUE 0.     INKRP174
00058      05  PV-REC-OUT              PIC S9(07)   COMP-3 VALUE 0.     INKRP174
00059      05  PV-PARAGRAPH-NAME       PIC  X(30).                      INKRP174
00061      05  PV-END-OF-INPUT-SW      PIC  X(01)          VALUE 'N'.   INKRP174
00062          88  PV-END-OF-INPUT                         VALUE 'Y'.   INKRP174
00063          88  PV-NOT-END-OF-INPUT                     VALUE 'N'.   INKRP174
00069                                                                   INKRP174
00071      05  PV-PAGE-COUNT           PIC S9(05)   COMP-3 VALUE 0.     INKRP174
00072      05  PV-LINE-COUNT           PIC S9(03)   COMP-3 VALUE 66.    INKRP174
00077      05  PV-CURRENT-DATE.                                         INKRP174
00078          10  PV-CD-YY            PIC XX.                          INKRP174
00079          10  PV-CD-MM            PIC XX.                          INKRP174
00080          10  PV-CD-DD            PIC XX.                          INKRP174
00081      05  PV-CURRENT-TIME.                                         INKRP174
00082          10  PV-CT-HH            PIC XX.                          INKRP174
00083          10  PV-CT-MM            PIC XX.                          INKRP174
00084                                                                   INKRP174
00100  01  PA-PROGRAM-ACCUMULATORS     COMP-3.                                  
  101      05  PA-TOT-QTY-SOLD         PIC S9(07)    VALUE ZERO.                
  101      05  PA-STR-QTY-SOLD         PIC S9(07)    VALUE ZERO.                
  101      05  PA-GRD-QTY-SOLD         PIC S9(07)    VALUE ZERO.                
  101      05  PA-TOT-QTY-RETURN       PIC S9(07)    VALUE ZERO.                
  101      05  PA-STR-QTY-RETURN       PIC S9(07)    VALUE ZERO.                
  101      05  PA-GRD-QTY-RETURN       PIC S9(07)    VALUE ZERO.                
  101      05  PA-TOT-QTY-NET          PIC S9(07)    VALUE ZERO.                
  101      05  PA-STR-QTY-NET          PIC S9(07)    VALUE ZERO.                
  101      05  PA-GRD-QTY-NET          PIC S9(07)    VALUE ZERO.                
00106                                                                   INKRP174
00107  01  PC-PROGRAM-CONSTANTS.                                        INKRP174
00108      05  PC-MAX-RETRIES          PIC S9(4)    COMP   VALUE +3.    INKRP174
00109      05  PC-LINE-MAX             PIC S9(3)    COMP-3 VALUE 60.    INKRP174
00110      05  PC-1                    PIC X(01)    VALUE '1'.          INKRP174
00111      05  PC-ASTERISKS            PIC X(03)    VALUE '***'.        INKRP174
00112      05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'INKRP174'.   INKRP174
00115 /                                                                 INKRP174
00116  01  HL1-HEADER-LINE-1.                                           INKRP174
00117      05  FILLER                  PIC X(46)    VALUE SPACES.       INKRP174
00118      05  FILLER                  PIC X(048)   VALUE               INKRP174
00119          'TIMESTAMPS FOR RETURNS AND AFTER INVENTORY SALES'.              
00117      05  FILLER                  PIC X(50)    VALUE SPACES.       INKRP174
00120                                                                   INKRP174
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(08)    VALUE                       
               'STORE:  '.                                                      
W26600     05  HL2-STORE               PIC 9(05)    VALUE ZEROES.               
W26600     05  FILLER                  PIC X(118)   VALUE SPACES.               
                                                                                
       01  HL3-HEADING-LINE-3A.                                                 
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
W21732         'SKU '.                                                          
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'INVENTORY'.                                                     
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(11)    VALUE SPACES.               
           05  FILLER                  PIC X(03)    VALUE                       
               'QTY'.                                                           
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(03)    VALUE                       
               'QTY'.                                                           
           05  FILLER                  PIC X(14)    VALUE SPACES.               
           05  FILLER                  PIC X(03)    VALUE                       
               'NET'.                                                           
           05  FILLER                  PIC X(70)    VALUE SPACES.               
                                                                                
       01  HL3-HEADING-LINE-3C.                                                 
           05  FILLER                  PIC X(13)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'TIMESTAMP'.                                                     
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(10)    VALUE                       
               'TIMESTAMP '.                                                    
           05  FILLER                  PIC X(06)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE                       
               'RETURN'.                                                        
           05  FILLER                  PIC X(06)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'SOLD '.                                                         
           05  FILLER                  PIC X(12)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'QTY  '.                                                         
           05  FILLER                  PIC X(59)    VALUE SPACES.               
                                                                                
       01  TT1-TOTAL-LINE-1.                                                    
           05  FILLER                  PIC X(45)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE '------'.             
           05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE '------'.             
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE '------'.             
           05  FILLER                  PIC X(72)    VALUE SPACES.               
                                                                                
       01  TT1-TOTAL-LINE-2.                                                    
W26600     05  FILLER                  PIC X(27)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE 'STORE'.              
           05  FILLER                  PIC X(1)     VALUE SPACE.                
W26600*    05  ST-STORE-NUM            PIC X(03)    VALUE SPACES.               
W26600     05  ST-STORE-NUM            PIC 9(05)    VALUE ZEROES.               
           05  FILLER                  PIC X(1)     VALUE SPACE.                
           05  FILLER                  PIC X(03)    VALUE 'TTL'.                
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  ST-QTY-RETURN           PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  ST-QTY-SOLD             PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(8)     VALUE SPACES.               
           05  ST-QTY-NET              PIC ZZZZZ9-  VALUE ZEROES.               
W26600     05  FILLER                  PIC X(56)    VALUE SPACES.               
                                                                                
       01  TT1-TOTAL-LINE-3.                                                    
           05  FILLER                  PIC X(31)    VALUE SPACES.               
           05  FILLER                  PIC X(11)    VALUE                       
               '        TTL'.                                                   
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  TT-QTY-RETURN           PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  TT-QTY-SOLD             PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(8)     VALUE SPACES.               
           05  TT-QTY-NET              PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(63)    VALUE SPACES.               
                                                                                
       01  TT1-TOTAL-LINE-4.                                                    
           05  FILLER                  PIC X(31)    VALUE SPACES.               
           05  FILLER                  PIC X(11)    VALUE                       
               '  GRAND TTL'.                                                   
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  GT-QTY-RETURN           PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  GT-QTY-SOLD             PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(8)     VALUE SPACES.               
           05  GT-QTY-NET              PIC ZZZZZ9-  VALUE ZEROES.               
           05  FILLER                  PIC X(63)    VALUE SPACES.               
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  DL-ITEM-NUM.                                                     
W21732         07  DL-SKU-NBR          PIC X(08)    VALUE SPACES.               
W21732     05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  DL-INV-TIMESTAMP.                                                
               07  DL-MM               PIC X(02)    VALUE SPACES.               
               07  DL-SLASH            PIC X(01)    VALUE '/'.                  
               07  DL-DD               PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE SPACE.                
               07  DL-HH               PIC X(02)    VALUE SPACES.               
               07  DL-COLON1           PIC X(01)    VALUE ':'.                  
               07  DL-MIN              PIC X(02)    VALUE SPACES.               
               07  DL-COLON2           PIC X(01)    VALUE ':'.                  
               07  DL-SS               PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-SALE-TIMESTAMP.                                               
               07  DL-MM-SLS           PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE '/'.                  
               07  DL-DD-SLS           PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE SPACE.                
               07  DL-HH-SLS           PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE ':'.                  
               07  DL-MIN-SLS          PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE ':'.                  
               07  DL-SS-SLS           PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-QTY-RETURN           PIC ZZZZZZ-  VALUE ZEROES.               
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  DL-QTY-SOLD             PIC ZZZZZZ-  VALUE ZEROES.               
           05  FILLER                  PIC X(73)    VALUE SPACES.               
                                                                                
00121 *-----------------------------------------------------------------INKRP174
00122 * COPYBOOK FOR REMAINING REPORT HEADING LINES, DETAIL LINE                
00124 *-----------------------------------------------------------------INKRP174
00125                                                                   INKRP174
00128      COPY DPWS132O.                                                       
00133                                                                   INKRP174
00128      COPY INRD017.                                                        
00133                                                                   INKRP174
00143  LINKAGE SECTION.                                                 INKRP174
00144                                                                   INKRP174
00145  PROCEDURE DIVISION.                                              INKRP174
00146                                                                   INKRP174
00147  1000-MAIN-MODULE.                                                INKRP174
00148                                                                   INKRP174
00149      PERFORM 1100-HOUSEKEEPING.                                   INKRP174
00150                                                                   INKRP174
           PERFORM 2310-WRITE-HEADER.                                           
                                                                                
00151      PERFORM 2000-PROCESS-INPUT                                   INKRP174
00152          UNTIL PV-END-OF-INPUT.                                           
00153                                                                   INKRP174
           PERFORM 2250-PRINT-TOTAL.                                            
           PERFORM 2260-PRINT-STORE-TOTAL.                                      
00206      PERFORM 2400-GRAND-TOTALS.                                           
                                                                                
00154      PERFORM 1200-EOJ.                                            INKRP174
00155                                                                   INKRP174
00156      GOBACK.                                                      INKRP174
00157 /                                                                 INKRP174
00158 *----------------------------------------------------------------*INKRP174
00159 *                                                                *INKRP174
00160 *    OPEN THE FILES, GET THE FIRST RECORD AND SAVE THE KEYS.     *INKRP174
00161 *                                                                *INKRP174
00162 *----------------------------------------------------------------*INKRP174
00163                                                                   INKRP174
00164  1100-HOUSEKEEPING.                                               INKRP174
00165                                                                   INKRP174
00166      OPEN  INPUT DAILY-SALES-INPUT                                        
00168           OUTPUT INV-SLS-AFT-REPORT.                              INKRP174
00169                                                                   INKRP174
00170      ACCEPT PV-CURRENT-DATE    FROM  DATE.                        INKRP174
00171      ACCEPT PV-CURRENT-TIME    FROM  TIME.                        INKRP174
00172                                                                   INKRP174
00173      MOVE PV-CD-MM               TO  DP132O-RUN-MONTH.            INKRP174
00174      MOVE PV-CD-DD               TO  DP132O-RUN-DAY.              INKRP174
00175      MOVE PV-CD-YY               TO  DP132O-RUN-YEAR.             INKRP174
00176                                                                   INKRP174
00177      MOVE PV-CT-HH               TO  DP132O-RUN-HOUR.             INKRP174
00178      MOVE PV-CT-MM               TO  DP132O-RUN-MINUTE.           INKRP174
00179      MOVE PC-PROGRAM-NAME        TO  DP132O-PROGRAM-NAME.         INKRP174
00180      MOVE PC-1                   TO  DP132O-REPORT-NUMBER.        INKRP174
00181                                                                   INKRP174
00204      PERFORM 2100-READ-INPUT.                                             
           MOVE IN017-STR-NBR TO PV-HOLD-STORE.                                 
W21732     MOVE IN017-SKU      TO PV-HOLD-SKU                                   
W21732                            PV-SKU.                                       
00182 *----------------------------------------------------------------*INKRP174
00183 *                                                                *INKRP174
00184 *    HANDLE THE TOTAL LINE AND CLOSE THE FILES.                  *INKRP174
00185 *                                                                *INKRP174
00186 *----------------------------------------------------------------*INKRP174
00187                                                                   INKRP174
00188  1200-EOJ.                                                        INKRP174
00189                                                                   INKRP174
00190                                                                   INKRP174
00191      CLOSE DAILY-SALES-INPUT                                              
00193            INV-SLS-AFT-REPORT.                                    INKRP174
00194 /                                                                 INKRP174
00195 *----------------------------------------------------------------*INKRP174
00196 *                                                                *INKRP174
00197 *    FOR EACH STORE, PROCESS THE TIMESTAMP CUTOFF RECORD         *INKRP174
00198 *    INPUT FILE FOR EACH ITEM.                                   *INKRP174
00199 *                                                                *INKRP174
00200 *----------------------------------------------------------------*INKRP174
00201                                                                   INKRP174
00202  2000-PROCESS-INPUT.                                              INKRP174
00203                                                                   INKRP174
00208      IF  IN017-STR-NBR = PV-HOLD-STORE                                    
               IF PV-ITEM = PV-HOLD-ITEM                                        
00209             PERFORM 2200-PRINT                                            
               ELSE                                                             
                  PERFORM 2250-PRINT-TOTAL                                      
                  PERFORM 2200-PRINT                                            
               END-IF                                                           
           ELSE                                                                 
               PERFORM 2250-PRINT-TOTAL                                         
               PERFORM 2260-PRINT-STORE-TOTAL                                   
               PERFORM 2200-PRINT                                               
00219      END-IF                                                               
00204      PERFORM 2100-READ-INPUT.                                             
W21732     MOVE IN017-SKU      TO PV-SKU.                                       
00221 /                                                                 INKRP174
00268 *----------------------------------------------------------------*INKRP174
00269 *                                                                *INKRP174
00270 *    READ THE INPUT FILE.                                        *INKRP174
00271 *                                                                *INKRP174
00272 *----------------------------------------------------------------*INKRP174
00273                                                                   INKRP174
00274  2100-READ-INPUT.                                                 INKRP174
00275                                                                   INKRP174
00276      READ DAILY-SALES-INPUT INTO IN017-SALE-TIMESTAMPS                    
00277           AT END                                                  INKRP174
00278           SET PV-END-OF-INPUT TO TRUE                             INKRP174
00279      END-READ.                                                    INKRP174
00280 /                                                                 INKRP174
00281 *----------------------------------------------------------------*INKRP174
00282 *                                                                *INKRP174
00283 *    FORMAT THE PRINT RECORD, WRITE IT.                          *INKRP174
00284 *                                                                *INKRP174
00285 *----------------------------------------------------------------*INKRP174
00286                                                                   INKRP174
00287  2200-PRINT.                                                      INKRP174
00288                                                                   INKRP174
           MOVE IN017-STR-NBR          TO  PV-HOLD-STORE.                       
           IF PV-ITEM = PV-HOLD-ITEM                                            
              IF PV-FIRST-TIME = 'Y'                                            
W21732            MOVE IN017-SKU      TO  DL-SKU-NBR                            
W21732                                    PV-HOLD-SKU                           
00292             MOVE IN017-SCAN-DTE TO  PV-HOLD-DATE                          
                  MOVE PV-HOLD-MM     TO  DL-MM                                 
                  MOVE PV-HOLD-DD     TO  DL-DD                                 
                  MOVE IN017-SCAN-TIMESTAMP TO  PV-HOLD-TIME                    
                  MOVE PV-HH          TO  DL-HH                                 
                  MOVE PV-MIN         TO  DL-MIN                                
                  MOVE PV-SS          TO  DL-SS                                 
                  MOVE '/'            TO  DL-SLASH                              
                  MOVE ':'            TO  DL-COLON1                             
                                      DL-COLON2                                 
                  MOVE 'N'            TO PV-FIRST-TIME                          
              ELSE                                                              
                  MOVE SPACES TO DL-ITEM-NUM                                    
                                 DL-INV-TIMESTAMP                               
              END-IF                                                            
           ELSE                                                                 
W21732        MOVE IN017-SKU      TO  DL-SKU-NBR                                
W21732                                PV-HOLD-SKU                               
00292         MOVE IN017-SCAN-DTE TO  PV-HOLD-DATE                              
              MOVE PV-HOLD-MM     TO  DL-MM                                     
              MOVE PV-HOLD-DD     TO  DL-DD                                     
              MOVE IN017-SCAN-TIMESTAMP TO  PV-HOLD-TIME                        
              MOVE PV-HH          TO  DL-HH                                     
              MOVE PV-MIN         TO  DL-MIN                                    
              MOVE PV-SS          TO  DL-SS                                     
              MOVE '/'            TO  DL-SLASH                                  
              MOVE ':'            TO  DL-COLON1                                 
                                      DL-COLON2                                 
           END-IF.                                                              
           MOVE IN017-SALE-DTE         TO  PV-HOLD-DATE2.                       
           MOVE PV-MONTH               TO  DL-MM-SLS.                           
           MOVE PV-DAY                 TO  DL-DD-SLS.                           
           MOVE IN017-SALE-TIMESTAMP   TO  PV-HOLD-TIME.                        
           MOVE PV-HH                  TO  DL-HH-SLS.                           
           MOVE PV-MIN                 TO  DL-MIN-SLS.                          
           MOVE PV-SS                  TO  DL-SS-SLS.                           
                                                                                
           IF IN017-SALE                                                        
               MOVE IN017-SALES-QTY TO  DL-QTY-SOLD                             
               MOVE ZERO            TO  DL-QTY-RETURN                           
               COMPUTE PA-TOT-QTY-SOLD =                                        
                       PA-TOT-QTY-SOLD + IN017-SALES-QTY                        
               COMPUTE PA-TOT-QTY-NET =                                         
                       PA-TOT-QTY-NET + IN017-SALES-QTY                         
               COMPUTE PA-STR-QTY-SOLD =                                        
                       PA-STR-QTY-SOLD + IN017-SALES-QTY                        
               COMPUTE PA-STR-QTY-NET =                                         
                       PA-STR-QTY-NET + IN017-SALES-QTY                         
               COMPUTE PA-GRD-QTY-SOLD =                                        
                       PA-GRD-QTY-SOLD + IN017-SALES-QTY                        
               COMPUTE PA-GRD-QTY-NET =                                         
                       PA-GRD-QTY-NET + IN017-SALES-QTY                         
           ELSE                                                                 
               IF IN017-RETURN                                                  
                   MOVE IN017-SALES-QTY TO  DL-QTY-RETURN                       
                   MOVE ZERO            TO  DL-QTY-SOLD                         
                   COMPUTE PA-TOT-QTY-RETURN =                                  
                           PA-TOT-QTY-RETURN + IN017-SALES-QTY                  
                   COMPUTE PA-TOT-QTY-NET =                                     
                           PA-TOT-QTY-NET + IN017-SALES-QTY                     
                   COMPUTE PA-STR-QTY-RETURN =                                  
                           PA-STR-QTY-RETURN + IN017-SALES-QTY                  
                   COMPUTE PA-STR-QTY-NET =                                     
                           PA-STR-QTY-NET + IN017-SALES-QTY                     
                   COMPUTE PA-GRD-QTY-RETURN =                                  
                           PA-GRD-QTY-RETURN + IN017-SALES-QTY                  
                   COMPUTE PA-GRD-QTY-NET =                                     
                           PA-GRD-QTY-NET + IN017-SALES-QTY                     
                   END-IF                                                       
           END-IF.                                                              
                                                                                
00317      PERFORM 2300-WRITE-DETAIL.                                   INKRP174
00318 /                                                                 INKRP174
00319 *----------------------------------------------------------------*INKRP174
00320 *                                                                *INKRP174
00321 *    WRITE ITEM  TOTAL'S LINE.                                   *INKRP174
00322 *                                                                *INKRP174
00323 *----------------------------------------------------------------*INKRP174
00324                                                                   INKRP174
00325  2250-PRINT-TOTAL.                                                INKRP174
00326                                                                   INKRP174
00389      ADD 3                      TO  PV-LINE-COUNT.                INKRP174
                                                                                
00373      WRITE INV-SLS-AFT-REPORT-REC FROM TT1-TOTAL-LINE-1.                  
           MOVE PA-TOT-QTY-SOLD         TO  TT-QTY-SOLD.                        
           MOVE PA-TOT-QTY-RETURN       TO  TT-QTY-RETURN.                      
           MOVE PA-TOT-QTY-NET          TO  TT-QTY-NET.                         
00373      WRITE INV-SLS-AFT-REPORT-REC FROM TT1-TOTAL-LINE-3.                  
           MOVE ZEROES                  TO PA-TOT-QTY-SOLD.                     
           MOVE ZEROES                  TO PA-TOT-QTY-RETURN.                   
           MOVE ZEROES                  TO PA-TOT-QTY-NET.                      
           MOVE SPACES TO INV-SLS-AFT-REPORT-REC.                               
           WRITE INV-SLS-AFT-REPORT-REC                                         
              AFTER ADVANCING 1.                                                
00380 /                                                                 INKRP174
00319 *----------------------------------------------------------------*INKRP174
00320 *                                                                *INKRP174
00321 *    WRITE STORE TOTAL'S LINE.                                   *INKRP174
00322 *                                                                *INKRP174
00323 *----------------------------------------------------------------*INKRP174
00324                                                                   INKRP174
00325  2260-PRINT-STORE-TOTAL.                                                  
00326                                                                   INKRP174
00389      ADD 2                      TO  PV-LINE-COUNT.                INKRP174
                                                                                
           MOVE PV-HOLD-STORE           TO  ST-STORE-NUM.                       
           MOVE PA-STR-QTY-SOLD         TO  ST-QTY-SOLD.                        
           MOVE PA-STR-QTY-RETURN       TO  ST-QTY-RETURN.                      
           MOVE PA-STR-QTY-NET          TO  ST-QTY-NET.                         
00373      WRITE INV-SLS-AFT-REPORT-REC FROM TT1-TOTAL-LINE-2.                  
           MOVE IN017-STR-NBR           TO PV-HOLD-STORE.                       
           MOVE ZEROES                  TO PA-STR-QTY-SOLD.                     
           MOVE ZEROES                  TO PA-STR-QTY-RETURN.                   
           MOVE ZEROES                  TO PA-STR-QTY-NET.                      
           MOVE SPACES TO INV-SLS-AFT-REPORT-REC.                               
           WRITE INV-SLS-AFT-REPORT-REC                                         
              AFTER ADVANCING 1.                                                
           MOVE IN017-STR-NBR           TO PV-HOLD-STORE.                       
           COMPUTE PV-LINE-COUNT = PC-LINE-MAX + 2.                             
           MOVE 'Y' TO PV-FIRST-TIME.                                           
           IF NOT PV-END-OF-INPUT                                               
00391         PERFORM 2310-WRITE-HEADER.                                        
00380                                                                   INKRP174
00381 *----------------------------------------------------------------*INKRP174
00382 *                                                                *INKRP174
00383 *    WRITE THE DETAIL LINE.                                      *INKRP174
00384 *                                                                *INKRP174
00385 *----------------------------------------------------------------*INKRP174
00386                                                                   INKRP174
00387  2300-WRITE-DETAIL.                                               INKRP174
00388                                                                   INKRP174
00389      ADD 1                      TO  PV-LINE-COUNT.                INKRP174
00390      IF  PV-LINE-COUNT GREATER THAN PC-LINE-MAX                   INKRP174
00391          PERFORM 2310-WRITE-HEADER                                INKRP174
00392      END-IF.                                                      INKRP174
00393                                                                   INKRP174
00394      WRITE INV-SLS-AFT-REPORT-REC FROM DL-DETAIL-LINE.                    
00395 /                                                                 INKRP174
00396  2310-WRITE-HEADER.                                               INKRP174
00397                                                                   INKRP174
00389      ADD 7                      TO  PV-LINE-COUNT.                INKRP174
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
00398         ADD 1                   TO  PV-PAGE-COUNT                         
00399         MOVE PV-PAGE-COUNT      TO  DP132O-PAGE-NUMBER                    
00400         MOVE 10                 TO  PV-LINE-COUNT                         
              WRITE INV-SLS-AFT-REPORT-REC                                      
                 FROM DP132O-STANDRD-HEADER-132-COLS                            
00403            AFTER ADVANCING PAGE                                           
              WRITE INV-SLS-AFT-REPORT-REC                                      
                 FROM HL1-HEADER-LINE-1                                         
              MOVE SPACES TO INV-SLS-AFT-REPORT-REC                             
00405         WRITE INV-SLS-AFT-REPORT-REC                                      
00407                                                                           
              MOVE PV-HOLD-STORE TO HL2-STORE                                   
00408         WRITE INV-SLS-AFT-REPORT-REC FROM HL2-HEADING-LINE-2              
00409            AFTER ADVANCING 1                                              
00410                                                                   INKRP174
00411         WRITE INV-SLS-AFT-REPORT-REC FROM HL3-HEADING-LINE-3A             
00412            AFTER ADVANCING 2                                              
00416                                                                           
00411         WRITE INV-SLS-AFT-REPORT-REC FROM HL3-HEADING-LINE-3C             
00416                                                                           
00417         MOVE SPACES                TO  INV-SLS-AFT-REPORT-REC             
00418         WRITE INV-SLS-AFT-REPORT-REC                                      
           ELSE                                                                 
00404                                                                   INKRP174
              MOVE PV-HOLD-STORE TO HL2-STORE                                   
00408         WRITE INV-SLS-AFT-REPORT-REC FROM HL2-HEADING-LINE-2              
00409            AFTER ADVANCING 2                                              
00410                                                                   INKRP174
00417         MOVE SPACES                TO  INV-SLS-AFT-REPORT-REC             
00418         WRITE INV-SLS-AFT-REPORT-REC                                      
00389         SUBTRACT 4                 FROM PV-LINE-COUNT                     
           END-IF.                                                              
00419                                                                           
00420 /                                                                 INKRP174
00325  2400-GRAND-TOTALS.                                               INKRP174
00326                                                                   INKRP174
00389      ADD 2                      TO  PV-LINE-COUNT.                INKRP174
                                                                                
           MOVE SPACES     TO INV-SLS-AFT-REPORT-REC.                           
           WRITE INV-SLS-AFT-REPORT-REC.                                        
           MOVE PA-GRD-QTY-SOLD         TO  GT-QTY-SOLD.                        
           MOVE PA-GRD-QTY-RETURN       TO  GT-QTY-RETURN.                      
           MOVE PA-GRD-QTY-NET          TO  GT-QTY-NET.                         
00373      WRITE INV-SLS-AFT-REPORT-REC FROM TT1-TOTAL-LINE-4.                  
