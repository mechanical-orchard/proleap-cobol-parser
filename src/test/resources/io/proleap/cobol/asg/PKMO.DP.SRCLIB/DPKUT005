00001 *************************                                         06/25/92
00002  IDENTIFICATION DIVISION.                                         DPKUT005
00003 *************************                                            LV004
00004  PROGRAM-ID.      DPKUT005.                                          CL**2
00005  AUTHOR.          MICHAEL N TYE, CAP GEMINI AMERICA.                 CL**2
00006  INSTALLATION.    KOHLS DEPARTMENT STORES.                           CL**2
00007  DATE-WRITTEN.    AUGUST 12, 1988.                                   CL**2
00008  DATE-COMPILED.                                                      CL**2
00009                                                                      CL**2
00010 *             THIS PROGRAM USES THE OUTPUTS (AFTER THEY HAVE BEEN    CL**2
00011 *             SORTED BY FILE NAME), FROM PROGRAM DPKUT007 (DDNAME    CL**2
00012 *             PROCXRFO) AND PROGRAM DCOLLRPT (SYSPRINT) AS           CL**2
00013 *             INPUT TO CREATE A FILE TO BE USED TO LOAD AND          CL**2
00014 *             MAINTAIN ONE OF THE SYSTEM CROSS REFERENCE TABLES:     CL**2
00015 *                           FILE TABLE                               CL**2
00016                                                                      CL**2
00017  ENVIRONMENT DIVISION.                                               CL**2
00018  CONFIGURATION SECTION.                                              CL**2
00019  SOURCE-COMPUTER. IBM-3090.                                          CL**2
00020  OBJECT-COMPUTER. IBM-3090.                                          CL**2
00021                                                                      CL**2
00022  INPUT-OUTPUT SECTION.                                               CL**2
00023  FILE-CONTROL.                                                       CL**2
00024      SELECT PROCXREF-EDITED-FILE       ASSIGN TO INP01.              CL**2
00025      SELECT DCOLL-SYSPRINT-FILE        ASSIGN TO INP02.              CL**2
00026      SELECT FILE-TABLE-FILE            ASSIGN TO OUT01.              CL**2
00027      EJECT                                                           CL**2
00028 ***************                                                      CL**2
00029  DATA DIVISION.                                                      CL**2
00030 ***************                                                      CL**2
00031  FILE SECTION.                                                       CL**2
00032  FD  PROCXREF-EDITED-FILE                                            CL**2
00033                              BLOCK CONTAINS 0 RECORDS                CL**2
00034                              RECORD CONTAINS 143 CHARACTERS          CL**2
00035                              LABEL RECORDS ARE STANDARD              CL**2
00036                              RECORDING MODE F                        CL**2
00037                              DATA RECORD IS PROCXREF-EDITED-REC.     CL**2
00038  01  PROCXREF-EDITED-REC     PIC X(143).                             CL**2
00039                                                                      CL**2
00040  FD  DCOLL-SYSPRINT-FILE                                             CL**2
00041                              BLOCK CONTAINS 0 RECORDS                CL**2
00042                              RECORD CONTAINS 133 CHARACTERS          CL**2
00043                              LABEL RECORDS ARE STANDARD              CL**2
00044                              RECORDING MODE F                        CL**2
00045                              DATA RECORD IS DCOLL-SYSPRINT-REC.      CL**2
00046  01  DCOLL-SYSPRINT-REC      PIC X(133).                             CL**2
00047                                                                      CL**2
00048  FD  FILE-TABLE-FILE                                                 CL**2
00049                              BLOCK CONTAINS 0 RECORDS                CL**2
00050                              RECORD CONTAINS 83 CHARACTERS           CL**2
00051                              LABEL RECORDS ARE STANDARD              CL**2
00052                              RECORDING MODE F                        CL**2
00053                              DATA RECORD IS FILE-TABLE-REC.          CL**2
00054  01  FILE-TABLE-REC          PIC X(83).                              CL**2
00055                                                                      CL**2
00056      EJECT                                                           CL**2
00057  WORKING-STORAGE SECTION.                                            CL**2
00058                                                                      CL**2
00059 **   PROCXREF-EDITED-FILE LAYOUT (INPUT)   **                        CL**2
00060                                                                      CL**2
00061  01  PROCXRFI-RECORD.                                                CL**2
00062      05  PROCXRFI-FILE-NAME.                                         CL**2
00063          10  PROCXRFI-FILE-NAME-PREFIX                               CL**2
00064                                  PIC X(06)      VALUE SPACES.        CL**2
00065          10  FILLER              PIC X(38)      VALUE SPACES.        CL**2
00066      05  PROCXRFI-FILE-TYPE      PIC X          VALUE SPACES.        CL**2
00067          88  PROCXRFI-INPUT-FILE                VALUE 'I'.           CL**2
00068          88  PROCXRFI-OUTPUT-FILE               VALUE 'O'.           CL**2
00069          88  PROCXRFI-SYSTEM-FILE               VALUE 'S'.           CL**2
00070          88  PROCXRFI-TEMPORARY-FILE            VALUE 'T'.           CL**2
00071      05  PROCXRFI-PROC-NAME      PIC X(08)      VALUE SPACES.        CL**2
00072      05  PROCXRFI-PROGRAM-NAME   PIC X(08)      VALUE SPACES.        CL**2
00073      05  PROCXRFI-STEP-NAME      PIC X(08)      VALUE SPACES.        CL**2
00074      05  PROCXRFI-GDG-NUM        PIC X(04)      VALUE SPACES.        CL**2
00075          88  PROCXRFI-NEW-GDG                   VALUE '+1  '.        CL**2
00076      05  PROCXRFI-DISPOSITION.                                       CL**2
00077          10  PROCXRFI-DISP1      PIC X(03)      VALUE SPACES.        CL**2
00078          10  PROCXRFI-DISP2      PIC X(03)      VALUE SPACES.        CL**2
00079          10  PROCXRFI-DISP3      PIC X(03)      VALUE SPACES.        CL**2
00080      05  PROCXRFI-MEDIA          PIC X(08)      VALUE SPACES.        CL**2
00081          88  PROCXRFI-CARTRIDGE                 VALUE 'CART'.        CL**2
00082          88  PROCXRFI-DISK                      VALUE 'SYSDA'.       CL**2
00083          88  PROCXRFI-TAPE                      VALUES 'TAPE'        CL**2
00084                                                       'TAPE8'.       CL**2
00085      05  PROCXRFI-EXPDT-RETPD    PIC X(05)      VALUE SPACES.        CL**2
00086          88  PROCXRFI-NO-EXPDT                  VALUE '99000'.       CL**2
00087          88  PROCXRFI-RETPD-5-DAYS              VALUE '5    '.       CL**2
00088          88  PROCXRFI-RETPD-30-DAYS             VALUE '30   '.       CL**2
00089      05  PROCXRFI-SPACE-ALLOC    PIC X(25)      VALUE SPACES.        CL**2
00090      05  PROCXRFI-DCB.                                               CL**2
00091          10  PROCXRFI-DSORG      PIC X(02)      VALUE SPACES.        CL**2
00092              88  PROCXRFI-DSORG-UNSPECIFIED     VALUE SPACES.        CL**2
00093              88  PROCXRFI-PDS                   VALUE 'PO'.          CL**2
00094              88  PROCXRFI-SEQUENTIAL            VALUE 'PS'.          CL**2
00095          10  PROCXRFI-RECFM      PIC X(03)      VALUE SPACES.        CL**2
00096          10  PROCXRFI-LRECL      PIC X(05)      VALUE SPACES.        CL**2
00097          10  PROCXRFI-LRECL-TBL-ALPHA  REDEFINES  PROCXRFI-LRECL.    CL**2
00098              15  PROCXRFI-LRECL-TBL-ELEM-ALPHA  OCCURS 5 TIMES       CL**2
00099                                  INDEXED BY IX-RCL    PIC X.         CL**2
00100          10  PROCXRFI-LRECL-TBL-NUMER  REDEFINES  PROCXRFI-LRECL.    CL**2
00101              15  PROCXRFI-LRECL-TBL-ELEM-NUMER  OCCURS 5 TIMES       CL**2
00102                                  PIC 9.                              CL**2
00103          10  PROCXRFI-BLKSIZE    PIC X(05)      VALUE SPACES.        CL**2
00104      05  PROCXRFI-DDNAME         PIC X(08)      VALUE SPACES.        CL**2
00105                                                                      CL**2
00106      EJECT                                                           CL**2
00107                                                                      CL**2
00108 **   DCOLL-SYSPRINT-FILE LAYOUT (INPUT)  **                          CL**2
00109                                                                      CL**2
00110  01  DCOLLI-RECORD.                                                  CL**2
00111      05  DCOLLI-REPORT-LINE      PIC X(133)     VALUE SPACES.        CL**2
00112      05  DCOLLI-DETAIL-LINE  REDEFINES  DCOLLI-REPORT-LINE.          CL**2
00113          10  FILLER              PIC X(1).                           CL**2
00114          10  DCOLLI-FILE-NAME    PIC X(44).                          CL**2
00115          10  DCOLLI-FILE-NAME-TBL    REDEFINES                       CL**2
00116                                      DCOLLI-FILE-NAME.               CL**2
00117              15  DCOLLI-FILE-NAME-CHAR  OCCURS 44 TIMES              CL**2
00118                                      INDEXED BY IX-DFN               CL**2
00119                                      PIC X.                          CL**2
00120          10  FILLER              PIC X(9).                           CL**2
00121          10  DCOLLI-DSORG        PIC X(04).                          CL**2
00122              88  DCOLLI-DSORG-VSAM              VALUE 'AM  '.        CL**2
00123              88  DCOLLI-DSORG-DIRECT-ACCESS     VALUE 'DA  '.        CL**2
00124              88  DCOLLI-DSORG-GSAM              VALUE 'GS  '.        CL**2
00125              88  DCOLLI-DSORG-INDEXD-SEQUENTIAL VALUE 'IS  '.        CL**2
00126              88  DCOLLI-DSORG-PDS               VALUE 'PO  '.        CL**2
00127              88  DCOLLI-DSORG-SEQUENTIAL        VALUE 'PS  '.        CL**2
00128              88  DCOLLI-DSORG-PDSE              VALUE 'PDSE'.        CL**2
00129          10  FILLER              PIC X(2).                           CL**2
00130          10  DCOLLI-RECFM        PIC X(03).                          CL**2
00131              88  DCOLLI-RECFM-F                 VALUE 'F  '.         CL**2
00132              88  DCOLLI-RECFM-FS                VALUE 'F S'.         CL**2
00133              88  DCOLLI-RECFM-FB                VALUE 'FB '.         CL**2
00134              88  DCOLLI-RECFM-FBA               VALUE 'FBA'.         CL**2
00135              88  DCOLLI-RECFM-FBM               VALUE 'FBM'.         CL**2
00136              88  DCOLLI-RECFM-FBS               VALUE 'FBS'.         CL**2
00137              88  DCOLLI-RECFM-U                 VALUE 'U  '.         CL**2
00138              88  DCOLLI-RECFM-V                 VALUE 'V  '.         CL**2
00139              88  DCOLLI-RECFM-VA                VALUE 'V A'.         CL**2
00140              88  DCOLLI-RECFM-VB                VALUE 'VB '.         CL**2
00141              88  DCOLLI-RECFM-VS                VALUE 'V S'.         CL**2
00142              88  DCOLLI-RECFM-VBS               VALUE 'VBS'.         CL**2
00143          10  FILLER              PIC X(02).                          CL**2
00144          10  DCOLLI-LRECL        PIC 9(05).                          CL**2
00145          10  FILLER              PIC X(01).                          CL**2
00146          10  DCOLLI-BLKSIZE      PIC X(05).                          CL**2
00147          10  FILLER              PIC X(57).                          CL**2
00148                                                                      CL**2
00149      EJECT                                                           CL**2
00150                                                                      CL**2
00151  01  FILETBLO-RECORD.                                                CL**2
00152      05  FILETBLO-FILE-NAME.                                         CL**2
00153          10  FILETBLO-FILE-NAME-PREFIX                               CL**2
00154                                  PIC X(06)      VALUE SPACES.        CL**2
00155          10  FILLER              PIC X(38)      VALUE SPACES.        CL**2
00156      05  FILETBLO-FILE-NAME-TBL  REDEFINES  FILETBLO-FILE-NAME.      CL**2
00157          10  FILETBLO-FILE-NAME-CHAR  OCCURS 44 TIMES                CL**2
00158                                       INDEXED BY IX-FFN              CL**2
00159                                       PIC X.                         CL**2
00160      05  FILETBLO-TYPE-CODE      PIC X(04)      VALUE SPACES.        CL**2
00161          88  FILETBLO-IMS                       VALUE 'IMS '.        CL**2
00162          88  FILETBLO-PDS                       VALUE 'PDS '.        CL**2
00163          88  FILETBLO-SEQUENTIAL                VALUE 'SEQ '.        CL**2
00164          88  FILETBLO-VSAM                      VALUE 'VSAM'.        CL**2
00165      05  FILETBLO-MEDIA-CODE     PIC X          VALUE SPACES.        CL**2
00166          88  FILETBLO-CARTRIDGE                 VALUE 'C'.           CL**2
00167          88  FILETBLO-DISK                      VALUE 'D'.           CL**2
00168          88  FILETBLO-TAPE                      VALUE 'T'.           CL**2
00169      05  FILETBLO-LRECL          PIC S9(5)      VALUE +0   COMP-3.   CL**2
00170      05  FILETBLO-ALLOC-DESC     PIC X(30)      VALUE SPACES.        CL**2
00171      05  FILETBLO-MLTALLOC-CODE  PIC X(01)      VALUE SPACE.         CL**2
00172          88  FILETBLO-NO-ALLOC                  VALUE SPACE.         CL**2
00173          88  FILETBLO-SINGLE-ALLOC              VALUE 'S'.           CL**2
00174          88  FILETBLO-MULTI-ALLOC               VALUE 'M'.           CL**2
00175                                                                      CL**2
00176  01  PROGRAM-SWITCHES.                                               CL**2
00177      05  PS-NO                   PIC X          VALUE 'N'.           CL**2
00178      05  PS-YES                  PIC X          VALUE 'Y'.           CL**2
00179      05  PS-EOF-DCOLLI           PIC X          VALUE 'N'.           CL**2
00180          88  EOF-DCOLLI                         VALUE 'Y'.           CL**2
00181      05  PS-EOF-PROCXRFI         PIC X          VALUE 'N'.           CL**2
00182          88  EOF-PROCXRFI                       VALUE 'Y'.           CL**2
00183                                                                      CL**2
00184      EJECT                                                           CL**2
00185                                                                      CL**2
00186  01  WORK-AREA.                                                      CL**2
00187      05  SAVE-FILETBLO-FILE-NAME PIC X(44)      VALUE SPACES.        CL**2
00188      05  SUB1                    PIC 9(04)      VALUE ZEROES.        CL**2
00189      05  WS-LRECL-TABLE          PIC 9(05)      VALUE ZEROES.        CL**2
00190      05  WS-LRECL-TBL  REDEFINES  WS-LRECL-TABLE.                    CL**2
00191          10  WS-LRECL-ELEM  OCCURS 5 TIMES  PIC 9.                   CL**2
00192      05  WS-VARIABLES.                                               CL**2
00193          10  WS-A                PIC X          VALUE 'A'.           CL**2
00194          10  WS-D                PIC X          VALUE 'D'.           CL**2
00195          10  WS-G                PIC X          VALUE 'G'.           CL**2
00196          10  WS-LEFT-PAREN       PIC X          VALUE '('.           CL**2
00197          10  WS-LEFT-PAREN-COUNT PIC 9          VALUE ZERO.          CL**2
00198          10  WS-MEDIA-CODES.                                         CL**2
00199              15  WS-MEDIA-CODE-CARTRIDGE                             CL**2
00200                                  PIC X          VALUE 'C'.           CL**2
00201              15  WS-MEDIA-CODE-DISK                                  CL**2
00202                                  PIC X          VALUE 'D'.           CL**2
00203              15  WS-MEDIA-CODE-TAPE                                  CL**2
00204                                  PIC X          VALUE 'T'.           CL**2
00205          10  WS-MLTALLOC-CODES.                                      CL**2
00206              15  WS-MLTALLOC-CODE-SINGLE                             CL**2
00207                                  PIC X          VALUE 'S'.           CL**2
00208              15  WS-MLTALLOC-CODE-MULTIPLE                           CL**2
00209                                  PIC X          VALUE 'M'.           CL**2
00210          10  WS-NEW              PIC X(03)      VALUE 'NEW'.         CL**2
00211          10  WS-PERIOD           PIC X          VALUE '.'.           CL**2
00212          10  WS-SEQ-DSORG        PIC X(04)      VALUE 'PS  '.        CL**2
00213          10  WS-T                PIC X          VALUE 'T'.           CL**2
00214          10  WS-TYPE-CODES.                                          CL**2
00215              15  WS-TYPE-CODE-IMS                                    CL**2
00216                                  PIC X(04)      VALUE 'IMS '.        CL**2
00217              15  WS-TYPE-CODE-PDS                                    CL**2
00218                                  PIC X(04)      VALUE 'PDS '.        CL**2
00219              15  WS-TYPE-CODE-SEQUENTIAL                             CL**2
00220                                  PIC X(04)      VALUE 'SEQ '.        CL**2
00221              15  WS-TYPE-CODE-VSAM                                   CL**2
00222                                  PIC X(04)      VALUE 'VSAM'.        CL**2
00223          10  WS-V                PIC X          VALUE 'V'.           CL**2
00224                                                                      CL**2
00225      EJECT                                                           CL**2
00226                                                                      CL**2
00227 ********************                                                 CL**2
00228  PROCEDURE DIVISION.                                                 CL**2
00229 ********************                                                 CL**2
00230                                                                      CL**2
00231                                                                      CL**2
00232      OPEN INPUT     PROCXREF-EDITED-FILE                             CL**2
00233                     DCOLL-SYSPRINT-FILE                              CL**2
00234           OUTPUT    FILE-TABLE-FILE.                                 CL**2
00235                                                                      CL**2
00236                                                                      CL**2
00237      PERFORM X100-READ-DCOLL-FILE.                                   CL**2
00238                                                                      CL**2
00239      IF NOT EOF-DCOLLI                                               CL**2
00240         PERFORM X200-READ-PROCXREF-FILE                              CL**2
00241         IF NOT EOF-PROCXRFI                                          CL**2
00242            PERFORM A100-PROCESS-PROCXREF-FILE                        CL**2
00243                    UNTIL EOF-PROCXRFI                                CL**2
00244         END-IF                                                       CL**2
00245      END-IF.                                                         CL**2
00246                                                                      CL**2
00247      CLOSE          PROCXREF-EDITED-FILE                             CL**2
00248                     DCOLL-SYSPRINT-FILE                              CL**2
00249                     FILE-TABLE-FILE.                                 CL**2
00250                                                                      CL**2
00251                                                                      CL**2
00252      STOP RUN.                                                       CL**2
00253                                                                      CL**2
00254                                                                      CL**2
00255      EJECT                                                           CL**2
00256                                                                      CL**2
00257 ******************************************************************   CL**2
00258 *** A100 THIS PARAGRAPH PROCESSES THE INPUT FILE FROM "PROCXREF"     CL**2
00259 ***      BY MATCHING THE FILE (DSN) FROM THE JCL TO THE LISTING      CL**2
00260 ***      FROM "DCOLL".  IF A MATCH IS FOUND, IT PERFORMS THE PARA-   CL**2
00261 ***      GRAPH WHICH PROCESSES THE OUTPUT FILE TABLE DATA RECORD.    CL**2
00262 ******************************************************************   CL**2
00263  A100-PROCESS-PROCXREF-FILE.                                         CL**2
00264                                                                      CL**2
00265                                                                      CL**2
00266      IF FILETBLO-FILE-NAME  =  SPACES                                CL**2
00267          PERFORM B100-INIT-FILETBLO-FIELDS                           CL**2
00268      ELSE                                                            CL**2
00269          IF PROCXRFI-FILE-NAME  NOT =  FILETBLO-FILE-NAME            CL**2
00270              IF ( FILETBLO-MEDIA-CODE  =  SPACE )  AND               CL**2
00271                 ( FILETBLO-MLTALLOC-CODE  =  SPACE )                 CL**2
00272                  MOVE 'D'         TO FILETBLO-MEDIA-CODE             CL**2
00273              END-IF                                                  CL**2
00274              PERFORM B200-PROCESS-TYPE-CODE                          CL**2
00275              WRITE FILE-TABLE-REC                                    CL**2
00276                                 FROM FILETBLO-RECORD                 CL**2
00277              PERFORM B100-INIT-FILETBLO-FIELDS                       CL**2
00278          END-IF                                                      CL**2
00279      END-IF.                                                         CL**2
00280                                                                      CL**2
00281      PERFORM B300-FILL-FILETBLO-FIELDS.                              CL**2
00282                                                                      CL**2
00283      PERFORM X200-READ-PROCXREF-FILE.                                CL**2
00284                                                                      CL**2
00285  A100-END.                                                           CL**2
00286      EXIT.                                                           CL**2
00287      EJECT                                                           CL**2
00288                                                                      CL**2
00289 ******************************************************************   CL**2
00290 *** B100 THIS PARAGRAPH INITIALIZES THE FIELDS IN THE OUTPUT         CL**2
00291 ***      RECORD TO ENSURE NO CARRY-OVER OF DATA.                     CL**2
00292 ******************************************************************   CL**2
00293  B100-INIT-FILETBLO-FIELDS.                                          CL**2
00294                                                                      CL**2
00295                                                                      CL**2
00296      MOVE SPACES                  TO FILETBLO-FILE-NAME              CL**2
00297                                      FILETBLO-TYPE-CODE              CL**2
00298                                      FILETBLO-MEDIA-CODE             CL**2
00299                                      FILETBLO-ALLOC-DESC             CL**2
00300                                      FILETBLO-MLTALLOC-CODE.         CL**2
00301                                                                      CL**2
00302                                                                      CL**2
00303      MOVE ZEROES                  TO FILETBLO-LRECL.                 CL**2
00304                                                                      CL**2
00305                                                                      CL**2
00306  B100-END.                                                           CL**2
00307      EXIT.                                                           CL**2
00308      EJECT                                                           CL**2
00309                                                                      CL**2
00310 ******************************************************************   CL**2
00311 *** B200 THIS PARAGRAPH PROCESSES THE TYPE-CODE BY CHECKING FOR AN   CL**2
00312 ***      OVERRIDE TYPE-CODE IF IT EXISTS, AND BY FILLING THE TYPE-   CL**2
00313 ***      CODE FIELD IT IT HAS NOT ALREADY BEEN FILLED.               CL**2
00314 ******************************************************************   CL**2
00315  B200-PROCESS-TYPE-CODE.                                             CL**2
00316                                                                      CL**2
00317                                                                      CL**2
00318      IF FILETBLO-FILE-NAME-PREFIX  =  'PKDIMS'                       CL**2
00319          MOVE WS-TYPE-CODE-IMS    TO FILETBLO-TYPE-CODE              CL**2
00320      ELSE                                                            CL**2
00321          PERFORM B210-PROCESS-DCOLLI-FILE                            CL**2
00322      END-IF.                                                         CL**2
00323                                                                      CL**2
00324      IF FILETBLO-TYPE-CODE  =  SPACES                                CL**2
00325          IF FILETBLO-ALLOC-DESC  =  SPACES                           CL**2
00326              SET IX-FFN               TO 1                           CL**2
00327              SEARCH FILETBLO-FILE-NAME-CHAR                          CL**2
00328                  VARYING IX-FFN                                      CL**2
00329                  WHEN FILETBLO-FILE-NAME-CHAR (IX-FFN)  =            CL**2
00330                      WS-LEFT-PAREN                                   CL**2
00331                      MOVE WS-TYPE-CODE-PDS                           CL**2
00332                                   TO FILETBLO-TYPE-CODE              CL**2
00333          ELSE                                                        CL**2
00334              MOVE WS-TYPE-CODE-SEQUENTIAL                            CL**2
00335                                   TO FILETBLO-TYPE-CODE              CL**2
00336          END-IF                                                      CL**2
00337      END-IF.                                                         CL**2
00338                                                                      CL**2
00339  B200-END.                                                           CL**2
00340      EXIT.                                                           CL**2
00341      EJECT                                                           CL**2
00342                                                                      CL**2
00343 ******************************************************************   CL**2
00344 *** B210 THIS PARAGRAPH PROCESSES THE INPUT FILE FROM DCOLLI BY      CL**2
00345 ***      READING THE FILE UNTIL ITS FILE NAME IS GREATER THAN OR     CL**2
00346 ***      OR EQUAL TO THE PROCXREF FILE NAME.  IF THEY ARE EQUAL,     CL**2
00347 ***      THE DSORG CAN BE OBTAINED FROM THE DCOLLI RECORD.           CL**2
00348 ******************************************************************   CL**2
00349  B210-PROCESS-DCOLLI-FILE.                                           CL**2
00350                                                                      CL**2
00351                                                                      CL**2
00352      MOVE SPACES                  TO SAVE-FILETBLO-FILE-NAME.        CL**2
00353                                                                      CL**2
00354      UNSTRING FILETBLO-FILE-NAME DELIMITED BY WS-LEFT-PAREN          CL**2
00355          INTO SAVE-FILETBLO-FILE-NAME.                               CL**2
00356                                                                      CL**2
00357                                                                      CL**2
00358      IF ( NOT EOF-DCOLLI  AND                                        CL**2
00359           DCOLLI-FILE-NAME IS LESS THAN SAVE-FILETBLO-FILE-NAME )    CL**2
00360          PERFORM X100-READ-DCOLL-FILE                                CL**2
00361              UNTIL ( DCOLLI-FILE-NAME IS NOT LESS THAN               CL**2
00362                                       SAVE-FILETBLO-FILE-NAME )      CL**2
00363              OR EOF-DCOLLI                                           CL**2
00364      END-IF.                                                         CL**2
00365                                                                      CL**2
00366          IF ( NOT EOF-DCOLLI )  AND                                  CL**2
00367             ( DCOLLI-FILE-NAME  =  SAVE-FILETBLO-FILE-NAME )         CL**2
00368              IF DCOLLI-DSORG-VSAM                                    CL**2
00369                  MOVE WS-TYPE-CODE-VSAM                              CL**2
00370                                   TO FILETBLO-TYPE-CODE              CL**2
00371              ELSE                                                    CL**2
00372                  IF DCOLLI-DSORG-PDS                                 CL**2
00373                      MOVE WS-TYPE-CODE-PDS                           CL**2
00374                                   TO FILETBLO-TYPE-CODE              CL**2
00375                  ELSE                                                CL**2
00376                      IF DCOLLI-DSORG-SEQUENTIAL                      CL**2
00377                          MOVE WS-TYPE-CODE-SEQUENTIAL                CL**2
00378                                   TO FILETBLO-TYPE-CODE              CL**2
00379                      ELSE                                            CL**2
00380                          MOVE DCOLLI-DSORG                           CL**2
00381                                   TO FILETBLO-TYPE-CODE              CL**2
00382                      END-IF                                          CL**2
00383                   END-IF                                             CL**2
00384               END-IF                                                 CL**2
00385          END-IF.                                                     CL**2
00386                                                                      CL**2
00387  B210-END.                                                           CL**2
00388      EXIT.                                                           CL**2
00389      EJECT                                                           CL**2
00390                                                                      CL**2
00391 ******************************************************************   CL**2
00392 *** B300 THIS PARAGRAPH FILLS THE FIELDS IN THE OUTPUT RECORD WITH   CL**2
00393 ***      THE APPROPRIATE DATA ACCORDING TO THE INPUT FILES.  THIS    CL**2
00394 ***      INCLUDES RIGHT-JUSTIFYING THE INPUT LRECL WHICH IS STORED   CL**2
00395 ***      AS ALPHANUMERIC (LEFT-JUSTIFIED) IN THE INPUT FILE.         CL**2
00396 ******************************************************************   CL**2
00397  B300-FILL-FILETBLO-FIELDS.                                          CL**2
00398                                                                      CL**2
00399                                                                      CL**2
00400      MOVE PROCXRFI-FILE-NAME      TO FILETBLO-FILE-NAME.             CL**2
00401                                                                      CL**2
00402                                                                      CL**2
00403      IF PROCXRFI-CARTRIDGE                                           CL**2
00404          MOVE WS-MEDIA-CODE-CARTRIDGE                                CL**2
00405                                   TO FILETBLO-MEDIA-CODE             CL**2
00406          MOVE WS-TYPE-CODE-SEQUENTIAL                                CL**2
00407                                   TO FILETBLO-TYPE-CODE              CL**2
00408      ELSE                                                            CL**2
00409          IF PROCXRFI-DISK                                            CL**2
00410              MOVE WS-MEDIA-CODE-DISK                                 CL**2
00411                                   TO FILETBLO-MEDIA-CODE             CL**2
00412          ELSE                                                        CL**2
00413              IF PROCXRFI-TAPE                                        CL**2
00414                  MOVE WS-MEDIA-CODE-TAPE                             CL**2
00415                                   TO FILETBLO-MEDIA-CODE             CL**2
00416                  MOVE WS-TYPE-CODE-SEQUENTIAL                        CL**2
00417                                   TO FILETBLO-TYPE-CODE              CL**2
00418              END-IF                                                  CL**2
00419      END-IF.                                                         CL**2
00420                                                                      CL**2
00421      IF PROCXRFI-LRECL  NOT  =  SPACES                               CL**2
00422          MOVE 5                   TO SUB1                            CL**2
00423          MOVE ZEROES              TO WS-LRECL-TABLE                  CL**2
00424          PERFORM B310-RIGHT-JUSTIFY-LRECL                            CL**2
00425              VARYING IX-RCL FROM 5 BY -1                             CL**2
00426              UNTIL IX-RCL < 1                                        CL**2
00427          MOVE WS-LRECL-TABLE      TO FILETBLO-LRECL                  CL**2
00428      END-IF.                                                         CL**2
00429                                                                      CL**2
00430      IF FILETBLO-ALLOC-DESC  =  SPACES                               CL**2
00431          IF PROCXRFI-SPACE-ALLOC  NOT =  SPACES                      CL**2
00432              MOVE PROCXRFI-SPACE-ALLOC                               CL**2
00433                                   TO FILETBLO-ALLOC-DESC             CL**2
00434              MOVE WS-MLTALLOC-CODE-SINGLE                            CL**2
00435                                   TO FILETBLO-MLTALLOC-CODE          CL**2
00436          END-IF                                                      CL**2
00437      ELSE                                                            CL**2
00438          IF PROCXRFI-SPACE-ALLOC  NOT =  SPACES                      CL**2
00439              IF PROCXRFI-SPACE-ALLOC  NOT =  FILETBLO-ALLOC-DESC     CL**2
00440                  MOVE PROCXRFI-SPACE-ALLOC                           CL**2
00441                                   TO FILETBLO-ALLOC-DESC             CL**2
00442                  MOVE WS-MLTALLOC-CODE-MULTIPLE                      CL**2
00443                                   TO FILETBLO-MLTALLOC-CODE          CL**2
00444              END-IF                                                  CL**2
00445          END-IF                                                      CL**2
00446      END-IF.                                                         CL**2
00447                                                                      CL**2
00448      IF ( PROCXRFI-GDG-NUM  NOT =  SPACES )  OR                      CL**2
00449         ( ( PROCXRFI-DISP1  =  WS-NEW )  AND                         CL**2
00450           ( PROCXRFI-DSORG-UNSPECIFIED ) )                           CL**2
00451          MOVE WS-TYPE-CODE-SEQUENTIAL                                CL**2
00452                                   TO FILETBLO-TYPE-CODE              CL**2
00453      END-IF.                                                         CL**2
00454                                                                      CL**2
00455  B300-END.                                                           CL**2
00456      EXIT.                                                           CL**2
00457      EJECT                                                           CL**2
00458                                                                      CL**2
00459 ******************************************************************   CL**2
00460 *** B310 THIS PARAGRAPH RIGHT-JUSTIFIES THE INPUT LRECL WHICH IS     CL**2
00461 ***      ALPHANUMERIC (LEFT-JUSTIFIED) IN THE INPUT FILE.            CL**2
00462 ******************************************************************   CL**2
00463  B310-RIGHT-JUSTIFY-LRECL.                                           CL**2
00464                                                                      CL**2
00465                                                                      CL**2
00466      IF PROCXRFI-LRECL-TBL-ELEM-ALPHA (IX-RCL)  NOT =  SPACE         CL**2
00467          MOVE PROCXRFI-LRECL-TBL-ELEM-NUMER (IX-RCL)                 CL**2
00468                                   TO WS-LRECL-ELEM (SUB1)            CL**2
00469          SUBTRACT 1             FROM SUB1                            CL**2
00470      END-IF.                                                         CL**2
00471                                                                      CL**2
00472  B310-END.                                                           CL**2
00473      EXIT.                                                           CL**2
00474      EJECT                                                           CL**2
00475                                                                      CL**2
00476 ******************************************************************   CL**2
00477 *** X100 THIS PARAGRAPH PERFORMS THE READ    FROM "DCOLL".           CL**2
00478 ******************************************************************   CL**2
00479  X100-READ-DCOLL-FILE.                                               CL**2
00480                                                                      CL**2
00481      MOVE SPACES TO DCOLLI-DSORG.                                    CL**4
00482                                                                      CL**2
00483      PERFORM X105-READ-DCOLL-FILE UNTIL                              CL**2
00484             (EOF-DCOLLI)            OR                               CL**2
00485             (DCOLLI-DSORG = 'PDSE') OR                               CL**2
00486             (DCOLLI-DSORG = 'IS  ') OR                               CL**2
00487             (DCOLLI-DSORG = 'PS  ') OR                               CL**2
00488             (DCOLLI-DSORG = 'PO  ') OR                               CL**2
00489             (DCOLLI-DSORG = 'DA  ') OR                               CL**2
00490             (DCOLLI-DSORG = 'U   ') OR                               CL**2
00491             (DCOLLI-DSORG = 'GS  ') OR                               CL**2
00492             (DCOLLI-DSORG = 'AM  ') OR                               CL**2
00493             (DCOLLI-DSORG = '????').                                 CL**2
00494                                                                      CL**2
00495      IF ( NOT EOF-DCOLLI )                                           CL**2
00496         PERFORM X110-PROCESS-VSAM-GDG                                CL**2
00497             VARYING IX-DFN FROM 1 BY 1                               CL**2
00498             UNTIL IX-DFN  >  44                                      CL**2
00499      END-IF.                                                         CL**2
00500                                                                      CL**2
00501                                                                      CL**2
00502  X100-END.                                                           CL**2
00503      EXIT.                                                           CL**2
00504      EJECT                                                           CL**2
00505                                                                      CL**2
00506 ******************************************************************   CL**2
00507 *** X105 THIS PARAGRAPH READS THE INPUT FILE FROM "DCOLL".           CL**2
00508 ******************************************************************   CL**2
00509  X105-READ-DCOLL-FILE.                                               CL**2
00510                                                                      CL**2
00511      READ DCOLL-SYSPRINT-FILE   INTO DCOLLI-RECORD                   CL**2
00512          AT END                                                      CL**2
00513              MOVE PS-YES          TO PS-EOF-DCOLLI.                  CL**2
00514                                                                      CL**2
00515  X105-END.                                                           CL**2
00516      EXIT.                                                           CL**2
00517      EJECT                                                           CL**2
00518                                                                      CL**2
00519 ******************************************************************   CL**2
00520 *** X110 THIS PARAGRAPH PROCESSES VSAM AND GDG DATASETS.             CL**2
00521 ******************************************************************   CL**2
00522  X110-PROCESS-VSAM-GDG.                                              CL**2
00523                                                                      CL**2
00524                                                                      CL**2
00525      IF DCOLLI-FILE-NAME-CHAR (IX-DFN)  =  SPACE                     CL**2
00526          SET IX-DFN               TO 44                              CL**2
00527      ELSE                                                            CL**2
00528          IF ( DCOLLI-FILE-NAME-CHAR (IX-DFN)  =  WS-PERIOD )  AND    CL**2
00529             ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 1)  =  WS-D )   AND    CL**2
00530             ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 2)  =  WS-A )   AND    CL**2
00531             ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 3)  =  WS-T )   AND    CL**2
00532             ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 4)  =  WS-A )   AND    CL**2
00533             ( DCOLLI-DSORG-VSAM )                                    CL**2
00534              IF IX-DFN  =  40  OR                                    CL**2
00535                 ( IX-DFN  <  40  AND                                 CL**2
00536                   DCOLLI-FILE-NAME-CHAR (IX-DFN + 5)  =  SPACE )     CL**2
00537                  PERFORM X111-SPACE-OUT-REMAINDER                    CL**2
00538                      VARYING IX-DFN FROM IX-DFN BY 1                 CL**2
00539                      UNTIL IX-DFN  >  44                             CL**2
00540              END-IF                                                  CL**2
00541          ELSE                                                        CL**2
00542              IF ( DCOLLI-FILE-NAME-CHAR (IX-DFN) = WS-PERIOD ) AND   CL**2
00543                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 1) = WS-G )  AND   CL**2
00544                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 2) NUMERIC ) AND   CL**2
00545                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 3) NUMERIC ) AND   CL**2
00546                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 4) NUMERIC ) AND   CL**2
00547                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 5) NUMERIC ) AND   CL**2
00548                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 6) = WS-V )  AND   CL**2
00549                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 7) NUMERIC ) AND   CL**2
00550                 ( DCOLLI-FILE-NAME-CHAR (IX-DFN + 8) NUMERIC )       CL**2
00551                  IF IX-DFN  =  36  OR                                CL**2
00552                     ( IX-DFN  <  36  AND                             CL**2
00553                     DCOLLI-FILE-NAME-CHAR (IX-DFN + 9)  =  SPACE )   CL**2
00554                      MOVE WS-SEQ-DSORG                               CL**2
00555                                   TO DCOLLI-DSORG                    CL**2
00556                      PERFORM X111-SPACE-OUT-REMAINDER                CL**2
00557                          VARYING IX-DFN FROM IX-DFN BY 1             CL**2
00558                          UNTIL IX-DFN  >  44                         CL**2
00559                  END-IF                                              CL**2
00560              END-IF                                                  CL**2
00561          END-IF                                                      CL**2
00562      END-IF.                                                         CL**2
00563                                                                      CL**2
00564  X110-END.                                                           CL**2
00565      EXIT.                                                           CL**2
00566      EJECT                                                           CL**2
00567                                                                      CL**2
00568 ******************************************************************   CL**2
00569 *** X111 THIS PARAGRAPH SPACES OUT THE REMAINDER OF THE FILE NAME.   CL**2
00570 ******************************************************************   CL**2
00571  X111-SPACE-OUT-REMAINDER.                                           CL**2
00572                                                                      CL**2
00573                                                                      CL**2
00574      MOVE SPACE                   TO                                 CL**2
00575                                   DCOLLI-FILE-NAME-CHAR (IX-DFN).    CL**2
00576                                                                      CL**2
00577                                                                      CL**2
00578  X111-END.                                                           CL**2
00579      EXIT.                                                           CL**2
00580      EJECT                                                           CL**2
00581                                                                      CL**2
00582 ******************************************************************   CL**2
00583 *** X200 THIS PARAGRAPH READS THE INPUT FILE FROM "PROCXREF".        CL**2
00584 ******************************************************************   CL**2
00585  X200-READ-PROCXREF-FILE.                                            CL**2
00586                                                                      CL**2
00587                                                                      CL**2
00588      READ PROCXREF-EDITED-FILE  INTO PROCXRFI-RECORD                 CL**2
00589          AT END                                                      CL**2
00590              MOVE PS-YES          TO PS-EOF-PROCXRFI.                CL**2
00591                                                                      CL**2
00592                                                                      CL**2
00593  X200-END.                                                           CL**2
00594      EXIT.                                                           CL**2
00595      EJECT                                                           CL**2
