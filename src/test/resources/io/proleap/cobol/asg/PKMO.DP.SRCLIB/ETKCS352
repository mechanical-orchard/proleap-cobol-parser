      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    ETKCS352.                                                 
       AUTHOR.        DAN GRUBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10-09-96.                                                 
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    E352 - ADD/UPDATE/DELETE SIZE                               *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO ADD, UPDATE, OR DELETE A    *        
      *    SIZE.  THIS PROGRAM CAN BE ACCESSED FROM EITHER THE SIZE    *        
      *    CRITERIA PROGRAM (ETKCS351) OR THE SIZE LIST PROGRAM        *        
      *    (ETKCS353).  DIFFERENT EDIT CHECKS ARE PERFORMED BASED ON   *        
      *    WHICH FUNCTION IS SELECTED.  THE NRF CODE MUST BE NUMERIC   *        
      *    AND LESS THAN 98000 FOR ALL FUNCTIONS, SINCE CODES GREATER  *        
      *    THAN OR EQUAL TO 98000 ARE RESERVED FOR SIZE RANGES AND     *        
      *    WILL BE PROCESSED IN VENDOR STYLE MANAGEMENT.  THE KOHLS    *        
      *    AND PRIMARY DESCRIPTIONS ARE BOTH REQUIRED AND THE          *        
      *    SECONDARY DESCRIPTION IS OPTIONAL FOR THE ADD AND UPDATE    *        
      *    FUNCTIONS.  ALL OF THE DESCRIPTION FIELDS ARE ALPHANUMERIC  *        
      *    BUT CANNOT CONTAIN THE CHARACTERS *, %, _, OR '.            *        
      *                                                                *        
      *    A CHECK IS ALSO MADE TO PREVENT EXPIRED SIZES FROM BEING    *        
      *    UPDATED OR DELETED.  EXPIRED SIZES CAN BE RE-ACTIVATED OR   *        
      *    RE-ADDED TO THE DATABASE.  IN THIS CASE, THE APPROPRIATE    *        
      *    DB2 ROW IS UPDATED INSTEAD OF INSERTING A NEW ROW TO THE    *        
      *    SIZE TABLE.                                                 *        
      *                                                                *        
      *    ONCE ALL OF THE REQUESTED SIZES ARE PROCESSED, THE USER     *        
      *    MUST HIT THE PF3 KEY TO RETURN CONTROL TO EITHER THE SIZE   *        
      *    CRITERIA OR SIZE LIST SCREEN.                               *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                         
      *   11/06/2006  P. KEBER      WR28222 - SIZE PROFILE PROJECT              
      *   ADD STANDARD SIZE CODE AND DESCRIPTION TO THE SCREEN.                 
W32250*                                                                         
W32250*   08/17/2007  JILL LIN      WR32250 - ECOMM NEW ATTRIBUTES              
W32250*   ADD EXTENDED SIZE CODE AND DESCRIPTION TO THE SCREEN.                 
      *---------------------------------------------------------------*         
      * 10/11/2021 GERMAN BANDA                    WR/IR #: CHG0262418*         
      *            - CHANGE CALL OF DPKUT100 TO                       *         
      *              CALL DP010I-NUMERIC-EDIT-ROUTINE                 *         
      *              MAKING THE CALL DYNAMIC VS STATIC                *         
      *                                                               *         
      *---------------------------------------------------------------*         
CICS  *  CHANGED 10/28/2022    BY BARB SCHULTZ    WR: CHG0277210      *         
CICS  *                                                               *         
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   *         
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    *         
      *                                                               *         
      *---------------------------------------------------------------*         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-SIZE-GP-CODE                PIC S9(04)  VALUE ZERO            
                                                          COMP.                 
           05  DK-SIZE-GP-DESC                PIC  X(30)  VALUE SPACE.          
           05  DK-KOHLS-SIZE-CODE             PIC S9(09)  VALUE ZERO            
                                                          COMP.                 
           05  DK-NRF-SIZE-CODE               PIC  X(05)  VALUE SPACE.          
           05  DK-KOHLS-DESC                  PIC  X(10)  VALUE SPACE.          
           05  DK-EXP-SIZE-IND                PIC  X(01)  VALUE SPACE.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'ETKCS352'.          
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'ET352A  '.          
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'ETKM352 '.          
           05  PC-APPL-MAP              PIC S9(04)  VALUE  +1                   
                                                    COMP.                       
           05  PC-SIZE-RNG-CDE          PIC  X(01)  VALUE 'I'.                  
           05  PC-PERCENT               PIC  X(01)  VALUE '%'.                  
           05  PC-ASTERIK               PIC  X(01)  VALUE '*'.                  
           05  PC-UNDERLINE             PIC  X(01)  VALUE '_'.                  
           05  PC-QUOTE                 PIC  X(01)  VALUE ''''.                 
           05  PC-VALID-MAX-NRF-CODE    PIC  9(05)  VALUE  97999.               
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-INTER-APPL-CODES.                                             
               10  PC-IA-ADD-CRIT-SPEC  PIC  X(01)  VALUE '1'.                  
               10  PC-IA-UPD-CRIT-SPEC  PIC  X(01)  VALUE '2'.                  
               10  PC-IA-DEL-CRIT-SPEC  PIC  X(01)  VALUE '3'.                  
               10  PC-IA-ADD-LIST       PIC  X(01)  VALUE '4'.                  
               10  PC-IA-UPD-LIST       PIC  X(01)  VALUE '5'.                  
               10  PC-IA-DEL-LIST       PIC  X(01)  VALUE '6'.                  
           05  PC-MSG-VAR-SYMBOLIC      PIC  X(04)  VALUE '&VAR'.               
                                                                                
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
               10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.               
               10  PC-TSYMSG-00034      PIC  9(05)  VALUE  00034.               
               10  PC-TSYMSG-00059      PIC  9(05)  VALUE  00059.               
               10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
               10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
               10  PC-TSYMSG-00115      PIC  9(05)  VALUE  00115.               
               10  PC-TSYMSG-00229      PIC  9(05)  VALUE  00229.               
               10  PC-TSYMSG-00354      PIC  9(05)  VALUE  00354.               
               10  PC-TSYMSG-01425      PIC  9(05)  VALUE  01425.               
               10  PC-TSYMSG-01426      PIC  9(05)  VALUE  01426.               
               10  PC-TSYMSG-01427      PIC  9(05)  VALUE  01427.               
               10  PC-TSYMSG-01429      PIC  9(05)  VALUE  01429.               
               10  PC-TSYMSG-01430      PIC  9(05)  VALUE  01430.               
               10  PC-TSYMSG-01431      PIC  9(05)  VALUE  01431.               
               10  PC-TSYMSG-01432      PIC  9(05)  VALUE  01432.               
               10  PC-TSYMSG-01433      PIC  9(05)  VALUE  01433.               
               10  PC-TSYMSG-01493      PIC  9(05)  VALUE  01493.               
               10  PC-TSYMSG-01494      PIC  9(05)  VALUE  01494.               
               10  PC-TSYMSG-03517      PIC  9(05)  VALUE  03517.               
               10  PC-TSYMSG-03518      PIC  9(05)  VALUE  03518.               
W32250         10  PC-TSYMSG-03591      PIC  9(05)  VALUE  03591.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-ERROR-SW                    PIC  X(01)   VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
           05  PS-DONE-SW                     PIC  X(01)   VALUE 'N'.           
               88  PS-DONE                                 VALUE 'Y'.           
               88  PS-NOT-DONE                             VALUE 'N'.           
           05  PS-SIZE-FOUND-SW               PIC  X(01)   VALUE 'N'.           
               88  PS-SIZE-FOUND                           VALUE 'Y'.           
               88  PS-SIZE-NOT-FOUND                       VALUE 'N'.           
               88  PS-MULT-SIZES-FOUND                     VALUE 'M'.           
           05  PS-NRFCODE-FOUND-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-NRFCODE-FOUND                        VALUE 'Y'.           
               88  PS-NRFCODE-NOT-FOUND                    VALUE 'N'.           
               88  PS-MULT-NRFCODES-FOUND                  VALUE 'M'.           
           05  PS-NRFCODE-EXPIRED-SW          PIC  X(01)   VALUE 'N'.           
               88  PS-NRFCODE-EXPIRED                      VALUE 'Y'.           
               88  PS-NRFCODE-NOT-EXPIRED                  VALUE 'N'.           
           05  PS-ENOUGH-DATA-SW              PIC  X(01)   VALUE 'Y'.           
               88  PS-ENOUGH-DATA                          VALUE 'Y'.           
               88  PS-NOT-ENOUGH-DATA                      VALUE 'N'.           
           05  PS-INVALID-NRF-CODE-SW         PIC  X(01)   VALUE 'N'.           
               88  PS-INVALID-NRF-CODE                     VALUE 'Y'.           
               88  PS-NOT-INVALID-NRF-CODE                 VALUE 'N'.           
W32250     05  PS-INVALID-EXTD-SZ-SW          PIC  X(01)   VALUE 'N'.           
W32250         88  PS-INVALID-EXTD-SZ                      VALUE 'Y'.           
W32250         88  PS-NOT-INVALID-EXTD-SZ                  VALUE 'N'.           
           05  PS-STANDARD-SIZE-FOUND-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-STANDARD-SIZE-FOUND                  VALUE 'Y'.           
               88  PS-STANDARD-SIZE-NOT-FOUND              VALUE 'N'.           
           05  PS-INVALID-KDESC-SW            PIC  X(01)   VALUE 'N'.           
               88  PS-INVALID-KDESC                        VALUE 'Y'.           
               88  PS-NOT-INVALID-KDESC                    VALUE 'N'.           
           05  PS-FATAL-ERROR-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-FATAL-ERROR                          VALUE 'Y'.           
               88  PS-NOT-FATAL-ERROR                      VALUE 'N'.           
           05  PS-CHECK-KDESC-SW              PIC  X(01)   VALUE 'N'.           
               88  PS-CHECK-KDESC                          VALUE 'Y'.           
               88  PS-DONT-CHECK-KDESC                     VALUE 'N'.           
           05  PS-BOTTOM-OF-LIST-SW           PIC  X(01)   VALUE 'N'.           
               88  PS-BOTTOM-OF-LIST                       VALUE 'Y'.           
               88  PS-NOT-BOTTOM-OF-LIST                   VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-SUB                   PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-INVALID-CHARS-CNT     PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-NRF-SIZE-CODE-N       PIC  9(05)  VALUE ZEROES.               
           05  PV-NRF-SIZE-CODE-X       PIC  X(05)  VALUE SPACES.               
           05  PV-KOHLS-SIZE-CODE-N     PIC  9(05)  VALUE ZEROES.               
           05  PV-TOTAL-INV-CHARS       PIC  9(03)  VALUE ZEROES.               
           05  PV-SIZE-GP-CODE-N        PIC  9(02)  VALUE ZEROES.               
           05  PV-SIZE-GP-CODE-X        PIC  X(02)  VALUE SPACES.               
           05  PV-END-DATE-NULL-IND     PIC S9(04)  VALUE +0                    
                                                    COMP.                       
           05  PV-STD-SZ-DESC-NULL-IND  PIC S9(04)  VALUE +0                    
                                                    COMP.                       
W32250     05  PV-EXTD-SZ-CDE-NULL-IND  PIC S9(04)  VALUE +0                    
W32250                                              COMP.                       
                                                                                
           EJECT                                                                
      **--------------------------------------------------------------**        
      ** THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
      ** SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
      **--------------------------------------------------------------**        
                                                                                
           COPY ETWS353.                                                        
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY ETKM352.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CURRENT DATE AND TIME COPYBOOK.                             *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS017.                                                        
                                                                                
       EJECT                                                                    
      **--------------------------------------------------------------**        
      ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
      ** SUB-ROUTINE (DPKUT100).                                      **        
      **--------------------------------------------------------------**        
                                                                                
           COPY DPWS010I.                                                       
                                                                                
       EJECT                                                                    
      **--------------------------------------------------------------**        
CICS  ** PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.            **        
      **--------------------------------------------------------------**        
                                                                                
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
                                                                                
       EJECT                                                                    
      **--------------------------------------------------------------**        
      *    STANDARD COMMAREA.                                                   
      **--------------------------------------------------------------**        
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY ETWS351.                                                        
           COPY ETWS352.                                                        
W32250     COPY ETWS356.                                                        
                                                                                
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR ETKCS352.                                     
      *****************************************************************         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-INT-APPL-FORMAT-IND    PIC  X(01).                   
                   15  ASC-SIZE-GP-CODE           PIC S9(04) COMP.              
                   15  ASC-SIZE-GP-DESC           PIC  X(30).                   
                   15  ASC-KOHLS-SIZE-CODE        PIC S9(09) COMP.              
                   15  ASC-HOLD-KOHLS-SIZE-CODE   PIC S9(09) COMP.              
                   15  ASC-EFF-END-DATE           PIC  X(10).                   
                   15  ASC-NRF-SIZE-CODE          PIC  X(05).                   
                   15  ASC-PRI-DESC               PIC  X(40).                   
                   15  ASC-SEC-DESC               PIC  X(40).                   
                   15  ASC-KOHLS-DESC             PIC  X(10).                   
                   15  ASC-KOHLS-STD-SZ-ID        PIC  X(09).                   
                   15  ASC-KOHLS-STD-SZ-ID-NUM    PIC S9(09) COMP.              
                   15  ASC-STD-SZ-ID-NULL-IND     PIC S9(04) COMP.              
                   15  ASC-KOHLS-STD-SZ-DESC      PIC  X(10).                   
                   15  ASC-EXP-SIZE-IND           PIC  X(01).                   
                   15  ASC-USE-SELECTION-QUEUE-IND                              
                                                  PIC  X(01).                   
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-SEL-QUEUE-NAME         PIC  X(08).                   
                   15  ASC-NUMBER-OF-TS-ITEMS     PIC S9(05) COMP-3.            
                   15  ASC-SEL-SUB                PIC S9(04) COMP.              
                   15  ASC-SEL-ITEM               PIC S9(04) COMP.              
                   15  ASC-SELECT-SW              PIC  X(01).                   
                       88  ASC-SGC-NRF-SEL                   VALUE 'A'.         
                       88  ASC-SGC-KDSC-SEL                  VALUE 'B'.         
                       88  ASC-SGC-NRF-KDSC-SEL              VALUE 'C'.         
                       88  ASC-SGC-KSC-SEL                   VALUE 'D'.         
                   15  ASC-SIZE-EXPIRED-SW        PIC  X(01).                   
                       88  ASC-SIZE-EXPIRED                  VALUE 'Y'.         
                       88  ASC-SIZE-NOT-EXPIRED              VALUE 'N'.         
                   15  ASC-ADD-REQUEST-SW         PIC  X(01).                   
                       88  ASC-ADD-REQUEST                   VALUE 'Y'.         
                       88  ASC-NOT-ADD-REQUEST               VALUE 'N'.         
                   15  ASC-REACTIVATE-EXP-SIZE-SW PIC  X(01).                   
                       88  ASC-REACTIVATE-EXP-SIZE           VALUE 'Y'.         
                       88  ASC-DONT-REACTIVATE-EXP-SIZE      VALUE 'N'.         
                   15  ASC-CHANGES-MADE-SW        PIC  X(01).                   
                       88  ASC-CHANGES-MADE                  VALUE 'Y'.         
                       88  ASC-NO-CHANGES-MADE               VALUE 'N'.         
W32250             15  ASC-EXTD-SZ-CDE            PIC  X(02).                   
W32250             15  ASC-EXTD-SZ-DESC           PIC  X(20).                   
                                                                                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR THE TSIZE TABLE                               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSIZE                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR THE IMT_KOHLS_STD_SZ TABLE                    *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE IMT00021                                                
           END-EXEC.                                                            
                                                                                
W32250*---------------------------------------------------------------*         
W32250*    DB2 AREA FOR THE IMT_EXTD_SZ TABLE                         *         
W32250*---------------------------------------------------------------*         
W32250     EXEC SQL                                                             
W32250          INCLUDE IMT00025                                                
W32250     END-EXEC.                                                            
W32250                                                                          
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF ET352AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
      *                                                                         
           PERFORM 1000-CONTROL-PROCESSING                                      
      *                                                                         
W32250     IF PS-ERROR OR                                                       
W32250        DP020-NEXT-ACT-INITIAL OR                                         
W32250        DP020-NEXT-ACT-RETURN                                             
W32250         CONTINUE                                                         
W32250     ELSE                                                                 
W32250         EVALUATE TRUE                                                    
W32250*-->        ON ENTER                                                      
W32250            WHEN  DP020-SRC-AID = DP016-ENTER                             
W32250                MOVE ASC-EXTD-SZ-CDE  TO ET356-EXTD-SZ-CDE                
W32250                MOVE ASC-EXTD-SZ-DESC TO ET356-EXTD-SZ-DESC               
W32250         END-EVALUATE                                                     
W32250     END-IF.                                                              
W32250                                                                          
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
      ******************************************************************        
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
W32250     MOVE '*0001-CALL-CICS-ARCH-API.        *' TO DP013-PARAGRAPH         
W32250                                                                          
W32250     IF PS-ERROR                                                          
W32250         IF DP030-SET-CURSOR-APPL-1                                       
W32250             IF DP020-MSG-WARNING OR                                      
W32250                DP020-MSG-INFORMATIONAL                                   
W32250                 NEXT SENTENCE                                            
W32250             ELSE                                                         
W32250                 SET DP020-MSG-FATAL           TO TRUE                    
W32250             END-IF                                                       
W32250         ELSE                                                             
W32250             IF DP030-SET-CURSOR-ABSOLUTE                                 
W32250                 NEXT SENTENCE                                            
W32250             ELSE                                                         
W32250                 SET DP030-SET-CURSOR-TRAILER  TO TRUE                    
W32250             END-IF                                                       
W32250         END-IF                                                           
W32250     END-IF.                                                              
W32250                                                                          
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          ET352AI.                              
      *                                                                         
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
CICS           MOVE '0001-CALL-CICS-ARCH-API'                                   
CICS                                     TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                    PERFORM 1100-FIND-OBJECT-KEYS                               
                    IF  PS-COMMAREA-NOT-VALID                                   
                        IF  PS-NOT-ENOUGH-DATA                                  
                            MOVE PC-TSYMSG-01432   TO DP020-MSG-NUMBER          
                            SET DP020-MSG-FATAL              TO TRUE            
                            SET DP020-NEXT-ACT-APPL-ERROR    TO TRUE            
                        END-IF                                                  
                        IF  PS-INVALID-NRF-CODE                                 
                            MOVE PC-TSYMSG-01425   TO DP020-MSG-NUMBER          
                            SET DP020-MSG-FATAL              TO TRUE            
                            SET DP020-NEXT-ACT-APPL-ERROR    TO TRUE            
                        END-IF                                                  
                        IF  PS-INVALID-KDESC                                    
                            MOVE PC-TSYMSG-01426   TO DP020-MSG-NUMBER          
                            SET DP020-MSG-FATAL              TO TRUE            
                            SET DP020-NEXT-ACT-APPL-ERROR    TO TRUE            
                        END-IF                                                  
                    ELSE                                                        
                        PERFORM 4000-BUILD-INITIAL-PANEL                        
                        PERFORM 3200-RESET-ATTRIBUTES                           
                        IF  PS-INVALID-NRF-CODE                                 
                            MOVE PC-TSYMSG-01425   TO DP020-MSG-NUMBER          
                            SET DP020-MSG-FATAL              TO TRUE            
                            SET DP020-NEXT-ACT-APPL-ERROR    TO TRUE            
                        END-IF                                                  
                    END-IF                                                      
                                                                                
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
W32250             MOVE ET356-EXTD-SZ-CDE  TO ASC-EXTD-SZ-CDE                   
W32250             MOVE ET356-EXTD-SZ-DESC TO ASC-EXTD-SZ-DESC                  
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
W32250             IF ET356-EXTD-SZ-DESC EQUAL SPACES                           
W32250                 CONTINUE                                                 
W32250             ELSE                                                         
W32250                 IF ET356-EXTD-SZ-DESC NOT EQUAL ASC-EXTD-SZ-DESC         
W32250                     SET ASC-CHANGES-MADE TO TRUE                         
W32250                     MOVE ET356-EXTD-SZ-DESC TO ASC-EXTD-SZ-DESC          
W32250                 END-IF                                                   
W32250             END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO DP013-MESSAGE-TEXT(1)               
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **        
      ** INTER-APPLICATION FORMAT CODE.  IF THIS PROGRAM WAS CALLED   **        
      ** FROM THE CRITERIA SCREEN, USE THE DATA IN THE INTER-         **        
      ** APPLICATION COMMAREA.  IF THIS PROGRAM WAS CALLED FROM THE   **        
      ** SIZE LIST SCREEN, USE THE DATA PASSED IN THE TEMPORARY       **        
      ** STORAGE QUEUE.                                               **        
      ******************************************************************        
       1100-FIND-OBJECT-KEYS.                                                   
                                                                                
           SET PS-COMMAREA-VALID         TO TRUE.                               
           INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
           MOVE DP020-INT-APPL-FORMAT-IND                                       
                                         TO ASC-INT-APPL-FORMAT-IND.            
                                                                                
           EVALUATE ASC-INT-APPL-FORMAT-IND                                     
                                                                                
               WHEN PC-IA-ADD-CRIT-SPEC                                         
               WHEN PC-IA-UPD-CRIT-SPEC                                         
               WHEN PC-IA-DEL-CRIT-SPEC                                         
                   PERFORM 1110-EVALUATE-INTER-APPL-COM                         
                                                                                
               WHEN PC-IA-ADD-LIST                                              
               WHEN PC-IA-UPD-LIST                                              
               WHEN PC-IA-DEL-LIST                                              
                   SET ASC-USE-SELECTION-QUEUE TO TRUE                          
                   SET ASC-SGC-KSC-SEL         TO TRUE                          
                   MOVE ET352-SIZE-GP-CODE     TO  ASC-SIZE-GP-CODE             
                                                   DK-SIZE-GP-CODE              
                   MOVE ET352-SIZE-GP-DESC     TO  ASC-SIZE-GP-DESC             
                                                   DK-SIZE-GP-DESC              
                   MOVE ET352-NUMBER-OF-TS-ITEMS                                
                                         TO ASC-NUMBER-OF-TS-ITEMS              
                   MOVE ET352-SEL-QUEUE-NAME                                    
                                         TO ASC-SEL-QUEUE-NAME                  
                   PERFORM 1500-PREPARE-LIST-ENTRY                              
                                                                                
               WHEN OTHER                                                       
                   SET DP020-NEXT-ACT-APPL-ERROR  TO TRUE                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    EVALUATE THE DATA SENT TO THE INQUIRY PROGRAM FROM THE      *        
      *    SIZE CRITERIA SCREEN THROUGH THE INTER APPLICATION          *        
      *    COMMAREA.                                                   *        
      *----------------------------------------------------------------*        
       1110-EVALUATE-INTER-APPL-COM.                                            
                                                                                
           SET PS-ENOUGH-DATA            TO TRUE.                               
           SET PS-NOT-INVALID-NRF-CODE   TO TRUE.                               
           SET PS-NOT-INVALID-KDESC      TO TRUE.                               
           SET PS-DONT-CHECK-KDESC       TO TRUE.                               
                                                                                
           IF  ET351-NRF-SIZE-CODE GREATER THAN SPACES                          
               MOVE ET351-NRF-SIZE-CODE   TO PV-NRF-SIZE-CODE-N                 
               IF PV-NRF-SIZE-CODE-N > PC-VALID-MAX-NRF-CODE                    
                   SET PS-COMMAREA-NOT-VALID        TO TRUE                     
                   SET PS-INVALID-NRF-CODE          TO TRUE                     
               END-IF                                                           
               IF  ET351-KOHLS-DESC GREATER THAN SPACES                         
                   SET PS-CHECK-KDESC               TO TRUE                     
                   SET ASC-SGC-NRF-KDSC-SEL         TO TRUE                     
               ELSE                                                             
                   SET ASC-SGC-NRF-SEL              TO TRUE                     
               END-IF                                                           
           ELSE                                                                 
               IF  ET351-KOHLS-DESC GREATER THAN SPACES                         
                   SET PS-CHECK-KDESC               TO TRUE                     
                   SET ASC-SGC-KDSC-SEL             TO TRUE                     
               ELSE                                                             
                   SET PS-COMMAREA-NOT-VALID        TO TRUE                     
                   SET PS-NOT-ENOUGH-DATA           TO TRUE                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-CHECK-KDESC                                                    
               MOVE ZERO                 TO PV-TOTAL-INV-CHARS                  
               MOVE ZERO                 TO PV-INVALID-CHARS-CNT                
               INSPECT ET351-KOHLS-DESC                                         
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-ASTERIK                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ET351-KOHLS-DESC                                         
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-PERCENT                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ET351-KOHLS-DESC                                         
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-UNDERLINE                                         
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ET351-KOHLS-DESC                                         
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-QUOTE                                             
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               IF  PV-TOTAL-INV-CHARS > ZERO                                    
                   SET PS-COMMAREA-NOT-VALID TO TRUE                            
                   SET PS-INVALID-KDESC      TO TRUE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ET351-SIZE-GP-CODE     TO  ASC-SIZE-GP-CODE                     
                                           DK-SIZE-GP-CODE.                     
           MOVE ET351-SIZE-GP-DESC     TO  ASC-SIZE-GP-DESC                     
                                           DK-SIZE-GP-DESC.                     
           MOVE ET351-NRF-SIZE-CODE    TO  ASC-NRF-SIZE-CODE                    
                                           DK-NRF-SIZE-CODE.                    
           MOVE ET351-KOHLS-DESC       TO  ASC-KOHLS-DESC                       
                                           DK-KOHLS-DESC.                       
           MOVE ET351-EXP-SIZE-IND     TO  ASC-EXP-SIZE-IND                     
                                           DK-EXP-SIZE-IND.                     
                                                                                
      *----------------------------------------------------------------*        
      * ENTRY IS FROM A LIST.  GET THE KOHLS SIZE CODE FROM THE TEMP   *        
      * STORAGE QUEUE PASSED FROM THE LIST.                            *        
      *----------------------------------------------------------------*        
       1500-PREPARE-LIST-ENTRY.                                                 
           MOVE ET353-MAX-ITEMS          TO ASC-SEL-SUB.                        
           MOVE ZERO                     TO ASC-SEL-ITEM.                       
           PERFORM 2110-GET-NEXT-SIZE.                                          
                                                                                
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   EVALUATE ASC-INT-APPL-FORMAT-IND                             
                       WHEN PC-IA-DEL-CRIT-SPEC                                 
                       WHEN PC-IA-DEL-LIST                                      
                           SET DP030-SET-CURSOR-TRAILER  TO TRUE                
                           MOVE PC-TSYMSG-00354                                 
                                   TO DP020-MSG-NUMBER                          
                           SET DP020-MSG-WARNING         TO TRUE                
                       WHEN PC-IA-ADD-CRIT-SPEC                                 
                       WHEN PC-IA-ADD-LIST                                      
                           IF PS-NO-ERROR                                       
                               IF ASC-REACTIVATE-EXP-SIZE                       
                                   SET DP030-SET-CURSOR-TRAILER TO TRUE         
                                   MOVE PC-TSYMSG-01429                         
                                           TO DP020-MSG-NUMBER                  
                                   SET DP020-MSG-WARNING        TO TRUE         
                               END-IF                                           
                           END-IF                                               
                       WHEN OTHER                                               
                           IF PS-NO-ERROR                                       
                               SET DP030-SET-CURSOR-APPL-1   TO TRUE            
                               MOVE -1                       TO AKDESCL         
                           END-IF                                               
                   END-EVALUATE                                                 
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   SET DP030-DONT-ERASE                                         
                       DP030-SEND-DATAONLY (PC-APPL-MAP)                        
                                         TO TRUE                                
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2110-GET-NEXT-SIZE                               
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
      *                --- BOTTOM OF LIST ---                                   
                       MOVE PC-TSYMSG-00070                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-INFORMATIONAL                              
                                         TO TRUE                                
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       SET DP030-CONTINUE-GO                                    
                                         TO TRUE                                
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO                                    
                                         TO TRUE                                
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED       *        
      * FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED   *        
      * FROM THE CICS ARCHITECTURE API.                                *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   EVALUATE ASC-INT-APPL-FORMAT-IND                             
                       WHEN PC-IA-DEL-CRIT-SPEC                                 
                       WHEN PC-IA-DEL-LIST                                      
                           SET DP030-SET-CURSOR-TRAILER  TO TRUE                
                           MOVE PC-TSYMSG-00354                                 
                                   TO DP020-MSG-NUMBER                          
                           SET DP020-MSG-WARNING         TO TRUE                
                       WHEN PC-IA-ADD-CRIT-SPEC                                 
                       WHEN PC-IA-ADD-LIST                                      
                           IF ASC-REACTIVATE-EXP-SIZE                           
                               SET DP030-SET-CURSOR-TRAILER  TO TRUE            
                               MOVE PC-TSYMSG-01429                             
                                       TO DP020-MSG-NUMBER                      
                               SET DP020-MSG-WARNING         TO TRUE            
                           END-IF                                               
                       WHEN OTHER                                               
                           SET DP030-SET-CURSOR-APPL-1   TO TRUE                
                           MOVE -1                       TO AKDESCL             
                   END-EVALUATE                                                 
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                   SET DP030-CONTINUE-GO TO TRUE                                
                   IF ASC-DONT-REACTIVATE-EXP-SIZE                              
                       IF ASC-KOHLS-STD-SZ-ID = SPACES                          
      *                    --- INSERT A NEW STANDARD SIZE DESCRIPTION           
      *                    --- TABLE ROW AND FIND THE STANDARD SIZE             
                           PERFORM 8100-INSERT-KOHLS-STD-SZ-DESC                
                           PERFORM 8200-FIND-KOHLS-STD-SZ-DESC                  
                           MOVE ASC-KOHLS-STD-SZ-ID TO ASTNDSCO                 
                       END-IF                                                   
                       PERFORM 5000-ADD-SIZE                                    
      *                --- ADDS SUCCESSFUL ---                                  
                       MOVE PC-TSYMSG-00034  TO DP020-MSG-NUMBER                
                       SET DP020-MSG-INFORMATIONAL                              
                                             TO TRUE                            
                   ELSE                                                         
                       PERFORM 5500-REACT-EXP-SIZE                              
      *                --- REACTIVATE EXPIRED SIZE SUCCESSFUL ---               
                       MOVE PC-TSYMSG-01430  TO DP020-MSG-NUMBER                
                       SET DP020-MSG-INFORMATIONAL                              
                                             TO TRUE                            
                   END-IF                                                       
      *            --- MOVE ASC TO INTER-APPL COMMAREA ---                      
                   PERFORM 2300-PREPARE-INTER-APPL-COMM                         
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
                   SET DP030-CONTINUE-GO TO TRUE                                
                   IF ASC-KOHLS-STD-SZ-ID = SPACES                              
      *                --- INSERT A NEW STANDARD SIZE DESCRIPTION               
      *                --- TABLE ROW AND FIND THE STANDARD SIZE                 
                       PERFORM 8100-INSERT-KOHLS-STD-SZ-DESC                    
                       PERFORM 8200-FIND-KOHLS-STD-SZ-DESC                      
                       MOVE ASC-KOHLS-STD-SZ-ID TO ASTNDSCO                     
                   END-IF                                                       
                   PERFORM 6000-UPDATE-SIZE                                     
      *            --- UPDATES SUCCESSFUL ---                                   
                   IF ASC-CHANGES-MADE                                          
                       MOVE PC-TSYMSG-00059  TO DP020-MSG-NUMBER                
                   ELSE                                                         
                       MOVE PC-TSYMSG-01433  TO DP020-MSG-NUMBER                
                   END-IF                                                       
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
      *            --- MOVE ASC TO INTER-APPL COMMAREA ---                      
                   PERFORM 2300-PREPARE-INTER-APPL-COMM                         
                                                                                
W32250         WHEN DP020-FK-LOCAL-FUNC-10 (DP020-SRC-AID)                      
W32250             IF AESZDSL = ZERO                                            
W32250                 SET PS-INVALID-EXTD-SZ TO TRUE                           
W32250                 PERFORM 4500-CHECK-EXTD-SZ-INFO                          
W32250                 IF PS-INVALID-EXTD-SZ                                    
W32250                    AND ASC-EXTD-SZ-DESC > SPACES                         
W32250                    MOVE SPACES TO ASC-EXTD-SZ-DESC                       
W32250                                   ET356-EXTD-SZ-DESC                     
W32250                    SET DP030-CONTINUE-GO TO TRUE                         
W32250                 END-IF                                                   
W32250             END-IF                                                       
W32250                                                                          
               WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                            
                   SET DP030-CONTINUE-GO TO TRUE                                
                   PERFORM 7000-DELETE-SIZE                                     
      *            --- DELETES SUCCESSFUL ---                                   
                   MOVE PC-TSYMSG-00115  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * RETRIEVE THE NEXT SIZE FROM THE TEMPORARY STORAGE QUEUE AND    *        
      * IF PAST THE BOTTOM OF THE LIST, DISPLAY AN INFORMATIONAL       *        
      * MESSAGE ON THE SCREEN.                                         *        
      *----------------------------------------------------------------*        
       2110-GET-NEXT-SIZE.                                                      
                                                                                
           SET PS-NOT-BOTTOM-OF-LIST     TO TRUE.                               
           ADD +1                        TO ASC-SEL-SUB.                        
                                                                                
           IF  ASC-SEL-SUB > ET353-MAX-ITEMS                                    
               ADD +1                    TO ASC-SEL-ITEM                        
               MOVE +1                   TO ASC-SEL-SUB                         
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  ET353-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE ET353-KOHLS-SIZE-CODE (ASC-SEL-SUB)                     
                                         TO ASC-KOHLS-SIZE-CODE                 
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   EVALUATE ASC-INT-APPL-FORMAT-IND                             
                                                                                
                       WHEN PC-IA-ADD-LIST                                      
                            SET ET353-ADD (ASC-SEL-SUB) TO TRUE                 
                       WHEN PC-IA-UPD-LIST                                      
                            SET ET353-UPD (ASC-SEL-SUB) TO TRUE                 
                       WHEN PC-IA-DEL-LIST                                      
                            SET ET353-DEL (ASC-SEL-SUB) TO TRUE                 
                       WHEN OTHER                                               
                            SET ET353-ERR (ASC-SEL-SUB) TO TRUE                 
                                                                                
                   END-EVALUATE                                                 
                                                                                
                   IF PS-FATAL-ERROR                                            
                       SET ET353-ERR (ASC-SEL-SUB)      TO TRUE                 
                   END-IF                                                       
                                                                                
                   PERFORM 9200-REWRITE-LIST-QUEUE                              
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
      *            --- BOTTOM OF LIST ---                                       
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
                   SET PS-BOTTOM-OF-LIST TO TRUE                                
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE ET353-MAX-ITEMS      TO ASC-SEL-SUB                         
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
               SET PS-BOTTOM-OF-LIST     TO TRUE                                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS   *        
      * IN THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN   *        
      * THE APPLICATION-SPECIFIC COMMAERA, NOT ON THE SCREEN.  A CHECK *        
      * IS ALSO MADE TO SEE IF ANY DATA ON THE SCREEN HAS CHANGED TO   *        
      * DISPLAY THE CORRECT MESSAGE FOR AN UPDATE.                     *        
      *----------------------------------------------------------------*        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           IF  ANRFCDL > ZERO                                                   
               IF ANRFCDI NOT EQUAL ASC-NRF-SIZE-CODE                           
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE ANRFCDI              TO ASC-NRF-SIZE-CODE                   
           ELSE                                                                 
               IF  ANRFCDF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-NRF-SIZE-CODE                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  APDESCL > ZERO                                                   
               IF APDESCI NOT EQUAL ASC-PRI-DESC                                
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE APDESCI              TO ASC-PRI-DESC                        
           ELSE                                                                 
               IF  APDESCF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-PRI-DESC                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASDESCL > ZERO                                                   
               IF ASDESCI NOT EQUAL ASC-SEC-DESC                                
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE ASDESCI              TO ASC-SEC-DESC                        
           ELSE                                                                 
               IF  ASDESCF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-SEC-DESC                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AKDESCL > ZERO                                                   
               IF AKDESCI NOT EQUAL ASC-KOHLS-DESC                              
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE AKDESCI              TO ASC-KOHLS-DESC                      
           ELSE                                                                 
               IF  AKDESCF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-KOHLS-DESC                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
W32250     IF  AESZDSL > ZERO                                                   
W32250         IF AESZDSI NOT EQUAL ASC-EXTD-SZ-DESC                            
W32250             SET ASC-CHANGES-MADE TO TRUE                                 
W32250             IF AESZDSI EQUAL SPACES                                      
W32250                 MOVE SPACE TO ASC-EXTD-SZ-CDE                            
W32250             END-IF                                                       
W32250         END-IF                                                           
W32250         MOVE AESZDSI TO ASC-EXTD-SZ-DESC                                 
W32250     ELSE                                                                 
W32250         IF AESZDSF = DP015-ERASE-EOF                                     
W32250             MOVE SPACE TO ASC-EXTD-SZ-CDE                                
W32250                           ASC-EXTD-SZ-DESC                               
W32250         END-IF                                                           
W32250     END-IF.                                                              
W32250                                                                          
           IF  ASTNDSCL > ZERO                                                  
               IF ASTNDSCI NOT EQUAL ASC-KOHLS-STD-SZ-ID                        
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE ASTNDSCI             TO ASC-KOHLS-STD-SZ-ID                 
           ELSE                                                                 
               IF  ASTNDSCF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO ASC-KOHLS-STD-SZ-ID                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASTNDDEL > ZERO                                                  
               IF ASTNDDEI NOT EQUAL ASC-KOHLS-STD-SZ-DESC                      
                   SET ASC-CHANGES-MADE      TO TRUE                            
               END-IF                                                           
               MOVE ASTNDDEI             TO ASC-KOHLS-STD-SZ-DESC               
           ELSE                                                                 
               IF  ASTNDDEF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO ASC-KOHLS-STD-SZ-DESC               
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
      ** INTER-APPLICATION COMMAREA.                                  **        
      ******************************************************************        
       2300-PREPARE-INTER-APPL-COMM.                                            
           INITIALIZE ET351-INTER-APPL-COMMAREA.                                
           MOVE ASC-SIZE-GP-CODE         TO PV-SIZE-GP-CODE-N.                  
           MOVE PV-SIZE-GP-CODE-N        TO ET351-SIZE-GP-CODE.                 
           MOVE ASC-SIZE-GP-DESC         TO ET351-SIZE-GP-DESC.                 
           MOVE ASC-NRF-SIZE-CODE        TO ET351-NRF-SIZE-CODE.                
           MOVE ASC-KOHLS-DESC           TO ET351-KOHLS-DESC.                   
           MOVE ASC-EXP-SIZE-IND         TO ET351-EXP-SIZE-IND.                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
      * IS FOUND, THE CURSOR IS POSITIONED AT THAT FIELD AND AN        *        
      * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
      *----------------------------------------------------------------*        
       3000-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
           PERFORM 3200-RESET-ATTRIBUTES.                                       
                                                                                
           SET PS-NO-ERROR               TO TRUE.                               
                                                                                
           EVALUATE ASC-INT-APPL-FORMAT-IND                                     
                                                                                
               WHEN PC-IA-ADD-CRIT-SPEC                                         
               WHEN PC-IA-ADD-LIST                                              
               WHEN PC-IA-UPD-CRIT-SPEC                                         
               WHEN PC-IA-UPD-LIST                                              
                   PERFORM 3100-EDIT-NRF-SIZE-CODE                              
                   PERFORM 3110-EDIT-KOHLS-DESC                                 
                   PERFORM 3120-EDIT-PRI-DESC                                   
                   PERFORM 3130-EDIT-SEC-DESC                                   
                   PERFORM 3140-EDIT-STANDARD-SIZE-CODE                         
                   PERFORM 3150-EDIT-STANDARD-DESC                              
                   PERFORM 3160-CHECK-STANDARD-ENTRY                            
W32250             IF AESZDSL > ZERO                                            
W32250                 OR ET356-EXTD-SZ-DESC > SPACES                           
W32250                 PERFORM 3170-EDIT-EXTD-SZ-INFO                           
W32250             END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   CONTINUE                                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
           IF ASC-ADD-REQUEST                                                   
               PERFORM 4200-CHECK-GPC-NRFC                                      
               IF  PS-NRFCODE-FOUND                                             
                   IF  PS-NRFCODE-NOT-EXPIRED                                   
                       IF  PS-NO-ERROR                                          
                           SET DP020-MSG-FATAL        TO  TRUE                  
                           MOVE PC-TSYMSG-01494                                 
                                             TO  DP020-MSG-NUMBER               
                           SET PS-ERROR      TO  TRUE                           
                       END-IF                                                   
                   ELSE                                                         
                       SET ASC-REACTIVATE-EXP-SIZE    TO TRUE                   
                       IF  PS-NO-ERROR                                          
                           SET DP020-MSG-WARNING      TO TRUE                   
                           MOVE PC-TSYMSG-01429                                 
                                             TO  DP020-MSG-NUMBER               
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   IF  PS-MULT-NRFCODES-FOUND                                   
                       IF  PS-NO-ERROR                                          
                           SET DP020-MSG-FATAL            TO  TRUE              
                           MOVE PC-TSYMSG-01493                                 
                                             TO  DP020-MSG-NUMBER               
                           SET PS-ERROR      TO  TRUE                           
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-NO-ERROR                                                      
                                                                                
               EVALUATE ASC-INT-APPL-FORMAT-IND                                 
                   WHEN PC-IA-ADD-CRIT-SPEC                                     
                   WHEN PC-IA-ADD-LIST                                          
                       IF ASC-REACTIVATE-EXP-SIZE                               
                           MOVE -1                   TO AKDESCL                 
                       ELSE                                                     
                           MOVE -1                   TO ANRFCDL                 
                       END-IF                                                   
                   WHEN PC-IA-UPD-CRIT-SPEC                                     
                   WHEN PC-IA-UPD-LIST                                          
                       MOVE -1                       TO AKDESCL                 
                   WHEN OTHER                                                   
                       MOVE -1                       TO ANRFCDL                 
               END-EVALUATE                                                     
                                                                                
               SET DP030-SET-CURSOR-APPL-1   TO TRUE                            
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE NRF SIZE CODE ENTERED BY THE USER. THE    *        
      * NRF CODE MUST BE A NUMERIC VALUE AND MUST NOT BE GREATER THAN  *        
      * 97999, OTHERWISE AN ERROR MESSAGE WILL BE DISPLAYED AND THE    *        
      * CURSOR WILL BE POSITIONED IN THE FIELD.                        *        
      *----------------------------------------------------------------*        
       3100-EDIT-NRF-SIZE-CODE.                                                 
                                                                                
           IF  ASC-NRF-SIZE-CODE > SPACE                                        
               MOVE ASC-NRF-SIZE-CODE    TO DP010I-UNEDITED-FIELD               
               MOVE LENGTH OF ASC-NRF-SIZE-CODE                                 
                                         TO DP010I-MAXIMUM-DIGITS               
               MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
               CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
                         USING DP010I-NUMERIC-EDIT-AREA                         
               IF  DP010I-ERROR-DETECTED                                        
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ANRFCDA                             
                   MOVE DP015-RED        TO ANRFCDC                             
                   MOVE DP015-REVERSE    TO ANRFCDH                             
                   IF  PS-NO-ERROR                                              
                       SET PS-ERROR                                             
                           DP030-SET-CURSOR-APPL-1                              
                           DP020-MSG-FATAL     TO TRUE                          
                       MOVE PC-TSYMSG-00008    TO DP020-MSG-NUMBER              
                       MOVE -1                 TO ANRFCDL                       
                   END-IF                                                       
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO PV-NRF-SIZE-CODE-N                  
                   IF PV-NRF-SIZE-CODE-N <= PC-VALID-MAX-NRF-CODE               
                       MOVE PV-NRF-SIZE-CODE-N  TO PV-NRF-SIZE-CODE-X           
                       MOVE ASC-SIZE-GP-CODE    TO PV-SIZE-GP-CODE-N            
                       MOVE PV-SIZE-GP-CODE-N   TO PV-SIZE-GP-CODE-X            
                       IF ( PV-SIZE-GP-CODE-X(2:1)  EQUAL                       
                                     PV-NRF-SIZE-CODE-X(1:1) )                  
                           MOVE PV-NRF-SIZE-CODE-X                              
                                                  TO ASC-NRF-SIZE-CODE          
                           MOVE ASC-NRF-SIZE-CODE TO ANRFCDO                    
                       ELSE                                                     
                           MOVE DP015-UNP-ALP-BRT-OFF                           
                                                 TO ANRFCDA                     
                           MOVE DP015-RED        TO ANRFCDC                     
                           MOVE DP015-REVERSE    TO ANRFCDH                     
                           IF  PS-NO-ERROR                                      
                               SET PS-ERROR                                     
                                   DP030-SET-CURSOR-APPL-1                      
                                   DP020-MSG-FATAL                              
                                                 TO TRUE                        
                               MOVE PC-TSYMSG-01425                             
                                                 TO DP020-MSG-NUMBER            
                               MOVE -1           TO ANRFCDL                     
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                             TO ANRFCDA                         
                       MOVE DP015-RED        TO ANRFCDC                         
                       MOVE DP015-REVERSE    TO ANRFCDH                         
                       IF  PS-NO-ERROR                                          
                           SET PS-ERROR                                         
                               DP030-SET-CURSOR-APPL-1                          
                               DP020-MSG-FATAL     TO TRUE                      
                           MOVE PC-TSYMSG-01425    TO DP020-MSG-NUMBER          
                           MOVE -1                 TO ANRFCDL                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ANRFCDA                                 
               MOVE DP015-RED        TO ANRFCDC                                 
               MOVE DP015-REVERSE    TO ANRFCDH                                 
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL     TO TRUE                              
                   MOVE PC-TSYMSG-00007    TO DP020-MSG-NUMBER                  
                   MOVE -1                 TO ANRFCDL                           
               END-IF                                                           
               MOVE SPACES           TO ASC-NRF-SIZE-CODE                       
                                        ANRFCDO                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE KOHLS DESCRIPTION ENTERED BY THE USER.    *        
      * THE PRIMARY DESCRIPTION IS A REQUIRED FIELD AN MUST NOT        *        
      * CONTAIN THE CHARACTERS *, %, _, AND '.                         *        
      *----------------------------------------------------------------*        
       3110-EDIT-KOHLS-DESC.                                                    
                                                                                
           IF  ASC-KOHLS-DESC (1:1) = SPACE                                     
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO AKDESCA                             
               MOVE DP015-RED            TO AKDESCC                             
               MOVE DP015-REVERSE        TO AKDESCH                             
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO AKDESCL                             
               END-IF                                                           
           ELSE                                                                 
               MOVE ZERO                 TO PV-TOTAL-INV-CHARS                  
               MOVE ZERO                 TO PV-INVALID-CHARS-CNT                
               INSPECT ASC-KOHLS-DESC                                           
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-ASTERIK                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-KOHLS-DESC                                           
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-PERCENT                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-KOHLS-DESC                                           
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-UNDERLINE                                         
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-KOHLS-DESC                                           
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-QUOTE                                             
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               IF  PV-TOTAL-INV-CHARS > ZERO                                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AKDESCA                             
                   MOVE DP015-RED        TO AKDESCC                             
                   MOVE DP015-REVERSE    TO AKDESCH                             
                   IF  PS-NO-ERROR                                              
                       SET PS-ERROR                                             
                           DP030-SET-CURSOR-APPL-1                              
                           DP020-MSG-FATAL   TO TRUE                            
      *                ---- INVALID CHARS FOUND IN STRING ----                  
                       MOVE PC-TSYMSG-01426  TO DP020-MSG-NUMBER                
                       MOVE -1               TO AKDESCL                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE PRIMARY DESCRIPTION ENTERED BY THE USER.  *        
      * THE PRIMARY DESCRIPTION IS A REQUIRED FIELD AN MUST NOT        *        
      * CONTAIN THE CHARACTERS *, %, _, AND '.                         *        
      *----------------------------------------------------------------*        
       3120-EDIT-PRI-DESC.                                                      
                                                                                
           IF  ASC-PRI-DESC (1:1) = SPACE                                       
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO APDESCA                             
               MOVE DP015-RED            TO APDESCC                             
               MOVE DP015-REVERSE        TO APDESCH                             
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO APDESCL                             
               END-IF                                                           
           ELSE                                                                 
               MOVE ZERO                 TO PV-TOTAL-INV-CHARS                  
               MOVE ZERO                 TO PV-INVALID-CHARS-CNT                
               INSPECT ASC-PRI-DESC                                             
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-ASTERIK                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-PRI-DESC                                             
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-PERCENT                                           
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-PRI-DESC                                             
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-UNDERLINE                                         
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               INSPECT ASC-PRI-DESC                                             
                   TALLYING PV-INVALID-CHARS-CNT                                
                   FOR ALL PC-QUOTE                                             
                   ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS              
               IF  PV-TOTAL-INV-CHARS > ZERO                                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO APDESCA                             
                   MOVE DP015-RED        TO APDESCC                             
                   MOVE DP015-REVERSE    TO APDESCH                             
                   IF  PS-NO-ERROR                                              
                       SET PS-ERROR                                             
                           DP030-SET-CURSOR-APPL-1                              
                           DP020-MSG-FATAL   TO TRUE                            
      *                ---- INVALID CHARS FOUND IN STRING ----                  
                       MOVE PC-TSYMSG-01426  TO DP020-MSG-NUMBER                
                       MOVE -1               TO APDESCL                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE SECONDARY DESCRIPTION ENTERED BY THE      *        
      * USER. THE SECONDARY DESCRIPTION IS NOT REQUIRED, BUT MUST NOT  *        
      * CONTAIN THE CHARACTERS *, %, _, AND ' IF ONE IS ENTERED.       *        
      *----------------------------------------------------------------*        
       3130-EDIT-SEC-DESC.                                                      
                                                                                
           MOVE ZERO                 TO PV-TOTAL-INV-CHARS.                     
           MOVE ZERO                 TO PV-INVALID-CHARS-CNT.                   
           INSPECT ASC-SEC-DESC                                                 
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-ASTERIK.                                              
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-SEC-DESC                                                 
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-PERCENT.                                              
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-SEC-DESC                                                 
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-UNDERLINE.                                            
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-SEC-DESC                                                 
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-QUOTE.                                                
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           IF  PV-TOTAL-INV-CHARS > ZERO                                        
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ASDESCA                                 
               MOVE DP015-RED        TO ASDESCC                                 
               MOVE DP015-REVERSE    TO ASDESCH                                 
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL   TO TRUE                                
      *            ---- INVALID CHARS FOUND IN STRING ----                      
                   MOVE PC-TSYMSG-01426  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASDESCL                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE STANDARD SIZE CODE ENTERED BY THE USER.   *        
      * IT MUST BE A NUMERIC VALUE OR BLANK, OTHERWISE AN ERROR MESSAGE*        
      * WILL BE DISPLAYED AND THE CURSOR WILL BE POSITIONED IN THE     *        
      * FIELD.                                                         *        
      *----------------------------------------------------------------*        
       3140-EDIT-STANDARD-SIZE-CODE.                                            
                                                                                
           IF ASC-KOHLS-STD-SZ-ID > SPACE                                       
               MOVE ASC-KOHLS-STD-SZ-ID  TO DP010I-UNEDITED-FIELD               
               MOVE LENGTH OF ASC-KOHLS-STD-SZ-ID                               
                                         TO DP010I-MAXIMUM-DIGITS               
               MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
               CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
                         USING DP010I-NUMERIC-EDIT-AREA                         
               IF  DP010I-ERROR-DETECTED                                        
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ASTNDSCA                            
                   MOVE DP015-RED        TO ASTNDSCC                            
                   MOVE DP015-REVERSE    TO ASTNDSCH                            
                   IF  PS-NO-ERROR                                              
                       SET PS-ERROR                                             
                           DP030-SET-CURSOR-APPL-1                              
                           DP020-MSG-FATAL     TO TRUE                          
                       MOVE PC-TSYMSG-00008    TO DP020-MSG-NUMBER              
                       MOVE -1                 TO ASTNDSCL                      
                   END-IF                                                       
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD                                    
                                             TO ASC-KOHLS-STD-SZ-ID-NUM         
                   MOVE ASC-KOHLS-STD-SZ-ID-NUM TO ASTNDSCO                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON THE STANDARD DESCRIPTION ENTERED BY THE       *        
      * USER. THE STANDARD DESCRIPTION IS NOT REQUIRED, BUT MUST NOT   *        
      * CONTAIN THE CHARACTERS *, %, _, AND ' IF ONE IS ENTERED.       *        
      *----------------------------------------------------------------*        
       3150-EDIT-STANDARD-DESC.                                                 
                                                                                
           MOVE ZERO                 TO PV-TOTAL-INV-CHARS.                     
           MOVE ZERO                 TO PV-INVALID-CHARS-CNT.                   
           INSPECT ASC-KOHLS-STD-SZ-DESC                                        
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-ASTERIK.                                              
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-KOHLS-STD-SZ-DESC                                        
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-PERCENT.                                              
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-KOHLS-STD-SZ-DESC                                        
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-UNDERLINE.                                            
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           INSPECT ASC-KOHLS-STD-SZ-DESC                                        
               TALLYING PV-INVALID-CHARS-CNT                                    
               FOR ALL PC-QUOTE.                                                
               ADD PV-INVALID-CHARS-CNT  TO PV-TOTAL-INV-CHARS.                 
           IF  PV-TOTAL-INV-CHARS > ZERO                                        
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ASTNDDEA                                
               MOVE DP015-RED        TO ASTNDDEC                                
               MOVE DP015-REVERSE    TO ASTNDDEH                                
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL   TO TRUE                                
      *            ---- INVALID CHARS FOUND IN STRING ----                      
                   MOVE PC-TSYMSG-01426  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTNDDEL                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * EITHER THE STANDARD SIZE CODE OR STANDARD DESCRIPTION MUST BE  *        
      * ENTERED BY THE USER, OTHERWISE AN ERROR MESSAGE WILL BE        *        
      * DISPLAYED AND THE CURSOR WILL BE POSITIONED IN THE STANDARD    *        
      * SIZE CODE FIELD.  IF THE STANDARD SIZE CODE IS ENTERED, VERIFY *        
      * THAT IT EXISTS.                                                *        
      *----------------------------------------------------------------*        
       3160-CHECK-STANDARD-ENTRY.                                               
                                                                                
           IF ASC-KOHLS-STD-SZ-ID = SPACE                                       
             AND ASC-KOHLS-STD-SZ-DESC = SPACE                                  
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                     TO ASTNDSCA                                
               MOVE DP015-RED        TO ASTNDSCC                                
               MOVE DP015-REVERSE    TO ASTNDSCH                                
               IF  PS-NO-ERROR                                                  
                   SET PS-ERROR                                                 
                       DP030-SET-CURSOR-APPL-1                                  
                       DP020-MSG-FATAL     TO TRUE                              
      *            ---- EITHER STANDARD SIZE CODE OR STANDARD                   
      *                 DESCRIPTION MUST BE ENTERED ----                        
                   MOVE PC-TSYMSG-03517    TO DP020-MSG-NUMBER                  
                   MOVE -1                 TO ASTNDSCL                          
               END-IF                                                           
               MOVE SPACES           TO ASC-KOHLS-STD-SZ-ID                     
                                        ASTNDSCO                                
           END-IF.                                                              
                                                                                
           IF ASC-KOHLS-STD-SZ-ID > SPACE                                       
               PERFORM 8000-CHECK-STANDARD-SIZE                                 
               IF PS-STANDARD-SIZE-NOT-FOUND                                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ASTNDSCA                            
                   MOVE DP015-RED        TO ASTNDSCC                            
                   MOVE DP015-REVERSE    TO ASTNDSCH                            
                   IF  PS-NO-ERROR                                              
                       SET PS-ERROR                                             
                           DP030-SET-CURSOR-APPL-1                              
                           DP020-MSG-FATAL   TO TRUE                            
      *            ---- STANDARD SIZE CODE NOT FOUND ----                       
                       MOVE PC-TSYMSG-03518  TO DP020-MSG-NUMBER                
                       MOVE -1               TO ASTNDSCL                        
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
W32250*----------------------------------------------------------------*        
W32250* PERFORM EDITS ON THE EXTENDED SIZE DESC SELECTED BY THE USER.  *        
W32250* THE PRIMARY DESCRIPTION IS NOT A REQUIRED FIELD WHEN NRF-SZ-CDE*        
W32250* IS NOT SET FOR IT.                                             *        
W32250*----------------------------------------------------------------*        
W32250 3170-EDIT-EXTD-SZ-INFO.                                                  
W32250                                                                          
W32250     SET PS-INVALID-EXTD-SZ TO TRUE.                                      
W32250     PERFORM 4500-CHECK-EXTD-SZ-INFO.                                     
W32250     IF PS-INVALID-EXTD-SZ                                                
W32250         AND ASC-EXTD-SZ-DESC > SPACES                                    
W32250         MOVE ASC-EXTD-SZ-DESC TO ET356-EXTD-SZ-DESC                      
W32250         MOVE DP015-UNP-ALP-BRT-OFF                                       
W32250                               TO AESZDSA                                 
W32250         MOVE DP015-RED        TO AESZDSC                                 
W32250         MOVE DP015-REVERSE    TO AESZDSH                                 
W32250         IF  PS-NO-ERROR                                                  
W32250             SET PS-ERROR                                                 
W32250                 DP030-SET-CURSOR-APPL-1                                  
W32250                 DP020-MSG-FATAL   TO TRUE                                
W32250*            ---- INVALID EXTENDED SIZE DESC FOUND ----                   
W32250             MOVE PC-TSYMSG-03591  TO DP020-MSG-NUMBER                    
W32250             MOVE -1               TO AESZDSL                             
W32250         END-IF                                                           
W32250     END-IF.                                                              
W32250                                                                          
      *----------------------------------------------------------------*        
      * MOVE THE DEFAULT ATTRIBUTE VALUES TO THE ENTERABLE FIELDS ON   *        
      * MAP.  NONE OF THE FIELDS ARE ENTERABLE FOR THE DELETE REQUEST. *        
      *----------------------------------------------------------------*        
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           EVALUATE ASC-INT-APPL-FORMAT-IND                                     
                                                                                
               WHEN PC-IA-ADD-CRIT-SPEC                                         
               WHEN PC-IA-ADD-LIST                                              
                   MOVE DP015-UNP-ALP-NOR-OFF    TO ANRFCDA                     
                                                    APDESCA                     
                                                    ASDESCA                     
                                                    AKDESCA                     
W32250                                              AESZDSA                     
                                                    ASTNDSCA                    
                                                    ASTNDDEA                    
                   MOVE DP015-GREEN              TO ANRFCDC                     
                                                    APDESCC                     
                                                    ASDESCC                     
                                                    AKDESCC                     
W32250                                              AESZDSC                     
                                                    ASTNDSCC                    
                                                    ASTNDDEC                    
                   MOVE DP015-UNDERLINE          TO ANRFCDH                     
                                                    APDESCH                     
                                                    ASDESCH                     
                                                    AKDESCH                     
W32250                                              AESZDSH                     
                                                    ASTNDSCH                    
                                                    ASTNDDEH                    
                   IF ASC-REACTIVATE-EXP-SIZE                                   
                       MOVE DP015-ASK-NOR-OFF    TO ANRFCDA                     
                       MOVE DP015-BLUE           TO ANRFCDC                     
                       MOVE DP015-HL-OFF         TO ANRFCDH                     
                   END-IF                                                       
                                                                                
               WHEN PC-IA-UPD-CRIT-SPEC                                         
               WHEN PC-IA-UPD-LIST                                              
                   MOVE DP015-ASK-NOR-OFF        TO ANRFCDA                     
                   MOVE DP015-UNP-ALP-NOR-OFF    TO APDESCA                     
                                                    ASDESCA                     
                                                    AKDESCA                     
W32250                                              AESZDSA                     
                                                    ASTNDSCA                    
                                                    ASTNDDEA                    
                   MOVE DP015-BLUE               TO ANRFCDC                     
                   MOVE DP015-GREEN              TO APDESCC                     
                                                    ASDESCC                     
                                                    AKDESCC                     
W32250                                              AESZDSC                     
                                                    ASTNDSCC                    
                                                    ASTNDDEC                    
                   MOVE DP015-HL-OFF             TO ANRFCDH                     
                   MOVE DP015-UNDERLINE          TO APDESCH                     
                                                    ASDESCH                     
                                                    AKDESCH                     
W32250                                              AESZDSH                     
                                                    ASTNDSCH                    
                                                    ASTNDDEH                    
                                                                                
               WHEN PC-IA-DEL-CRIT-SPEC                                         
               WHEN PC-IA-DEL-LIST                                              
                   MOVE DP015-ASK-NOR-OFF        TO ANRFCDA                     
                                                    APDESCA                     
                                                    ASDESCA                     
                                                    AKDESCA                     
W32250                                              AESZDSA                     
                                                    ASTNDSCA                    
                                                    ASTNDDEA                    
                   MOVE DP015-BLUE               TO ANRFCDC                     
                                                    APDESCC                     
                                                    ASDESCC                     
                                                    AKDESCC                     
W32250                                              AESZDSC                     
                                                    ASTNDSCC                    
                                                    ASTNDDEC                    
                   MOVE DP015-HL-OFF             TO ANRFCDH                     
                                                    APDESCH                     
                                                    ASDESCH                     
                                                    AKDESCH                     
W32250                                              AESZDSH                     
                                                    ASTNDSCH                    
                                                    ASTNDDEH                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RETRIEVE THE SIZE INFORMATION TO BE DISPLAYED AND ALSO      *        
      *    PREPARE THE APPROPRIATE MESSAGE TO BE DISPLAYED AT THE      *        
      *    BOTTOM OF THE SCREEN.                                       *        
      *----------------------------------------------------------------*        
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           SET ASC-NOT-ADD-REQUEST                TO TRUE.                      
           SET ASC-DONT-REACTIVATE-EXP-SIZE       TO TRUE.                      
           SET PS-NOT-FATAL-ERROR                 TO TRUE.                      
                                                                                
           EVALUATE ASC-INT-APPL-FORMAT-IND                                     
                                                                                
               WHEN PC-IA-DEL-CRIT-SPEC                                         
               WHEN PC-IA-DEL-LIST                                              
                   SET DP030-SET-CURSOR-TRAILER   TO TRUE                       
                   IF PS-NOT-BOTTOM-OF-LIST                                     
                      MOVE PC-TSYMSG-00354        TO DP020-MSG-NUMBER           
                      SET DP020-MSG-WARNING       TO TRUE                       
                   END-IF                                                       
                                                                                
               WHEN PC-IA-ADD-CRIT-SPEC                                         
               WHEN PC-IA-ADD-LIST                                              
                   SET DP030-SET-CURSOR-APPL-1    TO TRUE                       
                   MOVE -1                        TO ANRFCDL                    
                   SET ASC-ADD-REQUEST            TO TRUE                       
                                                                                
               WHEN OTHER                                                       
                   SET DP030-SET-CURSOR-APPL-1    TO TRUE                       
                   MOVE -1                        TO AKDESCL                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
           MOVE ASC-SIZE-GP-CODE         TO DK-SIZE-GP-CODE.                    
           MOVE ASC-NRF-SIZE-CODE        TO DK-NRF-SIZE-CODE.                   
           MOVE ASC-KOHLS-DESC           TO DK-KOHLS-DESC.                      
           MOVE ASC-KOHLS-SIZE-CODE      TO DK-KOHLS-SIZE-CODE.                 
                                                                                
           PERFORM 4100-GET-SIZE-INFO.                                          
                                                                                
           IF  PS-SIZE-FOUND                                                    
               IF  ASC-SIZE-EXPIRED                                             
                   IF ASC-ADD-REQUEST                                           
                       SET ASC-REACTIVATE-EXP-SIZE    TO TRUE                   
                       MOVE  0                        TO ANRFCDL                
                       MOVE -1                        TO AKDESCL                
                       SET DP020-MSG-WARNING          TO TRUE                   
                       MOVE PC-TSYMSG-01429                                     
                                         TO  DP020-MSG-NUMBER                   
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   ELSE                                                         
                       SET PS-FATAL-ERROR             TO TRUE                   
                       SET DP020-MSG-FATAL            TO TRUE                   
                       SET DP020-NEXT-ACT-APPL-ERROR  TO TRUE                   
                       MOVE PC-TSYMSG-01427                                     
                                         TO  DP020-MSG-NUMBER                   
                   END-IF                                                       
               ELSE                                                             
                   IF ASC-ADD-REQUEST                                           
                       SET PS-FATAL-ERROR             TO TRUE                   
                       SET DP020-MSG-FATAL            TO TRUE                   
                       SET DP020-NEXT-ACT-APPL-ERROR  TO TRUE                   
                       MOVE PC-TSYMSG-01494                                     
                                         TO  DP020-MSG-NUMBER                   
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  PS-SIZE-NOT-FOUND                                            
                   IF  ASC-ADD-REQUEST                                          
                       PERFORM 4200-CHECK-GPC-NRFC                              
                       IF  PS-NRFCODE-FOUND                                     
                           IF PS-NRFCODE-NOT-EXPIRED                            
                               SET PS-FATAL-ERROR             TO TRUE           
                               SET DP020-MSG-FATAL            TO TRUE           
                               SET DP020-NEXT-ACT-APPL-ERROR  TO TRUE           
                               MOVE PC-TSYMSG-01494                             
                                             TO DP020-MSG-NUMBER                
                           ELSE                                                 
                               SET ASC-REACTIVATE-EXP-SIZE    TO TRUE           
                               MOVE  0                        TO ANRFCDL        
                               MOVE -1                        TO AKDESCL        
                               SET DP020-MSG-WARNING          TO TRUE           
                               MOVE PC-TSYMSG-01429                             
                                             TO DP020-MSG-NUMBER                
                               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             
                           END-IF                                               
                       ELSE                                                     
                           IF PS-MULT-NRFCODES-FOUND                            
                               SET PS-FATAL-ERROR             TO TRUE           
                               SET DP020-MSG-FATAL            TO TRUE           
                               SET DP020-NEXT-ACT-APPL-ERROR  TO TRUE           
                               MOVE PC-TSYMSG-01493                             
                                                 TO  DP020-MSG-NUMBER           
                           ELSE                                                 
                               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
                       SET PS-FATAL-ERROR            TO TRUE                    
                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    
                       SET DP020-MSG-FATAL   TO  TRUE                           
                       MOVE PC-TSYMSG-01431  TO  DP020-MSG-NUMBER               
                   END-IF                                                       
               ELSE                                                             
                   IF  PS-MULT-SIZES-FOUND                                      
                       SET PS-FATAL-ERROR            TO TRUE                    
                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    
                       SET DP020-MSG-FATAL   TO  TRUE                           
                       MOVE PC-TSYMSG-00229  TO  DP020-MSG-NUMBER               
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * SELECT A ROW FROM THE DB2 TSIZE TABLE.  USE THE APPROPRIATE    *        
      * SELECT STATEMENT DEPENDING ON WHAT INFORMATION WAS PASSED      *        
      * INTO THIS PROGRAM THROUGH THE COMMAREA OR TEMPORARY STORAGE    *        
      * QUEUE.                                                         *        
      *----------------------------------------------------------------*        
       4100-GET-SIZE-INFO.                                                      
                                                                                
           SET PS-SIZE-NOT-FOUND       TO TRUE.                                 
           SET PS-NOT-INVALID-NRF-CODE TO TRUE.                                 
           SET ASC-SIZE-NOT-EXPIRED    TO TRUE.                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN ASC-SGC-NRF-SEL                                             
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_SIZE_CDE                                    
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-EFF-END-DATE :PV-END-DATE-NULL-IND              
                         , :ASC-NRF-SIZE-CODE                                   
                         , :ASC-PRI-DESC                                        
                         , :ASC-SEC-DESC                                        
                         , :ASC-KOHLS-DESC                                      
                         , :ASC-KOHLS-SIZE-CODE                                 
                         , :ASC-KOHLS-STD-SZ-ID-NUM                             
                               :ASC-STD-SZ-ID-NULL-IND                          
                         , :ASC-KOHLS-STD-SZ-DESC                               
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :ASC-EXTD-SZ-CDE                                     
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE    = :DK-SIZE-GP-CODE                   
                       AND  NRF_SIZE_CDE   = :DK-NRF-SIZE-CODE                  
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-KDSC-SEL                                            
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_SIZE_CDE                                    
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-EFF-END-DATE :PV-END-DATE-NULL-IND              
                         , :ASC-NRF-SIZE-CODE                                   
                         , :ASC-PRI-DESC                                        
                         , :ASC-SEC-DESC                                        
                         , :ASC-KOHLS-DESC                                      
                         , :ASC-KOHLS-SIZE-CODE                                 
                         , :ASC-KOHLS-STD-SZ-ID-NUM                             
                               :ASC-STD-SZ-ID-NULL-IND                          
                         , :ASC-KOHLS-STD-SZ-DESC                               
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :ASC-EXTD-SZ-CDE                                     
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  KOHLS_DESC       =  :DK-KOHLS-DESC                  
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-NRF-KDSC-SEL                                        
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_SIZE_CDE                                    
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-EFF-END-DATE :PV-END-DATE-NULL-IND              
                         , :ASC-NRF-SIZE-CODE                                   
                         , :ASC-PRI-DESC                                        
                         , :ASC-SEC-DESC                                        
                         , :ASC-KOHLS-DESC                                      
                         , :ASC-KOHLS-SIZE-CODE                                 
                         , :ASC-KOHLS-STD-SZ-ID-NUM                             
                               :ASC-STD-SZ-ID-NULL-IND                          
                         , :ASC-KOHLS-STD-SZ-DESC                               
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :ASC-EXTD-SZ-CDE                                     
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  NRF_SIZE_CDE     =  :DK-NRF-SIZE-CODE               
                       AND  KOHLS_DESC       =  :DK-KOHLS-DESC                  
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-KSC-SEL                                             
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_SIZE_CDE                                    
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-EFF-END-DATE :PV-END-DATE-NULL-IND              
                         , :ASC-NRF-SIZE-CODE                                   
                         , :ASC-PRI-DESC                                        
                         , :ASC-SEC-DESC                                        
                         , :ASC-KOHLS-DESC                                      
                         , :ASC-KOHLS-SIZE-CODE                                 
                         , :ASC-KOHLS-STD-SZ-ID-NUM                             
                               :ASC-STD-SZ-ID-NULL-IND                          
                         , :ASC-KOHLS-STD-SZ-DESC                               
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :ASC-EXTD-SZ-CDE                                     
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  KOHLS_SIZE_CDE   =  :DK-KOHLS-SIZE-CODE             
                  END-EXEC                                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    SET PS-SIZE-FOUND            TO TRUE                        
                    MOVE ASC-NRF-SIZE-CODE       TO PV-NRF-SIZE-CODE-N          
                    IF PV-NRF-SIZE-CODE-N > PC-VALID-MAX-NRF-CODE               
                        SET PS-INVALID-NRF-CODE  TO TRUE                        
                    END-IF                                                      
                    IF PV-END-DATE-NULL-IND < 0                                 
                        SET ASC-SIZE-NOT-EXPIRED TO TRUE                        
                        MOVE SPACES              TO ASC-EFF-END-DATE            
                    ELSE                                                        
                        SET ASC-SIZE-EXPIRED     TO TRUE                        
                    END-IF                                                      
                    IF ASC-STD-SZ-ID-NULL-IND < 0                               
                        MOVE SPACES            TO ASC-KOHLS-STD-SZ-ID           
                        MOVE SPACES            TO ASC-KOHLS-STD-SZ-DESC         
                    ELSE                                                        
                        MOVE ASC-KOHLS-STD-SZ-ID-NUM                            
                                               TO ASC-KOHLS-STD-SZ-ID           
                    END-IF                                                      
                    IF PV-STD-SZ-DESC-NULL-IND < 0                              
                        MOVE SPACES            TO ASC-KOHLS-STD-SZ-DESC         
                    END-IF                                                      
W32250              IF PV-EXTD-SZ-CDE-NULL-IND < 0                              
W32250                  MOVE SPACES TO ASC-EXTD-SZ-CDE                          
W32250                                 ASC-EXTD-SZ-DESC                         
W32250              ELSE                                                        
W32250                  PERFORM 4300-GET-EXTD-SZ-DESC                           
W32250              END-IF                                                      
               WHEN SQLCODE = +100                                              
                    SET PS-SIZE-NOT-FOUND        TO TRUE                        
               WHEN SQLCODE = -811                                              
                    SET PS-MULT-SIZES-FOUND      TO TRUE                        
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4100-GET-SIZE-INFO'                                   
                                     TO DP013-PARAGRAPH                         
                    MOVE 'SELECT FROM TSIZE'                                    
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * CHECK FOR EXISTING ACTIVE GP CODE/NRF CODE COMBINATION.        *        
      *----------------------------------------------------------------*        
       4200-CHECK-GPC-NRFC.                                                     
                                                                                
           SET PS-NRFCODE-NOT-FOUND    TO TRUE.                                 
           SET PS-NRFCODE-NOT-EXPIRED  TO TRUE.                                 
           SET PS-NOT-INVALID-NRF-CODE TO TRUE.                                 
                                                                                
           MOVE ASC-SIZE-GP-CODE       TO DK-SIZE-GP-CODE.                      
           MOVE ASC-NRF-SIZE-CODE      TO DK-NRF-SIZE-CODE.                     
                                                                                
           EXEC SQL                                                             
              SELECT                                                            
                     EFF_END_DTE                                                
                  ,  NRF_SIZE_CDE                                               
                  ,  PRIM_DESC                                                  
                  ,  SEC_DESC                                                   
                  ,  KOHLS_DESC                                                 
                  ,  KOHLS_SIZE_CDE                                             
W32250            ,  EXTD_SZ_CDE                                                
              INTO  :SIZE-EFF-END-DTE :PV-END-DATE-NULL-IND                     
                  , :SIZE-NRF-SIZE-CDE                                          
                  , :SIZE-PRIM-DESC                                             
                  , :SIZE-SEC-DESC                                              
                  , :SIZE-KOHLS-DESC                                            
                  , :SIZE-KOHLS-SIZE-CDE                                        
W32250            , :SIZE-EXTD-SZ-CDE :PV-EXTD-SZ-CDE-NULL-IND                  
              FROM                                                              
                     TSIZE                                                      
              WHERE                                                             
                     SIZE_GP_CDE    = :DK-SIZE-GP-CODE                          
                AND  NRF_SIZE_CDE   = :DK-NRF-SIZE-CODE                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    SET PS-NRFCODE-FOUND            TO TRUE                     
                    MOVE SIZE-KOHLS-SIZE-CDE                                    
                                         TO ASC-HOLD-KOHLS-SIZE-CODE            
                    MOVE SIZE-NRF-SIZE-CDE       TO PV-NRF-SIZE-CODE-N          
                    IF PV-NRF-SIZE-CODE-N > PC-VALID-MAX-NRF-CODE               
                        SET PS-INVALID-NRF-CODE  TO TRUE                        
                    END-IF                                                      
                    IF PV-END-DATE-NULL-IND < 0                                 
                        SET PS-NRFCODE-NOT-EXPIRED  TO TRUE                     
                    ELSE                                                        
                        SET PS-NRFCODE-EXPIRED      TO TRUE                     
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    SET PS-NRFCODE-NOT-FOUND        TO TRUE                     
               WHEN SQLCODE = -811                                              
                    SET PS-MULT-NRFCODES-FOUND      TO TRUE                     
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4200-CHECK-GPC-NRFC'                                  
                                     TO DP013-PARAGRAPH                         
                    MOVE 'SELECT FROM TSIZE'                                    
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
W32250*----------------------------------------------------------------*        
W32250*-  GET EXTENDED SZ DESC                                        -*        
W32250*----------------------------------------------------------------*        
W32250 4300-GET-EXTD-SZ-DESC.                                                   
W32250     EXEC SQL                                                             
W32250        SELECT EXTD_SZ_DESC                                               
W32250        INTO  :ASC-EXTD-SZ-DESC                                           
W32250        FROM  IMT_EXTD_SZ                                                 
W32250        WHERE EXTD_SZ_CDE = :ASC-EXTD-SZ-CDE                              
W32250     END-EXEC.                                                            
W32250                                                                          
W32250     EVALUATE TRUE                                                        
W32250         WHEN SQLCODE = ZERO                                              
W32250              MOVE ASC-EXTD-SZ-CDE  TO ET356-EXTD-SZ-CDE                  
W32250              MOVE ASC-EXTD-SZ-DESC TO ET356-EXTD-SZ-DESC                 
W32250         WHEN SQLCODE = +100                                              
W32250              MOVE SPACES TO ASC-EXTD-SZ-CDE                              
W32250                             ASC-EXTD-SZ-DESC                             
W32250                             ET356-EXTD-SZ-CDE                            
W32250                             ET356-EXTD-SZ-DESC                           
W32250         WHEN OTHER                                                       
W32250              MOVE '4300-GET-EXTD-SZ-DESC'                                
W32250                               TO DP013-PARAGRAPH                         
W32250              MOVE 'GET EXTENDED SIZE DESC'                               
W32250                               TO DP013-MESSAGE-TEXT (1)                  
W32250              MOVE SQLCA       TO DP013-SQLCA                             
W32250              SET DP013-DB2-ABEND                                         
W32250                               TO TRUE                                    
W32250              PERFORM DP013-0000-PROCESS-ABEND                            
W32250     END-EVALUATE.                                                        
W32250                                                                          
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE ASC-SIZE-GP-CODE         TO PV-SIZE-GP-CODE-N.                  
           MOVE PV-SIZE-GP-CODE-N        TO ASZGPCDO.                           
           MOVE ASC-SIZE-GP-DESC         TO ASZGPDSO.                           
           MOVE ASC-NRF-SIZE-CODE        TO ANRFCDO.                            
           MOVE ASC-PRI-DESC             TO APDESCO.                            
           MOVE ASC-SEC-DESC             TO ASDESCO.                            
           MOVE ASC-KOHLS-DESC           TO AKDESCO.                            
W32250     MOVE ASC-EXTD-SZ-DESC         TO AESZDSO.                            
           MOVE ASC-KOHLS-STD-SZ-ID      TO ASTNDSCO.                           
           MOVE ASC-KOHLS-STD-SZ-DESC    TO ASTNDDEO.                           
           IF ASC-ADD-REQUEST                                                   
               MOVE 'ENTER EITHER STANDARD SIZE CODE OR STANDARD DESCRIP        
      -             'TION BUT NOT BOTH'   TO ASTNMSGO                           
           END-IF.                                                              
                                                                                
W32250*----------------------------------------------------------------*        
W32250* EDITED KEY IN EXTENDED SZ DESC                                 *        
W32250*----------------------------------------------------------------*        
W32250 4500-CHECK-EXTD-SZ-INFO.                                                 
W32250                                                                          
W32250     EXEC SQL                                                             
W32250        SELECT EXTD_SZ_CDE                                                
W32250        INTO  :ASC-EXTD-SZ-CDE                                            
W32250        FROM  IMT_EXTD_SZ                                                 
W32250        WHERE EXTD_SZ_DESC = :ASC-EXTD-SZ-DESC                            
W32250     END-EXEC.                                                            
W32250                                                                          
W32250     EVALUATE TRUE                                                        
W32250         WHEN SQLCODE = ZERO                                              
W32250              SET PS-NOT-INVALID-EXTD-SZ TO TRUE                          
W32250         WHEN SQLCODE = +100                                              
W32250              CONTINUE                                                    
W32250         WHEN OTHER                                                       
W32250              MOVE '4500-CHECK-EXTD-SZ-INFO'                              
W32250                               TO DP013-PARAGRAPH                         
W32250              MOVE 'CHECKING EXTENDED SIZE DESC'                          
W32250                               TO DP013-MESSAGE-TEXT (1)                  
W32250              MOVE SQLCA       TO DP013-SQLCA                             
W32250              SET DP013-DB2-ABEND                                         
W32250                               TO TRUE                                    
W32250              PERFORM DP013-0000-PROCESS-ABEND                            
W32250     END-EVALUATE.                                                        
W32250                                                                          
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ADD A NEW SIZE TO THE DB2 TSIZE TABLE.                         *        
      *----------------------------------------------------------------*        
       5000-ADD-SIZE.                                                           
                                                                                
           MOVE -1                   TO PV-END-DATE-NULL-IND.                   
                                                                                
           MOVE ASC-NRF-SIZE-CODE    TO PV-KOHLS-SIZE-CODE-N.                   
           MOVE PV-KOHLS-SIZE-CODE-N TO SIZE-KOHLS-SIZE-CDE.                    
           MOVE ASC-SIZE-GP-CODE     TO SIZE-SIZE-GP-CDE.                       
           MOVE ASC-NRF-SIZE-CODE    TO SIZE-NRF-SIZE-CDE.                      
           MOVE PC-SIZE-RNG-CDE      TO SIZE-SIZE-RNG-CDE.                      
           MOVE ASC-PRI-DESC         TO SIZE-PRIM-DESC.                         
           MOVE ASC-SEC-DESC         TO SIZE-SEC-DESC.                          
           MOVE ASC-KOHLS-DESC       TO SIZE-KOHLS-DESC.                        
W32250     MOVE ASC-EXTD-SZ-CDE      TO SIZE-EXTD-SZ-CDE.                       
           MOVE ASC-KOHLS-STD-SZ-ID-NUM                                         
                                     TO SIZE-KOHLS-STD-SZ-ID.                   
                                                                                
                                                                                
           PERFORM 5100-INSERT-TSIZE-TABLE.                                     
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 9000-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** INSERT THE ROW ON THE DB2 TABLE.                             **        
      ******************************************************************        
       5100-INSERT-TSIZE-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   INSERT INTO TSIZE                                            
                      (                                                         
                       KOHLS_SIZE_CDE                                           
                      ,EFF_BEG_DTE                                              
                      ,EFF_END_DTE                                              
                      ,SIZE_GP_CDE                                              
                      ,NRF_SIZE_CDE                                             
                      ,SIZE_RNG_CDE                                             
                      ,PRIM_DESC                                                
                      ,SEC_DESC                                                 
                      ,KOHLS_DESC                                               
                      ,KOHLS_STD_SZ_ID                                          
W32250                ,EXTD_SZ_CDE                                              
                      )                                                         
                   VALUES                                                       
                      (                                                         
                       :SIZE-KOHLS-SIZE-CDE                                     
                      ,CURRENT DATE                                             
                      ,:SIZE-EFF-END-DTE                                        
                       :PV-END-DATE-NULL-IND                                    
                      ,:SIZE-SIZE-GP-CDE                                        
                      ,:SIZE-NRF-SIZE-CDE                                       
                      ,:SIZE-SIZE-RNG-CDE                                       
                      ,:SIZE-PRIM-DESC                                          
                      ,:SIZE-SEC-DESC                                           
                      ,:SIZE-KOHLS-DESC                                         
                      ,:SIZE-KOHLS-STD-SZ-ID                                    
W32250                ,:SIZE-EXTD-SZ-CDE                                        
                      )                                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5100-INSERT-TSIZE-TABLE'                           
                                         TO DP013-PARAGRAPH                     
                       MOVE 'INSERT A ROW ON THE SIZE TABLE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSIZE'     TO DP013-DB2-TABLE-NAME (1)             
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5100-INSERT-TSIZE-TABLE'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT A SIZ        
      -             'E RECORD'           TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSIZE'              TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * REACTIVATE THE EXISTING EXPIRED SIZE.                          *        
      *----------------------------------------------------------------*        
       5500-REACT-EXP-SIZE.                                                     
                                                                                
           MOVE ASC-HOLD-KOHLS-SIZE-CODE TO DK-KOHLS-SIZE-CODE.                 
                                                                                
           PERFORM 5600-REACT-EXP-TSIZE-TABLE.                                  
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 9000-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** UPDATE (REACTIVATE) THE ROW ON THE DB2 TSIZE TABLE.          **        
      ******************************************************************        
       5600-REACT-EXP-TSIZE-TABLE.                                              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   UPDATE TSIZE                                                 
                       SET NRF_SIZE_CDE    = :ASC-NRF-SIZE-CODE                 
                         , PRIM_DESC       = :ASC-PRI-DESC                      
                         , SEC_DESC        = :ASC-SEC-DESC                      
                         , KOHLS_DESC      = :ASC-KOHLS-DESC                    
                         , EFF_BEG_DTE     =  CURRENT DATE                      
                         , EFF_END_DTE     =  NULL                              
                         , KOHLS_STD_SZ_ID = :ASC-KOHLS-STD-SZ-ID-NUM           
W32250                   , EXTD_SZ_CDE     = :ASC-EXTD-SZ-CDE                   
                   WHERE KOHLS_SIZE_CDE = :DK-KOHLS-SIZE-CODE                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5600-REACT-EXP-TSIZE-TABLE'                        
                                         TO DP013-PARAGRAPH                     
                       MOVE 'UPDATE A ROW ON THE SIZE TABLE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSIZE'     TO DP013-DB2-TABLE-NAME (1)             
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5600-REACT-EXP-TSIZE-TABLE'                                
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO UPDATE A SIZ        
      -             'E RECORD'           TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSIZE'              TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * APPLY THE CHANGES TO THE MESSAGE TO THE DB2 TABLE.             *        
      *----------------------------------------------------------------*        
       6000-UPDATE-SIZE.                                                        
                                                                                
           MOVE ASC-KOHLS-SIZE-CODE      TO DK-KOHLS-SIZE-CODE.                 
                                                                                
           PERFORM 6100-UPDATE-TSIZE-TABLE.                                     
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 9000-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** UPDATE THE ROW ON THE DB2 SIZE TABLE.                        **        
      ******************************************************************        
       6100-UPDATE-TSIZE-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   UPDATE TSIZE                                                 
                       SET NRF_SIZE_CDE    = :ASC-NRF-SIZE-CODE                 
                         , PRIM_DESC       = :ASC-PRI-DESC                      
                         , SEC_DESC        = :ASC-SEC-DESC                      
                         , KOHLS_DESC      = :ASC-KOHLS-DESC                    
                         , KOHLS_STD_SZ_ID = :ASC-KOHLS-STD-SZ-ID-NUM           
W32250                   , EXTD_SZ_CDE     = :ASC-EXTD-SZ-CDE                   
                   WHERE KOHLS_SIZE_CDE = :DK-KOHLS-SIZE-CODE                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '6100-UPDATE-TSIZE-TABLE'                           
                                         TO DP013-PARAGRAPH                     
                       MOVE 'UPDATE A ROW ON THE SIZE TABLE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSIZE'     TO DP013-DB2-TABLE-NAME (1)             
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '6100-UPDATE-TSIZE-TABLE'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO UPDATE A SIZ        
      -             'E RECORD'           TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSIZE'              TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DELETE(EXPIRE) THE SIZE ON THE TSIZE TABLE.                    *        
      *----------------------------------------------------------------*        
       7000-DELETE-SIZE.                                                        
                                                                                
           MOVE ASC-KOHLS-SIZE-CODE      TO DK-KOHLS-SIZE-CODE.                 
                                                                                
           PERFORM 7100-DELETE-TSIZE-TABLE.                                     
           IF  ASC-USE-SELECTION-QUEUE                                          
               PERFORM 9000-UPDATE-SELECTION-QUEUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** EXPIRE THE ROW ON THE DB2 TABLE BY CHANGING THE EFFECTIVE    **        
      ** END DATE TO THE CURRENT DATE.                                **        
      ******************************************************************        
       7100-DELETE-TSIZE-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   UPDATE TSIZE                                                 
                       SET EFF_END_DTE   = CURRENT DATE                         
                   WHERE KOHLS_SIZE_CDE = :DK-KOHLS-SIZE-CODE                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '7100-DELETE-TSIZE-TABLE'                           
                                         TO DP013-PARAGRAPH                     
                       MOVE 'DELETE A ROW ON THE SIZE TABLE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSIZE'     TO DP013-DB2-TABLE-NAME (1)             
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '7100-DELETE-TSIZE-TABLE'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO DELETE A SIZ        
      -             'E RECORD'           TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSIZE'              TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * CHECK FOR EXISTING STANDARD SIZE CODE AND DESCRIPTION.         *        
      *----------------------------------------------------------------*        
       8000-CHECK-STANDARD-SIZE.                                                
                                                                                
           MOVE ASC-KOHLS-STD-SZ-ID-NUM TO IMT00021-KOHLS-STD-SZ-ID.            
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES                         
                      OR SQLCODE = ZERO                                         
                      OR SQLCODE = +100 )                                       
               EXEC SQL                                                         
                  SELECT KOHLS_STD_SZ_DESC                                      
                  INTO  :IMT00021-KOHLS-STD-SZ-DESC                             
                  FROM                                                          
                         IMT_KOHLS_STD_SZ                                       
                  WHERE                                                         
                         KOHLS_STD_SZ_ID = :IMT00021-KOHLS-STD-SZ-ID            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        SET PS-STANDARD-SIZE-FOUND   TO TRUE                    
                        MOVE IMT00021-KOHLS-STD-SZ-DESC                         
                                             TO ASC-KOHLS-STD-SZ-DESC           
                        MOVE ASC-KOHLS-STD-SZ-DESC TO ASTNDDEO                  
                   WHEN SQLCODE = +100                                          
                        SET PS-STANDARD-SIZE-NOT-FOUND  TO TRUE                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '8000-CHECK-STANDARD-SIZE'                          
                                         TO DP013-PARAGRAPH                     
                       MOVE 'SELECT A ROW ON IMT_KOHLS_STD_SZ'                  
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_KOHLS_STD_SZ'                                  
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '8000-CHECK-STANDARD-SIZE'                                  
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT AN IM        
      -             'T_KOHLS_STD_SZ ROW' TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_KOHLS_STD_SZ'   TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * INSERT A NEW STANDARD SIZE DESCRIPTION TABLE ROW.              *        
      *----------------------------------------------------------------*        
       8100-INSERT-KOHLS-STD-SZ-DESC.                                           
                                                                                
           MOVE ASC-KOHLS-STD-SZ-DESC TO IMT00021-KOHLS-STD-SZ-DESC.            
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   INSERT INTO IMT_KOHLS_STD_SZ                                 
                      (                                                         
                       KOHLS_STD_SZ_DESC                                        
                      )                                                         
                   VALUES                                                       
                      (                                                         
                       :IMT00021-KOHLS-STD-SZ-DESC                              
                      )                                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '8100-INSERT-KOHLS-STD-SZ-DESC'                     
                                         TO DP013-PARAGRAPH                     
                       MOVE 'INSERT A ROW ON IMT_KOHLS_STD_SZ'                  
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_KOHLS_STD_SZ'                                  
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '8100-INSERT-KOHLS-STD-SZ-DESC'                             
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT AN IM        
      -             'T_KOHLS_STD_SZ ROW' TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_KOHLS_STD_SZ'   TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * FIND THE STANDARD SIZE JUST CREATED MATCHING THE DESCRIPTION.  *        
      *----------------------------------------------------------------*        
       8200-FIND-KOHLS-STD-SZ-DESC.                                             
                                                                                
           MOVE ASC-KOHLS-STD-SZ-DESC TO IMT00021-KOHLS-STD-SZ-DESC.            
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL (PV-RETRY-COUNTER > PC-MAX-RETRIES                         
                      OR SQLCODE = ZERO )                                       
               EXEC SQL                                                         
                  SELECT MAX(KOHLS_STD_SZ_ID)                                   
                  INTO  :IMT00021-KOHLS-STD-SZ-ID                               
                  FROM                                                          
                         IMT_KOHLS_STD_SZ                                       
                  WHERE                                                         
                         KOHLS_STD_SZ_DESC = :IMT00021-KOHLS-STD-SZ-DESC        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                        MOVE IMT00021-KOHLS-STD-SZ-ID                           
                              TO ASC-KOHLS-STD-SZ-ID                            
                                 ASC-KOHLS-STD-SZ-ID-NUM                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '8200-FIND-KOHLS-STD-SZ-DESC'                       
                                         TO DP013-PARAGRAPH                     
                       MOVE 'SELECT A ROW ON IMT_KOHLS_STD_SZ'                  
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'IMT_KOHLS_STD_SZ'                                  
                                         TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '8200-FIND-KOHLS-STD-SZ-DESC'                               
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT AN IM        
      -             'T_KOHLS_STD_SZ ROW' TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'IMT_KOHLS_STD_SZ'   TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * RECORD IN THE SELECTION TEMP STORAGE QUEUE THE FACT THAT THIS  *        
      * OBJECT WAS ADDED, UPDATED, OR DELETED(EXPIRED).                *        
      *----------------------------------------------------------------*        
       9000-UPDATE-SELECTION-QUEUE.                                             
                                                                                
           PERFORM 9100-READ-SEL-QUEUE.                                         
                                                                                
           EVALUATE ASC-INT-APPL-FORMAT-IND                                     
                                                                                
               WHEN PC-IA-ADD-LIST                                              
                    SET ET353-ADD (ASC-SEL-SUB) TO TRUE                         
               WHEN PC-IA-UPD-LIST                                              
                    SET ET353-UPD (ASC-SEL-SUB) TO TRUE                         
               WHEN PC-IA-DEL-LIST                                              
                    SET ET353-DEL (ASC-SEL-SUB) TO TRUE                         
               WHEN OTHER                                                       
                    SET ET353-ERR (ASC-SEL-SUB) TO TRUE                         
                                                                                
           END-EVALUATE.                                                        
                                                                                
           PERFORM 9200-REWRITE-LIST-QUEUE.                                     
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9100-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (ET353-SIZE-SEL-REC)                                   
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9200-REWRITE-LIST-QUEUE.                                                 
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (ET353-SIZE-SEL-REC)                                 
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESPONSE)                                   
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-LIST-QUEUE'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DATE AND TIME MODULE                                        *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
                                                                                
