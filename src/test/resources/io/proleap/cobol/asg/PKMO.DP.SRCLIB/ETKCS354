      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    ETKCS354.                                                 
       AUTHOR.        DAN GRUBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-30-96.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    E354 - SIZE INQUIRY PROGRAM.                               *         
      *                                                               *         
      *    THIS PROGRAM DISPLAYS THE SIZE GROUP CODE/DESCRIPTION,     *         
      *    NRF SIZE CODE, KOHLS DESCRIPTION, PRIMARY DESCRIPTION,     *         
      *    AND SECONDARY DESCRIPTION.                                 *         
      *                                                               *         
      *    IT CAN BE CALLED FROM EITHER THE SIZE CRITERIA SCREEN      *         
      *    OR THE SIZE LIST SCREEN.  AFTER THE INFORMATION IS         *         
      *    DISPLAYED ON THE SCREEN, THE USER CAN PRESS THE PF3        *         
      *    KEY TO RETURN TO THE PREVIOUS SCREEN.                      *         
      *---------------------------------------------------------------*         
      *                                                                         
      *   11/03/2006  P. KEBER      WR28222 - SIZE PROFILE PROJECT              
      *   ADD STANDARD SIZE CODE AND DESCRIPTION TO THE SCREEN.                 
W32250*                                                                         
W32250*   08/17/2007  JILL LIN      WR32250 - ECOMM NEW ATTRIBUTES              
W32250*   ADD EXTENDED SIZE CODE AND DESCRIPTION TO THE SCREEN.                 
      *---------------------------------------------------------------*         
CICS  *  CHANGED 10/28/2022    BY BARB SCHULTZ    WR: CHG0277210      *         
CICS  *                                                               *         
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   *         
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    *         
CICS  *---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           15  DK-SIZE-GP-CODE                PIC S9(04)  VALUE ZERO            
                                                          COMP.                 
           15  DK-SIZE-GP-DESC                PIC  X(30)  VALUE SPACE.          
           15  DK-KOHLS-SIZE-CODE             PIC S9(09)  VALUE ZERO            
                                                          COMP.                 
           15  DK-NRF-SIZE-CODE               PIC  X(05)  VALUE SPACE.          
           15  DK-KOHLS-DESC                  PIC  X(10)  VALUE SPACE.          
           15  DK-KOHLS-DESC-BYTE             REDEFINES DK-KOHLS-DESC           
                                              OCCURS 10 TIMES                   
                                              PIC  X(01).                       
           15  DK-EXP-SIZE-IND                PIC  X(01)  VALUE SPACE.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'ET354A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'ETKM354 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'ETKCS354'.           
           05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
           05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
           05  PC-PERCENT                     PIC  X(01)  VALUE '%'.            
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
                                                           COMP-3.              
                                                                                
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
           05  PC-TSYMSG-00229                PIC 9(5)    VALUE 00229.          
           05  PC-TSYMSG-01427                PIC 9(5)    VALUE 01427.          
           05  PC-TSYMSG-01431                PIC 9(5)    VALUE 01431.          
           05  PC-TSYMSG-01432                PIC 9(5)    VALUE 01432.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-DONE-SW                     PIC  X(01)   VALUE 'N'.           
               88  PS-DONE                                 VALUE 'Y'.           
               88  PS-NOT-DONE                             VALUE 'N'.           
           05  PS-LIST-END-SW                 PIC  X(01)   VALUE 'N'.           
               88  PS-LIST-END                             VALUE 'Y'.           
               88  PS-NO-LIST-END                          VALUE 'N'.           
           05  PS-SIZE-FOUND-SW               PIC  X(01)   VALUE 'N'.           
               88  PS-SIZE-FOUND                           VALUE 'Y'.           
               88  PS-SIZE-NOT-FOUND                       VALUE 'N'.           
               88  PS-MULT-SIZES-FOUND                     VALUE 'M'.           
           05  PS-SETUP-LIKE-FIELD-SW         PIC  X(01)   VALUE 'N'.           
               88  PS-SETUP-LIKE-FIELD                     VALUE 'Y'.           
               88  PS-DONT-SETUP-LIKE-FIELD                VALUE 'N'.           
           05  PS-ENOUGH-DATA-SW              PIC  X(01)   VALUE 'Y'.           
               88  PS-ENOUGH-DATA                          VALUE 'Y'.           
               88  PS-NOT-ENOUGH-DATA                      VALUE 'N'.           
                                                                                
           EJECT                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-CICS-RESP                   PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-SIZE-GP-CODE-N              PIC  9(02)   VALUE ZEROES.        
           05  PV-END-DATE-NULL-IND           PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-STD-SZ-DESC-NULL-IND        PIC S9(04)   VALUE +0             
                                                           COMP.                
W32250     05  PV-EXTD-SZ-CDE                 PIC  X(02)   VALUE SPACES.        
W32250     05  PV-EXTD-SZ-DESC                PIC  X(20)   VALUE SPACES.        
W32250     05  PV-EXTD-SZ-CDE-NULL-IND        PIC S9(04)   VALUE +0             
W32250                                                     COMP.                
                                                                                
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      *    INTER-APPLICATION COMMAREA.                                *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY ETWS351.                                                        
           COPY ETWS352.                                                        
      *---------------------------------------------------------------*         
      *    SPECIFIC COMMMAREA FOR ETKCS354.                           *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-LIST-KEYS.                                           
                       20  ASC-KEY-SIZE-GP-CODE            PIC S9(04)           
                                                           COMP.                
                       20  ASC-KEY-EFF-END-DATE            PIC  X(10).          
                       20  ASC-KEY-SIZE-GP-DESC            PIC  X(30).          
                       20  ASC-KEY-KOHLS-SIZE-CODE         PIC S9(09)           
                                                           COMP.                
                       20  ASC-KEY-NRF-SIZE-CODE           PIC  X(05).          
                       20  ASC-KEY-KOHLS-DESC              PIC  X(10).          
                       20  ASC-KEY-PRI-DESC                PIC  X(40).          
                       20  ASC-KEY-SEC-DESC                PIC  X(40).          
                       20  ASC-KEY-EXP-SIZE-IND            PIC  X(01).          
                       20  ASC-KEY-STD-SZ-ID               PIC S9(09)           
                                                           COMP.                
                       20  ASC-KEY-STD-SZ-ID-NULL-IND      PIC S9(04)           
                                                           COMP.                
                       20  ASC-KEY-STD-SZ-DESC             PIC  X(10).          
                   15  ASC-SEL-SUB                         PIC S9(04)           
                                                               COMP.            
                   15  ASC-SEL-ITEM                        PIC S9(04)           
                                                               COMP.            
                   15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
                                                               COMP-3.          
                   15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                                                                                
                   15  ASC-SELECT-SW                       PIC  X(01).          
                       88  ASC-SGC-NRF-SEL                 VALUE 'A'.           
                       88  ASC-SGC-KDSC-SEL                VALUE 'B'.           
                       88  ASC-SGC-NRF-KDSC-SEL            VALUE 'C'.           
                       88  ASC-SGC-KSC-SEL                 VALUE 'D'.           
           05  ASC-SIZE-EXPIRED-SW            PIC  X(01)   VALUE 'N'.           
               88  ASC-SIZE-EXPIRED                        VALUE 'Y'.           
               88  ASC-SIZE-NOT-EXPIRED                    VALUE 'N'.           
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    SIZE SELECTION TSQ RECORD LAYOUT.                          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY ETWS353.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY ETKM354.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR THE TSIZE TABLE                               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSIZE                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR THE IMT_KOHLS_STD_SZ TABLE                    *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE IMT00021                                                
           END-EXEC.                                                            
                                                                                
W32250*---------------------------------------------------------------*         
W32250*    DB2 AREA FOR THE IMT_EXTD_SZ TABLE                         *         
W32250*---------------------------------------------------------------*         
W32250     EXEC SQL                                                             
W32250          INCLUDE IMT00025                                                
W32250     END-EXEC.                                                            
W32250                                                                          
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
           MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
           MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
           SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
           MOVE LENGTH OF ET354AI       TO DP030-MAP-LENGTH (1).                
           MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING.                                     
                                                                                
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      *----------------------------------------------------------------*        
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
      *----------------------------------------------------------------*        
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          ET354AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK TO TRUE                                    
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               SET DP013-CICS-ABEND TO TRUE                                     
CICS           MOVE '0001-CALL-CICS-ARCH-API'                                   
CICS                                     TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                     TO DP013-MESSAGE-TEXT (3)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *----------------------------------------------------------------*        
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
      ***-----$TEMPLATE NOTE$-------------------------------------------        
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
      ***---------------------------------------------------------------        
                   IF  PS-COMMAREA-NOT-VALID                                    
                       IF  PS-NOT-ENOUGH-DATA                                   
                           MOVE PC-TSYMSG-01432   TO DP020-MSG-NUMBER           
                           SET DP020-MSG-FATAL              TO TRUE             
                           SET DP020-NEXT-ACT-APPL-ERROR    TO TRUE             
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      ******************************************************************        
      ** DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **        
      ** INTER-APPLICATION FORMAT CODE.  IF THIS PROGRAM WAS CALLED   **        
      ** FROM THE CRITERIA SCREEN, USE THE DATA IN THE INTER-         **        
      ** APPLICATION COMMAREA.  IF THIS PROGRAM WAS CALLED FROM THE   **        
      ** SIZE LIST SCREEN, USE THE DATA PASSED IN THE TEMPORARY       **        
      ** STORAGE QUEUE.                                               **        
      ******************************************************************        
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID     TO TRUE.                                   
           INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
                                                                                
           EVALUATE DP020-INT-APPL-FORMAT-IND                                   
               WHEN PC-CRIT-SPEC-FORMAT                                         
                   PERFORM 1110-EVALUATE-INTER-APPL-COM                         
               WHEN PC-LIST-FORMAT                                              
                   SET ASC-USE-SELECTION-QUEUE TO TRUE                          
                   SET ASC-SGC-KSC-SEL         TO TRUE                          
                   MOVE ET352-SIZE-GP-CODE                                      
                                     TO ASC-KEY-SIZE-GP-CODE                    
                                        DK-SIZE-GP-CODE                         
                   MOVE ET352-SIZE-GP-DESC                                      
                                     TO ASC-KEY-SIZE-GP-DESC                    
                   MOVE ET352-NUMBER-OF-TS-ITEMS                                
                                     TO ASC-NUMBER-OF-TS-ITEMS                  
                   MOVE ET352-SEL-QUEUE-NAME                                    
                                     TO ASC-SEL-QUEUE-NAME                      
                   PERFORM 1500-PREPARE-LIST-ENTRY                              
               WHEN OTHER                                                       
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    EVALUATE THE DATA SENT TO THE INQUIRY PROGRAM FROM THE      *        
      *    SIZE CRITERIA SCREEN THROUGH THE INTER APPLICATION          *        
      *    COMMAREA.                                                   *        
      *----------------------------------------------------------------*        
       1110-EVALUATE-INTER-APPL-COM.                                            
                                                                                
           SET PS-DONT-SETUP-LIKE-FIELD  TO TRUE.                               
           SET PS-ENOUGH-DATA            TO TRUE.                               
                                                                                
           IF  ET351-NRF-SIZE-CODE GREATER THAN SPACES                          
               IF  ET351-KOHLS-DESC GREATER THAN SPACES                         
                   SET PS-SETUP-LIKE-FIELD          TO TRUE                     
                   SET ASC-SGC-NRF-KDSC-SEL         TO TRUE                     
               ELSE                                                             
                   SET ASC-SGC-NRF-SEL              TO TRUE                     
               END-IF                                                           
           ELSE                                                                 
               IF  ET351-KOHLS-DESC GREATER THAN SPACES                         
                   SET PS-SETUP-LIKE-FIELD          TO TRUE                     
                   SET ASC-SGC-KDSC-SEL             TO TRUE                     
               ELSE                                                             
                   SET PS-COMMAREA-NOT-VALID        TO TRUE                     
                   SET PS-NOT-ENOUGH-DATA           TO TRUE                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ET351-SIZE-GP-CODE     TO  ASC-KEY-SIZE-GP-CODE                 
                                           DK-SIZE-GP-CODE.                     
           MOVE ET351-SIZE-GP-DESC     TO  ASC-KEY-SIZE-GP-DESC                 
                                           DK-SIZE-GP-DESC.                     
           MOVE ET351-NRF-SIZE-CODE    TO  ASC-KEY-NRF-SIZE-CODE                
                                           DK-NRF-SIZE-CODE.                    
           MOVE ET351-KOHLS-DESC       TO  ASC-KEY-KOHLS-DESC                   
                                           DK-KOHLS-DESC.                       
           MOVE ET351-EXP-SIZE-IND     TO  ASC-KEY-EXP-SIZE-IND                 
                                           DK-EXP-SIZE-IND.                     
                                                                                
           IF  PS-SETUP-LIKE-FIELD                                              
               PERFORM 1120-SETUP-KDESC-LIKE                                    
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * SETUP THE KOHLS DESCRIPTION FIELD REPLACING '*' BY '%' SO IT   *        
      *   CAN BE USED IN THE DB2 LIKE CLAUSE OF THE SELECT.            *        
      *----------------------------------------------------------------*        
       1120-SETUP-KDESC-LIKE.                                                   
                                                                                
           IF  DK-KOHLS-DESC-BYTE (1) EQUAL '*'                                 
               MOVE PC-PERCENT                                                  
                             TO DK-KOHLS-DESC-BYTE (1)                          
               INSPECT DK-KOHLS-DESC                                            
                   REPLACING FIRST '*' BY PC-PERCENT                            
           END-IF.                                                              
                                                                                
           SET PS-NOT-DONE   TO TRUE.                                           
                                                                                
           PERFORM VARYING PV-SUB                                               
               FROM LENGTH OF DK-KOHLS-DESC BY -1                               
               UNTIL PV-SUB EQUAL 1                                             
                 OR  PS-DONE                                                    
                                                                                
                   IF  DK-KOHLS-DESC-BYTE (PV-SUB) = '*'                        
                       MOVE PC-PERCENT                                          
                             TO DK-KOHLS-DESC-BYTE (PV-SUB)                     
                       SET PS-DONE                                              
                             TO TRUE                                            
                   END-IF                                                       
                                                                                
                   IF  DK-KOHLS-DESC-BYTE (PV-SUB) > SPACES                     
                       SET PS-DONE                                              
                             TO TRUE                                            
                   ELSE                                                         
                       MOVE PC-PERCENT                                          
                             TO DK-KOHLS-DESC-BYTE (PV-SUB)                     
                   END-IF                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
      *----------------------------------------------------------------*        
       1500-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE ET353-MAX-ITEMS TO ASC-SEL-SUB.                                 
           MOVE ZERO            TO ASC-SEL-ITEM.                                
           PERFORM 2100-GET-NEXT-SIZE.                                          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
      *----------------------------------------------------------------*        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   IF ASC-SIZE-EXPIRED                                          
                       SET DP020-MSG-INFORMATIONAL    TO TRUE                   
                       MOVE PC-TSYMSG-01427                                     
                                         TO  DP020-MSG-NUMBER                   
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   MOVE ASC-KEY-SIZE-GP-CODE                                    
                                         TO DK-SIZE-GP-CODE                     
                   MOVE ASC-KEY-SIZE-GP-DESC                                    
                                         TO DK-SIZE-GP-DESC                     
                   MOVE ASC-KEY-KOHLS-SIZE-CODE                                 
                                         TO DK-KOHLS-SIZE-CODE                  
                   MOVE ASC-KEY-NRF-SIZE-CODE                                   
                                         TO DK-NRF-SIZE-CODE                    
                   MOVE ASC-KEY-KOHLS-DESC                                      
                                         TO DK-KOHLS-DESC                       
                   MOVE ASC-KEY-EXP-SIZE-IND                                    
                                         TO DK-EXP-SIZE-IND                     
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                   MOVE ASC-KEY-SIZE-GP-CODE                                    
                                         TO DK-SIZE-GP-CODE                     
                   MOVE ASC-KEY-SIZE-GP-DESC                                    
                                         TO DK-SIZE-GP-DESC                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2100-GET-NEXT-SIZE                               
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                   MOVE ASC-KEY-SIZE-GP-CODE                                    
                                         TO DK-SIZE-GP-CODE                     
                   MOVE ASC-KEY-SIZE-GP-DESC                                    
                                         TO DK-SIZE-GP-DESC                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2150-GET-PREV-SIZE                               
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   ELSE                                                         
                       MOVE PC-TSYMSG-00069                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   IF ASC-SIZE-EXPIRED                                          
                       SET DP020-MSG-INFORMATIONAL    TO TRUE                   
                       MOVE PC-TSYMSG-01427                                     
                                         TO  DP020-MSG-NUMBER                   
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   SET DP013-CICS-ABEND TO TRUE                                 
                   MOVE '2000-PROCESS-PANEL' TO DP013-PARAGRAPH                 
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    GET THE NEXT SIZE IF A LIST OF SIZES ARE BEING PROCESSED.   *        
      *----------------------------------------------------------------*        
       2100-GET-NEXT-SIZE.                                                      
                                                                                
           ADD +1                       TO ASC-SEL-SUB.                         
           IF  ASC-SEL-SUB > ET353-MAX-ITEMS                                    
               ADD +1                   TO ASC-SEL-ITEM                         
               MOVE +1                  TO ASC-SEL-SUB                          
           END-IF.                                                              
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  ET353-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE ET353-KOHLS-SIZE-CODE (ASC-SEL-SUB)                     
                                        TO ASC-KEY-KOHLS-SIZE-CODE              
                                           DK-KOHLS-SIZE-CODE                   
                   SET ET353-INQ (ASC-SEL-SUB) TO TRUE                          
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
                   MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
                   SET PS-LIST-END             TO TRUE                          
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE ET353-MAX-ITEMS     TO ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE PREVIOUS SIZE IF A LIST OF SIZES ARE BEING          *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
       2150-GET-PREV-SIZE.                                                      
                                                                                
           SUBTRACT +1 FROM ASC-SEL-SUB.                                        
           IF  ASC-SEL-SUB < +1                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE ET353-MAX-ITEMS     TO ASC-SEL-SUB                          
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM > ZERO                                              
               PERFORM 9100-READ-SEL-QUEUE                                      
               MOVE ET353-KOHLS-SIZE-CODE (ASC-SEL-SUB)                         
                                        TO ASC-KEY-KOHLS-SIZE-CODE              
                                           DK-KOHLS-SIZE-CODE                   
           ELSE                                                                 
               MOVE +1                  TO ASC-SEL-ITEM                         
                                           ASC-SEL-SUB                          
               MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THIS PARAGRAPH POSITIONS THE CURSOR AT THE BOTTOM OF THE    *        
      *    SCREEN IN THE COMMAND FIELD SINCE NONE OF THE FIELDS ON THE *        
      *    INQUIRY SCREEN ARE ENTERABLE.                               *        
      *----------------------------------------------------------------*        
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           SET DP030-SET-CURSOR-TRAILER      TO TRUE.                           
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RETRIEVE THE SIZE INFORMATION TO BE DISPLAYED AND ALSO      *        
      *    PREPARE THE APPROPRIATE MESSAGE TO BE DISPLAYED AT THE      *        
      *    BOTTOM OF THE SCREEN.                                       *        
      *----------------------------------------------------------------*        
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           PERFORM 4300-GET-SIZE-INFO.                                          
                                                                                
           IF  PS-SIZE-NOT-FOUND                                                
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-01431  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               IF  PS-MULT-SIZES-FOUND                                          
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
                   SET DP020-MSG-FATAL   TO  TRUE                               
                   MOVE PC-TSYMSG-00229  TO  DP020-MSG-NUMBER                   
               ELSE                                                             
                   IF ASC-SIZE-EXPIRED                                          
                       SET DP020-MSG-INFORMATIONAL    TO TRUE                   
                       MOVE PC-TSYMSG-01427                                     
                                         TO  DP020-MSG-NUMBER                   
                   END-IF                                                       
                   SET DP030-SET-CURSOR-TRAILER       TO TRUE                   
W32250             IF PV-EXTD-SZ-CDE-NULL-IND < 0                               
W32250                 MOVE SPACE TO PV-EXTD-SZ-DESC                            
W32250             ELSE                                                         
W32250                 PERFORM 4500-GET-EXTD-SZ-INFO                            
W32250             END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
       4300-GET-SIZE-INFO.                                                      
                                                                                
           SET PS-SIZE-NOT-FOUND     TO TRUE.                                   
           SET ASC-SIZE-NOT-EXPIRED  TO TRUE.                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN ASC-SGC-NRF-SEL                                             
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-KEY-EFF-END-DATE :PV-END-DATE-NULL-IND          
                         , :ASC-KEY-NRF-SIZE-CODE                               
                         , :ASC-KEY-PRI-DESC                                    
                         , :ASC-KEY-SEC-DESC                                    
                         , :ASC-KEY-KOHLS-DESC                                  
                         , :ASC-KEY-STD-SZ-ID                                   
                               :ASC-KEY-STD-SZ-ID-NULL-IND                      
                         , :ASC-KEY-STD-SZ-DESC                                 
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :PV-EXTD-SZ-CDE                                      
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            S.SIZE_GP_CDE     = :DK-SIZE-GP-CODE                
                       AND  S.NRF_SIZE_CDE    = :DK-NRF-SIZE-CODE               
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-KDSC-SEL                                            
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-KEY-EFF-END-DATE :PV-END-DATE-NULL-IND          
                         , :ASC-KEY-NRF-SIZE-CODE                               
                         , :ASC-KEY-PRI-DESC                                    
                         , :ASC-KEY-SEC-DESC                                    
                         , :ASC-KEY-KOHLS-DESC                                  
                         , :ASC-KEY-STD-SZ-ID                                   
                               :ASC-KEY-STD-SZ-ID-NULL-IND                      
                         , :ASC-KEY-STD-SZ-DESC                                 
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :PV-EXTD-SZ-CDE                                      
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  KOHLS_DESC    LIKE  :DK-KOHLS-DESC                  
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-NRF-KDSC-SEL                                        
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-KEY-EFF-END-DATE :PV-END-DATE-NULL-IND          
                         , :ASC-KEY-NRF-SIZE-CODE                               
                         , :ASC-KEY-PRI-DESC                                    
                         , :ASC-KEY-SEC-DESC                                    
                         , :ASC-KEY-KOHLS-DESC                                  
                         , :ASC-KEY-STD-SZ-ID                                   
                               :ASC-KEY-STD-SZ-ID-NULL-IND                      
                         , :ASC-KEY-STD-SZ-DESC                                 
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :PV-EXTD-SZ-CDE                                      
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  NRF_SIZE_CDE     =  :DK-NRF-SIZE-CODE               
                       AND  KOHLS_DESC    LIKE  :DK-KOHLS-DESC                  
                  END-EXEC                                                      
                                                                                
               WHEN ASC-SGC-KSC-SEL                                             
                  EXEC SQL                                                      
                     SELECT                                                     
                            S.EFF_END_DTE                                       
                         ,  S.NRF_SIZE_CDE                                      
                         ,  S.PRIM_DESC                                         
                         ,  S.SEC_DESC                                          
                         ,  S.KOHLS_DESC                                        
                         ,  S.KOHLS_STD_SZ_ID                                   
                         ,  I.KOHLS_STD_SZ_DESC                                 
W32250                   ,  S.EXTD_SZ_CDE                                       
                     INTO  :ASC-KEY-EFF-END-DATE :PV-END-DATE-NULL-IND          
                         , :ASC-KEY-NRF-SIZE-CODE                               
                         , :ASC-KEY-PRI-DESC                                    
                         , :ASC-KEY-SEC-DESC                                    
                         , :ASC-KEY-KOHLS-DESC                                  
                         , :ASC-KEY-STD-SZ-ID                                   
                               :ASC-KEY-STD-SZ-ID-NULL-IND                      
                         , :ASC-KEY-STD-SZ-DESC                                 
                               :PV-STD-SZ-DESC-NULL-IND                         
W32250                   , :PV-EXTD-SZ-CDE                                      
W32250                         :PV-EXTD-SZ-CDE-NULL-IND                         
                     FROM                                                       
                            TSIZE S                                             
                          LEFT OUTER JOIN IMT_KOHLS_STD_SZ I                    
                        ON  S.KOHLS_STD_SZ_ID = I.KOHLS_STD_SZ_ID               
                     WHERE                                                      
                            SIZE_GP_CDE      =  :DK-SIZE-GP-CODE                
                       AND  KOHLS_SIZE_CDE   =  :DK-KOHLS-SIZE-CODE             
                  END-EXEC                                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    SET PS-SIZE-FOUND            TO TRUE                        
                    IF PV-END-DATE-NULL-IND < 0                                 
                        SET ASC-SIZE-NOT-EXPIRED TO TRUE                        
                        MOVE SPACES              TO ASC-KEY-EFF-END-DATE        
                    ELSE                                                        
                        SET ASC-SIZE-EXPIRED     TO TRUE                        
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    SET PS-SIZE-NOT-FOUND        TO TRUE                        
               WHEN SQLCODE = -811                                              
                    SET PS-MULT-SIZES-FOUND      TO TRUE                        
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4300-GET-SIZE-INFO'                                   
                                     TO DP013-PARAGRAPH                         
                    MOVE 'FETCH MESSAGE CURSOR'                                 
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RESTORE DATA TO THE SCREEN FROM THE COMM AREA.              *        
      *----------------------------------------------------------------*        
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE ASC-KEY-SIZE-GP-CODE   TO PV-SIZE-GP-CODE-N.                    
           MOVE PV-SIZE-GP-CODE-N      TO ASZGPCDO.                             
           MOVE ASC-KEY-SIZE-GP-DESC   TO ASZGPDSO.                             
           MOVE ASC-KEY-NRF-SIZE-CODE  TO ANRFCDO.                              
           MOVE ASC-KEY-KOHLS-DESC     TO AKDESCO.                              
W32250     MOVE PV-EXTD-SZ-DESC        TO AESZDSO.                              
           MOVE ASC-KEY-PRI-DESC       TO APDESCO.                              
           MOVE ASC-KEY-SEC-DESC       TO ASDESCO.                              
           IF ASC-KEY-STD-SZ-ID-NULL-IND < 0                                    
               MOVE SPACES TO ASTNDSCO                                          
               MOVE SPACES TO ASTNDDEO                                          
           ELSE                                                                 
               MOVE ASC-KEY-STD-SZ-ID   TO ASTNDSCO                             
               MOVE ASC-KEY-STD-SZ-DESC TO ASTNDDEO                             
           END-IF.                                                              
                                                                                
W32250*----------------------------------------------------------------*        
W32250*- GET EXTENDED SIZE DESC                                       -*        
W32250*----------------------------------------------------------------*        
W32250 4500-GET-EXTD-SZ-INFO.                                                   
W32250     EXEC SQL                                                             
W32250        SELECT EXTD_SZ_DESC                                               
W32250        INTO  :PV-EXTD-SZ-DESC                                            
W32250        FROM  IMT_EXTD_SZ                                                 
W32250        WHERE EXTD_SZ_CDE = :PV-EXTD-SZ-CDE                               
W32250     END-EXEC.                                                            
W32250                                                                          
W32250     EVALUATE TRUE                                                        
W32250         WHEN SQLCODE = ZERO                                              
W32250              CONTINUE                                                    
W32250         WHEN SQLCODE = +100                                              
W32250              CONTINUE                                                    
W32250         WHEN OTHER                                                       
W32250              MOVE '4500-GET-EXTD-SZ-INFO'                                
W32250                               TO DP013-PARAGRAPH                         
W32250              MOVE 'FETCH MESSAGE CURSOR'                                 
W32250                               TO DP013-MESSAGE-TEXT (1)                  
W32250              MOVE SQLCA       TO DP013-SQLCA                             
W32250              SET DP013-DB2-ABEND                                         
W32250                               TO TRUE                                    
W32250              PERFORM DP013-0000-PROCESS-ABEND                            
W32250     END-EVALUATE.                                                        
W32250                                                                          
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9100-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (ET353-SIZE-SEL-REC)                                   
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
                                                                                
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9200-REWRITE-SEL-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (ET353-SIZE-SEL-REC)                                 
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESP)                                       
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
