       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.    ICKUT014.                                         00020004
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030004
       DATE-WRITTEN.  06-24-2008.                                       00040004
       DATE-COMPILED.                                                   00050004
                                                                        00060004
      ******************************************************************00070004
      *PURPOSE:   THE PROGRAM IS DESIGNED TO FORMAT THE ICT_SKU_LOC_CNT 00080004
      *           UNLOAD FROM THE USA INVENTORY COUNTS SYSTEM TO EDW    00090004
      *           LOAD STANDARDS.                                       00100004
      *                (SEE THE FLOW SECTION FOR MORE DETAILS)          00110004
      *                                                                 00120004
      *CHANGE HISTORY:                                                  00130004
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00140004
      * ------- ---------- ----------- -------------------------------- 00150004
      *         12-14-2010 J.HELING    CREATION.....                    00160004
      ******************************************************************00170004
                                                                        00180004
       ENVIRONMENT DIVISION.                                            00190004
       CONFIGURATION SECTION.                                           00200004
       SOURCE-COMPUTER. IBM-3090.                                       00210004
       OBJECT-COMPUTER. IBM-3090.                                       00220004
       INPUT-OUTPUT SECTION.                                            00230004
       FILE-CONTROL.                                                    00240004
                                                                        00250004
           SELECT LOC-CNT-DTL-INPUT                                     00260004
               ASSIGN TO INP01.                                         00270004
                                                                        00280004
           SELECT LOC-CNT-DTL-CONTROL                                   00290004
               ASSIGN TO INP02.                                         00300004
                                                                        00310004
           SELECT LOC-CNT-DTL-OUTPUT                                    00320004
               ASSIGN TO OUT01.                                         00330004
                                                                        00340004
       DATA DIVISION.                                                   00350004
                                                                        00360004
       FILE SECTION.                                                    00370004
                                                                        00380004
       FD  LOC-CNT-DTL-INPUT                                            00390004
           RECORDING MODE IS F                                          00400004
           LABEL RECORDS ARE STANDARD.                                  00410004
       01  LOC-CNT-DTL-INPUT-RECORD             PIC X(118).             00420004
                                                                        00430004
       FD  LOC-CNT-DTL-CONTROL                                          00440004
           RECORDING MODE IS F                                          00450004
           LABEL RECORDS ARE STANDARD.                                  00460004
       01  LCDI-CONTROL-RECORD.                                         00470004
           05  LCDI-KEY.                                                00480004
               10  LCDI-CNT-STUP-ID             PIC S9(9) COMP.         00490004
               10  LCDI-LOC-NBR                 PIC S9(4) COMP.         00500004
           05  LCDI-REMAINDER                   PIC X(74).              00510004
                                                                        00520004
       FD  LOC-CNT-DTL-OUTPUT                                           00530004
           RECORDING MODE IS F                                          00540004
           LABEL RECORDS ARE STANDARD.                                  00550004
       01  LOC-CNT-DTL-OUTPUT-RECORD            PIC X(196).             00560004
                                                                        00570004
      ******************************************************************00580004
       WORKING-STORAGE SECTION.                                         00590004
      ******************************************************************00600004
       01  PC-PROGRAM-CONSTANTS.                                        00610004
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'ICKUT014'.00620005
                                                                        00630004
       01  PV-PROGRAM-VARIABLES.                                        00640004
           05  PV-CONTROL-RECS                PIC 9(09)   VALUE ZERO.   00650004
           05  PV-INPUT-RECS                  PIC 9(09)   VALUE ZERO.   00660004
           05  PV-OUTPUT-RECS                 PIC 9(09)   VALUE ZERO.   00670004
           05  PV-PARA-NAME                   PIC X(30)   VALUE ZERO.   00680004
           05  PV-SAVE-KEY.                                             00690004
               10  PV-CNT-STUP-ID             PIC S9(9) COMP VALUE +0.  00700004
               10  PV-LOC-NBR                 PIC S9(4) COMP VALUE +0.  00710004
                                                                        00720004
       01  PS-PROGRAM-SWITCHES.                                         00730004
           05  PS-LOC-CNT-DTL-CONTRL-SW       PIC X(01)   VALUE 'N'.    00740004
               88  PS-LOC-CNT-DTL-CONTRL-EOF              VALUE 'Y'.    00750004
           05  PS-LOC-CNT-DTL-INPUT-SW        PIC X(01)   VALUE 'N'.    00760004
               88  PS-LOC-CNT-DTL-INPUT-EOF               VALUE 'Y'.    00770004
                                                                        00780004
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00790004
                                                          COMP SYNC.    00800004
                                                                        00810004
      ******************************************************************00820006
           COPY ICRD036.                                                00830004
                                                                        00840004
      ******************************************************************00850006
           COPY ICRD072.                                                00860005
                                                                        00870004
      ******************************************************************00880004
      * WORKING-STORAGE FOR CALENDAR ROUTINE                            00890004
      ******************************************************************00900004
           COPY DPWS500.                                                00910004
                                                                        00920004
      ******************************************************************00980004
       PROCEDURE DIVISION.                                              00990004
                                                                        01000004
       0000-MAINLINE.                                                   01010004
                                                                        01020004
           PERFORM 1000-INITIALIZE.                                     01030004
                                                                        01040004
           PERFORM 2000-MAIN-PROCESS                                    01050004
               UNTIL PS-LOC-CNT-DTL-CONTRL-EOF.                         01060004
                                                                        01070004
           PERFORM 9000-END-OF-JOB-ROUTINE.                             01080004
                                                                        01090004
           STOP RUN.                                                    01100004
                                                                        01110004
      ******************************************************************01120004
       1000-INITIALIZE.                                                 01130004
      ******************************************************************01140004
           DISPLAY '   '.                                               01150004
           DISPLAY '****************************************'.          01160004
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                      01170004
                                                                        01180004
           OPEN INPUT  LOC-CNT-DTL-INPUT                                01190004
                INPUT  LOC-CNT-DTL-CONTROL                              01200004
               OUTPUT  LOC-CNT-DTL-OUTPUT.                              01210004
                                                                        01220004
           PERFORM 6000-READ-LOC-CNT-DTL-CONTROL.                       01230004
                                                                        01240004
           IF (NOT PS-LOC-CNT-DTL-CONTRL-EOF)                           01250004
               PERFORM 6100-READ-LOC-CNT-DTL-INPUT                      01260004
           END-IF.                                                      01270004
                                                                        01280004
      ***************************************************************** 01290004
       2000-MAIN-PROCESS.                                               01300004
      ******************************************************************01310004
           MOVE ICRD036-CNT-STUP-ID TO PV-CNT-STUP-ID.                  01320004
           MOVE ICRD036-LOC-NBR     TO PV-LOC-NBR.                      01330004
                                                                        01340004
           IF LCDI-KEY EQUAL PV-SAVE-KEY                                01350004
               PERFORM 3000-FORMAT                                      01360004
               PERFORM 6100-READ-LOC-CNT-DTL-INPUT                      01370004
           ELSE                                                         01380004
           IF LCDI-KEY GREATER PV-SAVE-KEY                              01390004
               PERFORM 6100-READ-LOC-CNT-DTL-INPUT                      01400004
           ELSE                                                         01410004
               PERFORM 6000-READ-LOC-CNT-DTL-CONTROL                    01420004
           END-IF.                                                      01430004
                                                                        01440004
      ***************************************************************** 01450004
       3000-FORMAT.                                                     01460004
      ******************************************************************01470004
                                                                        01480004
           INITIALIZE IC072-OUTPUT-RECORD.                              01490005
                                                                        01500004
           MOVE ICRD036-CNT-STUP-ID        TO IC072-CNT-STUP-ID.        01510005
           MOVE ICRD036-LOC-NBR            TO IC072-LOC-NBR.            01520005
           MOVE ICRD036-SKU-NBR            TO IC072-SKU-NBR.            01530005
           MOVE ICRD036-LAST-UPD-TMST      TO IC072-LAST-UPD-TMST.      01540005
           MOVE ICRD036-CNT-SEQ-NBR        TO IC072-CNT-SEQ-NBR.        01550005
           MOVE ICRD036-LAST-ACTN-CDE      TO IC072-LAST-ACTN-CDE.      01560005
           MOVE ICRD036-LAST-UPD-USR-ID    TO IC072-LAST-UPD-USR-ID.    01570005
           MOVE ICRD036-LOC-EXPTED-UNT-QTY TO IC072-LOC-EXPTED-UNT-QTY. 01580005
           MOVE ICRD036-UNT-RTL-AMT        TO IC072-UNT-RTL-AMT.        01590005
           MOVE ICRD036-EXEC-TMST          TO IC072-EXEC-TMST.          01600005
                                                                        01610004
           IF SCND-UNT-QTY-IS-NULL                                      01620004
               MOVE ZEROS                 TO IC072-SCND-UNT-QTY         01630005
           ELSE                                                         01640004
               MOVE ICRD036-SCND-UNT-QTY  TO IC072-SCND-UNT-QTY         01650005
           END-IF.                                                      01660004
                                                                        01670004
           IF IN-TRNST-UNT-QTY-IS-NULL                                  01680004
               MOVE ZEROS                 TO IC072-IN-TRNST-UNT-QTY     01690005
           ELSE                                                         01700004
               MOVE ICRD036-IN-TRNST-UNT-QTY TO IC072-IN-TRNST-UNT-QTY  01710005
           END-IF.                                                      01720004
                                                                        01730004
           IF ACPTD-TRUK-QTY-IS-NULL                                    01740004
               MOVE ZEROS                  TO IC072-ACPTD-TRUK-QTY      01750005
           ELSE                                                         01760004
               MOVE ICRD036-ACPTD-TRUK-QTY TO IC072-ACPTD-TRUK-QTY      01770005
           END-IF.                                                      01780004
                                                                        01790004
           IF RTL-SAL-UNT-QTY-IS-NULL                                   01800004
               MOVE ZEROS                  TO IC072-RTL-SAL-UNT-QTY     01810005
           ELSE                                                         01820004
               MOVE ICRD036-RTL-SAL-UNT-QTY TO IC072-RTL-SAL-UNT-QTY    01830005
           END-IF.                                                      01840004
                                                                        01850004
           IF RTL-RET-UNT-QTY-IS-NULL                                   01860004
               MOVE ZEROS                  TO IC072-RTL-RET-UNT-QTY     01870005
           ELSE                                                         01880004
               MOVE ICRD036-RTL-RET-UNT-QTY TO IC072-RTL-RET-UNT-QTY    01890005
           END-IF.                                                      01900004
                                                                        01910004
           IF XFER-IN-UNT-QTY-IS-NULL                                   01920004
               MOVE ZEROS                  TO IC072-XFER-IN-UNT-QTY     01930005
           ELSE                                                         01940004
               MOVE ICRD036-XFER-IN-UNT-QTY TO IC072-XFER-IN-UNT-QTY    01950005
           END-IF.                                                      01960004
                                                                        01970004
           IF XFER-OUT-UNT-QTY-IS-NULL                                  01980004
               MOVE ZEROS                  TO IC072-XFER-OUT-UNT-QTY    01990005
           ELSE                                                         02000004
               MOVE ICRD036-XFER-OUT-UNT-QTY TO IC072-XFER-OUT-UNT-QTY  02010005
           END-IF.                                                      02020004
                                                                        02030004
           PERFORM 5000-WRITE-LOC-CNT-DTL-OUTPUT.                       02040004
                                                                        02050004
      ******************************************************************02060004
       5000-WRITE-LOC-CNT-DTL-OUTPUT.                                   02070004
      ******************************************************************02080004
                                                                        02090004
           WRITE LOC-CNT-DTL-OUTPUT-RECORD                              02100004
                 FROM IC072-OUTPUT-RECORD.                              02110005
                                                                        02120004
           ADD +1 TO PV-OUTPUT-RECS.                                    02130004
                                                                        02140004
      ******************************************************************02150004
       6000-READ-LOC-CNT-DTL-CONTROL.                                   02160004
      ******************************************************************02170004
           READ LOC-CNT-DTL-CONTROL                                     02180004
                 AT END SET PS-LOC-CNT-DTL-CONTRL-EOF TO TRUE           02190004
                 NOT AT END ADD +1 TO PV-CONTROL-RECS                   02200004
           END-READ.                                                    02210004
                                                                        02220004
      ******************************************************************02230004
       6100-READ-LOC-CNT-DTL-INPUT.                                     02240004
      ******************************************************************02250004
           READ LOC-CNT-DTL-INPUT                                       02260004
               INTO ICRD036-IC-COUNTS-FILE                              02270004
                 AT END SET PS-LOC-CNT-DTL-INPUT-EOF TO TRUE            02280004
                        SET PS-LOC-CNT-DTL-CONTRL-EOF TO TRUE           02290004
                 NOT AT END ADD +1 TO PV-INPUT-RECS                     02300004
           END-READ.                                                    02310004
                                                                        02320004
      ******************************************************************02330004
       9000-END-OF-JOB-ROUTINE.                                         02340004
      ******************************************************************02350004
                                                                        02360004
           CLOSE LOC-CNT-DTL-INPUT                                      02370004
                 LOC-CNT-DTL-CONTROL                                    02380004
                 LOC-CNT-DTL-OUTPUT.                                    02390004
                                                                        02400004
           DISPLAY '****************************************'.          02410004
           DISPLAY '*** SUMMARY FOR '             PC-PROGRAM-NAME.      02420004
           DISPLAY '*** CONTROL RECORDS READ    = ' PV-CONTROL-RECS.    02430004
           DISPLAY '*** INPUT RECORDS READ      = ' PV-INPUT-RECS.      02440004
           DISPLAY '*** OUTPUT RECORDS WRITTEN  = ' PV-OUTPUT-RECS.     02450004
           DISPLAY '****************************************'.          02460004
                                                                        02470004
      ***************************************************************** 02480004
      ***************************************************************** 02490004
