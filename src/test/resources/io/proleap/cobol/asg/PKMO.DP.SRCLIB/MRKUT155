      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.       MRKUT155.                                              
       AUTHOR.           KUSHAL BOTHRA.                                         
       INSTALLATION.     KOHLS.                                                 
       DATE-WRITTEN      MAY 17 2012.                                           
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *        PROGRAM REQUEST FOR INNER OUTER DATA UPDATION           *        
      ******************************************************************        
      *                                                                *        
      *  THIS PROGRAM WILL RETURN DB2 SKU INFORMATION                  *        
      *  BASED ON THE INPUT RT DAY NUMBER                              *        
      *                                                                *        
      *   INPUT FIELDS:   RT-DAY-NBR,CURRENT DATE                      *        
      *                                                                *        
      *   OUTPUT FIELDS:  SKU                                          *        
      *                   DIST TYPE OF  B&M                            *        
      *                   ADDITIONAL HIERARCHICAL DATA                **        
      *                                                                *        
      *   I/O:   TMRITEM            DB2 TABLE   SELECT                 *        
      *          TSK                                                            
      *          TSKXREF                                               *        
      *          TUPCSKU                                               *        
      *          TUPC                                                  *        
      ******************************************************************        
P5207G* CHANGE LOG                                                     *        
P5207G* MADE BY : PRITHIVI (COGNIZANT) DATE:01/21/2014                 *        
P5207G* CHANGED THE FIND SKU CURSOR - ADDED GROUP BY CLAUSE TO THE     *        
P5207G* CURSOR. CHANGED THE OUTPUT FILE LENGTH TO 4576 TO ACCOMADATE   *        
P5207G* GROUPED SKUS, AS OF NOW WE CAN HAVE A MAXIMUM OF 500 SKU'S     *        
P5207G* GROUPED PER RECORD.  COPYBOOK MRRD168 HAD BEEN CHANGED TO MATCH*        
P5207G* OUTPUT LENGTH.   CHANGE TAG : P5207G                           *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT FLAT-FILE-INP                                                 
               ASSIGN                       TO INP01                            
               FILE STATUS                  IS WS-INFILE-SW.                    
                                                                                
           SELECT FLAT-FILE-OUT                                                 
               ASSIGN                       TO OUT01                            
               FILE STATUS                  IS WS-OUTFILE-SW.                   
           EJECT                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  FLAT-FILE-INP                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS  DATE-FILE.                                           
       01  DATE-FILE                       PIC X(80).                           
                                                                                
       FD  FLAT-FILE-OUT                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
P5207G*    RECORD CONTAINS 84 CHARACTERS                                        
P5207G     RECORD CONTAINS 27076 CHARACTERS                                     
           DATA RECORD IS  INOUT-FILE.                                          
                                                                                
       01  INOUT-FILE.                                                          
            COPY MRRD168.                                                       
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DATE-FILE-REC                   PIC X(80).                           
                                                                                
       01  WS-CONTROL                      PIC 9(3).                            
                                                                                
       01  WS-CURRENT-DAY.                                                      
           05 WS-YEAR                      PIC 9(04).                           
           05 WS-MONTH                     PIC 9(02).                           
           05 WS-DAY                       PIC 9(02).                           
                                                                                
       01  WS-DATE-RECORD                  PIC 9(8).                            
                                                                                
       01  WS-CONSTANTS.                                                        
           05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
           05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100.            
           05  OUTPUT-COUNT                PIC 9(09)     VALUE ZEROS.           
           05  WS-DIV                      PIC 9(10).                           
           05  WS-BEG-DATE                 PIC 9(8).                            
           05  WS-DATE-CONV-CHAR REDEFINES WS-BEG-DATE.                         
               10  WS-DATE                 PIC X(8).                            
           05  WS-QUOTIENT                 PIC 9(10).                           
           05  WS-REM                      PIC 9(01).                           
P5207G     05  WS-CONCAT-SKU               PIC X(27000) VALUE SPACES.           
                                                                                
       01  RT-DAY-NBR                      PIC 9(03).                           
       01  WS-RT-DAY-NBR                   PIC S9(03)V COMP-3.                  
                                                                                
       01  PV-MISC-FIELDS.                                                      
           05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
           05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
           05  PV-SQL-SUB                  PIC  9(03)    VALUE ZERO.            
           05  PC-MAX-RETRIES              PIC S9(04)   COMP SYNC               
                                                        VALUE +3.               
      *                                                                         
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FETCH-CSR-SW         PIC  X(01)    VALUE 'N'.             
               88  END-OF-FETCH-CSR                      VALUE 'Y'.             
                                                                                
       01  WS-INFILE-SW                    PIC X(02)     VALUE SPACES.          
           88  WS-INFILE-SUCESS                          VALUE '00'.            
           88  WS-INFILE-EOF                             VALUE '10'.            
                                                                                
       01  WS-OUTFILE-SW                   PIC X(02)     VALUE SPACES.          
           88  WS-OUTFILE-SUCESS                         VALUE '00'.            
           88  WS-OUTFILE-EOF                            VALUE '10'.            
                                                                                
       01      WS-DATE-CONV               PIC X(8).                             
       01      WS-DATE-CONV-R             PIC X(9).                             
      *---------------------------------------------------------------          
      *  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE  TABLES                    
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE TMRITEM                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE  TSK                                                     
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE TMRITST                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------          
      *  COMMUNICATIONS AREA FOR DB2                                            
      *---------------------------------------------------------------          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------          
      * DB2 COMMUNICATION AREA2                                                 
      *---------------------------------------------------------------          
                                                                                
           COPY SQLCA2.                                                         
                                                                                
           EXEC SQL                                                             
               DECLARE FIND-SKU CURSOR FOR                                      
                SELECT C.DEPT,                                                  
                       C.CLASS,                                                 
                       C.SUBCLASS,                                              
                       B.ENT_ID,                                                
P5207G*                C.SKU,                                                   
                       B.LONG_VENDOR_STYLE,                                     
P5207G                 SUBSTR( XMLSERIALIZE( XMLAGG( XMLTEXT(                   
P5207G                 CONCAT( ',', C.SKU ) ) ) AS CLOB( 30000 ) ), 2 )         
                    FROM TMRITEM A,                                             
                         TSK B,                                                 
                         TSKXREF C,                                             
                         TUPCSKU D,                                             
                         TUPC E                                                 
                    WHERE A.ITEM_NBR = B.ITEM_NMBR                              
                    AND   C.ITM_NBR = B.ITEM_NMBR                               
                    AND   D.ITEM_NBR = B.ITEM_NMBR                              
                    AND   D.UPC_NBR = E.UPC_NBR                                 
                    AND   E.UPC_ID = E.INTR_UPC_ID                              
                    AND   E.UPC_TYP_CDE = 'I'                                   
                    AND   E.WHSE_RPLNT_IND = 'N'                                
                    AND   RT_NBR > 0                                            
                    AND   RT_DAY_NBR = :WS-RT-DAY-NBR                           
                    AND EXISTS(                                                 
                        SELECT 'X'                                              
                         FROM TMRITST I,                                        
                              XMT_LOC L                                         
                        WHERE I.ITEM_NBR = A.ITEM_NBR                           
                          AND I.STR_NBR = L.LOC_NBR                             
                          AND I.FT_CDE <> 'X'                                   
                          AND LOC_TYP_CDE = 'DS'                                
                          AND E_BUS_IND = 'N')                                  
P5207G                    GROUP BY                                              
P5207G                       C.DEPT,                                            
P5207G                       C.CLASS,                                           
P5207G                       C.SUBCLASS,                                        
P5207G                       B.ENT_ID,                                          
P5207G                       B.LONG_VENDOR_STYLE                                
                    FOR FETCH ONLY WITH UR                                      
                END-EXEC.                                                       
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           DISPLAY '******** START OF PROGRAM  ********'.                       
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 1100-PROCESS-DATE.                                           
                                                                                
           PERFORM 1200-OPEN-SKU-CSR.                                           
                                                                                
           PERFORM 2200-FETCH-SKU-CURSOR                                        
                   UNTIL END-OF-FETCH-CSR.                                      
                                                                                
           PERFORM 3000-CLOSE-SKU-CSR.                                          
                                                                                
           PERFORM 4000-DISPLAY-UPDATES.                                        
                                                                                
           CLOSE  FLAT-FILE-INP                                                 
                  FLAT-FILE-OUT.                                                
                                                                                
           DISPLAY '******** END OF PROGRAM  ********'.                         
                                                                                
           GOBACK.                                                              
      *                                                                         
      *                                                                         
      ******************************************************************        
      ** OPEN THE SYSTEM FILE.                                                  
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE    WS-INFILE-SW.                                          
P5207G     INITIALIZE    WS-CONCAT-SKU.                                         
                                                                                
           OPEN INPUT  FLAT-FILE-INP.                                           
           OPEN OUTPUT FLAT-FILE-OUT.                                           
                                                                                
             IF WS-INFILE-SUCESS AND WS-OUTFILE-SUCESS                          
                DISPLAY 'FILE OPEN SUCCESSFUL'                                  
             ELSE                                                               
                DISPLAY '**  ABEND  **'                                         
                DISPLAY 'MRKUT155'                                              
                DISPLAY 'FILE OPEN ERROR'                                       
                MOVE +4008           TO PV-ABEND-CODE                           
                CALL 'ILBOABN0' USING PV-ABEND-CODE                             
             END-IF                                                             
                                                                                
             ACCEPT WS-CONTROL.                                                 
                                                                                
             READ  FLAT-FILE-INP INTO   DATE-FILE-REC.                          
      ******************************************************************        
      ** PROCESSING CURRENT DATE                                                
      ******************************************************************        
       1100-PROCESS-DATE.                                                       
                                                                                
               MOVE DATE-FILE-REC(1:4)  TO WS-YEAR                              
               MOVE DATE-FILE-REC(6:2)  TO WS-MONTH                             
               MOVE DATE-FILE-REC(9:2)  TO WS-DAY                               
               MOVE WS-CURRENT-DAY      TO WS-DATE-RECORD                       
               DISPLAY  'DATE-FILE-REC :' WS-CURRENT-DAY                        
                                                                                
               COMPUTE WS-DIV = FUNCTION INTEGER-OF-DATE(WS-DATE-RECORD)        
               DIVIDE WS-DIV BY 7 GIVING WS-QUOTIENT REMAINDER WS-REM           
                   EVALUATE WS-REM                                              
      * FOR SUNDAY                                                              
                      WHEN 0                                                    
                        DISPLAY ' PROGRAM NOT MEANT TO RUN ON SUNDAY'           
                        GOBACK                                                  
      * FOR MONDAY                                                              
                      WHEN 1                                                    
                         MOVE '002'   TO    WS-RT-DAY-NBR                       
                         SUBTRACT 1  FROM WS-DIV                                
      * FOR TUESDAY                                                             
                      WHEN 2                                                    
                         MOVE '003'   TO    WS-RT-DAY-NBR                       
                         SUBTRACT 2  FROM WS-DIV                                
      * FOR WEDNESDAY                                                           
                      WHEN 3                                                    
                         MOVE '004'   TO    WS-RT-DAY-NBR                       
                         SUBTRACT 3  FROM WS-DIV                                
      * FOR THRUSDAY                                                            
                      WHEN 4                                                    
                         MOVE '005'   TO    WS-RT-DAY-NBR                       
                         SUBTRACT 4  FROM WS-DIV                                
      * FOR FRIDAY                                                              
                      WHEN 5                                                    
                         DISPLAY ' PROGRAM NOT MEANT TO ON FRIDAY'              
                         GOBACK                                                 
      * FOR SATURDAY                                                            
                      WHEN 6                                                    
                         MOVE '001'   TO    WS-RT-DAY-NBR                       
                         SUBTRACT 6  FROM WS-DIV                                
                      WHEN OTHER                                                
                         DISPLAY ' PROGRAM NOT MEANT TO RUN TODAY'              
                         GOBACK                                                 
                 END-EVALUATE                                                   
             COMPUTE WS-BEG-DATE = FUNCTION DATE-OF-INTEGER(WS-DIV)             
             MOVE WS-DATE TO WS-DATE-CONV                                       
                                                                                
           EXEC SQL                                                             
        SELECT TO_CHAR(TO_DATE(:WS-DATE-CONV,'YYYYMMDD'),'DD-MON-YY')           
                     INTO :WS-DATE-CONV-R FROM SYSIBM.SYSDUMMY1                 
           END-EXEC.                                                            
                                                                                
             MOVE  WS-DATE-CONV-R        TO   WS-OTB-EFFECTIVE-DATE.            
                                                                                
            IF WS-CONTROL = ZERO                                                
               CONTINUE                                                         
            ELSE                                                                
               MOVE WS-CONTROL  TO   WS-RT-DAY-NBR                              
            END-IF.                                                             
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH FETCHES THE ITEM CURSOR FOR UPDATE RECORDS      *        
      ******************************************************************        
      *                                                                         
       2200-FETCH-SKU-CURSOR.                                                   
      *                                                                         
           MOVE '2200'                   TO PGM-LOCATION.                       
           MOVE '2200-FETCH-SKU-CURSOR'                                         
                                         TO PV-ABEND-PARAGRAPH.                 
                                                                                
            PERFORM                                                             
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                        OR (SQLCODE = 0 OR 100))                                
                 EXEC SQL                                                       
                    FETCH FIND-SKU                                              
                     INTO                                                       
                        :SKXREF-DEPT                                            
                       ,:SKXREF-CLASS                                           
                       ,:SKXREF-SUBCLASS                                        
                       ,:TSK-ENT-ID                                             
P5207G                 ,:TSK-LONG-VENDOR-STYLE                                  
P5207G*                ,:SKXREF-SKU                                             
P5207G                 ,:WS-CONCAT-SKU                                          
                  END-EXEC                                                      
                                                                                
                EVALUATE TRUE                                                   
                  WHEN SQLCODE         = ZERO                                   
                    ADD 1    TO OUTPUT-COUNT                                    
                    PERFORM 2220-WRITE-FLAT-FILE                                
                  WHEN SQLCODE         = PC-ROW-NOT-FOUND                       
                    MOVE 'Y'  TO END-OF-FETCH-CSR-SW                            
                  WHEN SQLCODE = -904                                           
                  WHEN SQLCODE = -911                                           
                  WHEN SQLCODE = -913                                           
                    CONTINUE                                                    
                  WHEN OTHER                                                    
                    DISPLAY 'ABENDING IN THE FETCH PARAGRAPH:'                  
                    DISPLAY PV-ABEND-PARAGRAPH                                  
                    PERFORM 9999-SQL-ERROR                                      
                 END-EVALUATE                                                   
            END-PERFORM.                                                        
                                                                                
            IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                               
               MOVE '2200-FETCH-SKU-CURSOR'                                     
                                      TO  PV-ABEND-PARAGRAPH                    
               DISPLAY  'FETCH CURSOR RETRIES EXCEEDED'                         
                                                                                
               PERFORM 9999-SQL-ERROR                                           
            END-IF.                                                             
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH MAPS THE DATA TO THE SEQUENTIAL OUTPUT FILE              
      ******************************************************************        
      *                                                                         
       2220-WRITE-FLAT-FILE.                                                    
                                                                                
              MOVE SKXREF-DEPT           TO WS-DEPT                             
              MOVE SKXREF-CLASS          TO WS-CLASS                            
              MOVE SKXREF-SUBCLASS       TO WS-SUBCLASS                         
              MOVE TSK-ENT-ID            TO WS-ENT-ID                           
P5207G*       MOVE SKXREF-SKU            TO WS-SKU                              
P5207G        MOVE WS-CONCAT-SKU         TO WS-SKU                              
              MOVE TSK-LONG-VENDOR-STYLE TO WS-LONG-VENDOR-STYLE                
              MOVE 'B'                   TO WS-DISTRB-TYP-CDE                   
              MOVE 'US'                  TO WS-COUNTRY-ID.                      
              MOVE 'F'                   TO WS-ORD-LCYC-CODE.                   
              MOVE  WS-DATE-CONV-R       TO WS-OTB-EFFECTIVE-DATE.              
                                                                                
               WRITE INOUT-FILE.                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH MAPS THE DATA TO THE SEQUENTIAL OUTPUT FILE              
      ******************************************************************        
      *                                                                         
       4000-DISPLAY-UPDATES.                                                    
                                                                                
            DISPLAY 'RECORDS WRITTEN TO OUTPUT : '  OUTPUT-COUNT.               
      ******************************************************************        
      * THIS PARAGRAPH OPENS THE SKU CURSOR                                     
      ******************************************************************        
      *                                                                         
      *                                                                         
       1200-OPEN-SKU-CSR.                                                       
      *                                                                         
                                                                                
           MOVE '1200'                   TO PGM-LOCATION.                       
           MOVE '1200-OPEN-SKU-CSR'      TO PV-ABEND-PARAGRAPH.                 
                                                                                
             PERFORM                                                            
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                  OPEN FIND-SKU                                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                  WHEN SQLCODE         = ZERO                                   
                  WHEN SQLCODE         = PC-ROW-NOT-FOUND                       
                  WHEN SQLCODE         = -911                                   
                  WHEN SQLCODE         = -913                                   
                  WHEN SQLCODE         = -904                                   
                     CONTINUE                                                   
                  WHEN OTHER                                                    
                     DISPLAY PV-ABEND-PARAGRAPH                                 
                     PERFORM 9999-SQL-ERROR                                     
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM 9999-SQL-ERROR                                           
           END-IF.                                                              
      ******************************************************************        
      * THIS PARAGRAPH CLOSES THE UPC CURSOR                           *        
      ******************************************************************        
      *                                                                         
       3000-CLOSE-SKU-CSR.                                                      
      *                                                                         
                                                                                
           MOVE '3000'                   TO PGM-LOCATION.                       
           MOVE '3000-CLOSE-UPC-CSR'                                            
                                         TO PV-ABEND-PARAGRAPH.                 
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB                                                
              FROM 1 BY 1                                                       
              UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                                
                       OR SQLCODE = 0)                                          
                                                                                
              EXEC SQL                                                          
                  CLOSE FIND-SKU                                                
              END-EXEC                                                          
                                                                                
              EVALUATE TRUE                                                     
                WHEN SQLCODE         = ZERO                                     
                WHEN SQLCODE         = -904                                     
                WHEN SQLCODE         = -911                                     
                WHEN SQLCODE         = -913                                     
                WHEN SQLCODE         = PC-ROW-NOT-FOUND                         
                  CONTINUE                                                      
                WHEN OTHER                                                      
                  DISPLAY PV-ABEND-PARAGRAPH                                    
                  PERFORM 9999-SQL-ERROR                                        
              END-EVALUATE                                                      
           END-PERFORM.                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
                  DISPLAY PV-ABEND-PARAGRAPH                                    
                  PERFORM 9999-SQL-ERROR                                        
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *   ERROR MESSAGE ROUTINE                                                 
      ******************************************************************        
      *                                                                         
       9999-SQL-ERROR.                                                          
                                                                                
           MOVE SQLCODE    TO SQLCODE2.                                         
           MOVE SQLERRD(3) TO SQLERRD3.                                         
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = MRKUT155'.                           
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
      ******************************************************************        
