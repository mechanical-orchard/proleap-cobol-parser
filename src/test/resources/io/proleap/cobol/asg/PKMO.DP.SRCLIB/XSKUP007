       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP007.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/28/2002.                                       00050003
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070008
      **       VSM ITEM MESSAGE - NUMBER 030 - STYLE DELETE             00080008
      ******************************************************************00090008
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO DELETE A STYLE ID ON   00100008
      ***     TABLE XST_VND_STYL. THIS WILL ALSO AUTOMATICALLY DELETE   00110014
      ***     ANY SKU AND UPC ROWS BECAUSE CASCADING DELETES HAVE BEEN  00120014
      ***     TURNED ON BECAUSE VSM IS SENDING THE MESSAGES OUT OF ORDER00130014
      ***     DUE TO THEIR PROCESS USING CASCADING DELETES.             00140014
      ******************************************************************00150008
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00160008
      ******************************************************************00170008
       ENVIRONMENT DIVISION.                                            00180000
       CONFIGURATION SECTION.                                           00190000
       SOURCE-COMPUTER.  IBM-3090.                                      00200000
       OBJECT-COMPUTER.  IBM-3090.                                      00210000
       DATA DIVISION.                                                   00220000
                                                                        00230003
       WORKING-STORAGE SECTION.                                         00240000
      ******************************************************************00250000
      **PROGRAM SWITCHES                                                00260000
      ******************************************************************00270000
       01  PS-PROGRAM-SWITCHES.                                         00280000
           05  PS-PROCESS-CONTINUE-SW           PIC X(01) VALUE 'Y'.    00290010
               88  PS-PROCESS-CONTINUE                    VALUE 'Y'.    00300008
               88  PS-PROCESS-COMPLETE                    VALUE 'N'.    00310008
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00320012
               88  PS-ACTION-REVERSE                  VALUE 01.         00330012
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00340012
               88  PS-ACTION-NOTHING                  VALUE 02.         00350012
      ******************************************************************00360000
      **PROGRAM VARIABLES                                               00370000
      ******************************************************************00380000
       01  PC-PROGRAM-VARIABLES.                                        00390009
           05  PC-MAX-RETRIES                   PIC 9(01) VALUE 5.      00400010
           05  PC-RETRIES                       PIC 9(01) VALUE ZEROS.  00410009
      ******************************************************************00420000
      **PROGRAM VARIABLES                                               00430009
      ******************************************************************00440000
       01  PV-PROGRAM-VARIABLES.                                        00450009
           05  PV-PARAGRAPH-NAME                PIC X(30) VALUE SPACE.  00460009
      ******************************************************************00470000
      **COPYBOOKS                                                       00480000
      ******************************************************************00490000
      ***ABEND COPYBOOK                                                 00500000
           COPY DPWS004.                                                00510015
      ***DB2 MESSAGE AREA                                               00520000
           COPY SQLCA2.                                                 00530015
      ***ITEM COPYBOOK                                                  00540007
           COPY IMRD008.                                                00550015
      ******************************************************************00560000
      **TABLE INCLUSIONS                                                00570000
      ******************************************************************00580000
      ***DB2 COMMUNICATIONS AREA                                        00590000
           EXEC SQL                                                     00600000
                INCLUDE SQLCA                                           00610000
           END-EXEC.                                                    00620000
      ***XST_VND_STYL TABLE                                             00630000
           EXEC SQL                                                     00640000
               INCLUDE XST00013                                         00650000
           END-EXEC.                                                    00660000
                                                                        00670000
       LINKAGE SECTION.                                                 00680000
      ******************************************************************00690000
      **LINKAGE VARIABLES                                               00700000
      ******************************************************************00710000
       01  LV-IMRD008-RECORD               PIC X(450).                  00720006
       01  LV-RETURN-CODE                  PIC S9(04).                  00730004
       01  LV-ACTION-IND                   PIC 9(02).                   00740012
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00750012
                                                                        00760006
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00770006
                                LV-RETURN-CODE,                         00780012
                                LV-ACTION-IND,                          00790012
                                LV-CURRENT-TIMESTAMP.                   00800012
      ******************************************************************00810000
       0000-MAINLINE.                                                   00820000
                                                                        00830000
            PERFORM 1000-INITIALIZATION.                                00840000
            PERFORM 2000-PROCESS-MESSAGE                                00850008
                    UNTIL PS-PROCESS-COMPLETE.                          00860004
            PERFORM 9999-WRAPUP.                                        00870000
                                                                        00880000
       EJECT.                                                           00890003
      ***************************************************************** 00900000
      **INITIALIZATION AREA                                             00910000
      ***************************************************************** 00920000
       1000-INITIALIZATION.                                             00930000
                                                                        00940000
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        00950009
                                                                        00960000
           MOVE LV-IMRD008-RECORD          TO IMRD008-RECORD.           00970011
           SET PS-PROCESS-CONTINUE         TO TRUE.                     00980004
           INITIALIZE PC-RETRIES.                                       00990009
                                                                        01000003
           MOVE IM008-ITEM-KEY-RMS-STYL-ID TO XST00013-STYL-ID.         01010011
                                                                        01020000
       EJECT.                                                           01030003
      ***************************************************************** 01040000
      **DELETES ROW ON XST_VND_STYL TABLE AND ANY ROWS ON XST_SKU AND   01050014
      **        XST_UPC.                                                01060014
      ***************************************************************** 01070000
       2000-PROCESS-MESSAGE.                                            01080008
                                                                        01090000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01100009
                                                                        01110000
           EXEC SQL                                                     01120004
                DELETE FROM XST_VND_STYL                                01130004
                      WHERE STYL_ID = :XST00013-STYL-ID                 01140004
           END-EXEC                                                     01150004
                                                                        01160004
           EVALUATE TRUE                                                01170004
      ***SUCCESSFUL PROCESSING                                          01180014
              WHEN SQLCODE = ZERO                                       01190004
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01200004
      ***ROW/TABLE LOCKING                                              01210014
              WHEN SQLCODE = -911                                       01220004
              WHEN SQLCODE = -913                                       01230014
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     01240009
                       SET PS-PROCESS-COMPLETE TO TRUE                  01250004
                   ELSE                                                 01260004
                       ADD +1                  TO PC-RETRIES            01270009
                   END-IF                                               01280004
              WHEN OTHER                                                01290004
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01300004
           END-EVALUATE.                                                01310004
                                                                        01320000
       EJECT.                                                           01330003
      ******************************************************************01340000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01350000
      ******************************************************************01360000
       9999-WRAPUP.                                                     01370000
                                                                        01380000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME.             01390012
                                                                        01400000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01410012
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01420012
                                                                        01430012
           EXIT PROGRAM.                                                01440000
                                                                        01450000
       EJECT.                                                           01460003
