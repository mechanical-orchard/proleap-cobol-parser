      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    INKUP102.                                                 
       AUTHOR.        TROY BIRK.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-93.                                                    
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
W26600* PROGRAM OBJECTIVE:                                             *        
W26600* ------------------                                             *        
W26600* SEQUENTAILLY READ A FILE OF VALID INVENTORY RECORDS SUMMED BY  *        
W26600* LOCATION ID, AREA NUMBER, SKU, UPC AND UNIT PRICE TO LOAD      *        
W26600* THE TSUMINV DB2 TABLE.                                         *        
W26600*                                                                *        
W26600*                                                                *        
W26600*                                                                *        
W26600*                                                                *        
W26600*                                                                *        
      *   THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.   *        
      ******************************************************************        
      *                      CHANGE LOG                                *        
      ******************************************************************        
      * 03/94 M BLAZICH                                                *        
      *       CHANGED TO INCLUDE LENGTHS TO INCLUDE SCAN-LINE-NBR      *        
      ******************************************************************        
      * 03/17/98 JULIE SEIDLER                                         *        
      *       IN THE EDITTED RECORDS FILE PROCESSED IN THIS PROGRAM,   *        
      *       THE IN009-QUANTITY FILE WILL BE IN 9(9) COMP FORMAT      *        
      *       INSTEAD OF 9(4) FORMAT.  THE IN009-QUANTITY FIELD NEEDED          
      *       TO BE EXPANDED SO THE SORTSUM STEP PREVIOUS TO THIS               
      *       PROGRAM WILL NOT OVERFLOW ON THE QUANTITY FIELD. BY OVER-         
      *       FLOWING, DUPLICATE RECORDS WERE IN THE INPUT FILE CAUSING         
      *       THIS PROGRAM TO ABEND.                                            
      ******************************************************************        
      * 03/98 J SEIDLER                                                *        
      *   ADDED LOGIC TO ACCEPTED A SECOND INVENTORY DATA FILE FOR              
      *   SELECTED STORES.  THIS INCLUDED ADDING A NEW STORE CONTROL            
      *   FILE, AND BYPASSING THE UPDATING LOGIC TO THE INV-REC-DATE,           
      ******************************************************************        
      * 05/98 J SEIDLER                                                *        
      *   TO IMPROVE THE RUN TIME PERFORMANCE OF THIS PROGRAM,THE               
      *   TSUMINV TABLE WILL BE LOADED SEQUENTIALLY INSTEAD                     
      *   OF RANDOMLY. TO DO THIS A DKEY TABLE (TINVKEY) WAS ADDED TO           
      *   KEEP TRACK OF THE LAST DKEY ASSIGNED IN THE TSUMINV TABLE.            
      ******************************************************************   CL**4
      * 02/99   MARIA KLAMIK                                               CL**6
      * EXPAND COPYBOOK INWS009 TO ACCOMMODATE NEW SCAN DATE AND           CL**3
      * TIMESTAMP FIELDS.                                                       
      ******************************************************************        
      ******************************************************************   CL**4
      * 10/00   RONNA BUTZ                                                 CL**6
      * REPLACED DB2 TABLE TINVKEY WITH TINCNTL                            CL**3
      ******************************************************************        
23511 ******************************************************************   CL**4
23511 * 04/2002 RONNA BUTZ                                                 CL**6
23511 * ADD A LOCATION RECORD FOR AREA 888888 - MASS DELETE PROJECT        CL**3
23511 * THIS AREA IS SPECIAL AND ONLY USED BY LOSS PREVENTION              CL**3
23511 * FOR MASS DELETE ADJUSTMENTS WHEN THE ORIGINAL AREA IS UNKNOWN      CL**3
23511 ******************************************************************        
W21732* 11/02  IGSI                                                             
W21732* DEPT/CLASS/SEQ ARE REMOVED AND 8 DIGIT SKU IS ADDED                     
W21732******************************************************************        
25248 * 25248    10-06-2003  UPC CAPABILITIES                                   
25248 *                      -ROD JANSSEN                                       
W26600* 26600    02-26-2004  LOC_NBR ADDED TO DB2 TABLE TSUMINV. ADD            
W26600*                      CODE TO INSERT LOC_NBR AND STORE_NBR               
W26600*                      -S. JACKSON                                        
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
728876*PM00728876 01-18-2005 INK300 ABEND IN STEP 300 DUE TO MISSING            
728876*                      MISSING FILE. <REMOVE DISPLAY>                     
728876*                      -ROD JANSSEN                                       
W28543*PM00728876 06-21-2005 ALLOW UP TO 100 LOCATIONS TO BE PROCESSED          
W28543*                      AS AFTER-INVENTORY STORES (IE. SHEET INV)          
W28543*                      -SCOTT JACKSON                                     
895217* PM00895217 06-28-2006 ROD JANSSEN SQL -100 WITH ONLY ONE STORE          
895217*                                   BEING PROCESSED THRU INK307.          
P28545*W28545  03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
RWJRWJ* PM00929121 09-22-2006 IS-IN-TEAM  S04C ABEND - DUPLICATE KEY            
RONNAA* PM00972541 01-19-2007 IS-IN-TEAM  S04C ABEND - DUPLICATE KEY            
W33304******************************************************************        
W33304* W33304 06-16-08   PATTY JAEGER  - CODEWORKS                        CL**6
W33304* HILO PROJECT CHANGES                                               CL**3
W33304******************************************************************        
SR3403* SR3403   04-02-2012  EXPAND LRECL OF INPUT FILE TO 100 AND              
SR3403*                      EXPAND PRICE FOR LARGE RETAIL PROJECT.             
SR3403*                      CHANDAN RACHH                                      
SR3403******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
W26600                                                                          
W26600     SELECT SUMMARIZED-INVENTORY-FILE                                     
W26600         ASSIGN TO INP01.                                                 
W26600                                                                          
           SELECT IN-AFT-INV-STORES-CC                                          
               ASSIGN TO INP02.                                                 
                                                                                
P28545     SELECT CC-JOBNAME-CNTL-FILE                                          
P28545         ASSIGN TO INP03.                                                 
P28545                                                                          
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
                                                                                
W26600 FD  SUMMARIZED-INVENTORY-FILE                                            
W26600     LABEL RECORDS ARE STANDARD                                           
W26600     RECORDING MODE IS F                                                  
W26600     BLOCK CONTAINS 0 RECORDS.                                            
W26600                                                                          
SR3403 01  SUMMARIZED-INVENTORY-RCD    PIC X(100).                              
W26600                                                                          
       FD  IN-AFT-INV-STORES-CC                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IN-AFT-INV-STORES-REC       PIC  X(80).                              
                                                                                
P28545 FD  CC-JOBNAME-CNTL-FILE                                                 
P28545     RECORDING MODE IS F                                                  
P28545     LABEL RECORDS ARE STANDARD                                           
P28545     BLOCK CONTAINS 0 RECORDS.                                            
P28545 01  CC-JOBNAME-CNTL-REC         PIC X(80).                               
P28545                                                                          
W26600******************************************************************        
       WORKING-STORAGE SECTION.                                                 
W26600******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-AFT-INV-STORE-SW     PIC  X(01)          VALUE 'N'.           
               88  PS-AFT-INV-STORE-FOUND                  VALUE 'Y'.           
               88  PS-AFT-INV-STORE-NOT-FOUND              VALUE 'N'.           
           05  PS-END-OF-AFT-INV-STORE-IND                                      
                                       PIC  X(01)          VALUE 'N'.           
               88  PS-END-OF-AFT-INV-STORE-CC              VALUE 'Y'.           
W26600     05  PS-SUM-INV-EOF-SW       PIC  X(01)          VALUE 'N'.           
W26600         88  PS-SUM-INV-EOF                          VALUE 'Y'.           
W26600     05  PS-EDIT-ERROR-SW        PIC  X(01)          VALUE 'N'.           
W26600         88  PS-EDIT-ERROR                           VALUE 'Y'.           
W26600     05  PS-RT-RUN-TYPE          PIC  X(01)          VALUE SPACE.         
W26600         88  PS-RT-READ                              VALUE 'R'.           
W26600         88  PS-RT-UPDATE                            VALUE 'U'.           
W26600     05  PS-PGM-RESTART-SW       PIC  X(01)          VALUE SPACE.         
W26600         88  PS-PGM-RESTART                          VALUE 'R'.           
P28545     05  PS-HEADER-FOUND-SW      PIC  X(01)          VALUE SPACE.         
P28545         88  PS-HEADER-FOUND                         VALUE 'Y'.           
W26600     05  PS-TCKRSTCNTL-RCD-FND-SW                                         
W26600                                 PIC  X(01)          VALUE 'N'.           
W26600         88  PS-TCKRSTCNTL-RCD-FND                   VALUE 'Y'.           
W26600     05  PS-TCKRSTINF-RCD-FND-SW PIC  X(01)          VALUE 'N'.           
W26600         88  PS-TCKRSTINF-RCD-FND                    VALUE 'Y'.           
W26600     05  PS-TINCNTL-RCD-FND-SW   PIC  X(01)          VALUE 'N'.           
W26600         88  PS-TINCNTL-RCD-FND                      VALUE 'Y'.           
895217     05  PS-FIRST-TIME-SW        PIC  X(01)          VALUE 'N'.           
895217         88  PS-FIRST-TIME                           VALUE 'N'.           
895217         88  PS-CONTINUED-PROCESSING                 VALUE 'Y'.           
      *                                                                         
      ******************************************************************        
      * SUMMARIZED INVENTORY RECORD LAYOUT                                      
      ******************************************************************        
25248      COPY INWS009.                                                        
                                                                                
RWJRWJ******************************************************************        
RWJRWJ     COPY INWS009 REPLACING IN009 BY ISAVE.                               
RWJRWJ                                                                          
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)          VALUE +0             
                                                           COMP SYNC.           
                                                                                
W26600******************************************************************        
       01  DK-DATABASE-KEYS.                                                    
W26600     05  DK-STORE                PIC  X(04)          VALUE ZEROS.         
                                                                                
W26600******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(04)          VALUE +3             
                                                           COMP SYNC.           
W26600     05  PC-PROGRAM-ID           PIC  X(08)          VALUE                
W26600         'INKUP102'.                                                      
           05  PC-CKPT-JOB-NAME        PIC  X(08)          VALUE                
               'INK300  '.                                                      
           05  PC-CKPT-PLAN-NAME       PIC  X(08)          VALUE                
               'INKUP102'.                                                      
           05  PC-EMPTY-DATE           PIC  X(10)          VALUE                
               '9999-09-09'.                                                    
                                                                                
      *** FOR SYSTEM TEST:                                                      
           05  PC-TEST-DATE            PIC  X(10)          VALUE                
               '1999-02-15'.                                                    
W28543     05  PC-MAX-AFT-INV-STORES   PIC  9(03)         VALUE 100.            
                                                                                
W26600******************************************************************        
       01  CC-AFT-INV-STORES.                                                   
25248      05  CC-STORE-NMBR.                                                   
25248 *        10  CC-STORE-P1         PIC  9(01)          VALUE ZEROS.         
W26600         10  CC-STORE            PIC  9(04)          VALUE ZEROS.         
25248      05  FILLER                  PIC  X(76)          VALUE SPACE.         
      *                                                                         
W26600******************************************************************        
W26600 01  PT-PROGRAM-TOTALS.                                                   
W26600     05  PT-NBR-SUM-INV-READ     PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
      *                                                                         
W26600******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY-FIELD11      PIC ZZZZZZZZZZZ-.                        
           05  PV-DISPLAY-FIELD17      PIC ZZZZZZZZZZZZZZZZZ-.                  
           05  PV-TODAYS-TIME.                                                  
               10  PV-HH               PIC  9(02)          VALUE ZEROS.         
               10  PV-MIN              PIC  9(02)          VALUE ZEROS.         
               10  PV-SEC              PIC  9(02)          VALUE ZEROS.         
               10  PV-HUND-SEC         PIC  9(02)          VALUE ZEROS.         
           05  PV-EXTENDED-RETAIL      PIC  9(09)V99       VALUE ZEROS.         
           05  PV-SQL-SUB              PIC  9(03)          VALUE ZEROS.         
           05  PV-RETRY-COUNT          PIC  9(03)          VALUE ZEROS.         
           05  PV-PARAGRAPH-NAME       PIC  X(30)          VALUE SPACE.         
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC  X(30)          VALUE SPACE.         
           05  PV-STORE-SUB            PIC  9(03)          VALUE ZEROS.         
           05  PV-SUMINV-DKEY          PIC S9(09)          VALUE ZEROS          
                                                           COMP.                
P28545     05  PV-LOC-REC-COUNT        PIC S9(17) COMP-3 VALUE ZEROS.           
P28545                                                                     CL**4
P28545 01  PV-PROCESS-JOB-NAME-CC.                                              
P28545     05  PV-PROCESS-JOB-NAME.                                             
P28545         10  PV-PROCESS-JOB      PIC X(07)   VALUE SPACES.                
P28545         10  PV-PROCESS-JOB-EXTD PIC X(01)   VALUE SPACE.                 
P28545     05  FILLER                  PIC X(72)   VALUE SPACES.                
W26600                                                                          
W26600******************************************************************        
       01  PV-AFT-INV-TABLE.                                                    
W28543     05  PV-AFT-INV-STORE-TAB    OCCURS 100 TIMES.                        
W26600         10  PV-AFT-INV-STORE    PIC  X(04).                              
                                                                                
W26600******************************************************************        
W26600 01  SA-SAVE-AREA.                                                        
W26600     05  SA-INV-KEY.                                                      
W26600         10  SA-NBR-SUM-INV-READ PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
W26600         10  SA-LOC-NBR          PIC  9(04)          VALUE ZEROS.         
W26600         10  SA-AREA-NBR         PIC  9(06)          VALUE ZEROS.         
W26600         10  SA-LINE-NBR         PIC  9(06)          VALUE ZEROS.         
W26600         10  SA-SKU-NBR          PIC  9(08)          VALUE ZEROS.         
W26600         10  SA-UPC-NBR          PIC  9(15)          VALUE ZEROS.         
SR3403         10  SA-INV-UNIT-PR-AMT  PIC  9(05)V99       VALUE ZEROS.         
W26600     05  SA-SUMINV-DKEY-NBR      PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600     05  SA-START-DKEY-NBR       PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600     05  SA-END-DKEY-NBR         PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600     05  SA-FILE-REC-COUNT       PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
                                                                                
W26600******************************************************************        
W26600 01  CR-CHECKPOINT-RESTART-AREA.                                          
W26600     05  CR-AREA-LEN             PIC S9(04)          VALUE +100           
W26600                                                     COMP.                
W26600     05  CR-CHECKPOINT-RESTART-VAR.                                       
W26600*        10  CR-PREV-DTL-KEY     PIC  X(11)          VALUE SPACE.         
W26600         10  CR-PREV-DTL-KEY.                                             
W26600             15  CR-NBR-SUM-INV-READ                                      
W26600                                 PIC S9(09)          VALUE +0             
W26600                                                     COMP-3.              
W26600             15  CR-LOC-NBR      PIC  9(04)          VALUE ZEROS.         
W26600             15  CR-AREA-NBR     PIC  9(06)          VALUE ZEROS.         
W26600             15  CR-LINE-NBR     PIC  9(06)          VALUE ZEROS.         
W26600             15  CR-SKU-NBR      PIC  9(08)          VALUE ZEROS.         
W26600             15  CR-UPC-NBR      PIC  9(15)          VALUE ZEROS.         
SR3403             15  CR-UNIT-PR-AMT  PIC  9(05)V99       VALUE ZEROS.         
W26600         10  CR-SUMINV-DKEY-NBR  PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600         10  CR-START-DKEY-NBR   PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600         10  CR-END-DKEY-NBR     PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
W26600         10  CR-FILE-REC-COUNT   PIC S9(09)          VALUE +0             
W26600                                                     COMP.                
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF THE SUMMARIZED                  
      *  INVENTORY DB2 TABLE.                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSUMINV                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF INVENTORY PARAMETERS            
      *  DB2 TABLE.                                                             
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF THE INVENTORY                   
      *  PHYSICAL LOCATION TABLE.                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVLOC                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF THE DKEY TABLE                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
W26600                                                                          
W26600*****************************************************************         
W26600*  CHECKPOINT RESTART CONTROL AND INFORMATION TABLES                      
W26600*****************************************************************         
W26600     EXEC SQL                                                             
W26600         INCLUDE TCKRSTCN                                                 
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         INCLUDE TCKRSTIN                                                 
W26600     END-EXEC.                                                            
                                                                                
      *                                                                         
      *  COMMUNICATIONS AREA FOR DB2                                            
      *                                                                         
           COPY SQLCA2.                                                         
                                                                                
      *                                                                         
      *  DB2 FORMATTED ERROR MESSAGE                                            
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
RONNAA******************************************************************        
RONNAA*  CURSOR TO BE ABLE TO SELECT FOR UPDATE                        *        
RONNAA******************************************************************        
RONNAA                                                                          
RONNAA     EXEC SQL                                                             
RONNAA          DECLARE TINCNTL_CSR CURSOR FOR                                  
RONNAA            SELECT LAST_DKEY_NBR                                          
RONNAA            FROM   TINCNTL A                                              
RONNAA            WHERE  EXISTS                                                 
RONNAA                (SELECT B.LOC_NBR                                         
RONNAA                   FROM TINVPAR B                                         
RONNAA                  WHERE A.ACTV_IND = 'Y'                                  
RONNAA                    AND A.INV_ID = B.INV_ID                               
RONNAA                    AND LOC_NBR = :INVPAR-LOC-NBR                         
W33304                    AND LOC_INV_STAT_CDE = 'IN'                           
RONNAA                    AND ACTL_FIN_BK_DTE = '9999-09-09')                   
RONNAA            FOR UPDATE OF LAST_DKEY_NBR                                   
RONNAA     END-EXEC.                                                            
RONNAA                                                                          
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
      *                                                                         
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
W26600 0000-PROGRAM-MAINLINE.                                                   
      *                                                                         
           PERFORM 8000-OPEN-FILES.                                             
                                                                                
           OPEN INPUT IN-AFT-INV-STORES-CC.                                     
W26600     PERFORM 8100-READ-AFT-INV-STORE-CC                                   
               VARYING PV-STORE-SUB FROM 1 BY 1                                 
               UNTIL PS-END-OF-AFT-INV-STORE-CC.                                
           CLOSE IN-AFT-INV-STORES-CC.                                          
W26600                                                                          
W26600     PERFORM 0100-INITIALIZE.                                             
W26600     PERFORM 1100-PROCESS-SUMM-INV-FILE                                   
W26600         UNTIL PS-SUM-INV-EOF                                             
W26600         OR PS-EDIT-ERROR.                                                
W26600                                                                          
W26600     IF SA-LOC-NBR > 0                                                    
P28545         DISPLAY 'PROCESSING LOC: ', SA-LOC-NBR,                          
P28545                 ' COUNT: ', PV-LOC-REC-COUNT                             
P28545         MOVE ZEROS        TO PV-LOC-REC-COUNT                            
W26600         PERFORM 2500-MASS-ADD-DELETE-AREA-ADD                            
W26600         PERFORM 2600-FMT-UPDT-RSRT-INFO                                  
W26600     END-IF.                                                              
W26600                                                                          
W26600     PERFORM 7900-COMMIT-WORK.                                            
W26600                                                                          
W26600     PERFORM 8900-CLOSE-FILES.                                            
W26600                                                                          
W26600     GOBACK.                                                              
W26600                                                                          
W26600******************************************************************        
W26600*                                                                         
W26600******************************************************************        
W26600 0100-INITIALIZE.                                                         
W26600                                                                          
W26600     PERFORM 7700-INIT-CHECKPOINT-SELECT.                                 
           IF PS-TCKRSTCNTL-RCD-FND                                             
W26600         IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
W26600           PERFORM 7800-SELECT-RESTART-INFO                               
                 IF PS-TCKRSTINF-RCD-FND                                        
W26600             MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                           
W26600                                 TO CR-CHECKPOINT-RESTART-VAR             
                   MOVE CR-SUMINV-DKEY-NBR                                      
                                       TO SA-SUMINV-DKEY-NBR                    
                   MOVE CR-START-DKEY-NBR                                       
                                       TO SA-START-DKEY-NBR                     
                   MOVE CR-END-DKEY-NBR                                         
                                       TO SA-END-DKEY-NBR                       
                   MOVE CR-FILE-REC-COUNT                                       
                                       TO SA-FILE-REC-COUNT                     
W26600             PERFORM 8200-READ-SUM-INV-FILE                               
W26600               UNTIL PT-NBR-SUM-INV-READ >                                
W26600                 TCKRSTCNTL-TRANS-QTY                                     
W26600               OR PS-SUM-INV-EOF                                          
                 END-IF                                                         
W26600         ELSE                                                             
W26600           PERFORM 0500-INITIALIZE-TCKRSTCNTL-RCD                         
W26600           PERFORM 8200-READ-SUM-INV-FILE                                 
W26600           IF NOT PS-SUM-INV-EOF                                          
W26600             IF IN009-FILE-HDR                                            
W26600               CONTINUE                                                   
W26600             ELSE                                                         
                     MOVE +4008          TO ABEND-CODE                          
                     DISPLAY '**  ABEND     **'                                 
                     DISPLAY '**  PROGRAM   **  =  INKUP102'                    
                     DISPLAY '**  '                                             
                     DISPLAY '**  '                                             
                     DISPLAY                                                    
                      'FILE MISSING RECORD COUNT HEADER'                        
                     DISPLAY '**  '                                             
                     DISPLAY '**  '                                             
                     CALL 'ILBOABN0'     USING ABEND-CODE                       
W26600             END-IF                                                       
W26600           END-IF                                                         
W26600         END-IF                                                           
           END-IF.                                                              
W26600                                                                          
W26600******************************************************************INKUP102
W26600 0500-INITIALIZE-TCKRSTCNTL-RCD.                                          
W26600                                                                          
W26000     MOVE '0'                    TO TCKRSTCNTL-CKPT-STAT-CODE.            
W26600     MOVE 0                      TO TCKRSTCNTL-TRANS-QTY.                 
W26600     MOVE 0                      TO TCKRSTCNTL-CKPT-TAKEN-QTY.            
W26600     MOVE 0                      TO TCKRSTCNTL-TRANS-PRCSD-QTY.           
W26600     PERFORM 7710-UPDT-TCKRSTCNTL-RCD.                            INKUP102
W26600     PERFORM 7900-COMMIT-WORK.                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1100-PROCESS-SUMM-INV-FILE.                                              
                                                                                
           EVALUATE TRUE                                                        
             WHEN IN009-FILE-HDR                                                
RWJRWJ         MOVE IN009-EDITED-INVENTORY-RECORD                               
RWJRWJ                         TO ISAVE-EDITED-INVENTORY-RECORD                 
P28545         MOVE 'Y'        TO PS-HEADER-FOUND-SW                            
P28545******   PERFORM 2000-UPDT-DKEY                                           
             WHEN OTHER                                                         
P28545         PERFORM 2000-UPDT-DKEY                                           
               IF IN009-STORE-NUMBER NOT EQUAL SA-LOC-NBR                       
               OR IN009-AREA-NUMBER NOT EQUAL SA-AREA-NBR                       
W26600           IF SA-LOC-NBR > 0                                              
W26600           AND IN009-STORE-NUMBER NOT EQUAL SA-LOC-NBR                    
P28545             DISPLAY 'PROCESSING LOC: ', SA-LOC-NBR,                      
P28545                     ' COUNT: ', PV-LOC-REC-COUNT                         
P28545             MOVE ZEROS        TO PV-LOC-REC-COUNT                        
W26600             PERFORM 2500-MASS-ADD-DELETE-AREA-ADD                        
W26600             PERFORM 2600-FMT-UPDT-RSRT-INFO                              
W26600             PERFORM 7900-COMMIT-WORK                                     
895217             MOVE 'Y'  TO PS-FIRST-TIME-SW                                
W26600           ELSE                                                           
W26600           IF SA-AREA-NBR > 0                                             
                 AND IN009-AREA-NUMBER NOT EQUAL SA-AREA-NBR                    
W26600             PERFORM 2600-FMT-UPDT-RSRT-INFO                              
W26600             PERFORM 7900-COMMIT-WORK                                     
895217             MOVE 'Y'  TO PS-FIRST-TIME-SW                                
                 END-IF                                                         
W26600           END-IF                                                         
895217                                                                          
895217           IF PS-FIRST-TIME                                               
895217             PERFORM 2600-FMT-UPDT-RSRT-INFO                              
895217             PERFORM 7900-COMMIT-WORK                                     
895217             MOVE 'Y'  TO PS-FIRST-TIME-SW                                
895217           END-IF                                                         
895217                                                                          
W26600           PERFORM 2300-FMT-INVLOC-ROW                                    
W26600           MOVE IN009-STORE-NUMBER                                        
W26600                                 TO SA-LOC-NBR                            
W26600           MOVE IN009-AREA-NUMBER                                         
W26600                                 TO SA-AREA-NBR                           
               END-IF                                                           
                                                                                
               PERFORM 2400-FMT-SUMINV-ROW                                      
P28545                                                                          
           END-EVALUATE.                                                        
W26600                                                                          
W26600     PERFORM 8200-READ-SUM-INV-FILE.                                      
                                                                                
W26600/                                                                         
      ******************************************************************        
      * OBTAIN THE LAST DKEY SEQUENCE NUMBER                                    
      ******************************************************************        
       2000-UPDT-DKEY.                                                          
                                                                                
P28545     IF PS-HEADER-FOUND                                                   
P28545                                                                          
P28545       MOVE 'N'        TO PS-HEADER-FOUND-SW                              
RONNAA                                                                          
RONNAA*      OPEN  CURSOR  & FETCH                                              
RONNAA       PERFORM 7400-OPEN-FETCH-TINCNTL                                    
                                                                                
             IF PS-TINCNTL-RCD-FND                                              
P28545         IF PV-PROCESS-JOB-EXTD = 'B'                                     
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
P28545                                     + 2500000                            
P28545         ELSE                                                             
P28545         IF PV-PROCESS-JOB-EXTD = 'C'                                     
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
P28545                                     + 3750000                            
P28545         ELSE                                                             
P28545         IF PV-PROCESS-JOB-EXTD = 'D'                                     
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
P28545                                     + 5000000                            
P28545         ELSE                                                             
P28545         IF PV-PROCESS-JOB-EXTD = 'E'                                     
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
P28545                                     + 6250000                            
P28545         ELSE                                                             
P28545         IF PV-PROCESS-JOB-EXTD = 'F'                                     
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
P28545                                     + 7500000                            
P28545         ELSE                                                             
P28545           COMPUTE SA-START-DKEY-NBR = INCNTL-LAST-DKEY-NBR               
RWJRWJ**********                           + 1250000                            
P28545         END-IF                                                           
P28545                                                                          
P28545         MOVE SA-START-DKEY-NBR    TO SA-SUMINV-DKEY-NBR                  
P28545                                                                          
P28545*        MOVE INCNTL-LAST-DKEY-NBR                                        
P28545*                                TO SA-START-DKEY-NBR                     
P28545*                                   SA-SUMINV-DKEY-NBR                    
P28545*                                                                         
               COMPUTE SA-END-DKEY-NBR   = SA-START-DKEY-NBR                    
RWJRWJ                                   + ISAVE-FILE-REC-COUNT                 
RWJRWJ*******                            + IN009-FILE-REC-COUNT                 
                                                                                
RWJRWJ         MOVE ISAVE-FILE-REC-COUNT TO SA-FILE-REC-COUNT                   
RWJRWJ*******  MOVE IN009-FILE-REC-COUNT TO SA-FILE-REC-COUNT                   
                                                                                
W26600         PERFORM 7510-UPDATE-TINCNTL                                      
RONNAA*        CLOSE CURSOR                                                     
RONNAA                                                                          
RONNAA         EXEC SQL                                                         
RONNAA             CLOSE TINCNTL_CSR                                            
RONNAA         END-EXEC                                                         
RONNAA                                                                          
RONNAA         EVALUATE TRUE                                                    
RONNAA             WHEN SQLCODE < ZERO                                          
RONNAA                 DISPLAY '** ABEND - INKUP102 **'                         
RONNAA                 DISPLAY '** UPDATE ERROR ON CHECK'                       
RONNAA                 DISPLAY '** TINCNTL TABLE'                               
RONNAA                 PERFORM 9900-SQL-ABEND                                   
RONNAA             WHEN SQLCODE > ZERO                                          
RONNAA             WHEN SQLWARN0 NOT = SPACES                                   
RONNAA                 DISPLAY '** ABEND - INKUP102 **'                         
RONNAA                 DISPLAY '** CLOSE WARNING ON CHECK'                      
RONNAA                 DISPLAY '** TINCNTL TABLE'                               
RONNAA                 PERFORM 9900-SQL-ABEND                                   
RONNAA             WHEN OTHER                                                   
RONNAA                 CONTINUE                                                 
RONNAA         END-EVALUATE                                                     
RONNAA                                                                          
W26600         PERFORM 7900-COMMIT-WORK                                         
P28545       END-IF                                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2300-FMT-INVLOC-ROW.                                                     
                                                                                
W26600     MOVE IN009-STORE-NUMBER     TO INVLOC-LOC-NBR.                       
           MOVE IN009-AREA-NUMBER      TO INVLOC-AREA-NBR.                      
           INITIALIZE                     INVLOC-TOT-REC-CORR-QTY               
                                          INVLOC-TOT-ITM-CORR-QTY               
                                          INVLOC-TOT-ITM-INVLID-QTY             
                                          INVLOC-TOT-REC-INVLID-QTY.            
           PERFORM 7200-INSERT-TINVLOC-ROW.                                     
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2400-FMT-SUMINV-ROW.                                                     
           DISPLAY '2400-FMT-SUMINV-ROW'                                        
                                                                                
W26600     MOVE IN009-STORE-NUMBER     TO SUMINV-LOC-NBR.                       
           MOVE IN009-AREA-NUMBER      TO SUMINV-AREA-NBR                       
W26600     MOVE IN009-LINE-NUMBER      TO SA-LINE-NBR                           
W21732     MOVE IN009-SKU              TO SUMINV-SKU-NBR                        
W26600                                    SA-SKU-NBR.                           
25248      MOVE IN009-UPC-CODE         TO SUMINV-UPC-NBR                        
W26600                                    SA-UPC-NBR.                           
W21732     MOVE IN009-DEPT             TO SUMINV-DEPT-NBR.                      
W21732     MOVE IN009-MAJ-CLASS        TO SUMINV-MAJ-CL-NBR.                    
W21732     MOVE IN009-SUB-CLASS        TO SUMINV-SUB-CL-NBR.                    
W26600     MOVE IN009-PRICE            TO SUMINV-INV-UNIT-PR-AMT                
W26600                                    SA-INV-UNIT-PR-AMT.                   
           MOVE IN009-QUANTITY-COMP    TO SUMINV-INV-QTY.                       
           MOVE IN009-MISSED-MARKDOWN-IND                                       
                                       TO SUMINV-MISSED-MD-IND.                 
W26600     COMPUTE SA-SUMINV-DKEY-NBR = SA-SUMINV-DKEY-NBR + 1.                 
W26600     MOVE SA-SUMINV-DKEY-NBR     TO SUMINV-DKEY-NBR.                      
           COMPUTE PV-EXTENDED-RETAIL ROUNDED =                                 
                   (IN009-PRICE * IN009-QUANTITY-COMP).                         
           MOVE PV-EXTENDED-RETAIL     TO SUMINV-EXTD-AMT.                      
           INITIALIZE                     SUMINV-CHG-ID-NBR.                    
W26600     MOVE PT-NBR-SUM-INV-READ    TO SA-NBR-SUM-INV-READ                   
W26600                                    TCKRSTCNTL-TRANS-QTY                  
W26600                                    TCKRSTCNTL-TRANS-PRCSD-QTY.           
           PERFORM 7000-INSERT-TSUMINV-ROW.                                     
P28545     ADD 1                       TO PV-LOC-REC-COUNT.                     
                                                                                
W26600******************************************************************        
W26600 2500-MASS-ADD-DELETE-AREA-ADD.                                           
W26600                                                                          
W26600     INITIALIZE                     DCLTINVLOC.                           
           MOVE SA-LOC-NBR             TO INVLOC-LOC-NBR                        
           MOVE 888888                 TO INVLOC-AREA-NBR                       
           INITIALIZE                     INVLOC-TOT-REC-CORR-QTY               
                                          INVLOC-TOT-ITM-CORR-QTY               
                                          INVLOC-TOT-ITM-INVLID-QTY             
                                          INVLOC-TOT-REC-INVLID-QTY             
           PERFORM 7200-INSERT-TINVLOC-ROW                                      
           MOVE 999999                 TO INVLOC-AREA-NBR.                      
           PERFORM 7200-INSERT-TINVLOC-ROW.                                     
           PERFORM 3000-UPDATE-DATA-REC-DATE.                                   
                                                                                
      ******************************************************************        
W26600 2600-FMT-UPDT-RSRT-INFO.                                                 
W26600                                                                          
W26600     IF NOT PS-SUM-INV-EOF                                                
W26600         MOVE SA-SAVE-AREA       TO TCKRSTINF-WORK-STRG-DESC-TEXT         
W26600         MOVE LENGTH OF SA-SAVE-AREA                                      
W26600                                 TO TCKRSTINF-WORK-STRG-DESC-LEN          
W26600         PERFORM 7810-UPDT-TCKRSTINF-RCD                                  
W26600         IF PS-TCKRSTINF-RCD-FND                                          
W26600           CONTINUE                                                       
W26600         ELSE                                                             
P28545           MOVE PV-PROCESS-JOB-NAME TO TCKRSTINF-JOB-NAME                 
W26600           MOVE PC-CKPT-PLAN-NAME                                         
W26600                                 TO TCKRSTINF-PLAN-NAME                   
W26600           MOVE 0                TO TCKRSTINF-RST-TRANS-SEQ-NMBR          
W26600                                    TCKRSTINF-SEQ-NMBR                    
W26600           PERFORM 7820-INSRT-TCKRSTINF-RCD                               
W26600         END-IF                                                           
W26600         ADD +1                  TO TCKRSTCNTL-CKPT-TAKEN-QTY             
W26600         MOVE '9'                TO TCKRSTCNTL-CKPT-STAT-CODE             
W26600     ELSE                                                                 
W26600         PERFORM 7840-DLT-TCKRSTINF-RCD                                   
W26600         MOVE '0'                TO TCKRSTCNTL-CKPT-STAT-CODE             
W26600     END-IF.                                                              
W26600                                                                          
W26600     PERFORM 7710-UPDT-TCKRSTCNTL-RCD.                            INKUP102
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       3000-UPDATE-DATA-REC-DATE.                                               
                                                                                
      ******************************************************************        
      * THE ACTL-INV-RECD-DATE DOES NOT GET UPDATED FOR STORES THAT             
      * HAD INVENTORY KEYED AND PROCESSED *AFTER* THE SCANNED INVENTORY.        
      *                                                                         
      * THE ORIGINAL ACTL-INV-RECD-DATE IS KEPT ON THE TABLE                    
      ******************************************************************        
      ****************************************************************          
      *  A SITUATION CAN EXIST WHERE, SHEET INVENTORY WILL BE                   
      *  PROCESSED FOR A STORE AFTER THE STORE HAS COMPLETED ITS                
      *  SCANNED INVENTORY.  IN THIS SITUATION THE DATE CHECKS                  
      *  WILL BE BY-PASSED WHEN PROCESSING THE "SECOND" SHEET INVENTORY         
      *  DATA                                                                   
      ****************************************************************          
           MOVE SA-LOC-NBR             TO DK-STORE.                             
                                                                                
           SET PS-AFT-INV-STORE-NOT-FOUND                                       
                                       TO TRUE.                                 
           PERFORM 5000-DET-AFTER-INV-STORE                                     
               VARYING PV-STORE-SUB FROM 1 BY 1                                 
               UNTIL PV-STORE-SUB > PC-MAX-AFT-INV-STORES                       
                   OR PS-AFT-INV-STORE-FOUND.                                   
                                                                                
           IF PS-AFT-INV-STORE-FOUND                                            
                CONTINUE                                                        
           ELSE                                                                 
W26600         MOVE SA-LOC-NBR         TO INVPAR-LOC-NBR                        
P28545         PERFORM 7250-SELECT-TINVPAR                                      
P28545******   PERFORM 7300-UPDATE-TINVPAR-ROW                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   SEARCH AFTER-INV STORE TABLE                                          
      ******************************************************************        
       5000-DET-AFTER-INV-STORE.                                                
                                                                                
           IF IN009-STORE-NUMBER = PV-AFT-INV-STORE(PV-STORE-SUB)               
W26600         SET PS-AFT-INV-STORE-FOUND                                       
W26600                                 TO TRUE                                  
W26600     END-IF.                                                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
W26600 7000-INSERT-TSUMINV-ROW.                                                 
                                                                                
           DISPLAY '7000-INSERT-TSUMINV-ROW'                                    
           PERFORM                                                              
             WITH TEST AFTER                                                    
             VARYING PV-SQL-SUB                                                 
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
               OR SQLCODE = 0)                                                  
                                                                                
             EXEC SQL                                                           
               INSERT INTO TSUMINV                                              
W26600                (LOC_NBR,                                                 
                       AREA_NBR,                                                
W21732                 SKU_NBR,                                                 
25248                  UPC_NBR,                                                 
                       INV_UNIT_PR_AMT,                                         
                       INV_QTY,                                                 
                       MISSED_MD_IND,                                           
                       DKEY_NBR,                                                
                       CHG_TMST,                                                
                       CHG_ID_NBR,                                              
                       EXTD_AMT,                                                
W21732                 DEPT_NBR,                                                
W21732                 MAJ_CL_NBR,                                              
W21732                 SUB_CL_NBR)                                              
W26600         VALUES (:SUMINV-LOC-NBR,                                         
                       :SUMINV-AREA-NBR,                                        
W21732                 :SUMINV-SKU-NBR,                                         
25248                  :SUMINV-UPC-NBR,                                         
                       :SUMINV-INV-UNIT-PR-AMT,                                 
                       :SUMINV-INV-QTY,                                         
                       :SUMINV-MISSED-MD-IND,                                   
                       :SUMINV-DKEY-NBR,                                        
                        CURRENT TIMESTAMP,                                      
                       :SUMINV-CHG-ID-NBR,                                      
                       :SUMINV-EXTD-AMT,                                        
W21732                 :SUMINV-DEPT-NBR,                                        
W21732                 :SUMINV-MAJ-CL-NBR,                                      
W21732                 :SUMINV-SUB-CL-NBR)                                      
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = 0                                                 
                 CONTINUE                                                       
               WHEN SQLCODE = -803                                              
                 DISPLAY 'LOC ' SUMINV-LOC-NBR                                  
                         ' AREA ' SUMINV-AREA-NBR                               
W21732                   ' SKU NBR ' SUMINV-SKU-NBR                             
25248                    ' UPC NBR ' SUMINV-UPC-NBR                             
                         ' UNIT PR ' SUMINV-INV-UNIT-PR-AMT                     
                         ' DKEY ' SUMINV-DKEY-NBR                               
W21732           MOVE '7000-INSERT-TSUMINV-ROW'                                 
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'INSERTING INTO TSUMINV TABLE'                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
W21732           MOVE '7000-INSERT-TSUMINV-ROW'                                 
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'INSERTING INTO TSUMINV TABLE'                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
             END-EVALUATE                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE '7000-INSERT-TSUMINV-ROW      '                             
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED   '                                   
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
W26600 7200-INSERT-TINVLOC-ROW.                                                 
                                                                                
           PERFORM                                                              
             WITH TEST AFTER                                                    
             VARYING PV-SQL-SUB                                                 
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
               OR SQLCODE = -803                                                
               OR SQLCODE = 0)                                                  
                                                                                
             EXEC SQL                                                           
               INSERT INTO TINVLOC                                              
W26600                   (LOC_NBR,                                              
                          AREA_NBR,                                             
                          TOT_REC_CORR_QTY,                                     
                          TOT_ITM_CORR_QTY,                                     
                          TOT_ITM_INVLID_QTY,                                   
                          TOT_REC_INVLID_QTY,                                   
W26600                    CHG_TMST)                                             
W26600         VALUES (:INVLOC-LOC-NBR,                                         
                          :INVLOC-AREA-NBR,                                     
                          :INVLOC-TOT-REC-CORR-QTY,                             
                          :INVLOC-TOT-ITM-CORR-QTY,                             
                          :INVLOC-TOT-ITM-INVLID-QTY,                           
                          :INVLOC-TOT-REC-INVLID-QTY,                           
                          CURRENT TIMESTAMP)                                    
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = 0                                                 
                 CONTINUE                                                       
               WHEN SQLCODE = -803                                              
                 CONTINUE                                                       
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                 MOVE '7200-INSERT-TINVLOC-ROW'                                 
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'INSERTING INTO TINVLOC TABLE'                            
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE '7200-INSERT-TINVLOC-ROW'                                   
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED   '                                   
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
P28545******************************************************************        
P28545* 7250-SELECT-TINVPAR.                                                    
P28545******************************************************************        
P28545 7250-SELECT-TINVPAR.                                                     
P28545                                                                          
P28545     PERFORM                                                              
P28545       WITH TEST AFTER                                                    
P28545       VARYING PV-RETRY-COUNT                                             
P28545         FROM 1 BY 1                                                      
P28545         UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES                           
P28545         OR SQLCODE = 0                                                   
P28545         OR SQLCODE = +100)                                               
P28545                                                                          
P28545       EXEC SQL                                                           
P28545            SELECT LOC_NBR,                                               
P28545                   A.INV_ID                                               
P28545            INTO :INVPAR-LOC-NBR,                                         
P28545                 :INVPAR-INV-ID                                           
P28545            FROM TINVPAR A,                                               
P28545                 TINCNTL B                                                
P28545            WHERE B.ACTV_IND = 'Y'                                        
P28545              AND A.INV_ID = B.INV_ID                                     
P28545              AND LOC_NBR = :INVPAR-LOC-NBR                               
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                               
W33304              AND A.ACTL_FIN_BK_DTE = '9999-09-09'                        
P28545            WITH UR                                                       
P28545       END-EXEC                                                           
P28545                                                                          
P28545       EVALUATE TRUE                                                      
P28545         WHEN SQLCODE = 0                                                 
P28545             PERFORM 7300-UPDATE-TINVPAR-ROW                              
P28545         WHEN SQLCODE = -904                                              
P28545           CONTINUE                                                       
P28545         WHEN SQLWARN0 NOT = SPACES                                       
P28545         WHEN SQLCODE < 0                                                 
P28545         WHEN SQLCODE > 0                                                 
P28545           INITIALIZE            PS-TINCNTL-RCD-FND-SW                    
P28545           MOVE '7250-SELECT-TINVPAR'                                     
P28545                                 TO PV-PARAGRAPH-NAME                     
P28545           MOVE 'SELECT TINVPAR TABLE'                                    
P28545                                 TO PV-DB2-OPERATION-ATTEMPTED            
P28545           PERFORM 9900-SQL-ABEND                                         
P28545       END-EVALUATE                                                       
P28545     END-PERFORM.                                                         
P28545                                                                          
P28545     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
P28545         MOVE '7250-SELECT-TINVPAR            '                           
P28545                                 TO PV-PARAGRAPH-NAME                     
P28545         MOVE 'MAX RETRIES EXCEEDED   '                                   
P28545                                 TO PV-DB2-OPERATION-ATTEMPTED            
P28545         PERFORM 9900-SQL-ABEND                                           
P28545     END-IF.                                                              
P28545                                                                          
      ******************************************************************        
      *                                                                         
      ******************************************************************        
W26600 7300-UPDATE-TINVPAR-ROW.                                                 
                                                                                
           PERFORM                                                              
             WITH TEST AFTER                                                    
             VARYING PV-SQL-SUB                                                 
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
             EXEC SQL                                                           
                 UPDATE TINVPAR                                                 
                 SET ACTL_INV_RECD_DTE  = DATE (CURRENT TIMESTAMP),             
                     CHG_ID_NBR = :PC-PROGRAM-ID,                               
                     CHG_TMST = CURRENT TIMESTAMP                               
W26600           WHERE (LOC_NBR         = :INVPAR-LOC-NBR)                      
P28545           AND   (INV_ID          = :INVPAR-INV-ID)                       
W26600           AND   (ACTL_INV_RECD_DTE = :PC-EMPTY-DATE)                     
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = 0                                                 
                 CONTINUE                                                       
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
P28545           DISPLAY 'INKUP102 - INVALID STORE ' INVPAR-LOC-NBR,            
P28545                   ' INV_ID - ' INVPAR-INV-ID                             
                 MOVE '7300-UPDATE-TINVPAR-ROW'                                 
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'UPDATING TINVPAR TABLE'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
P28545           DISPLAY 'INKUP102 - INVALID STORE ' INVPAR-LOC-NBR,            
P28545                   ' INV_ID - ' INVPAR-INV-ID                             
               MOVE '7300-UPDATE-TINVPAR-ROW'                                   
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED'                                      
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
RONNAA******************************************************************        
RONNAA 7400-OPEN-FETCH-TINCNTL.                                                 
RONNAA                                                                          
RONNAA     MOVE 'N'                  TO PS-HEADER-FOUND-SW                      
RONNAA     MOVE IN009-STORE-NUMBER   TO INVPAR-LOC-NBR                          
RONNAA                                                                          
RONNAA     PERFORM                                                              
RONNAA       WITH TEST AFTER                                                    
RONNAA       VARYING PV-RETRY-COUNT                                             
RONNAA       FROM 1 BY 1                                                        
RONNAA       UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES                             
                    OR SQLCODE = 0                                              
                    OR SQLCODE = +100)                                          
RONNAA                                                                          
RONNAA*                                                                         
RONNAA*      OPEN CURSOR                                                        
RONNAA       EXEC SQL                                                           
RONNAA           OPEN TINCNTL_CSR                                               
RONNAA       END-EXEC                                                           
RONNAA                                                                          
RONNAA       EVALUATE TRUE                                                      
RONNAA         WHEN SQLCODE = 0                                                 
RONNAA         WHEN SQLCODE = -904                                              
RONNAA              CONTINUE                                                    
RONNAA              DISPLAY 'SQLCODE = ' SQLCODE                                
RONNAA         WHEN OTHER                                                       
RONNAA              DISPLAY '** ABEND - INKUP102 **'                            
RONNAA              DISPLAY '** OPEN CURSOR          '                          
RONNAA              DISPLAY '** TINCNTL TABLE              '                    
RONNAA              MOVE '7400-OPEN-FETCH-TINCNTL'                              
RONNAA                                       TO PV-PARAGRAPH-NAME               
RONNAA              MOVE 'OPEN CURSOR         '                                 
RONNAA                                   TO PV-DB2-OPERATION-ATTEMPTED  ED      
RONNAA              PERFORM 9900-SQL-ABEND                                      
RONNAA              CONTINUE                                                    
RONNAA       END-EVALUATE                                                       
RONNAA                                                                          
RONNAA       IF SQLCODE = 0                                                     
                                                                                
RONNAA*          FETCH CURSOR                                                   
RONNAA           EXEC SQL                                                       
RONNAA              FETCH TINCNTL_CSR                                           
RONNAA               INTO :INCNTL-LAST-DKEY-NBR                                 
                 END-EXEC                                                       
                                                                                
                 EVALUATE TRUE                                                  
                   WHEN SQLCODE = 0                                             
W26600                  SET PS-TINCNTL-RCD-FND                                  
W26600                                     TO TRUE                              
                        CONTINUE                                                
                    WHEN SQLCODE = +100                                         
RONNAA              WHEN SQLCODE = -911                                         
RONNAA              WHEN SQLCODE = -913                                         
                    WHEN SQLCODE = -904                                         
W26600                  INITIALIZE            PS-TINCNTL-RCD-FND-SW             
                    WHEN SQLWARN0 NOT = SPACES                                  
                    WHEN SQLCODE < 0                                            
                    WHEN SQLCODE > 0                                            
W26600                  INITIALIZE            PS-TINCNTL-RCD-FND-SW             
                        MOVE '7500-FETCH-TINCNTL'                               
                                           TO PV-PARAGRAPH-NAME                 
                        MOVE 'FETCH  TINCNTL TABLE'                             
                                           TO PV-DB2-OPERATION-ATTEMPTED        
                        PERFORM 9900-SQL-ABEND                                  
                 END-EVALUATE                                                   
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
RONNAA*    UPDATE                                                               
RONNAA                                                                          
RONNAA     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
RONNAA         MOVE '7400-OPEN-FETCH-TINCNTL            '                       
RONNAA                                 TO PV-PARAGRAPH-NAME                     
RONNAA         MOVE 'MAX RETRIES EXCEEDED   '                                   
RONNAA                                 TO PV-DB2-OPERATION-ATTEMPTED            
RONNAA         PERFORM 9900-SQL-ABEND                                           
RONNAA     END-IF.                                                              
RONNAA                                                                          
                                                                                
W26600******************************************************************        
                                                                                
W26600******************************************************************        
W26600 7510-UPDATE-TINCNTL.                                                     
                                                                                
           PERFORM                                                              
             WITH TEST AFTER                                                    
             VARYING PV-SQL-SUB                                                 
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
             EXEC SQL                                                           
                 UPDATE TINCNTL                                                 
                 SET LAST_DKEY_NBR = :SA-END-DKEY-NBR                           
RONNAA*          WHERE INV_ID = :INCNTL-INV-ID                                  
RONNAA           WHERE CURRENT OF TINCNTL_CSR                                   
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = -904                                              
RONNAA         WHEN SQLCODE = -911                                              
RONNAA         WHEN SQLCODE = -913                                              
               WHEN SQLCODE = 0                                                 
                 DISPLAY 'TINCNTL UPDATED WITH DKEY: '                          
                           SA-END-DKEY-NBR                                      
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                 MOVE '7510-UPDATE-TINCNTL'                                     
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'UPDATING TINCNTL TABLE'                                  
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE '7510-UPDATE-TINCNTL            '                           
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED   '                                   
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
W26600                                                                          
W26600******************************************************************        
W26600*                                                                         
W26600******************************************************************        
W26600 7700-INIT-CHECKPOINT-SELECT.                                             
W26600                                                                          
P28545     MOVE PV-PROCESS-JOB-NAME TO TCKRSTCNTL-JOB-NAME.                     
P28545                                                                          
W26600     EXEC SQL                                                             
W26600       SELECT CKPT_STAT_CODE,                                             
W26600              TRANS_QTY,                                                  
W26600              CKPT_TAKEN_QTY,                                             
W26600              TRANS_PRCSD_QTY,                                            
W26600              PRCSD_TMESTP                                                
W26600       INTO   :TCKRSTCNTL-CKPT-STAT-CODE,                                 
W26600              :TCKRSTCNTL-TRANS-QTY,                                      
W26600              :TCKRSTCNTL-CKPT-TAKEN-QTY,                                 
W26600              :TCKRSTCNTL-TRANS-PRCSD-QTY,                                
W26600              :TCKRSTCNTL-PRCSD-TMESTP                                    
W26600       FROM   TCKRSTCNTL                                                  
P28545       WHERE  JOB_NAME  = :TCKRSTCNTL-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTCNTL-RCD-FND                                        
W26600                                 TO TRUE                                  
W26600       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTCNTL-RCD-FND-SW                 
W26600       WHEN OTHER                                                         
W26600         INITIALIZE              PS-TCKRSTCNTL-RCD-FND-SW                 
W26600         MOVE '7700-INIT-CHECKPOINT-SELECT'                               
W26600                                 TO PV-PARAGRAPH-NAME                     
W26600         MOVE 'SELECT TCKRSTCNTL TABLE'                                   
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP102
W26600 7710-UPDT-TCKRSTCNTL-RCD.                                        INKUP102
W26600                                                                          
P28545     MOVE PV-PROCESS-JOB-NAME TO TCKRSTCNTL-JOB-NAME.                     
P28545                                                                          
W26600     EXEC SQL                                                             
W26600       UPDATE TCKRSTCNTL                                                  
W26000       SET CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE,                   
W26600           TRANS_QTY = :TCKRSTCNTL-TRANS-QTY,                             
W26600           CKPT_TAKEN_QTY = :TCKRSTCNTL-CKPT-TAKEN-QTY,                   
W26600           TRANS_PRCSD_QTY = :TCKRSTCNTL-TRANS-PRCSD-QTY,                 
W26600           PRCSD_TMESTP = CURRENT_TIMESTAMP                               
P28545       WHERE  JOB_NAME  = :TCKRSTCNTL-JOB-NAME                            
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7710-UPDT-TCKRSTCNTL-RCD'                          INKUP102
W26600                                 TO PV-PARAGRAPH-NAME             INKUP102
W26600         MOVE 'UPDATE TCKRSTCNTL TABLE'                           INKUP102
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************        
W26600 7800-SELECT-RESTART-INFO.                                                
W26600                                                                          
P28545     MOVE PV-PROCESS-JOB-NAME TO TCKRSTINF-JOB-NAME.                      
P28545                                                                          
W26600     EXEC SQL                                                             
W26600       SELECT WORK_STRG_DESC                                              
W26600       INTO   :TCKRSTINF-WORK-STRG-DESC                                   
W26600       FROM   TCKRSTINF                                                   
P28545       WHERE  JOB_NAME  = :TCKRSTINF-JOB-NAME                             
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTINF-RCD-FND                                         
W26600                                 TO TRUE                                  
W26000       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600       WHEN OTHER                                                         
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600         MOVE '7800-SELECT-RESTART-INFO'                                  
W26600                                 TO PV-PARAGRAPH-NAME                     
W26600         MOVE 'SELECT TCKRSTINF TABLE'                                    
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP102
W26600 7810-UPDT-TCKRSTINF-RCD.                                                 
W26600                                                                          
P28545     MOVE PV-PROCESS-JOB-NAME TO TCKRSTINF-JOB-NAME.                      
P28545                                                                          
W26600     EXEC SQL                                                             
W26600       UPDATE TCKRSTINF                                                   
W26600       SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                     
P28545       WHERE  JOB_NAME  = :TCKRSTINF-JOB-NAME                             
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         SET PS-TCKRSTINF-RCD-FND                                         
W26600                                 TO TRUE                                  
W26600       WHEN SQLCODE = +100                                                
W26600         INITIALIZE              PS-TCKRSTINF-RCD-FND-SW                  
W26600       WHEN OTHER                                                         
W26600         MOVE '7810-UPDT-TCKRSTINF-RCD'                           INKUP102
W26600                                 TO PV-PARAGRAPH-NAME             INKUP102
W26600         MOVE 'UPDATE TCKRSTINF TABLE'                            INKUP102
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP102
W26600 7820-INSRT-TCKRSTINF-RCD.                                                
W26600                                                                          
W26600     EXEC SQL                                                             
W26600       INSERT INTO TCKRSTINF                                              
W26600       VALUES (:TCKRSTINF-JOB-NAME,                                       
W26600               :TCKRSTINF-PLAN-NAME,                                      
W26600               :TCKRSTINF-RST-TRANS-SEQ-NMBR,                             
W26600               :TCKRSTINF-SEQ-NMBR,                                       
W26600               :TCKRSTINF-WORK-STRG-DESC)                                 
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7820-INSRT-TCKRSTINF-RCD'                          INKUP102
W26600                                 TO PV-PARAGRAPH-NAME             INKUP102
W26600         MOVE 'INSERT TCKRSTINF TABLE'                            INKUP102
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************INKUP102
W26600 7840-DLT-TCKRSTINF-RCD.                                                  
W26600                                                                          
P28545     MOVE PV-PROCESS-JOB-NAME TO TCKRSTINF-JOB-NAME.                      
P28545                                                                          
W26600     EXEC SQL                                                             
W26600       DELETE FROM TCKRSTINF                                              
P28545       WHERE  JOB_NAME  = :TCKRSTINF-JOB-NAME                             
W26600       AND    PLAN_NAME = :PC-CKPT-PLAN-NAME                              
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7840-DLT-TCKRSTINF-RCD'                            INKUP102
W26600                                 TO PV-PARAGRAPH-NAME             INKUP102
W26600         MOVE 'DELETE TCKRSTINF TABLE'                            INKUP102
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************        
W26600 7900-COMMIT-WORK.                                                        
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         COMMIT WORK                                                      
W26600     END-EXEC.                                                            
W26600                                                                          
W26600     EVALUATE TRUE                                                        
W26600       WHEN SQLCODE = +0                                                  
W26600         CONTINUE                                                         
W26600       WHEN OTHER                                                         
W26600         MOVE '7900-COMMIT-WORK'                                  INKUP102
W26600                                 TO PV-PARAGRAPH-NAME             INKUP102
W26600         MOVE 'DB2 COMMIT'                                        INKUP102
W26600                                 TO PV-DB2-OPERATION-ATTEMPTED            
W26600         PERFORM 9900-SQL-ABEND                                           
W26600     END-EVALUATE.                                                        
W26600                                                                          
W26600******************************************************************        
W26600 7910-ROLLBACK-WORK.                                                      
W26600                                                                          
W26600     EXEC SQL                                                             
W26600         ROLLBACK WORK                                                    
W26600     END-EXEC.                                                            
W26600                                                                          
W26600/                                                                         
W26600******************************************************************        
W26600 8000-OPEN-FILES.                                                         
W26600                                                                          
P28545     OPEN INPUT SUMMARIZED-INVENTORY-FILE                                 
P28545          INPUT CC-JOBNAME-CNTL-FILE.                                     
                                                                                
P28545     PERFORM 8300-READ-PROCESS-JOB-NAME.                                  
P28545                                                                          
P28545     DISPLAY ' '.                                                         
P28545     DISPLAY ' PROCESSING JOB NAME: ' PV-PROCESS-JOB-NAME.                
P28545                                                                          
P28545     IF PV-PROCESS-JOB-NAME = SPACES                                      
P28545         MOVE +4002                 TO ABEND-CODE                         
P28545         DISPLAY '**  ABEND     **'                                       
P28545         DISPLAY '**  PROGRAM   **  =  INKUP102'                          
P28545         DISPLAY '**  INVALID PROCESSING JOB NAME'                        
P28545         CALL 'ILBOABN0'  USING ABEND-CODE                                
P28545     END-IF.                                                              
P28545                                                                          
      ****************************************************************          
      * READS THE STORES ON THE AFTER-INV-STORE-CONTROL-CARD                    
      ****************************************************************          
W26600 8100-READ-AFT-INV-STORE-CC.                                              
                                                                                
           READ IN-AFT-INV-STORES-CC                                            
             INTO CC-AFT-INV-STORES                                             
               AT END                                                           
                 MOVE 'Y'              TO PS-END-OF-AFT-INV-STORE-IND.          
                                                                                
           IF PS-END-OF-AFT-INV-STORE-CC                                        
               CONTINUE                                                         
           ELSE                                                                 
           IF PV-STORE-SUB > PC-MAX-AFT-INV-STORES                              
               MOVE +4008              TO ABEND-CODE                            
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUP102'                          
               DISPLAY '**  '                                                   
               DISPLAY '**  '                                                   
               DISPLAY                                                          
W28543           'PGM CAN PROCESS ONLY 100 "AFTER-INV" STORES'                  
               DISPLAY '**  '                                                   
               DISPLAY '**  '                                                   
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           ELSE                                                                 
               MOVE CC-STORE           TO PV-AFT-INV-STORE                      
                                          (PV-STORE-SUB)                        
           END-IF                                                               
           END-IF.                                                              
W26600                                                                          
W26600******************************************************************        
W26600*                                                                         
W26600******************************************************************        
W26600 8200-READ-SUM-INV-FILE.                                                  
W26600                                                                          
W26600     READ SUMMARIZED-INVENTORY-FILE                                       
W26600       INTO IN009-EDITED-INVENTORY-RECORD                                 
W26600         AT END SET PS-SUM-INV-EOF                                        
W26600                                 TO TRUE.                                 
W26600                                                                          
W26600     IF NOT PS-SUM-INV-EOF                                                
W26600         IF IN009-FILE-HDR                                                
W26600           CONTINUE                                                       
126600         ELSE                                                             
W26600           ADD +1                TO PT-NBR-SUM-INV-READ                   
W26600         END-IF                                                           
W26600     END-IF.                                                              
W26600                                                                          
P28545******************************************************************02700000
P28545* READS THE PROCESSING JOB NAME CONTROL CARD FILE.                02710000
P28545******************************************************************02720000
P28545 8300-READ-PROCESS-JOB-NAME.                                      02730000
P28545     READ CC-JOBNAME-CNTL-FILE INTO PV-PROCESS-JOB-NAME-CC        02740000
P28545         AT END                                                   02750000
P28545             MOVE +4003                 TO ABEND-CODE                     
P28545             DISPLAY '**  ABEND     **'                                   
P28545             DISPLAY '**  PROGRAM   **  =  INKUP102'                      
P28545             DISPLAY '**  EMPTY PROCESSING JOB NAME CONTROL CARD'         
P28545             CALL 'ILBOABN0'  USING ABEND-CODE.                           
P28545                                                                  02770000
W26600******************************************************************        
W26600 8900-CLOSE-FILES.                                                        
W26600                                                                          
P28545     CLOSE SUMMARIZED-INVENTORY-FILE                                      
P28545           CC-JOBNAME-CNTL-FILE.                                          
                                                                                
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE                               
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUP102'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           IF SQLCODE NOT = -911                                                
               EXEC SQL                                                         
                   ROLLBACK                                                     
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
