      *----------------------------------------------------------------*00010000
       IDENTIFICATION DIVISION.                                         00020000
      *----------------------------------------------------------------*00030000
       PROGRAM-ID.           HMKUT006.                                  00040000
       AUTHOR.               BOB MCASEY.                                00050000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00060000
       DATE-WRITTEN          08/27/2002.                                00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      *----------------------------------------------------------------*00100000
      *   HMKUT006 - RETRIEVE USER ID FOR COMMON EXTRACTS AT GMM AND   *00110000
      *              DMM LEVELS.                                       *00120000
      *----------------------------------------------------------------*00130000
      *  CHANGED:                                                      *00140000
      *                                                                *00150000
      *    WR/PITS#     DATE        DESCRIPTION                        *00160000
      *    --------   ----------   ---------------------------------   *00170000
      *    WR24087    08-27-2002    INITIAL DEVELOPMENT.               *00180000
      *    WR24706    04-25-2003    MODIFY LOGIC TO USE INDIVIDUAL 01  *00190011
      *                             LEVELS IN HMRD014.                 *00200011
      *----------------------------------------------------------------*00210012
                                                                        00220000
       ENVIRONMENT DIVISION.                                            00230000
      *----------------------------------------------------------------*00240000
       CONFIGURATION SECTION.                                           00250000
                                                                        00260000
       SOURCE-COMPUTER. IBM-3090.                                       00270000
       OBJECT-COMPUTER. IBM-3090.                                       00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
       FILE-CONTROL.                                                    00310000
           SELECT GMM-INP-FILE               ASSIGN TO INP01.           00320000
           SELECT DMM-INP-FILE               ASSIGN TO INP02.           00330000
           SELECT GMM-OUT-FILE               ASSIGN TO OUT01.           00340000
           SELECT DMM-OUT-FILE               ASSIGN TO OUT02.           00350000
008200                                                                  00360000
010200*----------------------------------------------------------------*00370000
010300 DATA DIVISION.                                                   00380000
010200*----------------------------------------------------------------*00390000
010600 FILE SECTION.                                                    00400000
1000RM*                                                                 00410000
                                                                        00420000
       FD  GMM-INP-FILE                                                 00430000
           LABEL RECORDS ARE STANDARD                                   00440000
           RECORDING MODE IS F                                          00450000
           BLOCK CONTAINS 0 RECORDS.                                    00460000
       01  GMM-INP-REC              PIC X(36).                          00470002
                                                                        00480000
       FD  DMM-INP-FILE                                                 00490000
           LABEL RECORDS ARE STANDARD                                   00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS.                                    00520000
       01  DMM-INP-REC              PIC X(42).                          00530002
                                                                        00540000
       FD  GMM-OUT-FILE                                                 00550000
           LABEL RECORDS ARE STANDARD                                   00560000
           RECORDING MODE IS F                                          00570000
           BLOCK CONTAINS 0 RECORDS.                                    00580000
       01  GMM-OUT-REC              PIC X(46).                          00590000
                                                                        00600000
       FD  DMM-OUT-FILE                                                 00610000
           LABEL RECORDS ARE STANDARD                                   00620000
           RECORDING MODE IS F                                          00630000
           BLOCK CONTAINS 0 RECORDS.                                    00640000
       01  DMM-OUT-REC              PIC X(52).                          00650002
                                                                        00660000
                                                                        00670000
       WORKING-STORAGE SECTION.                                         00680000
                                                                        00690000
       01  FILLER                           PIC X(50)    VALUE          00700000
           'BEGINNING OF HMKUT006 WORKING STORAGE'.                     00710000
                                                                        00720000
       01  PA-PROGRAM-ACCUMUALTORS.                                     00730000
           05  PA-GMM-RECS-READ             PIC 9(07)    VALUE 0.       00740000
           05  PA-DMM-RECS-READ             PIC 9(07)    VALUE 0.       00750000
           05  PA-GMM-RECS-WRITTEN          PIC 9(07)    VALUE 0.       00760000
           05  PA-DMM-RECS-WRITTEN          PIC 9(07)    VALUE 0.       00770000
                                                                        00780000
       01  PC-PROGRAM-CONSTANTS.                                        00790000
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE            00800000
                                                       'HMKUT006'.      00810000
           05  PC-MESSAGES.                                             00820000
               10  PC-SQL-ABEND-MESSAGE     PIC X(23)  VALUE            00830000
               '** ABEND -> SQLERROR = '.                               00840000
               10  PC-PROGRAM-MESSAGE       PIC X(23)  VALUE            00850000
               '** PROGRAM        ** = '.                               00860000
               10  PC-PARAGRAPH-MESSAGE     PIC X(23)  VALUE            00870000
               '** PARAGRAPH-NAME ** = '.                               00880000
                                                                        00890000
       01  PS-PROGRAM-SWITCHES.                                         00900000
           05  PS-EOF-GMM-FILE-SW         PIC X(01)   VALUE 'N'.        00910006
               88  PS-EOF-GMM-FILE                    VALUE 'Y'.        00920006
           05  PS-EOF-DMM-FILE-SW         PIC X(01)   VALUE 'N'.        00930006
               88  PS-EOF-DMM-FILE                    VALUE 'Y'.        00940006
                                                                        00950000
       01  PV-PROGRAM-VARIABLES.                                        00960000
           05  PV-ABEND-INFO.                                           00970000
               10  PV-ABEND-PARAGRAPH        PIC X(40)  VALUE SPACE.    00980000
               10  PV-ABEND-CODE COMP SYNC   PIC S9(04) VALUE +0000.    00990000
               10  PV-SQLCODE-DISPLAY        PIC ++++9  VALUE ZERO.     01000000
                                                                        01010000
                                                                        01020000
1202RM*----------------------------------------------------------------*01030008
1202RM*  DOMAIN EXTRACT - GMM LEVEL                                     01040008
1202RM*----------------------------------------------------------------*01050008
1202RM*    COPY HMRD006.                                                01060008
                                                                        01070000
1202RM*----------------------------------------------------------------*01080008
1202RM*  DOMAIN EXTRACT - DMM LEVEL                                     01090008
1202RM*----------------------------------------------------------------*01100008
1202RM*    COPY HMRD007.                                                01110008
                                                                        01120000
1202RM*----------------------------------------------------------------*01130008
1202RM*  COMMOM DOMAIN EXTRACT COPYBOOK                                 01140008
1202RM*----------------------------------------------------------------*01150008
1202RM     COPY HMRD014.                                                01160008
                                                                        01170008
      *----------------------------------------------------------------*01180000
      * TMDSEAR TABLE                                                   01190000
      *----------------------------------------------------------------*01200000
           EXEC SQL                                                     01210000
               INCLUDE TMDSEAR                                          01220000
           END-EXEC.                                                    01230000
                                                                        01240000
      *----------------------------------------------------------------*01250000
      * TRSPBAR TABLE                                                   01260000
      *----------------------------------------------------------------*01270000
           EXEC SQL                                                     01280000
               INCLUDE TRSPBAR                                          01290000
           END-EXEC.                                                    01300000
                                                                        01310000
      *----------------------------------------------------------------*01320000
      * TWORKER TABLE                                                   01330000
      *----------------------------------------------------------------*01340000
           EXEC SQL                                                     01350000
               INCLUDE TWORKER                                          01360000
           END-EXEC.                                                    01370000
                                                                        01380000
                                                                        01390000
           EXEC SQL                                                     01400000
               INCLUDE SQLCA                                            01410000
           END-EXEC.                                                    01420000
                                                                        01430000
           COPY SQLCA2.                                                 01440000
                                                                        01450000
      *----------------------------------------------------------------*01460000
      *  DB2 ERROR MESSAGES FORMAT AREA.                               *01470000
      *----------------------------------------------------------------*01480000
           COPY DPWS004.                                                01490000
                                                                        01500000
      *----------------------------------------------------------------*01510000
       PROCEDURE DIVISION.                                              01520000
                                                                        01530000
      *----------------------------------------------------------------*01540000
      *    MAINLINE-LOGIC                                              *01550000
      *----------------------------------------------------------------*01560000
                                                                        01570000
       0000-HMKUT006.                                                   01580000
                                                                        01590000
           PERFORM 1000-INITIALIZATION.                                 01600000
                                                                        01610000
           PERFORM 2000-PROCESS-GMM-DMM-DATA                            01620000
               UNTIL PS-EOF-GMM-FILE                                    01630000
                 AND PS-EOF-DMM-FILE.                                   01640000
                                                                        01650000
           PERFORM 9900-EOJ-LOGIC.                                      01660000
                                                                        01670000
           GOBACK.                                                      01680000
           EJECT                                                        01690000
      *----------------------------------------------------------------*01700000
      *    OPEN FILES, WRITE INITIAL HEADER RECORD                     *01710000
      *----------------------------------------------------------------*01720000
       1000-INITIALIZATION.                                             01730000
                                                                        01740000
           DISPLAY '**********************************'.                01750000
           DISPLAY '**------ BEGIN HMKUT006 ------**'.                  01760000
                                                                        01770000
           OPEN INPUT   GMM-INP-FILE.                                   01780000
           OPEN INPUT   DMM-INP-FILE.                                   01790000
           OPEN OUTPUT  GMM-OUT-FILE.                                   01800000
           OPEN OUTPUT  DMM-OUT-FILE.                                   01810000
                                                                        01820000
                                                                        01830000
      *----------------------------------------------------------------*01840000
      *    2000-PROCESS-GMM-DMM-DATA.                                  *01850000
      *      THIS IS PERFORMED ONLY VIA THE 0000-HMKUT006              *01860000
      *      PURPOSE: READ AND PROCESS THE INPUT FILES                 *01870000
      *----------------------------------------------------------------*01880000
       2000-PROCESS-GMM-DMM-DATA.                                       01890000
                                                                        01900000
           PERFORM 2100-PROCESS-GMM-FILE                                01910000
             UNTIL PS-EOF-GMM-FILE.                                     01920000
                                                                        01930000
           PERFORM 2200-PROCESS-DMM-FILE                                01940000
             UNTIL PS-EOF-DMM-FILE.                                     01950000
                                                                        01960000
       2100-PROCESS-GMM-FILE.                                           01970000
                                                                        01980000
           PERFORM 4000-READ-GMM-FILE.                                  01990000
           IF PS-EOF-GMM-FILE                                           02000000
               CONTINUE                                                 02010000
           ELSE                                                         02020000
1202RM*        MOVE HM006-GMA-NBR TO MDSEAR-GMA-NBR                     02030008
1202RM         MOVE HM014-GMM-GMA-NBR TO MDSEAR-GMA-NBR                 02040008
               PERFORM 6000-SELECT-GMM-USER-ID                          02050000
               PERFORM 5000-WRITE-GMM-RECORD                            02060000
           END-IF.                                                      02070000
                                                                        02080000
       2200-PROCESS-DMM-FILE.                                           02090000
                                                                        02100000
           PERFORM 4200-READ-DMM-FILE.                                  02110000
           IF PS-EOF-DMM-FILE                                           02120000
               CONTINUE                                                 02130000
           ELSE                                                         02140000
1202RM*        MOVE HM007-GMA-NBR TO MDSEAR-GMA-NBR                     02150008
1202RM*        MOVE HM007-DMA-NBR TO MDSEAR-DMA-NBR                     02160008
1202RM         MOVE HM014-DMM-GMA-NBR TO MDSEAR-GMA-NBR                 02170008
1202RM         MOVE HM014-DMM-DMA-NBR TO MDSEAR-DMA-NBR                 02180008
               PERFORM 6200-SELECT-DMM-USER-ID                          02190000
               PERFORM 5200-WRITE-DMM-RECORD                            02200000
           END-IF.                                                      02210000
                                                                        02220000
      *----------------------------------------------------------------*02230000
      *    READ THE GMM FILE                                            02240000
      *----------------------------------------------------------------*02250000
       4000-READ-GMM-FILE.                                              02260000
                                                                        02270000
1202RM*    READ GMM-INP-FILE  INTO HMRD006-RECORD                       02280008
0403RM*    READ GMM-INP-FILE  INTO HMRD014-RECORD                       02290009
0403RM     READ GMM-INP-FILE  INTO HMRD014-GMM-RECORD                   02300010
               AT END SET PS-EOF-GMM-FILE TO TRUE.                      02310000
                                                                        02320000
           IF PS-EOF-GMM-FILE                                           02330000
               CONTINUE                                                 02340000
           ELSE                                                         02350000
               ADD 1  TO PA-GMM-RECS-READ                               02360000
           END-IF.                                                      02370000
                                                                        02380000
      *----------------------------------------------------------------*02390000
      *    READ DMM FILE                                                02400000
      *----------------------------------------------------------------*02410000
       4200-READ-DMM-FILE.                                              02420000
                                                                        02430000
1202RM*    READ DMM-INP-FILE  INTO HMRD007-RECORD                       02440008
0403RM*    READ DMM-INP-FILE  INTO HMRD014-RECORD                       02450009
0403RM     READ DMM-INP-FILE  INTO HMRD014-DMM-RECORD                   02460010
               AT END SET PS-EOF-DMM-FILE TO TRUE.                      02470000
                                                                        02480000
           IF PS-EOF-DMM-FILE                                           02490000
               CONTINUE                                                 02500000
           ELSE                                                         02510000
               ADD 1  TO PA-DMM-RECS-READ                               02520000
           END-IF.                                                      02530000
                                                                        02540000
                                                                        02550000
      *----------------------------------------------------------------*02560000
      *    WRITE THE GMM RECORD                                         02570000
      *----------------------------------------------------------------*02580000
       5000-WRITE-GMM-RECORD.                                           02590000
                                                                        02600000
**TEMP*    DISPLAY '5000-WRITE-PRODUCT-RECORD. '.                       02610000
1202RM*    WRITE GMM-OUT-REC FROM HMRD006-RECORD.                       02620008
1202RM*    WRITE GMM-OUT-REC FROM HMRD014-RECORD.                       02630009
1202RM     WRITE GMM-OUT-REC FROM HMRD014-GMM-RECORD.                   02640009
           ADD 1 TO PA-GMM-RECS-WRITTEN.                                02650000
                                                                        02660000
                                                                        02670000
      *----------------------------------------------------------------*02680000
      *    WRITE THE DMM RECORD                                         02690000
      *----------------------------------------------------------------*02700000
       5200-WRITE-DMM-RECORD.                                           02710000
                                                                        02720000
**TEMP*    DISPLAY '5000-WRITE-PRODUCT-RECORD. '.                       02730000
1202RM*    WRITE DMM-OUT-REC FROM HMRD007-RECORD.                       02740008
0403RM*    WRITE DMM-OUT-REC FROM HMRD014-RECORD.                       02750009
0403RM     WRITE DMM-OUT-REC FROM HMRD014-DMM-RECORD.                   02760009
           ADD 1 TO PA-DMM-RECS-WRITTEN.                                02770000
                                                                        02780000
                                                                        02790000
      *----------------------------------------------------------------*02800000
      *    SELECT GMM USER ID                                           02810000
      *----------------------------------------------------------------*02820000
       6000-SELECT-GMM-USER-ID.                                         02830000
                                                                        02840000
           MOVE '6000-SELECT-GMM-USER-ID.' TO PV-ABEND-PARAGRAPH.       02850000
                                                                        02860000
           EXEC SQL                                                     02870000
              SELECT UID_NBR                                            02880000
1202RM*         INTO :HM006-GMM-USR-ID                                  02890008
1202RM          INTO :HM014-GMM-GMM-USR-ID                              02900008
                FROM TMDSEAR TMD                                        02910000
                   , TRSPBAR TRS                                        02920000
                   , TWORKER TW                                         02930000
              WHERE GMA_NBR              = :MDSEAR-GMA-NBR              02940000
                AND MDSELV_CDE           = 'GMA'                        02950000
                AND TMD.MDSEAR_DKEY      = TRS.MDSEAR_DKEY              02960000
                AND TRS.WORKER_NBR       = TW.WORKER_NBR                02970000
                AND (TRS.WORKER_STRT_DTE <= CURRENT_DATE)               02980001
                AND (TRS.WORKER_END_DTE  >= CURRENT_DATE)               02990001
                AND (TRS.RSPLVL_CDE       = 'GMM')                      03000001
              WITH UR                                                   03010000
           END-EXEC.                                                    03020000
                                                                        03030000
           PERFORM 8700-EVALUATE-SQLCODE.                               03040000
                                                                        03050000
      *----------------------------------------------------------------*03060000
      *    SELECT GMM USER ID                                           03070000
      *----------------------------------------------------------------*03080000
       6200-SELECT-DMM-USER-ID.                                         03090000
                                                                        03100000
           MOVE '6200-SELECT-DMM-USER-ID.' TO PV-ABEND-PARAGRAPH.       03110000
                                                                        03120000
           EXEC SQL                                                     03130000
              SELECT UID_NBR                                            03140000
1202RM*         INTO :HM007-DMM-USR-ID                                  03150008
1202RM          INTO :HM014-DMM-DMM-USR-ID                              03160008
                FROM TMDSEAR TMD                                        03170008
                   , TRSPBAR TRS                                        03180008
                   , TWORKER TW                                         03190008
              WHERE GMA_NBR              = :MDSEAR-GMA-NBR              03200008
                AND DMA_NBR              = :MDSEAR-DMA-NBR              03210008
                AND MDSELV_CDE           = 'DMA'                        03220008
                AND TMD.MDSEAR_DKEY      = TRS.MDSEAR_DKEY              03230008
                AND TRS.WORKER_NBR       = TW.WORKER_NBR                03240008
                AND (TRS.WORKER_STRT_DTE <= CURRENT_DATE)               03250008
                AND (TRS.WORKER_END_DTE  >= CURRENT_DATE)               03260008
                AND TRS.RSPLVL_CDE       = 'DMM'                        03270008
              WITH UR                                                   03280008
           END-EXEC.                                                    03290008
                                                                        03300008
           PERFORM 8700-EVALUATE-SQLCODE.                               03310008
                                                                        03320008
      *----------------------------------------------------------------*03330000
      *    EVALUATE THE SQL RETURN CODE                                 03340000
      *----------------------------------------------------------------*03350000
       8700-EVALUATE-SQLCODE.                                           03360000
                                                                        03370000
           EVALUATE TRUE                                                03380000
             WHEN SQLCODE = ZEROS                                       03390000
               CONTINUE                                                 03400000
             WHEN OTHER                                                 03410000
               DISPLAY PV-ABEND-PARAGRAPH                               03420000
               PERFORM 9999-SQL-ERROR                                   03430000
           END-EVALUATE.                                                03440000
                                                                        03450000
      *----------------------------------------------------------------*03460000
      * 9900-EOJ-LOGIC.                                                 03470000
      * PERFORMED AT END OF INPUT FILE TO CLOSE FILES, CURSOR AND       03480000
      * DISPLAY RECORD COUNTS.                                          03490000
      *----------------------------------------------------------------*03500000
       9900-EOJ-LOGIC.                                                  03510000
                                                                        03520000
           CLOSE  GMM-INP-FILE                                          03530000
                  DMM-INP-FILE                                          03540000
                  GMM-OUT-FILE                                          03550000
                  DMM-OUT-FILE.                                         03560000
                                                                        03570000
           DISPLAY 'GMM RECS READ    ' PA-GMM-RECS-READ.                03580000
           DISPLAY 'DMM RECS READ    ' PA-DMM-RECS-READ.                03590000
           DISPLAY 'GMM RECS WRITTEN ' PA-GMM-RECS-WRITTEN.             03600000
           DISPLAY 'DMM RECS WRITTEN ' PA-DMM-RECS-WRITTEN.             03610000
           DISPLAY '**------ END HMKUT006 ------**'.                    03620000
                                                                        03630000
084900******************************************************************03640000
085000*  STANDARD DB2 ERROR ABEND ROUTINE                               03650000
085100*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             03660000
085200******************************************************************03670000
       9999-SQL-ERROR.                                                  03680000
143800                                                                  03690000
085600     MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            03700000
085700     DISPLAY '**  ABEND     **'.                                  03710000
085800     DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              03720000
085900     DISPLAY '**  PARAGRAPH **  = ' PV-ABEND-PARAGRAPH.           03730000
086100     DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           03740000
144200*                                                                 03750000
143700     COPY DPPD004.                                                03760000
144300     MOVE +4013                      TO PV-ABEND-CODE.            03770000
144400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                         03780001
144500                                                                  03790000
      *---------------------------------------------------------------* 03800000
      *    END OF SOURCE CODE FOR HMKUT006                            * 03810000
      *---------------------------------------------------------------* 03820000
