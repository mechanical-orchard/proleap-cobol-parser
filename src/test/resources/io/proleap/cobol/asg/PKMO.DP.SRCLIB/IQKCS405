000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    IQKCS405.                                                 
000500 AUTHOR.        GREG SHEFF.                                               
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  02-19-03.                                                 
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*---------------------------------------------------------------*         
001100*    I405 - MAJOR AND SUB CLASS LIST                            *         
001200*                                                               *         
001300*    THERE ARE TWO WAYS THIS SCREEN WILL DISPLAY THE MAJOR      *         
001400*        AND SUB CLASS LIST.  IT IS ONLY ACCESSED FROM          *         
001500*        IQLDEPT (IQKCS404) - DEPARTMENT LIST.                  *         
001600*                                                               *         
001700*        1 - DISPLAY A LIST FOR A SPECIFIC VENDOR LABEL /       *         
001800*            VENDOR NAME AND BUSINESS GROUP THAT WAS SELECTED   *         
001900*            ON IQKCS402 (IQLVEND).                             *         
002000*                                                               *         
002100*        2 - DISPLAY A LIST FOR ALL VENDORS.                    *         
002200*                                                               *         
002300*    ALLOWS THE USER TO SELECT ONE MAJOR CLASS AND SUB CLASS    *         
002400*    AND PASS IT ALONG WITH THE VENDOR NAME (IF SUPPLIED)       *         
002500*    TO ONE THE FOLLOWING SCREENS.                              *         
002600*                                                               *         
002700*        F17 - IQLSKU   (IQKCS408) - SELECT A MAJOR, SUB CLASS  *         
002800*              FORMAT CODE IS '1'  - LOCAL-01                   *         
002900*        F19 - IQLCOLOR (IQKCS406) - SELECT A MAJOR, SUB CLASS  *         
003000*              FORMAT CODE IS '1'  - LOCAL-02                   *         
003100*        F20 - IQLCOLOR (IQKCS406) - NO SELECTION ALLOWED       *         
003200*              FORMAT CODE IS SPACE - LOCAL-03                  *         
003300*              WORKS JUST LIKE GOING FROM IQLDEPT TO IQLCOLOR.  *         
003400*                                                               *         
003500*---------------------------------------------------------------*         
003510*---------------------------------------------------------------*         
003520* DATE:        09/14/2021                                                 
003530* WR/IR#:      CHG0261678                                                 
003540* PROGRAMMER:  GERMAN BANDA                                               
003550* DESCRIPTION: CHANGE CALL OF DPKUT100 TO                                 
003560*              CALL DP010I-NUMERIC-EDIT-ROUTINE                           
003570*              MAKING THE CALL DYNAMIC VS STATIC                          
003580*---------------------------------------------------------------*         
CICS  * CHANGED 10/31/2022    BY JIM HUNTER      WR: CHG0277565                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
003600                                                                          
003700 ENVIRONMENT DIVISION.                                                    
003800                                                                          
003900 DATA DIVISION.                                                           
004000                                                                          
004100 WORKING-STORAGE SECTION.                                                 
004200 01  DK-DB2-KEYS.                                                         
004300     15  DK-LBL-NM                      PIC  X(30)  VALUE SPACES.         
004400     15  DK-DEPT-NBR                    PIC S9(04)  COMP-3                
004500                                                    VALUE ZERO.           
004600                                                                          
004700 01  PC-PROGRAM-CONSTANTS.                                                
004800     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
004900                                                    'IQ405A  '.           
005000     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
005100                                                    'IQKM405 '.           
005200     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
005300                                                    'IQKCS405'.           
005400     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
005500     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
005600     05  PC-FORMAT-INDICATOR            PIC  X(01)  VALUE ' '.            
005700     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +11             
005800                                                    COMP SYNC.            
005900     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
006000                                                    COMP SYNC.            
006100     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
006200                                                    +1000000              
006300                                                    COMP-3.               
006400     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
006500                                                    COMP SYNC.            
006600     05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
006700                                                    COMP-3.               
006800*                                                                         
006900     05  PC-TSYMSG-NUMBERS.                                               
007000         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
007100         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
007200         10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
007300         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
007400         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
007500         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
007600         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
007700         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
007800         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
007900         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
008000         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
008100         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
008200         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
008300         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
008310         10  PC-TSYMSG-03106            PIC  9(05)  VALUE 03106.          
008400*                                                                         
008500 01  PS-PROGRAM-SWITCHES.                                                 
008600     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
008700         88  PS-NO-ERROR                            VALUE 'Y'.            
008800         88  PS-ERROR                               VALUE 'N'.            
008900     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
009000         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
009100         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
009200     05  PS-CURSOR-SW                   PIC  X(01)  VALUE ' '.            
009300         88  CSR-CLASS-VENDOR                       VALUE 'A'.            
009400         88  CSR-CLASS-NO-VENDOR                    VALUE 'B'.            
009500     05  PS-COUNT-CURSOR-SW             PIC  X(01)  VALUE ' '.            
009600         88  CNT-CLASS-VENDOR                       VALUE 'A'.            
009700         88  CNT-CLASS-LABEL                        VALUE 'B'.            
009800         88  CNT-CLASS-NO-VENDOR                    VALUE 'C'.            
009900*                                                                         
010000 01  PV-PROGRAM-VARIABLES.                                                
010100     05  PV-CURSOR-NAME                 PIC X(18)   VALUE SPACES.         
010200     05  PV-SUB                         PIC S9(04)  VALUE +0              
010300                                                    COMP SYNC.            
010400     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
010500                                                    COMP SYNC.            
010600     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
010700                                                    COMP-3.               
010800     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
010900                                                    COMP-3.               
011000     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
011100                                                    COMP-3.               
011200     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
011300                                                    COMP-3.               
011400     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
011500                                                    COMP-3.               
011600     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
011700                                                    COMP-3.               
011800     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
011900                                                    COMP SYNC.            
012000     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
012100                                                    COMP SYNC.            
012200     05  PV-ITEM                        PIC S9(04)  VALUE +0              
012300                                                    COMP SYNC.            
012400     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
012500                                                    COMP SYNC.            
012600     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
012700                                                    COMP SYNC.            
012800     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
012900                                                    COMP SYNC.            
013000     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
013100                                                    COMP SYNC.            
013200     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
013300                                                    COMP SYNC.            
013400     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
013500                                                    COMP SYNC.            
013600     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
013700                                                    COMP SYNC.            
013800     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
013900                                                    COMP SYNC.            
014000     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
014100                                                    COMP SYNC.            
014200     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
014300                                                    COMP SYNC.            
014400     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
014500                                                    COMP SYNC.            
014600     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
014700                                                    COMP SYNC.            
014800     05  PV-SKU-CNT                     PIC S9(09)  VALUE +0              
014900                                                    COMP-3.               
015000******************************************************************        
015100**  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
015200**  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
015300**  APPLICATION-SPECIFIC COMMAREA.                              **        
015400******************************************************************        
015500 01  LQW-LIST-QUEUE-WORK-AREA.                                            
015600     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
015700     05  LQW-ITEM-ENTRIES               OCCURS 11 TIMES                   
015800                                        INDEXED BY LQW-IDX.               
015900         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
016000         10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
016100         10  LQW-SELECTED-SW            PIC  X(01).                       
016200             88  LQW-SELECT                         VALUE 'S'.            
016300             88  LQW-DESELECT                       VALUE ' '.            
016400         10  LQW-MAJ-CL                 PIC  9(04).                       
016500         10  LQW-SUB-CL                 PIC  9(04).                       
016600         10  LQW-SUB-CL-NM              PIC  X(20).                       
016700                                                                          
016800     EJECT                                                                
016900*---------------------------------------------------------------*         
017000*  RECORD LAYOUT FOR THE SELECTION TEMP STORAGE QUEUE.                    
017100*---------------------------------------------------------------*         
017200     COPY IQWS014.                                                        
017300                                                                          
017400     EJECT                                                                
017500*---------------------------------------------------------------*         
017600*    MAP LAYOUT                                                 *         
017700*---------------------------------------------------------------*         
017800                                                                          
017900     COPY IQKM405.                                                        
018000*                                                                         
018100 01  MR-OCCURS-LAYOUT                   REDEFINES  IQ405AO.               
018200     05  FILLER                         PIC X(161).                       
018300     05  MR-SCROLL-AREA                 OCCURS 11 TIMES.                  
018400         10  MR-SELECTL                 PIC S9(04) COMP.                  
018500         10  MR-SELECTA                 PIC  X(01).                       
018600         10  MR-SELECTC                 PIC  X(01).                       
018700         10  MR-SELECTH                 PIC  X(01).                       
018800         10  MR-SELECTI                 PIC  X(01).                       
018900                                                                          
019000         10  MR-MAJ-CLL                 PIC S9(04) COMP.                  
019100         10  MR-MAJ-CLA                 PIC  X(01).                       
019200         10  MR-MAJ-CLC                 PIC  X(01).                       
019300         10  MR-MAJ-CLH                 PIC  X(01).                       
019400         10  MR-MAJ-CL.                                                   
019500             15  MR-MAJ-CL9             PIC  9(04).                       
019600                                                                          
019700         10  MR-SUB-CLL                 PIC S9(04) COMP.                  
019800         10  MR-SUB-CLA                 PIC  X(01).                       
019900         10  MR-SUB-CLC                 PIC  X(01).                       
020000         10  MR-SUB-CLH                 PIC  X(01).                       
020100         10  MR-SUB-CL.                                                   
020200             15  MR-SUB-CL9             PIC  9(04).                       
020300                                                                          
020400         10  MR-SUB-CL-NML              PIC S9(04) COMP.                  
020500         10  MR-SUB-CL-NMA              PIC  X(01).                       
020600         10  MR-SUB-CL-NMC              PIC  X(01).                       
020700         10  MR-SUB-CL-NMH              PIC  X(01).                       
020800         10  MR-SUB-CL-NM               PIC  X(20).                       
020900     EJECT                                                                
021000*---------------------------------------------------------------*         
021100*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
021200*---------------------------------------------------------------*         
021300                                                                          
021400     COPY DPWS015.                                                        
021500                                                                          
021600*---------------------------------------------------------------*         
021700*    FUNCTION KEYS COPYBOOK.                                    *         
021800*---------------------------------------------------------------*         
021900                                                                          
022000     COPY DPWS016.                                                        
022100                                                                          
022200*---------------------------------------------------------------*         
022300*    NUMERIC EDIT LAYOUT.                                       *         
022400*---------------------------------------------------------------*         
022500                                                                          
022600     COPY DPWS010I.                                                       
022700 EJECT                                                                    
022800*---------------------------------------------------------------*         
022900*    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
023000*---------------------------------------------------------------*         
023100                                                                          
023200     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
023300 EJECT                                                                    
023400*---------------------------------------------------------------*         
023500*    STANDARD COMMAREA.                                         *         
023600*---------------------------------------------------------------*         
023700                                                                          
023800     COPY DPWS020.                                                        
023900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
024000*---------------------------------------------------------------*         
024100* INTER-APPLICATION COMMAREA.                                   *         
024200*   IQWS016 - IS THE INPUT LAYOUT WHEN COMING FROM THE PREVIOUS *         
024300*             PROGARM.                                          *         
024400*   IQWS016 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
024500*             THIS LIST PROGRAM.                                *         
024600*---------------------------------------------------------------*         
024700                                                                          
024800         10  INTER-APPL-COMM-AREA       PIC X(200).                       
024900     COPY IQWS016.                                                        
025000*---------------------------------------------------------------*         
025100*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
025200*        CURRENT LENGTH IS 1000 FOR IQSCLASS MNEMONIC.                    
025300*        YOU MUST CHECK THE COMMAREA LENGTH FOR THE MNEMONIC              
025400*            IF YOU ADD ANY MORE FIELDS.                                  
025500*---------------------------------------------------------------*         
025600         10  ASC-SPECIFIC-COMMAREA.                                       
025700             15  ASC-LIST-KEYS.                                           
025800                 20  ASC-LBL-NM                      PIC  X(30).          
025900                 20  ASC-GP-NBR                      PIC  9(03).          
026000                 20  ASC-GP-DESC                     PIC  X(25).          
026100                 20  ASC-DEPT-NBR                    PIC  9(04).          
026200                 20  ASC-DEPT-DESC                   PIC  X(20).          
026300                 20  ASC-SKU-CNT                     PIC  9(09).          
026400             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
026500                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
026600                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
026700                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
026800                                                                          
026900                 20  ASC-ITEM-ARRAY.                                      
027000                     25  ASC-ITEM-ENTRIES OCCURS 11 TIMES.                
027100                         30  ASC-LIST-ITEM-NUMBER                         
027200                                                     PIC S9(09)           
027300                                                     COMP-3.              
027400                         30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
027500                             88  ASC-VALID-SELECT    VALUE ' '            
027600                                                           'S'            
027700                                                           'B'            
027800                                                           'E'.           
027900                             88  ASC-TEMP-NO-SELECT  VALUE ' '.           
028000                             88  ASC-TEMP-SELECT     VALUE 'S'.           
028100                             88  ASC-TEMP-BEGIN      VALUE 'B'.           
028200                             88  ASC-TEMP-END        VALUE 'E'.           
028300                         30  ASC-SELECTED-SW         PIC  X(01).          
028400                             88  ASC-NO-SELECT       VALUE ' '.           
028500                             88  ASC-SELECT          VALUE 'S'.           
028600                             88  ASC-BEGIN           VALUE 'B'.           
028700                             88  ASC-END             VALUE 'E'.           
028800                         30  ASC-MAJ-CL              PIC  9(04).          
028900                         30  ASC-SUB-CL              PIC  9(04).          
029000                         30  ASC-SUB-CL-NM           PIC  X(20).          
029100                                                                          
029200                                                                          
029300             15  ASC-BLK-SUB                         PIC S9(04)           
029400                                                          COMP            
029500                                                          SYNC.           
029600             15  ASC-ITEM-SUB                        PIC S9(04)           
029700                                                          COMP            
029800                                                          SYNC.           
029900             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
030000                                                          COMP-3.         
030100             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
030200                                                          COMP-3.         
030300             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
030400                                                          COMP-3.         
030500             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
030600                                                          COMP-3.         
030700             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
030800                                                          COMP-3.         
030900             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
031000                                                          COMP            
031100                                                          SYNC.           
031200             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
031300                                                          COMP-3.         
031400             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
031500                                                          COMP-3.         
031600             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
031700                 88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
031800                 88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
031900             15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
032000                 88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
032100                 88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
032200             15  ASC-LIST-QUEUE-NAME.                                     
032300                 20  ASC-LIST-TERM-ID                PIC  X(04).          
032400                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
032500                                                          COMP-3.         
032600                 20  ASC-LIST-SEQ                    PIC  9(01).          
032700             15  ASC-SEL-QUEUE-NAME.                                      
032800                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
032900                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
033000                                                          COMP-3.         
033100                 20  ASC-SEL-SEQ                     PIC  9(01).          
033200             15  ASC-START-ITEM-REQUEST.                                  
033300                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
033400             15  ASC-FILLER                          PIC  X(76).          
033500         10  FILLER                                  PIC  X(1865).        
033600     EJECT                                                                
033700*---------------------------------------------------------------*         
033800*    ABEND PROCESSING COPYBOOK.                                 *         
033900*---------------------------------------------------------------*         
034000                                                                          
034100     COPY DPWS013.                                                        
034200                                                                          
034300 EJECT                                                                    
034400*---------------------------------------------------------------*         
034500*    DB2 AREA FOR XST_SKU                                                 
034600*---------------------------------------------------------------*         
034700     EXEC SQL                                                             
034800          INCLUDE XST00010                                                
034900     END-EXEC.                                                            
035000*---------------------------------------------------------------*         
035100*    DB2 AREA FOR XST_VND_STYL                                  *         
035200*---------------------------------------------------------------*         
035300     EXEC SQL                                                             
035400          INCLUDE XST00013                                                
035500     END-EXEC.                                                            
035600*---------------------------------------------------------------*         
035700*    DB2 AREA FOR XST_SUB_CL                                    *         
035800*---------------------------------------------------------------*         
035900     EXEC SQL                                                             
036000          INCLUDE XST00014                                                
036100     END-EXEC.                                                            
036200*---------------------------------------------------------------*         
036300*    DB2 AREA FOR XST_VND                                       *         
036400*---------------------------------------------------------------*         
036500     EXEC SQL                                                             
036600          INCLUDE XST00018                                                
036700     END-EXEC.                                                            
036800*---------------------------------------------------------------*         
036900*    DB2 AREA FOR COMMUNICATIONS                                *         
037000*---------------------------------------------------------------*         
037100     EXEC SQL                                                             
037200          INCLUDE SQLCA                                                   
037300     END-EXEC.                                                            
037400                                                                          
037500*---------------------------------------------------------------*         
037600*   FETCH DEPARTMENT, CLASS AND DESCRIPTION                               
037700*   ORDER BY DEPARTMENT, MAJOR AND SUB CLASS                              
037800*---------------------------------------------------------------*         
037900                                                                          
038000     EXEC SQL                                                             
038100          DECLARE CLASS-CSR CURSOR FOR                                    
038200            SELECT  DISTINCT                                              
038300                    D.DEPT_NBR,                                           
038400                    D.MAJ_CL_NBR,                                         
038500                    D.SUB_CL_NBR,                                         
038600                    C.SUB_CL_NM                                           
038700              FROM  XST_SUB_CL    C,                                      
038800                    XST_VND_STYL  D,                                      
038900                    XST_VND       E                                       
039000               WHERE  E.ENT_NM_DESC = :DK-LBL-NM                          
039100                 AND  D.DEPT_NBR    = :DK-DEPT-NBR                        
039200                 AND  C.DEPT_NBR    = D.DEPT_NBR                          
039300                 AND  C.MAJ_CL_NBR  = D.MAJ_CL_NBR                        
039400                 AND  C.SUB_CL_NBR  = D.SUB_CL_NBR                        
039500                 AND  D.ENT_ID      = E.ENT_ID                            
039600            UNION                                                         
039700            SELECT  DISTINCT                                              
039800                D.DEPT_NBR,                                               
039900                D.MAJ_CL_NBR,                                             
040000                D.SUB_CL_NBR,                                             
040100                C.SUB_CL_NM                                               
040200             FROM  XST_VND_STYL  D,                                       
040300                   XST_SUB_CL    C                                        
040400               WHERE  D.LBL_NM     = :DK-LBL-NM                           
040500                 AND  D.DEPT_NBR   = :DK-DEPT-NBR                         
040600                 AND  C.DEPT_NBR   = D.DEPT_NBR                           
040700                 AND  C.MAJ_CL_NBR = D.MAJ_CL_NBR                         
040800                 AND  C.SUB_CL_NBR = D.SUB_CL_NBR                         
040900               ORDER BY 1, 2, 3                                           
041000     END-EXEC.                                                            
041100                                                                          
041200     EXEC SQL                                                             
041300          DECLARE CLASS-NO-VENDOR CURSOR FOR                              
041400            SELECT  DISTINCT                                              
041500                    VNDSTYL.DEPT_NBR                                      
041600                   ,VNDSTYL.MAJ_CL_NBR                                    
041700                   ,VNDSTYL.SUB_CL_NBR                                    
041800                   ,SUBCL.SUB_CL_NM                                       
041900              FROM  XST_SUB_CL    SUBCL                                   
042000                   ,XST_VND_STYL  VNDSTYL                                 
042100             WHERE  VNDSTYL.DEPT_NBR = :DK-DEPT-NBR                       
042200               AND  SUBCL.DEPT_NBR   = VNDSTYL.DEPT_NBR                   
042300               AND  SUBCL.MAJ_CL_NBR = VNDSTYL.MAJ_CL_NBR                 
042400               AND  SUBCL.SUB_CL_NBR = VNDSTYL.SUB_CL_NBR                 
042500     END-EXEC.                                                            
042600                                                                          
042700*   COUNT AVAILABLE SKU'S IF KNOW THE VENDOR NAME AND DEPT                
042800     EXEC SQL                                                             
042900        DECLARE CNTVND-CSR CURSOR FOR                                     
043000           SELECT  COUNT(*)                                               
043100             FROM  XST_VND_STYL  D,                                       
043200                   XST_VND       E,                                       
043300                   XST_SKU       F                                        
043400               WHERE  E.ENT_NM_DESC = :DK-LBL-NM                          
043500                 AND  D.DEPT_NBR    = :DK-DEPT-NBR                        
043600                 AND  D.STYL_ID    =  F.STYL_ID                           
043700                 AND  D.ENT_ID      = E.ENT_ID                            
043800     END-EXEC.                                                            
043900                                                                          
044000*   COUNT AVAILABLE SKU'S IF KNOW THE LABEL NAME AND DEPT                 
044100     EXEC SQL                                                             
044200        DECLARE CNTLBL-CSR CURSOR FOR                                     
044300           SELECT  COUNT(*)                                               
044400             FROM  XST_VND_STYL  D,                                       
044500                   XST_SKU       F                                        
044600               WHERE  D.LBL_NM     = :DK-LBL-NM                           
044700                 AND  D.DEPT_NBR   = :DK-DEPT-NBR                         
044800                 AND  D.STYL_ID    =  F.STYL_ID                           
044900     END-EXEC.                                                            
045000                                                                          
045100     EXEC SQL                                                             
045200        DECLARE CNT-NOVND-CSR CURSOR FOR                                  
045300           SELECT  COUNT(*)                                               
045400             FROM  XST_VND_STYL  D,                                       
045500                   XST_SKU       F                                        
045600            WHERE  D.DEPT_NBR    = :DK-DEPT-NBR                           
045700              AND  D.STYL_ID    =  F.STYL_ID                              
045800     END-EXEC.                                                            
045900                                                                          
046000                                                                          
046100 LINKAGE SECTION.                                                         
046200                                                                          
046300 01  DFHCOMMAREA.                                                         
046400     05  FILLER                         OCCURS  1 TO 4072 TIMES           
046500                                        DEPENDING ON EIBCALEN.            
046600         10  FILLER                     PIC X.                            
046700                                                                          
046800 EJECT                                                                    
046900 PROCEDURE DIVISION.                                                      
047000******************************************************************        
047100**  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
047200**  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
047300**  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
047400******************************************************************        
047500 0000-MAIN-MODULE.                                                        
047600     INITIALIZE DP030-CICS-API-FIELDS.                                    
047700     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
047800     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
047900     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
048000     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
048100     MOVE LENGTH OF IQ405AI        TO DP030-MAP-LENGTH (1).               
048200     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
048300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
048400*                                                                         
048500     PERFORM 1000-CONTROL-PROCESSING                                      
048600*                                                                         
048700     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
048800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
048900                                                                          
049000******************************************************************        
049100**  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
049200**  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
049300******************************************************************        
049400 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
049600                                    DFHCOMMAREA                           
049700                                    DP030-CICS-API-FIELDS                 
049800                                    DP020-STANDARD-COMMAREA               
049900                                    IQ405AI.                              
050000*                                                                         
050100     IF  DP030-RC-CALL-SUCCESSFUL                                         
050200         CONTINUE                                                         
050300     ELSE                                                                 
050400         SET DP013-NO-ROLLBACK                                            
050500             DP013-XCTL-DISPLAY-RESTART                                   
050600             DP013-CICS-ABEND      TO TRUE                                
050700         MOVE '0001-CALL-CICS-ARCH-API'                                   
050800                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
051100         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
051200         PERFORM DP013-0000-PROCESS-ABEND                                 
051300     END-IF.                                                              
051400*----------------------------------------------------------------*        
051500* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
051600* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
051700*                                                                *        
051800* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
051900*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
052000*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
052100*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
052200*----------------------------------------------------------------*        
052300                                                                          
052400 1000-CONTROL-PROCESSING.                                                 
052500                                                                          
052600     EVALUATE TRUE                                                        
052700         WHEN DP020-NEXT-ACT-INITIAL                                      
052800             INITIALIZE ASC-SPECIFIC-COMMAREA                             
052900             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
053000                                      ASC-SEL-TERM-ID                     
053100             MOVE DP020-SRC-TASK-NUMBER                                   
053200                                   TO ASC-LIST-TASK-NUMBER                
053300                                      ASC-SEL-TASK-NUMBER                 
053400             MOVE PC-LIST-QUEUE-SEQ                                       
053500                                   TO ASC-LIST-SEQ                        
053600             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
053700             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
053800***-----$TEMPLATE NOTE$-------------------------------------------        
053900*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
054000*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
054100*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
054200***---------------------------------------------------------------        
054300             IF  PS-COMMAREA-NOT-VALID                                    
054400                 SET DP020-NEXT-ACT-APPL-ERROR                            
054500                                   TO TRUE                                
054600             ELSE                                                         
054700                 PERFORM 4000-BUILD-INITIAL-PANEL                         
054800             END-IF                                                       
054900                                                                          
055000         WHEN DP020-NEXT-ACT-READ-MAP                                     
055100             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
055200                                   TO PV-TOP-ITEM                         
055300             PERFORM 2000-PROCESS-PANEL                                   
055400                                                                          
055500         WHEN DP020-NEXT-ACT-RETURN                                       
055600             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
055700                                   TO PV-TOP-ITEM                         
055800             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
055900             PERFORM 6500-REINIT-ASC-FLDS                                 
056000             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
056200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
056300                                                                          
056400         WHEN OTHER                                                       
056500             SET DP013-LOGIC-ABEND TO TRUE                                
056600             SET DP013-NO-ROLLBACK TO TRUE                                
056700             MOVE '1000-CONTROL-PROCESSING'                               
056800                                   TO DP013-PARAGRAPH                     
056900             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
057000                                   TO DP013-MESSAGE-TEXT(1)               
057100             PERFORM DP013-0000-PROCESS-ABEND                             
057200     END-EVALUATE.                                                        
057300                                                                          
057400 EJECT                                                                    
057500*----------------------------------------------------------------*        
057600* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
057700* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
057800*----------------------------------------------------------------*        
057900                                                                          
058000 1100-PROCESS-INTER-APPL-COMM.                                            
058100     SET PS-COMMAREA-VALID         TO TRUE.                               
058200     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
058300     IF  DP020-INT-APPL-FORMAT-IND = PC-FORMAT-INDICATOR                  
058400         MOVE IQ016-LBL-NM       TO ASC-LBL-NM                            
058500                                    DK-LBL-NM                             
058600         MOVE IQ016-GP-NBR       TO ASC-GP-NBR                            
058700         MOVE IQ016-GP-DESC      TO ASC-GP-DESC                           
058800         MOVE IQ016-DEPT-NBR     TO ASC-DEPT-NBR                          
058900                                    DK-DEPT-NBR                           
059000         MOVE IQ016-DEPT-DESC    TO ASC-DEPT-DESC                         
059100     ELSE                                                                 
059200         IF  DP020-NAV-KEY-DATA > SPACE                                   
059300             MOVE DP020-NAV-KEY-DATA TO                                   
059400                                    ASC-LBL-NM                            
059500                                    DK-LBL-NM                             
059600                                    ASC-DEPT-NBR                          
059700                                    DK-DEPT-NBR                           
059800         END-IF                                                           
059900     END-IF.                                                              
060000                                                                          
060100 EJECT                                                                    
060200*----------------------------------------------------------------*        
060300* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
060400* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
060500* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
060600* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
060700* FUNCTION KEYS CAN GET THIS FAR.                                *        
060800*----------------------------------------------------------------*        
060900                                                                          
061000 2000-PROCESS-PANEL.                                                      
061100     EVALUATE TRUE                                                        
061200         WHEN DP020-SRC-AID = DP016-CLEAR                                 
061300             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
061400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
061500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
061600                                                                          
061700         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
061800             MOVE ASC-LBL-NM   TO DK-LBL-NM                               
061900             MOVE ASC-DEPT-NBR TO DK-DEPT-NBR                             
062000                                                                          
062100             PERFORM 2050-DELETE-LIST-QUEUE                               
062200             PERFORM 4000-BUILD-INITIAL-PANEL                             
062300                                                                          
062400         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
062500             PERFORM 2140-DESELECT-ALL-ITEMS                              
062600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
062700                                                                          
062800         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
062900             PERFORM 2150-SELECT-ALL-ITEMS                                
063000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
063100                                                                          
063200         WHEN OTHER                                                       
063300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
063400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
063500             IF  PS-NO-ERROR                                              
063600                 SET DP030-CONTINUE-GO                                    
063700                                 TO TRUE                                  
063800                 PERFORM 2100-CHECK-FUNCTION-KEY                          
063900             ELSE                                                         
064000                 SET DP030-OVERRIDE-GO                                    
064100                                 TO TRUE                                  
064200             END-IF                                                       
064300     END-EVALUATE.                                                        
064400 EJECT                                                                    
064500*----------------------------------------------------------------*        
064600*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
064700*----------------------------------------------------------------*        
064800 2050-DELETE-LIST-QUEUE.                                                  
064900                                                                          
065000     EXEC CICS                                                            
065100         DELETEQ TS                                                       
065200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
065300             RESP  (PV-CICS-RESP)                                         
065400     END-EXEC.                                                            
065500                                                                          
065600     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
065700        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
065800         CONTINUE                                                         
065900     ELSE                                                                 
066000         SET DP013-CICS-ABEND      TO TRUE                                
066100         SET DP013-NO-ROLLBACK     TO TRUE                                
066200                                                                          
066300         MOVE '2050-DELETE-LIST-QUEUE'                                    
066400                                   TO DP013-PARAGRAPH                     
066500         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
066600                                   TO DP013-MESSAGE-TEXT (1)              
066700         PERFORM DP013-0000-PROCESS-ABEND                                 
066800     END-IF.                                                              
066900                                                                          
067000     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
067100 EJECT                                                                    
067200*----------------------------------------------------------------*        
067300* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
067400* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
067500* CICS ARCHITECTURE API.                                         *        
067600*----------------------------------------------------------------*        
067700 2100-CHECK-FUNCTION-KEY.                                                 
067800     EVALUATE TRUE                                                        
067900*        LOCAL-01   SKU LIST  IQLSKU   - F17                              
068000*        LOCAL-02   COLORLST  IQLCOLOR - F19                              
068100         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
068200         WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
068300             IF  ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                      
068400*                --  A LIST ITEM MUST BE SELECTED --                      
068500                 MOVE PC-TSYMSG-00057                                     
068600                                   TO DP020-MSG-NUMBER                    
068700                 SET DP020-MSG-FATAL                                      
068800                     DP030-OVERRIDE-GO                                    
068900                                   TO TRUE                                
069000             ELSE                                                         
069100               IF  ASC-NUMBER-OF-SELECTED-ITEMS > 1                       
069200*                --  ONLY ONE LIST ITEM MAY BE SELECTED --                
069300                   MOVE PC-TSYMSG-00047                                   
069400                                   TO DP020-MSG-NUMBER                    
069500                   SET DP020-MSG-FATAL                                    
069600                       DP030-OVERRIDE-GO                                  
069700                                   TO TRUE                                
069800               ELSE                                                       
069900                 MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        
070000                                TO IQ016-NUMBER-OF-SELECTED-ITEMS         
070100                 PERFORM 5000-PREPARE-SEL-QUEUE                           
070200               END-IF                                                     
070300             END-IF                                                       
070400                                                                          
070500         WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
070510*        LOCAL-03   COLORLST  IQLCOLOR - F20                              
070600             IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                      
070700*                --  NO LIST ITEM CAN BE SELECTED --                      
070800                 MOVE PC-TSYMSG-03106                                     
070900                                   TO DP020-MSG-NUMBER                    
071000                 SET DP020-MSG-FATAL                                      
071100                     DP030-OVERRIDE-GO                                    
071200                                   TO TRUE                                
071800             END-IF                                                       
071900                                                                          
072000         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
072100             MOVE +1               TO PV-TOP-ITEM                         
072200             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
072300                                                                          
072400         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
072500             PERFORM 2110-BROWSE-BACKWARD                                 
072600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
072700                                                                          
072800         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
072900             PERFORM 2120-BROWSE-FORWARD                                  
073000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
073100                                                                          
073200         WHEN DP020-SRC-AID = DP016-ENTER                                 
073300             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
073400             IF  PS-ERROR                                                 
073500                 CONTINUE                                                 
073600             ELSE                                                         
073700                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
073800             END-IF                                                       
073900                                                                          
074000         WHEN OTHER                                                       
074100             SET DP013-NO-ROLLBACK                                        
074200                 DP013-XCTL-DISPLAY-RESTART                               
074300                 DP013-CICS-ABEND  TO TRUE                                
074400             MOVE '2100-CHECK-FUNCTION-KEY'                               
074500                                   TO DP013-PARAGRAPH                     
074600             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
074700                                   TO DP013-MESSAGE-TEXT (1)              
074800             PERFORM DP013-0000-PROCESS-ABEND                             
074900     END-EVALUATE.                                                        
075000*                                                                         
075100*----------------------------------------------------------------*        
075200*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
075300*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
075400*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
075500*----------------------------------------------------------------*        
075600                                                                          
075700 2110-BROWSE-BACKWARD.                                                    
075800     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
075900         GIVING PV-TOP-ITEM.                                              
076000*                                                                         
076100     IF  ((PV-TOP-ITEM < +1)                                              
076200       OR (PV-TOP-ITEM = +1))                                             
076300         MOVE +1                   TO PV-TOP-ITEM                         
076400*        -- TOP OF LIST --                                                
076500         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
076600         SET DP020-MSG-INFORMATIONAL TO TRUE                              
076700     END-IF.                                                              
076800                                                                          
076900 EJECT                                                                    
077000*----------------------------------------------------------------*        
077100*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
077200*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
077300*    PAST THE END, ISSUE THE BOTTOM OF LIST                      *        
077400*----------------------------------------------------------------*        
077500                                                                          
077600 2120-BROWSE-FORWARD.                                                     
077700                                                                          
077800     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
077900         GIVING PV-TOP-ITEM.                                              
078000     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
078100         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
078200                                   TO PV-TOP-ITEM                         
078300*        -- BOTTOM OF LIST --                                             
078400         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
078500         SET DP020-MSG-INFORMATIONAL                                      
078600                                   TO TRUE                                
078700     ELSE                                                                 
078800         COMPUTE PV-BOTTOM-ITEM =                                         
078900             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
079000         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
079100*            -- BOTTOM OF LIST --                                         
079200             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
079300             SET DP020-MSG-INFORMATIONAL                                  
079400                                   TO TRUE                                
079500         END-IF                                                           
079600     END-IF.                                                              
079700 EJECT                                                                    
079800*----------------------------------------------------------------*        
079900*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
080000*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
080100*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
080200*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
080300*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
080400*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
080500*----------------------------------------------------------------*        
080600                                                                          
080700 2130-CHECK-FOR-JUMP-BROWSE.                                              
080800     SET PS-NO-ERROR               TO TRUE.                               
080900     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
081000         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
081100                                   TO PV-TOP-ITEM                         
081200     ELSE                                                                 
081300         MOVE ASC-START-ITEM-REQUEST                                      
081400                                   TO DP010I-UNEDITED-FIELD               
081500         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
081600         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
081700         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
081800                                   TO TRUE                                
081910         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
081920                         USING DP010I-NUMERIC-EDIT-AREA                   
082000                                                                          
082100         IF  NOT DP010I-ERROR-DETECTED                                    
082200             MOVE DP010I-NUMERIC-FIELD                                    
082300                                   TO  ASC-START-ITEM-REQUEST-N           
082400             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
082500                 IF  ASC-START-ITEM-REQUEST-N >                           
082600                                          ASC-NUMBER-OF-ITEMS-READ        
082700                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
082800                     COMPUTE PV-TOP-ITEM =                                
082900                             ASC-NUMBER-OF-ITEMS-READ                     
083000                             - PC-ITEMS-PER-PANEL + 1                     
083100                     IF  PV-TOP-ITEM < +1                                 
083200                         MOVE +1   TO PV-TOP-ITEM                         
083300                         MOVE PC-TSYMSG-00070                             
083400                                   TO DP020-MSG-NUMBER                    
083500                         SET DP020-MSG-INFORMATIONAL                      
083600                                   TO TRUE                                
083700                     END-IF                                               
083800                 ELSE                                                     
083900                     MOVE ASC-START-ITEM-REQUEST-N                        
084000                                   TO PV-TOP-ITEM                         
084100                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
084200                     COMPUTE PV-BOTTOM-ITEM =                             
084300                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
084400                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
084500*                        -- BOTTOM OF LIST --                             
084600                         MOVE PC-TSYMSG-00070                             
084700                                   TO DP020-MSG-NUMBER                    
084800                         SET DP020-MSG-INFORMATIONAL                      
084900                                   TO TRUE                                
085000                     END-IF                                               
085100                 END-IF                                                   
085200             ELSE                                                         
085300*                --- VALUE OUT OF RANGE ----                              
085400                 MOVE PC-TSYMSG-00101                                     
085500                                   TO DP020-MSG-NUMBER                    
085600                 MOVE -1           TO ASTRTITL                            
085700                 SET DP030-SET-CURSOR-APPL-1                              
085800                                   TO TRUE                                
085900                 MOVE DP015-REVERSE                                       
086000                                   TO ASTRTITH                            
086100                 MOVE DP015-RED    TO ASTRTITC                            
086200                 MOVE DP015-UNP-NUM-BRT-MDT                               
086300                                   TO ASTRTITA                            
086400                 SET DP020-MSG-FATAL                                      
086500                                   TO TRUE                                
086600                 SET PS-ERROR      TO TRUE                                
086700             END-IF                                                       
086800                                                                          
086900         ELSE                                                             
087000             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
087100             MOVE -1               TO ASTRTITL                            
087200             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
087300             MOVE DP015-REVERSE    TO ASTRTITH                            
087400             MOVE DP015-RED        TO ASTRTITC                            
087500             MOVE DP015-UNP-NUM-BRT-MDT                                   
087600                                   TO ASTRTITA                            
087700             SET DP020-MSG-FATAL   TO TRUE                                
087800             SET PS-ERROR          TO TRUE                                
087900         END-IF                                                           
088000     END-IF.                                                              
088100 EJECT                                                                    
088200*----------------------------------------------------------------*        
088300*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
088400*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
088500*----------------------------------------------------------------*        
088600 2140-DESELECT-ALL-ITEMS.                                                 
088700     MOVE +1                       TO ASC-BLK-SUB.                        
088800                                                                          
088900     PERFORM 2141-CLEAR-SELECTED-INDS                                     
089000         VARYING ASC-ITEM-SUB                                             
089100         FROM 1 BY 1                                                      
089200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
089300                                                                          
089400     ADD +1                        TO ASC-BLK-SUB.                        
089500                                                                          
089600     PERFORM 2141-CLEAR-SELECTED-INDS                                     
089700         VARYING ASC-ITEM-SUB                                             
089800         FROM 1 BY 1                                                      
089900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
090000                                                                          
090100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
090200                                   TO PV-LIST-ITEM-NUMBER.                
090300                                                                          
090400     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
090500         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
090600                                                                          
090700     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
090800        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
090900         MOVE +1                   TO ASC-BLK-SUB                         
091000                                                                          
091100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
091200                                                                          
091300         MOVE +1                   TO PV-BLOCK-NUMBER                     
091400                                                                          
091500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
091600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
091700                                                                          
091800         PERFORM                                                          
091900             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
092000                                                                          
092100             PERFORM 4410-READ-TEMP-STORAGE                               
092200                                                                          
092300             PERFORM 2141-CLEAR-SELECTED-INDS                             
092400                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
092500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
092600                                                                          
092700             PERFORM 5100-WRITE-TEMP-STORAGE                              
092800                                                                          
092900             ADD +1                TO PV-BLOCK-NUMBER                     
093000                                                                          
093100         END-PERFORM                                                      
093200                                                                          
093300         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
093400             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
093500                                                                          
093600         PERFORM                                                          
093700             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
093800                   ASC-HIGHEST-BLOCK-WRITTEN                              
093900                                                                          
094000             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
094100                                   TO PV-BLOCK-NUMBER                     
094200                                                                          
094300             PERFORM 4410-READ-TEMP-STORAGE                               
094400                                                                          
094500             PERFORM 2141-CLEAR-SELECTED-INDS                             
094600                 VARYING ASC-ITEM-SUB                                     
094700                 FROM 1 BY 1                                              
094800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
094900                                                                          
095000             PERFORM 5100-WRITE-TEMP-STORAGE                              
095100                                                                          
095200             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
095300                                                                          
095400         END-PERFORM                                                      
095500                                                                          
095600         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
095700                                   TO PV-BLOCK-NUMBER                     
095800                                                                          
095900         PERFORM 4410-READ-TEMP-STORAGE                                   
096000     END-IF.                                                              
096100                                                                          
096200                                                                          
096300     MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS.         
096400     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
096500     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
096600                                                                          
096700     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
096800                                                                          
096900 EJECT                                                                    
097000*----------------------------------------------------------------*        
097100*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
097200*----------------------------------------------------------------*        
097300                                                                          
097400 2141-CLEAR-SELECTED-INDS.                                                
097500                                                                          
097600     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
097700             NOT EQUAL ZERO                                               
097800         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
097900             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
098000                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
098100                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
098200                                   TO TRUE                                
098300         END-IF                                                           
098400     END-IF.                                                              
098500                                                                          
098600 EJECT                                                                    
098700*----------------------------------------------------------------*        
098800*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
098900*  FOR ALL ITEMS.                                                *        
099000*----------------------------------------------------------------*        
099100                                                                          
099200 2150-SELECT-ALL-ITEMS.                                                   
099300                                                                          
099400     MOVE +1                       TO ASC-BLK-SUB.                        
099500                                                                          
099600     PERFORM 2151-SET-SELECTED-INDS                                       
099700         VARYING ASC-ITEM-SUB                                             
099800         FROM 1 BY 1                                                      
099900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
100000                                                                          
100100     ADD +1                        TO ASC-BLK-SUB.                        
100200                                                                          
100300     PERFORM 2151-SET-SELECTED-INDS                                       
100400         VARYING ASC-ITEM-SUB                                             
100500         FROM 1 BY 1                                                      
100600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
100700                                                                          
100800     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
100900                                   TO PV-LIST-ITEM-NUMBER.                
101000                                                                          
101100     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
101200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
101300                                                                          
101400     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
101500        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
101600         MOVE +1                   TO ASC-BLK-SUB                         
101700                                                                          
101800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
101900                                                                          
102000         MOVE +1                   TO PV-BLOCK-NUMBER                     
102100                                                                          
102200         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
102300             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
102400                                                                          
102500         PERFORM                                                          
102600             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
102700                                                                          
102800             PERFORM 4410-READ-TEMP-STORAGE                               
102900                                                                          
103000             PERFORM 2151-SET-SELECTED-INDS                               
103100                 VARYING ASC-ITEM-SUB                                     
103200                 FROM 1 BY 1                                              
103300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
103400                                                                          
103500             PERFORM 5100-WRITE-TEMP-STORAGE                              
103600                                                                          
103700             ADD +1                TO PV-BLOCK-NUMBER                     
103800                                                                          
103900         END-PERFORM                                                      
104000                                                                          
104100         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
104200             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
104300                                                                          
104400         PERFORM                                                          
104500             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
104600                   ASC-HIGHEST-BLOCK-WRITTEN                              
104700                                                                          
104800             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
104900                                   TO PV-BLOCK-NUMBER                     
105000                                                                          
105100             PERFORM 4410-READ-TEMP-STORAGE                               
105200                                                                          
105300             PERFORM 2151-SET-SELECTED-INDS                               
105400                 VARYING ASC-ITEM-SUB                                     
105500                 FROM 1 BY 1                                              
105600                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
105700                                                                          
105800             PERFORM 5100-WRITE-TEMP-STORAGE                              
105900                                                                          
106000             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
106100                                                                          
106200         END-PERFORM                                                      
106300                                                                          
106400         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
106500                                   TO PV-BLOCK-NUMBER                     
106600                                                                          
106700         PERFORM 4410-READ-TEMP-STORAGE                                   
106800     END-IF.                                                              
106900                                                                          
107000                                                                          
107100     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
107200                       ASC-NUMBER-OF-SELECTED-ITEMS.                      
107300                                                                          
107400     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
107500                                                                          
107600 EJECT                                                                    
107700*----------------------------------------------------------------*        
107800*    MARK EACH ITEM AS BEING SELECTED.                           *        
107900*----------------------------------------------------------------*        
108000                                                                          
108100 2151-SET-SELECTED-INDS.                                                  
108200                                                                          
108300     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
108400             NOT EQUAL ZERO                                               
108500         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
108600             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
108700                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
108800                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
108900                               TO  TRUE                                   
109000         END-IF                                                           
109100     END-IF.                                                              
109200******************************************************************        
109300** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
109400** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
109500******************************************************************        
109600 2200-MOVE-SCREEN-TO-COMMAREA.                                            
109700                                                                          
109800     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
109900     MOVE +1                       TO PV-SUB.                             
110000*                                                                         
110100     PERFORM 3100-SET-SUBSCRIPTS.                                         
110200     IF  ASTRTITL > ZERO                                                  
110300         MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
110400     ELSE                                                                 
110500         IF  ASTRTITF = DP015-ERASE-EOF                                   
110600             INITIALIZE ASC-START-ITEM-REQUEST                            
110700         END-IF                                                           
110800     END-IF.                                                              
110900     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
111000         VARYING PV-SUB                                                   
111100         FROM 1 BY 1                                                      
111200         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
111300******************************************************************        
111400** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
111500******************************************************************        
111600 2210-MOVE-LINES-TO-COMMAREA.                                             
111700     IF  MR-SELECTL (PV-SUB) > ZERO                                       
111800         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
111900         MOVE MR-SELECTI (PV-SUB)                                         
112000                                   TO ASC-TEMP-SELECTED-SW                
112100                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
112200     ELSE                                                                 
112300         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
112400                 SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
112500                 MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
112600                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
112700         END-IF                                                           
112800     END-IF.                                                              
112900     ADD +1                        TO ASC-ITEM-SUB.                       
113000     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
113100         ADD +1                    TO ASC-BLK-SUB                         
113200         MOVE +1                   TO ASC-ITEM-SUB                        
113300     END-IF.                                                              
113400******************************************************************        
113500** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
113600** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
113700******************************************************************        
113800 3000-EDIT-DATA-IN-COMMAREA.                                              
113900     SET PS-NO-ERROR               TO TRUE.                               
114000     PERFORM 3200-RESET-ATTRIBUTES.                                       
114100     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
114200**                                                                        
114300* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
114400* EDITTING IS DONE FROM THE BOTTOM UP.                                    
114500**                                                                        
114600     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
114700         ASC-ITEMS-DISPLAYED - 1.                                         
114800     PERFORM 3100-SET-SUBSCRIPTS.                                         
114900     PERFORM 3300-EDIT-SELECTIONS                                         
115000         VARYING PV-SUB                                                   
115100         FROM PV-SUB BY -1                                                
115200         UNTIL PV-SUB < +1                                                
115300*                                                                         
115400     IF  PS-NO-ERROR                                                      
115500         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
115600     END-IF.                                                              
115700*                                                                         
115800     IF  ((PS-NO-ERROR)                                                   
115900       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
116000       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
116100             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
116200     END-IF.                                                              
116300 EJECT                                                                    
116400*----------------------------------------------------------------*        
116500*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
116600*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
116700*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
116800*                                                                *        
116900*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
117000*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
117100*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
117200*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
117300*----------------------------------------------------------------*        
117400                                                                          
117500 3100-SET-SUBSCRIPTS.                                                     
117600                                                                          
117700     COMPUTE ASC-ITEM-SUB =                                               
117800         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
117900                                                                          
118000     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
118100         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
118200                                                                          
118300         MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
118400     ELSE                                                                 
118500         MOVE +1                     TO ASC-BLK-SUB                       
118600     END-IF.                                                              
118700******************************************************************        
118800** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
118900******************************************************************        
119000                                                                          
119100 3200-RESET-ATTRIBUTES.                                                   
119200     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
119300     MOVE DP015-GREEN              TO ASTRTITC.                           
119400     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
119500*                                                                         
119600     PERFORM                                                              
119700         VARYING PV-SUB                                                   
119800         FROM 1 BY 1                                                      
119900         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
120000         MOVE DP015-UNP-ALP-NOR-OFF                                       
120100                                   TO MR-SELECTA (PV-SUB)                 
120200         MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
120300         MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
120400     END-PERFORM.                                                         
120500******************************************************************        
120600** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
120700** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
120800** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
120900** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
121000******************************************************************        
121100 3300-EDIT-SELECTIONS.                                                    
121200     EVALUATE TRUE                                                        
121300         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
121400             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
121500                 CONTINUE                                                 
121600         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
121700             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
121800                 ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
121900                 MOVE ASC-TEMP-SELECTED-SW                                
122000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
122100                                TO ASC-SELECTED-SW                        
122200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
122300             END-IF                                                       
122400         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
122500                 = SPACE                                                  
122600             EVALUATE TRUE                                                
122700                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
122800                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
122900                     MOVE SPACE TO ASC-SELECTED-SW                        
123000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
123100                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
123200                     MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
123300                     MOVE SPACE TO ASC-SELECTED-SW                        
123400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
123500                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
123600                     MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
123700                     MOVE SPACE TO ASC-SELECTED-SW                        
123800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
123900             END-EVALUATE                                                 
124000         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
124100             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
124200                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
124300             END-IF                                                       
124400             PERFORM 3310-EDIT-RANGE                                      
124500         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
124600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
124700                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
124800             END-IF                                                       
124900             PERFORM 3310-EDIT-RANGE                                      
125000         WHEN OTHER                                                       
125100             MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
125200             SET DP020-MSG-FATAL    TO TRUE                               
125300             MOVE -1                TO MR-SELECTL (PV-SUB)                
125400             SET DP030-SET-CURSOR-APPL-1                                  
125500                                    TO TRUE                               
125600             MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
125700             MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
125800             SET PS-ERROR TO TRUE                                         
125900             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
126000                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
126100                 MOVE SPACE         TO ASC-SELECTED-SW                    
126200                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
126300             END-IF                                                       
126400     END-EVALUATE.                                                        
126500                                                                          
126600     SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
126700     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
126800     IF  ASC-ITEM-SUB < 1                                                 
126900         SUBTRACT +1 FROM ASC-BLK-SUB                                     
127000         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
127100     END-IF.                                                              
127200******************************************************************        
127300** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
127400** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
127500** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
127600** THE ENDING.                                                  **        
127700******************************************************************        
127800 3310-EDIT-RANGE.                                                         
127900     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
128000         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
128100             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
128200                                   TO ASC-SELECTED-SW                     
128300                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
128400             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
128500         ELSE                                                             
128600             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
128700                 CONTINUE                                                 
128800             ELSE                                                         
128900*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
129000                 MOVE PC-TSYMSG-00054                                     
129100                                   TO DP020-MSG-NUMBER                    
129200                 SET DP020-MSG-FATAL                                      
129300                                   TO TRUE                                
129400                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
129500                 SET DP030-SET-CURSOR-APPL-1                              
129600                                   TO TRUE                                
129700                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
129800                 MOVE DP015-UNDERLINE                                     
129900                                   TO MR-SELECTH (PV-SUB)                 
130000                 SET PS-ERROR      TO TRUE                                
130100             END-IF                                                       
130200         END-IF                                                           
130300     ELSE                                                                 
130400         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
130500             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
130600                                   TO ASC-SELECTED-SW                     
130700                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
130800             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
130900         ELSE                                                             
131000             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
131100                 CONTINUE                                                 
131200             ELSE                                                         
131300*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
131400                 MOVE PC-TSYMSG-00055                                     
131500                                   TO DP020-MSG-NUMBER                    
131600                 SET DP020-MSG-FATAL                                      
131700                                   TO TRUE                                
131800                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
131900                 SET DP030-SET-CURSOR-APPL-1                              
132000                                   TO TRUE                                
132100                 SET PS-ERROR      TO TRUE                                
132200             END-IF                                                       
132300         END-IF                                                           
132400     END-IF.                                                              
132500*                                                                         
132600******************************************************************        
132700** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
132800** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
132900******************************************************************        
133000 3400-CHECK-RANGE-SEQUENCE.                                               
133100     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
133200       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
133300       AND  ASC-START-OF-SELECTED-RANGE >                                 
133400                                         ASC-END-OF-SELECTED-RANGE        
133500*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
133600         MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
133700         SET DP020-MSG-FATAL TO TRUE                                      
133800         SET PS-ERROR TO TRUE                                             
133900         PERFORM 3410-POSITION-AT-ERROR                                   
134000     END-IF.                                                              
134100******************************************************************        
134200** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
134300** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
134400** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
134500******************************************************************        
134600 3410-POSITION-AT-ERROR.                                                  
134700     SET DP030-SET-CURSOR-APPL-1                                          
134800                                   TO TRUE.                               
134900     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
135000                    - PV-TOP-ITEM + 1.                                    
135100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
135200         CONTINUE                                                         
135300     ELSE                                                                 
135400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
135500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
135600         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
135700     END-IF.                                                              
135800*                                                                         
135900     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
136000                    - PV-TOP-ITEM + 1.                                    
136100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
136200         CONTINUE                                                         
136300     ELSE                                                                 
136400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
136500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
136600         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
136700     END-IF.                                                              
136800 EJECT                                                                    
136900*----------------------------------------------------------------*        
137000*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
137100*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
137200*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
137300*----------------------------------------------------------------*        
137400                                                                          
137500 3500-MARK-RANGE-AS-SELECTED.                                             
137600     MOVE +1                       TO  ASC-BLK-SUB.                       
137700     PERFORM 3510-MARK-SELECTED-INDS                                      
137800         VARYING ASC-ITEM-SUB                                             
137900         FROM 1 BY 1                                                      
138000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
138100                                                                          
138200     ADD +1                        TO ASC-BLK-SUB.                        
138300     PERFORM 3510-MARK-SELECTED-INDS                                      
138400         VARYING ASC-ITEM-SUB                                             
138500         FROM 1 BY 1                                                      
138600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
138700                                                                          
138800     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
138900                                   TO  PV-LIST-ITEM-NUMBER.               
139000     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
139100         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
139200               PC-ITEMS-PER-PANEL).                                       
139300                                                                          
139400     MOVE ASC-START-OF-SELECTED-RANGE                                     
139500                                   TO PV-START-OF-SELECTED-RANGE.         
139600     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
139700         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
139800               PC-ITEMS-PER-PANEL).                                       
139900                                                                          
140000     MOVE ASC-END-OF-SELECTED-RANGE                                       
140100                                   TO  PV-END-OF-SELECTED-RANGE.          
140200     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
140300         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
140400               PC-ITEMS-PER-PANEL).                                       
140500     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
140600         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
140700                                                                          
140800     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
140900        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
141000         MOVE +1                   TO ASC-BLK-SUB                         
141100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
141200         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
141300             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
141400                                                                          
141500         PERFORM                                                          
141600             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
141700                   PV-PREV-BLOCK-IN-ARRAY                                 
141800             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
141900                                   TO PV-BLOCK-NUMBER                     
142000             PERFORM 4410-READ-TEMP-STORAGE                               
142100             PERFORM 3510-MARK-SELECTED-INDS                              
142200                 VARYING ASC-ITEM-SUB                                     
142300                 FROM 1 BY 1                                              
142400                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
142500             PERFORM 5100-WRITE-TEMP-STORAGE                              
142600             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
142700         END-PERFORM                                                      
142800                                                                          
142900         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
143000             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
143100         PERFORM                                                          
143200             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
143300                   PV-LAST-BLOCK-IN-RANGE                                 
143400             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
143500                                   TO PV-BLOCK-NUMBER                     
143600             PERFORM 4410-READ-TEMP-STORAGE                               
143700             PERFORM 3510-MARK-SELECTED-INDS                              
143800                 VARYING ASC-ITEM-SUB                                     
143900                 FROM 1 BY 1                                              
144000                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
144100             PERFORM 5100-WRITE-TEMP-STORAGE                              
144200             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
144300         END-PERFORM                                                      
144400                                                                          
144500         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
144600                                   TO PV-BLOCK-NUMBER                     
144700         PERFORM 4410-READ-TEMP-STORAGE                                   
144800     END-IF.                                                              
144900                                                                          
145000     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
145100                                      ASC-END-OF-SELECTED-RANGE.          
145200                                                                          
145300 EJECT                                                                    
145400*----------------------------------------------------------------*        
145500* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
145600*----------------------------------------------------------------*        
145700                                                                          
145800 3510-MARK-SELECTED-INDS.                                                 
145900                                                                          
146000     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
146100                                ASC-ITEM-SUB) >                           
146200         ASC-START-OF-SELECTED-RANGE)                                     
146300     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
146400                                ASC-ITEM-SUB) =                           
146500         ASC-START-OF-SELECTED-RANGE)                                     
146600                                                                          
146700         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
146800                                    ASC-ITEM-SUB) <                       
146900             ASC-END-OF-SELECTED-RANGE)                                   
147000         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
147100                                    ASC-ITEM-SUB) =                       
147200             ASC-END-OF-SELECTED-RANGE)                                   
147300                                                                          
147400             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
147500                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
147600                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
147700                               TO TRUE                                    
147800                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
147900                               TO TRUE                                    
148000                 ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
148100             END-IF                                                       
148200         END-IF                                                           
148300     END-IF.                                                              
148400                                                                          
148500 EJECT                                                                    
148600*----------------------------------------------------------------*        
148700*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
148800*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
148900*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
149000*    DISPLAY THE PANEL TO USER.                                  *        
149100*----------------------------------------------------------------*        
149200                                                                          
149300 4000-BUILD-INITIAL-PANEL.                                                
149400                                                                          
149500     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
149600                                 ASC-BLOCK-ENTRIES (2).                   
149700     MOVE +1                 TO ASC-BLK-SUB.                              
149800     SET ASC-ITEMS-LEFT-ON-DB                                             
149900                             TO TRUE.                                     
150000                                                                          
150100     MOVE +0                 TO ASC-ITEM-SUB                              
150200                                ASC-NUMBER-OF-ITEMS-READ                  
150300                                ASC-NUMBER-OF-SELECTED-ITEMS              
150400                                ASC-HIGHEST-BLOCK-WRITTEN                 
150500                                ASC-START-OF-SELECTED-RANGE               
150600                                ASC-END-OF-SELECTED-RANGE                 
150700                                ASC-ITEM-AT-TOP-OF-PANEL.                 
150800*--------------------------------------------------------------*          
150900*    FOLLOWING LOGIC DETERMINES WHAT CURSOR TO USE TO RETRIEVE *          
151000*        THE DATA.  THIS SCREEN CAN ONLY BE GOTTEN TO FROM     *          
151100*        IQLDEPT - IQKCS404 - DEPARTMENT LIST SCREEN.          *          
151200*                                                              *          
151300*    THE VENDOR NAME IS EITHER SPACES OR HAS A VALUE AND THAT  *          
151400*        IS USED BY THE FOLLOWING LOGIC TO DETERMINE WHAT      *          
151500*        CURSOR TO USE.                                        *          
151600*                                                              *          
151700*--------------------------------------------------------------*          
151800                                                                          
151900     EVALUATE DK-LBL-NM                                                   
152000         WHEN SPACES                                                      
152100              SET CNT-CLASS-NO-VENDOR TO TRUE                             
152200              MOVE 'CNT-NOVND-CSR     ' TO PV-CURSOR-NAME                 
152300              PERFORM 4011-OPEN-COUNT-CURSOR                              
152400              PERFORM 4350-FETCH-COUNTS                                   
152500              PERFORM 4021-CLOSE-COUNT-CURSOR                             
152600                                                                          
152700              MOVE PV-SKU-CNT TO ASC-SKU-CNT                              
152800              MOVE ZEROES     TO PV-SKU-CNT                               
152900                                                                          
153000              SET CSR-CLASS-NO-VENDOR TO TRUE                             
153100              MOVE 'CLASS-NO-VENDOR   ' TO PV-CURSOR-NAME                 
153200                                                                          
153300         WHEN OTHER                                                       
153400              SET CNT-CLASS-VENDOR TO TRUE                                
153500              MOVE 'CNTVND-CSR        ' TO PV-CURSOR-NAME                 
153600              PERFORM 4011-OPEN-COUNT-CURSOR                              
153700              PERFORM 4350-FETCH-COUNTS                                   
153800              PERFORM 4021-CLOSE-COUNT-CURSOR                             
153900                                                                          
154000              MOVE PV-SKU-CNT TO ASC-SKU-CNT                              
154100              MOVE ZEROES     TO PV-SKU-CNT                               
154200                                                                          
154300              SET CNT-CLASS-LABEL  TO TRUE                                
154400              MOVE 'CNTLBL-CSR        ' TO PV-CURSOR-NAME                 
154500              PERFORM 4011-OPEN-COUNT-CURSOR                              
154600              PERFORM 4350-FETCH-COUNTS                                   
154700              PERFORM 4021-CLOSE-COUNT-CURSOR                             
154800                                                                          
154900              ADD  PV-SKU-CNT TO ASC-SKU-CNT                              
155000                                                                          
155100              SET CSR-CLASS-VENDOR TO TRUE                                
155200              MOVE 'CLASS-CSR         ' TO PV-CURSOR-NAME                 
155300                                                                          
155400     END-EVALUATE                                                         
155500                                                                          
155600     PERFORM 4010-OPEN-DATA-CURSOR.                                       
155700                                                                          
155800     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
155900                                                                          
156000     PERFORM 4020-CLOSE-CURSOR.                                           
156100                                                                          
156200     MOVE +1                   TO  PV-TOP-ITEM.                           
156300     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
156400         SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
156500         SET DP020-MSG-FATAL   TO  TRUE                                   
156600         MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
156700     ELSE                                                                 
156800         MOVE -1               TO MR-SELECTL (1)                          
156900         SET DP030-SET-CURSOR-APPL-1                                      
157000                               TO TRUE                                    
157100         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
157200     END-IF.                                                              
157300                                                                          
157400*----------------------------------------------------------------*        
157500* OPEN ONE OF THE DATA CURSORS.                                           
157600*----------------------------------------------------------------*        
157700                                                                          
157800 4010-OPEN-DATA-CURSOR.                                                   
157900                                                                          
158000     MOVE '4010-OPEN-DATA-CURSOR'  TO DP013-PARAGRAPH.                    
158100                                                                          
158200     PERFORM                                                              
158300         WITH TEST AFTER                                                  
158400         VARYING PV-RETRY-COUNTER                                         
158500         FROM 1 BY 1                                                      
158600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
158700                   OR (SQLCODE = ZERO))                                   
158800                                                                          
158900         EVALUATE TRUE                                                    
159000             WHEN CSR-CLASS-NO-VENDOR                                     
159100                  EXEC SQL                                                
159200                       OPEN CLASS-NO-VENDOR                               
159300                  END-EXEC                                                
159400                                                                          
159500             WHEN OTHER                                                   
159600                  EXEC SQL                                                
159700                       OPEN CLASS-CSR                                     
159800                  END-EXEC                                                
159900                                                                          
160000         END-EVALUATE                                                     
160100                                                                          
160200         EVALUATE TRUE                                                    
160300             WHEN SQLCODE = ZERO                                          
160400             WHEN SQLCODE = -904                                          
160500             WHEN SQLCODE = -913                                          
160600                 CONTINUE                                                 
160700             WHEN SQLWARN0 NOT = SPACES                                   
160800             WHEN SQLCODE < ZERO                                          
160900             WHEN SQLCODE > ZERO                                          
161000                 STRING 'OPEN '                                           
161100                         PV-CURSOR-NAME                                   
161200                        ' CURSOR'                                         
161300                         DELIMITED BY SIZE                                
161400                         INTO DP013-MESSAGE-TEXT (1)                      
161500                 MOVE SQLCA        TO DP013-SQLCA                         
161600                 MOVE 'SUB_CL, VND'                                       
161700                                   TO DP013-DB2-TABLE-NAME (1)            
161800                 MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)            
161900                 SET DP013-DB2-ABEND                                      
162000                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
162100                 PERFORM DP013-0000-PROCESS-ABEND                         
162200         END-EVALUATE                                                     
162300                                                                          
162400     END-PERFORM.                                                         
162500                                                                          
162600     IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
162700        STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '             
162800                PV-CURSOR-NAME                                            
162900               ' CURSOR'                                                  
163000                DELIMITED BY SIZE                                         
163100                INTO DP013-MESSAGE-TEXT (1)                               
163200         MOVE SQLCA                TO DP013-SQLCA                         
163300         MOVE 'SUB_CL, VND' TO DP013-DB2-TABLE-NAME (1)                   
163400         MOVE 'VND_STYL'    TO DP013-DB2-TABLE-NAME (2)                   
163500         SET DP013-DB2-ABEND                                              
163600             DP013-XCTL-DISPLAY-RESTART                                   
163700                                   TO TRUE                                
163800         PERFORM DP013-0000-PROCESS-ABEND                                 
163900     END-IF.                                                              
163910                                                                          
163920*----------------------------------------------------------------*        
163930*    OPEN ONE OF THE COUNT CURSORS USED TO PROVIDE AVAILABLE UPCS.        
163940*----------------------------------------------------------------*        
164000                                                                          
164100 4011-OPEN-COUNT-CURSOR.                                                  
164200                                                                          
164300     MOVE '4011-OPEN-COUNT-CURSOR'   TO DP013-PARAGRAPH.                  
164400                                                                          
164500     PERFORM                                                              
164600         WITH TEST AFTER                                                  
164700         VARYING PV-RETRY-COUNTER                                         
164800         FROM 1 BY 1                                                      
164900         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
165000                   OR (SQLCODE = ZERO))                                   
165100                                                                          
165200         EVALUATE TRUE                                                    
165300             WHEN CNT-CLASS-NO-VENDOR                                     
165400                  EXEC SQL                                                
165500                       OPEN CNT-NOVND-CSR                                 
165600                  END-EXEC                                                
165700                                                                          
165800             WHEN CNT-CLASS-VENDOR                                        
165900                  EXEC SQL                                                
166000                       OPEN CNTVND-CSR                                    
166100                  END-EXEC                                                
166200                                                                          
166300             WHEN CNT-CLASS-LABEL                                         
166400                  EXEC SQL                                                
166500                       OPEN CNTLBL-CSR                                    
166600                  END-EXEC                                                
166700                                                                          
166800             WHEN OTHER                                                   
166900                  CONTINUE                                                
167000                                                                          
167100         END-EVALUATE                                                     
167200                                                                          
167300         EVALUATE TRUE                                                    
167400             WHEN SQLCODE = ZERO                                          
167500             WHEN SQLCODE = -904                                          
167600             WHEN SQLCODE = -913                                          
167700                 CONTINUE                                                 
167800             WHEN SQLWARN0 NOT = SPACES                                   
167900             WHEN SQLCODE < ZERO                                          
168000             WHEN SQLCODE > ZERO                                          
168100                 STRING 'OPEN '                                           
168200                         PV-CURSOR-NAME                                   
168300                        ' CURSOR'                                         
168400                         DELIMITED BY SIZE                                
168500                         INTO DP013-MESSAGE-TEXT (1)                      
168600                 MOVE SQLCA        TO DP013-SQLCA                         
168700                 MOVE 'SKU, VND' TO DP013-DB2-TABLE-NAME (1)              
168800                 MOVE 'VND_STYL' TO DP013-DB2-TABLE-NAME (2)              
168900                 SET DP013-DB2-ABEND                                      
169000                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
169100                 PERFORM DP013-0000-PROCESS-ABEND                         
169200         END-EVALUATE                                                     
169300                                                                          
169400     END-PERFORM.                                                         
169500*                                                                         
169600     IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
169700        STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN '             
169800                PV-CURSOR-NAME                                            
169900               ' CURSOR'                                                  
170000                DELIMITED BY SIZE                                         
170100                INTO DP013-MESSAGE-TEXT (1)                               
170200         MOVE SQLCA                TO DP013-SQLCA                         
170300         MOVE 'SKU, VND ' TO DP013-DB2-TABLE-NAME (1)                     
170400         MOVE 'VND_STYL ' TO DP013-DB2-TABLE-NAME (2)                     
170500         SET DP013-DB2-ABEND                                              
170600             DP013-XCTL-DISPLAY-RESTART                                   
170700                                   TO TRUE                                
170800         PERFORM DP013-0000-PROCESS-ABEND                                 
170900     END-IF.                                                              
171000                                                                          
171100*----------------------------------------------------------------*        
171200* CLOSE ONE OF THE DATA CURSORS.                                          
171300*----------------------------------------------------------------*        
171400                                                                          
171500 4020-CLOSE-CURSOR.                                                       
171600                                                                          
171700     EVALUATE TRUE                                                        
171800         WHEN CSR-CLASS-NO-VENDOR                                         
171900              EXEC SQL                                                    
172000                   CLOSE CLASS-NO-VENDOR                                  
172100              END-EXEC                                                    
172200                                                                          
172300         WHEN OTHER                                                       
172400              EXEC SQL                                                    
172500                   CLOSE CLASS-CSR                                        
172600              END-EXEC                                                    
172700                                                                          
172800     END-EVALUATE.                                                        
172900                                                                          
173000     EVALUATE TRUE                                                        
173100         WHEN SQLCODE = ZERO                                              
173200             CONTINUE                                                     
173300         WHEN SQLWARN0 NOT = SPACES                                       
173400         WHEN SQLCODE < ZERO                                              
173500         WHEN SQLCODE > ZERO                                              
173600             MOVE '4020-CLOSE-CURSOR'                                     
173700                               TO DP013-PARAGRAPH                         
173800             STRING 'CLOSE '                                              
173900                     PV-CURSOR-NAME                                       
174000                    ' CURSOR'                                             
174100                     DELIMITED BY SIZE                                    
174200                     INTO DP013-MESSAGE-TEXT (1)                          
174300             MOVE SQLCA        TO DP013-SQLCA                             
174400             MOVE 'SUB_CL, VND'                                           
174500                               TO DP013-DB2-TABLE-NAME (1)                
174600             MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)                
174700             SET DP013-DB2-ABEND                                          
174800                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
174900             PERFORM DP013-0000-PROCESS-ABEND                             
175000     END-EVALUATE.                                                        
175100                                                                          
175200*----------------------------------------------------------------*        
175300*    CLOSE ONE OF THE COUNT CURSORS.                                      
175400*----------------------------------------------------------------*        
175500                                                                          
175600 4021-CLOSE-COUNT-CURSOR.                                                 
175700                                                                          
175800     EVALUATE TRUE                                                        
175900         WHEN CNT-CLASS-NO-VENDOR                                         
176000              EXEC SQL                                                    
176100                   CLOSE CNT-NOVND-CSR                                    
176200              END-EXEC                                                    
176300                                                                          
176400         WHEN CNT-CLASS-VENDOR                                            
176500              EXEC SQL                                                    
176600                   CLOSE CNTVND-CSR                                       
176700              END-EXEC                                                    
176800                                                                          
176900         WHEN CNT-CLASS-LABEL                                             
177000              EXEC SQL                                                    
177100                   CLOSE CNTLBL-CSR                                       
177200              END-EXEC                                                    
177300                                                                          
177400         WHEN OTHER                                                       
177500              CONTINUE                                                    
177600                                                                          
177700     END-EVALUATE.                                                        
177800                                                                          
177900     EVALUATE TRUE                                                        
178000         WHEN SQLCODE = ZERO                                              
178100             CONTINUE                                                     
178200         WHEN SQLWARN0 NOT = SPACES                                       
178300         WHEN SQLCODE < ZERO                                              
178400         WHEN SQLCODE > ZERO                                              
178500             MOVE '4020-CLOSE-CURSOR'                                     
178600                               TO DP013-PARAGRAPH                         
178700             STRING 'CLOSE '                                              
178800                     PV-CURSOR-NAME                                       
178900                    ' CURSOR'                                             
179000                     DELIMITED BY SIZE                                    
179100                     INTO DP013-MESSAGE-TEXT (1)                          
179200             MOVE SQLCA        TO DP013-SQLCA                             
179300             MOVE 'SUB_CL, VND'                                           
179400                               TO DP013-DB2-TABLE-NAME (1)                
179500             MOVE 'VND_STYL'   TO DP013-DB2-TABLE-NAME (2)                
179600             SET DP013-DB2-ABEND                                          
179700                 DP013-XCTL-DISPLAY-RESTART TO TRUE                       
179800             PERFORM DP013-0000-PROCESS-ABEND                             
179900     END-EVALUATE.                                                        
180000                                                                          
180100                                                                          
180200*----------------------------------------------------------------*        
180300*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
180400*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
180500*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
180600*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
180700*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
180800*                                                                *        
180900*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
181000*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
181100*    UPON INITIALIZATION.                                        *        
181200*----------------------------------------------------------------*        
181300                                                                          
181400 4300-READ-ITEMS-FROM-DB.                                                 
181500                                                                          
181600     PERFORM                                                              
181700         VARYING PV-READ-COUNT                                            
181800         FROM 1 BY 1                                                      
181900         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
182000            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
182100                                                                          
182200         PERFORM 4310-FETCH                                               
182300                                                                          
182400         IF  ASC-ITEMS-LEFT-ON-DB                                         
182500             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
182600                 ADD +1            TO ASC-ITEM-SUB                        
182700             ELSE                                                         
182800                 ADD +1            TO ASC-BLK-SUB                         
182900                 MOVE +1           TO ASC-ITEM-SUB                        
183000                                                                          
183100                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
183200                     MOVE +1       TO ASC-BLK-SUB                         
183300                     SET ASC-BLOCK-MODIFIED (1)                           
183400                                   TO TRUE                                
183500                     PERFORM 5100-WRITE-TEMP-STORAGE                      
183600                     MOVE ASC-BLOCK-ENTRIES                               
183700                               (PC-MAX-BLOCKS-IN-ARRAY)                   
183800                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
183900                     ADD +1        TO ASC-BLK-SUB                         
184000                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
184100                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
184200                                   TO TRUE                                
184300                 END-IF                                                   
184400             END-IF                                                       
184500             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
184600             MOVE ASC-NUMBER-OF-ITEMS-READ                                
184700                                   TO ASC-LIST-ITEM-NUMBER                
184800                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
184900             MOVE XST00013-MAJ-CL-NBR  TO ASC-MAJ-CL                      
185000                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
185100             MOVE XST00013-SUB-CL-NBR  TO ASC-SUB-CL                      
185200                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
185300             MOVE XST00014-SUB-CL-NM   TO ASC-SUB-CL-NM                   
185400                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
185500         END-IF                                                           
185600     END-PERFORM.                                                         
185700                                                                          
185800     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
185900         MOVE +1               TO ASC-BLK-SUB                             
186000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
186100         ADD +1                TO ASC-BLK-SUB                             
186200         PERFORM 5100-WRITE-TEMP-STORAGE                                  
186300                                                                          
186400         MOVE 1                TO ASC-BLK-SUB                             
186500                                  PV-BLOCK-NUMBER                         
186600         PERFORM 4410-READ-TEMP-STORAGE                                   
186700         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
186800             MOVE 2            TO ASC-BLK-SUB                             
186900                                  PV-BLOCK-NUMBER                         
187000             PERFORM 4410-READ-TEMP-STORAGE                               
187100         END-IF                                                           
187200     END-IF.                                                              
187300 EJECT                                                                    
187400*----------------------------------------------------------------*        
187500*    FETCH THE CLASS CURSOR.                                              
187600*----------------------------------------------------------------*        
187700                                                                          
187800 4310-FETCH.                                                              
187900                                                                          
188000     MOVE '4310-FETCH-CURSOR'  TO DP013-PARAGRAPH.                        
188100                                                                          
188200     PERFORM                                                              
188300         WITH TEST AFTER                                                  
188400         VARYING PV-RETRY-COUNTER                                         
188500         FROM 1 BY 1                                                      
188600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
188700                   OR  SQLCODE = ZERO                                     
188710                   OR  SQLCODE = +100)                                    
188800         EVALUATE TRUE                                                    
188900             WHEN CSR-CLASS-NO-VENDOR                                     
189000                  EXEC SQL                                                
189100                       FETCH CLASS-NO-VENDOR                              
189200                             INTO                                         
189300                              :XST00013-DEPT-NBR                          
189400                             ,:XST00013-MAJ-CL-NBR                        
189500                             ,:XST00013-SUB-CL-NBR                        
189600                             ,:XST00014-SUB-CL-NM                         
189700                  END-EXEC                                                
189800                                                                          
189900             WHEN OTHER                                                   
190000                  EXEC SQL                                                
190100                       FETCH CLASS-CSR                                    
190200                             INTO                                         
190300                              :XST00013-DEPT-NBR                          
190400                             ,:XST00013-MAJ-CL-NBR                        
190500                             ,:XST00013-SUB-CL-NBR                        
190600                             ,:XST00014-SUB-CL-NM                         
190700                  END-EXEC                                                
190800                                                                          
190900         END-EVALUATE                                                     
191000                                                                          
191100         EVALUATE TRUE                                                    
191200             WHEN SQLCODE = ZERO                                          
191300             WHEN SQLCODE = +100                                          
191400                  CONTINUE                                                
191500             WHEN SQLWARN0 NOT = SPACE                                    
191600             WHEN SQLCODE NOT = ZERO                                      
191700                  STRING 'FETCH '                                         
191800                          PV-CURSOR-NAME                                  
191900                         ' CURSOR'                                        
192000                          DELIMITED BY SIZE                               
192100                          INTO DP013-MESSAGE-TEXT (1)                     
192200                  MOVE SQLCA       TO DP013-SQLCA                         
192300                  SET DP013-DB2-ABEND                                     
192400                                   TO TRUE                                
192500                  PERFORM DP013-0000-PROCESS-ABEND                        
192600         END-EVALUATE                                                     
192700                                                                          
192800     END-PERFORM.                                                         
192900                                                                          
193000     IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
193100        STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '            
193200                PV-CURSOR-NAME                                            
193300               ' CURSOR'                                                  
193400                DELIMITED BY SIZE                                         
193500                INTO DP013-MESSAGE-TEXT (1)                               
193600         MOVE SQLCA                TO DP013-SQLCA                         
193700         SET DP013-DB2-ABEND                                              
193800             DP013-XCTL-DISPLAY-RESTART                                   
193900                                   TO TRUE                                
194000         PERFORM DP013-0000-PROCESS-ABEND                                 
194100     END-IF.                                                              
194200                                                                          
194300     IF  SQLCODE EQUAL +100                                               
194400         SET ASC-NO-ITEMS-LEFT-ON-DB                                      
194500                                   TO TRUE                                
194600     END-IF.                                                              
194700                                                                          
194800 EJECT                                                                    
194900*----------------------------------------------------------------*        
195000*    COUNT AVAILABLE UPCS                                        *        
195100*----------------------------------------------------------------*        
195200                                                                          
195300 4350-FETCH-COUNTS.                                                       
195400                                                                          
195410     MOVE '4350-FETCH-COUNTS'  TO DP013-PARAGRAPH.                        
195500                                                                          
195510     PERFORM                                                              
195520         WITH TEST AFTER                                                  
195530         VARYING PV-RETRY-COUNTER                                         
195540         FROM 1 BY 1                                                      
195550         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
195560                   OR  SQLCODE = ZERO                                     
195570                   OR  SQLCODE = +100)                                    
195580                                                                          
195600         EVALUATE TRUE                                                    
195700             WHEN CNT-CLASS-NO-VENDOR                                     
195800                  EXEC SQL                                                
195900                      FETCH CNT-NOVND-CSR                                 
196000                             INTO                                         
196100                              :PV-SKU-CNT                                 
196200                  END-EXEC                                                
196300                                                                          
196400             WHEN CNT-CLASS-VENDOR                                        
196500                  EXEC SQL                                                
196600                      FETCH CNTVND-CSR                                    
196700                             INTO                                         
196800                              :PV-SKU-CNT                                 
196900                  END-EXEC                                                
197000                                                                          
197100             WHEN CNT-CLASS-LABEL                                         
197200                  EXEC SQL                                                
197300                      FETCH CNTLBL-CSR                                    
197400                             INTO                                         
197500                              :PV-SKU-CNT                                 
197600                  END-EXEC                                                
197700                                                                          
197800             WHEN OTHER                                                   
197900                  CONTINUE                                                
198000                                                                          
198100         END-EVALUATE                                                     
198200                                                                          
198300         EVALUATE TRUE                                                    
198400             WHEN SQLCODE = ZERO                                          
198500             WHEN SQLCODE = +100                                          
198600                  CONTINUE                                                
198700                                                                          
198800             WHEN SQLWARN0 NOT = SPACE                                    
198900             WHEN SQLCODE NOT = ZERO                                      
199000                  STRING 'FETCH '                                         
199100                          PV-CURSOR-NAME                                  
199200                         ' CURSOR'                                        
199300                          DELIMITED BY SIZE                               
199400                          INTO DP013-MESSAGE-TEXT (1)                     
199500                  MOVE SQLCA       TO DP013-SQLCA                         
199600                  SET DP013-DB2-ABEND                                     
199700                                   TO TRUE                                
199800                  PERFORM DP013-0000-PROCESS-ABEND                        
199900         END-EVALUATE                                                     
199910                                                                          
199920     END-PERFORM.                                                         
199930                                                                          
199940     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
199971         STRING 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO FETCH '           
199972                 PV-CURSOR-NAME                                           
199973                ' CURSOR'                                                 
199974                 DELIMITED BY SIZE                                        
199975                 INTO DP013-MESSAGE-TEXT (1)                              
199980         MOVE SQLCA                TO DP013-SQLCA                         
199991         SET DP013-DB2-ABEND                                              
199992             DP013-XCTL-DISPLAY-RESTART                                   
199993                                   TO TRUE                                
199994         PERFORM DP013-0000-PROCESS-ABEND                                 
199995     END-IF.                                                              
200100                                                                          
200200*----------------------------------------------------------------*        
200300* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
200400*----------------------------------------------------------------*        
200500                                                                          
200600 4400-MOVE-COMMAREA-TO-SCREEN.                                            
200700                                                                          
200800     IF ASC-LBL-NM = SPACES                                               
200900        MOVE 'ALL' TO AVENDNMO                                            
201000     ELSE                                                                 
201100        MOVE ASC-LBL-NM TO AVENDNMO                                       
201200     END-IF.                                                              
201300                                                                          
201400     MOVE ASC-GP-NBR          TO ABUSGRPO.                                
201500     MOVE ASC-GP-DESC         TO ABUSDESO.                                
201600     MOVE ASC-SKU-CNT         TO ASKUO.                                   
201700     MOVE ASC-DEPT-NBR        TO ADEPTNOO.                                
201800     MOVE ASC-DEPT-DESC       TO ADPTDESO.                                
201900                                                                          
202000     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
202100         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
202200         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
202300             MOVE +1    TO ASC-BLK-SUB                                    
202400             PERFORM 5100-WRITE-TEMP-STORAGE                              
202500             ADD +1     TO ASC-BLK-SUB                                    
202600             PERFORM 5100-WRITE-TEMP-STORAGE                              
202700             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
202800                 GIVING PV-BLOCK-NUMBER                                   
202900                 REMAINDER PV-REMAINDER                                   
203000             IF  PV-REMAINDER > ZERO                                      
203100                 ADD +1 TO PV-BLOCK-NUMBER                                
203200             END-IF                                                       
203300             MOVE +1    TO ASC-BLK-SUB                                    
203400             PERFORM 4410-READ-TEMP-STORAGE                               
203500             ADD +1     TO ASC-BLK-SUB                                    
203600                           PV-BLOCK-NUMBER                                
203700             PERFORM 4410-READ-TEMP-STORAGE                               
203800     END-IF.                                                              
203900     MOVE +1            TO ASC-BLK-SUB.                                   
204000     COMPUTE ASC-ITEM-SUB =                                               
204100         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
204200         + PC-ITEMS-PER-PANEL).                                           
204300*                                                                         
204400     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
204500         ADD +1         TO ASC-BLK-SUB                                    
204600         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
204700     END-IF.                                                              
204800*                                                                         
204900     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
205000         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
205100     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
205200     PERFORM 4420-FORMAT-LIST-LINES                                       
205300         VARYING PV-SUB                                                   
205400         FROM PC-ITEMS-PER-PANEL BY -1                                    
205500         UNTIL PV-SUB < +1.                                               
205600*                                                                         
205700     COMPUTE ASC-ITEMS-DISPLAYED =                                        
205800         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
205900     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
206000                                      ASTRTITO.                           
206100     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
206200     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
206300*                                                                         
206400     IF  DP020-MSG-NUMBER-X = SPACE                                       
206500         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
206600*            --- MORE ITEMS TO DISPLAY ----                               
206700             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
206800             SET DP020-MSG-INFORMATIONAL TO TRUE                          
206900         END-IF                                                           
207000     END-IF.                                                              
207100*----------------------------------------------------------------*        
207200*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
207300*----------------------------------------------------------------*        
207400                                                                          
207500 4410-READ-TEMP-STORAGE.                                                  
207600                                                                          
207700     EXEC CICS                                                            
207800         READQ TS                                                         
207900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
208000             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
208100             ITEM  (PV-BLOCK-NUMBER)                                      
208200             RESP  (PV-CICS-RESP)                                         
208300     END-EXEC.                                                            
208400                                                                          
208500     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
208600       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
208700         CONTINUE                                                         
208800     ELSE                                                                 
208900         SET DP013-CICS-ABEND     TO TRUE                                 
209000         SET DP013-NO-ROLLBACK    TO TRUE                                 
209100         MOVE '4410-READ-TEMP-STORAGE'                                    
209200                                  TO DP013-PARAGRAPH                      
209300         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
209400                                  TO DP013-MESSAGE-TEXT(1)                
209500         PERFORM DP013-0000-PROCESS-ABEND                                 
209600     END-IF.                                                              
209700                                                                          
209800     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
209900*                                                                         
210000     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
210100         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
210200     END-IF.                                                              
210300 EJECT                                                                    
210400*----------------------------------------------------------------*        
210500*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
210600*----------------------------------------------------------------*        
210700                                                                          
210800 4420-FORMAT-LIST-LINES.                                                  
210900     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
211000         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
211100         MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)                   
211200         MOVE DP015-HL-OFF       TO MR-SELECTH (PV-SUB)                   
211300         MOVE SPACE              TO MR-SELECTI (PV-SUB)                   
211400                                    MR-MAJ-CL (PV-SUB)                    
211500                                    MR-SUB-CL (PV-SUB)                    
211600                                    MR-SUB-CL-NM (PV-SUB)                 
211700     ELSE                                                                 
211800         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
211900                                                   > SPACE                
212000             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
212100                                 TO MR-SELECTI (PV-SUB)                   
212200         ELSE                                                             
212300             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
212400                                 TO MR-SELECTI (PV-SUB)                   
212500         END-IF                                                           
212600*                                                                         
212700         IF  DP030-SET-CURSOR-TRAILER                                     
212800             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
212900                 > SPACE)                                                 
213000               OR (PV-SUB = 1))                                           
213100                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
213200                MOVE -1          TO MR-SELECTL (PV-SUB)                   
213300             END-IF                                                       
213400         END-IF                                                           
213500*                                                                         
213600         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
213700                                                                          
213800             MOVE ASC-MAJ-CL (ASC-BLK-SUB ASC-ITEM-SUB)                   
213900                                 TO MR-MAJ-CL (PV-SUB)                    
214000             MOVE ASC-SUB-CL (ASC-BLK-SUB ASC-ITEM-SUB)                   
214100                                 TO MR-SUB-CL (PV-SUB)                    
214200             MOVE ASC-SUB-CL-NM (ASC-BLK-SUB ASC-ITEM-SUB)                
214300                                 TO MR-SUB-CL-NM (PV-SUB)                 
214400         END-IF                                                           
214500     END-IF.                                                              
214600     SUBTRACT +1 FROM ASC-ITEM-SUB                                        
214700     IF  ASC-ITEM-SUB < + 1                                               
214800         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
214900         MOVE +1                 TO ASC-BLK-SUB                           
215000     END-IF.                                                              
215100******************************************************************        
215200** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
215300** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
215400** ISSUE A WARNING.                                             **        
215500******************************************************************        
215600 4430-CHECK-FOR-BEGIN-AND-END.                                            
215700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
215800         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
215900*            -- NO END-OF-RANGE SPECFIED ---                              
216000             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
216100             SET DP020-MSG-WARNING TO TRUE                                
216200             PERFORM 4431-POSITION-AT-WARNING                             
216300         END-IF                                                           
216400     ELSE                                                                 
216500         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
216600*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
216700             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
216800             SET DP020-MSG-WARNING TO TRUE                                
216900             PERFORM 4431-POSITION-AT-WARNING                             
217000         END-IF                                                           
217100     END-IF.                                                              
217200******************************************************************        
217300** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
217400** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
217500** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
217600** POSITION THE CURSOR THERE.                                   **        
217700******************************************************************        
217800 4431-POSITION-AT-WARNING.                                                
217900     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
218000                    - PV-TOP-ITEM + 1.                                    
218100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
218200         CONTINUE                                                         
218300     ELSE                                                                 
218400         SET DP030-SET-CURSOR-APPL-1                                      
218500                                   TO TRUE                                
218600         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
218700         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
218800         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
218900     END-IF.                                                              
219000*                                                                         
219100     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
219200                    - PV-TOP-ITEM + 1.                                    
219300     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
219400         CONTINUE                                                         
219500     ELSE                                                                 
219600         SET DP030-SET-CURSOR-APPL-1                                      
219700                                   TO TRUE                                
219800         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
219900         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
220000         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
220100     END-IF.                                                              
220200 EJECT                                                                    
220300*----------------------------------------------------------------*        
220400* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
220500* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
220600* FOR THE SELECTED ITEMS QUEUE.                                  *        
220700*                                                                *        
220800* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
220900* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
221000* THE ARRAY IS FULL.                                             *        
221100*----------------------------------------------------------------*        
221200                                                                          
221300 5000-PREPARE-SEL-QUEUE.                                                  
221400                                                                          
221500     MOVE +1                   TO  ASC-BLK-SUB.                           
221600     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
221700     ADD +1                    TO  ASC-BLK-SUB.                           
221800     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
221900                                                                          
222000     MOVE ASC-SEL-QUEUE-NAME   TO  IQ016-SEL-QUEUE-NAME.                  
222100     PERFORM 6010-DELETE-SEL-QUEUE.                                       
222200                                                                          
222300     SET ASC-UPDATE-TSQ        TO  TRUE.                                  
222400                                                                          
222500     SET IQ014-IDX             TO  1.                                     
222600     MOVE ZERO                 TO IQ016-NUMBER-OF-TS-ITEMS.               
222700     INITIALIZE IQ014-SELECTED-ITEM-ARRAY.                                
222800                                                                          
222900     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
223000             VARYING PV-ITEM                                              
223100             FROM 1 BY 1                                                  
223200             UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.             
223300                                                                          
223400     IF  IQ014-IDX > 1                                                    
223500         PERFORM 5205-WRITE-SEL-QUEUE                                     
223600         ADD +1                TO IQ016-NUMBER-OF-TS-ITEMS                
223700     END-IF.                                                              
223800 EJECT                                                                    
223900*----------------------------------------------------------------*        
224000*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
224100*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
224200*----------------------------------------------------------------*        
224300                                                                          
224400 5100-WRITE-TEMP-STORAGE.                                                 
224500     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
224600                                   TO PV-LIST-ITEM-NUMBER.                
224700     COMPUTE PV-BLOCK-NUMBER =                                            
224800         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
224900                                                                          
225000     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
225100     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
225200         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
225300             PERFORM 5105-REWRITE-ASC-LISTQ                               
225400         END-IF                                                           
225500     ELSE                                                                 
225600         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
225700         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
225800         PERFORM 5110-WRITE-ASC-LISTQ                                     
225900         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
226000     END-IF.                                                              
226100                                                                          
226200     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
226300******************************************************************        
226400** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
226500** SPECIFIC COMMAREA.                                           **        
226600******************************************************************        
226700 5105-REWRITE-ASC-LISTQ.                                                  
226800     EXEC CICS                                                            
226900         WRITEQ TS                                                        
227000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
227100             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
227200             ITEM  (PV-BLOCK-NUMBER)                                      
227300             REWRITE                                                      
227400             RESP  (PV-CICS-RESP)                                         
227500     END-EXEC.                                                            
227600                                                                          
227700     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
227800         CONTINUE                                                         
227900     ELSE                                                                 
228000         SET DP013-CICS-ABEND            TO TRUE                          
228100         SET DP013-NO-ROLLBACK           TO TRUE                          
228200         MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
228300         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
228400                                         TO DP013-MESSAGE-TEXT(1)         
228500         PERFORM DP013-0000-PROCESS-ABEND                                 
228600     END-IF.                                                              
228700                                                                          
228800******************************************************************        
228900** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
229000** SPECIFIC COMMAREA.                                           **        
229100******************************************************************        
229200 5110-WRITE-ASC-LISTQ.                                                    
229300     EXEC CICS                                                            
229400         WRITEQ TS                                                        
229500             QUEUE (ASC-LIST-QUEUE-NAME)                                  
229600             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
229700             ITEM  (PV-BLOCK-NUMBER)                                      
229800             RESP  (PV-CICS-RESP)                                         
229900     END-EXEC.                                                            
230000     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
230100         SET DP013-CICS-ABEND      TO TRUE                                
230200         SET DP013-NO-ROLLBACK     TO TRUE                                
230300         MOVE '5110-WRITE-ASC-LISTQ'                                      
230400                                   TO DP013-PARAGRAPH                     
230500         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
230600                                   TO DP013-MESSAGE-TEXT(1)               
230700         PERFORM DP013-0000-PROCESS-ABEND                                 
230800     END-IF.                                                              
230900 EJECT                                                                    
231000*----------------------------------------------------------------*        
231100* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
231200* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
231300*----------------------------------------------------------------*        
231400                                                                          
231500 5200-FILL-SEL-QUEUE-RECORD.                                              
231600                                                                          
231700     PERFORM 6112-READ-LIST-QUEUE.                                        
231800                                                                          
231900     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
232000         PERFORM                                                          
232100         VARYING LQW-IDX                                                  
232200         FROM 1 BY 1                                                      
232300         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
232400           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
232500                                                                          
232600             IF  LQW-SELECT (LQW-IDX)                                     
232700                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
232800                                   TO IQ014-ITEM (IQ014-IDX)              
232900                 MOVE LQW-MAJ-CL (LQW-IDX)                                
233000                             TO IQ014-MAJ-CL (IQ014-IDX)                  
233100                                IQ016-MAJ-CL                              
233200                 MOVE LQW-SUB-CL (LQW-IDX)                                
233300                             TO IQ014-SUB-CL (IQ014-IDX)                  
233400                                IQ016-SUB-CL                              
233500                 MOVE LQW-SUB-CL-NM (LQW-IDX)                             
233600                             TO IQ014-SUB-CL-NM (IQ014-IDX)               
233700                                IQ016-SUB-CL-NM                           
233800                 SET IQ014-IDX UP BY 1                                    
233900                 IF  IQ014-IDX > IQ014-MAX-ITEMS                          
234000                     PERFORM 5205-WRITE-SEL-QUEUE                         
234100                     ADD +1        TO IQ016-NUMBER-OF-TS-ITEMS            
234200                     INITIALIZE IQ014-SELECTED-ITEM-ARRAY                 
234300                     SET IQ014-IDX TO  1                                  
234400                 END-IF                                                   
234500             END-IF                                                       
234600         END-PERFORM                                                      
234700     END-IF.                                                              
234800                                                                          
234900*----------------------------------------------------------------*        
235000*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
235100*----------------------------------------------------------------*        
235200                                                                          
235300 5205-WRITE-SEL-QUEUE.                                                    
235400                                                                          
235500     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
235600                                   TO IQ014-SELECTED-ITEMS.               
235700                                                                          
235800     EXEC CICS                                                            
235900          WRITEQ TS                                                       
236000              QUEUE (ASC-SEL-QUEUE-NAME)                                  
236100              FROM  (IQ014-MESSAGE-SEL-REC)                               
236200              ITEM  (PV-SEL-QUEUE-ITEM)                                   
236300              RESP  (PV-CICS-RESP)                                        
236400     END-EXEC.                                                            
236500                                                                          
236600     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
236700         CONTINUE                                                         
236800     ELSE                                                                 
236900         SET DP013-CICS-ABEND      TO TRUE                                
237000         SET DP013-NO-ROLLBACK     TO TRUE                                
237100         MOVE '5205-WRITE-SEL-QUEUE'                                      
237200                                   TO DP013-PARAGRAPH                     
237300         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
237400                                   TO DP013-MESSAGE-TEXT(1)               
237500         PERFORM DP013-0000-PROCESS-ABEND                                 
237600     END-IF.                                                              
237700 EJECT                                                                    
237800******************************************************************        
237900** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
238000** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
238100** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
238200** THE USER.                                                    **        
238300******************************************************************        
238400 6000-REFRESH-PANEL-FROM-TEMP.                                            
238500     IF  ASC-UPDATE-TSQ                                                   
238600         SET ASC-NO-TSQ-UPDATE TO TRUE                                    
238700         MOVE ZERO                 TO PV-SAVE-ITEM                        
238800                                      PV-SEL-QUEUE-ITEM                   
238900         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
239000                                   TO PV-HOLD-SELECTED-ITEMS              
239100         MOVE +1                   TO PV-ITEMS-PROCESSED                  
239200         PERFORM 6100-UPDATE-TSQ                                          
239300             UNTIL PV-ITEMS-PROCESSED >                                   
239400                                     PV-HOLD-SELECTED-ITEMS               
239500         PERFORM 6010-DELETE-SEL-QUEUE                                    
239600         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
239700                    ASC-BLOCK-ENTRIES (2)                                 
239800     END-IF.                                                              
239900                                                                          
240000*----------------------------------------------------------------*        
240100*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
240200*----------------------------------------------------------------*        
240300 6010-DELETE-SEL-QUEUE.                                                   
240400                                                                          
240500     EXEC CICS                                                            
240600         DELETEQ TS                                                       
240700             QUEUE (ASC-SEL-QUEUE-NAME)                                   
240800             RESP  (PV-CICS-RESP)                                         
240900     END-EXEC.                                                            
241000*                                                                         
241100     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
241200        (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
241300         CONTINUE                                                         
241400     ELSE                                                                 
241500         SET DP013-CICS-ABEND TO TRUE                                     
241600         SET DP013-NO-ROLLBACK TO TRUE                                    
241700         MOVE '6010-DELETE-SEL-QUEUE'                                     
241800                        TO DP013-PARAGRAPH                                
241900         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
242000                        TO DP013-MESSAGE-TEXT (1)                         
242100         PERFORM DP013-0000-PROCESS-ABEND                                 
242200     END-IF.                                                              
242300                                                                          
242400*----------------------------------------------------------------*        
242500*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
242600*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
242700*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
242800*    THE TEMP QUEUE.                                             *        
242900*----------------------------------------------------------------*        
243000                                                                          
243100 6100-UPDATE-TSQ.                                                         
243200                                                                          
243300     ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
243400     PERFORM 6105-READ-SEL-QUEUE.                                         
243500                                                                          
243600     PERFORM                                                              
243700         VARYING IQ014-IDX                                                
243800         FROM 1 BY 1                                                      
243900         UNTIL ((PV-ITEMS-PROCESSED >                                     
244000                              PV-HOLD-SELECTED-ITEMS)                     
244100         OR (IQ014-IDX GREATER THAN IQ014-MAX-ITEMS))                     
244200         PERFORM 6110-PROCESS-ITEMS                                       
244300         ADD +1                     TO PV-ITEMS-PROCESSED                 
244400     END-PERFORM.                                                         
244500     PERFORM 6111-WRITE-LIST-QUEUE.                                       
244600*----------------------------------------------------------------*        
244700*    READ THE LIST QUEUE RECORD.                                 *        
244800*----------------------------------------------------------------*        
244900                                                                          
245000 6105-READ-SEL-QUEUE.                                                     
245100                                                                          
245200     EXEC CICS                                                            
245300          READQ TS                                                        
245400              QUEUE (ASC-SEL-QUEUE-NAME)                                  
245500              INTO  (IQ014-MESSAGE-SEL-REC)                               
245600              ITEM  (PV-SEL-QUEUE-ITEM)                                   
245700              RESP  (PV-CICS-RESP)                                        
245800     END-EXEC.                                                            
245900*                                                                         
246000     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
246100         CONTINUE                                                         
246200     ELSE                                                                 
246300         SET DP013-CICS-ABEND      TO  TRUE                               
246400         SET DP013-NO-ROLLBACK     TO  TRUE                               
246500         MOVE '6105-READ-SEL-QUEUE'                                       
246600                                   TO  DP013-PARAGRAPH                    
246700         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
246800                                   TO  DP013-MESSAGE-TEXT(1)              
246900         PERFORM DP013-0000-PROCESS-ABEND                                 
247000     END-IF.                                                              
247100 EJECT                                                                    
247200*----------------------------------------------------------------*        
247300*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
247400*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
247500*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
247600*----------------------------------------------------------------*        
247700                                                                          
247800 6110-PROCESS-ITEMS.                                                      
247900                                                                          
248000     COMPUTE PV-ITEM = 1 + ((IQ014-ITEM (IQ014-IDX) - 1)                  
248100                     / PC-ITEMS-PER-PANEL).                               
248200                                                                          
248300     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
248400         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
248500             PERFORM 6111-WRITE-LIST-QUEUE                                
248600         END-IF                                                           
248700         MOVE PV-ITEM          TO PV-SAVE-ITEM                            
248800                                                                          
248900         PERFORM 6112-READ-LIST-QUEUE                                     
249000         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
249100             CONTINUE                                                     
249200         END-IF                                                           
249300     END-IF.                                                              
249400                                                                          
249500     COMPUTE PV-SUB = (IQ014-ITEM (IQ014-IDX)                             
249600                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
249700     SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
249800     MOVE LQW-SELECTED-SW (PV-SUB)                                        
249900                               TO LQW-TEMP-SELECTED-SW (PV-SUB).          
250000                                                                          
250100     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
250200                                                                          
250300*----------------------------------------------------------------*        
250400*    WRITE THE TS QUEUE RECORD JUST UPDATED                      *        
250500*----------------------------------------------------------------*        
250600                                                                          
250700 6111-WRITE-LIST-QUEUE.                                                   
250800                                                                          
250900     EXEC CICS                                                            
251000         WRITEQ TS                                                        
251100             QUEUE (ASC-LIST-QUEUE-NAME)                                  
251200             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
251300             ITEM  (PV-SAVE-ITEM)                                         
251400             REWRITE                                                      
251500             RESP  (PV-CICS-RESP)                                         
251600     END-EXEC.                                                            
251700*                                                                         
251800     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
251900         CONTINUE                                                         
252000     ELSE                                                                 
252100         SET DP013-CICS-ABEND  TO  TRUE                                   
252200         SET DP013-NO-ROLLBACK TO  TRUE                                   
252300         MOVE '6111-WRITE-LIST-QUEUE'                                     
252400                               TO  DP013-PARAGRAPH                        
252500         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
252600                               TO  DP013-MESSAGE-TEXT(1)                  
252700         PERFORM DP013-0000-PROCESS-ABEND                                 
252800     END-IF.                                                              
252900                                                                          
253000 EJECT                                                                    
253100*----------------------------------------------------------------*        
253200*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
253300*----------------------------------------------------------------*        
253400                                                                          
253500 6112-READ-LIST-QUEUE.                                                    
253600                                                                          
253700     EXEC CICS                                                            
253800         READQ TS                                                         
253900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
254000             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
254100             ITEM  (PV-ITEM)                                              
254200             RESP  (PV-CICS-RESP)                                         
254300     END-EXEC.                                                            
254400*                                                                         
254500     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
254600         CONTINUE                                                         
254700     ELSE                                                                 
254800         SET DP013-CICS-ABEND        TO TRUE                              
254900         SET DP013-NO-ROLLBACK       TO TRUE                              
255000         MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
255100         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
255200                                     TO DP013-MESSAGE-TEXT(1)             
255300         PERFORM DP013-0000-PROCESS-ABEND                                 
255400     END-IF.                                                              
255500                                                                          
255600 EJECT                                                                    
255700*----------------------------------------------------------------*        
255800*    RE-INITIALIZE ASC- FIELDS AFTER F3 RETURN                   *        
255900*----------------------------------------------------------------*        
256000 6500-REINIT-ASC-FLDS.                                                    
256100                                                                          
256200     MOVE ASC-LBL-NM       TO   IQ016-LBL-NM.                             
256300     MOVE ASC-GP-NBR       TO   IQ016-GP-NBR.                             
256400     MOVE ASC-GP-DESC      TO   IQ016-GP-DESC.                            
256500     MOVE ASC-DEPT-NBR     TO   IQ016-DEPT-NBR.                           
256600     MOVE ASC-DEPT-DESC    TO   IQ016-DEPT-DESC.                          
256700                                                                          
256800*----------------------------------------------------------------*        
256900*    ABEND PROCESSOR MODULE                                      *        
257000*----------------------------------------------------------------*        
257100     COPY DPPD013.                                                        
