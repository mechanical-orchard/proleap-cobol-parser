      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             ARKUT144.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2019.                                       
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  PURPOSE:                                                               
      *  THIS PROGRAM WILL TOKENIZE THE ACCOUNT NUMBER FIELD IN THE             
      *  INPUT FILE.                                                            
      *  PROTEGRITY MODULE WILL BE INVOKED TO TOKENIZE THE ACCOUNT              
      *  NUMBER                                                                 
      *                                                                         
      *  INPUT FILES :   APLPAY-FILE               DDNAME = INP01               
      *  OUTPUT FILES:   APLPAY-TOKEN-FILE         DDNAME = OUT01               
      *                                                                         
      *****************************************************************         
      *----------------------------------------------------------------*        
      *                                                                *        
      * DATE:        07/24/2019                                        *        
      * WR/IR#:      CHG0230275                                        *        
      * PROGRAMMER:  COGNIZANT                                         *        
      * DESCRIPTION: INITIAL VERSION CREATED FOR FD FILE TRANSFER      *        
      *              PROJECT                                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT APLPAY-FILE                                                   
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT APLPAY-TOKEN-FILE                                             
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  APLPAY-FILE                                                          
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  APLPAY-RECORD               PIC X(1408).                             
                                                                                
       FD  APLPAY-TOKEN-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  APLPAY-TOKEN-RECORD         PIC X(1408).                             
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  FILLER                            PIC X(50)  VALUE                   
           ' *** WORKING STORAGE STARTS HERE FOR ARKUT144 *** '.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW          PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-END-OF-FILE                VALUE 'N'.                 
               88  PS-END-OF-FILE                    VALUE 'Y'.                 
           05  PS-HDR-TRLR-REC-SW         PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-HDR-TRLR-REC               VALUE 'N'.                 
               88  PS-HDR-TRLR-REC                   VALUE 'Y'.                 
           05  PS-ACCT-SSN                PIC X(08)  VALUE SPACES.              
               88  PS-ACCT-NBR                       VALUE 'ACCT NBR'.          
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME            PIC  X(08) VALUE                      
                                                       'ARKUT144'.              
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-READ-COUNT              PIC  9(09) VALUE ZEROES.              
           05  PV-WRITE-COUNT             PIC  9(09) VALUE ZEROES.              
           05  PV-INPUT-LENGTH            PIC  9(09) VALUE ZEROES.              
           05  PV-OUTPUT-RECORD           PIC  X(1408) VALUE SPACES.            
           05  PV-ACCT-NBR                PIC  X(16) VALUE SPACES.              
           05  PV-ENTRR-ACCT-NR           PIC  X(19) VALUE SPACES.              
           05  PV-TOKN-SRVC-NR            PIC  X(19) VALUE SPACES.              
                                                                                
       01  ABEND-CODE                     PIC S9(04) VALUE ZERO                 
                                                        COMP SYNC.              
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR DATALINK ACCOUNT FILE - INPUT AND OUTPUT     *        
      ******************************************************************        
           COPY ARRD144.                                                        
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY PTYCCSIP.                                                       
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY DPWS031.                                                        
                                                                                
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA                                                      
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE ACCOUNT-NUMBER                      
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-PARA                                            
                 UNTIL PS-END-OF-FILE.                                          
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE                                                         
      *     INITIALIZE THE VARIABLES, OPEN THE INPUT AND OUTPUT FILES           
      *     AND READ THE INPUT FILE                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           DISPLAY 'PROGRAM - ' PC-PROGRAM-NAME.                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
           MOVE PC-PROGRAM-NAME               TO DP031-PROGRAM-NAME.            
                                                                                
           OPEN INPUT  APLPAY-FILE                                              
                OUTPUT APLPAY-TOKEN-FILE.                                       
                                                                                
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PARA                                                       
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE ACCOUNT AND SSN NUMBER              
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       2000-PROCESS-PARA.                                                       
                                                                                
           IF PS-NOT-HDR-TRLR-REC                                               
              INITIALIZE PV-ACCT-NBR                                            
                         PV-INPUT-LENGTH                                        
                                                                                
              SET PS-ACCT-NBR          TO TRUE                                  
              MOVE AR144-CHD-ACCT-NR   TO PV-ACCT-NBR                           
                                                                                
              INSPECT FUNCTION REVERSE (PV-ACCT-NBR)                            
                   TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                  
              COMPUTE PV-INPUT-LENGTH =                                         
                   LENGTH OF PV-ACCT-NBR - PV-INPUT-LENGTH                      
                                                                                
              PERFORM 2300-TOKENIZE-CHD-ACCT-NR                                 
                                                                                
              INITIALIZE PV-ENTRR-ACCT-NR                                       
                         PV-INPUT-LENGTH                                        
                                                                                
              SET PS-ACCT-NBR          TO TRUE                                  
              MOVE AR144-ENTRR-ACCT-NR TO PV-ENTRR-ACCT-NR                      
                                                                                
              INSPECT FUNCTION REVERSE (PV-ENTRR-ACCT-NR)                       
                   TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                  
              COMPUTE PV-INPUT-LENGTH =                                         
                   LENGTH OF PV-ENTRR-ACCT-NR  - PV-INPUT-LENGTH                
                                                                                
              PERFORM 2400-TOKENIZE-ENTRR-ACCT-NR                               
                                                                                
              IF AR144-TOKN-SRVC-NR EQUAL SPACES                                
                                                                                
                 CONTINUE                                                       
              ELSE                                                              
                                                                                
                 INITIALIZE PV-TOKN-SRVC-NR                                     
                            PV-INPUT-LENGTH                                     
                                                                                
                 SET PS-ACCT-NBR          TO TRUE                               
                 MOVE AR144-TOKN-SRVC-NR  TO PV-TOKN-SRVC-NR                    
                                                                                
                 INSPECT FUNCTION REVERSE (PV-TOKN-SRVC-NR)                     
                      TALLYING PV-INPUT-LENGTH FOR LEADING SPACES               
                 COMPUTE PV-INPUT-LENGTH =                                      
                     LENGTH OF PV-TOKN-SRVC-NR  - PV-INPUT-LENGTH               
                                                                                
                 PERFORM 2500-TOKENIZE-TOKN-SRVC-NR                             
              END-IF                                                            
           END-IF                                                               
                                                                                
           PERFORM 2200-WRITE-FILE.                                             
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2100-READ-FILE                                                          
      *      READ THE INPUT RECORDS                                             
      ******************************************************************        
       2100-READ-FILE.                                                          
                                                                                
           READ APLPAY-FILE INTO AR144-RECORDS                                  
              AT END                                                            
                 SET PS-END-OF-FILE  TO TRUE                                    
              NOT AT END                                                        
                 ADD 1               TO PV-READ-COUNT                           
           END-READ.                                                            
           IF AR144-RECORDS(1:3) = 'TRL' OR 'HDR'                               
              SET PS-HDR-TRLR-REC     TO TRUE                                   
           ELSE                                                                 
              SET PS-NOT-HDR-TRLR-REC TO TRUE                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-WRITE-FILE                                                         
      *      WRITE THE RECORDS TO OUTPUT                                        
      ******************************************************************        
       2200-WRITE-FILE.                                                         
                                                                                
           INITIALIZE PV-OUTPUT-RECORD.                                         
           MOVE AR144-RECORDS            TO PV-OUTPUT-RECORD.                   
                                                                                
           WRITE APLPAY-TOKEN-RECORD FROM PV-OUTPUT-RECORD.                     
           ADD 1                         TO PV-WRITE-COUNT.                     
                                                                                
      ******************************************************************        
      * 2300-TOKENIZE-CHD-ACCT-NR                                               
      *    TOKENIZE RDT-CHD-ACCOUNT-NUMBER                                      
      ******************************************************************        
       2300-TOKENIZE-CHD-ACCT-NR.                                               
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-ACCT-NBR             TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT  TO PV-ACCT-NBR                            
              MOVE PV-ACCT-NBR        TO AR144-CHD-ACCT-NR                      
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2400-TOKENIZE-ENTRR-ACCT-NR                                             
      *    TOKENIZE ENTRR-ACCT-NR                                               
      ******************************************************************        
       2400-TOKENIZE-ENTRR-ACCT-NR.                                             
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-ENTRR-ACCT-NR        TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT  TO PV-ENTRR-ACCT-NR                       
              MOVE PV-ENTRR-ACCT-NR   TO AR144-ENTRR-ACCT-NR                    
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2500-TOKENIZE-TOKN-SRVC-NR                                              
      *    TOKENIZE ENTRR-ACCT-NR                                               
      ******************************************************************        
       2500-TOKENIZE-TOKN-SRVC-NR.                                              
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-NBR'              TO DP031-FIELD                          
           MOVE PV-TOKN-SRVC-NR         TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT  TO PV-TOKN-SRVC-NR                        
              MOVE PV-TOKN-SRVC-NR    TO AR144-TOKN-SRVC-NR                     
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9000-EOJ-ROUTINE                                                        
      *      CLOSE THE INPUT AND OUTPUT FILES, DISPLAY THE MESSAGES             
      *      AND TERMINATE THE PROCESS.                                         
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE APLPAY-FILE                                                    
                 APLPAY-TOKEN-FILE.                                             
                                                                                
           IF PV-READ-COUNT = 0                                                 
              DISPLAY 'INPUT FILE IS EMPTY'                                     
           ELSE                                                                 
              DISPLAY 'RECORDS READ   : ' PV-READ-COUNT                         
              DISPLAY 'RECORDS WRITTEN: ' PV-WRITE-COUNT                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE           
      *      PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                      
      ******************************************************************        
            COPY DPPD031.                                                       
                                                                                
      ******************************************************************        
