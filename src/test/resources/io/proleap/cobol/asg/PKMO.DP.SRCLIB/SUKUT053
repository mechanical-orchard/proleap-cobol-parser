000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT053.                                                 
000600 AUTHOR.        GERMAN BANDA.                                             
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   OCTOBER 2001.                                             
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM DELETE THE SPECIFIED ROWS FROM THE                        
001300*  SMOOTHING WORK TABLE (SUT_SMOOTHING_PRC).                              
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000     SELECT SUT-SMOOTH-PRC-EXTRACT                                        
003100         ASSIGN TO INP01.                                                 
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700                                                                          
003800                                                                          
003900 FD  SUT-SMOOTH-PRC-EXTRACT                                               
004000     RECORDING MODE IS F                                                  
004100     LABEL RECORDS ARE STANDARD                                           
004200     BLOCK CONTAINS 0 RECORDS.                                            
004300                                                                          
004420 01  SMTH-PRC-DATA-RECORD              PIC  X(20).                        
004500*                                                                         
004600*                                                                         
004700******************************************************************        
004800 WORKING-STORAGE SECTION.                                                 
004900******************************************************************        
005000*                                                                         
005100*                                                                         
006500******************************************************************        
006600*  DB2 FORMATTED MESSAGE AREA                                             
006700******************************************************************        
006800     COPY DPWS004.                                                        
006900*                                                                         
007000*                                                                         
007100*                                                                         
007200******************************************************************        
007300*  COMMUNICATIONS AREA FOR DB2                                            
007400******************************************************************        
007500     EXEC SQL                                                             
007600          INCLUDE SQLCA                                                   
007700     END-EXEC.                                                            
007800                                                                          
007900******************************************************************        
008000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
008100*  TABLE                                                                  
008200******************************************************************        
008300     EXEC SQL                                                             
008400          INCLUDE SUT00003                                                
008500     END-EXEC.                                                            
009300*                                                                         
009400 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
009500                                                   COMP SYNC.             
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-END-OF-FILE-SW           PIC  X(01)    VALUE 'N'.             
009900         88  PS-END-OF-FILE                        VALUE 'Y'.             
010000         88  PS-NOT-END-OF-FILE                    VALUE 'N'.             
010100                                                                          
010200                                                                          
010300 01  PC-PROGRAM-CONSTANTS.                                                
010400     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
010500                                                   COMP SYNC.             
010600     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
010700         'SUKUT053'.                                                      
010710     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 100.             
010800                                                                          
010900                                                                          
010910 01  DK-DB2-KEYS.                                                         
010911     05   DK-SMTHG-PRC-ID              PIC  S9(9) COMP.                   
010915     05   DK-CRE-TMST                  PIC   X(26).                       
010916     05   DK-PO-NBR                    PIC  S9(9) COMP.                   
010917     05   DK-RCVR-SEQ-NBR              PIC  S9(9) COMP.                   
010920                                                                          
011000 01  PV-PROGRAM-VARIABLES.                                                
011200     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
011300     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
011400     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
011500                                                   COMP SYNC.             
011600     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
011700                                                   COMP SYNC.             
011800     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
011900                                                   COMP SYNC.             
012000     05  PV-DELETE-COUNT             PIC S9(09)    VALUE ZERO             
012100                                                   COMP SYNC.             
012101     05  PV-TOTAL-DELETE-COUNT       PIC S9(09)    VALUE ZERO             
012102                                                   COMP SYNC.             
012110     05  PV-NOT-FOUND-COUNT          PIC S9(09)    VALUE ZERO             
012120                                                   COMP SYNC.             
012200     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
012300     05  PV-DISPLAY-PRC-ID           PIC 9(09)     VALUE ZEROES.          
012700     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
012800                                                   COMP-3.                
012900 01  SPD-SMOOTH-PROCESS-RECORD.                                           
012910     05  SPD-SMTHG-PRC-ID            PIC  S9(9) COMP.                     
012920     05  SPD-PO-NBR                  PIC  S9(9) COMP.                     
012930     05  SPD-RCVR-SEQ-NBR            PIC  S9(9) COMP.                     
012940     05  SPD-CRE-USR-ID              PIC   X(8).                          
013000                                                                          
013700******************************************************************        
013800 PROCEDURE DIVISION.                                                      
013900******************************************************************        
014000                                                                          
014100 0000-PROGRAM-MAINLINE.                                                   
014200******************************************************************        
014300* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
014400*      PROGRAM.                                                           
014500******************************************************************        
014600                                                                          
014700     PERFORM 1000-INITIALIZE.                                             
014800     IF PS-NOT-END-OF-FILE                                                
014900         PERFORM 2000-PROCESS-SMTHG-DELETE                                
015000            UNTIL PS-END-OF-FILE                                          
015100     END-IF.                                                              
015200     PERFORM 9000-EOJ-ROUTINE.                                            
015300                                                                          
015400     STOP RUN.                                                            
015500                                                                          
015600 1000-INITIALIZE.                                                         
015700******************************************************************        
015800*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
015900*    -- OPEN INPUT FILE                                                   
016000*    -- READ FILE                                                         
016100******************************************************************        
016200                                                                          
016300     OPEN INPUT  SUT-SMOOTH-PRC-EXTRACT.                                  
016400     PERFORM 7000-READ-SMTHNG-DELETES.                                    
016500                                                                          
016600                                                                          
016700                                                                          
016710******************************************************************        
016800 2000-PROCESS-SMTHG-DELETE.                                               
017400                                                                          
017500     PERFORM 8000-DELETE-SMTHNG-DATA.                                     
019100                                                                          
019101     IF PV-DELETE-COUNT > PC-COMMIT-HOLD                                  
019102         EXEC SQL                                                         
019103            COMMIT                                                        
019104         END-EXEC                                                         
019105                                                                          
019106         IF SQLCODE NOT = +0                                              
019107             DISPLAY '### COMMIT FAILED ! ###'                            
019108             MOVE '2000-PROCESS-SMTHG-DELETE'                             
019109                         TO PV-PARAGRAPH-NAME                             
019110             MOVE 'COMMIT OF SUT_SMOOTHING_PRC'                           
019111                         TO PV-DB2-OPERATION-ATTEMPTED                    
019112             PERFORM 9100-SQL-ABEND                                       
019113         END-IF                                                           
019114                                                                          
019115         MOVE +0 TO PV-DELETE-COUNT                                       
019116     END-IF.                                                              
019117                                                                          
019120                                                                          
019200     PERFORM 7000-READ-SMTHNG-DELETES.                                    
019300                                                                          
019310******************************************************************        
019400 7000-READ-SMTHNG-DELETES.                                                
019600      READ SUT-SMOOTH-PRC-EXTRACT                                         
019700           INTO SPD-SMOOTH-PROCESS-RECORD                                 
019800           AT END SET PS-END-OF-FILE TO TRUE.                             
019900                                                                          
020010******************************************************************        
020020*  DELETE THE ROW FROM THE LABEL PRINT DATA TABLE                         
020030******************************************************************        
020100 8000-DELETE-SMTHNG-DATA.                                                 
020500                                                                          
020510     MOVE  SPD-SMTHG-PRC-ID     TO  PV-DISPLAY-PRC-ID.                    
020511     MOVE  SPD-SMTHG-PRC-ID     TO  DK-SMTHG-PRC-ID.                      
020512     MOVE  SPD-PO-NBR           TO  DK-PO-NBR.                            
020513     MOVE  SPD-RCVR-SEQ-NBR     TO  DK-RCVR-SEQ-NBR.                      
020520                                                                          
020600     EXEC SQL                                                             
020700         DELETE                                                           
020800             FROM  SUT_SMOOTHING_PRC                                      
020910             WHERE SMTHG_PRC_ID    = :DK-SMTHG-PRC-ID                     
020940               AND PO_NBR          = :DK-PO-NBR                           
020950               AND RCVR_SEQ_NBR    = :DK-RCVR-SEQ-NBR                     
021000     END-EXEC.                                                            
021100                                                                          
021200     EVALUATE TRUE                                                        
021300         WHEN SQLCODE = 0                                                 
021400             ADD +1           TO PV-DELETE-COUNT                          
021410             ADD +1           TO PV-TOTAL-DELETE-COUNT                    
021500         WHEN SQLCODE = +100                                              
021510             CONTINUE                                                     
021700         WHEN SQLCODE < 0                                                 
021800         WHEN SQLCODE > 0                                                 
021900         WHEN SQLWARN0 = 'W'                                              
022000             MOVE '8000-DELETE-SMTHNG-DATA'                               
022100                         TO PV-PARAGRAPH-NAME                             
022200             MOVE 'DELETE OF SUT_SMOOTHING_PRC'                           
022300                         TO PV-DB2-OPERATION-ATTEMPTED                    
022400             PERFORM 9100-SQL-ABEND                                       
022500     END-EVALUATE.                                                        
022600                                                                          
022700                                                                          
022800 9000-EOJ-ROUTINE.                                                        
022900******************************************************************        
023000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
023100******************************************************************        
023200     CLOSE SUT-SMOOTH-PRC-EXTRACT.                                        
023300                                                                          
023400     MOVE PV-TOTAL-DELETE-COUNT  TO PV-DISPLAY-COUNT.                     
023520     DISPLAY 'TOTAL DISTINCT SMTHNG_PRC DELETED' PV-DISPLAY-COUNT.        
023800                                                                          
023900                                                                          
024000 9100-SQL-ABEND.                                                          
024100******************************************************************        
024200*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
024300*  ERROR OR WARNING CODE OCCURS.                                          
024400******************************************************************        
024500     DISPLAY '9100-SQL-ABEND'.                                            
024600     DISPLAY '** ABEND     **'.                                           
024700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
024800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
024900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
025000                                                                          
025100******************************************************************        
025200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
025300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
025400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
025500* FORMAT.                                                                 
025600******************************************************************        
025700     COPY DPPD004.                                                        
025800                                                                          
025900     MOVE +4013                  TO ABEND-CODE.                           
026000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
