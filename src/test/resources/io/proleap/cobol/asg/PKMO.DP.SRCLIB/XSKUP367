      *----------------------------------------------------------------*00010000
       IDENTIFICATION DIVISION.                                         00020000
      *----------------------------------------------------------------*00030000
       PROGRAM-ID.      XSKUP367.                                       00040011
       AUTHOR.          COGNIZANT.                                      00050000
       DATE-WRITTEN.    05/30/2014.                                     00060000
                                                                        00070000
      *----------------------------------------------------------------*00080000
      *         PROGRAM TO LOAD XST_GMA TABLE FROM THE XSK099 JOB       00090000
      *----------------------------------------------------------------*00100000
      *     COMPARES THE INPUT FILE WITH XST_GMA TABLE AND INSERTS,     00110000
      *     UPDATES, DELETES RECORDS BASED ON INPUT RECORD.             00120000
      *----------------------------------------------------------------*00130000
      * HISTORY LOG:                                                    00140000
      *----------------------------------------------------------------*00150000
      * DATE: 02/20/2017                                                00160012
      * WR/IR#: CHG0162699                                              00170012
      * PROGRAMMER: SRINATH(COGNIZANT)                                  00180012
      * DESCRIPTION: XSK367 WAS RUNNING LONG SINCE THE END OF FILE      00190012
      * SWITCH FOR THE HM FILE IS INITIALIZED TO SPACES INSTEAD OF 'N'. 00200012
      * IN CASE THE HM FILE HAS MORE RECORDS THAN THE XST_GMA TABLE     00201012
      * EXTRACT, IT WILL KEEP LOOPING IN 3000- PARA, AS THE SWITCH IS   00202012
      * NEVER SET, SINCE IT IS INITIALIZED TO SPACES.                   00203012
      *----------------------------------------------------------------*00210000
       ENVIRONMENT DIVISION.                                            00220000
      *----------------------------------------------------------------*00230000
       CONFIGURATION SECTION.                                           00240000
       SOURCE-COMPUTER.        IBM-3090.                                00250000
       OBJECT-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
       FILE-CONTROL.                                                    00290000
                                                                        00300000
           SELECT GMA-FILE                                              00310000
                  ASSIGN TO INP01.                                      00320000
           SELECT GMA-TABLE-FILE                                        00321007
                  ASSIGN TO INP02.                                      00322007
                                                                        00330000
       DATA DIVISION.                                                   00340000
       FILE SECTION.                                                    00350000
                                                                        00360000
       FD  GMA-FILE                                                     00370000
           RECORDING MODE IS F                                          00380000
           BLOCK CONTAINS 0 RECORDS                                     00390000
           LABEL RECORDS ARE STANDARD.                                  00400000
       01  GMA-FILE-RECORD                     PIC X(46).               00410005
                                                                        00420000
       FD  GMA-TABLE-FILE                                               00421007
           RECORDING MODE IS F                                          00422007
           BLOCK CONTAINS 0 RECORDS                                     00423007
           LABEL RECORDS ARE STANDARD.                                  00424007
       01  GMA-FILE-TABLE-RECORD               PIC X(45).               00425007
                                                                        00426007
                                                                        00430000
      *----------------------------------------------------------------*00440000
       WORKING-STORAGE SECTION.                                         00450000
                                                                        00460000
      *----------------------------------------------------------------*00470000
      * PS- PROGRAM SWITCHES                                            00480000
      *----------------------------------------------------------------*00490000
       01  PS-PROGRAM-SWITCHES.                                         00500000
                                                                        00510000
           05  PS-END-OF-GMA-FILE-SW           PIC X(01) VALUE 'N'.     00520012
               88  PS-END-OF-GMA-FILE                    VALUE 'Y'.     00530000
               88  PS-NOT-END-OF-GMA-FILE                VALUE 'N'.     00540000
           05  PS-END-OF-GMA-TABLE-FILE-SW     PIC X(01) VALUE 'N'.     00550007
               88  PS-END-OF-GMA-TABLE-FILE              VALUE 'Y'.     00560007
               88  PS-NOT-END-OF-GMA-TABLE-FILE          VALUE 'N'.     00570007
           05  PS-PROCESS-INDICATOR-SW         PIC X(01) VALUE 'Y'.     00580000
               88  PS-PROCESS-COMPLETE                   VALUE 'N'.     00590000
               88  PS-PROCESS-CONTINUE                   VALUE 'Y'.     00600000
                                                                        00610000
      *----------------------------------------------------------------*00620000
      * PC- PROGRAM CONSTANTS                                           00630000
      *----------------------------------------------------------------*00640000
       01  PC-PROGRAM-CONSTANTS.                                        00650000
                                                                        00660000
           05  PC-PROGRAM-NAME           PIC X(08)  VALUE 'XSKUP367'.   00670011
                                                                        00710000
      *----------------------------------------------------------------*00720000
      * PA- PROGRAM ACCUMULATORS                                        00730000
      *----------------------------------------------------------------*00740000
       01  PA-PROGRAM-ACCUMULATORS.                                     00750000
                                                                        00760000
           05  PA-GMAFILE-COUNT          PIC 9(09) VALUE ZEROS.         00770005
           05  PA-GMAFILE-TABLE-COUNT    PIC 9(09) VALUE ZEROS.         00771007
           05  PA-UPDATE-GMA-COUNT       PIC 9(09) VALUE ZEROS.         00780005
           05  PA-DELETE-GMA-COUNT       PIC 9(09) VALUE ZEROS.         00790005
           05  PA-INSERT-GMA-COUNT       PIC 9(09) VALUE ZEROS.         00800005
                                                                        00810000
      *----------------------------------------------------------------*00811007
      * PL- INPUT RECORD LAYOUT                                         00812008
      *----------------------------------------------------------------*00813007
       01  PL-TBL-INPUT-RECORD.                                         00814008
                                                                        00815007
           05  PL-TBL-GMA-NBR            PIC S9(4)V COMP-3.             00815208
           05  PL-TBL-GMA-NM             PIC X(30)  VALUE SPACES.       00815308
           05  PL-TBL-GMA-USR-ID         PIC X(08)  VALUE SPACES.       00815408
                                                                        00819107
      *----------------------------------------------------------------*00820000
      * PV- PROGRAM VARIABLES                                           00830000
      *----------------------------------------------------------------*00840000
       01  PV-PRIMARY-KEYS.                                             00850000
                                                                        00860000
           05  PV-GMA-NBR                PIC S9(4)V COMP-3.             00870005
           05  PV-MESSAGE                PIC X(60)  VALUE SPACES.       00880005
           05  PV-PARAGRAPH              PIC X(30)  VALUE SPACES.       00881005
           05  PV-SQL-CODE               PIC S9(04) VALUE +0 COMP-3.    00882005
           05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.        00883008
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.        00884008
           05  PV-OPERATION-MSG          PIC X(35)  VALUE SPACE.        00885008
                                                                        00890000
      ******************************************************************00900000
      *                        ERROR HANDLING                           00910000
      ******************************************************************00920000
       01  ABEND-CODE                    PIC S9(4)   VALUE ZEROS        00930000
                                                     COMP SYNC.         00940000
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.       00950000
                                                                        00960000
      *----------------------------------------------------------------*00970000
      * GMA FILE LAYOUT                                                 00980000
      *----------------------------------------------------------------*00990000
           COPY HMRD006.                                                01000000
                                                                        01010000
      *----------------------------------------------------------------*01020000
      * DB2 SQLCA ROUTINE DPPD004                                       01030000
      *----------------------------------------------------------------*01040000
           COPY DPWS004.                                                01050000
                                                                        01060000
      *----------------------------------------------------------------*01070000
      * DB2 ABEND COPYBOOK                                              01080000
      *----------------------------------------------------------------*01090000
           COPY DPWS900.                                                01100000
                                                                        01110000
      *----------------------------------------------------------------*01120000
      * DB2 MESSAGE AREA                                                01130000
      *----------------------------------------------------------------*01140000
           COPY SQLCA2.                                                 01150000
                                                                        01160000
      *----------------------------------------------------------------*01170000
      * DB2 COMMUNICATIONS AREA                                         01180000
      *----------------------------------------------------------------*01190000
           EXEC SQL                                                     01200000
             INCLUDE SQLCA                                              01210000
           END-EXEC.                                                    01220000
                                                                        01230000
      *----------------------------------------------------------------*01240000
      * DB2 TABLE XST00068(XST_GMA)                                     01250000
      *----------------------------------------------------------------*01260000
           EXEC SQL                                                     01270000
             INCLUDE XST00068                                           01280000
           END-EXEC.                                                    01290000
      *                                                                 01450000
      *----------------------------------------------------------------*01460000
       PROCEDURE DIVISION.                                              01470000
      *----------------------------------------------------------------*01480000
       0000-MAINLINE.                                                   01490000
                                                                        01500000
           PERFORM 1000-INITIALIZE.                                     01510000
                                                                        01520000
           PERFORM 2000-PROCESS-RECORDS                                 01530000
             UNTIL PS-END-OF-GMA-FILE                                   01540005
                OR PS-END-OF-GMA-TABLE-FILE.                            01541007
                                                                        01550000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01560000
             UNTIL PS-END-OF-GMA-FILE                                   01570000
               AND PS-END-OF-GMA-TABLE-FILE.                            01580007
                                                                        01590000
           PERFORM 9000-EOJ-ROUTINE.                                    01600005
                                                                        01610000
           GOBACK.                                                      01620000
      *----------------------------------------------------------------*01630000
      * 1000-INITIALIZATION.                                            01640000
      *     SETS UP INITIAL VALUES, OPENS FILES, PREPS FOR PROCESSING.  01650000
      *----------------------------------------------------------------*01660000
       1000-INITIALIZE.                                                 01670000
                                                                        01680000
            OPEN INPUT GMA-FILE                                         01690008
                       GMA-TABLE-FILE.                                  01691008
                                                                        01700000
            PERFORM 7000-READ-GMA-FILE.                                 01710005
                                                                        01720000
            IF PS-END-OF-GMA-FILE                                       01730000
               DISPLAY "NO GMA RECORDS TO PROCESS"                      01740000
               PERFORM 9999-PROCESS-ABEND                               01741008
            END-IF.                                                     01750000
                                                                        01760000
            PERFORM 7200-READ-GMA-TABLE-FILE.                           01770007
                                                                        01800007
            IF PS-END-OF-GMA-TABLE-FILE                                 01810007
               DISPLAY "NO GMA RECORDS TO PROCESS FROM TABLE"           01820007
            END-IF.                                                     01830007
                                                                        01840007
      *----------------------------------------------------------------*02510000
      *2000-PROCESS-RECORDS.                                            02520000
      * THIS SECTION COMPARES THE KEY FIELD OF XST_GMA TABLE WITH THE   02530000
      * INPUT FILE. IF RECORD MATCHES THEN COMPARISON OF FIELDS IS MADE 02540000
      * TO UPDATE THE TABLE. IF RECORD NOT FOUND IN XST_GMA TABLE THEN  02550000
      * INSERTION OF RECORD HAPPENS. IF A PARTICULAR RECORD IS MISSING  02560000
      * IN INPUT FILE THEN DELETE THAT PARTICULAR RECORD.               02570000
      *----------------------------------------------------------------*02580000
       2000-PROCESS-RECORDS.                                            02590000
                                                                        02600000
           MOVE HM006-GMA-NBR TO PV-GMA-NBR                             02610000
                                                                        02630000
           EVALUATE TRUE                                                02631008
           WHEN PV-GMA-NBR = PL-TBL-GMA-NBR                             02640008
                PERFORM 2100-COMPARE-FIELDS                             02650008
                PERFORM 7000-READ-GMA-FILE                              02651008
                PERFORM 7200-READ-GMA-TABLE-FILE                        02652008
           WHEN PV-GMA-NBR < PL-TBL-GMA-NBR                             02653008
                PERFORM 2300-INSERT-RECORDS                             02654008
                PERFORM 7000-READ-GMA-FILE                              02655008
           WHEN PV-GMA-NBR > PL-TBL-GMA-NBR                             02670008
                PERFORM 2200-DELETE-RECORDS                             02680008
                PERFORM 7200-READ-GMA-TABLE-FILE                        02681008
           END-EVALUATE.                                                02740008
                                                                        02750000
                                                                        02752004
      *----------------------------------------------------------------*02760000
      * 2100-COMPARE-FIELDS.                                            02770000
      *     SINCE THE KEY FIELD MATCHED, EXAMINE THE REMAINING          02780000
      *     FIELDS TO DETERMINE IF ANY OTHER FIELD DOES NOT MATCH.      02790000
      *----------------------------------------------------------------*02800000
       2100-COMPARE-FIELDS.                                             02810000
                                                                        02820000
           IF  PV-GMA-NBR       = PL-TBL-GMA-NBR    AND                 02830008
               HM006-GMA-NM     = PL-TBL-GMA-NM     AND                 02840008
               HM006-GMM-USR-ID = PL-TBL-GMA-USR-ID                     02850008
               CONTINUE                                                 02860000
           ELSE                                                         02870000
               PERFORM 2110-UPDATE-RECORDS                              02880000
           END-IF.                                                      02890000
                                                                        02900000
                                                                        02910000
      *----------------------------------------------------------------*02920000
      * 2110-UPDATE-RECORDS.                                            02930000
      *     UPDATE THE MODIFIED FIELDS.                                 02940000
      *----------------------------------------------------------------*02950000
       2110-UPDATE-RECORDS.                                             02960000
                                                                        02970000
           MOVE PV-GMA-NBR                 TO XST00068-GMA-NBR          02990007
           MOVE HM006-GMA-NM               TO XST00068-GMA-NM           03000007
           MOVE HM006-GMM-USR-ID           TO XST00068-GMA-USR-ID       03010007
                                                                        03020000
             EXEC SQL                                                   03030000
               UPDATE XST_GMA                                           03040000
               SET GMA_NM     = :XST00068-GMA-NM,                       03070009
                   GMA_USR_ID = :XST00068-GMA-USR-ID                    03080000
                 WHERE                                                  03081009
                   GMA_NBR    = :XST00068-GMA-NBR                       03082009
             END-EXEC                                                   03090000
                                                                        03100000
           MOVE SQLCODE                    TO PV-SQL-CODE               03110005
           EVALUATE TRUE                                                03120000
              WHEN SQLCODE = 0                                          03121008
                ADD 1                      TO PA-UPDATE-GMA-COUNT       03122008
              WHEN SQLCODE = -904                                       03130000
              WHEN SQLCODE = -911                                       03140000
              WHEN SQLCODE = -913                                       03150000
                CONTINUE                                                03160000
              WHEN OTHER                                                03190000
                SET PS-PROCESS-COMPLETE    TO TRUE                      03200005
                MOVE '2110-UPDATE-RECORDS' TO PV-PARAGRAPH              03210000
                MOVE 'BAD SQLCODE FOR UPDATE XST_GMA'                   03220005
                                           TO PV-MESSAGE                03221005
                PERFORM 8900-PROCESS-DB2-ERROR                          03230006
           END-EVALUATE.                                                03240000
                                                                        03250000
      *----------------------------------------------------------------*03260000
      * 2200-DELETE-RECORDS.                                            03270000
      *     DELETE THE RECORDS WHICH ARE NOT PRESENT IN THE INPUT FILE  03280000
      *----------------------------------------------------------------*03290000
       2200-DELETE-RECORDS.                                             03300000
                                                                        03310000
             MOVE PL-TBL-GMA-NBR TO XST00068-GMA-NBR                    03311008
                                                                        03312007
             EXEC SQL                                                   03320000
               DELETE FROM XST_GMA                                      03330000
               WHERE GMA_NBR    = :XST00068-GMA-NBR                     03340000
             END-EXEC                                                   03350000
                                                                        03360000
           MOVE SQLCODE                    TO PV-SQL-CODE               03370005
           EVALUATE TRUE                                                03380000
              WHEN SQLCODE = 0                                          03381008
                ADD 1                      TO PA-DELETE-GMA-COUNT       03382008
              WHEN SQLCODE = -904                                       03390000
              WHEN SQLCODE = -911                                       03400000
              WHEN SQLCODE = -913                                       03410000
                CONTINUE                                                03420000
              WHEN OTHER                                                03450000
                SET PS-PROCESS-COMPLETE    TO TRUE                      03460005
                MOVE '2200-DELETE-RECORDS' TO PV-PARAGRAPH              03470000
                MOVE 'BAD SQLCODE FOR DELETE XST_GMA'                   03480005
                                           TO PV-MESSAGE                03481005
                PERFORM 8900-PROCESS-DB2-ERROR                          03490006
           END-EVALUATE.                                                03500000
                                                                        03510000
      *----------------------------------------------------------------*03520000
      * 2300-INSERT-RECORDS.                                            03530000
      *     INSERT THE RECORD WHEN THE INPUT FILE HAS THAT RECORD       03540000
      *----------------------------------------------------------------*03550000
       2300-INSERT-RECORDS.                                             03560000
                                                                        03570000
           MOVE PV-GMA-NBR                 TO XST00068-GMA-NBR          03580005
           MOVE HM006-GMA-NM               TO XST00068-GMA-NM           03600005
           MOVE HM006-GMM-USR-ID           TO XST00068-GMA-USR-ID       03610005
                                                                        03620000
             EXEC SQL                                                   03630000
               INSERT INTO XST_GMA                                      03640000
                  (GMA_NBR                                              03650000
                  ,GMA_NM                                               03670000
                  ,GMA_USR_ID)                                          03680000
                VALUES                                                  03690000
                  (:XST00068-GMA-NBR                                    03700000
                  ,:XST00068-GMA-NM                                     03720000
                  ,:XST00068-GMA-USR-ID )                               03730000
             END-EXEC                                                   03740000
                                                                        03750000
           MOVE SQLCODE                    TO PV-SQL-CODE               03760005
           EVALUATE TRUE                                                03770000
              WHEN SQLCODE = 0                                          03771008
                ADD 1                      TO PA-INSERT-GMA-COUNT       03772008
              WHEN SQLCODE = -904                                       03780000
              WHEN SQLCODE = -911                                       03790000
              WHEN SQLCODE = -913                                       03800000
                CONTINUE                                                03810000
              WHEN OTHER                                                03840000
                SET PS-PROCESS-COMPLETE    TO TRUE                      03850005
                MOVE '2300-INSERT-RECORDS' TO PV-PARAGRAPH              03860000
                MOVE 'BAD SQLCODE FOR INSERT XST_GMA'                   03870005
                                           TO PV-MESSAGE                03871005
                PERFORM 8900-PROCESS-DB2-ERROR                          03880006
           END-EVALUATE.                                                03890000
                                                                        03900000
      *----------------------------------------------------------------*03910000
      * 3000-PROCESS-EXTRA-RECORDS.                                     03920000
      * PROCESS RECORDS UNTIL END OF RECORDS IN BOTH INPUT AND TABLE    03930000
      *----------------------------------------------------------------*03940000
       3000-PROCESS-EXTRA-RECORDS.                                      03950000
                                                                        03960000
           MOVE HM006-GMA-NBR TO PV-GMA-NBR                             03970000
                                                                        03990000
           IF PS-NOT-END-OF-GMA-FILE THEN                               04000000
              PERFORM 2300-INSERT-RECORDS                               04010000
              PERFORM 7000-READ-GMA-FILE                                04011008
           END-IF                                                       04020008
                                                                        04021008
           IF PS-NOT-END-OF-GMA-TABLE-FILE THEN                         04030008
              PERFORM 2200-DELETE-RECORDS                               04040008
              PERFORM 7200-READ-GMA-TABLE-FILE                          04050008
           END-IF.                                                      04060000
                                                                        04070000
      *----------------------------------------------------------------*04071006
      * 7000-READ-GMA-FILE.                                             04072006
      *     READS THE INPUT GMA FILE.                                   04073006
      *----------------------------------------------------------------*04074006
       7000-READ-GMA-FILE.                                              04075006
                                                                        04076006
            READ GMA-FILE INTO HMRD006-RECORD                           04077006
              AT END                                                    04078006
                 SET PS-END-OF-GMA-FILE TO TRUE                         04079006
            END-READ.                                                   04079106
                                                                        04079206
            IF NOT PS-END-OF-GMA-FILE                                   04079306
                 ADD 1                  TO PA-GMAFILE-COUNT             04079406
            END-IF.                                                     04079506
                                                                        04079606
      *----------------------------------------------------------------*04079706
      * 7200-READ-GMA-TABLE-FILE.                                       04079807
      *     OPENS THE GMA RECORDS FROM TABLE                            04079907
      *----------------------------------------------------------------*04080006
       7200-READ-GMA-TABLE-FILE.                                        04080107
                                                                        04080206
            READ GMA-TABLE-FILE INTO PL-TBL-INPUT-RECORD                04080308
              AT END                                                    04080407
                 SET PS-END-OF-GMA-TABLE-FILE                           04080507
                                        TO TRUE                         04080607
            END-READ.                                                   04080707
                                                                        04080807
            IF NOT PS-END-OF-GMA-TABLE-FILE                             04080907
                 ADD 1                  TO PA-GMAFILE-TABLE-COUNT       04081007
            END-IF.                                                     04081107
                                                                        04081207
       8900-PROCESS-DB2-ERROR.                                          04086006
                                                                        04090000
           DISPLAY '***************************************************'04100000
           DISPLAY '**  ERROR     **'.                                  04110000
           DISPLAY '**  SQLCODE   **  = ' PV-SQL-CODE.                  04120000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              04130000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH.                 04140000
           DISPLAY '**  MESSAGE   **  = ' PV-MESSAGE.                   04150000
                                                                        04160000
      *ROUTINE TO CONVERT THE SQLCA INTO A READABLE FORMAT, USING       04170000
      *THE CXST00068ED MODULE DSNTIAR.                                  04180000
           COPY DPPD004.                                                04190000
                                                                        04200000
           MOVE +4013 TO ABEND-CODE.                                    04210000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04220000
                                                                        04230000
      *----------------------------------------------------------------*04240000
      * 9000-EOJ-ROUTINE.                                               04250005
      *----------------------------------------------------------------*04260000
       9000-EOJ-ROUTINE.                                                04270005
                                                                        04280000
           DISPLAY '***************************************************'04290000
           DISPLAY 'NUMBER OF RECS READ IN GMA FILE:' PA-GMAFILE-COUNT. 04300005
           DISPLAY 'NUMBER OF RECS READ FROM TABLE:'                    04301007
                                             PA-GMAFILE-TABLE-COUNT.    04302007
           DISPLAY 'NUMBER OF RECS UPDATED:' PA-UPDATE-GMA-COUNT.       04310005
           DISPLAY 'NUMBER OF RECS INSERTED:' PA-INSERT-GMA-COUNT.      04320005
           DISPLAY 'NUMBER OF RECS DELETED:' PA-DELETE-GMA-COUNT.       04330005
           DISPLAY '***************************************************'04340000
           CLOSE GMA-FILE                                               04350008
                 GMA-TABLE-FILE.                                        04360008
                                                                        04370008
       9999-PROCESS-ABEND.                                              04380008
                                                                        04390008
           MOVE '1000-INITIALIZE'     TO PV-PARAGRAPH-NAME              04400008
           MOVE 'INPUT FILE CANNOT BE EMPTY'                            04410008
                                      TO PV-OPERATION-MSG               04420008
           DISPLAY '                     '.                             04430008
           DISPLAY '** ABEND           **'.                             04440008
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          04450008
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        04460008
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         04470008
                                                                        04480008
           CALL 'ILBOABN0' USING ABEND-CODE.                            04490008
                                                                        04500008
