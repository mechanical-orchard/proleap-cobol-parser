       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    DAKUT320.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  12-30-2003.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     DAKUT320 - APPLY THE DMM AND BUYER NUMBERS TO THE AP                
      *                TRANSACTION FILE RECORD - APRD045. THE NEW               
      *                RECORD LAYOUT IS DARD105.                                
      *                                                                         
      *CHANGE HISTORY:                                                          
      * PROJ   DATE       PGMR       DESCRIPTION                                
      * ------ ---------- ---------- ----------------------------------         
22405 * 22405  05-31-2001 R JANSSEN  DAII - ASSET RECOVERY ENHANCEMENT          
W30021* W30021 05-05-2006 P KEBER    WRITE OUT NEW '7D' CREDIT RECORDS.         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AP-TRANS-FILE-IN                                              
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT DA-TRANS-FILE-OUT                                             
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AP-TRANS-FILE-IN                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  AP-TRANS-FILE-REC      PIC X(550).                                   
                                                                                
       FD  DA-TRANS-FILE-OUT                                                    
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DA-TRANS-FILE-REC      PIC X(550).                                   
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'DAKUT320'.             
           05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
           05  PC-MDSELV-GMA                PIC X(03)  VALUE 'GMA'.             
           05  PC-MDSELV-DMA                PIC X(03)  VALUE 'DMA'.             
           05  PC-MDSELV-DPT                PIC X(03)  VALUE 'DPT'.             
           05  PC-MDSELV-SCL                PIC X(03)  VALUE 'SCL'.             
           05  PC-RSPLVL-CDE-GMM            PIC X(03)  VALUE 'GMM'.             
           05  PC-RSPLVL-CDE-DMM            PIC X(03)  VALUE 'DMM'.             
           05  PC-RSPLVL-CDE-BYR            PIC X(03)  VALUE 'BYR'.             
           05  PC-UNKNOWN                   PIC X(07)  VALUE 'UNKNOWN'.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-MAX-SCL-DATE-NULL-IND     PIC S9(04) VALUE ZEROS              
                                                       COMP.                    
           05  PV-MAX-DEPT-DATE-NULL-IND    PIC S9(04) VALUE ZEROS              
                                                       COMP.                    
           05  PV-EXTRACT-DTE               PIC X(10)  VALUE SPACES.            
           05  PV-PREV-DOC-NBR              PIC X(16)  VALUE SPACES.            
           05  PV-PREV-DEPT-NBR             PIC X(03)  VALUE SPACES.            
           05  PV-PREV-SUB-CL-NBR           PIC X(02)  VALUE SPACES.            
           05  PV-INPUT-REC-CNT             PIC 9(11)  VALUE ZEROS              
                                                        COMP-3.                 
           05  PV-OUTPUT-REC-CNT            PIC 9(11)  VALUE ZEROS              
                                                        COMP-3.                 
           05  PV-MBS-NOT-FOUND-CNT         PIC 9(11)  VALUE ZEROS              
                                                        COMP-3.                 
           05  PV-DMM-FIR-NAME              PIC X(12)  VALUE SPACES.            
           05  PV-DMM-LAST-NAME             PIC X(18)  VALUE SPACES.            
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MBS-DONE-SW          PIC  X(01)    VALUE 'N'.                 
               88  PS-MBS-IS-DONE                    VALUE 'Y'.                 
               88  PS-MBS-IS-NOT-DONE                VALUE 'N'.                 
           05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.               
               88  MBS-INFO-FOUND                      VALUE 'Y'.               
               88  MBS-INFO-NOT-FOUND                  VALUE 'N'.               
           05  PS-MBS-MAX-DATE-FOUND-SW     PIC X      VALUE 'N'.               
               88  MBS-MAX-DATE-FOUND                  VALUE 'Y'.               
               88  MBS-MAX-DATE-NOT-FOUND              VALUE 'N'.               
           05  PS-MBS-USING-MAX-DATE-SW     PIC X      VALUE 'N'.               
               88  MBS-USING-MAX-DATE                  VALUE 'Y'.               
               88  MBS-NOT-USING-MAX-DATE              VALUE 'N'.               
                                                                                
      /****************************************************************         
      * COPYBOOK - AP TRANSACTION FILE RECORD                                   
      *****************************************************************         
           COPY APRD045.                                                        
           05  PV-APRD045-WORK-FLDS.                                            
               10  PV-APRD045-BATCH-NBR    PIC X(03).                           
               10  PV-APRD045-CENPPC-NBR   PIC S9(07)    COMP-3.                
               10  PV-APRD045-RTL-AMT      PIC S9(09)V99 COMP-3.                
               10  PV-APRD045-NET-CST-AMT  PIC S9(09)V99 COMP-3.                
               10  PV-APRD045-EFF-DTE      PIC X(19).                           
               10  FILLER                  PIC X(12).                           
                                                                                
      /****************************************************************         
      * COPYBOOK - DA TRANSACTION FILE RECORD                                   
      *****************************************************************         
           COPY DARD105.                                                        
                                                                                
      /****************************************************************         
      * COPYBOOK - WORKING STORAGE AREA FOR ROUTINE TO FORMAT NAME              
      *****************************************************************         
           COPY DPWS024.                                                        
                                                                                
      /*****************************************************************        
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TMDSEAR                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TRSPBAR                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE ERROR HANDLING                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0100-MAINLINE-DAKUT320.                                                  
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS-LOOP                                       
               UNTIL PS-MBS-IS-DONE.                                            
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS.                                       
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      * 1000-INITIALIZATION.                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '*  START OF DAKUT320                     *'.                
           DISPLAY '******************************************'.                
                                                                                
           OPEN INPUT  AP-TRANS-FILE-IN                                         
           OPEN OUTPUT DA-TRANS-FILE-OUT.                                       
                                                                                
           INITIALIZE DA105-TRANSACTION-FILE.                                   
           MOVE SPACES              TO PV-PREV-DEPT-NBR                         
                                       PV-PREV-SUB-CL-NBR.                      
           MOVE +30                 TO DP024-FLNAME-LENGTH.                     
                                                                                
      * READ AN AP TRANSACTION RECORD...                                        
           PERFORM 8500-READ-AP-TRANS-FILE.                                     
                                                                                
      /*****************************************************************        
      * 2000-MAIN-PROCESS-LOOP.                                                 
      ******************************************************************        
       2000-MAIN-PROCESS-LOOP.                                                  
                                                                                
           IF AP045-DETAIL-RECORD                                               
W30021         IF AP045-AP-TRN-CDE IS EQUAL TO '69' OR '87' OR '7D'             
                   PERFORM 3000-FORMAT-OTHER-ENTRY                              
               ELSE                                                             
                   PERFORM 3100-FORMAT-DETAIL-ENTRY                             
               END-IF                                                           
           ELSE                                                                 
               PERFORM 3000-FORMAT-OTHER-ENTRY                                  
           END-IF.                                                              
                                                                                
      * READ AN AP TRANSACTION RECORD...                                        
           PERFORM 8500-READ-AP-TRANS-FILE.                                     
                                                                                
      /*****************************************************************        
      * 3000-FORMAT-OTHER-ENTRY.                                                
W30021* INTERNAL LOADS, ASSET RECOVERIES, AND OFF INVOICE DA'S CREATE           
      * REVERSING ENTRIES WHICH DO NOT INCLUDE DEPT/CLASS/SKU ON THEM.          
      * CREATE THE MBS STRUCTURE WITH SPACES TO ACCOMODATE THIS.                
      ******************************************************************        
       3000-FORMAT-OTHER-ENTRY.                                                 
                                                                                
           PERFORM 3200-MOVE-AP-TO-DA-TRANS.                                    
                                                                                
           IF AP045-DETAIL-RECORD                                               
               MOVE MDSEAR-GMA-NBR     TO DA105-GMM                             
               MOVE MDSEAR-DMA-NBR     TO DA105-DMM                             
               MOVE MDSEAR-BYR-NBR     TO DA105-BUYER                           
               MOVE DP024-FLNAME       TO DA105-NAME-DMM                        
           ELSE                                                                 
               MOVE SPACES             TO DA105-GMM                             
               MOVE SPACES             TO DA105-DMM                             
               MOVE SPACES             TO DA105-BUYER                           
               MOVE SPACES             TO DA105-NAME-DMM                        
           END-IF.                                                              
                                                                                
           PERFORM 5000-WRITE-EXTRACT-REC.                                      
                                                                                
      ******************************************************************        
      * 3100-FORMAT-DETAIL-ENTRY.                                               
      *   BUILD MBS STRUCTURE AND FORMAT THE RECORD.                            
      ******************************************************************        
       3100-FORMAT-DETAIL-ENTRY.                                                
                                                                                
           IF AP045-DOC-NBR NOT EQUAL PV-PREV-DOC-NBR OR                        
              AP045-DEPT-NBR NOT EQUAL PV-PREV-DEPT-NBR OR                      
              AP045-CL-NBR NOT EQUAL PV-PREV-SUB-CL-NBR                         
               PERFORM 4000-GET-MBS-DESC                                        
               MOVE AP045-DOC-NBR      TO PV-PREV-DOC-NBR                       
               MOVE AP045-DEPT-NBR     TO PV-PREV-DEPT-NBR                      
               MOVE AP045-CL-NBR       TO PV-PREV-SUB-CL-NBR                    
           END-IF.                                                              
                                                                                
           PERFORM 3200-MOVE-AP-TO-DA-TRANS.                                    
                                                                                
      *ÙMBS STRUCTURE...                                                        
           MOVE MDSEAR-GMA-NBR         TO DA105-GMM.                            
           MOVE MDSEAR-DMA-NBR         TO DA105-DMM.                            
           MOVE MDSEAR-BYR-NBR         TO DA105-BUYER.                          
                                                                                
      * DMM NAME (DIVISIONAL MERCH MGR)...                                      
           MOVE PV-DMM-FIR-NAME        TO DP024-FNAME.                          
           MOVE PV-DMM-LAST-NAME       TO DP024-LNAME.                          
           PERFORM DP024-0000-FORMAT-NAME.                                      
           MOVE DP024-FLNAME           TO DA105-NAME-DMM.                       
                                                                                
           PERFORM 5000-WRITE-EXTRACT-REC.                                      
                                                                                
      ******************************************************************        
      * 3200-MOVE-AP-TO-DA-TRANS.                                               
      ******************************************************************        
       3200-MOVE-AP-TO-DA-TRANS.                                                
                                                                                
      * A/P TRANSACTION RECORD INFORMATION....                                  
           MOVE AP045-KEY-FIELDS       TO DA105-KEY-FIELDS.                     
           MOVE AP045-COMMON-FIELDS    TO DA105-COMMON-FIELDS.                  
           MOVE AP045-HEADER-FIELDS    TO DA105-HEADER-FIELDS.                  
           MOVE AP045-DETAIL-FIELDS    TO DA105-DETAIL-FIELDS.                  
           MOVE AP045-PYMT-DUE-DTE     TO DA105-PYMT-DUE-DTE.                   
           MOVE AP045-AUTH-NBR         TO DA105-AUTH-NBR.                       
                                                                                
      * DEBIT ALLOWANCE WORK FIELDS...                                          
           MOVE PV-APRD045-WORK-FLDS   TO DA105-WORK-FLDS.                      
                                                                                
      /****************************************************************         
      * 4000-GET-MBS-DESC.                                                      
      *   GET THE CORRECT MBS DESCRIPTION/NAME FIELDS                           
      *****************************************************************         
       4000-GET-MBS-DESC.                                                       
                                                                                
      * SET UP THE SELECT KEY VALUES FOR TMDSEAR & TRSPBAR....                  
           MOVE AP045-DEPT-NBR    TO MDSEAR-DEPT-NBR.                           
                                                                                
           STRING '0' DELIMITED BY SIZE                                         
                  AP045-CL-NBR DELIMITED BY SIZE                                
                                  INTO MDSEAR-SUB-CL-NBR.                       
                                                                                
           MOVE AP045-DOC-DTE     TO PV-EXTRACT-DTE.                            
                                                                                
      * GET MBS STRUCTURE (GMA/DMA/BUYER/DMA NAME)...                           
           PERFORM 8000-GET-EFF-MBS-SCL-INFO.                                   
                                                                                
           IF MBS-INFO-NOT-FOUND                                                
              PERFORM 8100-GET-LATEST-MBS-SCL-DATE                              
              IF MBS-MAX-DATE-FOUND                                             
                 MOVE MDSEAR-END-DTE TO PV-EXTRACT-DTE                          
                 PERFORM 8000-GET-EFF-MBS-SCL-INFO                              
                 IF MBS-INFO-NOT-FOUND                                          
                    PERFORM 4100-SET-MBS-TO-UNKNOWN                             
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 4100-SET-MBS-TO-UNKNOWN                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 4100-SET-MBS-TO-UNKNOWN.                                                
      *   SET MBS MBS DESCRIPTION/NAME FIELDS TO "UNKNOWN" SINCE NOTHING        
      * WAS FOUND FOR THE DEPT/SUB CLASS BEING PROCESSED.                       
      *****************************************************************         
       4100-SET-MBS-TO-UNKNOWN.                                                 
                                                                                
           MOVE SPACES              TO MDSEAR-GMA-NBR.                          
           MOVE SPACES              TO MDSEAR-DMA-NBR.                          
           MOVE SPACES              TO MDSEAR-BYR-NBR.                          
           MOVE SPACES              TO PV-DMM-FIR-NAME.                         
           MOVE SPACES              TO PV-DMM-LAST-NAME.                        
                                                                                
           ADD 1                    TO PV-MBS-NOT-FOUND-CNT.                    
                                                                                
      /*****************************************************************        
      * 5000-WRITE-EXTRACT-REC.                                                 
      *   CREATE A STORE INVENTORY EXCEPTIONS OUTPUT RECORD CONTAINING          
      * MATCHING TSKST INFORMATION.                                             
      ******************************************************************        
       5000-WRITE-EXTRACT-REC.                                                  
                                                                                
           WRITE DA-TRANS-FILE-REC  FROM DA105-TRANSACTION-FILE.                
                                                                                
      /****************************************************************         
      * DPPD024 COMBINES SEPARATE FIRST AND LAST NAME FIELDS INTO ONE           
      * FIRST/LAST NAME FIELD.                                                  
      *****************************************************************         
           COPY DPPD024.                                                        
                                                                                
      *****************************************************************         
      * GET THE DIFFERENT LEVELS OF MBS INFO BASED DEPT/SUBCL/EXTR DTE          
      *****************************************************************         
       8000-GET-EFF-MBS-SCL-INFO.                                               
                                                                                
           EXEC SQL                                                             
              SELECT A.GMA_NBR                                                  
                    ,B.DMA_NBR                                                  
                    ,F.WORKER_FIR_NAME                                          
                    ,F.WORKER_LAST_NAME                                         
                    ,C.BYR_NBR                                                  
              INTO  :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-DMA-NBR                                             
                   ,:PV-DMM-FIR-NAME                                            
                   ,:PV-DMM-LAST-NAME                                           
                   ,:MDSEAR-BYR-NBR                                             
               FROM TMDSEAR A                                                   
                   ,TMDSEAR B                                                   
                   ,TMDSEAR C                                                   
                   ,TMDSEAR D                                                   
                   ,TRSPBAR F                                                   
              WHERE A.OPERCO_NBR = :PC-OPERCO-NBR                               
                AND B.OPERCO_NBR = A.OPERCO_NBR                                 
                AND C.OPERCO_NBR = A.OPERCO_NBR                                 
                AND D.OPERCO_NBR = A.OPERCO_NBR                                 
                AND A.MDSELV_CDE = :PC-MDSELV-GMA                               
                AND B.MDSELV_CDE = :PC-MDSELV-DMA                               
                AND C.MDSELV_CDE = :PC-MDSELV-DPT                               
                AND D.MDSELV_CDE = :PC-MDSELV-SCL                               
                                                                                
                AND A.GMA_NBR    = B.GMA_NBR                                    
                AND B.DMA_NBR    = C.DMA_NBR                                    
                AND C.DEPT_NBR   = D.DEPT_NBR                                   
                AND C.DEPT_NBR   = :MDSEAR-DEPT-NBR                             
                AND D.SUB_CL_NBR = :MDSEAR-SUB-CL-NBR                           
                                                                                
                AND :PV-EXTRACT-DTE BETWEEN A.STRT_DTE AND A.END_DTE            
                AND :PV-EXTRACT-DTE BETWEEN B.STRT_DTE AND B.END_DTE            
                AND :PV-EXTRACT-DTE BETWEEN C.STRT_DTE AND C.END_DTE            
                AND :PV-EXTRACT-DTE BETWEEN D.STRT_DTE AND D.END_DTE            
                                                                                
                AND B.MDSEAR_DKEY = F.MDSEAR_DKEY                               
                AND F.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM                          
                AND :PV-EXTRACT-DTE BETWEEN F.WORKER_STRT_DTE AND               
                                            F.WORKER_END_DTE                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET MBS-INFO-FOUND     TO TRUE                               
               WHEN SQLCODE = +100                                              
                   IF MBS-USING-MAX-DATE                                        
                      DISPLAY ' '                                               
                      DISPLAY 'DOCUMENT: ' AP045-DOC-NBR                        
                      DISPLAY 'MBS INFO NOT FOUND FOR '                         
                              'DEPT/SUB CL ' AP045-DEPT-NBR                     
                                         '/' AP045-CL-NBR                       
                                         ' ' PV-EXTRACT-DTE                     
                      SET MBS-NOT-USING-MAX-DATE TO TRUE                        
                   END-IF                                                       
                   SET MBS-INFO-NOT-FOUND        TO TRUE                        
               WHEN OTHER                                                       
                   MOVE '8000-GET-EFF-MBS-SCL-INFO'                             
                                          TO PV-PARAGRAPH-NAME                  
                   MOVE 'SELECT ROW FROM TMDSEAR/TRSPBAR'                       
                                          TO PV-DB2-OPER-ATTEMPTED              
                   DISPLAY 'DOCUMENT: ' AP045-DOC-NBR                           
                   DISPLAY 'DEPT/SUB CL ' AP045-DEPT-NBR                        
                                         '/' AP045-CL-NBR                       
                                 'DATE ' PV-EXTRACT-DTE                         
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * GET THE MAX MBS DATE FOR THE DEPT/MCL AT THE MCL LEVEL.  ONLY           
      * DONE WHEN THE MBS INFO CAN NOT BE FOUND FOR EXTRACT DATE RANGE          
      *****************************************************************         
       8100-GET-LATEST-MBS-SCL-DATE.                                            
                                                                                
           EXEC SQL                                                             
               SELECT MAX(END_DTE)                                              
                 INTO :MDSEAR-END-DTE :PV-MAX-SCL-DATE-NULL-IND                 
                 FROM TMDSEAR                                                   
                WHERE MDSELV_CDE = :PC-MDSELV-SCL                               
                  AND DEPT_NBR   = :MDSEAR-DEPT-NBR                             
                  AND SUB_CL_NBR = :MDSEAR-SUB-CL-NBR                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      *             IF NULL IND IS < 0, MAX END DATE WAS NOT FOUND              
                    IF PV-MAX-SCL-DATE-NULL-IND < 0                             
                       DISPLAY ' '                                              
                       DISPLAY 'DOCUMENT: ' AP045-DOC-NBR                       
                       DISPLAY 'UNKNOWN WILL BE LOADED TO MBS FIELDS'           
                       SET MBS-MAX-DATE-NOT-FOUND   TO TRUE                     
                    ELSE                                                        
                       SET MBS-USING-MAX-DATE       TO TRUE                     
                       SET MBS-MAX-DATE-FOUND       TO TRUE                     
                    END-IF                                                      
                 WHEN OTHER                                                     
                     MOVE '8100-GET-LATEST-MBS-SCL-DATE'                        
                                                TO PV-PARAGRAPH-NAME            
                     MOVE 'SELECT ROW FROM TMDSEAR'                             
                                                TO PV-DB2-OPER-ATTEMPTED        
                     DISPLAY 'DOCUMENT: ' AP045-DOC-NBR                         
                     DISPLAY 'MBS MAX END DATE NOT FOUND FOR '                  
                             'DEPT/SUB CL ' AP045-DEPT-NBR                      
                                        '/' AP045-CL-NBR                        
                                       ' ' PV-EXTRACT-DTE                       
                     PERFORM 9900-SQL-ABEND                                     
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * 8500-READ-AP-TRANS-FILE.                                                
      *   READ THE FORMATTED AP TRANSACTION FILE RECORD.                        
      *****************************************************************         
       8500-READ-AP-TRANS-FILE.                                                 
                                                                                
           READ AP-TRANS-FILE-IN INTO AP045-TRANSACTION-FILE                    
               AT END  MOVE 'Y' TO PS-MBS-DONE-SW                               
           END-READ.                                                            
                                                                                
      /*****************************************************************        
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.                
      ******************************************************************        
       9000-END-OF-JOB-TASKS.                                                   
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '******************************************'.                
           DISPLAY '*  END OF DAKUT320                       *'.                
           DISPLAY '******************************************'.                
                                                                                
           CLOSE AP-TRANS-FILE-IN                                               
                 DA-TRANS-FILE-OUT.                                             
                                                                                
      /****************************************************************         
      * 9900-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      *****************************************************************         
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
