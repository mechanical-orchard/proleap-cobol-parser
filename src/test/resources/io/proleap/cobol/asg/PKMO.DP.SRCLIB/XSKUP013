       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    XSKUP013.                                         00020001
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  09/29/2002.                                       00050009
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070009
      **       VSM ITEM MESSAGE - NUMBER 055 - SKU ATTR CHNG            00080009
      ******************************************************************00090009
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100021
      ***     ATTRIBUTES:                                               00110021
      ***     1. SKU_DESC                                               00120021
      ***     2. TX_PRD_CDE_DESC                                        00130021
      ***     3. REGN_PRIC_IND                                          00140021
      ******************************************************************00150009
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00160009
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00170018
011703**01/17/2003 CHANGED BY:JULIE SMITH  TAX DESC FIX                 00180022
022103**02/21/2003 CHANGED BY:JULIE SMITH  COMPARE INTRL UPC ID AND     00190023
022103**           UPC ID ON MESSAGE AND IF EQUAL PROCESS MESSAGE       00200023
022103**           ELSE DO NOT PROCESS BECAUSE IT IS A RENUMBER SKU     00210023
022103**03/12/2003 CHANGED BY:CHRIS CLARK ADDED LOGIC TO UPDATE         00220037
      **           XST_SKU_LOC TABLE AND DELETE RECORDS FROM            00230037
      **           XST_SKU_LOC_ATTR TABLE.                              00240037
022103**09/11/2003 CHANGED BY:SANDY POTTER  IMPROVED RCP LOGIC TO RETURN00241042
      **           ZERO SQLCODE IF UPDATE/DELETE OF STORE SPECIFIC ROWS 00242042
      **           FINDS NO STORE SPECIFIC ROWS (CHANGE 100 VALUE TO 0  00243047
      **           BECAUSE NOT FINDING A ROW TO DELETE IS OK).          00244047
022103**01/10/2005 CHANGED BY:SANDY POTTER  WHERE 'PEKUP040' IS USED,   00245049
      **           ALSO SPECIFY 'IMKUP039'.  CHANGE MADE FOR NEW PERM   00246049
      **           PROJECT, FOR WHICH IMK PROGRAM IS REPLACING PEK      00247049
      **           PROGRAM.                                             00248049
      ******************************************************************00250009
       ENVIRONMENT DIVISION.                                            00260001
       CONFIGURATION SECTION.                                           00270001
       SOURCE-COMPUTER.  IBM-3090.                                      00280001
       OBJECT-COMPUTER.  IBM-3090.                                      00290001
       DATA DIVISION.                                                   00300001
       WORKING-STORAGE SECTION.                                         00310001
      ******************************************************************00320001
      **PROGRAM SWITCHES                                                00330001
      ******************************************************************00340001
       01  PS-PROGRAM-SWITCHES.                                         00350001
           05  PS-PROCESS-CONTINUE-SW      PIC X(01) VALUE 'N'.         00360009
               88  PS-PROCESS-CONTINUE               VALUE 'Y'.         00370034
               88  PS-PROCESS-COMPLETE               VALUE 'N'.         00380009
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00390015
               88  PS-ACTION-REVERSE                  VALUE 01.         00400015
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00410015
               88  PS-ACTION-NOTHING                  VALUE 02.         00420015
           05  PS-BYPASS-RENUM-IND         PIC X(01)  VALUE 'N'.        00420141
               88  PS-BYPASS-RENUM                   VALUE 'Y'.         00420240
               88  PS-NOT-A-RENUM                    VALUE 'N'.         00420340
                                                                        00420440
      ******************************************************************00430001
      **PROGRAM COUNTERS                                                00440012
      ******************************************************************00450001
       01  PC-PROGRAM-COUNTERS.                                         00460012
           05  PC-MAX-RETRIES              PIC 9(01) VALUE 5.           00470012
           05  PC-RETRIES                  PIC 9(01) VALUE ZEROS.       00480012
      ******************************************************************00490006
      **PROGRAM CALCULATIONS                                            00500006
      ******************************************************************00510006
       01  PC-PROGRAM-CALCULATIONS.                                     00520006
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00530006
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00540006
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00550006
      ******************************************************************00560001
      **PROGRAM VARIABLES                                               00570012
      ******************************************************************00580001
       01  PV-PROGRAM-VARIABLES.                                        00590012
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00600012
           05  PV-RETURN-SQLCODE           PIC S9(04) VALUE ZEROS.      00600142
      ******************************************************************00610001
      **COPYBOOKS                                                       00620001
      ******************************************************************00630001
      ***ABEND COPYBOOK                                                 00640001
           COPY DPWS004.                                                00650042
      ***DB2 MESSAGE AREA                                               00660001
           COPY SQLCA2.                                                 00670042
      ***ITEM COPYBOOK                                                  00680009
           COPY IMRD008.                                                00690042
      ******************************************************************00700001
      **TABLE INCLUSIONS                                                00710001
      ******************************************************************00720001
      ***DB2 COMMUNICATIONS AREA                                        00730001
           EXEC SQL                                                     00740001
                INCLUDE SQLCA                                           00750001
           END-EXEC.                                                    00760001
      ***XST_SKU_LOC TABLE                                              00770026
           EXEC SQL                                                     00780026
               INCLUDE XST00008                                         00790026
           END-EXEC.                                                    00800026
      ***XST_SKU TABLE                                                  00810001
           EXEC SQL                                                     00820001
               INCLUDE XST00010                                         00830001
           END-EXEC.                                                    00840001
      ***XST_SKU_LOC_ATTR TABLE                                         00850026
           EXEC SQL                                                     00860026
               INCLUDE XST00041                                         00870026
           END-EXEC.                                                    00880026
                                                                        00890001
       LINKAGE SECTION.                                                 00900001
      ******************************************************************00910001
      **LINKAGE VARIABLES                                               00920001
      ******************************************************************00930001
       01  LV-IMRD008-RECORD               PIC X(450).                  00940009
       01  LV-RETURN-CODE                  PIC S9(4).                   00950001
       01  LV-ACTION-IND                   PIC 9(02).                   00960015
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00970015
                                                                        00980001
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00990009
                                LV-RETURN-CODE,                         01000015
                                LV-ACTION-IND,                          01010015
                                LV-CURRENT-TIMESTAMP.                   01020015
      ******************************************************************01030001
       0000-MAINLINE.                                                   01040001
                                                                        01050001
            PERFORM 1000-INITIALIZATION.                                01060001
                                                                        01061042
            PERFORM 2000-PROCESS-MESSAGE                                01070009
                    UNTIL PS-PROCESS-COMPLETE.                          01080035
            MOVE SQLCODE TO PV-RETURN-SQLCODE.                          01080143
                                                                        01080240
            IF PS-NOT-A-RENUM  AND                                      01090040
               SQLCODE = 0                                              01091040
               IF IM008-SKU-ATTR-REG-PRC-IND-CHG                   AND  01100050
                  IM008-SKU-ATTR-REGN-PRIC-IND-N      = 'N'        AND  01110050
                  (IM008-SKU-ATTR-LAST-UPD-USR-ID NOT = 'PEKUP040' AND  01120050
                   IM008-SKU-ATTR-LAST-UPD-USR-ID NOT = 'IMKUP039')     01120150
                                                                        01130026
                  SET PS-PROCESS-CONTINUE  TO TRUE                      01140035
                  MOVE IM008-SKU-ATTR-LAST-UPD-TMST                     01150035
                                           TO XST00008-END-TMST         01160035
                  MOVE IM008-CRE-TMST      TO XST00008-SOR-LAST-MSG-TMST01170035
                                                                        01180031
                  PERFORM 3000-UPDATE-SKU-LOC                           01190035
                          UNTIL PS-PROCESS-COMPLETE                     01200035
                                                                        01210038
                  IF SQLCODE = 0 OR SQLCODE = +100                      01220042
                                                                        01232042
                     SET PS-PROCESS-CONTINUE   TO TRUE                  01240038
                     PERFORM 3500-DELETE-SKU-LOC                        01250038
                             UNTIL PS-PROCESS-COMPLETE                  01260038
                                                                        01270026
                     IF SQLCODE = 0 OR SQLCODE = +100                   01280038
                        SET PS-PROCESS-CONTINUE   TO TRUE               01290038
                        PERFORM 4000-DELETE-SKU-LOC-ATTR                01310038
                                UNTIL PS-PROCESS-COMPLETE               01320038
                     END-IF                                             01330442
                  END-IF                                                01330548
                                                                        01330648
                  IF SQLCODE = 0 OR SQLCODE = +100                      01330748
                     MOVE ZERO TO PV-RETURN-SQLCODE                     01330948
                  ELSE                                                  01331442
                     MOVE SQLCODE TO PV-RETURN-SQLCODE                  01332042
                     DISPLAY '** XSKUP013 SQLCODE '                     01333048
                             PV-RETURN-SQLCODE                          01334048
                             ' IN PARAGRAPH '                           01335048
                             PV-PARAGRAPH-NAME                          01336048
                  END-IF                                                01340038
               END-IF                                                   01350035
            END-IF.                                                     01360024
            PERFORM 9990-WRAPUP.                                        01370044
                                                                        01380001
       EJECT.                                                           01390006
      ***************************************************************** 01400001
      **INITIALIZATION AREA                                             01410001
      ***************************************************************** 01420001
       1000-INITIALIZATION.                                             01430001
                                                                        01440001
           MOVE '1000-INITIALIZATION'       TO PV-PARAGRAPH-NAME.       01450012
                                                                        01460001
           INITIALIZE PV-PROGRAM-VARIABLES.                             01470013
           MOVE LV-IMRD008-RECORD           TO IMRD008-RECORD.          01480014
                                                                        01490006
           MOVE IM008-SKU-ATTR-SKU-DESC-NEW TO XST00010-SKU-DESC.       01500006
011703     MOVE IM008-SKU-ATTR-TX-PRD-CDE-DE-N                          01510022
                                            TO XST00010-TX-PRD-CDE-DESC.01520006
           MOVE IM008-SKU-ATTR-REGN-PRIC-IND-N                          01530011
                                           TO XST00010-REGN-PRIC-IND.   01540011
                                                                        01550006
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01560010
      *     COMP FIELDS.                                                01570010
           MOVE IM008-ITEM-KEY-INTR-UPC-ID  TO PC-10-DIGIT.             01580006
           MOVE PC-10-DIGIT                 TO PC-18-DIGIT.             01590006
           MOVE PC-18-DIGIT                 TO PC-09-DIGIT.             01600006
           MOVE PC-09-DIGIT                 TO XST00008-INTR-UPC-ID     01610031
                                               XST00010-INTR-UPC-ID     01620031
                                               XST00041-INTR-UPC-ID.    01630031
                                                                        01640010
121402     MOVE LV-CURRENT-TIMESTAMP        TO XST00010-LAST-UPD-TMST.  01650020
121402     MOVE IM008-CRE-TMST            TO XST00010-SOR-LAST-MSG-TMST.01660051
121402     MOVE IM008-SKU-ATTR-GP-CDE-NEW TO XST00010-GP-CDE.           01661051
                                                                        01670001
022103*DETERMINES IF MESSAGE SHOULD BE PROCESSED OR PASSED IF FIELDS    01680023
022103*    ARE NOT EQUAL THEN MESSAGE IS FOR THE RENUMBERED SKU         01690023
022103*    AND SHOULD NOT BE PROCESSED AT THE SKU LEVEL                 01700023
022103     IF  IM008-ITEM-KEY-INTR-UPC-ID = IM008-ITEM-KEY-UPC-ID       01710023
022103         SET PS-PROCESS-CONTINUE TO TRUE                          01720023
022103         SET PS-NOT-A-RENUM      TO TRUE                          01721040
022103     ELSE                                                         01730023
022103         SET PS-PROCESS-COMPLETE TO TRUE                          01740023
022103         SET PS-BYPASS-RENUM     TO TRUE                          01741040
022103     END-IF.                                                      01750023
                                                                        01760023
       EJECT.                                                           01770006
      ***************************************************************** 01780001
      **UPDATES A ATTRIBUTES ON XST_SKU TABLE                           01790021
      ***************************************************************** 01800001
       2000-PROCESS-MESSAGE.                                            01810009
                                                                        01820001
           MOVE '2000-PROCESS-MESSAGE'             TO PV-PARAGRAPH-NAME.01830012
                                                                        01840001
           EXEC SQL                                                     01850006
              UPDATE XST_SKU                                            01860006
                  SET   SKU_DESC        = :XST00010-SKU-DESC            01870007
                      , TX_PRD_CDE_DESC = :XST00010-TX-PRD-CDE-DESC     01880006
                      , REGN_PRIC_IND   = :XST00010-REGN-PRIC-IND       01890011
121402                , LAST_UPD_TMST   = :XST00010-LAST-UPD-TMST       01900018
121402                , SOR_LAST_MSG_TMST = :XST00010-SOR-LAST-MSG-TMST 01910018
                      , GP_CDE            = :XST00010-GP-CDE            01911051
                  WHERE INTR_UPC_ID     = :XST00010-INTR-UPC-ID         01920006
           END-EXEC.                                                    01930006
                                                                        01940006
           EVALUATE TRUE                                                01950006
      ***SUCCESSFUL PROCESSING                                          01960021
              WHEN SQLCODE = ZERO                                       01970006
                   SET PS-PROCESS-COMPLETE         TO TRUE              01980035
      ***ROW/TABLE LOCK                                                 01990021
              WHEN SQLCODE = -904                                       02000039
              WHEN SQLCODE = -911                                       02010039
              WHEN SQLCODE = -913                                       02020006
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     02030012
                       SET PS-PROCESS-COMPLETE TO TRUE                  02040009
                   ELSE                                                 02050006
                       ADD +1                      TO PC-RETRIES        02060012
                       SET PS-PROCESS-CONTINUE     TO TRUE              02070009
                   END-IF                                               02080006
              WHEN OTHER                                                02090006
                   SET PS-PROCESS-COMPLETE         TO TRUE              02100009
           END-EVALUATE.                                                02110006
                                                                        02120001
       EJECT.                                                           02130006
      ***************************************************************** 02140024
      **UPDATES (ENDS) CURRENT STORE SPECIFIC ROWS ON XST_SKU_LOC TABLE 02150042
      ***************************************************************** 02160024
       3000-UPDATE-SKU-LOC.                                             02170024
                                                                        02180024
           MOVE '3000-UPDATE-SKU-LOC'     TO PV-PARAGRAPH-NAME.         02190026
                                                                        02200024
           EXEC SQL                                                     02210024
              UPDATE XST_SKU_LOC                                        02220024
                  SET  END_TMST          = :XST00008-END-TMST           02230026
                     , LAST_UPD_TMST     = CURRENT TIMESTAMP            02240030
                     , SOR_LAST_MSG_TMST = :XST00008-SOR-LAST-MSG-TMST  02250026
                  WHERE INTR_UPC_ID      = :XST00008-INTR-UPC-ID        02260026
                    AND LOC_NBR          > 0                            02270024
                    AND (END_TMST        > :XST00008-END-TMST           02280026
                     OR END_TMST         IS NULL)                       02290026
                    AND BEG_TMST         < :XST00008-END-TMST           02300039
           END-EXEC.                                                    02310024
                                                                        02320024
           EVALUATE TRUE                                                02330024
      ***SUCCESSFUL PROCESSING                                          02340024
              WHEN SQLCODE = ZERO                                       02350024
              WHEN SQLCODE = +100                                       02351042
                   SET PS-PROCESS-COMPLETE         TO TRUE              02360024
      ***ROW/TABLE LOCK                                                 02370024
              WHEN SQLCODE = -904                                       02380039
              WHEN SQLCODE = -911                                       02390039
              WHEN SQLCODE = -913                                       02400024
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     02410024
                       SET PS-PROCESS-COMPLETE TO TRUE                  02420024
                   ELSE                                                 02430024
                       ADD +1                      TO PC-RETRIES        02440024
                       SET PS-PROCESS-CONTINUE     TO TRUE              02450024
                   END-IF                                               02460024
              WHEN OTHER                                                02470024
                   SET PS-PROCESS-COMPLETE         TO TRUE              02480024
           END-EVALUATE.                                                02490024
                                                                        02500032
           IF SQLCODE = +100                                            02501042
              DISPLAY 'XSKUP013 - NO STORE ROWS TO END FOR INTR UPC ID '02510048
                      IM008-ITEM-KEY-INTR-UPC-ID                        02510142
           END-IF.                                                      02511042
                                                                        02520024
       EJECT.                                                           02530024
      ***************************************************************** 02540038
      **DELETES FUTURE STORE SPECIFIC ROWS FROM XST_SKU_LOC TABLE       02550042
      ***************************************************************** 02560038
       3500-DELETE-SKU-LOC.                                             02570038
                                                                        02580038
           MOVE '3500-DELETE-SKU-LOC'    TO PV-PARAGRAPH-NAME.          02590038
                                                                        02600038
           EXEC SQL                                                     02610038
              DELETE FROM XST_SKU_LOC                                   02620038
                  WHERE INTR_UPC_ID      = :XST00008-INTR-UPC-ID        02630038
                    AND LOC_NBR          > 0                            02640038
                    AND BEG_TMST        >= :XST00008-END-TMST           02650039
           END-EXEC.                                                    02660038
                                                                        02670038
           EVALUATE TRUE                                                02680038
      ***SUCCESSFUL PROCESSING                                          02690038
              WHEN SQLCODE = ZERO                                       02700038
              WHEN SQLCODE = +100                                       02701042
                   SET PS-PROCESS-COMPLETE         TO TRUE              02710038
      ***ROW/TABLE LOCK                                                 02740038
              WHEN SQLCODE = -904                                       02750039
              WHEN SQLCODE = -911                                       02760039
              WHEN SQLCODE = -913                                       02770038
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     02780038
                       SET PS-PROCESS-COMPLETE TO TRUE                  02790038
                   ELSE                                                 02800038
                       ADD +1                      TO PC-RETRIES        02810038
                       SET PS-PROCESS-CONTINUE     TO TRUE              02820038
                   END-IF                                               02830038
              WHEN OTHER                                                02840038
                   SET PS-PROCESS-COMPLETE         TO TRUE              02850038
           END-EVALUATE.                                                02860038
                                                                        02870038
       EJECT.                                                           02880038
      ***************************************************************** 02890026
      **DELETES STORE SPECIFIC ROWS FROM XST_SKU_LOC_ATTR TABLE         02900042
      ***************************************************************** 02910026
       4000-DELETE-SKU-LOC-ATTR.                                        02920026
                                                                        02930026
           MOVE '4000-DELETE-SKU-LOC-ATTR' TO PV-PARAGRAPH-NAME.        02940026
                                                                        02950026
           EXEC SQL                                                     02960026
              DELETE FROM XST_SKU_LOC_ATTR                              02970026
                  WHERE INTR_UPC_ID      = :XST00041-INTR-UPC-ID        02980026
                    AND LOC_NBR          > 0                            02990026
           END-EXEC.                                                    03000026
                                                                        03010026
           EVALUATE TRUE                                                03020026
      ***SUCCESSFUL PROCESSING                                          03030026
              WHEN SQLCODE = ZERO                                       03040026
              WHEN SQLCODE = +100                                       03041042
                   SET PS-PROCESS-COMPLETE         TO TRUE              03050026
      ***ROW/TABLE LOCK                                                 03060026
              WHEN SQLCODE = -904                                       03070039
              WHEN SQLCODE = -911                                       03080039
              WHEN SQLCODE = -913                                       03090026
                   IF  PC-RETRIES >= PC-MAX-RETRIES                     03100026
                       SET PS-PROCESS-COMPLETE TO TRUE                  03110026
                   ELSE                                                 03120026
                       ADD +1                      TO PC-RETRIES        03130026
                       SET PS-PROCESS-CONTINUE     TO TRUE              03140026
                   END-IF                                               03150026
              WHEN OTHER                                                03160026
                   SET PS-PROCESS-COMPLETE         TO TRUE              03170026
           END-EVALUATE.                                                03180026
                                                                        03181042
           IF SQLCODE = +100                                            03182042
              DISPLAY 'XSKUP013 - NO STORE SKU LOC ATTR ROWS TO DELETE' 03183048
                      ' FOR INTR UPC ID ' IM008-ITEM-KEY-INTR-UPC-ID    03184048
           END-IF.                                                      03185042
                                                                        03190026
       EJECT.                                                           03200026
      ******************************************************************03210001
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             03220001
      ******************************************************************03230001
       9990-WRAPUP.                                                     03240044
                                                                        03250001
           MOVE '9990-WRAPUP'         TO PV-PARAGRAPH-NAME              03260044
                                                                        03270001
           MOVE PV-RETURN-SQLCODE     TO LV-RETURN-CODE.                03280042
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 03290015
                                                                        03300015
           EXIT PROGRAM.                                                03310001
                                                                        03320001
