       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.    MLKRP220.                                                 
       AUTHOR.        RONNA BUTZ.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/09/99.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *     MLKRP220 - DETAILED SUMMARY REPORT OF WHEN SALES                    
      *     TRANSACTIONS SHOULD                                                 
      *                HAVE BEEN POSTED, THIS INCLUDES THE DEPT,                
      *                CLASS AND SKU.                                           
      *                                                                         
      * THIS PROGRAM READS AN EXTRACT CONTAINING BACK POSTED SALES              
      * TRANSACTIONS, AND CREATES A REPORT.  THE INPUT FILE IS SORTED           
      * BY STORE NUMBER, FINANCIAL LIABLITY DATE, AND SALES CORRECTION          
      * CODE.                                                                   
      *                                                                         
      * THE REPORT BREAKS AND TOTALS AT THE STORE LEVEL.  THERE ARE             
      * ALSO GRAND TOTALS                                                       
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      *   DATE :  11/15/99      CHANGE ID : WR20418    WHO : TKMA523            
      * CORRECTED VERBAGE ON REPORT WHEN THERE ISN'T ANY INPUT DATA.            
      *   DATE :  05/07/02      CHANGE ID : WR21731    WHO : TKMA372            
      * NEW SKU CHANGES TO ADD SKU AND ITS ATTRIBUTES TO REPORT.                
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
W33304* W33304  11-16-2008 D. THEEL CHANGES FOR HILO PROJECT                    
W33304*                    CHANGED 8000-GET-TINVPAR SQL                         
      ******************************************************************        
      *                                                                         
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BACK-POSTED-EXTRACT-FILE                                      
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT CNTL-DATE-FILE                                                
               ASSIGN TO INP02.                                                 
      *                                                                         
           SELECT BACK-POSTED-REPORT-FILE                                       
               ASSIGN RPT01.                                                    
      *                                                                         
      *                                                                         
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  BACK-POSTED-EXTRACT-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  BACK-POSTED-EXTRACT-RECORD            PIC  X(80).                    
      *                                                                         
       FD  CNTL-DATE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  CNTL-DATE-RECORD                      PIC  X(80).                    
      *                                                                         
       FD  BACK-POSTED-REPORT-FILE                                              
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  BACK-POSTED-REPORT-RECORD             PIC  X(132).                   
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
      *****************************************************************         
      * RECORD LAYOUT FOR BACK POSTING INPUT FILE                               
      *****************************************************************         
      *                                                                         
           COPY MLRD022.                                                        
      *                                                                         
      *****************************************************************         
      * CONTROL CARD LAYOUT FOR MLCC015                                         
      * (CODE FROM PGM=MLKUP021 FOR CONTROL CARD DATE PROCESSING)               
      *****************************************************************         
       01  CC-CONTROL-CARD.                                                     
           05  CC-CALENDAR-TYPE                PIC X(02).                       
           05  FILLER                          PIC X.                           
           05  CC-PROC-DATE-ISO                PIC X(10).                       
           05  CC-PROC-DATE-RDF  REDEFINES                                      
               CC-PROC-DATE-ISO.                                                
               10  CC-PROC-CC                  PIC X(02).                       
               10  CC-PROC-YY                  PIC X(02).                       
               10  FILLER                      PIC X(01).                       
               10  CC-PROC-MM                  PIC X(02).                       
               10  FILLER                      PIC X(01).                       
               10  CC-PROC-DD                  PIC X(02).                       
           05  FILLER                          PIC X.                           
           05  CC-PROCESSING-MODE              PIC X(04).                       
               88  CC-PROCESSING-MODE-VALID    VALUE 'ML'  'INV' 'BOTH'.        
               88  CC-PROCESSING-MODE-ML       VALUE 'ML'  'BOTH'.              
               88  CC-PROCESSING-MODE-ML-ONLY  VALUE 'ML'.                      
               88  CC-PROCESSING-MODE-INV      VALUE 'INV' 'BOTH'.              
               88  CC-PROCESSING-MODE-INV-ONLY VALUE 'INV'.                     
           05  FILLER                          PIC X.                           
           05  CC-CHECK-INV-DATES-SW           PIC X.                           
               88  CC-CHECK-INV-DATES-SW-VALID VALUE 'Y' 'N'.                   
               88  CC-CHECK-INV-DATES          VALUE 'Y'.                       
               88  CC-DONT-CHECK-INV-DATES     VALUE 'N'.                       
           05  FILLER                          PIC X(60).                       
                                                                                
      *****************************************************************         
      * 100 CHARACTER REPORT LAYOUT                                             
      *****************************************************************         
      *                                                                         
      *    COPY DPWS100O.                                                       
      *                                                                         
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
      *                                                                         
           COPY DPWS004.                                                        
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX                  PIC S9(04)     VALUE +60.           
      *                                                                         
       01  HN-HOLD-NUMBERS.                                                     
W26603     05  HN-STORE-NBR                 PIC  X(05)     VALUE ZERO.          
W26603     05  HN-STORE-NBR-NUM REDEFINES                                       
W26603         HN-STORE-NBR                 PIC  9(05).                         
           05  HN-FIN-LIAB-DTE              PIC  X(10)     VALUE SPACES.        
W21731     05  HN-DEPT-NBR                  PIC  9(04)     VALUE ZERO.          
W21731     05  HN-CLASS                     PIC  9(04)     VALUE ZERO.          
W21731     05  HN-SKU                       PIC  X(08)     VALUE SPACES.        
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-EXTRACT-SW         PIC X          VALUE 'N'.           
               88 PS-END-OF-EXTRACT                        VALUE 'Y'.           
           05  CNTL-DATE-FILE-SW            PIC X          VALUE 'N'.           
               88  END-OF-CNTL-DTE-FILE                    VALUE 'Y'.           
           05  PS-BREAK-LEVEL-SW            PIC X          VALUE SPACE.         
               88  PS-BREAK-LEVEL-SW-INIT                  VALUE ' '.           
               88  PS-BREAK-IS-STORE                       VALUE 'S'.           
               88  PS-BREAK-IS-DETAIL                      VALUE 'D'.           
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-CNT                  PIC S9(05)     COMP-3               
                                                           VALUE ZERO.          
           05  PV-LINE-COUNT                PIC S9(05)     COMP-3               
                                                           VALUE ZERO.          
           05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACE.         
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)     VALUE SPACE.         
           05  PV-ABEND-CODE                PIC S9(04)     COMP                 
                                                           VALUE ZERO.          
           05  PV-NULL-INDICATOR            PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
           05  PV-HOLD-SKU                  PIC  S9(8)V COMP-3                  
                                                       VALUE ZERO.              
      *                                                                         
       01  DT-DATE-TOTALS.                                                      
           05  DT-TOT-AMT                   PIC S9(13)V99  COMP-3               
                                                           VALUE ZERO.          
           05  DT-LATE-AMT                  PIC S9(13)V99  COMP-3               
                                                           VALUE ZERO.          
           05  DT-SALES-AMT                 PIC S9(13)V99  COMP-3               
                                                           VALUE ZERO.          
      *                                                                         
       01  ST-STORE-TOTALS.                                                     
           05  ST-TOT-AMT                   PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
           05  ST-LATE-AMT                  PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
           05  ST-SALES-AMT                 PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
      *                                                                         
       01  GT-GRAND-TOTALS.                                                     
           05  GT-TOT-AMT                   PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
           05  GT-LATE-AMT                  PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
           05  GT-SALES-AMT                 PIC S9(13)V99  COMP-3               
                                                           VALUE ZEROES.        
      *                                                                         
       01  DB2-DATE-FMT.                                                        
           05  DB2-YEAR                         PIC  X(04) VALUE ZEROES.        
           05  FILLER                           PIC  X(01) VALUE '-'.           
           05  DB2-MONTH                        PIC  X(02) VALUE ZEROES.        
           05  FILLER                           PIC  X(01) VALUE '-'.           
           05  DB2-DAY                          PIC  X(02) VALUE ZEROES.        
      *                                                                         
       01  RPT-DATE-FMT.                                                        
           05  RPT-MONTH                        PIC  X(02) VALUE ZEROES.        
           05  FILLER                           PIC  X(01) VALUE '/'.           
           05  RPT-DAY                          PIC  X(02) VALUE ZEROES.        
           05  FILLER                           PIC  X(01) VALUE '/'.           
           05  RPT-YEAR                         PIC  X(04) VALUE ZEROES.        
      *                                                                         
       01  TD-TODAYS-DATE.                                                      
           05  TD-YEAR                          PIC  9(02) VALUE ZEROES.        
           05  TD-MONTH                         PIC  9(02) VALUE ZEROES.        
           05  TD-DAY                           PIC  9(02) VALUE ZEROES.        
      *                                                                         
       01  TT-TODAYS-TIME.                                                      
           05  TT-HOUR                          PIC  9(02) VALUE ZEROES.        
           05  TT-MINUTE                        PIC  9(02) VALUE ZEROES.        
           05  FILLER                           PIC  9(04) VALUE ZEROES.        
      *                                                                         
00001  01  DP132O-STANDRD-HEADER-132-COLS.                              11/25/92
00002      05  DP132O-LASER-NEW-PAGE-INDICATR    PIC X(02) VALUE SPACE. DPWS132O
00003      05  DP132O-RUN-DATE.                                            LV006
00004          10  DP132O-RUN-MONTH              PIC 9(02) VALUE ZERO.  DPWS132O
00005          10  FILLER                        PIC X(01) VALUE '/'.   DPWS132O
00006          10  DP132O-RUN-DAY                PIC 9(02) VALUE ZERO.  DPWS132O
00007          10  FILLER                        PIC X(01) VALUE '/'.   DPWS132O
00008          10  DP132O-RUN-YEAR               PIC 9(02) VALUE ZERO.  DPWS132O
00009      05  FILLER                            PIC X(02) VALUE SPACE. DPWS132O
00010      05  DP132O-RUN-TIME.                                         DPWS132O
00011          10  DP132O-RUN-HOUR               PIC 9(02) VALUE ZERO.  DPWS132O
00012          10  FILLER                        PIC X(01) VALUE ':'.   DPWS132O
00013          10  DP132O-RUN-MINUTE             PIC 9(02) VALUE ZERO.  DPWS132O
00014      05  FILLER                            PIC X(27) VALUE SPACE. DPWS132O
00015      05  FILLER                            PIC X(45)              DPWS132O
00016          VALUE 'K O H L S   D E P A R T M E N T   S T O R E S'.   DPWS132O
00017      05  FILLER                            PIC X(21) VALUE SPACE. DPWS132O
00018      05  DP132O-PROGRAM-NAME               PIC X(08) VALUE SPACE. DPWS132O
00019      05  FILLER                            PIC X(01) VALUE '-'.   DPWS132O
00020      05  DP132O-REPORT-NUMBER              PIC 9(01) VALUE ZERO.  DPWS132O
00021      05  FILLER                            PIC X(02) VALUE SPACE. DPWS132O
00022      05  FILLER                            PIC X(05) VALUE 'PAGE'.DPWS132O
00023      05  DP132O-PAGE-NUMBER                PIC ZZZZ9 VALUE ZERO.  DPWS132O
00024                                                                   DPWS132O
      *                                                                         
       01  PH2-PAGE-HEADING-2.                                                  
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  FILLER                           PIC  X(43) VALUE SPACES.        
           05  FILLER                           PIC  X(44) VALUE                
                         'SALES CORRECTION BACK POSTING DETAIL REPORT '.        
           05  FILLER                           PIC  X(43) VALUE SPACES.        
                                                                                
      *                                                                 DPWS100O
       01  PH3-PAGE-HEADING-3.                                                  
           05  FILLER                           PIC  X(54) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE 'FOR '.        
           05  PH2-FSCL-MN-NM                   PIC  X(09) VALUE SPACES.        
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  PH2-FSCL-YR-NBR                  PIC  X(04) VALUE SPACES.        
           05  FILLER                           PIC  X(06) VALUE                
                                                               ' WEEK '.        
           05  PH2-WEEK-NBR                     PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(53) VALUE SPACES.DPWS100O
                                                                                
      *                                                                 DPWS100O
       01  DH1-DETAIL-HEADING1.                                                 
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(11) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(06) VALUE SPACES.        
           05  FILLER                           PIC  X(06) VALUE                
                                                              'STORE '.         
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE                
                                                                'DEPT'.         
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(05) VALUE                
                                                               ' SCL '.         
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(09) VALUE                
                                                           '   SKU   '.         
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           ' INV DATE '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           'SALES DATE'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '  TOTAL AMOUNT  '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '   LATE AMOUNT  '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '  SALES AMOUNT  '.        
           05  FILLER                           PIC  X(12) VALUE SPACES.        
      *                                                                         
       01  UL1-UNDER-LINE1.                                                     
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(11) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(06) VALUE SPACES.        
           05  FILLER                           PIC  X(06) VALUE                
                                                               '______'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE                
                                                                 '____'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(05) VALUE                
                                                                '_____'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(09) VALUE                
                                                           '_________'.         
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           '__________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           '__________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(05) VALUE SPACES.        
      *                                                                         
       01  TUL1-TOTAL-UNDER-LINE1.                                              
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  FILLER                           PIC  X(61) VALUE SPACES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(12) VALUE SPACES.        
      *                                                                         
       01  DL1-DETAIL-LINE1.                                                    
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(11) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(06) VALUE SPACES.        
W26603     05  DL1-STORE-NBR                    PIC  X(05) VALUE SPACES.        
W26603     05  FILLER                           PIC  X(03) VALUE SPACES.        
W21731     05  DL1-DEPT-NBR                     PIC  ZZZ9  VALUE ZERO.          
W21731     05  FILLER                           PIC  X(01) VALUE SPACES.        
W21731     05  DL1-CLASS                        PIC  ZZZ9  VALUE ZERO.          
W21731     05  FILLER                           PIC  X(03) VALUE SPACES.        
W21731     05  DL1-SKU                          PIC  X(08) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE SPACES.        
           05  DL1-INV-DTE                      PIC  X(10) VALUE SPACES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL1-SALES-DTE                    PIC  X(10) VALUE SPACES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL1-TOT-AMT                      PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL1-LATE-AMT                     PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL1-SALES-AMT                    PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(12) VALUE SPACES.        
      *                                                                         
       01  TL1-TOTAL-LINE1.                                                     
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  FILLER                           PIC  X(11) VALUE SPACES.        
           05  TL1-DETAIL-DESC                  PIC  X(16) VALUE SPACES.        
W26603     05  TL1-STORE                        PIC  X(05) VALUE SPACES.        
W26603     05  FILLER                           PIC  X(32) VALUE SPACES.        
           05  TL1-TOT-AMT                      PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  TL1-LATE-AMT                     PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  TL1-SALES-AMT                    PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(12) VALUE SPACES.        
      *                                                                         
       01  DH2-DETAIL-HEADING2.                                                 
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(17) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(12) VALUE SPACES.        
            5  FILLER                           PIC  X(06) VALUE                
                                                               'STORE '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE                
                                                                 'DEPT'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(05) VALUE                
                                                                'CLASS'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
W21731*    05  FILLER                           PIC  X(04) VALUE                
W21731     05  FILLER                           PIC  X(09) VALUE                
                                                           '   SKU   '.         
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           'SALES DATE'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '  TOTAL AMOUNT  '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '   LATE AMOUNT  '.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '  SALES AMOUNT  '.        
           05  FILLER                           PIC  X(19) VALUE SPACES.        
      *                                                                         
       01  UL2-UNDER-LINE2.                                                     
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(17) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(12) VALUE SPACES.        
           05  FILLER                           PIC  X(06) VALUE                
                                                               '______'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE                
                                                                 '____'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(05) VALUE                
                                                                '_____'.        
           05  FILLER                           PIC  X(01) VALUE SPACES.        
           05  FILLER                           PIC  X(09) VALUE                
                                                           '_________'.         
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(10) VALUE                
                                                           '__________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(19) VALUE SPACES.        
      *                                                                         
       01  TUL2-TOTAL-UNDER-LINE2.                                              
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  FILLER                           PIC  X(57) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  FILLER                           PIC  X(16) VALUE                
                                                     '________________'.        
           05  FILLER                           PIC  X(19) VALUE SPACES.        
      *                                                                         
       01  DL2-DETAIL-LINE2.                                                    
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
W21731*    05  FILLER                           PIC  X(17) VALUE SPACES.        
W21731     05  FILLER                           PIC  X(12) VALUE SPACES.        
W26603     05  DL2-STORE-NBR                    PIC  X(05) VALUE SPACES.        
W26603     05  FILLER                           PIC  X(03) VALUE SPACES.        
W21731     05  DL2-DEPT-NBR                     PIC  ZZZ9  VALUE ZERO.          
W21731     05  FILLER                           PIC  X(01) VALUE SPACES.        
W21731     05  DL2-CLASS                        PIC  ZZZ9  VALUE ZERO.          
W21731     05  FILLER                           PIC  X(03) VALUE SPACES.        
W21731*    05  DL2-SKU                          PIC  X(03) VALUE SPACES.        
W21731     05  DL2-SKU                          PIC  X(08) VALUE SPACES.        
           05  FILLER                           PIC  X(04) VALUE SPACES.        
           05  DL2-SALES-DTE                    PIC  X(10) VALUE SPACES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL2-TOT-AMT                      PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL2-LATE-AMT                     PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  DL2-SALES-AMT                    PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(19) VALUE SPACES.        
      *                                                                         
      *                                                                         
       01  TL2-TOTAL-LINE2.                                                     
           05  FILLER                           PIC  X(02) VALUE SPACES.DPWS100O
           05  FILLER                           PIC  X(17) VALUE SPACES.        
           05  TL2-DETAIL-DESC                  PIC  X(16) VALUE SPACES.        
W26603     05  TL2-STORE                        PIC  X(05) VALUE SPACES.        
W26603     05  FILLER                           PIC  X(19) VALUE SPACES.        
           05  TL2-TOT-AMT                      PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  TL2-LATE-AMT                     PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(03) VALUE SPACES.        
           05  TL2-SALES-AMT                    PIC  $ZZZ,ZZZ,ZZ9.99-           
                                                           VALUE ZEROES.        
           05  FILLER                           PIC  X(19) VALUE SPACES.        
      *                                                                         
      *                                                                         
      ****************************************************************          
      *                                                                         
      ****************************************************************          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
      *                                                                         
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
      *                                                                         
      ****************************************************************          
      * ML CONTROL TABLE                                                        
      ****************************************************************          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
      *                                                                         
      ****************************************************************          
      * SKU CROSS-REFERENCE TABLE                                               
      ****************************************************************          
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      *                                                                         
       LINKAGE SECTION.                                                         
      *                                                                         
      *                                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      *                                                                         
       1000-MAIN-MODULE.                                                        
      *                                                                         
           OPEN INPUT  BACK-POSTED-EXTRACT-FILE                                 
                       CNTL-DATE-FILE.                                          
           OPEN OUTPUT BACK-POSTED-REPORT-FILE.                                 
      *                                                                         
           PERFORM 1150-INIT.                                                   
      *                                                                         
           PERFORM 7000-READ-BACK-POST-FILE.                                    
      *                                                                         
           IF PS-END-OF-EXTRACT                                                 
              MOVE 4095 TO RETURN-CODE                                          
              MOVE ZERO TO PV-LINE-COUNT                                        
      *       PRINT HEADINGS                                                    
              PERFORM 7100-PRINT-PAGE-HEADINGS                                  
              PERFORM 7200-PRINT-DETAIL-HEADINGS                                
              PERFORM 7500-PRINT-BLANK-LINE                                     
              MOVE '                   NO DATA FOR TODAY          '             
                        TO BACK-POSTED-REPORT-RECORD                            
              WRITE BACK-POSTED-REPORT-RECORD                                   
                 AFTER ADVANCING 2 LINES                                        
           ELSE                                                                 
              MOVE 0    TO RETURN-CODE                                          
           END-IF.                                                              
      *                                                                         
           PERFORM 2000-PROCESS-EXTRACT                                         
               UNTIL PS-END-OF-EXTRACT.                                         
      *                                                                         
           IF RETURN-CODE NOT = 4095                                            
               PERFORM 2400-PROCESS-GRAND-TOTALS                                
           ELSE                                                                 
               MOVE 0    TO RETURN-CODE.                                        
      *                                                                         
           CLOSE BACK-POSTED-EXTRACT-FILE                                       
                 CNTL-DATE-FILE                                                 
                 BACK-POSTED-REPORT-FILE.                                       
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
      *                                                                         
       1150-INIT.                                                               
      *                                                                         
           READ CNTL-DATE-FILE INTO CC-CONTROL-CARD                             
               AT END                                                           
                   DISPLAY '** PROGRAM ABEND **'                                
                   DISPLAY 'MLKRP220  - CONTROL CARD EMPTY'                     
                   MOVE +4003              TO PV-ABEND-CODE                     
                   PERFORM 9999-ABEND.                                          
      *                                                                         
           DISPLAY '** MLKRP220 CONTROL CARD:' CNTL-DATE-RECORD.                
      *                                                                         
           IF  NOT CC-PROCESSING-MODE-VALID                                     
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM MLKRP220 **'                                 
               DISPLAY '** PARAGRAPH ' PV-PARAGRAPH-NAME                        
               IF CC-PROCESSING-MODE = SPACE                                    
                   DISPLAY '** PROCESSING MODE ON CONTROL CARD IS BLANK'        
               ELSE                                                             
                   DISPLAY '** INVALID PROCESSING MODE ON CONTROL CARD:'        
                            CC-PROCESSING-MODE                                  
               END-IF                                                           
               MOVE +4003 TO PV-ABEND-CODE                                      
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
      *                                                                         
           PERFORM 8100-READ-CONTROL-TABLE.                                     
      *                                                                         
           MOVE MLCNTL-PSTG-FSCL-WK-DTE  TO  DTATTR-KY-DTE.                     
           PERFORM 8200-GET-DATE-ATTRIBUTES.                                    
                                                                                
           MOVE DTATTR-FSCL-YR-NBR         TO PH2-FSCL-YR-NBR.                  
           MOVE DTATTR-FSCL-MN-NM          TO PH2-FSCL-MN-NM.                   
           MOVE DTATTR-WK-IN-MN-NBR        TO PH2-WEEK-NBR                      
                                                                                
      *    BUILD STANDARD REPORT HEADING.                                       
      *                                                                         
           ACCEPT TD-TODAYS-DATE FROM DATE.                                     
           MOVE TD-YEAR    TO DP132O-RUN-YEAR.                                  
           MOVE TD-MONTH   TO DP132O-RUN-MONTH.                                 
           MOVE TD-DAY     TO DP132O-RUN-DAY.                                   
      *                                                                         
           ACCEPT TT-TODAYS-TIME FROM TIME.                                     
           MOVE TT-HOUR    TO DP132O-RUN-HOUR.                                  
           MOVE TT-MINUTE  TO DP132O-RUN-MINUTE.                                
      *                                                                         
           MOVE 'MLKRP220' TO DP132O-PROGRAM-NAME.                              
           MOVE 1          TO DP132O-REPORT-NUMBER.                             
      *                                                                         
           MOVE PC-LINE-MAX TO PV-LINE-COUNT.                                   
           ADD +1           TO PV-LINE-COUNT.                                   
           INITIALIZE GT-GRAND-TOTALS.                                          
      *                                                                         
026800     PERFORM 8100-READ-CONTROL-TABLE.                                     
      *                                                                         
      *                                                                         
       2000-PROCESS-EXTRACT.                                                    
      *                                                                         
      ******************************************************************        
      *    MOVE VALUES TO OUTPUT FIELDS AND PRINT OUTPUT LINE                   
      *    AND ACCUMULATE STORE TOTALS                                          
      ******************************************************************        
      *                                                                         
           MOVE ML022-SALE-STORE-NBR TO HN-STORE-NBR.                           
           INITIALIZE ST-STORE-TOTALS.                                          
      *                                                                         
           PERFORM 2100-PROCESS-STORE                                           
               UNTIL (ML022-SALE-STORE-NBR NOT = HN-STORE-NBR)                  
                  OR (PS-END-OF-EXTRACT).                                       
      *                                                                         
           SET PS-BREAK-IS-STORE TO TRUE.                                       
      *                                                                         
           PERFORM 2300-PROCESS-BREAKS.                                         
      *                                                                         
      *                                                                         
       2100-PROCESS-STORE.                                                      
      *                                                                         
      ******************************************************************        
      *    PROCESS ALL TRANSACTIONS FOR THE STORE WHERE THE DEPARTMENT,         
      *    CLASS, SKU, AND FINANCIAL LIABILITY DATE ARE THE SAME.               
      ******************************************************************        
      *                                                                         
           MOVE ML022-SALE-FIN-LIAB-DTE   TO HN-FIN-LIAB-DTE.                   
W21731     MOVE ML022-SALE-SKU            TO HN-SKU.                            
W21731     MOVE ML022-SALE-SKU            TO PV-HOLD-SKU                        
W21731**   MOVE ML022-SALE-DEPT-NBR       TO HN-DEPT-NBR.                       
W21731**   MOVE ML022-SALE-CLASS-NBR      TO HN-CLASS.                          
W21731*    MOVE ML022-SALE-SEQ-NBR        TO HN-SKU.                            
W21731                                                                          
W21731     PERFORM 4000-RETRIEVE-SKU-ATTRIBUTES.                                
W21731                                                                          
W21731     IF SQLCODE = 0                                                       
W21731         MOVE SKXREF-DEPT               TO HN-DEPT-NBR                    
W21731         MOVE SKXREF-SUBCLASS           TO HN-CLASS                       
W21731     ELSE                                                                 
W21731         MOVE ML022-SALE-SKU (1:3)      TO HN-DEPT-NBR                    
W21731         MOVE ML022-SALE-SKU (4:2)      TO HN-CLASS                       
W21731     END-IF                                                               
      *                                                                         
           INITIALIZE DT-DATE-TOTALS.                                           
      *                                                                         
           PERFORM 2200-PROCESS-DATE                                            
      *        UNTIL (ML022-SALE-FIN-LIAB-DTE NOT = HN-FIN-LIAB-DTE)            
      *           OR (ML022-SALE-DEPT-NBR     NOT = HN-DEPT-NBR)                
      *           OR (ML022-SALE-CLASS-NBR    NOT = HN-CLASS)                   
      *           OR (ML022-SALE-SEQ-NBR      NOT = HN-SKU)                     
      *           OR (ML022-SALE-STORE-NBR    NOT = HN-STORE-NBR)               
               UNTIL (ML022-SALE-STORE-NBR    NOT = HN-STORE-NBR)               
                  OR  PS-END-OF-EXTRACT.                                        
      *                                                                         
      *    SET PS-BREAK-IS-DETAIL TO TRUE.                                      
      *                                                                         
      *    PERFORM 2300-PROCESS-BREAKS.                                         
      *                                                                         
      *                                                                         
       2200-PROCESS-DATE.                                                       
      *                                                                         
      ******************************************************************        
      *    ACCUMULATE DATE TOTALS.                                              
      ******************************************************************        
           IF ML022-SYSTEM-LATE-CORR                                            
              ADD ML022-SALE-EXTD-RTL-AMT  TO DT-LATE-AMT                       
           ELSE                                                                 
              ADD ML022-SALE-EXTD-RTL-AMT  TO DT-SALES-AMT                      
           END-IF.                                                              
                                                                                
           ADD ML022-SALE-EXTD-RTL-AMT     TO DT-TOT-AMT.                       
                                                                                
           SET PS-BREAK-IS-DETAIL TO TRUE.                                      
           PERFORM 2300-PROCESS-BREAKS.                                         
                                                                                
           PERFORM 7000-READ-BACK-POST-FILE.                                    
      *                                                                         
      *                                                                         
       2300-PROCESS-BREAKS.                                                     
      *                                                                         
      ******************************************************************        
      *    THIS BREAK IS EITHER AT SALES DATE, OR STORE LEVEL THE BREAK         
      *    AT THE SALES DATE LEVEL ONLY REQUIRES A DETAIL LINE TO GET           
      *    WRITTEN. ONE DETAIL LINE IS REALLY A SUMMARY AT A STORE AND          
      *    FINANCIAL LIABILITY DATE.  A BREAK AT A STORE LEVEL WILL             
      *    PRINT ACCUMULATED TOTALS AT THE END OF THE REPORT THERE WILL         
      *    BE ACCUMULATED GRAND TOTALS.  IF THE NUMBER OF PRINTED LINES         
      *    ON A REPORT IS > 60 THEN PRINT THE HEADINGS AND START A NEW          
      *    PAGE.                                                                
      ******************************************************************        
      *                                                                         
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
              MOVE ZERO TO PV-LINE-COUNT                                        
      *       PRINT HEADINGS                                                    
              PERFORM 7100-PRINT-PAGE-HEADINGS                                  
              PERFORM 7200-PRINT-DETAIL-HEADINGS                                
           END-IF.                                                              
      *                                                                         
           IF PS-BREAK-IS-STORE                                                 
      *        ADD STORE VALUES TO GRAND TOTALS                                 
               ADD ST-TOT-AMT    TO  GT-TOT-AMT                                 
               ADD ST-LATE-AMT   TO  GT-LATE-AMT                                
               ADD ST-SALES-AMT  TO  GT-SALES-AMT                               
               IF CC-PROCESSING-MODE-INV                                        
                   MOVE 'TOTAL FOR STORE '                                      
                                        TO TL1-DETAIL-DESC                      
                   MOVE HN-STORE-NBR    TO TL1-STORE                            
                   MOVE ST-TOT-AMT      TO TL1-TOT-AMT                          
                   MOVE ST-LATE-AMT     TO TL1-LATE-AMT                         
                   MOVE ST-SALES-AMT    TO TL1-SALES-AMT                        
               ELSE                                                             
                   MOVE 'TOTAL FOR STORE '                                      
                                        TO TL2-DETAIL-DESC                      
                   MOVE HN-STORE-NBR    TO TL2-STORE                            
                   MOVE ST-TOT-AMT      TO TL2-TOT-AMT                          
                   MOVE ST-LATE-AMT     TO TL2-LATE-AMT                         
                   MOVE ST-SALES-AMT    TO TL2-SALES-AMT                        
               END-IF                                                           
      *        ZERO OUT STORE TOTALS                                            
               MOVE ZEROS             TO ST-TOT-AMT                             
                                         ST-LATE-AMT                            
                                         ST-SALES-AMT                           
      *        PRINT STORE TOTALS                                               
               PERFORM 7400-PRINT-TOTAL-LINE                                    
      *        PRINT BLANK LINE                                                 
               PERFORM 7500-PRINT-BLANK-LINE                                    
               PERFORM 7500-PRINT-BLANK-LINE                                    
           END-IF.                                                              
                                                                                
           IF PS-BREAK-IS-DETAIL                                                
               IF DT-TOT-AMT   = ZERO AND                                       
                  DT-LATE-AMT  = ZERO AND                                       
                  DT-SALES-AMT = ZERO                                           
                  CONTINUE                                                      
               ELSE                                                             
      *        ADD DATE VALUES TO STORE TOTALS                                  
                   ADD DT-TOT-AMT    TO  ST-TOT-AMT                             
                   ADD DT-LATE-AMT   TO  ST-LATE-AMT                            
                   ADD DT-SALES-AMT  TO  ST-SALES-AMT                           
                   IF CC-PROCESSING-MODE-INV                                    
      *                GET THE INVENTORY DATE                                   
                       PERFORM 8000-GET-TINVPAR                                 
                       MOVE INVPAR-PLND-INV-DTE                                 
                                            TO DB2-DATE-FMT                     
                       PERFORM 2500-FORMAT-DB2-TO-RPT-DATE                      
                       MOVE RPT-DATE-FMT    TO DL1-INV-DTE                      
      *                MOVE DATE TOTALS TO PRINT LINE1                          
                       MOVE HN-STORE-NBR    TO DL1-STORE-NBR                    
                       MOVE HN-DEPT-NBR     TO DL1-DEPT-NBR                     
                       MOVE HN-CLASS        TO DL1-CLASS                        
                       MOVE HN-SKU          TO DL1-SKU                          
                       MOVE HN-FIN-LIAB-DTE TO DB2-DATE-FMT                     
                       PERFORM 2500-FORMAT-DB2-TO-RPT-DATE                      
                       MOVE RPT-DATE-FMT    TO DL1-SALES-DTE                    
                       MOVE DT-TOT-AMT      TO DL1-TOT-AMT                      
                       MOVE DT-LATE-AMT     TO DL1-LATE-AMT                     
                       MOVE DT-SALES-AMT    TO DL1-SALES-AMT                    
                   ELSE                                                         
      *                MOVE DATE TOTALS TO PRINT LINE2                          
                       MOVE HN-STORE-NBR    TO DL2-STORE-NBR                    
                       MOVE HN-DEPT-NBR     TO DL2-DEPT-NBR                     
                       MOVE HN-CLASS        TO DL2-CLASS                        
                       MOVE HN-SKU          TO DL2-SKU                          
                       MOVE HN-FIN-LIAB-DTE TO DB2-DATE-FMT                     
                       PERFORM 2500-FORMAT-DB2-TO-RPT-DATE                      
                       MOVE RPT-DATE-FMT    TO DL2-SALES-DTE                    
                       MOVE DT-TOT-AMT      TO DL2-TOT-AMT                      
                       MOVE DT-LATE-AMT     TO DL2-LATE-AMT                     
                       MOVE DT-SALES-AMT    TO DL2-SALES-AMT                    
                   END-IF                                                       
      *            ZERO OUT DATE  TOTALS                                        
                   MOVE ZEROS             TO DT-TOT-AMT                         
                                             DT-LATE-AMT                        
                                             DT-SALES-AMT                       
      *            PRINT DATE TOTALS                                            
                   PERFORM 7300-PRINT-DETAIL-LINE                               
           END-IF.                                                              
      *                                                                         
      *                                                                         
       2400-PROCESS-GRAND-TOTALS.                                               
      *                                                                         
      *    MOVE GRAND TOTAL VALUES TO OUTPUT RECORD                             
           IF CC-PROCESSING-MODE-INV                                            
               MOVE 'GRAND TOTAL     '                                          
                                    TO TL1-DETAIL-DESC                          
               MOVE SPACES          TO TL1-STORE                                
               MOVE GT-TOT-AMT      TO TL1-TOT-AMT                              
               MOVE GT-LATE-AMT     TO TL1-LATE-AMT                             
               MOVE GT-SALES-AMT    TO TL1-SALES-AMT                            
           ELSE                                                                 
               MOVE 'GRAND TOTAL     '                                          
                                    TO TL2-DETAIL-DESC                          
               MOVE SPACES          TO TL2-STORE                                
               MOVE GT-TOT-AMT      TO TL2-TOT-AMT                              
               MOVE GT-LATE-AMT     TO TL2-LATE-AMT                             
               MOVE GT-SALES-AMT    TO TL2-SALES-AMT                            
           END-IF                                                               
      *                                                                         
           PERFORM 7500-PRINT-BLANK-LINE.                                       
           PERFORM 7400-PRINT-TOTAL-LINE.                                       
      *                                                                         
      *                                                                         
       2500-FORMAT-DB2-TO-RPT-DATE.                                             
      *                                                                         
           MOVE DB2-YEAR            TO RPT-YEAR.                                
           MOVE DB2-MONTH           TO RPT-MONTH.                               
           MOVE DB2-DAY             TO RPT-DAY.                                 
      *                                                                         
W21731******************************************************************18870000
W21731* IF SKU ATTRIBUTES (DEPT/SCL) ARE NOT ON INPUT FILE, USE 8-BYTE  18870000
W21731* SKU NUMBER TO RETRIEVE ATTRIBUTES (DEPT / SUBCLASS) FROM TSKXREF18870000
W21731******************************************************************18870000
W21731 4000-RETRIEVE-SKU-ATTRIBUTES.                                    18870000
  |                                                                     18870000
  |         EXEC SQL                                                            
  |             SELECT DEPT                                                     
  |                   ,SUBCLASS                                                 
  |              INTO  :SKXREF-DEPT                                             
  |                   ,:SKXREF-SUBCLASS                                         
  |              FROM  TSKXREF                                                  
W21731          WHERE  SKU = :PV-HOLD-SKU                                       
W26603          WITH UR                                                         
W21731      END-EXEC.                                                           
  |   *                                                                         
  |        EVALUATE SQLCODE                                                     
  |            WHEN 0                                                           
  |               CONTINUE                                                      
  |            WHEN +100                                                        
  |               CONTINUE                                                      
  |            WHEN OTHER                                                       
  |               MOVE '4000-RETRIEVE-SKU-ATTRIBUTES'                           
  |                              TO PV-PARAGRAPH-NAME                           
  |               MOVE 'ERROR ON SELECT FROM TSKXREF'                           
  |                              TO PV-DB2-OPER-ATTEMPTED                       
  |               PERFORM 9998-SQL-ABEND                                        
  |        END-EVALUATE.                                                        
  |                                                                     18870000
W21731                                                                  18870000
      *                                                                         
       7000-READ-BACK-POST-FILE.                                                
      *                                                                         
           READ BACK-POSTED-EXTRACT-FILE INTO ML022-SALE-RECORD                 
               AT END SET PS-END-OF-EXTRACT TO TRUE.                            
      *                                                                         
      *                                                                         
       7100-PRINT-PAGE-HEADINGS.                                                
      *                                                                         
      ******************************************************************        
      * PRINT THE PAGE HEADINGS.                                                
      ******************************************************************        
      *                                                                         
      *                                                                         
           ADD  1           TO PV-PAGE-CNT.                                     
           MOVE PV-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
      *                                                                         
           WRITE BACK-POSTED-REPORT-RECORD                                      
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
      *                                                                         
           WRITE BACK-POSTED-REPORT-RECORD                                      
               FROM PH2-PAGE-HEADING-2                                          
               AFTER ADVANCING 1 LINE.                                          
      *                                                                         
           WRITE BACK-POSTED-REPORT-RECORD                                      
               FROM PH3-PAGE-HEADING-3                                          
               AFTER ADVANCING 1 LINE.                                          
           ADD +3 TO PV-LINE-COUNT.                                             
      *                                                                         
           PERFORM 7500-PRINT-BLANK-LINE.                                       
           PERFORM 7500-PRINT-BLANK-LINE.                                       
                                                                                
      *                                                                         
       7200-PRINT-DETAIL-HEADINGS.                                              
      *                                                                         
           IF CC-PROCESSING-MODE-INV                                            
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM DH1-DETAIL-HEADING1                                     
                   AFTER ADVANCING 2 LINES                                      
      *                                                                         
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM UL1-UNDER-LINE1                                         
                   AFTER ADVANCING 1 LINE                                       
           ELSE                                                                 
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM DH2-DETAIL-HEADING2                                     
                   AFTER ADVANCING 2 LINES                                      
                                                                                
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM UL2-UNDER-LINE2                                         
                   AFTER ADVANCING 1 LINE                                       
           END-IF.                                                              
      *                                                                         
           ADD +3 TO PV-LINE-COUNT.                                             
      *                                                                         
      *                                                                         
       7300-PRINT-DETAIL-LINE.                                                  
      *                                                                         
           IF CC-PROCESSING-MODE-INV                                            
               PERFORM 8000-GET-TINVPAR                                         
               MOVE INVPAR-PLND-INV-DTE TO DB2-DATE-FMT                         
               PERFORM 2500-FORMAT-DB2-TO-RPT-DATE                              
               MOVE RPT-DATE-FMT        TO DL1-INV-DTE                          
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM DL1-DETAIL-LINE1                                        
                   AFTER ADVANCING 1 LINE                                       
           ELSE                                                                 
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM DL2-DETAIL-LINE2                                        
                   AFTER ADVANCING 1 LINE                                       
           END-IF.                                                              
      *                                                                         
           ADD +1 TO PV-LINE-COUNT.                                             
      *                                                                         
      *                                                                         
       7400-PRINT-TOTAL-LINE.                                                   
      *                                                                         
           IF CC-PROCESSING-MODE-INV                                            
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM TUL1-TOTAL-UNDER-LINE1                                  
                   AFTER ADVANCING 1 LINE                                       
      *                                                                         
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM TL1-TOTAL-LINE1                                         
                   AFTER ADVANCING 1 LINE                                       
           ELSE                                                                 
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM TUL2-TOTAL-UNDER-LINE2                                  
                   AFTER ADVANCING 1 LINE                                       
      *                                                                         
               WRITE BACK-POSTED-REPORT-RECORD                                  
                   FROM TL2-TOTAL-LINE2                                         
                   AFTER ADVANCING 1 LINE                                       
           END-IF                                                               
      *                                                                         
           ADD +2 TO PV-LINE-COUNT.                                             
      *                                                                         
      *                                                                         
       7500-PRINT-BLANK-LINE.                                                   
      *                                                                         
           INITIALIZE BACK-POSTED-REPORT-RECORD.                                
           WRITE BACK-POSTED-REPORT-RECORD                                      
              AFTER ADVANCING 1 LINE.                                           
      *                                                                         
           ADD +1 TO PV-LINE-COUNT.                                             
      *                                                                         
      *                                                                         
       8000-GET-TINVPAR.                                                        
      *                                                                         
           MOVE SPACES TO INVPAR-PLND-INV-DTE.                                  
                                                                                
W26603     MOVE HN-STORE-NBR        TO INVPAR-LOC-NBR.                          
W26603                                                                          
           EXEC SQL                                                             
               SELECT MAX(PLND_INV_DTE)                                         
               INTO :INVPAR-PLND-INV-DTE :PV-NULL-INDICATOR                     
               FROM TINVPAR                                                     
               WHERE LOC_NBR       = :INVPAR-LOC-NBR                            
W33304            AND PLND_INV_DTE <= :MLCNTL-PSTG-FSCL-MN-DTE                  
W33304            AND LOC_INV_STAT_CDE = 'IN'                                   
W26603         WITH UR                                                          
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   IF PV-NULL-INDICATOR < 0                                     
                      MOVE '9999/99/99' TO INVPAR-PLND-INV-DTE                  
                   END-IF                                                       
               WHEN OTHER                                                       
                   DISPLAY 'HN-STORE-NBR : ' HN-STORE-NBR                       
                   MOVE    '8000-GET-TINVPAR        '                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TINVPAR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
073100******************************************************************        
073200* READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
073300******************************************************************        
073400 8100-READ-CONTROL-TABLE.                                                 
073500*                                                                         
073600      EXEC SQL                                                            
073700          SELECT PSTG_FSCL_MN_DTE                                         
073800                ,PSTG_FSCL_MN_NBR                                         
073900                ,PSTG_FSCL_WK_DTE                                         
074000                ,PSTG_FSCL_WK_NBR                                         
074100           INTO  :MLCNTL-PSTG-FSCL-MN-DTE                                 
074200                ,:MLCNTL-PSTG-FSCL-MN-NBR                                 
074300                ,:MLCNTL-PSTG-FSCL-WK-DTE                                 
074400                ,:MLCNTL-PSTG-FSCL-WK-NBR                                 
074500           FROM  TMLCNTL                                                  
W26603           WITH UR                                                        
074600      END-EXEC.                                                           
074700*                                                                         
074800     EVALUATE SQLCODE                                                     
074900         WHEN 0                                                           
075000             DISPLAY 'CTRL TABLE POSTING MONTH END DATE : ',              
075100                                        MLCNTL-PSTG-FSCL-MN-DTE           
075200             DISPLAY 'CTRL TABLE POSTING MONTH NUMBER   : ',              
075300                                        MLCNTL-PSTG-FSCL-MN-NBR           
075400             DISPLAY 'CTRL TABLE POSTING WEEK DATE      : '               
075500                                        MLCNTL-PSTG-FSCL-WK-DTE           
075600             DISPLAY 'CTRL TABLE POSTING WEEK NUMBER    : '               
075700                                        MLCNTL-PSTG-FSCL-WK-NBR           
075800         WHEN OTHER                                                       
075900            MOVE '8100-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME           
                  MOVE 'SELECT ROW FROM TMLCNTL'                                
                                                TO PV-DB2-OPER-ATTEMPTED        
076100            PERFORM 9998-SQL-ABEND                                        
076200     END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
       8200-GET-DATE-ATTRIBUTES.                                                
      *                                                                         
           EXEC SQL                                                             
                SELECT                                                          
                    FSCL_YR_NBR                                                 
                   ,FSCL_MN_NM                                                  
                   ,WK_IN_MN_NBR                                                
                INTO                                                            
                    :DTATTR-FSCL-YR-NBR                                         
                   ,:DTATTR-FSCL-MN-NM                                          
                   ,:DTATTR-WK-IN-MN-NBR                                        
                FROM                                                            
                    TDTATTR                                                     
                WHERE                                                           
                    KY_DTE = :DTATTR-KY-DTE                                     
W26603          WITH UR                                                         
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    '8050-GET-DATE-ATTRIBUTES'                           
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
       9998-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013 TO PV-ABEND-CODE.                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKRP220'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
      *                                                                         
           PERFORM 9999-ABEND.                                                  
      *                                                                         
      *                                                                         
       9999-ABEND.                                                              
      *                                                                         
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
