00001  IDENTIFICATION DIVISION.                                         06/19/95
00002                                                                   ETKUP039
00003                                                                      LV006
00004  PROGRAM-ID.    ETKUP039.                                            CL**5
00005  AUTHOR.        STEVE FLUNKER.                                       CL**5
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**5
00007  DATE-WRITTEN.  06-12-95.                                            CL**5
00008  DATE-COMPILED.                                                      CL**5
00009                                                                      CL**5
00010 ******************************************************************   CL**5
00011 *      ETKUP039 - EDI/INVO RECYCLE (UPDATE/DELETE)                   CL**5
00012 *                                                                    CL**5
00013 * THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.         CL**5
00014 * IT READS THE PREPARED EDI INVOICES TO BE UPD/DEL FILE FROM         CL**5
00015 * ETKUT039. FOR EACH RECORD READ, IT UPDATES OR DELETES ROWS ON      CL**5
00016 * THE APPROPRIATE TABLE.  THIS IS THE REVALIDATION PORTION OF THE    CL**5
00017 * RECYCLE.                                                           CL**5
00018 ******************************************************************   CL**5
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS991 TO MAKE CALL TO DPKUT901 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
260107******************************************************************   CL**7
00019                                                                      CL**5
00020                                                                      CL**5
00021                                                                      CL**5
00022  ENVIRONMENT DIVISION.                                               CL**5
00023                                                                      CL**5
00024                                                                      CL**5
00025  CONFIGURATION SECTION.                                              CL**5
00026                                                                      CL**5
00027  SOURCE-COMPUTER.    IBM-3090.                                       CL**5
00028  OBJECT-COMPUTER.    IBM-3090.                                       CL**5
00029                                                                      CL**5
00030                                                                      CL**5
00031  INPUT-OUTPUT SECTION.                                               CL**5
00032                                                                      CL**5
00033  FILE-CONTROL.                                                       CL**5
00034                                                                      CL**5
00035  DATA DIVISION.                                                      CL**5
00036                                                                      CL**5
00037                                                                      CL**5
00038  WORKING-STORAGE SECTION.                                            CL**5
00039                                                                      CL**5
00040  01  ABEND-CODE                       PIC S9(04)    COMP SYNC        CL**5
00041                                                     VALUE +0.        CL**5
00042  01  INV-UPD-DEL-RECORD.                                             CL**5
00043      05 UD-TABLE-NAME             PIC X(07).                         CL**5
00044      05 UD-TP-DKEY                PIC S9(9) COMP.                    CL**5
00045      05 UD-TP-DKEY-TMST           PIC X(26) VALUE SPACES.            CL**5
00046      05 UD-DATA-AREA              PIC X(354).                        CL**5
00047      05 UD-TFGTRHL-AREA REDEFINES UD-DATA-AREA.                      CL**5
00048         10  UD-TRN-SET-CDE        PIC X(3).                          CL**5
00049         10  UD-TRN-DTL-TXT        PIC 9(9).                          CL**5
00050         10  UD-RLSE-IND           PIC X(01).                         CL**5
00051         10  UD-RLSE-DTE           PIC X(10).                         CL**5
00052         10  UD-USER-ID            PIC X(08).                         CL**5
00053         10  FILLER                PIC X(323).                        CL**5
00054      05 UD-TTRPOHL-AREA REDEFINES UD-DATA-AREA.                      CL**5
00055         10  UD-PO-NBR             PIC 9(9).                          CL**5
00056         10  UD-DEPT-NBR           PIC X(03).                         CL**5
00057         10  UD-PO-RLSE-IND        PIC X(01).                         CL**5
00058         10  FILLER                PIC X(341).                        CL**5
00059      05 UD-TTRTRDT-AREA REDEFINES UD-DATA-AREA.                      CL**5
00060         10  UD-TRN-SET-SEQ-NBR    PIC S9(9) COMP.                    CL**5
00061         10  UD-TRN-SET-DATA       PIC X(350).                        CL**5
00062      05 UD-UPD-DEL-FLAG           PIC X(01) VALUE SPACES.            CL**5
00063         88  UD-UPDATE-DB                    VALUE 'U'.               CL**5
00064         88  UD-DELETE-DB                    VALUE 'D'.               CL**5
00065         88  UD-NO-DB-ACTION                 VALUE ' '.               CL**5
00066                                                                      CL**5
00067  01  WS-SAVE-DATE                     PIC X(10) VALUE SPACES.        CL**5
00068                                                                      CL**5
00069  01  PC-PROGRAM-CONSTANTS.                                           CL**5
00070      05  PC-MAX-RETRIES               PIC S9(04)    COMP SYNC        CL**5
00071                                                     VALUE +3.        CL**5
00072      05  PC-MAX-DEADLOCKS             PIC S9(04)    COMP SYNC        CL**5
00073                                                     VALUE +3.        CL**5
00074      05  PC-CKPT-JOB-NAME             PIC  X(08)    VALUE            CL**5
00075                                                          'ETK142'.   CL**6
00076      05  PC-CKPT-PLAN-NAME            PIC  X(08)    VALUE            CL**5
00077                                                        'ETKUP039'.   CL**5
00078      05  PC-TRAN-PRES-FUNC-CODES.                                    CL**5
00079          10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05)    VALUE 'OPEN'.    CL**5
00080          10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05)    VALUE 'TRANS'.   CL**5
00081          10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05)    VALUE 'RSTRT'.   CL**5
00082          10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05)    VALUE 'CLOSE'.   CL**5
00083                                                                      CL**5
00084  01  PS-PROGRAM-SWITCHES.                                            CL**5
00085      05  PS-RESTART-REQUIRED-SW       PIC X         VALUE 'N'.       CL**5
00086          88  PS-RESTART-REQUIRED                    VALUE 'Y'.       CL**5
00087          88  PS-RESTART-NOT-REQUIRED                VALUE 'N'.       CL**5
00088                                                                      CL**5
00089  01  PV-PROGRAM-VARIABLES.                                           CL**5
00090      05  PV-SQL-SUB                   PIC 9(03)     VALUE ZERO.      CL**5
00091      05  PV-DEADLOCK-COUNTER          PIC 9(03)     VALUE ZERO.      CL**5
00092      05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.     CL**5
00093      05  PV-DB2-OPER-ATTEMPTED        PIC X(30)     VALUE SPACE.     CL**5
00094                                                                      CL**5
00095 *****************************************************************    CL**5
00096 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD TABLE.                       CL**5
00097 *****************************************************************    CL**5
00098                                                                      CL**5
00099      EXEC SQL                                                        CL**5
00100          INCLUDE TFGTRHL                                             CL**5
00101      END-EXEC.                                                       CL**5
00102                                                                      CL**5
00103 *****************************************************************    CL**5
00104 *  DCLGEN FOR THE PO HELD TABLE.                                     CL**5
00105 *****************************************************************    CL**5
00106                                                                      CL**5
00107      EXEC SQL                                                        CL**5
00108          INCLUDE TTRPOHL                                             CL**5
00109      END-EXEC.                                                       CL**5
00110                                                                      CL**5
00111 *****************************************************************    CL**5
00112 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD DATA TABLE.                  CL**5
00113 *****************************************************************    CL**5
00114                                                                      CL**5
00115      EXEC SQL                                                        CL**5
00116          INCLUDE TFGTRDT                                             CL**5
00117      END-EXEC.                                                       CL**5
00118                                                                      CL**5
00119 *****************************************************************    CL**5
00120 *  COMMUNICATIONS AREA FOR DB2                                       CL**5
00121 *****************************************************************    CL**5
00122                                                                      CL**5
00123      COPY SQLCA2.                                                    CL**5
00124                                                                      CL**5
00125                                                                      CL**5
00126 *****************************************************************    CL**5
00127 *  DB2 FORMATTED ERROR MESSAGE                                       CL**5
00128 *****************************************************************    CL**5
00129                                                                      CL**5
00130      COPY DPWS004.                                                   CL**5
00131                                                                      CL**5
00132                                                                      CL**5
00133 *****************************************************************    CL**5
00134 *  TRANSACTION PRESENTATION MODULE COMMUNICATIONS AREA               CL**5
00135 *****************************************************************    CL**5
00136                                                                      CL**5
260107     COPY DPWS991.                                                   CL**5
00136                                                                      CL**5
00137      COPY DPLS001.                                                   CL**5
00138      05  WORKING-STORAGE-PASSED.                                     CL**5
00139          10  FILLER                   PIC X.                         CL**5
00140                                                                      CL**5
00141  01  FILLER                           PIC  X(4096)  VALUE SPACE.     CL**5
00142                                                                      CL**5
00143 ******************************************************************   CL**5
00144 *  COMMUNICATIONS AREA FOR DB2                                   *   CL**5
00145 ******************************************************************   CL**5
00146                                                                      CL**5
00147      EXEC SQL                                                        CL**5
00148          INCLUDE SQLCA                                               CL**5
00149      END-EXEC.                                                       CL**5
00150  EJECT                                                               CL**5
00151  PROCEDURE DIVISION.                                                 CL**5
00152                                                                      CL**5
00153                                                                      CL**5
00154  A100-PROGRAM-MAINLINE.                                              CL**5
00155                                                                      CL**5
00156                                                                      CL**5
00157      MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.                 CL**5
00158      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**5
00159                                                                      CL**5
00160      PERFORM B200-PROCESS-INV-RECYCLE-RECS                           CL**5
00161          UNTIL DP001-PREP-TRANS-EOF.                                 CL**5
00162                                                                      CL**5
00163      MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.                CL**5
00164      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**5
00165                                                                      CL**5
00166                                                                      CL**5
00167      STOP RUN.                                                       CL**5
00168                                                                      CL**5
00169                                                                      CL**5
00170 ******************************************************************   CL**5
00171 * PROCESS THE RECORDS ON THE INVOICE RECLYCLE FILE.  FOR EACH        CL**6
00172 * RECORD ON THE FILE, DELETE OR UPDATE A ROW IN THE APPROPRIATE      CL**5
00173 * TABLE.                                                             CL**5
00174 ******************************************************************   CL**5
00175  B200-PROCESS-INV-RECYCLE-RECS.                                      CL**5
00176                                                                      CL**5
00177      SET PS-RESTART-NOT-REQUIRED TO TRUE.                            CL**5
00178                                                                      CL**5
00179      PERFORM C100-BUILD-UPD-DEL-ROW.                                 CL**5
00180                                                                      CL**5
00181 *    *------------------------------------------------------------   CL**5
00182 *    * IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN    CL**5
00183 *    * A TRANS CALL. THIS WILL ALLOW PROCESSING TO RESUME WITH       CL**5
00184 *    * THE FIRST TRANSACTION AFTER THE LAST COMMIT.                  CL**5
00185 *    *------------------------------------------------------------   CL**5
00186      IF PS-RESTART-REQUIRED                                          CL**5
00187          MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE             CL**5
00188      ELSE                                                            CL**5
00189          MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE             CL**5
00190      END-IF.                                                         CL**5
00191                                                                      CL**5
00192      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**5
00193 *                                                                    CL**5
00194 ******************************************************************   CL**5
00195 * DETERMINE WHICH TABLE THE ROW SHOULD BE UPDATED INTO AND           CL**5
00196 * PERFORM THE APPROPRIATE UPDATE.                                    CL**5
00197 ******************************************************************   CL**5
00198  C100-BUILD-UPD-DEL-ROW.                                             CL**5
00199                                                                      CL**5
00200      IF UD-TABLE-NAME = 'TFGTRHL'                                    CL**5
00201         AND UD-UPDATE-DB                                             CL**5
00202                                                                      CL**5
00203         EXEC SQL                                                     CL**5
00204            SET :WS-SAVE-DATE = CURRENT DATE                          CL**5
00205         END-EXEC                                                     CL**5
00206                                                                      CL**5
00207         EVALUATE TRUE                                                CL**5
00208            WHEN SQLCODE = 0                                          CL**5
00209               CONTINUE                                               CL**5
00210            WHEN OTHER                                                CL**5
00211               MOVE 'C100-BUILD-UPD-DEL-ROW' TO                       CL**5
00212                    PV-PARAGRAPH-NAME                                 CL**5
00213               MOVE 'SELECT CURRENT TIMESTAMP' TO                     CL**5
00214                    PV-DB2-OPER-ATTEMPTED                             CL**5
00215               PERFORM Z900-SQL-ABEND                                 CL**5
00216         END-EVALUATE                                                 CL**5
00217                                                                      CL**5
00218         PERFORM Z200-UPDATE-TFGTRHL-ROW                              CL**5
00219      ELSE                                                            CL**5
00220         IF UD-TABLE-NAME = 'TTRPOHL'                                 CL**5
00221            AND UD-UPDATE-DB                                          CL**5
00222            PERFORM Z300-UPDATE-TTRPOHL-ROW                           CL**5
00223         ELSE                                                         CL**5
00224            IF UD-TABLE-NAME = 'TTRPOHL'                              CL**5
00225               AND UD-DELETE-DB                                       CL**5
00226               PERFORM Z350-DELETE-TTRPOHL-ROW                        CL**5
00227            ELSE                                                      CL**5
00228               IF UD-TABLE-NAME = 'TFGTRDT'                           CL**5
00229                  AND UD-UPDATE-DB                                    CL**5
00230                  PERFORM Z400-UPDATE-TFGTRDT-ROW                     CL**5
00231               ELSE                                                   CL**5
00232                  IF UD-TABLE-NAME = 'TFGTRDT'                        CL**5
00233                     AND UD-DELETE-DB                                 CL**5
00234                     PERFORM Z450-DELETE-TFGTRDT-ROW                  CL**5
00235                  END-IF                                              CL**5
00236               END-IF                                                 CL**5
00237            END-IF                                                    CL**5
00238         END-IF                                                       CL**5
00239      END-IF.                                                         CL**5
00240 *                                                                    CL**5
00241 ******************************************************************   CL**5
00242 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION         CL**5
00243 * PRESENTATION MODULE.                                               CL**5
00244 ******************************************************************   CL**5
00245                                                                      CL**5
00246  Z100-BUILD-COMM-AREA-TRAN-PRES.                                     CL**5
00247                                                                      CL**5
00248      MOVE LENGTH OF WORKING-STORAGE-PASSED                           CL**5
00249                              TO DP001-WORK-STRG-LENGTH.              CL**5
00250      MOVE PC-CKPT-JOB-NAME   TO DP001-JOB-NAME.                      CL**5
00251      MOVE PC-CKPT-PLAN-NAME  TO DP001-PLAN-NAME.                     CL**5
00252      PERFORM Z900-CALL-TRANS-PRES-MODULE.                            CL**5
00253      MOVE DP001-TRANS-RECORD TO INV-UPD-DEL-RECORD.                  CL**5
00254      IF DP001-CKPT-TAKEN                                             CL**5
00255          INITIALIZE PV-DEADLOCK-COUNTER                              CL**5
00256      END-IF.                                                         CL**5
00257                                                                      CL**5
00258 ******************************************************************   CL**5
00259 *  UPDATE ROW IN THE TFGTRHL TABLE                                   CL**5
00260 ******************************************************************   CL**5
00261  Z200-UPDATE-TFGTRHL-ROW.                                            CL**5
00262 *                                                                    CL**5
00263      PERFORM                                                         CL**5
00264          WITH TEST AFTER                                             CL**5
00265          VARYING PV-SQL-SUB                                          CL**5
00266          FROM 1 BY 1                                                 CL**5
00267          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00268                    OR SQLCODE = -911                                 CL**5
00269                    OR SQLCODE = 0)                                   CL**5
00270 *                                                                    CL**5
00271          EXEC SQL                                                    CL**5
00272              UPDATE TFGTRHL                                          CL**5
00273              SET RLSE_DTE = :WS-SAVE-DATE,                           CL**5
00274                  RLSE_IND = 'Y',                                     CL**5
00275                  USER_ID  = :PC-CKPT-JOB-NAME                        CL**5
00276              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**5
00277                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**5
00278          END-EXEC                                                    CL**5
00279 *                                                                    CL**5
00280          EVALUATE TRUE                                               CL**5
00281              WHEN SQLCODE = -904                                     CL**5
00282              WHEN SQLCODE = 0                                        CL**5
00283                  CONTINUE                                            CL**5
00284              WHEN SQLCODE = -911                                     CL**5
00285                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00286                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00287                      MOVE 'Z200-UPDATE-TFGTRHL-ROW'                  CL**5
00288                                      TO PV-PARAGRAPH-NAME            CL**5
00289                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00290                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00291                      PERFORM Z900-SQL-ABEND                          CL**5
00292                  ELSE                                                CL**5
00293                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00294                  END-IF                                              CL**5
00295              WHEN SQLWARN NOT = SPACES                               CL**5
00296              WHEN SQLCODE NOT = ZERO                                 CL**5
00297                  MOVE 'Z200-UPDATE-TFGTRHL-ROW'                      CL**5
00298                                TO  PV-PARAGRAPH-NAME                 CL**5
00299                  MOVE 'UPDATE ROW INTO TFGTRHL'                      CL**5
00300                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00301                  PERFORM Z900-SQL-ABEND                              CL**5
00302          END-EVALUATE                                                CL**5
00303      END-PERFORM.                                                    CL**5
00304 *                                                                    CL**5
00305      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00306          MOVE 'Z200-UPDATE-TFGTRHL-ROW'                              CL**5
00307                                TO  PV-PARAGRAPH-NAME                 CL**5
00308          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00309                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00310          PERFORM Z900-SQL-ABEND                                      CL**5
00311      END-IF.                                                         CL**5
00312 *                                                                    CL**5
00313 ******************************************************************   CL**5
00314 *  UPDATE ROW IN THE TTRPOHL TABLE                                   CL**5
00315 ******************************************************************   CL**5
00316  Z300-UPDATE-TTRPOHL-ROW.                                            CL**5
00317 *                                                                    CL**5
00318      PERFORM                                                         CL**5
00319          WITH TEST AFTER                                             CL**5
00320          VARYING PV-SQL-SUB                                          CL**5
00321          FROM 1 BY 1                                                 CL**5
00322          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00323                    OR SQLCODE = -911                                 CL**5
00324                    OR SQLCODE = 0)                                   CL**5
00325 *                                                                    CL**5
00326          EXEC SQL                                                    CL**5
00327              UPDATE TTRPOHL                                          CL**5
00328              SET DEPT_NBR  = :UD-DEPT-NBR                            CL**5
00329              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**5
00330                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**5
00331          END-EXEC                                                    CL**5
00332 *                                                                    CL**5
00333          EVALUATE TRUE                                               CL**5
00334              WHEN SQLCODE = -904                                     CL**5
00335              WHEN SQLCODE = 0                                        CL**5
00336                  CONTINUE                                            CL**5
00337              WHEN SQLCODE = -911                                     CL**5
00338                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00339                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00340                      MOVE 'Z300-UPDATE-TTRPOHL-ROW'                  CL**5
00341                                      TO PV-PARAGRAPH-NAME            CL**5
00342                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00343                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00344                      PERFORM Z900-SQL-ABEND                          CL**5
00345                  ELSE                                                CL**5
00346                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00347                  END-IF                                              CL**5
00348              WHEN SQLWARN NOT = SPACES                               CL**5
00349              WHEN SQLCODE NOT = ZERO                                 CL**5
00350                  MOVE 'Z300-UPDATE-TTRPOHL-ROW'                      CL**5
00351                                TO  PV-PARAGRAPH-NAME                 CL**5
00352                  MOVE 'UPDATE ROW INTO TTRPOHL'                      CL**5
00353                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00354                  PERFORM Z900-SQL-ABEND                              CL**5
00355          END-EVALUATE                                                CL**5
00356      END-PERFORM.                                                    CL**5
00357 *                                                                    CL**5
00358      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00359          MOVE 'Z300-UPDATE-TTRPOHL-ROW'                              CL**5
00360                                TO  PV-PARAGRAPH-NAME                 CL**5
00361          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00362                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00363          PERFORM Z900-SQL-ABEND                                      CL**5
00364      END-IF.                                                         CL**5
00365 *                                                                    CL**5
00366 ******************************************************************   CL**5
00367 *  DELETE ROW IN THE TTRPOHL TABLE                                   CL**5
00368 ******************************************************************   CL**5
00369  Z350-DELETE-TTRPOHL-ROW.                                            CL**5
00370 *                                                                    CL**5
00371      PERFORM                                                         CL**5
00372          WITH TEST AFTER                                             CL**5
00373          VARYING PV-SQL-SUB                                          CL**5
00374          FROM 1 BY 1                                                 CL**5
00375          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00376                    OR SQLCODE = -911                                 CL**5
00377                    OR SQLCODE = 0)                                   CL**5
00378 *                                                                    CL**5
00379          EXEC SQL                                                    CL**5
00380              DELETE FROM TTRPOHL                                     CL**5
00381              WHERE TP_DKEY      = :UD-TP-DKEY AND                    CL**5
00382                    TP_DKEY_TMST = :UD-TP-DKEY-TMST                   CL**5
00383          END-EXEC                                                    CL**5
00384 *                                                                    CL**5
00385          EVALUATE TRUE                                               CL**5
00386              WHEN SQLCODE = -904                                     CL**5
00387              WHEN SQLCODE = 0                                        CL**5
00388                  CONTINUE                                            CL**5
00389              WHEN SQLCODE = -911                                     CL**5
00390                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00391                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00392                      MOVE 'Z350-DELETE-TTRPOHL-ROW'                  CL**5
00393                                      TO PV-PARAGRAPH-NAME            CL**5
00394                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00395                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00396                      PERFORM Z900-SQL-ABEND                          CL**5
00397                  ELSE                                                CL**5
00398                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00399                  END-IF                                              CL**5
00400              WHEN SQLWARN NOT = SPACES                               CL**5
00401              WHEN SQLCODE NOT = ZERO                                 CL**5
00402                  MOVE 'Z350-DELETE-TTRPOHL-ROW'                      CL**5
00403                                TO  PV-PARAGRAPH-NAME                 CL**5
00404                  MOVE 'DELETE ROW FROM TTRPOHL'                      CL**5
00405                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00406                  PERFORM Z900-SQL-ABEND                              CL**5
00407          END-EVALUATE                                                CL**5
00408      END-PERFORM.                                                    CL**5
00409 *                                                                    CL**5
00410      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00411          MOVE 'Z350-DELETE-TTRPOHL-ROW'                              CL**5
00412                                TO  PV-PARAGRAPH-NAME                 CL**5
00413          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00414                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00415          PERFORM Z900-SQL-ABEND                                      CL**5
00416      END-IF.                                                         CL**5
00417 *                                                                    CL**5
00418 ******************************************************************   CL**5
00419 *  UPDATE ROW IN THE TFGTRDT TABLE                                   CL**5
00420 ******************************************************************   CL**5
00421  Z400-UPDATE-TFGTRDT-ROW.                                            CL**5
00422 *                                                                    CL**5
00423      PERFORM                                                         CL**5
00424          WITH TEST AFTER                                             CL**5
00425          VARYING PV-SQL-SUB                                          CL**5
00426          FROM 1 BY 1                                                 CL**5
00427          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00428                    OR SQLCODE = -911                                 CL**5
00429                    OR SQLCODE = 0)                                   CL**5
00430 *                                                                    CL**5
00431          EXEC SQL                                                    CL**5
00432              UPDATE TFGTRDT                                          CL**5
00433              SET TRN_SET_DATA = :UD-TRN-SET-DATA                     CL**5
00434              WHERE TP_DKEY         = :UD-TP-DKEY AND                 CL**5
00435                    TP_DKEY_TMST    = :UD-TP-DKEY-TMST AND            CL**5
00436                    TRN_SET_SEQ_NBR = :UD-TRN-SET-SEQ-NBR             CL**5
00437          END-EXEC                                                    CL**5
00438 *                                                                    CL**5
00439          EVALUATE TRUE                                               CL**5
00440              WHEN SQLCODE = -904                                     CL**5
00441              WHEN SQLCODE = 0                                        CL**5
00442                  CONTINUE                                            CL**5
00443              WHEN SQLCODE = -911                                     CL**5
00444                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00445                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00446                      MOVE 'Z400-UPDATE-TFGTRDT-ROW'                  CL**5
00447                                      TO PV-PARAGRAPH-NAME            CL**5
00448                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00449                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00450                      PERFORM Z900-SQL-ABEND                          CL**5
00451                  ELSE                                                CL**5
00452                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00453                  END-IF                                              CL**5
00454              WHEN SQLWARN NOT = SPACES                               CL**5
00455              WHEN SQLCODE NOT = ZERO                                 CL**5
00456                  MOVE 'Z400-UPDATE-TFGTRDT-ROW'                      CL**5
00457                                TO  PV-PARAGRAPH-NAME                 CL**5
00458                  MOVE 'UPDATE ROW INTO TFGTRDT'                      CL**5
00459                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00460                  PERFORM Z900-SQL-ABEND                              CL**5
00461          END-EVALUATE                                                CL**5
00462      END-PERFORM.                                                    CL**5
00463 *                                                                    CL**5
00464      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00465          MOVE 'Z400-UPDATE-TFGTRDT-ROW'                              CL**5
00466                                TO  PV-PARAGRAPH-NAME                 CL**5
00467          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00468                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00469          PERFORM Z900-SQL-ABEND                                      CL**5
00470      END-IF.                                                         CL**5
00471 *                                                                    CL**5
00472 ******************************************************************   CL**5
00473 *  DELETE ROW FROM THE TFGTRDT TABLE                                 CL**5
00474 ******************************************************************   CL**5
00475  Z450-DELETE-TFGTRDT-ROW.                                            CL**5
00476 *                                                                    CL**5
00477      PERFORM                                                         CL**5
00478          WITH TEST AFTER                                             CL**5
00479          VARYING PV-SQL-SUB                                          CL**5
00480          FROM 1 BY 1                                                 CL**5
00481          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**5
00482                    OR SQLCODE = -911                                 CL**5
00483                    OR SQLCODE = 0)                                   CL**5
00484 *                                                                    CL**5
00485          EXEC SQL                                                    CL**5
00486              DELETE FROM TFGTRDT                                     CL**5
00487              WHERE TP_DKEY         = :UD-TP-DKEY AND                 CL**5
00488                    TP_DKEY_TMST    = :UD-TP-DKEY-TMST AND            CL**5
00489                    TRN_SET_SEQ_NBR = :UD-TRN-SET-SEQ-NBR             CL**5
00490          END-EXEC                                                    CL**5
00491 *                                                                    CL**5
00492          EVALUATE TRUE                                               CL**5
00493              WHEN SQLCODE = -904                                     CL**5
00494              WHEN SQLCODE = 0                                        CL**5
00495                  CONTINUE                                            CL**5
00496              WHEN SQLCODE = -911                                     CL**5
00497                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**5
00498                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**5
00499                      MOVE 'Z450-DELETE-TFGTRDT-ROW'                  CL**5
00500                                      TO PV-PARAGRAPH-NAME            CL**5
00501                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**5
00502                                      TO PV-DB2-OPER-ATTEMPTED        CL**5
00503                      PERFORM Z900-SQL-ABEND                          CL**5
00504                  ELSE                                                CL**5
00505                      SET PS-RESTART-REQUIRED TO TRUE                 CL**5
00506                  END-IF                                              CL**5
00507              WHEN SQLWARN NOT = SPACES                               CL**5
00508              WHEN SQLCODE NOT = ZERO                                 CL**5
00509                  MOVE 'Z450-DELETE-TFGTRDT-ROW'                      CL**5
00510                                TO  PV-PARAGRAPH-NAME                 CL**5
00511                  MOVE 'DELETE ROW FROM TFGTRDT'                      CL**5
00512                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00513                  PERFORM Z900-SQL-ABEND                              CL**5
00514          END-EVALUATE                                                CL**5
00515      END-PERFORM.                                                    CL**5
00516 *                                                                    CL**5
00517      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**5
00518          MOVE 'Z450-DELETE-TFGTRDT-ROW'                              CL**5
00519                                TO  PV-PARAGRAPH-NAME                 CL**5
00520          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**5
00521                                TO  PV-DB2-OPER-ATTEMPTED             CL**5
00522          PERFORM Z900-SQL-ABEND                                      CL**5
00523      END-IF.                                                         CL**5
00524 *                                                                    CL**5
00525 ******************************************************************   CL**5
00526 * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING        CL**5
00527 * CODE OCCURS.                                                       CL**5
00528 ******************************************************************   CL**5
00529  Z900-SQL-ABEND.                                                     CL**5
00530                                                                      CL**5
00531      DISPLAY '** ABEND     **'.                                      CL**5
00532      DISPLAY '** PROGRAM   ** = ETKUP039'.                           CL**5
00533      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**5
00534      DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.             CL**5
00535                                                                      CL**5
00536                                                                      CL**5
00537 ******************************************************************   CL**5
00538 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**5
00539 * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**5
00540 * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**5
00541 * FORMAT.                                                            CL**5
00542 ******************************************************************   CL**5
00543      COPY DPPD004.                                                   CL**5
00544                                                                      CL**5
00545      MOVE +4013 TO ABEND-CODE.                                       CL**5
00546      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**5
00547                                                                      CL**5
00548 ******************************************************************   CL**5
00549 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**5
00550 * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION     CL**5
00551 * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.            CL**5
00552 ******************************************************************   CL**5
00553      COPY DPPD001.                                                   CL**5
