      ******************************************************************01/12/95
00002  IDENTIFICATION DIVISION.                                         INKUT030
00003 ******************************************************************   LV004
00004                                                                      CL**1
00005  PROGRAM-ID.              INKUT030.                                  CL**1
00006  AUTHOR.                  STEVE ROEGNER.                             CL**1
00007  INSTALLATION.            KOHLS DEPARTMENT STORES.                   CL**1
00008  DATE-WRITTEN             11-10-93.                                  CL**1
00009  DATE-COMPILED.                                                      CL**1
00010                                                                      CL**1
00011 ******************************************************************   CL**1
00012 *       THE PURPOSE OF THIS PROGRAM IS TO DETERMINE IF A         *   CL**1
00013 *  PARTICULAR STORE HAS TAKEN ANY CUTOFFS.  THIS IS DONE BY      *   CL**1
00014 *  TAKING THE STORE NUMBER OFF THE INPUT FILE AND LOOKING AT THE *   CL**1
00015 *  ITM_CTOFF_IND ON THE INVENTORY PARAMETER TALBLE.  IF THE      *   CL**1
00016 *  OF THE INDICATOR IS 'Y' THEN CUTOFFS WERE TAKEN, OTHERWISE    *   CL**1
00017 *  CUTOFFS WERE NOT TAKEN.  IF CUTOFFS WERE TAKEN ALL RECORDS    *   CL**1
00018 *  FOR THAT PARTICULAR STORE ARE WRITTEN TO OUT01, OTHERWISE     *   CL**1
00019 *  ALL RECORDS FOR THE STORE ARE WRITTEN TO OUT02.                   CL**1
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      *  CHANGED: 04/23/98   JEFF HARTIG     WR# 6041                           
      *  PGM CHANGED FOR Y2K COMPLIANCE. REMOVE ALL PV-DB2-DATE, CC-            
      *  DATE, PV-CURRENT DATE LOGIC.  OBSOLETE, NO LONGER USED.                
      *                                                                         
      *  CHANGED: 02/02/99   M. KLAMIK       WR# 10657                          
      *     UPDATE INWS018 COPYBOOK WITH NEW TIMESTAMP FIELDS.                  
      *                                                                         
W21732*  CHANGED: 02/10/02   SUKUMAR          WR# 21732                         
W21732*  NI-SKU PROJECT : COMPILED PROGRAM WITH CHANGED COPYBOOK                
W21732*                   INWS018.                                              
25248 * 25248    10-06-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
W33304* W33304 06-06-2008 PATTY JAEGER - HILO CHANGES                           
W33304*                   (TINCNTL/TINVPAR CHANGES)                             
      ******************************************************************   CL**1
                                                                           CL**1
00023  ENVIRONMENT DIVISION.                                               CL**1
00026  CONFIGURATION SECTION.                                              CL**1
00028  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00029  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00031  INPUT-OUTPUT SECTION.                                               CL**1
00033  FILE-CONTROL.                                                       CL**1
00034                                                                      CL**1
00035      SELECT INVENTORY-DATA-FILE                                      CL**1
00036          ASSIGN TO INP01.                                            CL**1
00037                                                                      CL**1
00038      SELECT INVENTORY-CUTOFF-FILE-YES                                CL**1
00039          ASSIGN TO OUT01.                                            CL**1
00040                                                                      CL**1
00041      SELECT INVENTORY-CUTOFF-FILE-NO                                 CL**1
00042          ASSIGN TO OUT02.                                            CL**1
00043                                                                      CL**1
00045  DATA DIVISION.                                                      CL**1
00048  FILE SECTION.                                                       CL**1
00049                                                                      CL**1
00050  FD  INVENTORY-DATA-FILE                                             CL**1
00051      RECORDING MODE IS F                                             CL**1
00052      LABEL RECORDS ARE STANDARD                                      CL**1
00053      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00054                                                                      CL**1
00055  01  INVENTORY-DATA-RECORD       PIC X(58).                          CL**1
00056                                                                      CL**1
00057  FD  INVENTORY-CUTOFF-FILE-YES                                       CL**1
00058      RECORDING MODE IS F                                             CL**1
00059      LABEL RECORDS ARE STANDARD                                      CL**1
00060      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00061                                                                      CL**1
00062  01  INVENTORY-CUTOFF-Y-RECORD   PIC X(58).                          CL**1
00063                                                                      CL**1
00064  FD  INVENTORY-CUTOFF-FILE-NO                                        CL**1
00065      RECORDING MODE IS F                                             CL**1
00066      LABEL RECORDS ARE STANDARD                                      CL**1
00067      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00068                                                                      CL**1
00069  01  INVENTORY-CUTOFF-N-RECORD   PIC X(58).                          CL**1
00070                                                                      CL**1
00076 /****************************************************************    CL**1
00073  WORKING-STORAGE SECTION.                                            CL**1
00076 *****************************************************************    CL**1
00081                                                                      CL**1
00083 *****************************************************************    CL**1
00077 * RECORD LAYOUT OF INPUT FILE FROM SCANNING OR KEYING SERVICE.       CL**1
00078 *****************************************************************    CL**1
25248      COPY INWS018.                                                   CL**1
00081                                                                      CL**1
00083 *****************************************************************    CL**1
00084 *  DB2 SQL ERROR/WARNING PROCESSING                                  CL**1
00085 *****************************************************************    CL**1
00088      COPY DPWS004.                                                   CL**1
00089                                                                      CL**1
00092 /**************************************************************      CL**1
00093 * THIS IS A WORKING STORAGE LAYOUT FOR ABEND CODE VALUES.            CL**1
00094 *     USE THE SET COMMAND TO PROVIDE VALUES TO ABEND-CODE.           CL**1
00095 *     IE SET AE-DB2-ERR TO TRUE:  CALL USING ABEND-CODE.             CL**1
00096 ***************************************************************      CL**1
00097 *                                                                    CL**1
00098  01  ABEND-CODE-INFO.                                                CL**1
00099      05 ABEND-CODE                     PIC S9(4).                    CL**1
00100          88  AC-INVALID-KEY-WRITE              VALUE +4001.          CL**1
00101          88  AC-INVALID-KEY-READ               VALUE +4002.          CL**1
00102          88  AC-INVALID-CONTROL-CARD           VALUE +4003.          CL**1
00103          88  AC-TABLE-OVERFLOW                 VALUE +4004.          CL**1
00104          88  AC-OPEN-FILE-ERROR                VALUE +4005.          CL**1
00105          88  AC-CLOSE-FILE-ERROR               VALUE +4006.          CL**1
00106          88  AC-SEQUENCE-ERROR                 VALUE +4007.          CL**1
00107          88  AC-INVALID-DATA                   VALUE +4008.          CL**1
00108          88  AC-INVALID-KEYPUNCH               VALUE +4009.          CL**1
00111          88  AC-INTERNAL-SORT-ERROR            VALUE +4012.          CL**1
00112          88  AC-DB2-ERROR                      VALUE +4013.          CL**1
00113          88  AC-DPWS500-DATE-ERROR             VALUE +4019.          CL**1
00114                                                                      CL**1
00115  01  PA-PROGRAM-ACCUMULATORS.                                        CL**1
00116      05  PA-RETRY-COUNT          PIC S9(04)  VALUE 0 COMP.           CL**1
00117 *                                                                    CL**1
00118  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00119      05  PC-INV-REC-DATE         PIC X(10)    VALUE '9999-09-09'.    CL**1
00120      05  PC-INV-BOOK-DATE        PIC X(10)    VALUE '9999-09-09'.    CL**1
00121      05  PC-INV-FINANCIAL-DATE   PIC X(10)    VALUE '9999-09-09'.    CL**1
00122      05  PC-HEADER-REC           PIC X(14)    VALUE                  CL**1
00123                                                  'HEADER RECORD'.    CL**1
00124                                                                      CL**1
00125      05  PC-MORE-THAN-MAX-RETRY  PIC S9(04)   VALUE 4 COMP.          CL**1
00126      05  PC-MAX-RETRIES          PIC S9(4)    VALUE +3               CL**1
00127                                               COMP SYNC.             CL**1
00128 *                                                                    CL**1
00129  01  PV-PROGRAM-VARIABLES.                                           CL**1
00130                                                                      CL**1
00131      05  PV-PARAGRAPH-NMBR       PIC X(05)    VALUE SPACES.          CL**1
00132      05  PV-DB2-OPERATION        PIC X(20)    VALUE SPACES.          CL**1
00133      05  PV-EOF                  PIC X(01)    VALUE 'N'.             CL**1
00134      05  PV-NULL                 PIC S9(4)    COMP VALUE ZERO.       CL**1
00148      05  PV-FETCH-COUNTER        PIC S9(4)    VALUE 0                CL**1
00149                                               COMP SYNC.             CL**1
00151  01  SA-SAVE-AREA.                                                   CL**1
25248      05  SA-STORE-NUMBER         PIC X(4)     VALUE SPACES.          CL**1
00153      05  SA-STORE-NMBR REDEFINES SA-STORE-NUMBER                     CL**1
25248                                  PIC 9(4).                           CL**1
00155                                                                      CL**1
00156  01  WS-INPUT-RECORD.                                                CL**1
00157      05  WS-HEADER-REC           PIC X(13)    VALUE SPACES.          CL**1
00158      05  FILLER                  PIC X(45).                          CL**1
00159                                                                      CL**1
00160  01  WS-OUTPUT-RECORD.                                               CL**1
00161      05  WS-OUTPUT-REC           PIC X(58)    VALUE SPACES.          CL**1
00162                                                                      CL**1
00163  01  HEADER-REC-FLAG.                                                CL**1
00164      05  HR-FLAG                 PIC X(1).                           CL**1
00165          88  HR-HEADER-REC-YES   VALUE 'Y'.                          CL**1
00166          88  HR-HEADER-REC-NO    VALUE 'N'.                          CL**1
00167                                                                      CL**1
00172  EJECT                                                               CL**1
00173 *                                                                    CL**1
00174 ****                                                                 CL**1
00175 *   SQLCA                                                            CL**1
00176 ****                                                                 CL**1
00177      EXEC SQL                                                        CL**1
00178          INCLUDE SQLCA                                               CL**1
00179      END-EXEC.                                                       CL**1
00180                                                                      CL**1
P28545/*****************************************************************        
P28545* TINCNTL - INVENTORY MASTER CONTROL TABLE                                
P28545******************************************************************        
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
00181 ****                                                                 CL**1
00182 *   DCLGEN FOR THE INVENTORY PARAMETERS TABLE.                       CL**1
00183 ****                                                                 CL**1
00184      EXEC SQL                                                        CL**1
00185          INCLUDE TINVPAR                                             CL**1
00186      END-EXEC.                                                       CL**1
00187  EJECT                                                               CL**1
00188 *                                                                    CL**1
00189  EJECT                                                               CL**1
00190 ******************************************************************   CL**1
00191  PROCEDURE DIVISION.                                                 CL**1
00192 ******************************************************************   CL**1
00193 *                                                                    CL**1
00194  A100-MAINLINE.                                                      CL**1
00195       OPEN INPUT  INVENTORY-DATA-FILE.                               CL**1
00196       OPEN OUTPUT INVENTORY-CUTOFF-FILE-YES                          CL**1
00197                   INVENTORY-CUTOFF-FILE-NO.                          CL**1
00198                                                                      CL**1
00204       PERFORM E100-READ-INPUT-FILE.                                  CL**1
00205                                                                      CL**1
00206       IF PV-EOF = 'Y'                                                CL**1
00207          SET AC-INVALID-DATA TO TRUE                                 CL**1
00208          MOVE 'A100-' TO PV-PARAGRAPH-NMBR                           CL**1
00209          MOVE 'INPUT DATASET EMPTY' TO                               CL**1
00210                PV-DB2-OPERATION                                      CL**1
00211          MOVE 4052 TO RETURN-CODE                                    CL**3
00212       ELSE                                                           CL**2
00213          IF WS-HEADER-REC = PC-HEADER-REC                            CL**2
00214             MOVE WS-INPUT-RECORD TO WS-OUTPUT-RECORD                 CL**2
00215          ELSE                                                        CL**2
00216             MOVE 'HDER REC NOT FOUND'                                CL**2
00217                  TO PV-DB2-OPERATION                                 CL**2
00218             MOVE 'A100-' TO PV-PARAGRAPH-NMBR                        CL**2
00219             SET AC-INVALID-DATA TO TRUE                              CL**2
00220             PERFORM Z100-ABEND                                       CL**2
00221          END-IF                                                      CL**2
00222 *                                                                    CL**2
00223          PERFORM B100-PROCESS-CONTROL UNTIL PV-EOF = 'Y'             CL**4
00224       END-IF.                                                        CL**2
00225                                                                      CL**1
00226       CLOSE INVENTORY-DATA-FILE                                      CL**1
00227            INVENTORY-CUTOFF-FILE-YES                                 CL**1
00228            INVENTORY-CUTOFF-FILE-NO.                                 CL**1
00229                                                                      CL**1
00230       STOP RUN.                                                      CL**1
00231                                                                      CL**1
00232 ******************************************************************   CL**1
00233 * B100-PROCESS-CONTROL.                                              CL**1
00234 * THIS PARAGRAPH CONTROLS THE PROCESSING OF THE INPUT FILE.          CL**1
00235 * IT CONTROLS THE DETERMINATION OF WHETHER CUTOFFS WERE TAKEN        CL**1
00236 * AND THEN PERFORMS THE APPROPRIATE PARAGRAPH FOR WRITING THE STOR   CL**1
00237 * DEPENDING ON THE VALUE OF THE ITEM CUTOFF INDICATOR WHICH GETS     CL**1
00238 * RETRIEVED IN THE NEXT PARAGRAPH.                                   CL**1
00239 *          ****   PERFORMED BY A100-MAINLINE ****                    CL**1
00240 ******************************************************************   CL**1
00241  B100-PROCESS-CONTROL.                                               CL**1
00242       PERFORM E100-READ-INPUT-FILE.                                  CL**1
00243       PERFORM C100-CHECK-STATUS.                                     CL**1
00244                                                                      CL**1
00245       IF INVPAR-ITM-CTOFF-IND   = 'Y'                                CL**1
00246          WRITE INVENTORY-CUTOFF-Y-RECORD FROM                        CL**1
00247                WS-OUTPUT-RECORD                                      CL**1
               INITIALIZE IN018-INVENTORY-COUNT-RECORD                          
00248          MOVE WS-INPUT-RECORD TO                                     CL**1
00249               IN018-INVENTORY-COUNT-RECORD                           CL**1
00250          WRITE INVENTORY-CUTOFF-Y-RECORD FROM                        CL**1
00251               IN018-INVENTORY-COUNT-RECORD                           CL**1
00252          PERFORM D100-WRITE-CUTOFF-FILE-YES                          CL**1
00253                UNTIL HR-HEADER-REC-YES OR                            CL**1
00254                PV-EOF = 'Y'                                          CL**1
00255       ELSE                                                           CL**1
00256          WRITE INVENTORY-CUTOFF-N-RECORD FROM                        CL**1
00257                WS-OUTPUT-RECORD                                      CL**1
               INITIALIZE IN018-INVENTORY-COUNT-RECORD                          
00258          MOVE WS-INPUT-RECORD TO                                     CL**1
00259               IN018-INVENTORY-COUNT-RECORD                           CL**1
00260          WRITE INVENTORY-CUTOFF-N-RECORD FROM                        CL**1
00261               IN018-INVENTORY-COUNT-RECORD                           CL**1
00262          PERFORM D200-WRITE-CUTOFF-FILE-NO                           CL**1
00263                UNTIL HR-HEADER-REC-YES OR                            CL**1
00264                PV-EOF = 'Y'                                          CL**1
00265       END-IF.                                                        CL**1
00266                                                                      CL**1
00267 ******************************************************************   CL**1
00268 * C100-CHECK-STATUS.                                                 CL**1
00269 * THIS PARAGRAPH CONTROLS THE DETERMINATION OF WHETHER CUTOFFS       CL**1
00270 * WERE TAKEN.  THE FIRST SQL CALL RETRIEVES THE MAXIMUM INVENTORY    CL**1
00271 * DATE FOR THE STORE ON THE INPUT RECORD.  THE SECOND SQL CALL       CL**1
00272 * RETRIEVES THE VALUE FOR THE ITEM CUTOFF INDICATOR.  THE VALUE      CL**1
00273 * OF THIS CUTOFF INDICATOR IS EVALUATED TO DETERMINE WHICH FILE      CL**1
00274 * TO WRITE THE STORE RECORDS TO.                                     CL**1
00275 *          ****   PERFORMED BY B100-PROCESS-CONTROL ****             CL**1
00276 ******************************************************************   CL**1
00277  C100-CHECK-STATUS.                                                  CL**1
00278      SET HR-HEADER-REC-NO TO TRUE.                                   CL**1
           INITIALIZE IN018-INVENTORY-COUNT-RECORD.                             
00279      MOVE WS-INPUT-RECORD TO IN018-INVENTORY-COUNT-RECORD.           CL**1
25248      MOVE IN018-STORE-NUMBER TO INVPAR-LOC-NBR.                      CL**1
00281                                                                      CL**1
00282      PERFORM                                                         CL**1
00283          WITH TEST AFTER                                             CL**1
00284          VARYING PV-FETCH-COUNTER                                    CL**1
00285          FROM 1 BY 1                                                 CL**1
00286          UNTIL (PV-FETCH-COUNTER > PC-MAX-RETRIES                    CL**1
00287                    OR SQLCODE = 0                                    CL**1
00288                    OR SQLCODE = +100)                                CL**1
W33304         EXEC SQL                                                    CL**1
W33304            SELECT                                                   CL**1
W33304                ITM_CTOFF_IND                                        CL**1
W33304            INTO                                                     CL**1
W33304                :INVPAR-ITM-CTOFF-IND                                CL**1
W33304            FROM TINVPAR A,                                          CL**1
W33304                 TINCNTL B                                           CL**1
W33304            WHERE B.ACTV_IND = 'Y'                                        
W33304              AND A.INV_ID = B.INV_ID                                     
W33304              AND LOC_NBR = :INVPAR-LOC-NBR                          CL**1
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                          CL**1
W33304              AND ACTL_FIN_BK_DTE = '9999-09-09'                     CL**1
W33304            WITH UR                                                       
W33304         END-EXEC                                                    CL**1
00331                                                                      CL**1
00332          EVALUATE TRUE                                               CL**1
00333              WHEN SQLCODE = ZERO                                     CL**1
00335                  CONTINUE                                            CL**1
W33304             WHEN SQLCODE = +100                                     CL**1
W33304                 MOVE 'N' TO INVPAR-ITM-CTOFF-IND                    CL**1
00336              WHEN SQLCODE = -904                                     CL**1
00337              WHEN SQLCODE = -913                                     CL**1
00338              WHEN SQLWARN0 NOT = SPACES                              CL**1
00339              WHEN SQLCODE NOT EQUAL ZERO                             CL**1
00340                  MOVE 'C100-' TO PV-PARAGRAPH-NMBR                   CL**1
00341                  MOVE 'ITM_CTOFF_IND RETRIEVE' TO                    CL**1
00342                                  PV-DB2-OPERATION                    CL**1
00343                  PERFORM Z100-ABEND                                  CL**1
00344          END-EVALUATE                                                CL**1
00346      END-PERFORM.                                                    CL**1
00347 *                                                                    CL**1
00348      IF PV-FETCH-COUNTER > PC-MAX-RETRIES                            CL**1
00349          SET AC-DB2-ERROR TO TRUE                                    CL**1
00350          MOVE 'C100-' TO PV-PARAGRAPH-NMBR                           CL**1
00351          MOVE 'MAX RETRIES ' TO PV-DB2-OPERATION                     CL**1
00352          PERFORM Z100-ABEND                                          CL**1
00353      END-IF.                                                         CL**1
00354                                                                      CL**1
00355 ******************************************************************   CL**1
00356 * D100-WRITE-CUTOFF-FILE-YES.                                        CL**1
00357 * THIS PARAGRAPH WRITES RECORDS TO OUT01 IF CUTOFFS WERE TAKEN FOR   CL**1
00358 * THE STORE.  IT WRITES EACH RECORD UNTIL THE VALUE                  CL**1
00359 * 'HEADER RECORD' IS ENCOUNTERED IN THE FILE.  WHEN THIS VALUE IS    CL**1
00360 * ENCOUNTERED, THE NEXT RECORD IS EVALUATED TO DETERMINE IF          CL**1
00361 * CUTOFFS WERE TAKEN AND THIS PROCESS GETS REPEATED.                 CL**1
00362 *          ****   PERFORMED BY B100-PROCESS-CONTROL ****             CL**1
00363 ******************************************************************   CL**1
00364  D100-WRITE-CUTOFF-FILE-YES.                                         CL**1
00365      PERFORM E100-READ-INPUT-FILE.                                   CL**1
00366                                                                      CL**1
00367      IF PV-EOF = 'Y'                                                 CL**1
00368         CONTINUE                                                     CL**1
00369      ELSE                                                            CL**1
00370         IF WS-HEADER-REC = PC-HEADER-REC                             CL**1
00371            SET HR-HEADER-REC-YES TO TRUE                             CL**1
00372            MOVE WS-INPUT-RECORD TO WS-OUTPUT-RECORD                  CL**1
00373         ELSE                                                         CL**1
                 INITIALIZE IN018-INVENTORY-COUNT-RECORD                        
00374            MOVE WS-INPUT-RECORD TO IN018-INVENTORY-COUNT-RECORD      CL**1
00375            WRITE INVENTORY-CUTOFF-Y-RECORD FROM                      CL**1
00376                  IN018-INVENTORY-COUNT-RECORD                        CL**1
00377          END-IF                                                      CL**1
00378      END-IF.                                                         CL**1
00379                                                                      CL**1
00380 ******************************************************************   CL**1
00381 * D200-WRITE-CUTOFF-FILE-NO.                                         CL**1
00382 * THIS PARAGRAPH WRITES RECORDS TO OUT02 IF CUTOFFS WERE NOT TAKEN   CL**1
00383 * FOR THE STORE.  IT WRITES EACH RECORD UNTIL THE VALUE              CL**1
00384 * 'HEADER RECORD' IS ENCOUNTERED IN THE FILE.  WHEN THIS VALUE IS    CL**1
00385 * ENCOUNTERED, THE NEXT RECORD IS EVALUATED TO DETERMINE IF          CL**1
00386 * CUTOFFS WERE TAKEN AND THIS PROCESS GETS REPEATED.                 CL**1
00387 *          ****   PERFORMED BY B100-PROCESS-CONTROL ****             CL**1
00388 ******************************************************************   CL**1
00389  D200-WRITE-CUTOFF-FILE-NO.                                          CL**1
00390      PERFORM E100-READ-INPUT-FILE.                                   CL**1
00391                                                                      CL**1
00392      IF PV-EOF = 'Y'                                                 CL**1
00393         CONTINUE                                                     CL**1
00394      ELSE                                                            CL**1
00395         IF WS-HEADER-REC = PC-HEADER-REC                             CL**1
00396            SET HR-HEADER-REC-YES TO TRUE                             CL**1
00397            MOVE WS-INPUT-RECORD TO WS-OUTPUT-RECORD                  CL**1
00398         ELSE                                                         CL**1
                 INITIALIZE IN018-INVENTORY-COUNT-RECORD                        
00399            MOVE WS-INPUT-RECORD TO IN018-INVENTORY-COUNT-RECORD      CL**1
00400            WRITE INVENTORY-CUTOFF-N-RECORD FROM                      CL**1
00401                  IN018-INVENTORY-COUNT-RECORD                        CL**1
00402         END-IF                                                       CL**1
00403      END-IF.                                                         CL**1
00404                                                                      CL**1
00405 ******************************************************************   CL**1
00406 * E100-READ-INPUT-FILE.                                              CL**1
00407 * THIS PARAGRAPH SIMPLY READS THE INPUT FILE.                        CL**1
00408 *          ****   PERFORMED BY A100-MAINLINE ****                    CL**1
00409 *          ****   PERFORMED BY B100-PROCESS-CONTROL ****             CL**1
00410 *          ****   PERFORMED BY D100-WRITE-CUTOFF-FILE-YES ***        CL**1
00411 *          ****   PERFORMED BY D200-WRITE-CUTOFF-FILE-NO ***         CL**1
00412 ******************************************************************   CL**1
00413  E100-READ-INPUT-FILE.                                               CL**1
00414      READ INVENTORY-DATA-FILE INTO                                   CL**1
00415           WS-INPUT-RECORD                                            CL**1
00416           AT END MOVE 'Y' TO PV-EOF                                  CL**1
00417      END-READ.                                                       CL**1
00418                                                                      CL**1
00419 ******************************************************************   CL**1
00420 * Z100-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE             CL**1
00421 * EVENT THAT AN SQL ERROR CODE OCCURS.                               CL**1
00422 ******************************************************************   CL**1
00423 *                                                                    CL**1
00424  Z100-ABEND.                                                         CL**1
00425      DISPLAY '** ABEND **         **'.                               CL**1
00426      DISPLAY '** PROGRAM          ** = INKUT030'.                    CL**1
00427      DISPLAY '** PARAGRAPH NMBR   ** = ' PV-PARAGRAPH-NMBR.          CL**1
00428      DISPLAY '** DB2 OPERATION    ** = ' PV-DB2-OPERATION.           CL**1
00429                                                                      CL**1
00430      EJECT                                                           CL**1
00431                                                                      CL**1
00432 *                                                                    CL**1
00433 **********                                                           CL**1
00434 *      COPYBOOK FOR STANDARD DB2 ERROR ROUTINE                       CL**1
00435 *      THIS SHOULD BE COPIED TOGETHER DPWS004 FOR THE WORKING-STOR   CL**1
00436 **********                                                           CL**1
00437 *                                                                    CL**1
00438      COPY DPPD004.                                                   CL**1
00439 *                                                                    CL**1
00440      IF SQLCODE NOT = -911                                           CL**1
00441          EXEC SQL                                                    CL**1
00442              COMMIT                                                  CL**1
00443          END-EXEC                                                    CL**1
00444      END-IF.                                                         CL**1
00445 *                                                                    CL**1
00446      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
00447 *                                                                    CL**1
