       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         IMKUT011.                                            
       AUTHOR.             D. THEEL.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       JUNE 2008.                                           
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  CREATE DAILY FILE OF STYLE CREATES                                     
      *  THE CONTROL CARD IS THE ESP RUN DATE IN MM/DD/CCYY FORMAT.             
      *  THE STYLE INPUT FILE IS STYLE ITEM DOMAIN RECS IN IMRD058              
      *  FORMAT SORTED BY RMS_STYL_ID.                                          
      *  READ IN REC, VALIDATE NEW STYLE IS ACTIVE ON TMDARVS AND HAS           
      *  A BRAND TYPE OF REGULAR.  IF SO, WRITE OUT THE RMS STYLE ID            
      *  AND A STYLE CREATE DATE = CONTROL CARD DATE.                           
      ******************************************************************        
      ******************************************************************        
      *  CHANGES:                                                               
      *                                                                         
      *  06/25/2008 D. THEEL                                                    
      *     INITIAL PROGRAM                                                     
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-FILE         ASSIGN TO INP01.                    
           SELECT STYLE-FILE                ASSIGN TO INP02.                    
                                                                                
           SELECT OUT-FILE                  ASSIGN TO OUT01.                    
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                            00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  SD-CONTROL-RECORD       PIC X(80).                           00620094
                                                                                
       FD  STYLE-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 510 CHARACTERS                                       
           DATA RECORD IS STYLE-RECORD.                                         
       01  STYLE-RECORD                PIC  X(510).                             
                                                                                
       FD  OUT-FILE                                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 15 CHARACTERS                                        
           DATA RECORD IS OUT-RECORD.                                           
                                                                                
       01  OUT-RECORD                    PIC X(15).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  PV-ABEND-4001                   VALUE +4001.                     
      *        EMPTY CONTROL CARD                                               
                                                                                
                                                                                
      ******************************************************************        
      *    PC-PROGRAM CONSTANTS                                        *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'IMKUT011'.        
010700     05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04)  VALUE +2                
010800                                                  COMP SYNC.              
                                                                                
      ****************************************************************  00830094
      * RECORD LAYOUT FOR CONTROL CARD FILE.                            00840094
      * DATE WILL BE ESP CONTROLLED - SYSTEM DATE - MM/DD/CCYY          00840094
      ****************************************************************  00850094
       01  CC-CONTROL-CARD.                                             00860094
           05  CC-CONTROL-DATE              PIC X(10).                  00870094
           05  FILLER                       PIC X(70).                  00880094
                                                                                
      ******************************************************************        
      *       PROGRAM VARIABLES                                        *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)  VALUE SPACES.            
007000     05  PV-RETRY-COUNT               PIC S9(04) VALUE ZERO               
007100                                                 COMP SYNC.               
24521      05  PV-DB2-TEST                  PIC S9(04) VALUE ZEROES             
24521                                       COMP.                               
       01  SA-REC-OUT.                                                          
           05  SA-RMS-STYL-ID               PIC S9(8)V COMP-3.                  
           05  SA-STYLE-CREATE-DATE         PIC X(10)  VALUE SPACES.            
                                                                                
      ******************************************************************        
      *    PS-PROGRAM SWITCHES                                         *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW            PIC  X(01)   VALUE 'N'.             
               88  PS-END-OF-FILE                        VALUE 'Y'.             
           05  SW-END-OF-CONTROL-CARDS-SW  PIC X(01)  VALUE 'N'.        01590094
               88  END-OF-CONTROL-CARDS               VALUE 'Y'.        01600094
           05  PS-STYLE-CURR-FND-SW            PIC  X(01)   VALUE 'N'.          
               88  PS-STYLE-CURR-FND                       VALUE 'Y'.           
               88  PS-STYLE-CURR-NOT-FND                   VALUE 'N'.           
                                                                                
      ******************************************************************        
      *    PA-PROGRAM ACCUMULATORS                                     *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-OUTPUT-RECORDS-WRITTEN    PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-RECS-READ-CNT             PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
           05  PA-STYLE-SELECT-CNT          PIC S9(9) COMP-3                    
                                                       VALUE ZEROES.            
                                                                                
      ******************************************************************        
      *    STYLE INPUT RECORD LAYOUT                                   *        
      ******************************************************************        
019000     COPY IMRD058.                                                        
                                                                                
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    DEPT SCL STYLE TABLE                                        *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    BRAND TABLE                                                 *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE IMT00000                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-RECS                                            
               UNTIL PS-END-OF-FILE.                                            
                                                                                
           PERFORM B300-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - READS THE CONTROL FILE RECORD                            *        
      *     - READS THE FIRST RECORD FROM STYLE FILE                   *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
           OPEN INPUT CONTROL-CARD-FILE                                         
33885                 STYLE-FILE                                                
33885           OUTPUT OUT-FILE.                                                
                                                                                
           PERFORM C100-READ-CONTROL-CARD.                              03240094
           PERFORM C200-READ-STYLE-REC.                                         
           IF PS-END-OF-FILE                                                    
               DISPLAY ' '                                                      
               DISPLAY '***********************************'                    
               DISPLAY ' NO INPUT RECS TO PROCESS.'                             
               DISPLAY '***********************************'                    
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     B200-PROCESS-RECS                                          *        
      * ==> PROCESSES:                                                 *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B200-PROCESS-RECS.                                                       
           PERFORM D100-STYLE-EXISTS-CK.                                        
           IF SQLCODE = +0                                                      
               PERFORM F100-PRINT-DETAIL                                        
           END-IF.                                                              
           PERFORM C200-READ-STYLE-REC.                                         
                                                                                
      ******************************************************************        
      *     B300-EOJ-ROUTINE                                           *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE FILES                                         *        
      *     - DISPLAYS THE NUMBER OF RECORDS READ AND WRITTEN          *        
      * ==> PERFORMED FROM:  A100-MAINLINE                             *        
      ******************************************************************        
       B300-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CONTROL-CARD-FILE                                              
33885            STYLE-FILE                                                     
33885            OUT-FILE.                                                      
                                                                                
           DISPLAY '*******'.                                                   
           DISPLAY 'MESSAGES FOR PROGRAM   '  PC-PROGRAM-NAME.                  
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'RECS WRITTEN    :      '  PA-OUTPUT-RECORDS-WRITTEN.        
           DISPLAY '*******'.                                                   
                                                                                
      ******************************************************************        
      *     C100-READ-CONTROL-CARD                                     *        
      ******************************************************************        
       C100-READ-CONTROL-CARD.                                                  
           MOVE 'C100-READ-CONTROL-CARD' TO PV-PARAGRAPH-NAME.                  
                                                                        07210094
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  07190094
               AT END SET END-OF-CONTROL-CARDS TO TRUE.                 07200094
                                                                        07210094
           IF END-OF-CONTROL-CARDS                                      07220094
               DISPLAY '*** NO DATE CONTROL CARD ***'                   07230094
               SET PV-ABEND-4001 TO TRUE                                        
               PERFORM 9999-ABEND                                       07240094
           ELSE                                                         07250094
               DISPLAY ' CONTROL-DATE ' CC-CONTROL-DATE                 07260094
           END-IF.                                                      07280094
                                                                                
      ******************************************************************        
      *     C200-READ-STYLE-REC                                        *        
      ******************************************************************        
       C200-READ-STYLE-REC.                                                     
           MOVE 'C200-READ-STYLE-REC' TO PV-PARAGRAPH-NAME.                     
           INITIALIZE IM058-ITEM-STYLE-DOMAIN-REC.                              
           READ STYLE-FILE                                                      
               INTO IM058-ITEM-STYLE-DOMAIN-REC                                 
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1                 TO PA-RECS-READ-CNT               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *     D100-STYLE-EXISTS-CK                                       *        
      *     CHECK THAT THE INPUT STYLE REC IS ACTIVE ON TMDARVS AND    *        
      *     HAS A BRAND OF REGULAR.                                    *        
      ******************************************************************        
       D100-STYLE-EXISTS-CK.                                                    
           MOVE 'D100-STYLE-EXISTS-CK' TO PV-PARAGRAPH-NAME.                    
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
                                                                                
               EXEC SQL                                                         
00559              SELECT 1                                             FSKUP045
                   INTO :PV-DB2-TEST                                            
00560              FROM TMDARVS A                                       FSKUP045
                       ,IMT_LBL B                                               
                   WHERE A.RMS_STYL_ID = :IM058-RMS-STYL-ID                     
                     AND A.ACTV_IND    = 'Y'                                    
                     AND A.LBL_SEQ_NBR = B.LBL_SEQ_NBR                          
                     AND B.LBL_TYP_CDE = 'R'                                    
24521              FETCH FIRST 1 ROW ONLY                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +0                                            
                       ADD +1 TO PA-STYLE-SELECT-CNT                            
                   WHEN SQLCODE = +100                                          
                       CONTINUE                                                 
045200             WHEN SQLCODE = -911                                          
045300             WHEN SQLCODE = -913                                          
045400                  ADD 1          TO PV-RETRY-COUNT                        
                   WHEN OTHER                                                   
                       DISPLAY '************************'                       
                       DISPLAY ' D100-STYLE-EXISTS-CK'                          
                       DISPLAY '************************'                       
                       MOVE 'D100-STYLE-EXISTS-CK'                              
                                         TO PV-PARAGRAPH-NAME                   
                       MOVE 'SELECT FROM TMDARVS/IMT_LBL'                       
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
046200     END-PERFORM.                                                         
046300*                                                                         
046400     IF  PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                          
               MOVE 'D100-STYLE-EXISTS-CK'                                      
046600                                 TO PV-PARAGRAPH-NAME                CL**1
               MOVE 'SELECT FROM TMDARVS/IMT_LBL'                               
046800                                 TO PV-DB2-OPERATION-ATTEMPTED       CL**1
               PERFORM Z900-SQL-ABEND                                           
047000     END-IF.                                                              
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *     F100-PRINT-DETAIL                                          *        
      * ==> PROCESSES:                                                 *        
      *     - WRITES THE DETAIL LINES FOR THE REPORT                   *        
      * ==> PERFORMED FROM:  B200-WRITE-RECS                           *        
      ******************************************************************        
       F100-PRINT-DETAIL.                                                       
                                                                                
           MOVE CC-CONTROL-DATE TO SA-STYLE-CREATE-DATE.                        
           MOVE IM058-RMS-STYL-ID TO SA-RMS-STYL-ID.                            
           WRITE OUT-RECORD FROM SA-REC-OUT.                                    
                                                                                
           ADD 1                         TO PA-OUTPUT-RECORDS-WRITTEN.          
                                                                                
      ******************************************************************        
      *     Z900-SQL-ABEND                                             *        
      * ==> PROCESSES:                                                 *        
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *        
      * ==> PERFORMED FROM:                                            *        
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z900-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.                       
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.            
           DISPLAY 'RECS READ      :       '  PA-RECS-READ-CNT.                 
           DISPLAY 'STYLE SELECTS  :       '  PA-STYLE-SELECT-CNT.              
           DISPLAY 'RECS WRITTEN    :      '  PA-OUTPUT-RECORDS-WRITTEN.        
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                    TO ABEND-CODE.                         
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         07780094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
