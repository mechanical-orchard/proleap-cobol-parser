000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT034.                                        
000400 AUTHOR.                 VIRGENE LENNEMAN.                                
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING MERCHANT HIERARCHY          
001200*  INFORMATION FOR COMPLIANCE NETWORK                                     
001300*                                                                         
001400*                                                                         
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 CONFIGURATION SECTION.                                                   
001800                                                                          
001900 SOURCE-COMPUTER.                        IBM-3090.                        
002000 OBJECT-COMPUTER.                        IBM-3090.                        
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300                                                                          
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT  VL-MERCH-FILE                                                
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002900     SELECT  VL-MARKER-FILE                                               
003000         ASSIGN TO OUT02.                                                 
003100                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700*                                                                         
003800 FD  VL-MERCH-FILE                                                        
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  VL-MERCH-RECORD                  PIC  X(92).                         
004300*                                                                         
004400 FD  VL-MARKER-FILE                                                       
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800 01  VL-MARKER-RECORD                 PIC  X(78).                         
004900*                                                                         
005000*                                                                         
005100 WORKING-STORAGE SECTION.                                                 
005200                                                                          
005300*****************************************************************         
005400*    INPUT CONTROL CARD                                                   
005500*****************************************************************         
005600 01  CC-CONTROL-CARD.                                                     
005700     05  CC-SCHEDULE-DATE.                                                
005800         10  CC-CCYY                 PIC X(04).                           
005900         10  CC-MM                   PIC X(02).                           
006000         10  CC-DD                   PIC X(02).                           
006100                                                                          
006200*****************************************************************         
006300*    PROGRAM CONSTANTS                                                    
006400*****************************************************************         
006500 01  PC-CONSTANTS.                                                        
006600     05  PC-CURRENT-PGM-VLKUT034      PIC  X(08) VALUE 'VLKUT034'.        
006700     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
006800                                                 COMP SYNC.               
006900                                                                          
007000*****************************************************************         
007100*    PROGRAM SWITCHES                                                     
007200*****************************************************************         
007300 01  PS-SWITCHES.                                                         
007400     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
007500         88  PS-END-CSR                          VALUE 'Y'.               
007600         88  PS-NOT-END-CSR                      VALUE 'N'.               
007700                                                                          
007800*****************************************************************         
007900*    PROGRAM VARIABLES                                                    
008000*****************************************************************         
008100 01  PV-VARIABLES.                                                        
008200     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
008300                                                 COMP SYNC.               
008400     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
008500                                                 COMP SYNC.               
008600     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
008700     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
008800     05  PV-ABEND-STRUCTURE-1         PIC  X(20) VALUE SPACES.            
008900     05  PV-ABEND-STRUCTURE-2         PIC  X(20) VALUE SPACES.            
009000     05  PV-ABEND-STRUCTURE-3         PIC  X(20) VALUE SPACES.            
009100     05  PV-ERROR-MSG                 PIC  X(60) VALUE SPACES.            
009200     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
009300     05  PV-RUN-DATE                  PIC  X(10) VALUE SPACES.            
009400     05  PV-RUN-DATE-R REDEFINES PV-RUN-DATE.                             
009500         10  PV-RUN-CCYY              PIC  X(04).                         
009600         10  FILLER                   PIC  X(01).                         
009700         10  PV-RUN-MO                PIC  X(02).                         
009800         10  FILLER                   PIC  X(01).                         
009900         10  PV-RUN-DD                PIC  X(02).                         
010000     05  PV-RUN-DATE-CN.                                                  
010100         10  PV-RUN-CCYY-CN           PIC  X(04) VALUE SPACES.            
010200         10  PV-RUN-MO-CN             PIC  X(02) VALUE SPACES.            
010300         10  PV-RUN-DD-CN             PIC  X(02) VALUE SPACES.            
010400     05  PV-RUN-TIME                  PIC  X(08) VALUE SPACES.            
010500     05  PV-RUN-TIME-R REDEFINES PV-RUN-TIME.                             
010600         10  PV-RUN-HH                PIC  X(02).                         
010700         10  FILLER                   PIC  X(01).                         
010800         10  PV-RUN-MM                PIC  X(02).                         
010900         10  FILLER                   PIC  X(01).                         
011000         10  PV-RUN-SS                PIC  X(02).                         
011100     05  PV-RUN-TIME-CN.                                                  
011200         10  PV-RUN-HH-CN             PIC  X(02) VALUE SPACES.            
011300         10  PV-RUN-MM-CN             PIC  X(02) VALUE SPACES.            
011400         10  PV-RUN-SS-CN             PIC  X(02) VALUE SPACES.            
011500***** FILE NAME CONSISTS OF RUN-DATE-CN_VENDOR.DAT                        
011600*****     EXAMPLE: 20050317_VENDOR.DAT                                    
011700     05  PV-FILE-NAME.                                                    
011800         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
011900         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
012000         10  PV-MERCHANT-DAT          PIC  X(12) VALUE SPACES.            
012100                                                                          
012200***  MARKER FILE LAYOUT                                                   
012300     COPY VLRD029.                                                        
012400                                                                          
012500***  MERCHANT FILE LAYOUT                                                 
012600     COPY VLRD034.                                                        
012700                                                                          
012800***  DB2 ABEND COPYBOOK                                                   
012900     COPY DPWS004.                                                        
013000                                                                          
013100***  CALENDAR ROUTINE                                                     
013200     COPY DPWS500.                                                        
013300                                                                          
013400     EXEC SQL                                                             
013500         INCLUDE SQLCA                                                    
013600     END-EXEC.                                                            
013700                                                                          
013800     COPY SQLCA2.                                                         
013900                                                                          
014000***  MERCHANDISE AREA                                                     
014100     EXEC SQL                                                             
014200         INCLUDE TMDSEAR                                                  
014300     END-EXEC.                                                            
014400                                                                          
014500***  RESPONSIBILITY AREA                                                  
014600     EXEC SQL                                                             
014700         INCLUDE TRSPBAR                                                  
014800     END-EXEC.                                                            
014900                                                                          
015000***  EXTRACT CURSOR                                                       
015100     EXEC SQL                                                             
015200         DECLARE EXTRACT-CSR CURSOR FOR                                   
015300         SELECT GMA_NBR                                                   
015400              , DMA_NBR                                                   
015500              , DEPT_NBR                                                  
015600              , BYR_NBR                                                   
015700              , TRS.RSPLVL_CDE                                            
015800              , TRS.WORKER_LAST_NAME                                      
015900              , TRS.WORKER_FIR_NAME                                       
016000           FROM TMDSEAR TMD                                               
016100              , TRSPBAR TRS                                               
016200          WHERE TMD.MDSEAR_DKEY = TRS.MDSEAR_DKEY                         
016300            AND TRS.RSPLVL_CDE IN ('GMM','DMM','BYR')                     
016400            AND TRS.WORKER_END_DTE = '9999-09-09'                         
016500       ORDER BY GMA_NBR, DMA_NBR, BYR_NBR, DEPT_NBR,                      
016600                TRS.RSPLVL_CDE                                            
016700        WITH UR                                                           
016800     END-EXEC.                                                            
016900                                                                          
017000 LINKAGE SECTION.                                                         
017100                                                                          
017200 PROCEDURE DIVISION.                                                      
017300*****************************************************************         
017400*  MAINLINE LOGIC FOR PROGRAM                                             
017500*****************************************************************         
017600 0000-MAINLINE.                                                           
017700     PERFORM 1000-INITIALIZE.                                             
017800                                                                          
017900     PERFORM 2000-PROCESS                                                 
018000         UNTIL PS-END-CSR.                                                
018100                                                                          
018200     PERFORM 9000-EOJ.                                                    
018300                                                                          
018400     STOP RUN.                                                            
018500                                                                          
018600*****************************************************************         
018700*  INITIALIZATION PARAGRAPH                                               
018800*****************************************************************         
018900 1000-INITIALIZE.                                                         
019000     OPEN OUTPUT VL-MERCH-FILE                                            
019100                 VL-MARKER-FILE.                                          
019200                                                                          
019300     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
019400                                                                          
019500     DISPLAY '****************************************'                   
019600     DISPLAY '** CONTROL CARD  ==> '  CC-CONTROL-CARD                     
019700                                                                          
019800     PERFORM 1050-VALIDATE-CONTROL-CARD.                                  
019900                                                                          
020000********  TIME IS CONTAINED IN CURRENT DATE IN POSITION 9 FOR 8           
020100     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME.                      
020200                                                                          
020300     DISPLAY '**       RUN TIME ==> '  PV-RUN-TIME.                       
020400                                                                          
020500     MOVE PV-RUN-HH          TO PV-RUN-HH-CN.                             
020600     MOVE PV-RUN-MM          TO PV-RUN-MM-CN.                             
020700     MOVE PV-RUN-SS          TO PV-RUN-SS-CN.                             
020800                                                                          
020900     PERFORM 7000-OPEN-EXTRACT-CURSOR.                                    
021000     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
021100                                                                          
021200******************************************************************        
021300**  VALIDATE THE DATE FROM THE SCHEDULE                                   
021400******************************************************************        
021500 1050-VALIDATE-CONTROL-CARD.                                              
021600     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
021700     SET DPG51-FISCAL-454-CALENDAR                                        
021800         DPG51-DO-NOT-INCR-DECR-DATE                                      
021900         DPG52-LK-DTE-ISO           TO TRUE.                              
022000                                                                          
022100     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
022200                                                                          
022300     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
022400             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
022500                                                                          
022600     IF DPG54-NO-ERROR                                                    
022700         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
022800     ELSE                                                                 
022900         DISPLAY '** ABEND **'                                            
023000         DISPLAY '** PROGRAM = VLKUT034 -- CALLED MODULE '                
023100                  'DPKUT500 **'                                           
023200         DISPLAY '** PARAGRAPH = 1050-VALIDATE-CONTROL-CARD **'           
023300         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
023400         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
023500         DISPLAY DPG54-ERROR-MESSAGE                                      
023600         MOVE +4003 TO PV-ABEND-CODE                                      
023700         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
023800     END-IF.                                                              
023900                                                                          
024000*****************************************************************         
024100* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD                        
024200*****************************************************************         
024300 2000-PROCESS.                                                            
024400     INITIALIZE VL034-MERCHANT-RECORD                                     
024500                VL-MERCH-RECORD.                                          
024600     MOVE 'A'                     TO VL034-ACD-INDICATOR.                 
024700     EVALUATE RSPBAR-RSPLVL-CDE                                           
024800         WHEN 'GMM'                                                       
024900             MOVE MDSEAR-GMA-NBR    TO VL034-MERCHANT-NUMBER              
025000             MOVE RSPBAR-RSPLVL-CDE TO VL034-MERCHANT-DESIGNATOR          
025100         WHEN 'DMM'                                                       
025200             MOVE MDSEAR-DMA-NBR    TO VL034-MERCHANT-NUMBER              
025300             MOVE RSPBAR-RSPLVL-CDE TO VL034-MERCHANT-DESIGNATOR          
025400             MOVE MDSEAR-GMA-NBR    TO VL034-REPORT-TO-NUM                
025500             MOVE 'GMM'             TO VL034-REPORT-TO-DESIGNATOR         
025600         WHEN 'BYR'                                                       
025700             MOVE MDSEAR-BYR-NBR    TO VL034-MERCHANT-NUMBER              
025800             MOVE RSPBAR-RSPLVL-CDE TO VL034-MERCHANT-DESIGNATOR          
025900             MOVE MDSEAR-DMA-NBR    TO VL034-REPORT-TO-NUM                
026000             MOVE 'DMM'             TO VL034-REPORT-TO-DESIGNATOR         
026100             MOVE MDSEAR-DEPT-NBR   TO VL034-DEPARTMENT                   
026200     END-EVALUATE.                                                        
026300                                                                          
026400     STRING RSPBAR-WORKER-LAST-NAME DELIMITED BY '  '                     
026500            ',' DELIMITED BY SIZE                                         
026600            RSPBAR-WORKER-FIR-NAME DELIMITED BY '  '                      
026700       INTO VL034-MERCHANT-NAME.                                          
026800                                                                          
026900     MOVE 'X'                     TO VL034-ANCHOR-CONSTANT.               
027000                                                                          
027100     PERFORM  8000-WRITE-OUTPUT.                                          
027200                                                                          
027300     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
027400                                                                          
027500******************************************************************        
027600* OPEN CURSOR                                                             
027700******************************************************************        
027800 7000-OPEN-EXTRACT-CURSOR.                                                
027900     PERFORM                                                              
028000         WITH TEST AFTER                                                  
028100         VARYING PV-RETRY-COUNT                                           
028200         FROM 1 BY 1                                                      
028300         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
028400         OR  (SQLCODE = +0))                                              
028500                                                                          
028600         EXEC SQL                                                         
028700              OPEN EXTRACT-CSR                                            
028800         END-EXEC                                                         
028900                                                                          
029000         EVALUATE TRUE                                                    
029100             WHEN SQLCODE = +0                                            
029200                 CONTINUE                                                 
029300             WHEN SQLWARN0 NOT EQUAL SPACE                                
029400             WHEN SQLCODE NOT EQUAL ZERO                                  
029500                 DISPLAY '**********  ABEND **********'                   
029600                 DISPLAY '****** PROGRAM = VLKUT034 **'                   
029700                 DISPLAY '** PARAGRAPH = 7000-OPEN-EXTRACT-CURSOR'        
029800                 MOVE 'TMDSEAR'        TO PV-ABEND-STRUCTURE-1            
029900                 MOVE 'TRSPBAR'        TO PV-ABEND-STRUCTURE-2            
030000                 PERFORM 9200-SQL-ERROR                                   
430100         END-EVALUATE                                                     
030200     END-PERFORM.                                                         
030300                                                                          
030400     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
030500         MOVE '7000-OPEN-EXTRACT-CURSOR' TO PV-PARAGRAPH                  
030600         MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-FUNCTION               
030700         MOVE 'TMDSEAR'                  TO PV-ABEND-STRUCTURE-1          
030800         MOVE 'TRSPBAR'                  TO PV-ABEND-STRUCTURE-2          
030900         PERFORM 9200-SQL-ERROR                                           
031000     END-IF.                                                              
031100                                                                          
031200******************************************************************        
031300* CLOSE CURSOR                                                            
031400******************************************************************        
031500 7100-CLOSE-EXTRACT-CURSOR.                                               
031600     PERFORM                                                              
031700         WITH TEST AFTER                                                  
031800         VARYING PV-RETRY-COUNT                                           
031900         FROM 1 BY 1                                                      
032000         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
032100         OR  (SQLCODE = +0))                                              
032200                                                                          
032300         EXEC SQL                                                         
032400             CLOSE EXTRACT-CSR                                            
032500         END-EXEC                                                         
032600                                                                          
032700         EVALUATE TRUE                                                    
032800             WHEN SQLCODE = +0                                            
032900                 CONTINUE                                                 
033000             WHEN SQLWARN0 NOT EQUAL SPACE                                
033100             WHEN SQLCODE NOT EQUAL ZERO                                  
033200                 DISPLAY '************ ABEND **********'                  
033300                 DISPLAY '******* PROGRAM = VLKUT034 **'                  
033400                 DISPLAY '* PARAGRAPH = 7100-CLOSE-EXTRACT-CURSOR'        
033500                 MOVE 'TMDSEAR'         TO PV-ABEND-STRUCTURE-1           
033600                 MOVE 'TRSPBAR'         TO PV-ABEND-STRUCTURE-2           
033700                 PERFORM 9200-SQL-ERROR                                   
033800         END-EVALUATE                                                     
033900     END-PERFORM.                                                         
034000                                                                          
034100     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
034200         MOVE '7100-CLOSE-EXTRACT-CURSOR' TO PV-PARAGRAPH                 
034300         MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-FUNCTION               
034400         MOVE 'TMDSEAR'                  TO PV-ABEND-STRUCTURE-1          
034500         MOVE 'TRSPBAR'                  TO PV-ABEND-STRUCTURE-2          
034600         PERFORM 9200-SQL-ERROR                                           
034700     END-IF.                                                              
034800                                                                          
034900******************************************************************        
035000*  FETCH CURSOR                                                           
035100******************************************************************        
035200 7200-FETCH-EXTRACT-CURSOR.                                               
035300     PERFORM                                                              
035400         WITH TEST AFTER                                                  
035500         VARYING PV-RETRY-COUNT                                           
035600         FROM 1 BY 1                                                      
035700         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
035800         OR  (SQLCODE = +0) OR (PS-END-CSR))                              
035900                                                                          
036000         EXEC SQL                                                         
036100             FETCH EXTRACT-CSR                                            
036200             INTO :MDSEAR-GMA-NBR                                         
036300                , :MDSEAR-DMA-NBR                                         
036400                , :MDSEAR-DEPT-NBR                                        
036500                , :MDSEAR-BYR-NBR                                         
036600                , :RSPBAR-RSPLVL-CDE                                      
036700                , :RSPBAR-WORKER-LAST-NAME                                
036800                , :RSPBAR-WORKER-FIR-NAME                                 
036900         END-EXEC                                                         
037000                                                                          
037100         EVALUATE TRUE                                                    
037200             WHEN SQLCODE = ZERO                                          
037300                 CONTINUE                                                 
037400             WHEN SQLCODE = +100                                          
037500                 SET PS-END-CSR TO TRUE                                   
037600             WHEN SQLWARN0 NOT EQUAL SPACE                                
037700             WHEN SQLCODE NOT EQUAL ZERO                                  
037800                 DISPLAY '********** ABEND ************'                  
037900                 DISPLAY '******* PROGRAM = VLKUT034 **'                  
438000                 DISPLAY '* PARAGRAPH = 7200-FETCH-EXTRACT-CURSOR'        
038100                 MOVE 'TMDSEAR'      TO PV-ABEND-STRUCTURE-1              
038200                 MOVE 'TRSPBAR'      TO PV-ABEND-STRUCTURE-2              
038300                 PERFORM 9200-SQL-ERROR                                   
038400         END-EVALUATE                                                     
038500     END-PERFORM.                                                         
038600                                                                          
038700     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
038800         MOVE '7200-FETCH-EXTRACT-CURSOR' TO PV-PARAGRAPH                 
038900         MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-FUNCTION               
039000         MOVE 'TMDSEAR'                  TO PV-ABEND-STRUCTURE-1          
039100         MOVE 'TRSPBAR'                  TO PV-ABEND-STRUCTURE-2          
039200         PERFORM 9200-SQL-ERROR                                           
039300     END-IF.                                                              
039400                                                                          
039500                                                                          
039600*****************************************************************         
039700*  WRITE OUTPUT FILE                                                      
039800*****************************************************************         
039900 8000-WRITE-OUTPUT.                                                       
040000     WRITE VL-MERCH-RECORD FROM VL034-MERCHANT-RECORD.                    
040100                                                                          
040200     ADD 1                TO PV-RECS-WRITTEN.                             
040300                                                                          
040400*****************************************************************         
040500*  CREATE MARKER FILE                                                     
040600*****************************************************************         
040700 8500-CREATE-MARKER.                                                      
040800     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
040900     MOVE 'MERCHANT.DAT'  TO PV-MERCHANT-DAT.                             
041000     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
041100     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
041200     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
041300     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
041400     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
041500                                                                          
041600     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
041700                                                                          
041800*****************************************************************         
041900*  END OF JOB PROCESSING                                                  
042000*****************************************************************         
042100 9000-EOJ.                                                                
042200     PERFORM 7100-CLOSE-EXTRACT-CURSOR.                                   
042300                                                                          
042400     DISPLAY 'RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.                    
042500                                                                          
042600     PERFORM 8500-CREATE-MARKER.                                          
042700                                                                          
042800     CLOSE VL-MERCH-FILE                                                  
042900           VL-MARKER-FILE.                                                
043000                                                                          
043100                                                                          
043200*****************************************************************         
043300*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
043400*****************************************************************         
043500 9200-SQL-ERROR.                                                          
043600     CLOSE VL-MERCH-FILE                                                  
043700           VL-MARKER-FILE.                                                
043800                                                                          
043900     MOVE SQLCODE                     TO SQLCODE2.                        
044000     MOVE SQLERRD(3)                  TO SQLERRD3.                        
044100                                                                          
044200     DISPLAY '** ABEND **         ** = SQLERROR'.                         
044300     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT034.                
044400     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
044500     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
044600     DISPLAY '**           ** = ' PV-ERROR-MSG                            
044700                                                                          
044800     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
044900     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
045000     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
045100     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
045200                                                                          
045300     COPY DPPD004.                                                        
045400                                                                          
045500     MOVE +4013                       TO PV-ABEND-CODE.                   
045600     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
045700                                                                          
045800*************  END OF VLKUT034  ********************************          
