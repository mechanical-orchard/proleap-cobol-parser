       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      INKUT143.                                               
       AUTHOR.          JAN BLAZICH.                                            
       DATE-WRITTEN.    01/12/06.                                               
      *                                                                         
      ******************************************************************        
      * PURPOSE.                                                                
      *      EXTRACT ALL RE-CLASSED SKU'S WITHIN THE CURRENT WEEK               
      *      (BASED ON A CONTROL CARD) FROM THE IMT_RECLS DB2 TABLE.            
      ******************************************************************        
      *      DB2 TABLES:  TDTATTR                                               
      *                   IMT_RECLS(IMT00015)                                   
      *      DB2 CURSORS: RECLASS_SKU_CSR                                       
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RECLS-DATE-CNTL-FILE    ASSIGN TO INP01.                      
           SELECT RECLS-EXTRACT-FILE      ASSIGN TO OUT01.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RECLS-DATE-CNTL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RECLS-DATE-CNTL-REC              PIC X(80).                          
                                                                                
       FD  RECLS-EXTRACT-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RECLS-EXTRACT-REC                PIC  X(56).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE +0.                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-EXTRACT-OUT-COUNT         PIC S9(09) COMP-3 VALUE +0.         
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +2.                
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'INKUT143'.        
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-INPUT-SW           PIC X      VALUE 'N'.               
               88  PS-END-OF-INPUT                     VALUE 'Y'.               
               88  PS-NOT-END-OF-INPUT                 VALUE 'N'.               
           05  PS-TDTATTR-ROW-IS-THERE-SW   PIC X      VALUE 'N'.               
               88  PS-TDTATTR-ROW-IS-THERE             VALUE 'Y'.               
               88  PS-TDTATTR-ROW-IS-THERE-NO          VALUE 'N'.               
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.             
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.             
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.              
           05  PV-RETRY-COUNTER             PIC S9(04)    VALUE +0              
                                                          COMP SYNC.            
                                                                                
008500     05  PV-PROCESS-TMST.                                                 
008600         10  PV-PROCESS-DATE          PIC X(10).                          
008600         10  FILLER           REDEFINES PV-PROCESS-DATE.                  
008800             15  PV-PROCESS-CCYY      PIC X(04).                          
008900             15  PV-PROCESS-HYPHEN1   PIC X(01).                          
009000             15  PV-PROCESS-MM        PIC X(02).                          
009100             15  PV-PROCESS-HYPHEN2   PIC X(01).                          
009200             15  PV-PROCESS-DD        PIC X(02).                          
009300         10  FILLER                   PIC X(16)                           
009400             VALUE '-00.00.00.000000'.                                    
           05  PV-PROC-DATE.                                                    
               10  PV-PROC-CCYY             PIC X(04).                          
               10  PV-PROC-MM               PIC X(02).                          
               10  PV-PROC-DD               PIC X(02).                          
           05  PV-PROC-DATE-R REDEFINES PV-PROC-DATE                            
                                            PIC 9(8).                           
           05  PV-PROC-DATE-COMP            PIC 9(4)  COMP   VALUE 0.           
                                                                                
           05  PV-FSCL-WK-BEG-DTE           PIC X(10).                          
           05  PV-FSCL-WK-END-DTE           PIC X(10).                          
           05  PV-FSCL-MN-BEG-DTE           PIC X(10).                          
           05  PV-FSCL-MN-END-DTE           PIC X(10).                          
                                                                                
      ******************************************************************        
      *  DATE CONTROL CARD                                                      
      ******************************************************************        
       01  IN143-CONTROL-RECORD.                                                
           05  IN143-CC-DATE               PIC X(10).                           
           05  FILLER  REDEFINES IN143-CC-DATE.                                 
               10  IN143-CC-CCYY           PIC X(4).                            
               10  FILLER                  PIC X.                               
               10  IN143-CC-MM             PIC X(2).                            
               10  FILLER                  PIC X.                               
               10  IN143-CC-DD             PIC X(2).                            
           05  FILLER                      PIC X(70).                           
                                                                                
      ******************************************************************        
      *  DB2 FORMATTED MESSAGE AREA                                             
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * CUTOFF DATABASE - EXTRACT COPYBOOK                                      
      *****************************************************************         
      *01  IN143-SKU-RECLASS-EXTRACT.                                           
           COPY INRD143.                                                        
                                                                                
      ******************************************************************        
      * SQL COMMUNICATIONS WORK AREA                                            
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00015                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SQL CURSORS                                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               DECLARE RECLASS_SKU_CSR CURSOR FOR                               
                 SELECT SKU_NBR                                                 
                       ,RECLS_TMST                                              
                       ,OLD_DEPT_NBR                                            
                       ,OLD_MAJ_CL_NBR                                          
                       ,OLD_SUB_CL_NBR                                          
                       ,NW_DEPT_NBR                                             
                       ,NW_MAJ_CL_NBR                                           
                       ,NW_SUB_CL_NBR                                           
                   FROM IMT_RECLS                                               
                  WHERE DATE(RECLS_TMST) >=  :PV-FSCL-WK-BEG-DTE                
                    AND DATE(RECLS_TMST) <=  :PV-FSCL-WK-END-DTE                
                  ORDER BY DATE(RECLS_TMST)                                     
                          ,SKU_NBR                                              
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 1500-OPEN-RECLASS-SKU-CSR.                                   
                                                                                
           PERFORM 2000-FETCH-RECLS-SKUS                                        
             UNTIL PS-END-OF-INPUT.                                             
                                                                                
           PERFORM 3000-CLOSE-RECLASS-SKU-CSR.                                  
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN ALL FILES AND READ A  INPUT FILE RECORD.                 *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  RECLS-DATE-CNTL-FILE                                     
                OUTPUT RECLS-EXTRACT-FILE.                                      
           PERFORM 1100-READ-RECLS-DATE.                                        
                                                                                
           SET PS-NOT-END-OF-INPUT        TO TRUE.                              
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE.                        
                                                                                
           IF PV-PROCESS-CCYY NOT NUMERIC                                       
             OR PV-PROCESS-MM NOT NUMERIC                                       
             OR PV-PROCESS-DD NOT NUMERIC                                       
             OR PV-PROCESS-HYPHEN1 NOT = '-'                                    
             OR PV-PROCESS-HYPHEN2 NOT = '-'                                    
               MOVE +4002                 TO ABEND-CODE                         
               DISPLAY '**  ABEND     **'                                       
               DISPLAY '**  PROGRAM   **  =  INKUT143'                          
               DISPLAY '**  INVALID PROCESSING DATE '                           
               CALL 'ILBOABN0'  USING ABEND-CODE                                
           END-IF.                                                              
                                                                                
           PERFORM 1300-GET-BEG-END-DATES.                                      
           IF PS-TDTATTR-ROW-IS-THERE                                           
               DISPLAY ' PROCESSING DATE: ' PV-PROCESS-DATE                     
               DISPLAY                                                          
                 ' DTATTR-FSCL-WK-BEG-DTE=' DTATTR-FSCL-WK-BEG-DTE              
               DISPLAY                                                          
                 ' DTATTR-FSCL-WK-END-DTE=' DTATTR-FSCL-WK-END-DTE              
               DISPLAY                                                          
                 ' DTATTR-FSCL-MN-BEG-DTE=' DTATTR-FSCL-MN-BEG-DTE              
               DISPLAY                                                          
                 ' DTATTR-FSCL-MN-END-DTE=' DTATTR-FSCL-MN-END-DTE.             
                                                                                
           MOVE DTATTR-FSCL-WK-BEG-DTE    TO PV-FSCL-WK-BEG-DTE                 
           MOVE DTATTR-FSCL-WK-END-DTE    TO PV-FSCL-WK-END-DTE                 
           MOVE DTATTR-FSCL-MN-BEG-DTE    TO PV-FSCL-MN-BEG-DTE                 
           MOVE DTATTR-FSCL-MN-END-DTE    TO PV-FSCL-MN-END-DTE.                
      ******************************************************************        
      *    READ THE PROCESS DATE CONTROL CARD FILE                              
      ******************************************************************        
       1100-READ-RECLS-DATE.                                                    
                                                                                
           READ RECLS-DATE-CNTL-FILE                                            
               INTO IN143-CONTROL-RECORD                                        
               AT END                                                           
                   MOVE +4001                 TO ABEND-CODE                     
                   DISPLAY '**  ABEND     **'                                   
                   DISPLAY '**  PROGRAM   **  =  INKUT143'                      
                   DISPLAY '**  EMPTY DATE CONTROL FILE'                        
                   CALL 'ILBOABN0'  USING ABEND-CODE                            
           END-READ.                                                            
                                                                                
      *  REMOVE DASHES FROM DATE AND PUT THE DATE IN A BINARY FIELD.            
           MOVE IN143-CC-DATE              TO PV-PROCESS-DATE.                  
           MOVE IN143-CC-CCYY              TO PV-PROC-CCYY.                     
           MOVE IN143-CC-MM                TO PV-PROC-MM.                       
           MOVE IN143-CC-DD                TO PV-PROC-DD.                       
           MOVE PV-PROC-DATE-R             TO PV-PROC-DATE-COMP.                
                                                                                
      ******************************************************************        
      *    GET BEGINNING AND END OF WEEK DATES FROM TDTATTR                     
      ******************************************************************        
       1300-GET-BEG-END-DATES.                                                  
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
               UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES                        
                  OR   SQLCODE = ZERO                                           
                  OR   SQLCODE = +100                                           
                                                                                
             EXEC SQL                                                           
               SELECT FSCL_WK_BEG_DTE                                           
                     ,FSCL_WK_END_DTE                                           
                     ,FSCL_MN_BEG_DTE                                           
                     ,FSCL_MN_END_DTE                                           
                 INTO                                                           
                     :DTATTR-FSCL-WK-BEG-DTE                                    
                    ,:DTATTR-FSCL-WK-END-DTE                                    
                    ,:DTATTR-FSCL-MN-BEG-DTE                                    
                    ,:DTATTR-FSCL-MN-END-DTE                                    
                 FROM TDTATTR                                                   
                WHERE KY_DTE = :PV-PROCESS-DATE                                 
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = ZERO                                            
                     SET PS-TDTATTR-ROW-IS-THERE TO TRUE                        
                 WHEN SQLCODE = +100                                            
                     SET PS-TDTATTR-ROW-IS-THERE-NO TO TRUE                     
                                                                                
                 WHEN SQLWARN0 NOT EQUAL SPACE                                  
                 WHEN SQLCODE NOT EQUAL ZERO                                    
                     DISPLAY '****************************************'         
                     DISPLAY '** ABEND                              **'         
                     DISPLAY '** PROGRAM = INKUT143                 **'         
                     DISPLAY '** PARAGRAPH = 1300-GET-BEG-END-DATES **'         
                     DISPLAY '** SELECT TDTATTR                     **'         
                     DISPLAY '****************************************'         
                     PERFORM 9999-SQL-ABEND                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
               SQLCODE NOT = ZERO                                               
               DISPLAY '****************************************'               
               DISPLAY '** MAX RETRIES EXCEEDED ON SELECT     **'               
               DISPLAY '** FROM TDTATTR                       **'               
               DISPLAY '** PARAGRAPH = 1300-GET-BEG-END-DATES **'               
               DISPLAY '****************************************'               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      ******************************************************************        
      *    OPEN RECLASS_SKU_CSR                                                 
      ******************************************************************        
       1500-OPEN-RECLASS-SKU-CSR.                                               
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
                                                                                
                                                                                
               EXEC SQL                                                         
                   OPEN RECLASS_SKU_CSR                                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                     CONTINUE                                                   
                 WHEN SQLWARN0 NOT EQUAL SPACE                                  
                 WHEN SQLCODE NOT EQUAL ZERO                                    
                     DISPLAY '**************************************'           
                     DISPLAY '** ABEND                            **'           
                     DISPLAY '** PROGRAM = INKUT143               **'           
                     DISPLAY '** PARA = 1500-OPEN-RECLASS_SKU_CSR **'   '       
                     DISPLAY '** OPEN RECLASS_SKU_CSR             **'   '       
                     DISPLAY '**************************************'           
                     PERFORM 9999-SQL-ABEND                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
               (SQLCODE NOT = ZERO)                                             
               DISPLAY '****************************************'               
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
               DISPLAY '** OPEN RECLASS_SKU_CSR               **'               
               DISPLAY '** PARA = 1500-OPEN-RECLASS_SKU_CSR   **'               
               DISPLAY '****************************************'               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  FETCH ROWS THAT ARE WITHIN THE DATE RANGE                              
      *  EXTRACT THIS WEEK'S SKUS                                               
      ******************************************************************        
       2000-FETCH-RECLS-SKUS.                                                   
                                                                                
           EXEC SQL                                                             
             FETCH                                                              
               FROM RECLASS_SKU_CSR                                             
               INTO                                                             
                 :IMT00015-SKU-NBR                                              
                ,:IMT00015-RECLS-TMST                                           
                ,:IMT00015-OLD-DEPT-NBR                                         
                ,:IMT00015-OLD-MAJ-CL-NBR                                       
                ,:IMT00015-OLD-SUB-CL-NBR                                       
                ,:IMT00015-NW-DEPT-NBR                                          
                ,:IMT00015-NW-MAJ-CL-NBR                                        
                ,:IMT00015-NW-SUB-CL-NBR                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   MOVE IMT00015-RECLS-TMST   TO IN143-RECLASS-EFF-DATE         
                   MOVE IMT00015-OLD-DEPT-NBR   TO IN143-OLD-DEPT               
                   MOVE IMT00015-OLD-MAJ-CL-NBR TO IN143-OLD-MCL                
                   MOVE IMT00015-OLD-SUB-CL-NBR TO IN143-OLD-SCL                
                   MOVE IMT00015-SKU-NBR        TO IN143-SKU                    
                   MOVE IMT00015-NW-DEPT-NBR    TO IN143-NEW-DEPT               
                   MOVE IMT00015-NW-MAJ-CL-NBR  TO IN143-NEW-MCL                
                   MOVE IMT00015-NW-SUB-CL-NBR  TO IN143-NEW-SCL                
                   PERFORM 2500-WRITE-RECLS-EXTR-REC                            
                                                                                
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-INPUT         TO TRUE                          
                                                                                
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   DISPLAY '***************************************'            
                   DISPLAY '** ABEND                             **'            
                   DISPLAY '** PROGRAM = INKUT143                **'            
                   DISPLAY '** PARA = 3000-FETCH-ITM-RECLS-ROWS  **'            
                   DISPLAY '**************************************'             
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE A RECLASS EXTRACT OUTPUT FILE RECORD                             
      ******************************************************************        
       2500-WRITE-RECLS-EXTR-REC.                                               
                                                                                
           WRITE RECLS-EXTRACT-REC FROM IN143-SKU-RECLASS-EXTRACT.              
           ADD +1 TO PA-EXTRACT-OUT-COUNT.                                      
           INITIALIZE DCLIMT00015                                               
                      IN143-SKU-RECLASS-EXTRACT.                                
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE RECLASS_SKU_CSR                                                
      ******************************************************************        
       3000-CLOSE-RECLASS-SKU-CSR.                                              
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
                                                                                
                                                                                
               EXEC SQL                                                         
                   CLOSE RECLASS_SKU_CSR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZERO                                            
                     CONTINUE                                                   
                 WHEN SQLWARN0 NOT EQUAL SPACE                                  
                 WHEN SQLCODE NOT EQUAL ZERO                                    
                     DISPLAY '**************************************'           
                     DISPLAY '** ABEND                            **'           
                     DISPLAY '** PROGRAM = INKUT143               **'           
                     DISPLAY '** PARA = 3000-CLOSE-RECLASS_SKU_CSR**'           
                     DISPLAY '** OPEN RECLASS_SKU_CSR             **'           
                     DISPLAY '**************************************'           
                     PERFORM 9999-SQL-ABEND                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
               (SQLCODE NOT = ZERO)                                             
               DISPLAY '****************************************'               
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'               
               DISPLAY '** OPEN RECLASS_SKU_CSR               **'               
               DISPLAY '** PARA   = 3000-CLOSE-RECLASS_SKU_CSR**'               
               DISPLAY '****************************************'               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    - CLOSE THE FILES PROCESSED BY PROGRAM                      *        
      *    - DISPLAY OUTPUT RECORD COUNTS                              *        
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE RECLS-DATE-CNTL-FILE                                           
                 RECLS-EXTRACT-FILE.                                            
                                                                                
           DISPLAY SPACE.                                                       
           DISPLAY '*** NUMBER OF EXTRACT RECORDS WRITTEN = '                   
               PA-EXTRACT-OUT-COUNT.                                            
           DISPLAY SPACE.                                                       
                                                                                
                                                                                
      ******************************************************************        
      * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING             
      * CODE OCCURS.                                                            
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      *--------------------------------------------------------------*          
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *--------------------------------------------------------------*          
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
