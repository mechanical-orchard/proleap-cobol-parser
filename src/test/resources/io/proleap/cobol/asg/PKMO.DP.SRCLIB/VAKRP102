       ID  DIVISION.                                                            
W34517 PROGRAM-ID.         VAKRP102                                             
       AUTHOR.             MARC BLAZICH - G&R.                                  
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       9/93.                                                
       DATE-COMPILED.                                                           
                                                                                
W34517****IMPORTANT*****************************************************        
W34517* THIS IS A CLONE OF THE INKRP102 CREATED FOR THE VA PROJECT     *        
W34517* THE MAJOR CHANGES ARE .....                                    *        
W34517******************************************************************        
      ******************************************************************        
      *       A D J U S T E D   P H Y S I C A L   I N V E N T O R Y             
      ******************************************************************        
      *   THIS REPORT IS PRODUCED BY STORE, SPANNING ALL DEPARTMENTS.           
      ******************************************************************        
      *  CHANGED: 04/22/98     JEFF HARTIG     WR# 6041                *        
      *                                                                *        
      *  PGM CHANGED FOR Y2K COMPLIANCE.  REMOVE PV-TODAYS-DATE-FMT    *        
      *  AND ALL ASSOCIATED FIELDS.                                    *        
      ******************************************************************        
      *  CHANGED: 09/16/98     SCOTT SWESSEL   WR#99999                *        
      *                                                                *        
      *  PGM CHANGED TO REPLACE CONSIGNMENT COLUMN WITH SALE           *        
      *  AJUSTMENTS.                                                   *        
      ******************************************************************        
      *  CHANGED: 02/24/99     MARIA KLAMIK    WR#99999                *        
      *                                                                *        
      *  CHANGED LOGIC OF PROGRAM TO CAPTURE DATA FOR STORES           *        
      *  DOING INVENTORY DURING THE OPEN FISCAL MONTH.                 *        
      *  TOOK OUT REFERENCES TO TMDSEAR & TMDSEDP.                     *        
      ******************************************************************        
      *  CHANGED: 11/23/99     AMY LAWSON      WR#99999                *        
      *                                                                *        
      *  ADDED STORE NUMBER TO REPORT.                                 *        
      ******************************************************************        
      *  CHANGED: 06/22/00     JULIE SEIDLER   WR#99999                *        
      *                                                                *        
      *  REMOVE DISPLAY OF TADJDTL-STATUS-CODE.  THIS IS AN            *        
      *  UNNESSARY DISPLAY THAT FILLS UP THE SYSOUT                    *        
      ******************************************************************        
      *  CHANGED: 06/01/01  SCOTT JACKSON   WR#22000   CHGID:MAY01     *        
      *                                                                *        
      *  MISSED MARKDOWN ADJUSTMENTS (TADJDTL_TYPE_CDE = 'MM') ARE     *        
      *  TO BE REPORTED SEPARATELY FROM 'AUDIT CORRECTIONS'. NEW RPT   *        
      *  COLUMN WAS ADDED TO THIS REPORT TO SEGREGATE THESE CORRECTIONS*        
W21732******************************************************************        
W21732*  CHANGED: 02/13/03  SUDHARSHAN, IGSI    CHGID: W21732          *        
W21732*                                                                *        
W21732*  REMOVED THE LOGIC FOR FETCHING RECORDS FROM DIFFERENT TABLES  *        
W21732*  USING CURSORS. THIS LOGIC IS IMPLEMENTED IN THE NEW INKUT109  *        
W21732*  PROGRAM. NEW CHANGES ARE DONE TO READ THE FILE THAT GENERATED *        
W21732*  FROM INKUT109 PROGRAM AND TO CREATE THE REPORT.               *        
RLB312******************************************************************        
RLB312*  CHANGED: 03/12/03  RONNA BUTZ          CHGID: RLB312          *        
RLB312*                                                                *        
RLB312*  FIXED BUGS FROM CHANGES WITH CHANGE ID W21732                          
RLB312*  THE PROBLEM WAS THAT RECORDS WERE BEING DROPPED WHEN A STORE           
RLB312*  CHANGED IN PARAGRAPH B200                                              
RLB312*  ALSO CLEANED UP THE CODE IN PARAGRAPH B300.  THE NESTED IF             
RLB312*  THEN ELSE LOGIC WAS REPLACED WITH SIMPLER IF LOGIC AND                 
RLB312*  THE EVALUATE STATEMENT.                                                
21732 ******************************************************************        
21732 *  CHANGED: 04/28/03  ROD JANSSEN         CHGID: WR21732                  
21732 *                                                                         
21732 *  DAILY DETAIL STOCKROOM WITHDRAWAL & STORE CORRECTIONS REPORT           
21732 *  CREATION.                                                              
      ******************************************************************        
W26600*  CHANGED: 04/28/04  PAUL KEBER          CHGID: WR26600                  
W26600*                                                                         
W26600*  STORE NUMBER EXPANSION PROJECT CHANGES TO  REPLACE TSTR000 WITH        
W26600*  XMT_LOC AND TO ALLOW A 5 DIGIT STORE.                                  
      ******************************************************************        
W27710*  CHANGED: 01/28/05  ROD JANSSEN         CHGID: WR27710                  
W27710*                                                                         
W27710*  REMOVE CONTENTION ISSUES WITH OTHER INVENTORY JOBS.                    
      ******************************************************************        
W28543* W28543  05-02-2005 ROD JANSSEN MISSED MARKDOWN/MISSED MARKUP            
W28543*                                ENHANCEMENTS                             
      ******************************************************************        
W34517* W34517  10-14-2008 IN-VA INVENTORY STREAM SPLIT                         
      ******************************************************************        
W33304*  CHANGED: 11/11/2008 NIJESH/INFOSYS     CHGID: WR33304                  
W33304*  HOLDING INVENTORY LEDGER OPEN ACROSS PERIODS.                          
W33304******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
W21732     SELECT PHY-INVENTORY-SUMMARY-FILE                                    
W21732         ASSIGN TO INP01.                                                 
W21732                                                                          
           SELECT ADJ-PHY-INVENTORY-REPORT                                      
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
W21732                                                                          
W21732 FD  PHY-INVENTORY-SUMMARY-FILE                                           
W21732     RECORDING MODE IS F                                          INKUT115
W21732     BLOCK CONTAINS 0 CHARACTERS                                  INKUT115
W21732     LABEL RECORDS ARE STANDARD.                                  INKUT115
W21732                                                                  INKUT115
21732  01  PHY-INVENTORY-SUMMARY-REC     PIC X(100).                       CL*12
W21732                                                                  INKUT115
       FD  ADJ-PHY-INVENTORY-REPORT                                             
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE.                                                         
           05  REPORT-LINE-CC    PIC X(001).                                    
           05  REPORT-LINE-DATA  PIC X(132).                                    
           05  FILLER REDEFINES  REPORT-LINE-DATA.                              
               10  FILLER        PIC X(131).                                    
               10  REPORT-HEADER-FONT-INDICATOR                                 
                                 PIC X(001).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO.                  
           05  PC-LINE-COUNT           PIC S9(03)  VALUE ZERO COMP-3.           
           05  PC-REPORT-CC            PIC X(01)  VALUE SPACES.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W26600     05  PV-PREV-STORE           PIC X(5)     VALUE SPACES.               
W26600     05  PV-STORE-NUM            PIC 9(5)     VALUE ZERO.                 
           05  PV-PARA                 PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PC-MAX-RETRIES          PIC S9(4)                                
                                           COMP SYNC VALUE +2.                  
W33304     05  INT-STAT-CDE            PIC X(02)    VALUE SPACES.               
W33304     05  INT-STAT-CDE-DESC       PIC X(40)    VALUE SPACES.               
W33304     05  TINCNTL-INV-NM          PIC X(20)    VALUE SPACES.               
W33304     05  HOLD-STAT-CDE           PIC X(02)    VALUE SPACES.               
W33304     05  HOLD-INV-ID             PIC S9(9) COMP VALUE +0.                 
W33304     05  HOLD-INV-NAME           PIC X(20)    VALUE SPACES.               
W33304     05  WORK-INV-ID             PIC S9(5)    VALUE ZEROS.                
W33304     05  WORK-INV-ID-R  REDEFINES WORK-INV-ID                             
W33304                                 PIC 9(5).                                
           05  PV-RETRY-COUNT          PIC S9(4) COMP SYNC VALUE +0.            
           05  PV-SYSTEM-DATE.                                                  
               10  PV-SYS-YY           PIC 9(2)    VALUE ZEROES.                
               10  PV-SYS-MM           PIC 9(2)    VALUE ZEROES.                
               10  PV-SYS-DD           PIC 9(2)    VALUE ZEROES.                
           05  PV-DB2-DATE.                                                     
               10  PV-DB2-CC           PIC 9(2)    VALUE ZEROES.                
               10  PV-DB2-YY           PIC 9(2)    VALUE ZEROES.                
               10  FILLER              PIC X(1)    VALUE '-'.                   
               10  PV-DB2-MM           PIC 9(2)    VALUE ZEROES.                
               10  FILLER              PIC X(1)    VALUE '-'.                   
               10  PV-DB2-DD           PIC 9(2)    VALUE ZEROES.                
W33304     05  CYCL-HEAD               PIC X(5)    VALUE SPACES.                
W33304     05  CYCL-STAT-CDE           PIC X(02)   VALUE SPACES.                
W33304     05  CYCL-INV-DESC           PIC X(65)   VALUE SPACES.                
                                                                                
W34517     05  PV-JOB-INFO.                                                     
W34517         10  FILLER                  PIC X(01)       VALUE '('.           
W34517         10  PV-JOB-NAME             PIC X(08)       VALUE SPACES.        
W34517         10  FILLER                  PIC X(01)       VALUE ')'.           
                                                                                
           05  PV-COMPUTED-FIELDS.                                              
               10  PV-TSUMINV-AMT                PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
               10  PV-TSTKRMW-AMT                PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-AUDIT-AMT          PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-STORE-AMT          PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-SALE-ADJMT-AMT     PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
W28543         10  PV-TADJDTL-MISS-MKUP-AMT      PIC S9(15)V99 VALUE +0         
                                                 COMP-3.                        
W21732     05  PV-TOT-TSUMINV-AMT            PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-TSTKRMW-AMT            PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-TADJDTL-AUDIT-AMT      PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-TADJDTL-SALE-ADJMT-AMT PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W28543     05  PV-TOT-TADJDTL-MISS-MKUP-AMT  PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-TADJDTL-STORE-AMT      PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-NET-ADJUSTMENT         PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-TOT-ADJUSTED-PHYSICAL      PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-ADJUSTED-PHYSICAL          PIC S9(15)V99 VALUE +0             
W21732                                           COMP-3.                        
W33304     05  PV-CYL-TSUMINV-AMT            PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-TSTKRMW-AMT            PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-TADJDTL-STORE-AMT      PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-TADJDTL-MISS-MKUP-AMT  PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-TADJDTL-AUDIT-AMT      PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-TADJDTL-SALE-ADJMT-AMT PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-CYL-ADJUSTED-PHYSICAL      PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TSUMINV-AMT             PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TSTKRMW-AMT             PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TADJDTL-STORE-AMT       PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TADJDTL-MISS-MKUP-AMT   PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TADJDTL-AUDIT-AMT       PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-TADJDTL-SALE-ADJMT-AMT  PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
W33304     05  PV-GT-ADJUSTED-PHYSICAL       PIC S9(15)V99 VALUE +0             
W33304                                           COMP-3.                        
                                                                                
       01  SWITCHES.                                                            
W21732     05  PS-END-OF-PHY-INV-IND      PIC X       VALUE 'N'.                
W21732         88  PS-END-OF-PHY-INV-FILE          VALUE 'Y'.                   
W33304     05  FST-SW                     PIC X       VALUE 'Y'.                
W33304     05  CYCLE-BK                   PIC X       VALUE 'N'.                
W33304     05  END-PGM                    PIC X       VALUE 'N'.                
                                                                                
       01  CPU-DATE.                                                            
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.          
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.          
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.          
           05  FILLER                      PIC X(02)     VALUE SPACES.          
       01  H1-HEADING.                                                          
           05  FILLER              PIC X(44)   VALUE SPACES.                    
           05  FILLER              PIC X(89)   VALUE                            
              'SUMMARIZED ADJUSTED PHYSICAL INVENTORY REPORT'.                  
                                                                                
W33304 01  H1A-HEADING.                                                         
W33304     05  FILLER              PIC X(01)   VALUE ' '.                       
W33304     05  H1A-CYCLE-CDE       PIC 9(05)   VALUE ZEROS.                     
W33304     05  FILLER              PIC X(02)   VALUE SPACES.                    
W33304     05  H1A-INV-STAT-CDE    PIC X(02)   VALUE SPACES.                    
W33304     05  FILLER              PIC X(34)   VALUE SPACES.                    
W33304     05  H1A-INV-CYCLE       PIC X(22)   VALUE SPACES.                    
W33304     05  FILLER              PIC X(02)   VALUE SPACES.                    
W33304     05  H1A-INV-STAT        PIC X(65)   VALUE SPACES.                    
W33304                                                                          
       01  H2-HEADING.                                                          
W33304     05  FILLER              PIC X(01)   VALUE   ' '.                     
W33304     05  STORE-DISP          PIC X(05)   VALUE SPACES.                    
W33304     05  FILLER              PIC X(44)   VALUE                            
W26600**        ----5----0----5----0----5----0----5----0----5----0----5         
W33304               '                   PHYSICAL      STOCKROOM'.              
           05  FILLER              PIC X(51)   VALUE                            
               '        STORE         MISSED        AUDIT          '.           
W26600     05  FILLER              PIC X(32)   VALUE                            
W26600         '     SALE              ADJUSTED '.                              
                                                                                
       01  H3-HEADING.                                                          
           05  FILLER              PIC X(01)   VALUE   ' '.                     
W26600     05  FILLER              PIC X(49)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
W26600         '                          INVENTORY     WITHDRAWL'.             
           05  FILLER              PIC X(51)   VALUE                            
W28543         '    CORRECTIONS       MARKUPS    CORRECTIONS       '.           
           05  FILLER              PIC X(32)   VALUE                            
               ' ADJUSTMENTS           PHYSICAL '.                              
           EJECT                                                                
       01  D1-DETAIL-LINE.                                                      
           05  D1-CC                    PIC X(01).                              
               88  D1-SINGLE-SPACE                       VALUE ' '.             
               88  D1-DOUBLE-SPACE                       VALUE '0'.             
           05  D1-DETAIL-DATA.                                                  
W26600         10  D1-STORE-NBR         PIC X(05)        VALUE SPACES.          
               10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-STORE-NAME        PIC X(10)        VALUE SPACES.          
W26600         10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-PHYSICAL-INVEN    PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.                
               10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-STOCKROOM-WDRAWL  PIC Z,ZZZ,ZZ9.99-.                      
               10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-STORE-CORRECTIONS PIC ZZ,ZZZ,ZZ9.99-.                     
               10  FILLER               PIC X(01)        VALUE SPACES.          
W28543         10  D1-MISSED-MARKUPS    PIC Z,ZZZ,ZZ9.99-.                      
MAY01          10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-AUDIT-CORRECTIONS PIC ZZ,ZZZ,ZZ9.99-.                     
               10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-SALE-ADJUST       PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
               10  FILLER               PIC X(01)        VALUE SPACES.          
               10  D1-ADJUSTED-PHYSICAL PIC ZZ,ZZZ,ZZZ,ZZ9.99-.                 
                                                                                
      * COPY FOR STANDARD HEADING                                               
           COPY DPWS133O.                                                       
                                                                                
W34517******************************************************************        
W34517*  HEADER LINE                                                            
W34517******************************************************************        
W34517*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
W34517*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
W34517     COPY DPWS085.                                                        
                                                                                
W21732*****************************************************************         
W21732*  COPYBOOK FOR INPUT                                                     
W21732*****************************************************************         
W21732                                                                          
21732      COPY INRD109.                                                        
W21732                                                                          
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
W26600* DB2 XMT LOCATION TABLE DCLGEN                                           
      *****************************************************************         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 MERCHANDISE DEPARTMENT                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
W33304*****************************************************************         
W33304* TINCNTL DCLGEN                                                          
W33304*****************************************************************         
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
W33304*****************************************************************         
W33304* INT_INV_STAT_CDE DCLGEN                                                 
W33304*****************************************************************         
W33304     EXEC SQL                                                             
W33304         INCLUDE INT00001                                                 
W33304     END-EXEC.                                                            
W33304                                                                          
           EJECT                                                                
                                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W21732     PERFORM B200-PROCESS-PHY-INV-FILE                                    
W21732          UNTIL PS-END-OF-PHY-INV-FILE.                                   
                                                                                
           PERFORM B600-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
           EJECT                                                                
       B100-HOUSEKEEPING.                                                       
           MOVE '*B100-HOUSEKEEPING.              *' TO PV-PARA                 
                                                                                
W21732     OPEN INPUT  PHY-INVENTORY-SUMMARY-FILE                               
W21732          OUTPUT ADJ-PHY-INVENTORY-REPORT.                                
                                                                                
W34517*  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
W34517     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
W34517         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
W34517     MOVE DP085-JOB-NAME     TO PV-JOB-NAME.                              
W34517                                                                          
W34517     MOVE PV-JOB-INFO                                                     
W34517         TO DP133O-STANDRD-HEADER-133-COLS(20:10).                        
                                                                                
           ACCEPT CPU-DATE FROM DATE.                                           
           MOVE CPU-YEAR   TO DP133O-RUN-YEAR.                                  
           MOVE CPU-MONTH  TO DP133O-RUN-MONTH.                                 
           MOVE CPU-DAY    TO DP133O-RUN-DAY.                                   
                                                                                
           ACCEPT CPU-TIME FROM TIME.                                           
           MOVE CPU-HOUR   TO DP133O-RUN-HOUR.                                  
           MOVE CPU-MINUTE TO DP133O-RUN-MINUTE.                                
W34517     MOVE 'VAKRP102' TO DP133O-PROGRAM-NAME.                              
                                                                                
           MOVE +0         TO PC-PAGE-COUNT.                                    
                                                                                
W21732     PERFORM B150-READ-PHY-INV-FILE.                                      
W21732     IF NOT PS-END-OF-PHY-INV-FILE                                        
W33304        PERFORM Z200-READ-CNTL                                            
W21732        MOVE IN109-STORE-NBR TO PV-PREV-STORE                             
W21732        PERFORM Z100-READ-XMTLOC                                          
W33304        MOVE IN109-FINCL-INV-STAT-CDE TO HOLD-STAT-CDE                    
W21732        PERFORM Z800-PRINT-HEADINGS                                       
W21732     END-IF.                                                              
W21732                                                                          
W21732 B150-READ-PHY-INV-FILE.                                                  
W21732                                                                          
W21732     READ PHY-INVENTORY-SUMMARY-FILE                                      
W21732            INTO IN109-PHY-IN-SUMMARY-RECORD                              
W21732            AT END                                                        
W21732                 SET PS-END-OF-PHY-INV-FILE TO TRUE                       
W21732     END-READ.                                                            
W21732                                                                          
W21732 B200-PROCESS-PHY-INV-FILE.                                               
W21732                                                                          
W33304      IF  IN109-INV-ID NOT = HOLD-INV-ID                                  
W33304          AND FST-SW = 'N'                                                
W33304          PERFORM B400-PRINT-DETAIL-LINE                                  
W33304          PERFORM Z100-READ-XMTLOC                                        
W33304          INITIALIZE PV-COMPUTED-FIELDS                                   
W33304          PERFORM B700-STAT-BREAK                                         
W33304          MOVE 'Y' TO CYCLE-BK                                            
W33304          PERFORM Z800-PRINT-HEADINGS                                     
W33304          PERFORM B800-CYCLE-BREAK                                        
W33304          MOVE 'N' TO CYCLE-BK                                            
W33304          PERFORM Z800-PRINT-HEADINGS                                     
W33304          MOVE IN109-FINCL-INV-STAT-CDE TO HOLD-STAT-CDE                  
W33304      END-IF.                                                             
W33304                                                                          
W33304      IF  IN109-FINCL-INV-STAT-CDE NOT = HOLD-STAT-CDE                    
W33304          PERFORM B400-PRINT-DETAIL-LINE                                  
W33304          PERFORM Z100-READ-XMTLOC                                        
W33304          INITIALIZE PV-COMPUTED-FIELDS                                   
W33304          PERFORM B700-STAT-BREAK                                         
W33304          PERFORM Z800-PRINT-HEADINGS                                     
W33304          MOVE IN109-FINCL-INV-STAT-CDE TO HOLD-STAT-CDE                  
W33304      END-IF.                                                             
W33304                                                                          
W33304     MOVE 'N' TO FST-SW.                                                  
W21732     IF IN109-STORE-NBR EQUAL TO PV-PREV-STORE                            
W21732            PERFORM B300-PROCESS-RECORD                                   
W21732      ELSE                                                                
W21732        IF (PV-TSUMINV-AMT       NOT = +0) OR                             
W21732           (PV-TSTKRMW-AMT       NOT = +0) OR                             
W21732           (PV-TADJDTL-AUDIT-AMT NOT = +0) OR                             
W21732           (PV-TADJDTL-STORE-AMT NOT = +0) OR                             
W28543           (PV-TADJDTL-MISS-MKUP-AMT NOT = +0)                            
W21732            PERFORM B400-PRINT-DETAIL-LINE                                
W21732            PERFORM Z100-READ-XMTLOC                                      
W21732            INITIALIZE PV-COMPUTED-FIELDS                                 
W21732        END-IF                                                            
RLB312        PERFORM B300-PROCESS-RECORD                                       
W21732      END-IF.                                                             
W21732                                                                          
W21732      MOVE IN109-STORE-NBR  TO PV-PREV-STORE                              
W21732                                                                          
W21732      PERFORM B150-READ-PHY-INV-FILE.                                     
W21732                                                                          
W21732 B300-PROCESS-RECORD.                                                     
W21732                                                                          
W21732     IF IN109-TABLE-NAME = 'SUMINV'                                       
W21732           ADD IN109-EXTD-AMT TO PV-TSUMINV-AMT                           
RLB312     END-IF.                                                              
RLB312                                                                          
W21732     IF IN109-TABLE-NAME = 'STKRMW'                                       
W21732           ADD IN109-EXTD-AMT TO PV-TSTKRMW-AMT                           
RLB312     END-IF.                                                              
RLB312                                                                          
W21732     IF IN109-TABLE-NAME = 'ADJDTL'                                       
RLB312        EVALUATE TRUE                                                     
RBL312           WHEN IN109-ADJUST-TYPE-CODE = 'CO'                             
W21732              ADD IN109-EXTD-AMT TO PV-TADJDTL-STORE-AMT                  
RBL312           WHEN IN109-ADJUST-TYPE-CODE = 'SA'                             
W21732              ADD IN109-EXTD-AMT TO PV-TADJDTL-SALE-ADJMT-AMT             
RBL312           WHEN IN109-ADJUST-TYPE-CODE = 'MM'                             
W28543              ADD IN109-EXTD-AMT TO PV-TADJDTL-MISS-MKUP-AMT              
RBL312           WHEN OTHER                                                     
W21732              ADD IN109-EXTD-AMT TO PV-TADJDTL-AUDIT-AMT                  
RLB312        END-EVALUATE                                                      
RLB312     END-IF.                                                              
RLB312     IF IN109-TABLE-NAME = 'ADJADD'                                       
W21732                 ADD IN109-EXTD-AMT TO                                    
W21732                                PV-TADJDTL-AUDIT-AMT                      
W21732     END-IF.                                                              
                                                                                
       B400-PRINT-DETAIL-LINE.                                                  
                                                                                
           MOVE SPACES                    TO D1-DETAIL-DATA.                    
                                                                                
W26600     MOVE PV-PREV-STORE             TO D1-STORE-NBR                       
W26600     MOVE XMT00001-LOC-10-ABBR-NM   TO D1-STORE-NAME                      
           MOVE PV-TSUMINV-AMT            TO D1-PHYSICAL-INVEN                  
           MOVE PV-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL                
           MOVE PV-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS               
W28543     MOVE PV-TADJDTL-MISS-MKUP-AMT  TO D1-MISSED-MARKUPS                  
           MOVE PV-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS               
           MOVE PV-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST                     
                                                                                
           COMPUTE PV-ADJUSTED-PHYSICAL =                                       
                   PV-TSUMINV-AMT           -                                   
                   PV-TSTKRMW-AMT           +                                   
                   PV-TADJDTL-STORE-AMT     +                                   
W28543             PV-TADJDTL-MISS-MKUP-AMT +                                   
                   PV-TADJDTL-AUDIT-AMT     +                                   
                   PV-TADJDTL-SALE-ADJMT-AMT                                    
                                                                                
           IF PC-LINE-COUNT > +59                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE PV-ADJUSTED-PHYSICAL     TO D1-ADJUSTED-PHYSICAL.               
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           SET  D1-SINGLE-SPACE          TO TRUE.                               
                                                                                
W33304     ADD PV-TSUMINV-AMT TO                                                
W33304                         PV-TOT-TSUMINV-AMT                               
W33304                         PV-CYL-TSUMINV-AMT                               
W33304                         PV-GT-TSUMINV-AMT.                               
W33304                                                                          
W33304     ADD PV-TSTKRMW-AMT TO                                                
W33304                         PV-TOT-TSTKRMW-AMT                               
W33304                         PV-CYL-TSTKRMW-AMT                               
W33304                         PV-GT-TSTKRMW-AMT.                               
W33304                                                                          
W33304     ADD PV-TADJDTL-STORE-AMT TO                                          
W33304                         PV-TOT-TADJDTL-STORE-AMT                         
W33304                         PV-CYL-TADJDTL-STORE-AMT                         
W33304                         PV-GT-TADJDTL-STORE-AMT.                         
W33304                                                                          
W33304     ADD PV-TADJDTL-SALE-ADJMT-AMT TO                                     
W33304                         PV-TOT-TADJDTL-SALE-ADJMT-AMT                    
W33304                         PV-CYL-TADJDTL-SALE-ADJMT-AMT                    
W33304                         PV-GT-TADJDTL-SALE-ADJMT-AMT.                    
W33304                                                                          
W33304     ADD PV-TADJDTL-MISS-MKUP-AMT TO                                      
W33304                         PV-TOT-TADJDTL-MISS-MKUP-AMT                     
W33304                         PV-CYL-TADJDTL-MISS-MKUP-AMT                     
W33304                         PV-GT-TADJDTL-MISS-MKUP-AMT.                     
W33304                                                                          
W33304     ADD PV-TADJDTL-AUDIT-AMT TO                                          
W33304                         PV-TOT-TADJDTL-AUDIT-AMT                         
W33304                         PV-CYL-TADJDTL-AUDIT-AMT                         
W33304                         PV-GT-TADJDTL-AUDIT-AMT.                         
W33304                                                                          
       B600-EOJ.                                                                
                                                                                
W21732        IF (PV-TSUMINV-AMT       NOT = +0) OR                             
W21732           (PV-TSTKRMW-AMT       NOT = +0) OR                             
W21732           (PV-TADJDTL-AUDIT-AMT NOT = +0) OR                             
W21732           (PV-TADJDTL-STORE-AMT NOT = +0) OR                             
W28543           (PV-TADJDTL-MISS-MKUP-AMT NOT = +0)                            
W21732            PERFORM B400-PRINT-DETAIL-LINE                                
W21732            INITIALIZE PV-COMPUTED-FIELDS                                 
W21732        END-IF                                                            
           IF PC-LINE-COUNT > +58                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE SPACES                        TO D1-DETAIL-LINE.                
           SET  D1-DOUBLE-SPACE               TO TRUE.                          
                                                                                
           MOVE 'TOTAL'                       TO D1-STORE-NAME                  
           MOVE PV-TOT-TSUMINV-AMT            TO D1-PHYSICAL-INVEN              
           MOVE PV-TOT-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL            
           MOVE PV-TOT-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS           
W28543     MOVE PV-TOT-TADJDTL-MISS-MKUP-AMT  TO D1-MISSED-MARKUPS              
           MOVE PV-TOT-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS           
           MOVE PV-TOT-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST                 
                                                                                
           COMPUTE PV-TOT-ADJUSTED-PHYSICAL     =                               
                   PV-TOT-TSUMINV-AMT           -                               
                   PV-TOT-TSTKRMW-AMT           +                               
                   PV-TOT-TADJDTL-STORE-AMT     +                               
W28543             PV-TOT-TADJDTL-MISS-MKUP-AMT +                               
                   PV-TOT-TADJDTL-AUDIT-AMT     +                               
                   PV-TOT-TADJDTL-SALE-ADJMT-AMT                                
                                                                                
           MOVE PV-TOT-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL.               
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
W33304     ADD PV-TOT-ADJUSTED-PHYSICAL  TO                                     
W33304                                      PV-CYL-ADJUSTED-PHYSICAL            
W33304                                      PV-GT-ADJUSTED-PHYSICAL.            
W33304     MOVE 'Y' TO CYCLE-BK                                                 
W33304     PERFORM Z800-PRINT-HEADINGS.                                         
W33304                                                                          
W33304     MOVE 'CYCLE TOTL'                  TO D1-STORE-NAME.                 
W33304     MOVE PV-CYL-TSUMINV-AMT            TO D1-PHYSICAL-INVEN.             
W33304     MOVE PV-CYL-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL.           
W33304     MOVE PV-CYL-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS.          
W33304     MOVE PV-CYL-TADJDTL-MISS-MKUP-AMT  TO D1-MISSED-MARKUPS.             
W33304     MOVE PV-CYL-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS.          
W33304     MOVE PV-CYL-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST.                
W33304     MOVE PV-CYL-ADJUSTED-PHYSICAL      TO D1-ADJUSTED-PHYSICAL.          
W33304     MOVE D1-DETAIL-LINE                TO REPORT-LINE.                   
W33304     PERFORM Z700-WRITE-REPORT-LINE.                                      
W33304                                                                          
W33304     MOVE 'Y' TO END-PGM.                                                 
W33304     PERFORM Z800-PRINT-HEADINGS                                          
W33304     MOVE 'GRAND TOTL'                  TO D1-STORE-NAME.                 
W33304     MOVE PV-GT-TSUMINV-AMT             TO D1-PHYSICAL-INVEN.             
W33304     MOVE PV-GT-TSTKRMW-AMT             TO D1-STOCKROOM-WDRAWL.           
W33304     MOVE PV-GT-TADJDTL-STORE-AMT       TO D1-STORE-CORRECTIONS.          
W33304     MOVE PV-GT-TADJDTL-MISS-MKUP-AMT   TO D1-MISSED-MARKUPS.             
W33304     MOVE PV-GT-TADJDTL-AUDIT-AMT       TO D1-AUDIT-CORRECTIONS.          
W33304     MOVE PV-GT-TADJDTL-SALE-ADJMT-AMT  TO D1-SALE-ADJUST.                
W33304     MOVE PV-GT-ADJUSTED-PHYSICAL       TO D1-ADJUSTED-PHYSICAL.          
W33304     MOVE D1-DETAIL-LINE                TO REPORT-LINE.                   
W33304     PERFORM Z700-WRITE-REPORT-LINE.                                      
W33304                                                                          
W21732     CLOSE PHY-INVENTORY-SUMMARY-FILE                                     
W21732           ADJ-PHY-INVENTORY-REPORT.                                      
W33304                                                                          
W33304 B700-STAT-BREAK.                                                         
W33304     MOVE '*B700-STAT-BREAK.                 *' TO PV-PARA.               
W33304                                                                          
W33304     IF PC-LINE-COUNT > +58                                               
W33304         PERFORM Z800-PRINT-HEADINGS                                      
W33304     END-IF.                                                              
W33304                                                                          
W33304     MOVE SPACES                        TO D1-DETAIL-LINE.                
W33304     SET  D1-DOUBLE-SPACE               TO TRUE.                          
W33304                                                                          
W33304     MOVE 'TOTAL'                       TO D1-STORE-NAME.                 
W33304     MOVE PV-TOT-TSUMINV-AMT            TO D1-PHYSICAL-INVEN.             
W33304     MOVE PV-TOT-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL.           
W33304     MOVE PV-TOT-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS.          
W33304     MOVE PV-TOT-TADJDTL-MISS-MKUP-AMT  TO D1-MISSED-MARKUPS.             
W33304     MOVE PV-TOT-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS.          
W33304     MOVE PV-TOT-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST.                
W33304                                                                          
W33304     COMPUTE PV-TOT-ADJUSTED-PHYSICAL     =                               
W33304             PV-TOT-TSUMINV-AMT           -                               
W33304             PV-TOT-TSTKRMW-AMT           +                               
W33304             PV-TOT-TADJDTL-STORE-AMT     +                               
W33304             PV-TOT-TADJDTL-MISS-MKUP-AMT +                               
W33304             PV-TOT-TADJDTL-AUDIT-AMT     +                               
W33304             PV-TOT-TADJDTL-SALE-ADJMT-AMT.                               
W33304                                                                          
W33304     MOVE PV-TOT-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL.               
W33304     MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
W33304     PERFORM Z700-WRITE-REPORT-LINE.                                      
W33304                                                                          
W33304     ADD PV-TOT-ADJUSTED-PHYSICAL  TO                                     
W33304                                      PV-CYL-ADJUSTED-PHYSICAL            
W33304                                      PV-GT-ADJUSTED-PHYSICAL.            
W33304                                                                          
W33304     MOVE ZEROS TO PV-TOT-ADJUSTED-PHYSICAL                               
W33304                   PV-TOT-TSUMINV-AMT                                     
W33304                   PV-TOT-TSTKRMW-AMT                                     
W33304                   PV-TOT-TADJDTL-STORE-AMT                               
W33304                   PV-TOT-TADJDTL-MISS-MKUP-AMT                           
W33304                   PV-TOT-TADJDTL-AUDIT-AMT                               
W33304                   PV-TOT-TADJDTL-SALE-ADJMT-AMT                          
W33304                   PV-TOT-ADJUSTED-PHYSICAL.                              
W33304                                                                          
W33304 B800-CYCLE-BREAK.                                                        
W33304                                                                          
W33304     MOVE '*B800-CYCLE-BREAK.                *' TO PV-PARA.               
W33304                                                                          
W33304     IF PC-LINE-COUNT > +58                                               
W33304         PERFORM Z800-PRINT-HEADINGS                                      
W33304     END-IF.                                                              
W33304                                                                          
W33304     MOVE SPACES                        TO D1-DETAIL-LINE.                
W33304     SET  D1-DOUBLE-SPACE               TO TRUE.                          
W33304                                                                          
W33304     MOVE 'CYCLE TOTL'                  TO D1-STORE-NAME.                 
W33304     MOVE PV-CYL-TSUMINV-AMT            TO D1-PHYSICAL-INVEN.             
W33304     MOVE PV-CYL-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL.           
W33304     MOVE PV-CYL-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS.          
W33304     MOVE PV-CYL-TADJDTL-MISS-MKUP-AMT  TO D1-MISSED-MARKUPS.             
W33304     MOVE PV-CYL-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS.          
W33304     MOVE PV-CYL-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST.                
W33304                                                                          
W33304     COMPUTE PV-CYL-ADJUSTED-PHYSICAL     =                               
W33304             PV-CYL-TSUMINV-AMT           -                               
W33304             PV-CYL-TSTKRMW-AMT           +                               
W33304             PV-CYL-TADJDTL-STORE-AMT     +                               
W33304             PV-CYL-TADJDTL-MISS-MKUP-AMT +                               
W33304             PV-CYL-TADJDTL-AUDIT-AMT     +                               
W33304             PV-CYL-TADJDTL-SALE-ADJMT-AMT.                               
W33304                                                                          
W33304     MOVE PV-CYL-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL.               
W33304     MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
W33304     PERFORM Z700-WRITE-REPORT-LINE.                                      
W33304                                                                          
W33304     MOVE ZEROS TO PV-CYL-ADJUSTED-PHYSICAL                               
W33304                   PV-CYL-TSUMINV-AMT                                     
W33304                   PV-CYL-TSTKRMW-AMT                                     
W33304                   PV-CYL-TADJDTL-STORE-AMT                               
W33304                   PV-CYL-TADJDTL-MISS-MKUP-AMT                           
W33304                   PV-CYL-TADJDTL-AUDIT-AMT                               
W33304                   PV-CYL-TADJDTL-SALE-ADJMT-AMT                          
W33304                   PV-CYL-ADJUSTED-PHYSICAL.                              
                                                                                
W26600 Z100-READ-XMTLOC.                                                        
W21732                                                                          
W26600     MOVE '*Z100-READ-XMTLOC.                *' TO PV-PARA.               
W26600     MOVE IN109-STORE-NBR           TO PV-STORE-NUM.                      
W26600     MOVE PV-STORE-NUM              TO XMT00001-LOC-NBR.                  
W21732                                                                          
W21732     EXEC SQL                                                             
W26600         SELECT LOC_10_ABBR_NM                                            
W26600          INTO :XMT00001-LOC-10-ABBR-NM                                   
W26600          FROM XMT_LOC                                                    
W26600         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
W27710         WITH UR                                                          
W21732     END-EXEC                                                             
W21732                                                                          
W21732     EVALUATE TRUE                                                        
W21732         WHEN SQLCODE = +0                                                
W21732             CONTINUE                                                     
W21732         WHEN OTHER                                                       
W26600             MOVE 'READING XMTLOC TABLE'                                  
W21732                           TO  PV-DB2-OPER-ATTEMPTED                      
W21732             PERFORM Z900-SQL-ABEND                                       
W21732     END-EVALUATE.                                                        
W33304                                                                          
W33304 Z200-READ-CNTL.                                                          
W33304                                                                          
W33304     MOVE '*Z200-READ-CNTL.                  *' TO PV-PARA.               
W33304     INITIALIZE INCNTL-INV-NM-TEXT.                                       
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         SELECT INV_ID                                                    
W33304               ,INV_NM                                                    
W33304          INTO :INCNTL-INV-ID                                             
W33304              ,:INCNTL-INV-NM                                             
W33304          FROM TINCNTL                                                    
W33304         WHERE INV_ID = :IN109-INV-ID                                     
W33304     END-EXEC.                                                            
W33304     MOVE INCNTL-INV-ID      TO HOLD-INV-ID.                              
W33304                                                                          
W33304 Z300-READ-INV-STAT.                                                      
W33304                                                                          
W33304     MOVE '*Z300-READ-INV-STAT               *' TO PV-PARA.               
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         SELECT INV_STAT_CDE                                              
W33304               ,INV_STAT_CDE_DESC                                         
W33304          INTO :INT-STAT-CDE                                              
W33304              ,:INT-STAT-CDE-DESC                                         
W33304          FROM INT_INV_STAT_CDE                                           
W33304         WHERE INV_STAT_CDE = :IN109-FINCL-INV-STAT-CDE                   
W33304     END-EXEC.                                                            
                                                                                
                                                                                
       Z700-WRITE-REPORT-LINE.                                                  
                                                                                
           MOVE REPORT-LINE-CC  TO  PC-REPORT-CC.                               
                                                                                
           WRITE REPORT-LINE.                                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN PC-REPORT-CC   = ' '                                        
                   ADD +1 TO  PC-LINE-COUNT                                     
               WHEN PC-REPORT-CC   = '0'                                        
                   ADD +2 TO  PC-LINE-COUNT                                     
               WHEN PC-REPORT-CC   = '-'                                        
                   ADD +3 TO  PC-LINE-COUNT                                     
           END-EVALUATE.                                                        
                                                                                
       Z800-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
           MOVE '1'           TO REPORT-LINE-CC                                 
           MOVE ' '           TO REPORT-HEADER-FONT-INDICATOR                   
                                                                                
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
W33304     PERFORM Z300-READ-INV-STAT.                                          
W33304     PERFORM Z200-READ-CNTL.                                              
W33304                                                                          
W33304     MOVE IN109-FINCL-INV-STAT-CDE TO H1A-INV-STAT-CDE.                   
W33304                                                                          
W33304     MOVE INT-STAT-CDE-DESC TO H1A-INV-STAT.                              
W33304                                                                          
W33304     MOVE INCNTL-INV-NM-TEXT TO H1A-INV-CYCLE.                            
W33304                                                                          
W33304     MOVE IN109-INV-ID  TO WORK-INV-ID.                                   
W33304                                                                          
W33304     MOVE WORK-INV-ID-R TO H1A-CYCLE-CDE.                                 
W33304     MOVE 'STORE'       TO STORE-DISP.                                    
W33304     IF CYCLE-BK = 'Y'                                                    
W33304        MOVE 'N' TO CYCLE-BK                                              
W33304        MOVE SPACES  TO STORE-DISP                                        
W33304        MOVE SPACES  TO H1A-INV-STAT-CDE                                  
W33304        MOVE SPACES  TO H1A-INV-STAT                                      
W33304        MOVE HOLD-INV-NAME TO H1A-INV-CYCLE                               
W33304        MOVE CYCL-HEAD TO H1A-CYCLE-CDE.                                  
W33304                                                                          
W33304     MOVE WORK-INV-ID-R TO CYCL-HEAD.                                     
W33304     MOVE INCNTL-INV-NM-TEXT TO HOLD-INV-NAME.                            
W33304     MOVE IN109-FINCL-INV-STAT-CDE TO CYCL-STAT-CDE.                      
W33304     MOVE INT-STAT-CDE-DESC TO CYCL-INV-DESC.                             
W33304     IF END-PGM = 'Y'                                                     
W33304        MOVE 99999 TO H1A-CYCLE-CDE                                       
W33304        MOVE 'ALL ACTIVE CYCLES     ' TO H1A-INV-CYCLE                    
W33304        MOVE SPACES TO H1A-INV-STAT-CDE                                   
W33304                       H1A-INV-STAT                                       
W33304                       STORE-DISP.                                        
W33304                                                                          
           MOVE H1-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
W33304     MOVE H1A-HEADING TO REPORT-LINE.                                     
W33304     PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE H2-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE H3-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE +6         TO PC-LINE-COUNT.                                    
                                                                                
           SET  D1-DOUBLE-SPACE          TO TRUE.                               
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
W34517     DISPLAY '**  PROGRAM   **  =  VAKRP102'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
