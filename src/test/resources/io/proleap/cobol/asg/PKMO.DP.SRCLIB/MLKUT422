       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT422.                                         00020000
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  11-17-98.                                         00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      *      MLKUT422 - CREATE DEPT/SUBCLASS TABLES UNLOAD CONTROL CARDS00090000
      ******************************************************************00091100
      *                                                                 00091200
      *  THIS PROGRAM WILL CREATE PLATINUM UNLOAD WHERE CLAUSE DATE     00092000
      *  CONTROL CARDS TO BE USED IN UNLOADING TMNSCLS AND TWKSCLS      00100000
      *  RECORDS FOR SALES AND INVENTORY RANKING REPORTS.  A SEPARATE   00100100
      *  CONTROL CARD FILE WILL BE PRODUCED FOR EACH TABLE UNLOAD.      00111000
      *                                                                 00160000
      ******************************************************************00230000
      *    04/12/99 - PAUL KEBER  WR9999 - PROD PROBLEM                 00230104
      *  SET RETURN CODE OF 4095 AND DON'T CREATE TWKSCLS CONTROL CARD  00231004
      *  IF THE LAST YEAR DAY KEY DATE IS FROM WEEK 53 (9999-09-09).    00231104
      ******************************************************************00232004
      *                                                                 00240000
       ENVIRONMENT DIVISION.                                            00250000
      *                                                                 00260000
       CONFIGURATION SECTION.                                           00270000
      *                                                                 00280000
       SOURCE-COMPUTER.        IBM-3090.                                00290000
       OBJECT-COMPUTER.        IBM-3090.                                00300000
      *                                                                 00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
                                                                        00340000
           SELECT TMNSCLS-DATE-CNTL-FILE                                00350001
               ASSIGN TO OUT01.                                         00360000
                                                                        00370000
           SELECT TWKSCLS-DATE-CNTL-FILE                                00371001
               ASSIGN TO OUT02.                                         00372001
                                                                        00373001
      ******************************************************************00380000
       DATA DIVISION.                                                   00390000
      ******************************************************************00400000
                                                                        00410000
       FILE SECTION.                                                    00420000
                                                                        00430000
       FD  TMNSCLS-DATE-CNTL-FILE                                       00440001
           RECORDING MODE IS F                                          00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           BLOCK CONTAINS 0 RECORDS.                                    00470000
                                                                        00480000
       01  TMNSCLS-DATE-CNTL-RECORD    PIC X(80).                       00490001
                                                                        00491001
       FD  TWKSCLS-DATE-CNTL-FILE                                       00492001
           RECORDING MODE IS F                                          00493001
           LABEL RECORDS ARE STANDARD                                   00494001
           BLOCK CONTAINS 0 RECORDS.                                    00495001
                                                                        00496001
       01  TWKSCLS-DATE-CNTL-RECORD    PIC X(80).                       00497001
      *                                                                 00500000
      ******************************************************************00510000
       WORKING-STORAGE SECTION.                                         00520000
      ******************************************************************00530000
                                                                        00540000
       01  PC-PROGRAM-CONSTANTS.                                        00820004
           05  PC-WEEK53-DATE          PIC X(10)   VALUE '9999-09-09'.  00830004
                                                                        00831004
       01  PV-PROGRAM-VARIABLES.                                        00832004
           05  PV-OPN-FSCL-MN-DTE      PIC X(10)   VALUE SPACES.        00833004
           05  PV-OPN-FSCL-MN-NBR      PIC X(02)   VALUE SPACES.        00840001
           05  PV-OPN-FSCL-WK-DTE      PIC X(10)   VALUE SPACES.        00850001
                                                                        00860000
           05  PV-CRNT-LY-THIS-DAY-KY-DTE                               00870000
                                       PIC X(10)   VALUE SPACES.        00880001
           05  PV-CRNT-FSCL-YR-BEG-DTE PIC X(10)   VALUE SPACES.        00890001
           05  PV-PRIOR-FSCL-YR-BEG-DTE                                 00900000
                                       PIC X(10)   VALUE SPACES.        00910001
           05  PV-OPEN-FSCL-MN-END-DTE-18                               00930000
                                       PIC X(18)   VALUE SPACES.        00940001
           05  FILLER REDEFINES PV-OPEN-FSCL-MN-END-DTE-18.             00950000
               10  PV-OPEN-FSCL-MN-END-DTE-10                           00960000
                                       PIC X(10).                       00970000
               10  PV-OPEN-FSCL-MN-END-DTE-8                            00980000
                                       PIC X(8).                        00990000
                                                                        01000000
       01  PV-TMNSCLS-DATE-CNTL-RECORD.                                 01040001
           05  FILLER                  PIC X(33)   VALUE                01050001
               '  WHERE FSCL_MN_END_DTE BETWEEN '''.                    01200001
           05  PV-TMNSCLS-BEGIN-DATE   PIC X(10)   VALUE SPACES.        01201001
           05  FILLER                  PIC X(07)   VALUE                01202001
               ''' AND '''.                                             01203001
           05  PV-TMNSCLS-END-DATE     PIC X(10)   VALUE SPACES.        01204001
           05  FILLER                  PIC X(20)   VALUE ''';'.         01205001
                                                                        01206001
       01  PV-TWKSCLS-DATE-CNTL-RECORD.                                 01207001
           05  FILLER                  PIC X(33)   VALUE                01208001
               '  WHERE FSCL_WK_END_DTE BETWEEN '''.                    01209003
           05  PV-TWKSCLS-BEGIN-DATE   PIC X(10)   VALUE SPACES.        01209101
           05  FILLER                  PIC X(07)   VALUE                01209201
               ''' AND '''.                                             01209301
           05  PV-TWKSCLS-END-DATE     PIC X(10)   VALUE SPACES.        01209401
           05  FILLER                  PIC X(20)   VALUE ''';'.         01209501
                                                                        01210001
       01 ABEND-AREAS.                                                  01790000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           01800000
                                                   COMP SYNC.           01810000
               88  AC-BAD-DATA                     VALUE +4003.         01820000
               88  AC-DB2-ERROR                    VALUE +4013.         01830000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                01840000
                 '*****       ABEND'.                                   01850000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                01860000
                   '*****   PROGRAM: MLKUT422'.                         01870000
           05  AA-PARAGRAPH-LIT.                                        01880000
               10  FILLER              PIC X(17)   VALUE                01890000
                   '***** PARAGRAPH: '.                                 01900000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        01910000
           05  AA-MESSAGE-LINE.                                         01920000
               10  FILLER              PIC X(06)   VALUE '*****'.       01930000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        01940000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                01950000
                   '*****    DB2 ERROR'.                                01960000
           05  AA-DB2-OPERATION-LIT.                                    01970000
               10  FILLER              PIC X(17)   VALUE                01980000
                   '***** OPERATION: '.                                 01990000
               10  AA-DB2-OPERATION.                                    02000000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        02010000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        02020000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        02030000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        02040000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        02050000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        02060000
                                                                        02070000
       EJECT                                                            02080000
                                                                        02090000
      ******************************************************************02100000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02110000
      ******************************************************************02120000
           COPY DPWS004.                                                02130000
                                                                        02140000
      ******************************************************************02150000
      *  DB2 ERRORS                                                     02160000
      ******************************************************************02170000
           COPY SQLCA2.                                                 02180000
                                                                        02190000
      ******************************************************************02200000
      *  DB2 MLCNTL TABLE LAYOUT                                        02210000
      ******************************************************************02220000
           EXEC SQL                                                     02230000
                INCLUDE TMLCNTL                                         02240000
           END-EXEC.                                                    02250000
                                                                        02260000
      ******************************************************************02270000
      *  DB2 DTATTR TABLE LAYOUT                                        02280000
      ******************************************************************02290000
           EXEC SQL                                                     02300000
                INCLUDE TDTATTR                                         02310000
           END-EXEC.                                                    02320000
                                                                        02330000
      ******************************************************************02620000
      *  DB2 COMMUNICATION AREA                                         02630000
      ******************************************************************02640000
           EXEC SQL                                                     02650000
                INCLUDE SQLCA                                           02660000
           END-EXEC.                                                    02670000
                                                                        03010000
       EJECT                                                            03020000
                                                                        03030000
      ******************************************************************03040000
       PROCEDURE DIVISION.                                              03050000
      ******************************************************************03060000
                                                                        03070000
      ******************************************************************03080000
       0100-MAINLINE.                                                   03090001
      ******************************************************************03100000
      *                                                                 03110000
           PERFORM 7000-SELECT-TMLCNTL.                                 03440000
                                                                        03450000
           DISPLAY ' '.                                                 03460000
           DISPLAY '*** OPEN FISCAL DATES USED ARE:  '                  03470000
           DISPLAY '      MLCNTL-OPN-FSCL-MN-DTE = '                    03480000
             PV-OPN-FSCL-MN-DTE.                                        03490000
           DISPLAY '      MLCNTL-OPN-FSCL-MN-NBR = '                    03500000
             PV-OPN-FSCL-MN-NBR.                                        03510000
           DISPLAY '      MLCNTL-OPN-FSCL-WK-DTE = '                    03520000
             PV-OPN-FSCL-WK-DTE.                                        03530000
                                                                        03540000
           MOVE PV-OPN-FSCL-MN-DTE     TO PV-OPEN-FSCL-MN-END-DTE-10.   03550000
           MOVE ZERO                   TO PV-OPEN-FSCL-MN-END-DTE-8.    03560000
                                                                        03570000
           PERFORM 7100-SELECT-TDTATTR-THIS-WEEK.                       03580000
                                                                        03590000
           DISPLAY ' '.                                                 03591000
           DISPLAY '*** CURRENT FISCAL WEEK DATES USED ARE:  '          03592000
           DISPLAY '      DTATTR-LY-THIS-DAY-KY-DTE = '                 03592100
             DTATTR-LY-THIS-DAY-KY-DTE.                                 03592200
           DISPLAY '      DTATTR-FSCL-YR-BEG-DTE = '                    03592300
             DTATTR-FSCL-YR-BEG-DTE.                                    03592400
                                                                        03593000
           DISPLAY ' '.                                                 03593104
           IF DTATTR-LY-THIS-DAY-KY-DTE = PC-WEEK53-DATE                03594004
               DISPLAY '*** CORRESPONDING LAST YEAR FISCAL WEEK DATE IS 03594104
      -            'FROM WEEK 53 AND CANNOT BE USED FOR RANKING'        03594204
           ELSE                                                         03595004
               PERFORM 7200-SELECT-TDTATTR-LY-SAME-TM                   03600004
               DISPLAY '*** CORRESPONDING LAST YEAR FISCAL WEEK DATE USE03612004
      -            'D:'                                                 03612104
               DISPLAY '      DTATTR-FSCL-YR-BEG-DTE = '                03612204
                     DTATTR-FSCL-YR-BEG-DTE                             03612304
           END-IF.                                                      03612404
                                                                        03630000
           OPEN OUTPUT TMNSCLS-DATE-CNTL-FILE                           03630101
                       TWKSCLS-DATE-CNTL-FILE.                          03630201
                                                                        03630301
           MOVE PV-CRNT-FSCL-YR-BEG-DTE    TO PV-TMNSCLS-BEGIN-DATE.    03630401
           MOVE PV-OPN-FSCL-MN-DTE         TO PV-TMNSCLS-END-DATE.      03630501
                                                                        03630901
           WRITE TMNSCLS-DATE-CNTL-RECORD                               03631001
               FROM  PV-TMNSCLS-DATE-CNTL-RECORD.                       03631101
                                                                        03632000
           IF DTATTR-LY-THIS-DAY-KY-DTE NOT = PC-WEEK53-DATE            03632104
               MOVE PV-PRIOR-FSCL-YR-BEG-DTE TO PV-TWKSCLS-BEGIN-DATE   03632204
               MOVE PV-CRNT-LY-THIS-DAY-KY-DTE TO PV-TWKSCLS-END-DATE   03632304
               WRITE TWKSCLS-DATE-CNTL-RECORD                           03632504
                   FROM PV-TWKSCLS-DATE-CNTL-RECORD                     03632604
           END-IF.                                                      03632704
                                                                        03632801
           CLOSE TMNSCLS-DATE-CNTL-FILE                                 03632901
                 TWKSCLS-DATE-CNTL-FILE.                                03633001
                                                                        03634000
           IF DTATTR-LY-THIS-DAY-KY-DTE = PC-WEEK53-DATE                03634104
              MOVE 4095 TO RETURN-CODE                                  03634304
           ELSE                                                         03634404
              MOVE 0 TO RETURN-CODE                                     03634504
           END-IF.                                                      03634604
                                                                        03634704
           STOP RUN.                                                    03635000
                                                                        03636000
       EJECT                                                            06670000
                                                                        06680000
      ******************************************************************06690000
       7000-SELECT-TMLCNTL.                                             06700000
      ******************************************************************06710000
      *                                                                 06720000
           EXEC SQL                                                     06730000
               SELECT OPN_FSCL_MN_DTE,                                  06740000
                      OPN_FSCL_MN_NBR,                                  06750000
                      OPN_FSCL_WK_DTE                                   06760000
               INTO  :PV-OPN-FSCL-MN-DTE,                               06770000
                     :PV-OPN-FSCL-MN-NBR,                               06780000
                     :PV-OPN-FSCL-WK-DTE                                06790000
               FROM TMLCNTL                                             06800000
           END-EXEC.                                                    06810000
                                                                        06820000
           EVALUATE TRUE                                                06830000
               WHEN SQLCODE = ZERO                                      06840000
                   CONTINUE                                             06850000
               WHEN OTHER                                               06860000
                   MOVE '7000-SELECT-TMLCNTL'                           06870000
                                       TO AA-PARAGRAPH-NAME             06880000
                   MOVE 'TMLCNTL ROW NOT FOUND'                         06890000
                                       TO AA-MESSAGE                    06900000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              06910000
                   PERFORM 9901-SQL-ERROR                               06920000
           END-EVALUATE.                                                06930000
                                                                        06940000
       EJECT                                                            06950000
                                                                        06960000
      ******************************************************************06970000
       7100-SELECT-TDTATTR-THIS-WEEK.                                   06980000
      ******************************************************************06990000
      *                                                                 07000000
           EXEC SQL                                                     07010000
               SELECT LY_THIS_DAY_KY_DTE,                               07020000
                      FSCL_YR_BEG_DTE                                   07030000
               INTO   :DTATTR-LY-THIS-DAY-KY-DTE,                       07040000
                      :DTATTR-FSCL-YR-BEG-DTE                           07050000
               FROM   TDTATTR                                           07060000
               WHERE  (KY_DTE = :PV-OPN-FSCL-WK-DTE)                    07070000
           END-EXEC.                                                    07080000
                                                                        07090000
           EVALUATE TRUE                                                07100000
               WHEN SQLCODE = 0                                         07110000
                   MOVE DTATTR-LY-THIS-DAY-KY-DTE                       07120000
                                       TO PV-CRNT-LY-THIS-DAY-KY-DTE    07130000
                   MOVE DTATTR-FSCL-YR-BEG-DTE                          07140000
                                       TO PV-CRNT-FSCL-YR-BEG-DTE       07150000
               WHEN SQLCODE = +100                                      07160000
                   MOVE '7100-SELECT-TDTATTR-THIS-WEEK'                 07170000
                                       TO AA-PARAGRAPH-NAME             07180000
                   MOVE 'TDTATTR ROW NOT FOUND'                         07190000
                                       TO AA-MESSAGE                    07200000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              07210000
                   PERFORM 9901-SQL-ERROR                               07220000
               WHEN OTHER                                               07230000
                   MOVE '7100-SELECT-TDTATTR-THIS-WEEK'                 07240000
                                       TO AA-PARAGRAPH-NAME             07250000
                   MOVE 'TDTATTR TABLE ERROR'                           07260000
                                       TO AA-MESSAGE                    07270000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              07280000
                   PERFORM 9901-SQL-ERROR                               07290000
           END-EVALUATE.                                                07300000
                                                                        07310000
       EJECT                                                            07320000
                                                                        07330000
      ******************************************************************07340000
       7200-SELECT-TDTATTR-LY-SAME-TM.                                  07350000
      ******************************************************************07360000
      *                                                                 07370000
           EXEC SQL                                                     07380000
               SELECT FSCL_YR_BEG_DTE                                   07390000
               INTO   :DTATTR-FSCL-YR-BEG-DTE                           07400000
               FROM   TDTATTR                                           07410000
               WHERE  (KY_DTE = :PV-CRNT-LY-THIS-DAY-KY-DTE)            07420000
           END-EXEC.                                                    07430000
                                                                        07440000
           EVALUATE TRUE                                                07450000
               WHEN SQLCODE = ZERO                                      07460000
                   MOVE DTATTR-FSCL-YR-BEG-DTE                          07470000
                                       TO PV-PRIOR-FSCL-YR-BEG-DTE      07480000
               WHEN SQLCODE = +100                                      07490000
                   MOVE '7200-SELECT-TDTATTR-LY-SAME-TM'                07500000
                                       TO AA-PARAGRAPH-NAME             07510000
                   MOVE 'TDTATTR ROW NOT FOUND'                         07520000
                                       TO AA-MESSAGE                    07530000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              07540000
                   PERFORM 9901-SQL-ERROR                               07550000
               WHEN OTHER                                               07560000
                   MOVE '7200-SELECT-TDTATTR-LY-SAME-TM'                07570000
                                       TO AA-PARAGRAPH-NAME             07580000
                   MOVE 'TDTATTR TABLE ERROR'                           07590000
                                       TO AA-MESSAGE                    07600000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              07610000
                   PERFORM 9901-SQL-ERROR                               07620000
           END-EVALUATE.                                                07630000
                                                                        07640000
       EJECT                                                            10260000
                                                                        10270000
      ******************************************************************10280000
       9901-SQL-ERROR.                                                  10290000
      ******************************************************************10300000
      *                                                                 10310000
                                                                        10330000
           DISPLAY AA-ABEND-LIT.                                        10340000
           DISPLAY AA-DB2-ERROR-LIT.                                    10350000
           DISPLAY AA-PROGRAM-LIT.                                      10360000
           DISPLAY AA-PARAGRAPH-LIT.                                    10370000
           DISPLAY AA-MESSAGE-LINE.                                     10380000
           DISPLAY AA-DB2-OPERATION-LIT.                                10390000
           SET AC-DB2-ERROR TO TRUE.                                    10400000
           MOVE +4013                  TO ABEND-CODE.                   10410000
                                                                        10420000
           COPY DPPD004.                                                10430000
           CALL 'ILBOABN0' USING ABEND-CODE.                            10440000
                                                                        10450000
