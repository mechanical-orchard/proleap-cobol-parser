      ***************************************************************** 00010099
       IDENTIFICATION DIVISION.                                         00020099
      ***************************************************************** 00030099
                                                                        00040099
       PROGRAM-ID.       SVKUT090.                                      00050099
       AUTHOR.           KELLY HUPPERT.                                 00060099
       INSTALLATION.     KOHLS.                                         00070099
       DATE-WRITTEN      MAY 2010.                                      00080099
       DATE-COMPILED.                                                   00090099
                                                                        00100099
      ***************************************************************** 00110099
      *      * * *>>> THIS PROGRAM MUST BE COMPILED COBOL/390 <<<* * * *00120099
      ***************************************************************** 00130099
      *                                                                 00140099
      *  MODULE TYPE: COBOL2 DB2 BATCH                                  00150099
      *                                                                 00160099
      * THIS PROGRAM WILL CREATE A FILE TO BE SENT TO XPEDEX            00170099
      * WITH GIFT CARD STYLE TOTALS FOR STORE REPLENISHMENT.            00180099
      *                                                                 00190099
      *  THE INPUT TABLES SVT_KOHLS_CRD_ACCT, SVT_KOHLS_CRD_TXN,        00200099
      *  SVT_CRD_STK_DNMNTN AND SVT_CRD_PROD ARE USED TO GATHER         00210099
      *  INFORMATION TO CREATE AN OUTPUT FILE TO SEND TO XPEDX.         00220099
      *                                                                 00230099
      ******************************************************************00231099
      * CHANGE DATE: 05/17/2016                                         00231199
      * CHANGE MADE: RECOMPILE FOR SVWS004 AND SVRD022                  00232099
      * MADE BY    : ACCENTURE                   CHANGE# CHG0141575     00233099
      ******************************************************************00240099
                                                                        00250099
       ENVIRONMENT DIVISION.                                            00260099
       CONFIGURATION SECTION.                                           00270099
                                                                        00280099
       SOURCE-COMPUTER.        IBM-3090.                                00290099
       OBJECT-COMPUTER.        IBM-3090.                                00300099
                                                                        00310099
       INPUT-OUTPUT SECTION.                                            00320099
       FILE-CONTROL.                                                    00330099
                                                                        00340099
           SELECT DATE-CARD-FILE                                        00350099
               ASSIGN TO INP01.                                         00360099
                                                                        00370099
           SELECT SV-XPEDX-FILE                                         00380099
               ASSIGN TO OUT01.                                         00390099
                                                                        00400099
      ******************************************************************00410099
       DATA DIVISION.                                                   00420099
      ******************************************************************00430099
                                                                        00440099
       FILE SECTION.                                                    00450099
                                                                        00460099
       FD  DATE-CARD-FILE                                               00470099
           RECORDING MODE IS F                                          00480099
           BLOCK CONTAINS 0 RECORDS                                     00490099
           LABEL RECORDS ARE STANDARD.                                  00500099
                                                                        00510099
       01  DATE-CARD-RECORD                PIC X(80).                   00520099
                                                                        00530099
       FD  SV-XPEDX-FILE                                                00540099
           RECORDING MODE IS F                                          00550099
           BLOCK CONTAINS 0  RECORDS.                                   00560099
                                                                        00570099
       01  SV-XPEDX-EXTRACT-RECORD         PIC  X(37).                  00580099
                                                                        00590099
      ******************************************************************00600099
       WORKING-STORAGE SECTION.                                         00610099
      ******************************************************************00620099
       01  FILLER                            PIC X(50)  VALUE           00630099
           ' *** WORKING STORAGE STARTS HERE FOR SVKUT090 *** '.        00640099
                                                                        00650099
      ******************************************************************00660099
      *  RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                   00670099
      ******************************************************************00680099
       01  CC-CONTROL-CARD.                                             00690099
           05  CC-DATE-CCYY-MM-DD      PIC X(10)    VALUE SPACES.       00700099
           05  FILLER                  PIC X(70)    VALUE SPACES.       00710099
                                                                        00720099
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      00730099
                                                        COMP SYNC.      00740099
           88  AC-BAD-DATA                              VALUE +4003.    00750099
           88  AC-DB2-ERROR                             VALUE +4013.    00760099
           88  AC-DATE-ERROR                            VALUE +4019.    00770099
                                                                        00790099
      ******************************************************************00800099
      *    PC-PROGRAM CONSTANTS                                        *00810099
      ******************************************************************00820099
       01  PC-CONSTANTS.                                                00830099
           05  PC-PROGRAM-NAME             PIC  X(08)    VALUE          00840099
                                                         'SVKUT090'.    00850099
           05  PC-JOB-NAME             PIC X(08)      VALUE 'SVK0911 '. 00860099
           05  PC-DAYS-TO-INCREMENT    PIC 9(04)      VALUE 1.          00870099
                                                                        00880099
      ******************************************************************00890099
      *    PS-PROGRAM SWITCHES                                         *00900099
      ******************************************************************00910099
       01  PS-PROGRAM-SWITCHES.                                         00920099
           05  PS-END-OF-XPEDX-CARD-SW       PIC X(01)  VALUE 'N'.      00930099
               88  PS-END-OF-XPEDX-CARD                 VALUE 'Y'.      00940099
               88  PS-NOT-END-OF-XPEDX-CARD             VALUE 'N'.      00950099
           05  PS-END-OF-CARD-FILE-SW        PIC  X(01) VALUE 'N'.      00960099
               88  PS-END-OF-CARD-FILE                  VALUE 'Y'.      00970099
           05  PV-DATE-SW                    PIC  X(01) VALUE 'N'.      00980099
               88  VALID-DATE                           VALUE 'Y'.      00990099
               88  NOT-VALID-DATE                       VALUE 'N'.      01000099
                                                                        01010099
      ******************************************************************01020099
      *    PV-PROGRAM VARIABLES                                        *01030099
      ******************************************************************01040099
       01  PV-MISC-FIELDS.                                              01050099
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01060099
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01070099
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01080099
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01090099
                                                                        01100099
           05  PV-CURR-STORE-HOLD          PIC  9(05)    VALUE ZERO.    01110099
           05  FILLER REDEFINES PV-CURR-STORE-HOLD.                     01120099
               10 PV-CURR-STORE-1          PIC  9(01).                  01130099
               10 PV-CURR-STORE-4          PIC  9(04).                  01140099
                                                                        01150099
           05  PV-CURR-UPC-HOLD            PIC  9(12)    VALUE ZERO.    01160099
           05  FILLER REDEFINES PV-CURR-UPC-HOLD.                       01170099
               10 PV-CURR-UPC-1            PIC  9(01).                  01180099
               10 PV-CURR-UPC-11           PIC  9(11).                  01190099
                                                                        01200099
           05  PV-POS-DATE-CCYY-MM-DD  PIC X(10)         VALUE SPACES.  01210099
           05  FILLER REDEFINES PV-POS-DATE-CCYY-MM-DD.                 01220099
               10  PV-POS-CCYY         PIC X(04).                       01230099
               10  FILLER              PIC X(01).                       01240099
               10  PV-POS-MM           PIC X(02).                       01250099
               10  FILLER              PIC X(01).                       01260099
               10  PV-POS-DD           PIC X(02).                       01270099
                                                                        01280099
           05  PV-BEGIN-DATE-TIME          PIC X(26).                   01290099
           05  PV-BEGIN-DATE-TIMESTAMP.                                 01300099
               10  PV-BEGIN-DATE           PIC X(10).                   01310099
               10  FILLER                  PIC X(01)   VALUE '-'.       01320099
               10  PV-BEGIN-TIME           PIC X(15)   VALUE            01330099
                                '00.00.00.000000'.                      01340099
                                                                        01350099
           05  PV-END-DATE-TIME            PIC X(26).                   01360099
           05  PV-END-DATE-TIMESTAMP.                                   01370099
               10  PV-END-DATE             PIC X(10).                   01380099
               10  FILLER                  PIC X(01)   VALUE '-'.       01390099
               10  PV-END-TIME             PIC X(15)   VALUE            01400099
                                '23.59.59.999999'.                      01410099
                                                                        01420099
      ******************************************************************01500099
      *    PA-PROGRAM ACCUMULATORS                                     *01510099
      ******************************************************************01520099
       01  PA-PROGRAM-ACCUMULATORS.                                     01530099
           05  PA-STORE-ROWS-FETCHED         PIC  9(09)  VALUE ZERO.    01540099
           05  PA-TOTAL-ROWS-FETCHED         PIC  9(09)  VALUE ZERO.    01550099
                                                                        01560099
       01  HEADER-LINE.                                                 01570099
           05  FILLER                        PIC  X(37)  VALUE          01580099
               'ETRADINGID,UPCCODE,LOCALE,QTYSOLD,UOM'.                 01590099
                                                                        01600099
      ******************************************************************01610099
      *    XPEDX-EXTRACT-RECORD OUTPUT FILE LAYOUT                     *01620099
      ******************************************************************01630099
           COPY SVRD066.                                                01640099
                                                                        01650099
      ******************************************************************01660099
      *    DATE ROUTINE COPY MEMBERS                                   *01670099
      ******************************************************************01680099
           COPY DPWS500.                                                01690099
                                                                        01700099
      ******************************************************************01710099
      *  TABLES FOR PROCESSING.                                         01720099
      *-----------------------------------------------------------------01730099
                                                                        01740099
      *-----------------------------------------------------------------01750099
      *  DB2 TABLE DB2PDBA.SVT_CRD_PROD                                 01760099
      *                  - KOHLS CARD                                   01770099
      *-----------------------------------------------------------------01780099
           EXEC SQL                                                     01790099
                INCLUDE SVT00000                                        01800099
           END-EXEC.                                                    01810099
                                                                        01820099
      *-----------------------------------------------------------------01830099
      *  DB2 TABLE DB2PDBA.SVT_KOHLS_CRD_ACCT                           01840099
      *                  - KOHLS CARD ACCOUNT                           01850099
      *-----------------------------------------------------------------01860099
           EXEC SQL                                                     01870099
                INCLUDE SVT00001                                        01880099
           END-EXEC.                                                    01890099
                                                                        01900099
      *-----------------------------------------------------------------01910099
      *  DB2 TABLE DB2PDBA.SVT_KOHLS_CRD_TXN                            01920099
      *                  - KOHLS MERCHANDISE CARD ACTIVITY TABLE        01930099
      *-----------------------------------------------------------------01940099
           EXEC SQL                                                     01950099
                INCLUDE SVT00002                                        01960099
           END-EXEC.                                                    01970099
                                                                        01980099
      *-----------------------------------------------------------------01990099
      *  DB2 TABLE DB2PDBA.SVT_CRD_STK_DNMNTN                           02000099
      *                  - KOHLS CARD STOCK DENOMINATION                02010099
      *-----------------------------------------------------------------02020099
           EXEC SQL                                                     02030099
                INCLUDE SVT00008                                        02040099
           END-EXEC.                                                    02050099
                                                                        02060099
      *---------------------------------------------------------------- 02070099
      *    STANDARD LAYOUT FOR THE SQL COMMUNCATIONS AREA               02080099
      *---------------------------------------------------------------- 02090099
           EXEC SQL                                                     02100099
               INCLUDE SQLCA                                            02110099
           END-EXEC.                                                    02120099
                                                                        02130099
           COPY SQLCA2.                                                 02140099
                                                                        02150099
      ******************************************************************02160099
      *  MAIN CURSOR - SVT_KOHLS_CRD_TXN                                02170099
      *              - GIFT CARDS THAT WERE SOLD LAST WEEK              02180099
      ******************************************************************02190099
           EXEC SQL                                                     02200099
             DECLARE XPEDX-CARD-CSR CURSOR FOR                          02210099
                 SELECT DISTINCT                                        02220099
                        KCT.POS_DTE                                     02230099
                       ,KCT.STR_NBR                                     02240099
                       ,CSD.UPC_NBR                                     02250099
                       ,KCT.CRD_NBR                                     02260099
                 FROM   SVT_KOHLS_CRD_ACCT KCA                          02270099
                       ,SVT_KOHLS_CRD_TXN  KCT                          02280099
                       ,SVT_CRD_STK_DNMNTN CSD                          02290099
                       ,SVT_CRD_PROD       CP                           02300099
                 WHERE  KCA.CRD_NBR           = KCT.CRD_NBR             02310099
                 AND    CP.CRD_PROD_YR        = KCA.CRD_PROD_YR         02320099
                 AND    CP.SHIPT_REQ_NBR      = KCA.SHIPT_REQ_NBR       02330099
                 AND    KCA.CRD_TYP_CDE       = CSD.CRD_TYP_CDE         02340099
                 AND    CP.CRD_STK_TYP_ID     = CSD.CRD_STK_TYP_ID      02350099
                 AND    KCT.ACTVY_TYP_CDE     IN (1,2)                  02360099
                 AND    KCA.CRD_SPCTY_TYP_CDE <> 2                      02361099
                 AND    KCT.STR_NBR       NOT IN (90                    02370099
                                                 ,676                   02371099
                                                 ,880                   02372099
                                                 ,918                   02373099
                                                 ,919)                  02374099
                 AND    CSD.UPC_NBR       NOT IN (40089993157           02400099
                                                 ,40089993161           02410099
                                                 ,40090354401           02420099
                                                 ,40090354446)          02430099
               AND   (KCT.TRN_AUTH_TMST BETWEEN :PV-BEGIN-DATE-TIME     02440099
                                            AND :PV-END-DATE-TIME)      02450099
                 ORDER BY KCT.STR_NBR, CSD.UPC_NBR                      02460099
                 WITH UR                                                02470099
           END-EXEC.                                                    02480099
                                                                        02490099
      ***************************************************************** 02500099
      * PROCEDURE DIVISION.                                           * 02510099
      ***************************************************************** 02520099
       PROCEDURE DIVISION.                                              02530099
                                                                        02540099
       0000-MAINLINE-PROCESSING.                                        02550099
                                                                        02560099
           PERFORM 1000-INITIALIZE-PROGRAM.                             02570099
                                                                        02580099
           PERFORM 2000-CREATE-FILE                                     02590099
             UNTIL PS-END-OF-XPEDX-CARD.                                02600099
                                                                        02610099
           PERFORM 3000-WRITE-RECORD.                                   02620099
                                                                        02630099
           PERFORM 4000-EOJ-ROUTINE.                                    02640099
                                                                        02650099
           GOBACK.                                                      02660099
                                                                        02670099
      ******************************************************************02680099
      * 1000-INITIALIZE-PROGRAM                                         02690099
      *                                                                 02700099
      ******************************************************************02710099
       1000-INITIALIZE-PROGRAM.                                         02720099
                                                                        02730099
           OPEN OUTPUT SV-XPEDX-FILE.                                   02740099
                                                                        02750099
           PERFORM 1500-PROCESS-DATES.                                  02760099
                                                                        02770099
           PERFORM 5000-OPEN-XPEDX-CARD-CURSOR.                         02780099
           PERFORM 5100-FETCH-XPEDX-CARD-CURSOR.                        02790099
                                                                        02800099
           MOVE SVT00002-STR-NBR            TO PV-CURR-STORE-HOLD.      02810099
           MOVE SVT00008-UPC-NBR            TO PV-CURR-UPC-HOLD.        02820099
           MOVE 0                           TO PA-STORE-ROWS-FETCHED.   02830099
           MOVE 1                           TO PA-TOTAL-ROWS-FETCHED.   02840099
                                                                        02850099
           WRITE SV-XPEDX-EXTRACT-RECORD                                02860099
               FROM HEADER-LINE.                                        02870099
                                                                        02880099
      ******************************************************************02890099
      * 1500-PROCESS-DATES.                                            *02900099
      *                                                                *02910099
      ******************************************************************02920099
       1500-PROCESS-DATES.                                              02930099
                                                                        02940099
           PERFORM 1510-GET-TODAYS-DTE.                                 02950099
           PERFORM 1520-GET-BEG-DATE.                                   02960099
           PERFORM 1525-GET-END-DATE.                                   02970099
                                                                        02980099
      ******************************************************************02990099
      * 1510-GET-TODAYS-DTE                                             03000099
      * SELECT TODAYS DATE                                              03010099
      ******************************************************************03020099
       1510-GET-TODAYS-DTE.                                             03030099
                                                                        03040099
           OPEN INPUT  DATE-CARD-FILE.                                  03050099
                                                                        03060099
           READ DATE-CARD-FILE INTO CC-CONTROL-CARD                     03070099
              AT END                                                    03080099
                 SET PS-END-OF-CARD-FILE TO TRUE.                       03090099
                                                                        03100099
           IF PS-END-OF-CARD-FILE                                       03110099
               MOVE '1000-INITIALIZE'  TO PV-PARAGRAPH-NAME             03120099
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  03130099
           SET AC-BAD-DATA               TO TRUE                        03140099
           PERFORM 9999-ABEND                                           03150099
           END-IF.                                                      03160099
                                                                        03170099
           MOVE CC-DATE-CCYY-MM-DD       TO PV-POS-DATE-CCYY-MM-DD.     03180099
                                                                        03190099
           CLOSE DATE-CARD-FILE.                                        03200099
                                                                        03300099
      ******************************************************************03310099
      * 1520-GET-BEG-DATE                                               03320099
      *      CALCULATE BEGINING OF THE WEEK DATE (8 DAYS AGO)           03330099
      ******************************************************************03340099
       1520-GET-BEG-DATE.                                               03350099
                                                                        03360099
           MOVE SPACES                    TO PV-DATE-SW.                03370099
           INITIALIZE DPG51,                                            03380099
                      DPG52,                                            03390099
                      DPG53,                                            03400099
                      DPG54,                                            03410099
                      DPG55,                                            03420099
                      DPG56.                                            03430099
           SET DPG51-ACTUAL-CALENDAR-ONLY                               03440099
               DPG51-DECREMENT-DATE                                     03450099
               DPG52-LK-DTE-ISO-FMT       TO TRUE.                      03470099
           MOVE CC-DATE-CCYY-MM-DD        TO DPG52-LK-DATE-INPUT.       03490099
           MOVE 8                         TO DPG51-INCR-DECR-DAYS-9.    03500099
                                                                        03510099
           PERFORM 1530-CALL-DATE-ROUTINE.                              03520099
                                                                        03530099
           IF VALID-DATE                                                03540099
               MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-BEGIN-DATE          03550099
               MOVE PV-BEGIN-DATE-TIMESTAMP   TO PV-BEGIN-DATE-TIME     03560099
               DISPLAY 'BEGIN-DATE-TIME: ' PV-BEGIN-DATE-TIME           03570099
           ELSE                                                         03580099
               DISPLAY 'DATE VALUE: '  DPG55-DB2-ISO-DATE-FMT           03591099
               MOVE '1520-CALC-BEG-END'   TO PV-PARAGRAPH-NAME          03600099
               SET AC-DATE-ERROR          TO TRUE                       03610099
               PERFORM 9999-ABEND                                       03620099
           END-IF.                                                      03630099
                                                                        03640099
      ******************************************************************03650099
      * 1525-GET-END-DATE                                               03660099
      *      CALCULATE END OF THE WEEK DATE (1 DAY AGO)                 03670099
      ******************************************************************03680099
       1525-GET-END-DATE.                                               03690099
                                                                        03700099
           MOVE SPACES                    TO PV-DATE-SW.                03710099
           INITIALIZE DPG51,                                            03720099
                      DPG52,                                            03730099
                      DPG53,                                            03740099
                      DPG54,                                            03750099
                      DPG55,                                            03760099
                      DPG56.                                            03770099
           SET DPG51-ACTUAL-CALENDAR-ONLY                               03780099
               DPG51-DECREMENT-DATE                                     03790099
               DPG52-LK-DTE-ISO-FMT       TO TRUE.                      03810099
           MOVE CC-DATE-CCYY-MM-DD        TO DPG52-LK-DATE-INPUT.       03830099
           MOVE 1                         TO DPG51-INCR-DECR-DAYS-9.    03840099
                                                                        03850099
           PERFORM 1530-CALL-DATE-ROUTINE.                              03860099
                                                                        03870099
           IF VALID-DATE                                                03880099
               MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-END-DATE            03890099
               MOVE PV-END-DATE-TIMESTAMP     TO PV-END-DATE-TIME       03900099
               DISPLAY ' END-DATE-TIME: ' PV-END-DATE-TIME              03910099
           ELSE                                                         03920099
               DISPLAY 'DATE VALUE: '  DPG55-DB2-ISO-DATE-FMT           03930099
               MOVE '1525-CALC-BEG-END'   TO PV-PARAGRAPH-NAME          03940099
               SET AC-DATE-ERROR          TO TRUE                       03950099
               PERFORM 9999-ABEND                                       03960099
           END-IF.                                                      03970099
                                                                        03980099
      ******************************************************************03990099
      * 1530-CALL-DATE-ROUTINE                                          04000099
      *                                                                 04010099
      ******************************************************************04020099
       1530-CALL-DATE-ROUTINE.                                          04030099
                                                                        04040099
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            04050099
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               04060099
           IF (DPG54-SEVERE-ERROR OR                                    04070099
               DPG54-WARNING-ERROR)                                     04080099
               SET NOT-VALID-DATE TO TRUE                               04090099
           ELSE                                                         04100099
               SET VALID-DATE TO TRUE                                   04110099
           END-IF.                                                      04120099
                                                                        04130099
      ******************************************************************04140099
      * 2000-CREATE-FILE                                                04150099
      ******************************************************************04160099
       2000-CREATE-FILE.                                                04170099
                                                                        04180099
           IF (SVT00002-STR-NBR NOT EQUAL PV-CURR-STORE-HOLD)           04190099
            OR (SVT00008-UPC-NBR NOT EQUAL PV-CURR-UPC-HOLD)            04200099
              PERFORM 3000-WRITE-RECORD                                 04210099
           ELSE                                                         04220099
              PERFORM 5100-FETCH-XPEDX-CARD-CURSOR                      04230099
           END-IF.                                                      04240099
                                                                        04250099
      ******************************************************************04260099
      * 3000-WRITE-RECORD                                               04270099
      * WRITE RECORDS FOR GIFT CARD STYLE REPLENISHMENT                 04280099
      ******************************************************************04290099
       3000-WRITE-RECORD.                                               04300099
                                                                        04310099
           INITIALIZE SV-XPEDX-EXTRACT-RECORD.                          04320099
                                                                        04330099
           MOVE "KOHLS"                    TO SV066-KOHLS.              04340099
           MOVE PV-CURR-STORE-4            TO SV066-STR-NBR.            04350099
           MOVE ','                        TO SV066-COMMA1.             04360099
           MOVE PV-CURR-UPC-11             TO SV066-UPC-NBR.            04370099
           MOVE ','                        TO SV066-COMMA2.             04380099
           MOVE 'EN_US'                    TO SV066-LOCALE.             04390099
           MOVE ','                        TO SV066-COMMA3.             04400099
           MOVE PA-STORE-ROWS-FETCHED      TO SV066-STORE-TOTAL         04410099
           MOVE ','                        TO SV066-COMMA4.             04420099
           MOVE "EA"                       TO SV066-UNITOFMEASURE.      04430099
                                                                        04440099
           WRITE SV-XPEDX-EXTRACT-RECORD                                04450099
               FROM SV066-XPDEX-EXTRACT-RECORD.                         04460099
                                                                        04470099
      *  RESET FIELDS FOR NEXT STORE.                                   04480099
           MOVE SVT00002-STR-NBR           TO PV-CURR-STORE-HOLD.       04490099
           MOVE SVT00008-UPC-NBR           TO PV-CURR-UPC-HOLD.         04500099
           MOVE 0                          TO PA-STORE-ROWS-FETCHED.    04510099
                                                                        04520099
      ******************************************************************04530099
      * 4000-EOJ-ROUTINE                                                04540099
      ******************************************************************04550099
       4000-EOJ-ROUTINE.                                                04560099
                                                                        04570099
           PERFORM 5200-CLOSE-XPEDX-CARD-CURSOR.                        04580099
                                                                        04590099
           CLOSE   SV-XPEDX-FILE.                                       04600099
                                                                        04610099
           PERFORM 9990-DISPLAY-TOTALS.                                 04620099
                                                                        04630099
      ******************************************************************04640099
      * 5000-OPEN-XPEDX-CARD-CURSOR                                     04650099
      ******************************************************************04660099
       5000-OPEN-XPEDX-CARD-CURSOR.                                     04670099
           EXEC SQL                                                     04680099
                OPEN XPEDX-CARD-CSR                                     04690099
           END-EXEC.                                                    04700099
                                                                        04710099
           EVALUATE TRUE                                                04720099
               WHEN SQLCODE = ZERO                                      04730099
                    CONTINUE                                            04740099
               WHEN SQLWARN0 NOT EQUAL SPACE                            04750099
               WHEN SQLCODE NOT EQUAL ZERO                              04760099
                   MOVE '5000-OPEN-XPEDX-CARD-CURSOR '                  04770099
                                            TO PV-PARAGRAPH-NAME        04780099
                   MOVE 'ERROR ON OPEN OF XPEDX-CARD-CSR '              04790099
                                            TO PV-DB2-OPERATION         04800099
                   PERFORM 9100-SQL-ABEND                               04810099
           END-EVALUATE.                                                04820099
                                                                        04830099
      ******************************************************************04840099
      * 5100-FETCH-XPEDX-CARD-CURSOR                                    04850099
      ******************************************************************04860099
       5100-FETCH-XPEDX-CARD-CURSOR.                                    04870099
           EXEC SQL                                                     04880099
                FETCH XPEDX-CARD-CSR                                    04890099
                 INTO :SVT00002-POS-DTE                                 04900099
                     ,:SVT00002-STR-NBR                                 04910099
                     ,:SVT00008-UPC-NBR                                 04920099
                     ,:SVT00002-CRD-NBR                                 04930099
           END-EXEC.                                                    04940099
                                                                        04950099
           EVALUATE TRUE                                                04960099
               WHEN SQLCODE = ZERO                                      04970099
                   ADD +1                   TO PA-STORE-ROWS-FETCHED    04980099
                   ADD +1                   TO PA-TOTAL-ROWS-FETCHED    04990099
               WHEN SQLCODE = +100                                      05000099
                   SET PS-END-OF-XPEDX-CARD TO TRUE                     05010099
                   ADD +1                   TO PA-STORE-ROWS-FETCHED    05020099
               WHEN SQLWARN0 NOT EQUAL SPACE                            05030099
               WHEN SQLCODE NOT EQUAL ZERO                              05040099
                   MOVE '5100-FETCH-XPEDX-CARD-CURSOR'                  05050099
                                           TO PV-PARAGRAPH-NAME         05060099
                   MOVE 'ERROR ON FETCH OF XPEDX-CARD-CSR'              05070099
                                           TO PV-DB2-OPERATION          05080099
                   PERFORM 9100-SQL-ABEND                               05090099
           END-EVALUATE.                                                05100099
                                                                        05110099
      ******************************************************************05120099
      * 5200-CLOSE-XPEDX-CARD-CURSOR                                    05130099
      ******************************************************************05140099
       5200-CLOSE-XPEDX-CARD-CURSOR.                                    05150099
                                                                        05160099
           EXEC SQL                                                     05170099
                CLOSE XPEDX-CARD-CSR                                    05180099
           END-EXEC.                                                    05190099
                                                                        05200099
           EVALUATE TRUE                                                05210099
               WHEN SQLCODE = ZERO                                      05220099
                    CONTINUE                                            05230099
               WHEN SQLWARN0 NOT EQUAL SPACE                            05240099
               WHEN SQLCODE NOT EQUAL ZERO                              05250099
                   MOVE '5200-CLOSE-XPEDX-CARD-CURSOR'                  05260099
                                           TO PV-PARAGRAPH-NAME         05270099
                   MOVE 'ERROR ON CLOSE OF XPEDX-CARD-CSR'              05280099
                                           TO PV-DB2-OPERATION          05290099
                   PERFORM 9100-SQL-ABEND                               05300099
           END-EVALUATE.                                                05310099
                                                                        05320099
      ******************************************************************05330099
      * 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      05340099
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                 05350099
      ******************************************************************05360099
       9100-SQL-ABEND.                                                  05370099
           PERFORM 9990-DISPLAY-TOTALS.                                 05380099
           DISPLAY '** ABEND     **'.                                   05390099
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                05400099
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05410099
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05420099
                                                                        05430099
           SET AC-DB2-ERROR TO TRUE.                                    05440099
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         05450099
                                                                        05460099
      ******************************************************************05470099
      * 9990-DISPLAY-TOTALS.                                            05480099
      *  DISPLAY TOTALS AT END OF JOB.                                  05490099
      ******************************************************************05500099
       9990-DISPLAY-TOTALS.                                             05510099
                                                                        05520099
           DISPLAY '*************************************'.             05530099
           DISPLAY 'TOTAL XPEDX CARDS READ ===> ' PA-TOTAL-ROWS-FETCHED.05540099
           DISPLAY '*************************************'.             05550099
                                                                        05560099
      ******************************************************************05570099
      * 9999-ABEND - PERFORMS A NON-SQL-ABEND                           05580099
      ******************************************************************05590099
       9999-ABEND.                                                      05600099
           DISPLAY '** ABEND           **'.                             05610099
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05620099
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05630099
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       05640099
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       05650099
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         05660099
