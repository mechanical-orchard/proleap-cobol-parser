       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKCS029.                                                 
       AUTHOR.        M. FURLICK.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  05-14-92.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    Z029 - MNEMONIC CRITERIA SPEC (DPSMNEM)                     *        
      *                                                                *        
      *    THERE ARE 4 DATA ENTERABLE FIELDS ON THIS SELECTION SCREEN: *        
      *          SCREEN TYPE (LIST, DETAIL, MENU) AND MNEMONIC.        *        
      *                                                                *        
      *    ANY COMBINATION OF THESE FIELDS MAY BE ENTERED.  CONTROL    *        
      *    IS PASSED TO THE MNEMONIC ADD, INQUIRY, UPDATE, OR DELETE   *        
      *    SCREENS, OR THE MENU ITEM LIST OR MNEMONIC LIST SCREEN.     *        
      *                                                                *        
      *    03/08/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED MAP (DPKM029) AND PGM TO     *        
      *                          ACCEPT UPD-VSAM-IND AS CRITERIA TO    *        
      *                          BUILD THE LIST.                       *        
      *                       2) MODIFIED MAP (DPKM029) AND PGM TO     *        
      *                          ACCEPT PROGRAM-NAME AS CRITERIA TO    *        
      *                          BUILD THE LIST.                       *        
      *                       WITH THESE CHANGES, THERE ARE NOW        *        
      *                       6 DATA ENTERABLE FIELDS ON THIS SCREEN.  *        
      *    09/09/99  TKMA464  ADD ACCURATE LOCATION REQUIRED INDICATOR.*        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.        
           05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072          
                                                           COMP SYNC.           
           05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE                
           'DP029A  '.                                                          
           05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE                
           'DPKM029 '.                                                          
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE                
           'DPKCS029'.                                                          
           05  PC-MAX-RETRIES                 PIC 9(02)    VALUE  3.            
           05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128           
                                                           COMP SYNC.           
           05  FILLER REDEFINES PC-ERASE-EOF-COMP.                              
               10  FILLER                     PIC X(01).                        
               10  PC-ERASE-EOF               PIC X(01).                        
           05  PC-COMPANY                     PIC X(02)    VALUE '03'.          
           05  PC-S                           PIC X(01)    VALUE 'S'.           
           05  PC-VSAM-UPD-IND-VALUES.                                          
               10  PC-Y-UPDATE-REQUESTED      PIC X(01)    VALUE 'Y'.           
               10  PC-N-NO-UPDATE-NEEDED      PIC X(01)    VALUE 'N'.           
               10  PC-D-DELETE-REQUESTED      PIC X(01)    VALUE 'D'.           
               10  PC-E-ERROR-OCCURRED        PIC X(01)    VALUE 'E'.           
           05  PC-DB2-PKG-CDE-VALUES.                                           
               10  PC-P-PROD-TABLES           PIC X(01)    VALUE 'P'.           
               10  PC-A-ARCHIVE-TABLES        PIC X(01)    VALUE 'A'.           
           05  PC-ACCURATE-LOCATION-REQ.                                        
               10  PC-Y-ACRT-LOC-REQR         PIC X(01)    VALUE 'Y'.           
               10  PC-N-ACRT-LOC-REQR         PIC X(01)    VALUE 'N'.           
           05  PC-ASTERISK                    PIC X(01)    VALUE '*'.           
           05  PC-TSYMSG-NUMBERS.                                               
               10 PC-TSYMSG-00005             PIC 9(05)    VALUE 00005.         
               10 PC-TSYMSG-00007             PIC 9(05)    VALUE 00007.         
               10 PC-TSYMSG-00036             PIC 9(05)    VALUE 00036.         
               10 PC-TSYMSG-00045             PIC 9(05)    VALUE 00045.         
               10 PC-TSYMSG-00059             PIC 9(05)    VALUE 00059.         
               10 PC-TSYMSG-00115             PIC 9(05)    VALUE 00115.         
               10 PC-TSYMSG-00134             PIC 9(05)    VALUE 00134.         
               10 PC-TSYMSG-00137             PIC 9(05)    VALUE 00137.         
               10 PC-TSYMSG-00146             PIC 9(05)    VALUE 00146.         
               10 PC-TSYMSG-00148             PIC 9(05)    VALUE 00148.         
               10 PC-TSYMSG-00254             PIC 9(05)    VALUE 00254.         
               10 PC-TSYMSG-01202             PIC 9(05)    VALUE 01202.         
                                                                                
       01  PM-PROGRAM-MESSAGES.                                                 
           05  PM-PROGRAM-MESSAGE-1           PIC X(78)    VALUE                
           'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.           
               88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.           
               88  PS-SEND-DATA-ONLY                       VALUE '4'.           
               88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.           
               88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.           
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
           05  PS-PGM-NAME-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-PGM-NAME-ERROR                       VALUE 'Y'.           
               88  PS-NO-PGM-NAME-ERROR                    VALUE 'N'.           
           05  PS-TRAN-SW                     PIC X(01)    VALUE 'N'.           
               88  PS-TRAN-ERROR                           VALUE 'Y'.           
               88  PS-NO-TRAN-ERROR                        VALUE 'N'.           
           05  PS-MNEMONIC-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-MNEMONIC-ERROR                       VALUE 'Y'.           
               88  PS-NO-MNEMONIC-ERROR                    VALUE 'N'.           
           05  PS-ASTERISK-FOUND-SW           PIC X(01)    VALUE 'N'.           
               88  PS-ASTERISK-FOUND                       VALUE 'Y'.           
               88  PS-NO-ASTERISK-FOUND                    VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-NAV-MNEMONIC                PIC X(08)    VALUE SPACES.        
           05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.        
           05  PV-RETRY-COUNTER               PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SQL-SUB                     PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SUB                         PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-DB2-COUNT                   PIC S9(9)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-WORK-MNEMONIC               PIC X(8).                         
           05  PV-WM-BYTE  REDEFINES  PV-WORK-MNEMONIC                          
                           OCCURS 8           PIC X(1).                         
           05  PV-WORK-PROGRAM                PIC X(8).                         
           05  PV-WP-BYTE  REDEFINES  PV-WORK-PROGRAM                           
                           OCCURS 8           PIC X(1).                         
           05  PV-WORK-TRAN                   PIC X(4).                         
           05  PV-WT-BYTE  REDEFINES  PV-WORK-TRAN                              
                           OCCURS 4           PIC X(1).                         
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *        
      *----------------------------------------------------------------*        
           COPY DPWS006.                                                        
       EJECT                                                                    
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
                                                                                
           COPY DPWS055.                                                        
                                                                                
      *****************************************************************         
      *    APPLICATION SPECIFIC COMMAREA                                        
      *****************************************************************         
                                                                                
               10  DP029-SPECIFIC-COMMAREA.                                     
                   15  FILLER                      PIC X(02).                   
                   15  DP029-REQUEST-DATA.                                      
                                                                                
                       20  DP029-LIST-CRITERION    PIC X(01).                   
                                                                                
                       20  DP029-DETAIL-CRITERION  PIC X(01).                   
                                                                                
                       20  DP029-MENU-CRITERION    PIC X(01).                   
                                                                                
                       20  DP029-MNEMONIC          PIC X(08).                   
                                                                                
                       20  DP029-PROG              PIC X(08).                   
                                                                                
                       20  DP029-UPD-VSAM-IND      PIC X(01).                   
                                                                                
                       20  DP029-DB2-PKG-CDE       PIC X(01).                   
                                                                                
                       20  DP029-ACCURATE-LOC-REQ  PIC X(01).                   
                                                                                
                       20  DP029-TRAN              PIC X(04).                   
                                                                                
                   15  DP029-SAVE-INTER-APPL-COMM  PIC X(200).                  
                   15  DP029-RESTORE-SW            PIC X(01).                   
                       88  DP029-RESTORE-COMMAREA             VALUE 'Y'.        
                       88  DP029-DONT-RESTORE-COMMAREA        VALUE 'N'.        
                   15  FILLER                      PIC X(2636).                 
      *****************************************************************         
      *    ABEND PROCESSING COPYBOOK.                                           
      *****************************************************************         
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *****************************************************************         
      *    NUMERIC FIELD EDIT ROUTINE WORKING STORAGE                           
      *****************************************************************         
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *****************************************************************         
      *    ATTRIBUTE SETTINGS COPYBOOK.                                         
      *****************************************************************         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *****************************************************************         
      *    CURRENT DATE AND TIME COPYBOOK.                                      
      *****************************************************************         
                                                                                
           COPY DPWS017.                                                        
                                                                                
      *****************************************************************         
      *    TOGGLE FUNCTION KEY LINES COPYBOOK.                                  
      *****************************************************************         
                                                                                
           COPY DPWS009.                                                        
                                                                                
      *****************************************************************         
      *    FUNCTION KEYS COPYBOOK.                                              
      *****************************************************************         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *****************************************************************         
      *    DPKUT045 COPYBOOK.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS045.                                                        
                                                                                
           COPY DPRD507I.                                                       
       EJECT                                                                    
                                                                                
      *****************************************************************         
      *    STANDARD ATTENTION IDENTIFIER LIST                                   
      *****************************************************************         
                                                                                
           COPY DFHAID.                                                         
                                                                                
      *****************************************************************         
      *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST                
      *****************************************************************         
                                                                                
           COPY DFHBMSCA.                                                       
                                                                                
      *****************************************************************         
      *    MAP LAYOUT                                                           
      *****************************************************************         
                                                                                
           COPY DPKM029.                                                        
                                                                                
       01  MD-MAP-DATA-AREA REDEFINES DP029AO.                                  
           05  FILLER                         PIC X(700).                       
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.          *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *----------------------------------------------------------------*        
      *    DP020-SRC-RETURN-W-TRAN-ID WILL BE TRUE IF THIS PROGRAM WAS *        
      *    SENT A STANDARD COMMAREA. IF NOT SET TRUE (MEANING OTHER    *        
      *    THAN A STANDARD COMMAREA WAS SENT TO THIS PROGRAM, THIS     *        
      *    PROGRAM WILL RETURN TO CISS.                                *        
      *----------------------------------------------------------------*        
           IF DP020-SRC-RETURN-W-TRAN-ID                                        
               PERFORM 1000-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
       EJECT                                                                    
       1000-CONTROL-PROCESSING.                                                 
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *                                                                *        
      *    PERFORMED BY 0000-MAIN-MODULE.                              *        
      *----------------------------------------------------------------*        
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   INITIALIZE DP029-SPECIFIC-COMMAREA                           
                   MOVE INTER-APPL-COMM-AREA                                    
                                     TO  DP029-SAVE-INTER-APPL-COMM             
                   PERFORM 2000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 3000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   IF  DP029-RESTORE-COMMAREA                                   
                       MOVE DP029-SAVE-INTER-APPL-COMM                          
                                       TO  INTER-APPL-COMM-AREA                 
                   END-IF                                                       
                   IF  DP055-UPDATED                                            
                       MOVE PC-TSYMSG-00059                                     
                                       TO  DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL                              
                                       TO  TRUE                                 
                   END-IF                                                       
                   IF  DP055-NOT-FOUND                                          
                       MOVE PC-TSYMSG-00148                                     
                                       TO  DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL                              
                                       TO  TRUE                                 
                   END-IF                                                       
                   IF  DP055-DELETED                                            
                       MOVE PC-TSYMSG-00115                                     
                                       TO  DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL                              
                                       TO  TRUE                                 
                   END-IF                                                       
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                           
      *            IF  DP055-ERROR                                              
      *                MOVE PC-TSYMSG-00254                                     
      *                                TO  DP020-MSG-NUMBER                     
      *                SET DP020-MSG-FATAL                                      
      *                                TO  TRUE                                 
      *            END-IF                                                       
                   MOVE SPACES         TO  DP055-ACTION                         
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
               WHEN DP020-NEXT-ACT-APPL-ERROR                                   
                   MOVE DP020-MSG-MESSAGE       TO PV-MESSAGE                   
                                                                                
                   IF DP020-NAV-MNEMONIC = SPACES                               
                       MOVE SPACES              TO ANAVCMDO                     
                       MOVE DP015-GREEN         TO ANAVCMDC                     
                       MOVE DP015-UNDERLINE     TO ANAVCMDH                     
                   ELSE                                                         
                       MOVE DP015-RED           TO ANAVCMDC                     
                       MOVE DP015-REVERSE       TO ANAVCMDH                     
                   END-IF                                                       
                                                                                
                   SET PS-ERROR                 TO TRUE                         
                   SET PS-SEND-DATA-ONLY-FRSET  TO TRUE                         
                                                                                
                   MOVE PV-MESSAGE              TO DP020-MSG-MESSAGE            
                   MOVE -1                      TO ANAVCMDL                     
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND        TO TRUE                         
                   SET DP013-NO-ROLLBACK        TO TRUE                         
                                                                                
                   MOVE 'B100-CONTROL-PROCESSING' TO                            
                        DP013-PARAGRAPH                                         
                                                                                
                   MOVE PM-PROGRAM-MESSAGE-1      TO                            
                        DP013-MESSAGE-TEXT(1)                                   
                                                                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
       2000-BUILD-INITIAL-PANEL.                                                
                                                                                
      *----------------------------------------------------------------*        
      *    PERFORMED ON THE FIRST PASS OF THIS PROGRAM.  THERE IS NO   *        
      *    DATA NEEDED TO BUILD THIS PANEL, THEREFORE, JUST DISPLAY IT.*        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE LOW-VALUES           TO  DP029-REQUEST-DATA.                    
           PERFORM 2200-DISPLAY-PANEL.                                          
                                                                                
           EJECT                                                                
       2200-DISPLAY-PANEL.                                                      
                                                                                
      *----------------------------------------------------------------*        
      *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.      *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY 3000-PROCESS-PANEL.                            *        
      *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                        *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
           MOVE DP017-EDITED-TIME-HMS  TO  ATIMEO.                              
                                                                                
           IF  PS-SEND-MAP-N-DATA-FRSET                                         
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
               PERFORM 3400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           IF (DP020-SRC-AID = DP016-PA2) OR                                    
              (PS-SEND-MAP-N-DATA-FRSET)                                        
               PERFORM DP009-0000-LOAD-FK-LINES                                 
           END-IF.                                                              
                                                                                
           IF  PS-NO-ERROR                                                      
               MOVE -1                 TO  AMNEML                               
           END-IF.                                                              
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
                                                                                
       EJECT                                                                    
       3000-PROCESS-PANEL.                                                      
                                                                                
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP, CONDITIONALLY SAVE THE SCREEN DATA, THEN   *        
      *    DETERMINE THE PROCESSING PATH.                              *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
           SET PS-SEND-DATA-ONLY-FRSET TO TRUE.                                 
                                                                                
           PERFORM DP007-0000-GET-PANEL.                                        
                                                                                
           IF (DP020-SRC-AID NOT = DP016-PF5)  AND                              
              (DP020-SRC-AID NOT = DP016-CLEAR)                                 
               PERFORM 3200-MOVE-SCREEN-TO-COMMAREA                             
           END-IF.                                                              
                                                                                
           PERFORM 3100-CHECK-LOCAL-FKEYS.                                      
       EJECT                                                                    
                                                                                
       3100-CHECK-LOCAL-FKEYS.                                                  
                                                                                
      *----------------------------------------------------------------*        
      *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION   *        
      *    KEY THE USER HIT.                                           *        
      *                                                                *        
      *    PERFORMED BY 3000-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID) OR                       
                    DP020-FK-RETURN(DP020-SRC-AID)                              
                    SET DP029-DONT-RESTORE-COMMAREA                             
                                     TO  TRUE                                   
                    PERFORM 4000-NAVIGATE                                       
                                                                                
               WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
                    PERFORM 3300-EDIT-DATA-IN-COMMAREA                          
                    IF  PS-NO-ERROR                                             
                        IF (PS-ASTERISK-FOUND                                   
                        AND (DP020-SRC-AID NOT EQUAL DP016-PF17                 
                        AND  DP020-SRC-AID NOT EQUAL DP016-PF13))               
                            MOVE PC-TSYMSG-00137                                
                                     TO  DP020-MSG-NUMBER                       
                            SET DP020-MSG-FATAL                                 
                                     TO  TRUE                                   
                            MOVE -1  TO  AMNEML                                 
                            MOVE DP015-RED                                      
                                     TO  AMNEMC                                 
                            MOVE DP015-REVERSE                                  
                                     TO  AMNEMH                                 
                            SET PS-ERROR                                        
                                     TO  TRUE                                   
                            PERFORM 2200-DISPLAY-PANEL                          
                        ELSE                                                    
                            SET DP029-DONT-RESTORE-COMMAREA                     
                                     TO  TRUE                                   
                            PERFORM 4000-NAVIGATE                               
                        END-IF                                                  
                    ELSE                                                        
                        PERFORM 2200-DISPLAY-PANEL                              
                    END-IF                                                      
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                    MOVE SPACES       TO  ANAVCMDO                              
                                         ANAVKEYO                               
                    SET PS-SEND-MAP-N-DATA-FRSET                                
                                       TO  TRUE                                 
                    INITIALIZE DP029-REQUEST-DATA                               
                    PERFORM 2000-BUILD-INITIAL-PANEL                            
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                    IF  DP020-NAV-MNEMONIC GREATER THAN SPACES                  
                        SET DP029-RESTORE-COMMAREA                              
                                     TO  TRUE                                   
                        PERFORM 4000-NAVIGATE                                   
                    ELSE                                                        
                        PERFORM 3300-EDIT-DATA-IN-COMMAREA                      
                        PERFORM 2200-DISPLAY-PANEL                              
                    END-IF                                                      
                                                                                
               WHEN DP020-SRC-AID = DP016-PA2                                   
                    SET PS-SEND-DATA-ONLY TO TRUE                               
                    MOVE -1               TO ANAVCMDL                           
                    PERFORM DP017-0000-GET-CURR-DATE-TIME                       
                    PERFORM DP009-0000-LOAD-FK-LINES                            
                    PERFORM DP008-0000-WRITE-PANEL                              
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                    SET PS-SEND-MAP-N-DATA-FRSET                                
                                       TO  TRUE                                 
                    PERFORM 3300-EDIT-DATA-IN-COMMAREA                          
                    PERFORM 2200-DISPLAY-PANEL                                  
                                                                                
               WHEN OTHER                                                       
                   PERFORM 3300-EDIT-DATA-IN-COMMAREA                           
                   MOVE PC-TSYMSG-00045                                         
                                       TO  DP020-MSG-NUMBER                     
                   SET DP020-MSG-FATAL TO  TRUE                                 
                   MOVE -1             TO  ANAVCMDL                             
                   PERFORM 2200-DISPLAY-PANEL                                   
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
                                                                                
       3200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
      *----------------------------------------------------------------*        
      *    MOVE THE FIELDS FROM THE MAP TO THE COMMAREA IF ANYTHING    *        
      *    WAS ENTERED IN THAT FIELD.                                  *        
      *                                                                *        
      *    PERFORMED BY 3000-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
           IF (AMNEML GREATER THAN ZERO                                         
           OR  AMNEMA EQUAL PC-ERASE-EOF)                                       
              MOVE AMNEMI              TO DP029-MNEMONIC                        
           END-IF.                                                              
                                                                                
           IF (APROGL GREATER THAN ZERO                                         
           OR  APROGA EQUAL PC-ERASE-EOF)                                       
               MOVE APROGI             TO DP029-PROG                            
           END-IF.                                                              
                                                                                
           IF (ATRANL GREATER THAN ZERO                                         
           OR  ATRANA EQUAL PC-ERASE-EOF)                                       
               MOVE ATRANI             TO DP029-TRAN                            
           END-IF.                                                              
                                                                                
           IF (AUVINDL GREATER THAN ZERO                                        
           OR  AUVINDA EQUAL PC-ERASE-EOF)                                      
               MOVE AUVINDI            TO DP029-UPD-VSAM-IND                    
           END-IF.                                                              
                                                                                
           IF (ALISTL GREATER THAN ZERO                                         
           OR  ALISTA EQUAL PC-ERASE-EOF)                                       
               MOVE ALISTI             TO DP029-LIST-CRITERION                  
           END-IF.                                                              
                                                                                
           IF (ADETAILL GREATER THAN ZERO                                       
           OR  ADETAILA EQUAL PC-ERASE-EOF)                                     
              MOVE ADETAILI            TO DP029-DETAIL-CRITERION                
           END-IF.                                                              
                                                                                
           IF (AMENUL GREATER THAN ZERO                                         
           OR  AMENUA EQUAL PC-ERASE-EOF)                                       
              MOVE AMENUI              TO DP029-MENU-CRITERION                  
           END-IF.                                                              
                                                                                
           IF (APKGL GREATER THAN ZERO                                          
           OR  APKGA EQUAL PC-ERASE-EOF)                                        
              MOVE APKGI               TO DP029-DB2-PKG-CDE                     
           END-IF.                                                              
                                                                                
           IF (ALOCL GREATER THAN ZERO                                          
           OR  ALOCA EQUAL PC-ERASE-EOF)                                        
              MOVE ALOCI               TO DP029-ACCURATE-LOC-REQ                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
       3300-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
      *----------------------------------------------------------------*        
      *    VALIDATE THE SCREEN FIELDS.                                 *        
      *                                                                *        
      *    PERFORMED BY 1000-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY 3000-PROCESS-PANEL.                            *        
      *    PERFORMED BY 3100-CHECK-LOCAL-FKEYS.                        *        
      *----------------------------------------------------------------*        
                                                                                
           SET PS-NO-ERROR             TO  TRUE.                                
                                                                                
           IF (DP020-NAV-MNEMONIC NOT GREATER THAN SPACES                       
           AND DP020-NAV-KEY-DATA GREATER THAN SPACES)                          
               MOVE DP015-RED          TO  ANAVKEYC                             
               MOVE DP015-REVERSE      TO  ANAVKEYH                             
               MOVE -1                 TO  ANAVKEYL                             
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           ELSE                                                                 
               MOVE DP015-GREEN        TO  ANAVKEYC                             
               MOVE DP015-UNDERLINE    TO  ANAVKEYH                             
           END-IF.                                                              
                                                                                
           IF (DP029-ACCURATE-LOC-REQ EQUAL PC-Y-ACRT-LOC-REQR                  
           OR  DP029-ACCURATE-LOC-REQ EQUAL PC-N-ACRT-LOC-REQR                  
           OR  DP029-ACCURATE-LOC-REQ NOT GREATER THAN SPACES)                  
               MOVE DP015-GREEN        TO  ALOCC                                
               MOVE DP015-UNDERLINE    TO  ALOCH                                
           ELSE                                                                 
               MOVE DP015-RED          TO  ALOCC                                
               MOVE DP015-REVERSE      TO  ALOCH                                
               MOVE -1                 TO  ALOCL                                
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF (DP029-DB2-PKG-CDE    EQUAL PC-P-PROD-TABLES                      
           OR  DP029-DB2-PKG-CDE    EQUAL PC-A-ARCHIVE-TABLES                   
           OR  DP029-DB2-PKG-CDE    NOT GREATER THAN SPACES)                    
               MOVE DP015-GREEN        TO  APKGC                                
               MOVE DP015-UNDERLINE    TO  APKGH                                
           ELSE                                                                 
               MOVE DP015-RED          TO  APKGC                                
               MOVE DP015-REVERSE      TO  APKGH                                
               MOVE -1                 TO  APKGL                                
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF (DP029-MENU-CRITERION EQUAL PC-S                                  
           OR  DP029-MENU-CRITERION NOT GREATER THAN SPACES)                    
               MOVE DP015-GREEN        TO  AMENUC                               
               MOVE DP015-UNDERLINE    TO  AMENUH                               
           ELSE                                                                 
               MOVE DP015-RED          TO  AMENUC                               
               MOVE DP015-REVERSE      TO  AMENUH                               
               MOVE -1                 TO  AMENUL                               
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF (DP029-DETAIL-CRITERION EQUAL PC-S                                
           OR  DP029-DETAIL-CRITERION NOT GREATER THAN SPACES)                  
               MOVE DP015-GREEN        TO  ADETAILC                             
               MOVE DP015-UNDERLINE    TO  ADETAILH                             
           ELSE                                                                 
               MOVE DP015-RED          TO  ADETAILC                             
               MOVE DP015-REVERSE      TO  ADETAILH                             
               MOVE -1                 TO  ADETAILL                             
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF (DP029-LIST-CRITERION EQUAL PC-S                                  
           OR  DP029-LIST-CRITERION NOT GREATER THAN SPACES)                    
               MOVE DP015-GREEN        TO  ALISTC                               
               MOVE DP015-UNDERLINE    TO  ALISTH                               
           ELSE                                                                 
               MOVE DP015-RED          TO  ALISTC                               
               MOVE DP015-REVERSE      TO  ALISTH                               
               MOVE -1                 TO  ALISTL                               
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF (DP029-UPD-VSAM-IND   EQUAL PC-Y-UPDATE-REQUESTED                 
           OR  DP029-UPD-VSAM-IND   EQUAL PC-N-NO-UPDATE-NEEDED                 
           OR  DP029-UPD-VSAM-IND   EQUAL PC-D-DELETE-REQUESTED                 
           OR  DP029-UPD-VSAM-IND   EQUAL PC-E-ERROR-OCCURRED                   
           OR  DP029-UPD-VSAM-IND   NOT GREATER THAN SPACES)                    
               MOVE DP015-GREEN        TO  AUVINDC                              
               MOVE DP015-UNDERLINE    TO  AUVINDH                              
           ELSE                                                                 
               MOVE DP015-RED          TO  AUVINDC                              
               MOVE DP015-REVERSE      TO  AUVINDH                              
               MOVE -1                 TO  AUVINDL                              
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           MOVE DP015-GREEN            TO  ATRANC.                              
           MOVE DP015-UNDERLINE        TO  ATRANH.                              
           IF  DP029-TRAN     GREATER THAN SPACES                               
               PERFORM 3330-TRAN-CHECK                                          
               IF  PS-TRAN-ERROR                                                
                   MOVE DP015-RED          TO  ATRANC                           
                   MOVE DP015-REVERSE      TO  ATRANH                           
                   MOVE -1                 TO  ATRANL                           
                   MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                 
                   SET DP020-MSG-FATAL     TO  TRUE                             
                   SET PS-ERROR            TO  TRUE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DP015-GREEN            TO  APROGC.                              
           MOVE DP015-UNDERLINE        TO  APROGH.                              
           IF  DP029-PROG     GREATER THAN SPACES                               
               PERFORM 3320-PGM-NAME-CHECK                                      
               IF  PS-PGM-NAME-ERROR                                            
                   MOVE DP015-RED          TO  APROGC                           
                   MOVE DP015-REVERSE      TO  APROGH                           
                   MOVE -1                 TO  APROGL                           
                   MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                 
                   SET DP020-MSG-FATAL     TO  TRUE                             
                   SET PS-ERROR            TO  TRUE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DP015-GREEN            TO  AMNEMC.                              
           MOVE DP015-UNDERLINE        TO  AMNEMH.                              
           IF  DP029-MNEMONIC GREATER THAN SPACES                               
               PERFORM 3310-MNEMONIC-CHECK                                      
               IF  PS-MNEMONIC-ERROR                                            
                   MOVE DP015-RED          TO  AMNEMC                           
                   MOVE DP015-REVERSE      TO  AMNEMH                           
                   MOVE -1                 TO  AMNEML                           
                   MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                 
                   SET DP020-MSG-FATAL     TO  TRUE                             
                   SET PS-ERROR            TO  TRUE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    CROSS-CHECK EDITS.                                          *        
      *----------------------------------------------------------------*        
                                                                                
           IF  DP029-TRAN         GREATER THAN SPACES                           
           AND DP029-UPD-VSAM-IND GREATER THAN SPACES                           
               MOVE DP015-RED          TO  ATRANC                               
                                           AUVINDC                              
               MOVE DP015-REVERSE      TO  ATRANH                               
                                           AUVINDH                              
               MOVE -1                 TO  ATRANL                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF  DP029-TRAN         GREATER THAN SPACES                           
           AND DP029-PROG         GREATER THAN SPACES                           
               MOVE DP015-RED          TO  ATRANC                               
                                           APROGC                               
               MOVE DP015-REVERSE      TO  ATRANH                               
                                           APROGH                               
               MOVE -1                 TO  APROGL                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF  DP029-TRAN         GREATER THAN SPACES                           
           AND DP029-MNEMONIC     GREATER THAN SPACES                           
               MOVE DP015-RED          TO  ATRANC                               
                                           AMNEMC                               
               MOVE DP015-REVERSE      TO  ATRANH                               
                                           AMNEMH                               
               MOVE -1                 TO  AMNEML                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF  DP029-PROG         GREATER THAN SPACES                           
           AND DP029-UPD-VSAM-IND GREATER THAN SPACES                           
               MOVE DP015-RED          TO  APROGC                               
                                           AUVINDC                              
               MOVE DP015-REVERSE      TO  APROGH                               
                                           AUVINDH                              
               MOVE -1                 TO  APROGL                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF  DP029-MNEMONIC     GREATER THAN SPACES                           
           AND DP029-UPD-VSAM-IND GREATER THAN SPACES                           
               MOVE DP015-RED          TO  AMNEMC                               
                                           AUVINDC                              
               MOVE DP015-REVERSE      TO  AMNEMH                               
                                           AUVINDH                              
               MOVE -1                 TO  AMNEML                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
           IF  DP029-MNEMONIC     GREATER THAN SPACES                           
           AND DP029-PROG         GREATER THAN SPACES                           
               MOVE DP015-RED          TO  AMNEMC                               
                                           APROGC                               
               MOVE DP015-REVERSE      TO  AMNEMH                               
                                           APROGH                               
               MOVE -1                 TO  AMNEML                               
      *        HI-LIGHTED FIELDS ARE MUTUALLY EXCLUSIVE                         
               MOVE PC-TSYMSG-01202    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO  TRUE                                 
               SET PS-ERROR            TO  TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
       3310-MNEMONIC-CHECK.                                                     
      *----------------------------------------------------------------*        
      *    DETERMINE WHETHER THE MNEMONIC IS VALID.                    *        
      *                                                                *        
      *    PERFORMED BY 3300                                           *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE DP029-MNEMONIC       TO  PV-WORK-MNEMONIC.                      
           SET PS-MNEMONIC-ERROR     TO  TRUE.                                  
           SET PS-NO-ASTERISK-FOUND  TO  TRUE.                                  
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB GREATER THAN 8                                      
                   IF  PV-WM-BYTE (PV-SUB) GREATER THAN SPACE                   
                       SET PS-NO-MNEMONIC-ERROR                                 
                                     TO  TRUE                                   
                       IF  PV-WM-BYTE (PV-SUB) EQUAL '*'                        
                           SET PS-ASTERISK-FOUND                                
                                     TO  TRUE                                   
                       END-IF                                                   
                   END-IF                                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
       3320-PGM-NAME-CHECK.                                                     
      *----------------------------------------------------------------*        
      *    DETERMINE WHETHER THE PGM-NAME IS VALID.                    *        
      *                                                                *        
      *    PERFORMED BY 3300                                           *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE DP029-PROG           TO  PV-WORK-PROGRAM.                       
           SET PS-PGM-NAME-ERROR     TO  TRUE.                                  
           SET PS-NO-ASTERISK-FOUND  TO  TRUE.                                  
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB GREATER THAN 8                                      
                   IF  PV-WP-BYTE (PV-SUB) GREATER THAN SPACE                   
                       SET PS-NO-PGM-NAME-ERROR                                 
                                     TO  TRUE                                   
                       IF  PV-WP-BYTE (PV-SUB) EQUAL '*'                        
                           SET PS-ASTERISK-FOUND                                
                                     TO  TRUE                                   
                       END-IF                                                   
                   END-IF                                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
       3330-TRAN-CHECK.                                                         
      *----------------------------------------------------------------*        
      *    DETERMINE WHETHER THE TRANSACTION ENTERED IS VALID.         *        
      *                                                                *        
      *    PERFORMED BY 3300                                           *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE DP029-TRAN           TO  PV-WORK-TRAN.                          
           SET PS-NO-TRAN-ERROR      TO  TRUE.                                  
           SET PS-NO-ASTERISK-FOUND  TO  TRUE.                                  
                                                                                
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB GREATER THAN 4                                      
                   IF  PV-WT-BYTE (PV-SUB) = SPACE                              
                   OR  PV-WT-BYTE (PV-SUB) = '*'                                
                       SET PS-TRAN-ERROR                                        
                                     TO  TRUE                                   
                   END-IF                                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
       3400-MOVE-COMMAREA-TO-SCREEN.                                            
      *----------------------------------------------------------------*        
      *    MOVE THE FIELDS FROM THE COMMAREA TO THE SCREEN.            *        
      *                                                                *        
      *    PERFORMED BY 2200-DISPLAY-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE DP029-MNEMONIC         TO  AMNEMO.                              
           MOVE DP029-PROG             TO  APROGO.                              
           MOVE DP029-TRAN             TO  ATRANO.                              
           MOVE DP029-UPD-VSAM-IND     TO  AUVINDO.                             
           MOVE DP029-LIST-CRITERION   TO  ALISTO.                              
           MOVE DP029-DETAIL-CRITERION TO  ADETAILO.                            
           MOVE DP029-MENU-CRITERION   TO  AMENUO.                              
           MOVE DP029-DB2-PKG-CDE      TO  APKGO.                               
           MOVE DP029-ACCURATE-LOC-REQ TO  ALOCO.                               
                                                                                
       EJECT                                                                    
                                                                                
       4000-NAVIGATE.                                                           
      *----------------------------------------------------------------*        
      *    SETUP THE INTER APPLICATION COMMAREA WITH DATA FROM THE     *        
      *    SPECIFIC COMMAREA.                                          *        
      *                                                                *        
      *    PERFORMED BY 3300-EDIT-DATA-IN-COMMAREA                     *        
      *----------------------------------------------------------------*        
                                                                                
           MOVE DP029-MNEMONIC         TO  DP055-MNEMONIC.                      
           MOVE DP029-PROG             TO  DP055-PROG.                          
           MOVE DP029-TRAN             TO  DP055-TRAN.                          
           MOVE DP029-UPD-VSAM-IND     TO  DP055-UPD-VSAM-IND.                  
           MOVE DP029-LIST-CRITERION   TO  DP055-LIST-CRITERION.                
           MOVE DP029-DETAIL-CRITERION TO  DP055-DETAIL-CRITERION.              
           MOVE DP029-MENU-CRITERION   TO  DP055-MENU-CRITERION.                
           MOVE DP029-DB2-PKG-CDE      TO  DP055-DB2-PKG-CDE.                   
           MOVE DP029-ACCURATE-LOC-REQ TO  DP055-ACCURATE-LOCATION-REQ.         
                                                                                
           IF (DP055-MNEMONIC GREATER THAN SPACES                               
           OR  DP055-PROG           GREATER THAN SPACES                         
           OR  DP055-TRAN           GREATER THAN SPACES                         
           OR  DP055-UPD-VSAM-IND   GREATER THAN SPACES                         
           OR  DP055-LIST-CRITERION GREATER THAN SPACES                         
           OR  DP055-DETAIL-CRITERION GREATER THAN SPACES                       
           OR  DP055-MENU-CRITERION GREATER THAN SPACES                         
           OR  DP055-DB2-PKG-CDE    GREATER THAN SPACES)                        
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-MNEMONIC                       
           END-IF.                                                              
                                                                                
           IF DP029-PROG GREATER THAN SPACES                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-PROG                           
           END-IF.                                                              
                                                                                
           IF DP029-TRAN GREATER THAN SPACES                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-TRAN                           
           END-IF.                                                              
                                                                                
           IF DP029-UPD-VSAM-IND = PC-Y-UPDATE-REQUESTED                        
           OR DP029-UPD-VSAM-IND = PC-N-NO-UPDATE-NEEDED                        
           OR DP029-UPD-VSAM-IND = PC-D-DELETE-REQUESTED                        
           OR DP029-UPD-VSAM-IND = PC-E-ERROR-OCCURRED                          
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-UPD-VSAM-IND                   
           END-IF.                                                              
                                                                                
           IF DP029-DB2-PKG-CDE  = PC-P-PROD-TABLES                             
           OR DP029-DB2-PKG-CDE  = PC-A-ARCHIVE-TABLES                          
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-DB2-PKG-CDE                    
           END-IF.                                                              
                                                                                
           IF DP029-ACCURATE-LOC-REQ = PC-Y-ACRT-LOC-REQR                       
           OR DP029-ACCURATE-LOC-REQ = PC-N-ACRT-LOC-REQR                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-ASTERISK        TO  DP055-ACCURATE-LOCATION-REQ          
           END-IF.                                                              
                                                                                
           MOVE DP055-INTER-APPL-COMMAREA                                       
                                       TO  DP029-SAVE-INTER-APPL-COMM.          
           PERFORM DP015-1000-START-NAVIGATOR.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FILL IN SCREEN HEADER MODULE                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD022.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RETURN TO CICS MODULE                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD003.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT MESSAGE LINES MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD005.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    GET PANEL MODULE                                            *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD007.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE PANEL MODULE                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD008.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    TOGGLE THE FUNCTION KEY LINE MODULE                         *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD009.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CHECK COMMUNICATION AREA MODULE                             *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD015.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE TIME AND DATE MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
       EJECT                                                                    
