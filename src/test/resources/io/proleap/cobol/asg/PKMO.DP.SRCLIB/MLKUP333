       IDENTIFICATION DIVISION.                                         00010002
                                                                        00020002
       PROGRAM-ID.    MLKUP333                                          00030002
       AUTHOR.        RONNA BUTZ                                        00040002
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050002
       DATE-WRITTEN.  8/24/99.                                          00060002
       DATE-COMPILED.                                                   00070002
                                                                        00080002
      ******************************************************************00090002
      *    MLKUP333 - LEDGER DATABASE UPDATE PROGRAM                   *00100002
      *             - INSERT / UPDATE WEEKLY DEPT TABLE (TWKDPT)       *00110002
      *               WITH ON ORDER DATA                               *00120002
      *    WR/PITS#     DATE        DESCRIPTION                        *00130002
      *    --------   --------     ---------------------------------   *00140002
      *    WR10422    09/26/99     INITIALIZE ONORDER AMOUNTS IN       *00150002
      *                            TWKDPT FOR THE FSCL MONTH TO ZERO   *00160002
      *                                                                *00170002
      *    WR26603    03/10/04     INCREASE INP01 FROM 32 TO 50        *00171005
      *                            TO REFLECT MLRD132 CHANGES.  CHANGED*00172008
      *                            7000 TO EXISTANCE CHECK, MODIFIED   *00173008
      *                            SQL TO BE USING DCLGEN HOST         *00174008
      *                            VARIABLES.  CHANGED INSERT OF 0 TO  *00175008
      *                            0. BECAUSE TWKDPT COLS ARE DECIMAL. *00176008
      *                                                                *00176111
      *    WR21764    10/21/04     INCREASE INP01 FROM 50 TO 80        *00177011
      *                            TO REFLECT MLRD132 CHANGES.         *00178011
      ******************************************************************00180002
                                                                        00190002
                                                                        00200002
       ENVIRONMENT DIVISION.                                            00210002
                                                                        00220002
       CONFIGURATION SECTION.                                           00230002
       SOURCE-COMPUTER.        IBM-3090.                                00240002
       OBJECT-COMPUTER.        IBM-3090.                                00250002
       INPUT-OUTPUT SECTION.                                            00260002
       FILE-CONTROL.                                                    00270002
           SELECT ONORDER-FILE                                          00280002
               ASSIGN TO INP01.                                         00290002
                                                                        00300002
           SELECT LEDGER-EXTRACT-DATE-FILE                              00310002
               ASSIGN TO INP02.                                         00320002
                                                                        00330002
                                                                        00340002
       DATA DIVISION.                                                   00350002
       FILE SECTION.                                                    00360002
                                                                        00370002
       FD  ONORDER-FILE                                                 00380002
           LABEL RECORDS ARE STANDARD                                   00390002
           RECORDING MODE IS F                                          00400002
           BLOCK CONTAINS 0 RECORDS.                                    00410002
       01  ONORDER-RECORD              PIC X(80).                       00420011
                                                                        00430002
       FD  LEDGER-EXTRACT-DATE-FILE                                     00440002
           RECORDING MODE IS F                                          00450002
           LABEL RECORDS ARE STANDARD                                   00460002
           BLOCK CONTAINS 0 RECORDS.                                    00470002
                                                                        00480002
       01  LEDGER-EXTRACT-DATE-RECORD       PIC X(80).                  00490002
                                                                        00500002
      *                                                                 00510002
                                                                        00520002
       WORKING-STORAGE SECTION.                                         00530002
                                                                        00540002
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00550002
                                                                        00560002
       01  PC-PROGRAM-CONSTANTS.                                        00570002
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK110'.    00580002
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP333'.  00590002
           05  PC-MAX-RETRIES          PIC S9(03) VALUE +3.             00600002
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00610002
                                                                        00620002
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00630002
           05  PE-MSG-01                       PIC X(50) VALUE          00640002
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00650002
           05  PE-MSG-02                       PIC X(50) VALUE          00660002
                'ATTEMPT TO UPDATE ROW ON TABLE TWKDPT FAILED     '.    00670002
           05  PE-MSG-03                       PIC X(50) VALUE          00680002
                'ATTEMPT TO INSERT ROW ON TABLE TWKDPT FAILED     '.    00690002
           05  PE-MSG-04                       PIC X(50) VALUE          00700002
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00710002
           05  PE-MSG-05                       PIC X(50) VALUE          00720002
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00730002
           05  PE-MSG-06                       PIC X(50) VALUE          00740002
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00750002
           05  PE-MSG-07                       PIC X(50) VALUE          00760002
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00770002
           05  PE-MSG-08                       PIC X(50) VALUE          00780002
                'SELECT OF CHECKPOINT RESTART INFO FROM TCKRSTINF'.     00790002
           05  PE-MSG-09                       PIC X(50) VALUE          00800002
                'SET HOLD TIMESTAMP EQUAL TO CURRENT TIMESTAMP  '.      00810002
           05  PE-MSG-10                       PIC X(50) VALUE          00820002
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00830002
           05  PE-MSG-11                       PIC X(50) VALUE          00840002
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00850002
           05  PE-MSG-12                       PIC X(50) VALUE          00860002
                'UPDATE TWKDPT TO INIT ONORD AMOUNT FAILED       '.     00870002
           05  PE-MSG-13                       PIC X(50) VALUE          00880002
                'ATTEMPT TO SELECT ROW FROM TDTATTR FAILED        '.    00890002
      *                                                                 00900002
       01  PS-PROGRAM-SWITCHES.                                         00910002
           05  PS-ONORDER-FILE-EOF-SW          PIC X      VALUE 'N'.    00920002
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00930002
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00940002
           05  PS-EMPTY-FILE-SW                PIC X      VALUE 'N'.    00950002
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00960002
           05  PS-WKDPT-CURSOR-EOF-SW          PIC X      VALUE 'N'.    00970002
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00980002
           05  PS-FIRST-COMMIT-SW              PIC X      VALUE 'N'.    00990002
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    01000002
           05  PS-DEADLOCK-RESTART-SW          PIC X      VALUE 'N'.    01010002
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    01020002
           05  PS-PROCESSING-OPTIONS           PIC X      VALUE 'P'.    01030002
               88  PS-UPDATE-WKDPT-ROW                    VALUE 'U'.    01040002
               88  PS-INSERT-WKDPT-ROW                    VALUE 'I'.    01050002
           05  PS-END-OF-LEDGER-DATE-FILE-SW   PIC X      VALUE 'N'.    01060002
               88 PS-END-OF-LEDGER-DATE-FILE              VALUE 'Y'.    01070002
      *                                                                 01080002
       01  PROGRAM-VARIABLES.                                           01090002
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01100002
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01110002
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01120002
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01130002
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01140002
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01150002
           05  PV-MN-END-FSCL-WK-NBR           PIC  X(10) VALUE SPACES. 01160002
           05  PV-MN-BEG-FSCL-WK-NBR           PIC  X(10) VALUE SPACES. 01170002
W26603     05  PV-DB2-TEST                     PIC S9(04)  VALUE ZEROES 01174008
W26603                                                COMP.             01175008
      *                                                                 01180002
       01  PA-PROGRAM-ACCUMULATORS.                                     01190002
           05  PA-RECORD-COUNTS.                                        01200002
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01210002
               10  PA-UPDATE-COUNT             PIC  9(9)  VALUE ZEROES. 01220002
               10  PA-INSERT-COUNT             PIC  9(9)  VALUE ZEROES. 01230002
               10  PA-DEPTS-PROCESSED          PIC  9(9)  VALUE ZEROES. 01240002
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01250002
      * TEST VARS FOR TOTALS                                            01260002
       01  TEST-VARS.                                                   01270002
W26603     05 PV-UPDATE-RTL-TOT                PIC S9(11)V99 VALUE +0.  01280005
W26603     05 PV-INSERT-RTL-TOT                PIC S9(11)V99 VALUE +0.  01290005
W26603     05 PV-UPDATE-CST-TOT                PIC S9(11)V99 VALUE +0.  01300005
W26603     05 PV-INSERT-CST-TOT                PIC S9(11)V99 VALUE +0.  01310005
      *                                                                 01320002
      *                                                                 01330002
      ******************************************************************01340002
      * MLRD132 - M/L  WEEKLY DEPT ONORDER UPDATE TRANSACTION LAYOUT    01350002
      ******************************************************************01360002
           COPY MLRD132.                                                01370002
      *                                                                 01380002
      ******************************************************************01390002
      * MLRD135 - FISCAL MONTH END DATE                                 01400002
      ******************************************************************01410002
           COPY MLRD135.                                                01420002
      *                                                                 01430002
      ******************************************************************01440002
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01450002
      ******************************************************************01460002
           COPY DPWS004.                                                01470002
      *                                                                 01480002
      ******************************************************************01490002
      *  USED FOR DB2 ERRORS                                            01500002
      ******************************************************************01510002
           EXEC SQL                                                     01520002
               INCLUDE SQLCA                                            01530002
           END-EXEC.                                                    01540002
      *                                                                 01550002
      ******************************************************************01560002
      *  M/L WEEKLY DEPT TABLE                                          01570002
      ******************************************************************01580002
           EXEC SQL                                                     01590002
               INCLUDE TWKDPT                                           01600002
           END-EXEC.                                                    01610002
      *                                                                 01620002
      ***************************************************************** 01630002
      * DATE ATTRIBUTE TABLE                                            01640002
      ***************************************************************** 01650002
           EXEC SQL                                                     01660002
               INCLUDE TDTATTR                                          01670002
           END-EXEC.                                                    01680002
      *                                                                 01690002
      ***************************************************************** 01700002
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01710002
      ***************************************************************** 01720002
           EXEC SQL                                                     01730002
               INCLUDE TCKRSTCN                                         01740002
           END-EXEC.                                                    01750002
      *                                                                 01760002
           EXEC SQL                                                     01770002
               INCLUDE TCKRSTIN                                         01780002
           END-EXEC.                                                    01790002
      *                                                                 01800002
      ******************************************************************01810002
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01820002
      ******************************************************************01830002
       01  CR-CHECKPOINT-RESTART-AREA.                                  01840002
           05  CR-AREA-LEN                  PIC S9(04) VALUE +61  COMP. 01850002
           05  CR-CHECKPOINT-RESTART-VAR.                               01860002
               10  CR-PREV-DTL-KEY          PIC  X(15) VALUE SPACES.    01870002
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01880002
                   15  CR-FISCAL-NBR        PIC  X(02).                 01890002
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01900002
                   15  CR-DEPT-NBR          PIC  X(03).                 01910002
               10  FILLER                   PIC  X(01) VALUE SPACES.    01920002
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01930002
               10  CR-UPDATE-COUNT          PIC  9(09) VALUE ZEROES.    01940002
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01950002
               10  CR-DEPTS-PROCESSED       PIC  9(09) VALUE ZEROES.    01960002
               10  CR-TWKDPT-COMMIT-COUNT   PIC  9(09) VALUE ZEROES.    01970002
      *                                                                 01980002
       01  CK-CHECKPOINT-SAVE-AREA.                                     01990002
           05  CK-SAVE-PREV-KEY         PIC  X(15) VALUE SPACES.        02000002
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       02010002
               10  CK-FISCAL-NBR        PIC  X(02).                     02020002
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     02030002
               10  CK-DEPT-NBR          PIC  X(03).                     02040002
      *                                                                 02050002
      *                                                                 02060002
      *                                                                 02070002
      *                                                                 02080002
      *-------------------*                                             02090002
       PROCEDURE DIVISION.                                              02100002
      *-------------------*                                             02110002
                                                                        02120002
       0000-MAIN-MODULE.                                                02130002
                                                                        02140002
           PERFORM 1000-INITIALIZATION.                                 02150002
      *                                                                 02160002
           PERFORM 2000-PROCESS-INPUT                                   02170002
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         02180002
      *                                                                 02190002
           PERFORM 8000-EOJ-ROUTINE.                                    02200002
                                                                        02210002
           STOP RUN.                                                    02220002
      *                                                                 02230002
      *                                                                 02240002
      *                                                                 02250002
      ******************************************************************02260002
      * OPEN INPUT FILE , INITIALIZE COUNTERS AND THE ONORD VALUES IN   02270002
      * THE TWKDPT TABLE, READ FIRST INPUT RECORD PREPARE CHECKPOINT    02280002
      * RESTART TABLES FOR PROCESSING / RESTART                         02290002
      * TCKRSTCNTL-CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION   02300002
      ******************************************************************02310002
      *                                                                 02320002
       1000-INITIALIZATION.                                             02330002
      *                                                                 02340002
           OPEN INPUT ONORDER-FILE                                      02350002
                      LEDGER-EXTRACT-DATE-FILE.                         02360002
      *                                                                 02370002
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02380002
      *                                                                 02390002
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02400002
                PERFORM 7500-SELECT-RESTART-INFO                        02410002
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                   02420002
                                              CR-CHECKPOINT-RESTART-VAR 02430002
                MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY       02440002
           ELSE                                                         02450002
               PERFORM 4000-READ-LEDGER-EXTRACT-DATE                    02460002
      *                                                                 02470002
               IF PS-END-OF-LEDGER-DATE-FILE                            02480002
                   DISPLAY '** ABEND **'                                02490002
                   DISPLAY '** PROGRAM MLKRP215 **'                     02500002
                   DISPLAY '** PARAGRAPH 1000-INITIALIZATION'           02510002
                   DISPLAY '** LEDGER EXTRACT DATE FILE IS EMPTY.'      02520002
                   MOVE +4003 TO ABEND-CODE                             02530002
                   PERFORM 9999-ABEND                                   02540002
               END-IF                                                   02550002
      *                                                                 02560002
               MOVE ML135-EXTRACT-DTE          TO DTATTR-KY-DTE         02570002
      *                                                                 02580002
               PERFORM 6000-GET-DATE-ATTRIBUTES                         02590002
      *                                                                 02600002
               PERFORM 6100-INITIALIZE-ONORD                            02610002
      *                                                                 02620002
               SET PS-FIRST-COMMIT TO TRUE                              02630002
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02640002
           END-IF.                                                      02650002
      *                                                                 02660002
           PERFORM 4100-READ-ONORDER-FILE UNTIL                         02670002
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02680002
            OR PS-NO-MORE-INPUT-RECORDS.                                02690002
      *                                                                 02700002
      * IF PROGRAM IN RESTART MODE, BUT NO RECORDS EXIST AFTER LAST CKPT02710002
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02720002
           IF PS-NO-MORE-INPUT-RECORDS                                  02730002
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02740002
                   MOVE CR-INPUT-READ-COUNT     TO PA-INPUT-COUNT       02750002
                   MOVE CR-TWKDPT-COMMIT-COUNT  TO PA-COMMIT-COUNT      02760002
                   MOVE CR-UPDATE-COUNT         TO PA-UPDATE-COUNT      02770002
                   MOVE CR-INSERT-COUNT         TO PA-INSERT-COUNT      02780002
                   MOVE CR-DEPTS-PROCESSED      TO PA-DEPTS-PROCESSED   02790002
               ELSE                                                     02800002
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02810002
           ELSE                                                         02820002
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02830002
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02840002
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02850002
           END-IF.                                                      02860002
      *                                                                 02870002
      *                                                                 02880002
      ******************************************************************02890002
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02900002
      ******************************************************************02910002
       2000-PROCESS-INPUT.                                              02920002
      *                                                                 02930002
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02940002
      *                                                                 02950002
           PERFORM 7000-SELECT-DB-ROW.                                  02960002
      *                                                                 02970002
           IF PS-PROGRAM-DEADLOCKED                                     02980002
               CONTINUE                                                 02990002
           ELSE                                                         03000002
           EVALUATE TRUE                                                03010002
               WHEN PS-UPDATE-WKDPT-ROW                                 03020002
                   PERFORM 7100-UPDATE-WKDPT-ROW                        03030002
      *            ADD AMOUNTS TO ACCUM                                 03040002
                   ADD ML132-EXT-RTL-AMT       TO PV-UPDATE-RTL-TOT     03050002
                   ADD ML132-EXT-CST-AMT       TO PV-UPDATE-CST-TOT     03060002
               WHEN PS-INSERT-WKDPT-ROW                                 03070002
                   PERFORM 7200-INSERT-WKDPT-ROW                        03080002
      *            ADD AMOUNTS TO ACCUM                                 03090002
                   ADD ML132-EXT-RTL-AMT       TO PV-INSERT-RTL-TOT     03100002
                   ADD ML132-EXT-CST-AMT       TO PV-INSERT-CST-TOT     03110002
               WHEN OTHER                                               03120002
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03130002
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03140002
                   PERFORM 9998-SQL-ABEND                               03150002
           END-EVALUATE.                                                03160002
      *                                                                 03170002
           IF PS-PROGRAM-DEADLOCKED                                     03180002
               CONTINUE                                                 03190002
           ELSE                                                         03200002
               PERFORM 7700-COMMIT-PROCESSING                           03210002
               PERFORM 4100-READ-ONORDER-FILE.                          03220002
      *                                                                 03230002
      ******************************************************************03240002
      * READ THE DATE FILE                                              03250002
      ******************************************************************03260002
       4000-READ-LEDGER-EXTRACT-DATE.                                   03270002
           READ LEDGER-EXTRACT-DATE-FILE                                03280002
               INTO ML135-LEDGER-EXTRACT-DATE-REC                       03290002
               AT END SET PS-END-OF-LEDGER-DATE-FILE TO TRUE.           03300002
                                                                        03310002
      *                                                                 03320002
      ******************************************************************03330002
      * READS THE CONDENSED FILE INTO THE TWKDPT LAYOUT                 03340002
      ******************************************************************03350002
       4100-READ-ONORDER-FILE.                                          03360002
            READ ONORDER-FILE INTO ML132-ONORD-EXTRACT-WK-REC           03370002
               AT END MOVE 'Y' TO PS-ONORDER-FILE-EOF-SW.               03380002
      *                                                                 03390002
            IF PS-MORE-INPUT-RECORDS                                    03400002
                ADD 1                        TO PA-INPUT-COUNT.         03410002
      *                                                                 03420002
      ******************************************************************03430002
      * GET DATE ATTRIBUTES                                             03440002
      ******************************************************************03450002
       6000-GET-DATE-ATTRIBUTES.                                        03460002
      *                                                                 03470002
           EXEC SQL                                                     03480002
               SELECT                                                   03490002
                      A.FSCL_MN_BEG_DTE                                 03500002
                     ,A.FSCL_WK_NBR                                     03510002
                     ,B.FSCL_WK_NBR                                     03520002
               INTO  :DTATTR-FSCL-MN-BEG-DTE                            03530002
                    ,:PV-MN-END-FSCL-WK-NBR                             03540002
                    ,:PV-MN-BEG-FSCL-WK-NBR                             03550002
               FROM TDTATTR A                                           03560002
                   ,TDTATTR B                                           03570002
               WHERE A.KY_DTE          =:ML135-EXTRACT-DTE              03580002
                 AND A.FSCL_MN_BEG_DTE = B.KY_DTE                       03590002
W26603         WITH UR                                                  03591009
           END-EXEC.                                                    03600002
      *                                                                 03610002
           EVALUATE TRUE                                                03620002
               WHEN SQLCODE = ZERO                                      03630002
                   CONTINUE                                             03640002
               WHEN OTHER                                               03650002
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE              03660002
                   MOVE    '6000-GET-DATE-ATTRIBUTES'                   03670002
                                              TO PV-PARAGRAPH-NAME      03680002
                   MOVE PE-MSG-13             TO PV-OPERATION-ATTEMPTED 03690002
                   PERFORM 9998-SQL-ABEND                               03700002
           END-EVALUATE.                                                03710002
      *                                                                 03720002
      ******************************************************************03730002
      * SET ALL ONORDER AMOUNTS TO ZERO FOR ALL WEEKS IN THE FISCAL     03740002
      * MONTH                                                           03750002
      ******************************************************************03760002
       6100-INITIALIZE-ONORD.                                           03770002
      *                                                                 03780002
           EXEC SQL                                                     03790002
               UPDATE TWKDPT                                            03800002
W26603            SET ONORD_RTL_AMT       = 0                           03810010
W26603               ,ONORD_COST_AMT      = 0                           03820010
                WHERE FSCL_WK_NBR     BETWEEN :PV-MN-BEG-FSCL-WK-NBR    03830002
                                          AND :PV-MN-END-FSCL-WK-NBR    03840002
                 AND  FSCL_WK_END_DTE BETWEEN :DTATTR-FSCL-MN-BEG-DTE   03850002
                                          AND :ML135-EXTRACT-DTE        03860002
           END-EXEC.                                                    03870002
      *                                                                 03880002
           EVALUATE TRUE                                                03890002
               WHEN SQLCODE = ZERO                                      03900002
               WHEN SQLCODE = +100                                      03910003
                   CONTINUE                                             03920002
               WHEN OTHER                                               03930002
                   MOVE PE-MSG-12           TO PV-OPERATION-ATTEMPTED   03940002
                   MOVE  '6100-INITIALIZE-ONORD'                        03950002
                                            TO PV-PARAGRAPH-NAME        03960002
                   PERFORM 9998-SQL-ABEND                               03970002
           END-EVALUATE.                                                03980002
      *                                                                 03990002
      ******************************************************************04000002
      * EXISTENCE CK FROM THE WEEKLY DEPT TABLE THAT MATCHES INPUT KEY  04010008
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04020002
      ******************************************************************04030002
W26603 7000-SELECT-DB-ROW.                                              04040008
                                                                        04041008
W26603     MOVE ML132-ONORD-FSCL-WK-NBR     TO WKDPT-FSCL-WK-NBR.       04042008
W26603     MOVE ML132-ONORD-FSCL-WK-END-DTE TO WKDPT-FSCL-WK-END-DTE.   04042108
W26603     MOVE ML132-ONORD-DEPT-NBR        TO WKDPT-DEPT-NBR.          04042208
      *                                                                 04050002
           EXEC SQL                                                     04060002
W26603         SELECT 1                                                 04070008
W26603           INTO :PV-DB2-TEST                                      04100008
               FROM TWKDPT                                              04120002
W26603        WHERE   FSCL_WK_NBR        = :WKDPT-FSCL-WK-NBR           04130008
W26603          AND   FSCL_WK_END_DTE    = :WKDPT-FSCL-WK-END-DTE       04140008
W26603          AND   DEPT_NBR           = :WKDPT-DEPT-NBR              04150008
              FETCH FIRST ROW ONLY                                      04151008
           END-EXEC.                                                    04160002
      *                                                                 04170002
           EVALUATE SQLCODE                                             04180002
               WHEN 0                                                   04190002
                   SET PS-UPDATE-WKDPT-ROW        TO TRUE               04200002
               WHEN +100                                                04210002
                   SET PS-INSERT-WKDPT-ROW        TO TRUE               04220002
               WHEN -911                                                04230002
                   ADD +1             TO PV-DEADLOCK-COUNT              04240002
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04250002
                       MOVE '7000-SELECT-DB-ROW'    TO                  04260002
                                                    PV-PARAGRAPH-NAME   04270002
                       PERFORM 9000-DEADLOCK-ERROR                      04280002
                   ELSE                                                 04290002
                       PERFORM 7999-RESTART-PROCESS                     04300002
                   END-IF                                               04310002
               WHEN OTHER                                               04320002
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04330002
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04340002
                   PERFORM 9998-SQL-ABEND                               04350002
           END-EVALUATE.                                                04360002
      *                                                                 04370002
      *                                                                 04380002
      ***************************************************************** 04390002
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04400002
      *   CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC 04410002
      ***************************************************************** 04420002
       7100-UPDATE-WKDPT-ROW.                                           04430002
      *                                                                 04440002
W26603     MOVE ML132-EXT-RTL-AMT        TO WKDPT-ONORD-RTL-AMT.        04441008
W26603     MOVE ML132-EXT-CST-AMT        TO WKDPT-ONORD-COST-AMT.       04441108
W26603     MOVE ML132-ONORD-FSCL-WK-NBR     TO WKDPT-FSCL-WK-NBR.       04441208
W26603     MOVE ML132-ONORD-FSCL-WK-END-DTE TO WKDPT-FSCL-WK-END-DTE.   04441308
W26603     MOVE ML132-ONORD-DEPT-NBR        TO WKDPT-DEPT-NBR.          04441408
      *                                                                 04450002
           EXEC SQL                                                     04460002
               UPDATE TWKDPT                                            04470002
W26603             SET ONORD_RTL_AMT      = :WKDPT-ONORD-RTL-AMT        04480008
W26603               , ONORD_COST_AMT     = :WKDPT-ONORD-COST-AMT       04490008
              WHERE   FSCL_WK_NBR         = :WKDPT-FSCL-WK-NBR          04500008
                AND   FSCL_WK_END_DTE     = :WKDPT-FSCL-WK-END-DTE      04510008
                AND   DEPT_NBR            = :WKDPT-DEPT-NBR             04520008
           END-EXEC                                                     04530002
      *                                                                 04540002
           EVALUATE SQLCODE                                             04550002
               WHEN 0                                                   04560002
                   CONTINUE                                             04570002
               WHEN -911                                                04580002
                   ADD +1             TO PV-DEADLOCK-COUNT              04590002
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04600002
                       MOVE '7100-UPDATE-WKDPT-ROW' TO                  04610002
                                                    PV-PARAGRAPH-NAME   04620002
                       PERFORM 9000-DEADLOCK-ERROR                      04630002
                   ELSE                                                 04640002
                       PERFORM 7999-RESTART-PROCESS                     04650002
                   END-IF                                               04660002
               WHEN OTHER                                               04670002
                   MOVE '7100-UPDATE-WKDPT-ROW' TO PV-PARAGRAPH-NAME    04680002
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04690002
                   PERFORM 9998-SQL-ABEND                               04700002
           END-EVALUATE.                                                04710002
      *                                                                 04720002
           IF PS-PROGRAM-DEADLOCKED                                     04730002
               CONTINUE                                                 04740002
           ELSE                                                         04750002
               ADD 1 TO PA-UPDATE-COUNT                                 04760002
                        PA-DEPTS-PROCESSED.                             04770002
      *                                                                 04780002
      *                                                                 04790002
      ***************************************************************** 04800002
      * THERE WAS NO MATCHING ROW ON THE WEEKLY DEPT TABLE. INITIAL   * 04810002
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04820002
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04830002
      ***************************************************************** 04840002
       7200-INSERT-WKDPT-ROW.                                           04850002
      *                                                                 04860002
W26603     MOVE ML132-EXT-RTL-AMT        TO WKDPT-ONORD-RTL-AMT.        04860108
W26603     MOVE ML132-EXT-CST-AMT        TO WKDPT-ONORD-COST-AMT.       04860208
W26603     MOVE ML132-ONORD-FSCL-WK-NBR     TO WKDPT-FSCL-WK-NBR.       04863008
W26603     MOVE ML132-ONORD-FSCL-WK-END-DTE TO WKDPT-FSCL-WK-END-DTE.   04864008
W26603     MOVE ML132-ONORD-DEPT-NBR        TO WKDPT-DEPT-NBR.          04865008
      *                                                                 04870002
           EXEC SQL                                                     04880002
               INSERT INTO TWKDPT                                       04890002
                   (   FSCL_WK_NBR                                      04900002
                     , FSCL_WK_END_DTE                                  04910002
                     , DEPT_NBR                                         04920002
                     , RCPT_RTL_AMT                                     04930002
                     , RCPT_NET_COST_AMT                                04940002
                     , PADJ_RTL_AMT                                     04950002
                     , PADJ_NET_COST_AMT                                04960002
                     , SLS_RTL_AMT                                      04970002
                     , MKDN_RTL_AMT                                     04980002
                     , MKUP_RTL_AMT                                     04990002
                     , XFER_IN_RTL_AMT                                  05000002
                     , XFER_OUT_RTL_AMT                                 05010002
                     , INV_ADJ_RTL_AMT                                  05020002
                     , SHNK_RTL_AMT                                     05030002
                     , END_INV_RTL_AMT                                  05040002
                     , CUM_MKUP_PCT                                     05050002
                     , END_INV_COST_AMT                                 05060002
                     , SLS_COST_AMT                                     05070002
                     , GRO_MRGN_AMT                                     05080002
                     , ONORD_RTL_AMT                                    05090002
                     , ONORD_COST_AMT    )                              05100002
               VALUES                                                   05110002
W26603             (  :WKDPT-FSCL-WK-NBR                                05120008
W26603               ,:WKDPT-FSCL-WK-END-DTE                            05130008
W26603               ,:WKDPT-DEPT-NBR                                   05140008
W26603               , 0                                                05150010
W26603               , 0                                                05160010
W26603               , 0                                                05170010
W26603               , 0                                                05180010
W26603               , 0                                                05190010
W26603               , 0                                                05200010
W26603               , 0                                                05210010
W26603               , 0                                                05220010
W26603               , 0                                                05230010
W26603               , 0                                                05240010
W26603               , 0                                                05250010
W26603               , 0                                                05260010
W26603               , 0                                                05270010
W26603               , 0                                                05280010
W26603               , 0                                                05290010
W26603               , 0                                                05300010
W26603               ,:WKDPT-ONORD-RTL-AMT                              05310008
W26603               ,:WKDPT-ONORD-COST-AMT  )                          05320008
           END-EXEC                                                     05330002
      *                                                                 05340002
           EVALUATE SQLCODE                                             05350002
               WHEN 0                                                   05360002
                   CONTINUE                                             05370002
               WHEN -911                                                05380002
                   ADD +1             TO PV-DEADLOCK-COUNT              05390002
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05400002
                       MOVE '7200-INSERT-WKDPT-ROW' TO                  05410002
                                                    PV-PARAGRAPH-NAME   05420002
                       PERFORM 9000-DEADLOCK-ERROR                      05430002
                   ELSE                                                 05440002
                       PERFORM 7999-RESTART-PROCESS                     05450002
                   END-IF                                               05460002
               WHEN OTHER                                               05470002
                   MOVE '7200-INSERT-WKDPT-ROW' TO PV-PARAGRAPH-NAME    05480002
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05490002
                   PERFORM 9998-SQL-ABEND                               05500002
           END-EVALUATE                                                 05510002
      *                                                                 05520002
           IF PS-PROGRAM-DEADLOCKED                                     05530002
               CONTINUE                                                 05540002
           ELSE                                                         05550002
               ADD 1 TO PA-INSERT-COUNT                                 05560002
                        PA-DEPTS-PROCESSED.                             05570002
      *                                                                 05580002
      *                                                                 05590002
      ******************************************************************05600002
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05610002
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05620002
      *            CONTROL TABLE                                       *05630002
      ******************************************************************05640002
       7300-GET-COMMIT-TIMESTAMP.                                       05650002
                                                                        05660002
           EXEC SQL                                                     05670002
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05680002
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05690002
               INTO :PV-COMMIT-TIMESTAMP                                05700002
               FROM TCKRSTCNTL                                          05710002
              WHERE JOB_NAME  = :PC-JOB-NAME                            05720002
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05730002
           END-EXEC.                                                    05740002
                                                                        05750002
           EVALUATE TRUE                                                05760002
               WHEN SQLCODE = 0                                         05770002
                   CONTINUE                                             05780002
               WHEN SQLCODE NOT EQUAL ZERO                              05790002
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05800002
                   MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED 05810002
                   PERFORM 9998-SQL-ABEND                               05820002
           END-EVALUATE.                                                05830002
      *                                                                 05840002
      *                                                                 05850002
      ******************************************************************05860002
      * 7400-INIT-CHECKPOINT-SELECT                                    *05870002
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05880002
      *            IF WE ARE IN A RESTART CONDITION.                   *05890002
      ******************************************************************05900002
       7400-INIT-CHECKPOINT-SELECT.                                     05910002
                                                                        05920002
           EXEC SQL                                                     05930002
             SELECT  CKPT_STAT_CODE                                     05940002
                    ,CKPT_FRQNCY_QTY                                    05950002
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05960002
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05970002
               FROM  TCKRSTCNTL                                         05980002
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05990002
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06000002
           END-EXEC.                                                    06010002
                                                                        06020002
           IF SQLCODE = 0                                               06030002
               CONTINUE                                                 06040002
           ELSE                                                         06050002
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06060002
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06070002
               PERFORM 9998-SQL-ABEND                                   06080002
           END-IF.                                                      06090002
      *                                                                 06100002
      *                                                                 06110002
      ******************************************************************06120002
      * 7500-SELECT-RESTART-INFO                                       *06130002
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06140002
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06150002
      ******************************************************************06160002
       7500-SELECT-RESTART-INFO.                                        06170002
                                                                        06180002
           EXEC SQL                                                     06190002
             SELECT  WORK_STRG_DESC                                     06200002
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06210002
               FROM  TCKRSTINF                                          06220002
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06230002
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06240002
           END-EXEC.                                                    06250002
                                                                        06260002
           IF SQLCODE = 0                                               06270002
               CONTINUE                                                 06280002
           ELSE                                                         06290002
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06300002
               MOVE PE-MSG-08             TO PV-OPERATION-ATTEMPTED     06310002
               PERFORM 9998-SQL-ABEND                                   06320002
           END-IF.                                                      06330002
      *                                                                 06340002
      *                                                                 06350002
      ******************************************************************06360002
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06370002
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06380002
      ******************************************************************06390002
       7600-SET-CURRENT-TIMESTAMP.                                      06400002
                                                                        06410002
           EXEC SQL                                                     06420002
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06430002
           END-EXEC.                                                    06440002
                                                                        06450002
           IF SQLCODE = 0                                               06460002
               CONTINUE                                                 06470002
           ELSE                                                         06480002
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06490002
               MOVE PE-MSG-09             TO PV-OPERATION-ATTEMPTED     06500002
               PERFORM 9998-SQL-ABEND                                   06510002
           END-IF.                                                      06520002
      *                                                                 06530002
      *                                                                 06540002
      ******************************************************************06550002
      * 7700-COMMIT-PROCESSING.                                        *06560002
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06570002
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06580002
      ******************************************************************06590002
       7700-COMMIT-PROCESSING.                                          06600002
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06610002
                                                                        06620002
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06630002
      *                                                                 06640002
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06650002
      *        PREPARE COMMIT RECORD FOR UPDATE                         06660002
               ADD 1                          TO PA-COMMIT-COUNT        06670002
               MOVE ML132-ONORD-FSCL-WK-NBR   TO CR-FISCAL-NBR          06680002
               MOVE ML132-ONORD-FSCL-WK-END-DTE                         06690002
                                              TO CR-FISCAL-MN-END-DTE   06700002
               MOVE ML132-ONORD-DEPT-NBR      TO CR-DEPT-NBR            06710002
               MOVE PA-COMMIT-COUNT           TO CR-TWKDPT-COMMIT-COUNT 06720002
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06730002
               MOVE PA-UPDATE-COUNT           TO CR-UPDATE-COUNT        06740002
               MOVE PA-INSERT-COUNT           TO CR-INSERT-COUNT        06750002
               MOVE PA-DEPTS-PROCESSED        TO CR-DEPTS-PROCESSED     06760002
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06770002
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06780002
                                             TCKRSTINF-WORK-STRG-DESC   06790002
               IF PS-FIRST-COMMIT                                       06800002
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06810002
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06820002
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06830002
               END-IF                                                   06840002
               PERFORM 7800-COMMIT-CHANGES                              06850002
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06860002
           END-IF.                                                      06870002
      *                                                                 06880002
      *                                                                 06890002
      ******************************************************************06900002
      * 7800-COMMIT-CHANGES.                                            06910002
      *   PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE. TAKE A COMMIT 06920002
      ******************************************************************06930002
       7800-COMMIT-CHANGES.                                             06940002
                                                                        06950002
           EXEC SQL                                                     06960002
             UPDATE TCKRSTINF                                           06970002
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06980002
              WHERE JOB_NAME       = :PC-JOB-NAME                       06990002
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07000002
           END-EXEC.                                                    07010002
                                                                        07020002
           IF SQLCODE = 0                                               07030002
               CONTINUE                                                 07040002
           ELSE                                                         07050002
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07060002
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07070002
               PERFORM 9998-SQL-ABEND                                   07080002
           END-IF.                                                      07090002
                                                                        07100002
           EXEC SQL                                                     07110002
               COMMIT                                                   07120002
           END-EXEC.                                                    07130002
                                                                        07140002
           IF SQLCODE = 0                                               07150002
               CONTINUE                                                 07160002
           ELSE                                                         07170002
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07180002
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07190002
               PERFORM 9998-SQL-ABEND                                   07200002
           END-IF.                                                      07210002
      *                                                                 07220002
      *                                                                 07230002
      ******************************************************************07240002
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07250002
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07260002
      *                                                                 07270002
      ******************************************************************07280002
       7900-UPDATE-CHECKPOINT-STATUS.                                   07290002
                                                                        07300002
           EXEC SQL                                                     07310002
             UPDATE  TCKRSTCNTL                                         07320002
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07330002
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07340002
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07350002
           END-EXEC.                                                    07360002
                                                                        07370002
           IF SQLCODE = 0                                               07380002
               CONTINUE                                                 07390002
           ELSE                                                         07400002
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07410002
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED07420002
               PERFORM 9998-SQL-ABEND                                   07430002
           END-IF.                                                      07440002
                                                                        07450002
           EJECT                                                        07460002
      *                                                                 07470002
      *                                                                 07480002
      ******************************************************************07490002
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKDPT ROW FOR UPDATE 07500002
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07510002
      ******************************************************************07520002
       7999-RESTART-PROCESS.                                            07530002
      *                                                                 07540002
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07550002
      *                                                                 07560002
           CLOSE ONORDER-FILE                                           07570002
                 LEDGER-EXTRACT-DATE-FILE.                              07580002
      *                                                                 07590002
           PERFORM 7500-SELECT-RESTART-INFO.                            07600002
      *                                                                 07610002
           DISPLAY '**** RESTART **** '.                                07620002
           DISPLAY '** SQLCODE -911 ** ', PV-DEADLOCK-COUNT, 'TIMES'.   07630002
           DISPLAY 'TCKRSTINF-LAST CKPT ', TCKRSTINF-WORK-STRG-DESC.    07640002
      *                                                                 07650002
           MOVE TCKRSTINF-WORK-STRG-DESC TO CR-CHECKPOINT-RESTART-AREA. 07660002
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07670002
      *                                                                 07680002
           OPEN INPUT ONORDER-FILE.                                     07690002
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07700002
      *                                                                 07710002
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07720002
           PERFORM 4100-READ-ONORDER-FILE                               07730002
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07740002
      *                                                                 07750002
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07760002
           MOVE CR-UPDATE-COUNT        TO PA-UPDATE-COUNT.              07770002
           MOVE CR-INSERT-COUNT        TO PA-INSERT-COUNT.              07780002
           MOVE CR-DEPTS-PROCESSED     TO PA-DEPTS-PROCESSED.           07790002
      *                                                                 07800002
      *                                                                 07810002
      ******************************************************************07820002
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07830002
      ******************************************************************07840002
       8000-EOJ-ROUTINE.                                                07850002
      *                                                                 07860002
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07870002
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07880002
      *                                                                 07890002
           DISPLAY '                                             '.     07900002
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  07910002
           DISPLAY 'UPDATE COUNT    ', PA-UPDATE-COUNT.                 07920002
           DISPLAY 'INSERT COUNT    ', PA-INSERT-COUNT.                 07930002
           DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.              07940002
           DISPLAY '                                             '.     07950002
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 07960002
           DISPLAY 'UPDATE RTL    TOTALS ' PV-UPDATE-RTL-TOT.           07970002
           DISPLAY 'UPDATE CST    TOTALS ' PV-UPDATE-CST-TOT.           07980002
           DISPLAY 'INSERT RTL    TOTALS ' PV-INSERT-RTL-TOT.           07990002
           DISPLAY 'INSERT CST    TOTALS ' PV-INSERT-CST-TOT.           08000002
                                                                        08010002
      *                                                                 08020002
           CLOSE ONORDER-FILE                                           08030002
                 LEDGER-EXTRACT-DATE-FILE.                              08040002
      *                                                                 08050002
      *                                                                 08060002
      ******************************************************************08070002
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       08080002
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08090002
      ******************************************************************08100002
       9000-DEADLOCK-ERROR.                                             08110002
      *                                                                 08120002
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08130002
               PERFORM 9998-SQL-ABEND.                                  08140002
      *                                                                 08150002
      ******************************************************************08160002
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08170002
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08180002
      ******************************************************************08190002
       9998-SQL-ABEND.                                                  08200002
                                                                        08210002
           MOVE +4013                 TO ABEND-CODE.                    08220002
           DISPLAY '**  ABEND     **'.                                  08230002
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08240002
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08250002
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08260002
                                                                        08270002
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08280002
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08290002
           COPY DPPD004.                                                08300002
           PERFORM 9999-ABEND.                                          08310002
      *                                                                 08320002
      *                                                                 08330002
       9999-ABEND.                                                      08340002
      *                                                                 08350002
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08360002
