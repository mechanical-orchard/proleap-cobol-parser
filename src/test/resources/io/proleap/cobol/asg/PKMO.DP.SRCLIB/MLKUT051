       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUT051.                                         00030001
       AUTHOR.        JOHN SELWITSCHKA.                                 00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  06/02/2010.                                       00060001
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUT051 - DETERMINE LOC TYPE AND ASSIGN LOC NUMBER  *00100001
      *                     (MLRD080 FORMAT)                           *00101001
      ******************************************************************00110000
      ******************************************************************00110100
      *    WR/PITS#     DATE        DESCRIPTION                        *00110200
      *    --------   --------     ---------------------------------   *00110300
      *    WR37965    06/02/10     INITIAL CREATION                    *00110401
      ******************************************************************00110500
                                                                        00110600
       ENVIRONMENT DIVISION.                                            00110700
                                                                        00110800
       CONFIGURATION SECTION.                                           00110900
       SOURCE-COMPUTER.        IBM-3090.                                00111000
       OBJECT-COMPUTER.        IBM-3090.                                00112000
       INPUT-OUTPUT SECTION.                                            00113000
       FILE-CONTROL.                                                    00114000
           SELECT SHNK-PRORATE-FILE-IN                                  00115001
               ASSIGN TO INP01.                                         00116000
                                                                        00117000
           SELECT SHNK-PRORATE-FILE-OUT                                 00118001
               ASSIGN TO OUT01.                                         00118100
                                                                        00118200
       DATA DIVISION.                                                   00118300
       FILE SECTION.                                                    00118400
                                                                        00118500
       FD  SHNK-PRORATE-FILE-IN                                         00118601
           LABEL RECORDS ARE STANDARD                                   00118700
           RECORDING MODE IS F                                          00118800
           BLOCK CONTAINS 0 RECORDS.                                    00118900
       01  SHNK-PRORATE-IN-REC          PIC X(080).                     00119002
                                                                        00120000
       FD  SHNK-PRORATE-FILE-OUT                                        00130001
           LABEL RECORDS ARE STANDARD                                   00140000
           RECORDING MODE IS F                                          00150000
           BLOCK CONTAINS 0 RECORDS.                                    00160000
       01  SHNK-PRORATE-OUT-REC         PIC X(080).                     00170002
      *                                                                 00180000
                                                                        00190000
       WORKING-STORAGE SECTION.                                         00200000
                                                                        00210000
       01  FILLER                      PIC X(28)   VALUE                00220000
                'BEGINNING OF WORKING STORAGE'.                         00230000
                                                                        00240000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00250000
                                                                        00260000
       01  PC-PROGRAM-CONSTANTS.                                        00270000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUT051'.  00290001
      *                                                                 00300000
       01  PS-PROGRAM-SWITCHES.                                         00310000
           05  PS-EOF-SHNK-PRORATE-FILE-SW  PIC X(01)     VALUE 'N'.    00320001
               88  PS-EOF-SHNK-PRORATE-FILE               VALUE 'Y'.    00330001
           05  PS-LOCATION-FOUND-SW         PIC X(01)     VALUE 'N'.    00340000
               88  PS-LOCATION-FOUND-Y                    VALUE 'Y'.    00350000
               88  PS-LOCATION-FOUND-N                    VALUE 'N'.    00360000
           05  PS-LOC-CSR-SW                PIC X(01)     VALUE 'N'.    00370000
               88  PS-END-OF-LOC-CSR                      VALUE 'Y'.    00380000
               88  PS-MORE-LOC-ROWS                       VALUE 'N'.    00390000
      *                                                                 00400000
       01  PROGRAM-VARIABLES.                                           00410000
           05  PV-SUB                    PIC 9(04)  VALUE ZEROES.       00420000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00430000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00440000
      *                                                                 00450000
       01  PA-PROGRAM-ACCUMULATORS.                                     00460000
           05  PA-RECORD-COUNTS.                                        00470000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00480000
               10  PA-OUTPUT-COUNT       PIC  9(09)  VALUE ZEROES.      00490000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00500000
      *                                                                 00510000
       01  PT-PROGRAM-TABLES.                                           00520000
           05  LOC-TBL-MAX               PIC 9(5)    VALUE 100.         00530000
           05  LOCATION-TABLE.                                          00540000
               10  LOC-TBL-SIZE          PIC 9(5)    VALUE ZEROES.      00550000
               10  LOC-ENTRY  OCCURS 100 TIMES INDEXED BY LOC-INDEX.    00560000
                   15  LOC-NBR           PIC 9(05).                     00570000
      *                                                                 00580000
      ******************************************************************00590000
      * MLRD080 - SHRINK PRORATION FILE                                 00600001
      ******************************************************************00610000
           COPY MLRD080.                                                00620004
      *                                                                 00630000
      ******************************************************************00640000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00650000
      ******************************************************************00660000
           COPY DPWS004.                                                00670000
      *                                                                 00680000
      ******************************************************************00690000
      *  USED FOR DB2 ERRORS                                            00700000
      ******************************************************************00710000
           EXEC SQL                                                     00720000
               INCLUDE SQLCA                                            00730000
           END-EXEC.                                                    00740000
      *                                                                 00750000
      ******************************************************************00760000
      *  LOCATION TABLE                                                 00770000
      ******************************************************************00780000
           EXEC SQL                                                     00790000
               INCLUDE XMT00001                                         00800000
           END-EXEC.                                                    00810000
      *                                                                 00820000
      ******************************************************************00830000
      * CURSOR TO RETRIEVE E-BUSINESS LOCATIONS                         00840000
      ******************************************************************00850000
                                                                        00860000
           EXEC SQL                                                     00870000
             DECLARE LOC_CSR CURSOR FOR                                 00880000
                   SELECT LOC_NBR                                       00890000
                     FROM XMT_LOC                                       00900000
                    WHERE E_BUS_IND = 'Y'                               00910000
                    ORDER BY LOC_NBR                                    00920000
                     WITH UR                                            00930000
           END-EXEC.                                                    00940000
                                                                        00950000
      *-------------------*                                             00960000
       PROCEDURE DIVISION.                                              00970000
      *-------------------*                                             00980000
                                                                        00990000
       0000-MLKUT051.                                                   01000001
                                                                        01010000
           PERFORM 1000-INITIALIZATION.                                 01020000
      *                                                                 01030000
           PERFORM 2000-PROCESS-INPUT                                   01040000
                UNTIL PS-EOF-SHNK-PRORATE-FILE.                         01050001
      *                                                                 01060000
           PERFORM 8000-EOJ-ROUTINE.                                    01070000
                                                                        01080000
           STOP RUN.                                                    01090000
                                                                        01100000
      ******************************************************************01110000
      * OPEN FILES, BUILD LOCATION TABLE, READ FIRST INPUT RECORD       01120000
      ******************************************************************01130000
      *                                                                 01140000
       1000-INITIALIZATION.                                             01150000
      *                                                                 01160000
           OPEN INPUT SHNK-PRORATE-FILE-IN.                             01170001
                                                                        01180000
           OPEN OUTPUT SHNK-PRORATE-FILE-OUT.                           01190001
                                                                        01200000
           PERFORM 1100-BUILD-LOC-TBL.                                  01210000
                                                                        01220000
           PERFORM 4000-READ-SHNK-PRORATE-FILE.                         01230001
      *                                                                 01240000
      ******************************************************************01250000
      * BUILD THE E-BUSINESS LOCATION INTERNAL TABLE                    01260000
      ******************************************************************01270000
       1100-BUILD-LOC-TBL.                                              01280000
                                                                        01290000
           MOVE 'N' TO PS-LOC-CSR-SW.                                   01300000
           PERFORM 8100-OPEN-LOC-CSR.                                   01310000
           PERFORM 8150-FETCH-LOC-CSR.                                  01320000
                                                                        01330000
           DISPLAY 'E-BUSINESS LOCATIONS'                               01331000
           PERFORM VARYING LOC-INDEX FROM 1 BY 1                        01340000
                             UNTIL LOC-INDEX > LOC-TBL-MAX              01350000
                                OR PS-END-OF-LOC-CSR                    01360000
             MOVE XMT00001-LOC-NBR      TO LOC-NBR(LOC-INDEX)           01370000
             DISPLAY LOC-NBR(LOC-INDEX)                                 01371000
             ADD 1 TO LOC-TBL-SIZE                                      01380000
             PERFORM 8150-FETCH-LOC-CSR                                 01390000
           END-PERFORM.                                                 01400000
                                                                        01410000
           IF LOC-INDEX >  LOC-TBL-MAX                                  01420000
             DISPLAY '*********** ABEND ************************'       01430000
             DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME             01440000
             DISPLAY '**  PARAGRAPH ** => ' PV-PARAGRAPH-NAME           01450000
             DISPLAY ' TABLE OVERFLOW ON LOCATION-TABLE         '       01460000
             DISPLAY ' INCREASE LOC-TBL-MAX LIMIT               '       01470000
             DISPLAY ' CURRENT LIMIT = ' LOC-TBL-MAX                    01480000
             DISPLAY '******************************************'       01490000
             DISPLAY '  '                                               01500000
             MOVE +4004 TO ABEND-CODE                                   01510000
             CALL 'ILBOABN0' USING ABEND-CODE                           01520000
           END-IF.                                                      01530000
                                                                        01540000
           PERFORM 8200-CLOSE-LOC-CSR.                                  01550000
                                                                        01560000
      ******************************************************************01570000
      * PROCESS THE INPUT AUDIT FILE                                    01580000
      ******************************************************************01590000
       2000-PROCESS-INPUT.                                              01600000
      *                                                                 01610000
           PERFORM 2100-SEARCH-LOC-TABLE.                               01620000
                                                                        01630000
           PERFORM 5000-WRITE-SHNK-PRORATE-FILE.                        01640001
                                                                        01650000
           PERFORM 4000-READ-SHNK-PRORATE-FILE.                         01660001
      *                                                                 01670000
      ******************************************************************01680000
      *  SEARCH THE E-BUSINESS LOCATION TABLE FOR LOC-NBR               01690000
      ******************************************************************01700000
       2100-SEARCH-LOC-TABLE.                                           01710000
           SET PS-LOCATION-FOUND-N            TO TRUE.                  01720000
           PERFORM VARYING PV-SUB FROM 1 BY 1                           01730000
               UNTIL PS-LOCATION-FOUND-Y                                01740000
                  OR PV-SUB > LOC-TBL-SIZE                              01750000
               IF LOC-NBR(PV-SUB) = ML080-LOC-NBR                       01760001
                   SET PS-LOCATION-FOUND-Y    TO TRUE                   01770000
               END-IF                                                   01780000
           END-PERFORM.                                                 01790000
           IF PS-LOCATION-FOUND-N                                       01800000
               MOVE ZEROES                    TO ML080-LOC-NBR          01810001
               ADD 1                          TO PA-UPDATE-COUNT        01820000
           END-IF.                                                      01830000
      *                                                                 01840000
      ******************************************************************01850000
      * READS THE SHRINK FILE INTO MLRD080 LAYOUT                       01860001
      ******************************************************************01870000
       4000-READ-SHNK-PRORATE-FILE.                                     01880001
           READ SHNK-PRORATE-FILE-IN INTO ML080-BRAND-VENDOR-SHNK       01890001
              AT END                                                    01900000
                MOVE 'Y' TO PS-EOF-SHNK-PRORATE-FILE-SW.                01910001
                                                                        01920000
           IF NOT PS-EOF-SHNK-PRORATE-FILE                              01921001
               ADD 1                    TO PA-INPUT-COUNT               01930000
           END-IF.                                                      01931000
      *                                                                 01940000
      ******************************************************************01950000
      * WRITES THE SHRINK FILE FROM THE MLRD080 LAYOUT                  01960001
      ******************************************************************01970000
       5000-WRITE-SHNK-PRORATE-FILE.                                    01980001
           WRITE SHNK-PRORATE-OUT-REC FROM ML080-BRAND-VENDOR-SHNK.     01990001
                                                                        02000000
           ADD 1                        TO PA-OUTPUT-COUNT.             02010000
      *                                                                 02020000
      ******************************************************************02030000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *02040000
      ******************************************************************02050000
       8000-EOJ-ROUTINE.                                                02060000
                                                                        02070000
           DISPLAY SPACES.                                              02080000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         02090000
           DISPLAY 'RECS UPDATED             ', PA-UPDATE-COUNT.        02100003
           DISPLAY 'OUTPUT RECORDS PROCESSED ', PA-OUTPUT-COUNT.        02110000
           DISPLAY SPACES.                                              02120000
      *                                                                 02130000
           CLOSE SHNK-PRORATE-FILE-IN                                   02140001
                 SHNK-PRORATE-FILE-OUT.                                 02150001
      *                                                                 02160000
      ******************************************************************02170000
      *  OPEN THE CURSOR TO RETRIEVE E-BUSINESS LOCATIONS              *02180000
      ******************************************************************02190000
       8100-OPEN-LOC-CSR.                                               02200000
                                                                        02210000
           EXEC SQL                                                     02220000
               OPEN LOC_CSR                                             02230000
           END-EXEC.                                                    02240000
                                                                        02250000
           EVALUATE TRUE                                                02260000
               WHEN SQLCODE = ZERO                                      02270000
                   CONTINUE                                             02280000
               WHEN OTHER                                               02290000
                   MOVE '8100-OPEN-LOC-CSR'   TO PV-PARAGRAPH-NAME      02300000
                   MOVE 'OPEN LOC_CSR'        TO PV-OPERATION-ATTEMPTED 02310000
                   PERFORM 9999-SQL-ABEND                               02320000
           END-EVALUATE.                                                02330000
                                                                        02340000
      ******************************************************************02350000
      *  FETCH THE E-BUSINESS LOCATIONS CURSOR                         *02360000
      ******************************************************************02370000
       8150-FETCH-LOC-CSR.                                              02380000
                                                                        02390000
           EXEC SQL                                                     02400000
               FETCH LOC_CSR                                            02410000
               INTO   :XMT00001-LOC-NBR                                 02420000
           END-EXEC.                                                    02430000
                                                                        02440000
           EVALUATE TRUE                                                02450000
               WHEN SQLCODE = ZERO                                      02460000
                   CONTINUE                                             02470000
               WHEN SQLCODE = +100                                      02480000
                   SET PS-END-OF-LOC-CSR TO TRUE                        02490000
               WHEN OTHER                                               02500000
                   MOVE '8150-FETCH-LOC-CSR'   TO PV-PARAGRAPH-NAME     02510000
                   MOVE 'FETCH LOC_CSR'        TO PV-OPERATION-ATTEMPTED02520000
                   PERFORM 9999-SQL-ABEND                               02530000
           END-EVALUATE.                                                02540000
                                                                        02550000
      ******************************************************************02560000
      *  CLOSE THE E-BUSINESS LOCATIONS CURSOR                         *02570000
      ******************************************************************02580000
       8200-CLOSE-LOC-CSR.                                              02590000
                                                                        02600000
           EXEC SQL                                                     02610000
               CLOSE LOC_CSR                                            02620000
           END-EXEC.                                                    02630000
                                                                        02640000
           EVALUATE TRUE                                                02650000
               WHEN SQLCODE = ZERO                                      02660000
                   CONTINUE                                             02670000
               WHEN OTHER                                               02680000
                   MOVE '8200-CLOSE-LOC-CSR'   TO PV-PARAGRAPH-NAME     02690000
                   MOVE 'CLOSE LOC_CSR'        TO PV-OPERATION-ATTEMPTED02700000
                   PERFORM 9999-SQL-ABEND                               02710000
           END-EVALUATE.                                                02720000
      *                                                                 02730000
      ******************************************************************02740000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               02750000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             02760000
      ******************************************************************02770000
       9999-SQL-ABEND.                                                  02780000
                                                                        02790000
           MOVE +4013                 TO ABEND-CODE.                    02800000
           DISPLAY '**  ABEND     **'.                                  02810000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              02820000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            02830000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       02840000
                                                                        02850000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         02860000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        02870000
                                                                        02880000
           COPY DPPD004.                                                02890000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           02900000
