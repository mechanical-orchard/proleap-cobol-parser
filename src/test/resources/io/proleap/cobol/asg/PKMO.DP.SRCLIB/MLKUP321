       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP321.                                         00030000
       AUTHOR.        SCOTT JACKSON.                                    00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  5-01-2000.                                        00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP321 - WEEKLY MAJOR CLASS STORE MERCHANDISE      *00100000
      *                      LEDGER TABLE UPDATE (TWKMCLS)             *00110000
      ******************************************************************00120000
      *     THIS PROGRAM WILL MAINTAIN THE WEEKLY MAJOR CLASS/STR DB2  *00130000
      * TABLE WITH THE CURRENT WEEK'S DATA.  THE TABLE IS READ USING   *00140000
      * THE TABLE KEY FROM EACH INPUT FILE RECORD.  IF THE ROW IS      *00150000
      * FOUND, THE ROW IS UPDATED WITH DATA FROM THE INPUT FILE RECORD.*00160000
      * IF THE ROW IS NOT FOUND, A NEW ROW IS INSERTED ON THE DB2      *00170000
      * TABLE (TWKMCLS). COMMITS ARE TAKEN EVERY TEN SECONDS, AND      *00180000
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                      *00190000
      *                                                                *00200000
      *    WR/PITS#     DATE        DESCRIPTION                        *00210000
      *    --------   --------     ---------------------------------   *00220000
      *    WR26603    03/18/04     INCREASED FIELD SIZES FOR STORE     *00230001
      *                            EXPANSION PROJECT.  CHANGED DB2     *00231001
      *                            WHERE CLAUSES FROM USING MLRD138    *00232001
      *                            FIELDS TO DCLGEN HOST VARIABLES.    *00233001
      ******************************************************************00240000
                                                                        00250000
                                                                        00260000
       ENVIRONMENT DIVISION.                                            00270000
                                                                        00280000
       CONFIGURATION SECTION.                                           00290000
       SOURCE-COMPUTER.        IBM-3090.                                00300000
       OBJECT-COMPUTER.        IBM-3090.                                00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
           SELECT UPDATE-FILE                                           00340000
               ASSIGN TO INP01.                                         00350000
                                                                        00360000
                                                                        00370000
       DATA DIVISION.                                                   00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  UPDATE-FILE                                                  00410000
           LABEL RECORDS ARE STANDARD                                   00420000
           RECORDING MODE IS F                                          00430000
           BLOCK CONTAINS 0 RECORDS.                                    00440000
       01  UPDATE-RECORD               PIC X(100).                      00450000
      *                                                                 00460000
                                                                        00470000
       WORKING-STORAGE SECTION.                                         00480000
                                                                        00490000
       01  FILLER                      PIC X(28)   VALUE                00500000
                'BEGINNING OF WORKING STORAGE'.                         00510000
                                                                        00520000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00530000
                                                                        00540000
       01  PC-PROGRAM-CONSTANTS.                                        00550000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK285'.    00560000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP321'.  00570000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00580000
                                                                        00590000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00600000
           05  PE-MSG-01                       PIC X(50) VALUE          00610000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00620000
           05  PE-MSG-02                       PIC X(50) VALUE          00630000
                'ATTEMPT TO UPDATE ROW ON TABLE TWKMCLS FAILED   '.     00640000
           05  PE-MSG-03                       PIC X(50) VALUE          00650000
                'ATTEMPT TO INSERT ROW ON TABLE TWKMCLS FAILED   '.     00660000
           05  PE-MSG-04                       PIC X(50) VALUE          00670000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00680000
           05  PE-MSG-05                       PIC X(50) VALUE          00690000
                'UPDATE TO TCKRSTINF FAILED                      '.     00700000
           05  PE-MSG-06                       PIC X(50) VALUE          00710000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00720000
           05  PE-MSG-07                       PIC X(50) VALUE          00730000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00740000
      *                                                                 00750000
       01  PS-PROGRAM-SWITCHES.                                         00760000
           05  PS-EOF-UPDATE-FILE-SW        PIC X(01)     VALUE 'N'.    00770000
               88  PS-EOF-UPDATE-FILE                     VALUE 'Y'.    00780000
           05  PS-WKMCLS-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00790000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00800000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00810000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00820000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00830000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00840000
                                                                        00850000
       01  PI-PROGRAM-INDICATORS.                                       00860000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00870000
               88  PI-UPDATE-WKMCLS-ROW                   VALUE 'U'.    00880000
               88  PI-INSERT-WKMCLS-ROW                   VALUE 'I'.    00890000
      *                                                                 00900000
       01  PROGRAM-VARIABLES.                                           00910000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00920000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00930000
      *                                                                 00940000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00950000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00960000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00970000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00980000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 00990000
      *                                                                 01000000
       01  PA-PROGRAM-ACCUMULATORS.                                     01010000
           05  PA-RECORD-COUNTS.                                        01020000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01030000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01040000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01050000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01060000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01070000
                                                                        01080000
       01  PD-PROGRAM-DISPLAYS.                                         01090000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01100000
                                                                        01110000
       01  SAVE-AREA.                                                   01120000
           05  SV-PRIMARY-KEY.                                          01130000
               10  SV-FSCL-WK-NBR        PIC X(02)  VALUE SPACES.       01140000
               10  SV-FSCL-WK-END-DTE    PIC X(10)  VALUE SPACES.       01150000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01160000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01170000
               10  SV-STR-NBR            PIC S9(04)     COMP   VALUE +0.01180000
           05  SV-UPDATE-FIELDS.                                        01190000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01200001
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01210001
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01220001
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01230001
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01240001
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250001
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01260001
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01270001
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01280001
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01290001
      *                                                                 01300000
      ******************************************************************01310000
      * MLRD138 - M/L WEEKLY MAJOR CLASS STORE DB UPDATE TRANS LAYOUT   01320000
      ******************************************************************01330000
           COPY MLRD138.                                                01340000
      *                                                                 01350000
      ******************************************************************01360000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01370000
      ******************************************************************01380000
           COPY DPWS004.                                                01390000
      *                                                                 01400000
      ******************************************************************01410000
      *  USED FOR DB2 ERRORS                                            01420000
      ******************************************************************01430000
           EXEC SQL                                                     01440000
               INCLUDE SQLCA                                            01450000
           END-EXEC.                                                    01460000
      *                                                                 01470000
      ******************************************************************01480000
      *  M/L WEEK-TO-DATE MAJOR CLASS TABLE                             01490000
      ******************************************************************01500000
           EXEC SQL                                                     01510000
               INCLUDE TWKMCLS                                          01520000
           END-EXEC.                                                    01530000
      *                                                                 01540000
      ***************************************************************** 01550000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01560000
      ***************************************************************** 01570000
           EXEC SQL                                                     01580000
               INCLUDE TCKRSTCN                                         01590000
           END-EXEC.                                                    01600000
      *                                                                 01610000
           EXEC SQL                                                     01620000
               INCLUDE TCKRSTIN                                         01630000
           END-EXEC.                                                    01640000
      *                                                                 01650000
      ******************************************************************01660000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01670000
      ******************************************************************01680000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01690000
W26603     05  CR-AREA-LEN                  PIC S9(04) VALUE +31  COMP. 01700001
           05  CR-CHECKPOINT-RESTART-VAR.                               01710000
W26603         10  CR-PREV-DTL-KEY          PIC  X(22) VALUE SPACES.    01720001
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01730000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01740000
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                 01750000
                   15  CR-DEPT-NBR          PIC  X(03).                 01760000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01770000
W26603             15  CR-STR-NBR           PIC  X(05).                 01780001
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01790000
      *                                                                 01800000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01810000
W26603     05  CK-SAVE-PREV-KEY         PIC  X(22) VALUE SPACES.        01820001
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01830000
               10  CK-FISCAL-NBR        PIC  X(02).                     01840000
               10  CK-FISCAL-WK-END-DTE PIC  X(10).                     01850000
               10  CK-DEPT-NBR          PIC  X(03).                     01860000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     01870000
W26603         10  CK-STR-NBR           PIC  X(05).                     01880001
                                                                        01890000
                                                                        01900000
      *-------------------*                                             01910000
       PROCEDURE DIVISION.                                              01920000
      *-------------------*                                             01930000
                                                                        01940000
       0000-MLKUP321.                                                   01950000
                                                                        01960000
           PERFORM 1000-INITIALIZATION.                                 01970000
      *                                                                 01980000
           PERFORM 2000-PROCESS-INPUT                                   01990000
                UNTIL PS-EOF-UPDATE-FILE.                               02000000
      *                                                                 02010000
           PERFORM 8000-EOJ-ROUTINE.                                    02020000
                                                                        02030000
           STOP RUN.                                                    02040000
                                                                        02050000
      ******************************************************************02060000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02070000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02080000
      ******************************************************************02090000
      *                                                                 02100000
       1000-INITIALIZATION.                                             02110000
      *                                                                 02120000
           OPEN INPUT UPDATE-FILE.                                      02130000
                                                                        02140000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02150000
                                                                        02160000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02170000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02180000
               PERFORM 7500-SELECT-RESTART-INFO                         02190000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02200000
                                            CR-CHECKPOINT-RESTART-VAR   02210000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02220000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02230000
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 02240000
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          02250000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02260000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02270000
               DISPLAY 'STORE NUMBER    ' CR-STR-NBR                    02280000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02290000
           ELSE                                                         02300000
               SET PS-FIRST-COMMIT TO TRUE                              02310000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02320000
           END-IF.                                                      02330000
      *                                                                 02340000
           PERFORM 4000-READ-UPDATE-FILE UNTIL                          02350000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02360000
            OR PS-EOF-UPDATE-FILE.                                      02370000
      *                                                                 02380000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02390000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02400000
                        PA-INPUT-COUNT                                  02410000
               DISPLAY 'FISCAL WEEK NBR       ' ML138-FSCL-WK-NBR       02420000
               DISPLAY 'FISCAL WEEK END DATE  ' ML138-FSCL-WK-END-DTE   02430000
               DISPLAY 'DEPARTMENT NBR        ' ML138-DEPT-NBR          02440000
               DISPLAY 'MAJOR CLASS NBR       ' ML138-MAJ-CL-NBR        02450000
               DISPLAY 'STORE NUMBER          ' ML138-STR-NBR           02460000
           END-IF.                                                      02470000
                                                                        02480000
           IF PS-EOF-UPDATE-FILE                                        02490000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02500000
                   CONTINUE                                             02510000
               ELSE                                                     02520000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02530000
           ELSE                                                         02540000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02550000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02560000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02570000
           END-IF.                                                      02580000
                                                                        02590000
      ******************************************************************02600000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02610000
      ******************************************************************02620000
       2000-PROCESS-INPUT.                                              02630000
      *                                                                 02640000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02650000
      *                                                                 02660000
           PERFORM 7000-SELECT-DB-ROW.                                  02670000
      *                                                                 02680000
           IF PS-PROGRAM-DEADLOCKED                                     02690000
               CONTINUE                                                 02700000
           ELSE                                                         02710000
               EVALUATE TRUE                                            02720000
                   WHEN PI-UPDATE-WKMCLS-ROW                            02730000
                       PERFORM 7100-UPDATE-WKMCLS-ROW                   02740000
                   WHEN PI-INSERT-WKMCLS-ROW                            02750000
                       PERFORM 7200-INSERT-WKMCLS-ROW                   02760000
                   WHEN OTHER                                           02770000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02780000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02790000
                       PERFORM 9999-SQL-ABEND                           02800000
               END-EVALUATE                                             02810000
           END-IF.                                                      02820000
      *                                                                 02830000
           IF PS-PROGRAM-DEADLOCKED                                     02840000
               CONTINUE                                                 02850000
           ELSE                                                         02860000
               PERFORM 7700-COMMIT-PROCESSING                           02870000
               PERFORM 4000-READ-UPDATE-FILE                            02880000
           END-IF.                                                      02890000
      *                                                                 02900000
      *                                                                 02910000
      ******************************************************************02920000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02930000
      ******************************************************************02940000
       2100-CALCULATE-UPDATES.                                          02950000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02960000
      *                                                                 02970000
           COMPUTE SV-SLS-RTL-AMT       =                               02980000
                   WKMCLS-SLS-RTL-AMT      + ML138-SLS-RTL-AMT.         02990000
           COMPUTE SV-MKDN-RTL-AMT      =                               03000000
                   WKMCLS-MKDN-RTL-AMT     + ML138-MKDN-RTL-AMT.        03010000
           COMPUTE SV-MKUP-RTL-AMT      =                               03020000
                   WKMCLS-MKUP-RTL-AMT     + ML138-MKUP-RTL-AMT.        03030000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03040000
                   WKMCLS-XFER-IN-RTL-AMT  + ML138-XFER-IN-RTL-AMT.     03050000
           COMPUTE SV-XFER-OUT-RTL      =                               03060000
                   WKMCLS-XFER-OUT-RTL-AMT + ML138-XFER-OUT-RTL-AMT.    03070000
           COMPUTE SV-RCPT-RTL-AMT      =                               03080000
                   WKMCLS-RCPT-RTL-AMT     + ML138-RCPT-RTL-AMT.        03090000
           COMPUTE SV-RCPT-NET-COST     =                               03100000
                   WKMCLS-RCPT-NET-COST-AMT + ML138-RCPT-NET-COST-AMT.  03110000
           COMPUTE SV-PADJ-RTL-AMT      =                               03120000
                   WKMCLS-PADJ-RTL-AMT     + ML138-PADJ-RTL-AMT.        03130000
           COMPUTE SV-PADJ-NET-COST     =                               03140000
                   WKMCLS-PADJ-NET-COST-AMT + ML138-PADJ-NET-COST-AMT.  03150000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03160000
                    WKMCLS-INV-ADJ-RTL-AMT + ML138-INV-ADJ-RTL-AMT.     03170000
      *                                                                 03180000
      ******************************************************************03190000
      * READS THE CONDENSED FILE INTO THE TWKMCLS LAYOUT                03200000
      ******************************************************************03210000
       4000-READ-UPDATE-FILE.                                           03220000
           READ UPDATE-FILE INTO ML138-UPDATE-REC                       03230000
              AT END                                                    03240000
                MOVE 'Y' TO PS-EOF-UPDATE-FILE-SW.                      03250000
     *                                                                  03260000
           IF PS-EOF-UPDATE-FILE                                        03270000
               MOVE ML138-FSCL-WK-NBR         TO CR-FISCAL-NBR          03280000
               MOVE ML138-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   03290000
               MOVE ML138-DEPT-NBR            TO CR-DEPT-NBR            03300000
               MOVE ML138-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03310000
               MOVE ML138-STR-NBR             TO CR-STR-NBR             03320000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03330000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03340000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03350000
                                             TCKRSTINF-WORK-STRG-DESC   03360000
               EXEC SQL                                                 03370000
                 UPDATE TCKRSTINF                                       03380000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03390000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03400000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03410000
               END-EXEC                                                 03420000
               IF SQLCODE = 0                                           03430000
                   CONTINUE                                             03440000
               ELSE                                                     03450000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03460000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03470000
                   PERFORM 9999-SQL-ABEND                               03480000
               END-IF                                                   03490000
            ELSE                                                        03500000
                ADD 1                        TO PA-INPUT-COUNT          03510000
            END-IF.                                                     03520000
      *                                                                 03530000
      ******************************************************************03540000
      * SELECT A ROW FROM THE MTHLY MCL STR TAB  THAT MATCHES INPUT KEY 03550000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03560000
      ******************************************************************03570000
       7000-SELECT-DB-ROW.                                              03580000
      *                                                                 03590000
W26603     MOVE ML138-FSCL-WK-NBR          TO WKMCLS-FSCL-WK-NBR.       03591001
W26603     MOVE ML138-FSCL-WK-END-DTE      TO WKMCLS-FSCL-WK-END-DTE.   03592001
W26603     MOVE ML138-DEPT-NBR             TO WKMCLS-DEPT-NBR.          03593001
W26603     MOVE ML138-MAJ-CL-NBR           TO WKMCLS-MAJ-CL-NBR.        03594001
W26603     MOVE ML138-STR-NBR              TO WKMCLS-STR-NBR.           03595001
           EXEC SQL                                                     03600000
               SELECT SLS_RTL_AMT                                       03610000
                     ,MKDN_RTL_AMT                                      03620000
                     ,MKUP_RTL_AMT                                      03630000
                     ,XFER_IN_RTL_AMT                                   03640000
                     ,XFER_OUT_RTL_AMT                                  03650000
                     ,RCPT_RTL_AMT                                      03660000
                     ,RCPT_NET_COST_AMT                                 03670000
                     ,PADJ_RTL_AMT                                      03680000
                     ,PADJ_NET_COST_AMT                                 03690000
                     ,INV_ADJ_RTL_AMT                                   03700000
                 INTO :WKMCLS-SLS-RTL-AMT                               03710000
                     ,:WKMCLS-MKDN-RTL-AMT                              03720000
                     ,:WKMCLS-MKUP-RTL-AMT                              03730000
                     ,:WKMCLS-XFER-IN-RTL-AMT                           03740000
                     ,:WKMCLS-XFER-OUT-RTL-AMT                          03750000
                     ,:WKMCLS-RCPT-RTL-AMT                              03760000
                     ,:WKMCLS-RCPT-NET-COST-AMT                         03770000
                     ,:WKMCLS-PADJ-RTL-AMT                              03780000
                     ,:WKMCLS-PADJ-NET-COST-AMT                         03790000
                     ,:WKMCLS-INV-ADJ-RTL-AMT                           03800000
               FROM TWKMCLS                                             03810000
W26603        WHERE   FSCL_WK_NBR         = :WKMCLS-FSCL-WK-NBR         03820001
W26603          AND   FSCL_WK_END_DTE     = :WKMCLS-FSCL-WK-END-DTE     03830001
W26603          AND   DEPT_NBR            = :WKMCLS-DEPT-NBR            03840001
W26603          AND   MAJ_CL_NBR          = :WKMCLS-MAJ-CL-NBR          03850001
W26603          AND   STR_NBR             = :WKMCLS-STR-NBR             03860001
W26603        WITH UR                                                   03861001
           END-EXEC.                                                    03870000
      *                                                                 03880000
           EVALUATE SQLCODE                                             03890000
               WHEN 0                                                   03900000
                   SET PI-UPDATE-WKMCLS-ROW       TO TRUE               03910000
               WHEN +100                                                03920000
                   SET PI-INSERT-WKMCLS-ROW       TO TRUE               03930000
               WHEN -911                                                03940000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            03950000
                   ADD +1             TO PV-DEADLOCK-COUNT              03960000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03970000
                       MOVE '7000-SELECT-DB-ROW' TO                     03980000
                                                    PV-PARAGRAPH-NAME   03990000
                       PERFORM 9000-DEADLOCK-ERROR                      04000000
                   ELSE                                                 04010000
                       PERFORM 7999-RESTART-PROCESS                     04020000
                   END-IF                                               04030000
               WHEN OTHER                                               04040000
                  DISPLAY ML138-FSCL-WK-NBR          ' '                04050000
                          ML138-FSCL-WK-END-DTE      ' '                04060000
                          ML138-DEPT-NBR             ' '                04070000
                          ML138-MAJ-CL-NBR           ' '                04080000
                          ML138-STR-NBR              ' '                04090000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04100000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04110000
                   PERFORM 9999-SQL-ABEND                               04120000
           END-EVALUATE.                                                04130000
      *                                                                 04140000
      *                                                                 04150000
      ***************************************************************** 04160000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04170000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04180000
      * FROM INPUT REC MLRD138                                          04190000
      ***************************************************************** 04200000
       7100-UPDATE-WKMCLS-ROW.                                          04210000
      *                                                                 04220000
           PERFORM 2100-CALCULATE-UPDATES.                              04230000
W26603     MOVE ML138-FSCL-WK-NBR          TO WKMCLS-FSCL-WK-NBR.       04231001
W26603     MOVE ML138-FSCL-WK-END-DTE      TO WKMCLS-FSCL-WK-END-DTE.   04232001
W26603     MOVE ML138-DEPT-NBR             TO WKMCLS-DEPT-NBR.          04233001
W26603     MOVE ML138-MAJ-CL-NBR           TO WKMCLS-MAJ-CL-NBR.        04234001
W26603     MOVE ML138-STR-NBR              TO WKMCLS-STR-NBR.           04235001
      *                                                                 04240000
           EXEC SQL                                                     04250000
               UPDATE TWKMCLS                                           04260000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04270000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          04280000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04290000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          04300000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            04310000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04320000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04330000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04340000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           04350000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04360000
W26603        WHERE   FSCL_WK_NBR          = :WKMCLS-FSCL-WK-NBR        04411001
W26603          AND   FSCL_WK_END_DTE      = :WKMCLS-FSCL-WK-END-DTE    04412001
W26603          AND   DEPT_NBR             = :WKMCLS-DEPT-NBR           04413001
W26603          AND   MAJ_CL_NBR           = :WKMCLS-MAJ-CL-NBR         04414001
W26603          AND   STR_NBR              = :WKMCLS-STR-NBR            04415001
           END-EXEC                                                     04420000
      *                                                                 04430000
           EVALUATE SQLCODE                                             04440000
               WHEN 0                                                   04450000
                   CONTINUE                                             04460000
               WHEN -911                                                04470000
                   ADD +1             TO PV-DEADLOCK-COUNT              04480000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04490000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04500000
                       MOVE '7100-UPDATE-WKMCLS-ROW' TO                 04510000
                                                    PV-PARAGRAPH-NAME   04520000
                       PERFORM 9000-DEADLOCK-ERROR                      04530000
                   ELSE                                                 04540000
                       PERFORM 7999-RESTART-PROCESS                     04550000
                   END-IF                                               04560000
               WHEN OTHER                                               04570000
                   MOVE '7100-UPDATE-WKMCLS-ROW' TO PV-PARAGRAPH-NAME   04580000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04590000
                   PERFORM 9999-SQL-ABEND                               04600000
           END-EVALUATE.                                                04610000
      *                                                                 04620000
           ADD 1 TO PA-UPDATE-COUNT.                                    04630000
      *                                                                 04640000
      ***************************************************************** 04650000
      * THERE WAS NO MATCHING ROW ON THE WEEKLY MCLS TABLE. INITIAL   * 04660000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04670000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN     * 04680000
      *   RECALC                                                      * 04690000
      ***************************************************************** 04700000
       7200-INSERT-WKMCLS-ROW.                                          04710000
      *                                                                 04720000
W26603     MOVE ML138-FSCL-WK-NBR          TO WKMCLS-FSCL-WK-NBR.       04722101
W26603     MOVE ML138-FSCL-WK-END-DTE      TO WKMCLS-FSCL-WK-END-DTE.   04722201
W26603     MOVE ML138-DEPT-NBR             TO WKMCLS-DEPT-NBR.          04722301
W26603     MOVE ML138-MAJ-CL-NBR           TO WKMCLS-MAJ-CL-NBR.        04722401
W26603     MOVE ML138-STR-NBR              TO WKMCLS-STR-NBR.           04722501
W26603     MOVE ML138-RCPT-RTL-AMT         TO WKMCLS-RCPT-RTL-AMT.      04726001
W26603     MOVE ML138-RCPT-NET-COST-AMT    TO WKMCLS-RCPT-NET-COST-AMT. 04727001
W26603     MOVE ML138-PADJ-RTL-AMT         TO WKMCLS-PADJ-RTL-AMT.      04728001
W26603     MOVE ML138-PADJ-NET-COST-AMT    TO WKMCLS-PADJ-NET-COST-AMT. 04729001
W26603     MOVE ML138-SLS-RTL-AMT          TO WKMCLS-SLS-RTL-AMT.       04729101
W26603     MOVE ML138-MKDN-RTL-AMT         TO WKMCLS-MKDN-RTL-AMT.      04729201
W26603     MOVE ML138-MKUP-RTL-AMT         TO WKMCLS-MKUP-RTL-AMT.      04729301
W26603     MOVE ML138-XFER-IN-RTL-AMT      TO WKMCLS-XFER-IN-RTL-AMT.   04729401
W26603     MOVE ML138-XFER-OUT-RTL-AMT     TO WKMCLS-XFER-OUT-RTL-AMT.  04729501
           MOVE ML138-INV-ADJ-RTL-AMT      TO WKMCLS-INV-ADJ-RTL-AMT.   04729601
      *                                                                 04730000
           EXEC SQL                                                     04740000
               INSERT INTO TWKMCLS                                      04750000
                   (   FSCL_WK_NBR                                      04760000
                     , FSCL_WK_END_DTE                                  04770000
                     , DEPT_NBR                                         04780000
                     , MAJ_CL_NBR                                       04790000
                     , STR_NBR                                          04800000
                     , RCPT_RTL_AMT                                     04810000
                     , RCPT_NET_COST_AMT                                04820000
                     , PADJ_RTL_AMT                                     04830000
                     , PADJ_NET_COST_AMT                                04840000
                     , SLS_RTL_AMT                                      04850000
                     , MKDN_RTL_AMT                                     04860000
                     , MKUP_RTL_AMT                                     04870000
                     , XFER_IN_RTL_AMT                                  04880000
                     , XFER_OUT_RTL_AMT                                 04890000
                     , INV_ADJ_RTL_AMT                                  04900000
                     , SHNK_RTL_AMT                                     04910000
                     , END_INV_RTL_AMT                                  04920000
                     , CUM_MKUP_PCT                                     04930000
                     , END_INV_COST_AMT                                 04940000
                     , SLS_COST_AMT                                     04950000
                     , GRO_MRGN_AMT      )                              04960000
               VALUES                                                   04970000
W26603             (   :WKMCLS-FSCL-WK-NBR                              04980001
W26603               , :WKMCLS-FSCL-WK-END-DTE                          04990001
W26603               , :WKMCLS-DEPT-NBR                                 05000001
W26603               , :WKMCLS-MAJ-CL-NBR                               05010001
W26603               , :WKMCLS-STR-NBR                                  05020001
W26603               , :WKMCLS-RCPT-RTL-AMT                             05030001
W26603               , :WKMCLS-RCPT-NET-COST-AMT                        05040001
W26603               , :WKMCLS-PADJ-RTL-AMT                             05050001
W26603               , :WKMCLS-PADJ-NET-COST-AMT                        05060001
W26603               , :WKMCLS-SLS-RTL-AMT                              05070001
W26603               , :WKMCLS-MKDN-RTL-AMT                             05080001
W26603               , :WKMCLS-MKUP-RTL-AMT                             05090001
W26603               , :WKMCLS-XFER-IN-RTL-AMT                          05100001
W26603               , :WKMCLS-XFER-OUT-RTL-AMT                         05110001
W26603               , :WKMCLS-INV-ADJ-RTL-AMT                          05120001
W26603               , 0.                                               05130001
W26603               , 0.                                               05140001
W26603               , 0.                                               05150001
W26603               , 0.                                               05160001
W26603               , 0.                                               05170001
W26603               , 0.                      )                        05180001
           END-EXEC                                                     05190000
      *                                                                 05200000
           EVALUATE SQLCODE                                             05210000
               WHEN 0                                                   05220000
                   CONTINUE                                             05230000
               WHEN -911                                                05240000
                   ADD +1             TO PV-DEADLOCK-COUNT              05250000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05260000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05270000
                       MOVE '7200-INSERT-WKMCLS-ROW' TO                 05280000
                                                    PV-PARAGRAPH-NAME   05290000
                       PERFORM 9000-DEADLOCK-ERROR                      05300000
                   ELSE                                                 05310000
                       PERFORM 7999-RESTART-PROCESS                     05320000
                   END-IF                                               05330000
               WHEN OTHER                                               05340000
                  DISPLAY ML138-FSCL-WK-NBR          ' '                05350000
                          ML138-FSCL-WK-END-DTE      ' '                05360000
                          ML138-DEPT-NBR             ' '                05370000
                          ML138-MAJ-CL-NBR           ' '                05380000
                          ML138-STR-NBR              ' '                05390000
                   MOVE '7200-INSERT-WKMCLS-ROW' TO PV-PARAGRAPH-NAME   05400000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05410000
                   PERFORM 9999-SQL-ABEND                               05420000
           END-EVALUATE.                                                05430000
                                                                        05440000
           ADD 1 TO PA-INSERT-COUNT.                                    05450000
                                                                        05460000
      ******************************************************************05470000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05480000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05490000
      *            CONTROL TABLE                                       *05500000
      ******************************************************************05510000
       7300-GET-COMMIT-TIMESTAMP.                                       05520000
                                                                        05530000
           EXEC SQL                                                     05540000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05550000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05560000
               INTO :PV-COMMIT-TIMESTAMP                                05570000
               FROM TCKRSTCNTL                                          05580000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05590000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05600000
           END-EXEC.                                                    05610000
                                                                        05620000
           EVALUATE TRUE                                                05630000
               WHEN SQLCODE = 0                                         05640000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05650000
                   CONTINUE                                             05660000
               WHEN SQLCODE NOT EQUAL ZERO                              05670000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05680000
                   PERFORM 9999-SQL-ABEND                               05690000
           END-EVALUATE.                                                05700000
      *                                                                 05710000
      *                                                                 05720000
      ******************************************************************05730000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05740000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05750000
      *            IF WE ARE IN A RESTART CONDITION.                   *05760000
      ******************************************************************05770000
       7400-INIT-CHECKPOINT-SELECT.                                     05780000
                                                                        05790000
           EXEC SQL                                                     05800000
             SELECT  CKPT_STAT_CODE                                     05810000
                    ,CKPT_FRQNCY_QTY                                    05820000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05830000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05840000
               FROM  TCKRSTCNTL                                         05850000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05860000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05870000
           END-EXEC.                                                    05880000
                                                                        05890000
           IF SQLCODE = 0                                               05900000
               CONTINUE                                                 05910000
           ELSE                                                         05920000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05930000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05940000
               PERFORM 9999-SQL-ABEND                                   05950000
           END-IF.                                                      05960000
      *                                                                 05970000
      *                                                                 05980000
      ******************************************************************05990000
      * 7500-SELECT-RESTART-INFO                                       *06000000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *06010000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *06020000
      ******************************************************************06030000
       7500-SELECT-RESTART-INFO.                                        06040000
                                                                        06050000
           EXEC SQL                                                     06060000
             SELECT  WORK_STRG_DESC                                     06070000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06080000
               FROM  TCKRSTINF                                          06090000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06100000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06110000
           END-EXEC.                                                    06120000
                                                                        06130000
           IF SQLCODE = 0                                               06140000
               CONTINUE                                                 06150000
           ELSE                                                         06160000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06170000
               PERFORM 9999-SQL-ABEND                                   06180000
           END-IF.                                                      06190000
                                                                        06200000
      ******************************************************************06210000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06220000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06230000
      ******************************************************************06240000
       7600-SET-CURRENT-TIMESTAMP.                                      06250000
                                                                        06260000
           EXEC SQL                                                     06270000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06280000
           END-EXEC.                                                    06290000
                                                                        06300000
           IF SQLCODE = 0                                               06310000
               CONTINUE                                                 06320000
           ELSE                                                         06330000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06340000
               PERFORM 9999-SQL-ABEND                                   06350000
           END-IF.                                                      06360000
      *                                                                 06370000
      *                                                                 06380000
      ******************************************************************06390000
      * 7700-COMMIT-PROCESSING.                                        *06400000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06410000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06420000
      ******************************************************************06430000
       7700-COMMIT-PROCESSING.                                          06440000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06450000
                                                                        06460000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06470000
                                                                        06480000
      * PREPARE COMMIT RECORD FOR UPDATE                                06490000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06500000
               MOVE ML138-FSCL-WK-NBR         TO CR-FISCAL-NBR          06510000
               MOVE ML138-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   06520000
               MOVE ML138-DEPT-NBR            TO CR-DEPT-NBR            06530000
               MOVE ML138-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06540000
               MOVE ML138-STR-NBR             TO CR-STR-NBR             06550000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06560000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06570000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06580000
                                             TCKRSTINF-WORK-STRG-DESC   06590000
               IF PS-FIRST-COMMIT                                       06600000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06610000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06620000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06630000
               END-IF                                                   06640000
               PERFORM 7800-COMMIT-CHANGES                              06650000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06660000
           END-IF.                                                      06670000
      *                                                                 06680000
      *                                                                 06690000
      ******************************************************************06700000
      * 7800-COMMIT-CHANGES.                                            06710000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06720000
      *            TAKE A COMMIT.                                       06730000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06740000
      ******************************************************************06750000
       7800-COMMIT-CHANGES.                                             06760000
                                                                        06770000
           EXEC SQL                                                     06780000
             UPDATE TCKRSTINF                                           06790000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06800000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06810000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06820000
           END-EXEC.                                                    06830000
                                                                        06840000
           IF SQLCODE = 0                                               06850000
               CONTINUE                                                 06860000
           ELSE                                                         06870000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06880000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06890000
               PERFORM 9999-SQL-ABEND                                   06900000
           END-IF.                                                      06910000
                                                                        06920000
           EXEC SQL                                                     06930000
               COMMIT                                                   06940000
           END-EXEC.                                                    06950000
                                                                        06960000
           IF SQLCODE = 0                                               06970000
               CONTINUE                                                 06980000
           ELSE                                                         06990000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED07000000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07010000
               PERFORM 9999-SQL-ABEND                                   07020000
           END-IF.                                                      07030000
      *                                                                 07040000
      ******************************************************************07050000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07060000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07070000
      ******************************************************************07080000
       7900-UPDATE-CHECKPOINT-STATUS.                                   07090000
                                                                        07100000
           EXEC SQL                                                     07110000
             UPDATE  TCKRSTCNTL                                         07120000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07130000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07140000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07150000
           END-EXEC.                                                    07160000
                                                                        07170000
           IF SQLCODE = 0                                               07180000
               CONTINUE                                                 07190000
           ELSE                                                         07200000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07210000
               PERFORM 9999-SQL-ABEND                                   07220000
           END-IF.                                                      07230000
                                                                        07240000
           EJECT                                                        07250000
      *                                                                 07260000
      *                                                                 07270000
      ******************************************************************07280000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKMCLS ROW FOR UPDATE07290000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07300000
      ******************************************************************07310000
       7999-RESTART-PROCESS.                                            07320000
      *                                                                 07330000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07340000
      *                                                                 07350000
           CLOSE UPDATE-FILE.                                           07360000
      *                                                                 07370000
           PERFORM 7500-SELECT-RESTART-INFO.                            07380000
      *                                                                 07390000
           DISPLAY '**** RESTART **** '.                                07400000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07410000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07420000
                                                                        07430000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07440000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07450000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07460000
           IF PS-FIRST-COMMIT                                           07470000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07480000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07490000
                      ' BEGINNING'                                      07500000
           ELSE                                                         07510000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07520000
                        TCKRSTINF-WORK-STRG-DESC (1:28)                 07530000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07540000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07550000
                    CR-CHECKPOINT-RESTART-AREA                          07560000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07570000
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 07580000
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          07590000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07600000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 07610000
               DISPLAY 'STORE NUMBER    ' CR-STR-NBR                    07620000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07630000
           END-IF.                                                      07640000
                                                                        07650000
           OPEN INPUT UPDATE-FILE.                                      07660000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07670000
      *                                                                 07680000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07690000
           PERFORM 4000-READ-UPDATE-FILE                                07700000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07710000
                 OR PS-EOF-UPDATE-FILE.                                 07720000
           IF PS-EOF-UPDATE-FILE                                        07730000
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07740000
           ELSE                                                         07750000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07760000
                        PA-INPUT-COUNT                                  07770000
               DISPLAY 'FISCAL WEEK NBR       ' ML138-FSCL-WK-NBR       07780000
               DISPLAY 'FISCAL WEEK END DATE  ' ML138-FSCL-WK-END-DTE   07790000
               DISPLAY 'DEPARTMENT NBR        ' ML138-DEPT-NBR          07800000
               DISPLAY 'MAJOR CLASS NBR       ' ML138-MAJ-CL-NBR        07810000
               DISPLAY 'STORE NUMBER          ' ML138-STR-NBR           07820000
           END-IF.                                                      07830000
      *                                                                 07840000
      ******************************************************************07850000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07860000
      ******************************************************************07870000
       8000-EOJ-ROUTINE.                                                07880000
      *                                                                 07890000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07900000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07910000
                                                                        07920000
      *                                                                 07930000
           DISPLAY SPACES.                                              07940000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07950000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07960000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07970000
           DISPLAY SPACES.                                              07980000
      *                                                                 07990000
           CLOSE UPDATE-FILE.                                           08000000
      *                                                                 08010000
      *                                                                 08020000
      ******************************************************************08030000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       08040000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   08050000
      ******************************************************************08060000
       9000-DEADLOCK-ERROR.                                             08070000
      *                                                                 08080000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08090000
               PERFORM 9999-SQL-ABEND.                                  08100000
      *                                                                 08110000
      ******************************************************************08120000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08130000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08140000
      ******************************************************************08150000
       9999-SQL-ABEND.                                                  08160000
                                                                        08170000
           MOVE +4013                 TO ABEND-CODE.                    08180000
           DISPLAY '**  ABEND     **'.                                  08190000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08200000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08210000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08220000
                                                                        08230000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08240000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08250000
                                                                        08260000
           COPY DPPD004.                                                08270000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08280000
