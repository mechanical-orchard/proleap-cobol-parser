      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT028.                                                
       AUTHOR.         MARIBETH ZAMORA                                          
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   10/01/08.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM VENDOR STYLE ASSOCIATION DELTA PROGRAM                   *        
      *  THIS PROGRAM CREATES A VENDOR STYLE ASSOCIATION DELTA FILE    *        
      *  BY COMPARING THE PREVIOUS AND CURRENT GENERATIONS OF THE      *        
      *  VENDOR STYLE ASSOCIATION FILES                                *        
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT VSA-PREVIOUS-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VSA-CURRENT-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT VSA-CHANGE-FILE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  VSA-PREVIOUS-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VSA-PREVIOUS-RECORD                 PIC X(09).                       
                                                                                
       FD  VSA-CURRENT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VSA-CURRENT-RECORD                  PIC X(09).                       
                                                                                
       FD  VSA-CHANGE-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VSA-CHANGE-RECORD                   PIC X(020).                      
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURRENT-REC.                                                      
           05  WS-CURR-KEY-9                  PIC   9(14).                      
           05  WS-CURR-KEY-X  REDEFINES                                         
               WS-CURR-KEY-9                  PIC   X(14).                      
           05  WS-CURR-DATA                   PIC   9(5).                       
                                                                                
       01  WS-PREVIOUS-REC.                                                     
           05  WS-PREV-KEY-9                  PIC   9(14).                      
           05  WS-PREV-KEY-X  REDEFINES                                         
               WS-PREV-KEY-9                  PIC   X(14).                      
           05  WS-PREV-DATA                   PIC   X(5).                       
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT028'.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
      ***************************************************************           
      *   TEMP STORAGE AREA                                                     
      *   LENGTH = 105 BYTES                                                    
      ***************************************************************           
       01  WS-TEMP-REC.                                                         
           05  WS-TMP-RMS-STYL-ID            PIC   9(9).                        
           05  WS-TMP-CHARTC-CDE             PIC   9(5).                        
           05  WS-TMP-CHARTC-VAL-CDE         PIC   9(5).                        
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT                                                       
      ******************************************************************        
           COPY CWRD026.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW VENDOR STYLE ASSOCIATION FILE                 
      ******************************************************************        
           COPY CWRD028.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  MAINLINE                                                               
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL WS-CURR-KEY-X = HIGH-VALUES                                
                     AND                                                        
                     WS-PREV-KEY-X = HIGH-VALUES.                               
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  VSA-CURRENT-FILE                                         
                       VSA-PREVIOUS-FILE                                        
                OUTPUT VSA-CHANGE-FILE.                                         
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CW028-RECORD                                              
                                                                                
           IF WS-CURR-KEY-X  =  WS-PREV-KEY-X                                   
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
           ELSE                                                                 
               IF WS-CURR-KEY-X  <  WS-PREV-KEY-X                               
                   PERFORM 2200-INSERT-CURRENT                                  
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF WS-CURR-DATA  =  WS-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE WS-CURR-KEY-9     TO CW028-KEY                              
               MOVE WS-CURR-DATA      TO CW028-DATA                             
               MOVE 'U'               TO CW028-CHANGE-IND                       
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE-OUTPUT                                        
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT CURRENT DATA                                                    
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE WS-CURR-KEY-9 TO CW028-KEY                                      
           MOVE WS-CURR-DATA  TO CW028-DATA                                     
           MOVE 'I'           TO CW028-CHANGE-IND                               
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE-OUTPUT                                            
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE PREVIOUS DATA                                                   
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE WS-PREV-KEY-9 TO CW028-KEY                                      
           MOVE WS-PREV-DATA  TO CW028-DATA                                     
           MOVE 'D'           TO CW028-CHANGE-IND                               
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE-OUTPUT                                            
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE       WS-TEMP-REC.                                        
                                                                                
           MOVE CW026-RMS-STYL-ID       TO  WS-TMP-RMS-STYL-ID                  
           MOVE CW026-VS-CHARTC-CDE     TO  WS-TMP-CHARTC-CDE                   
           MOVE CW026-VS-CHARTC-VAL-CDE TO  WS-TMP-CHARTC-VAL-CDE.              
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE-OUTPUT.                                                       
                                                                                
           WRITE  VSA-CHANGE-RECORD FROM CW028-RECORD.                          
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ VSA-CURRENT-FILE INTO CW026-RECORD                              
               AT END                                                           
                   MOVE HIGH-VALUES  TO  WS-CURR-KEY-X                          
               NOT AT END                                                       
                   ADD 1             TO  PA-INPUT-RECORDS                       
                   ADD 1             TO  PA-INPUT-CUR-RECORDS                   
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   MOVE WS-TEMP-REC  TO  WS-CURRENT-REC                         
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ VSA-PREVIOUS-FILE INTO CW026-RECORD                             
               AT END                                                           
                   MOVE HIGH-VALUES  TO  WS-PREV-KEY-X                          
               NOT AT END                                                       
                   ADD 1             TO  PA-INPUT-RECORDS                       
                   ADD 1             TO  PA-INPUT-PRE-RECORDS                   
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   MOVE WS-TEMP-REC  TO  WS-PREVIOUS-REC                        
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE VSA-CURRENT-FILE                                               
                 VSA-PREVIOUS-FILE                                              
                 VSA-CHANGE-FILE.                                               
                                                                                
      ******************************************************************        
      ******************************************************************        
