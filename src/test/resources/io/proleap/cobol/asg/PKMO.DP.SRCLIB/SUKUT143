      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
       PROGRAM-ID.             SUKUT143.                                00040000
       AUTHOR.                 V.SUDHAN.                                00050000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060000
       DATE-WRITTEN.           SEPTEMBER, 2009.                         00070000
       DATE-COMPILED.                                                   00080000
      ***************************************************************** 00090000
      *                                                               * 00100000
      *  THIS PROGRAM WILL CREATE AN EXTRACT FILE CONTAINING THE TOTAL* 00110000
      *  NUMBER OF CARTONS PROCESSED THRU PRINT AND APPLY (WHETHER    * 00120000
      *  THEY WERE WITHIN THE SLA OR NOT), AS WELL AS THOSE OCCURRENCE* 00130000
      *  WHERE THE SLA HAS NOT BEEN MET.                              * 00140000
      *                                                               * 00150000
      ***************************************************************** 00160000
       ENVIRONMENT DIVISION.                                            00170000
      ***************************************************************** 00180000
      *                                                                 00190000
       CONFIGURATION SECTION.                                           00200000
      *                                                                 00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
      *                                                                 00240000
       INPUT-OUTPUT SECTION.                                            00250000
      *                                                                 00260000
       FILE-CONTROL.                                                    00270000
      *                                                                 00280000
           SELECT SU-PNA-SLA-EXTRACT-FILE                               00290000
                  ASSIGN TO OUT01.                                      00300000
      *                                                                 00310000
      ***************************************************************** 00320000
       DATA DIVISION.                                                   00330000
      ***************************************************************** 00340000
      *                                                                 00350000
       FILE SECTION.                                                    00360000
      *                                                                 00370000
       FD  SU-PNA-SLA-EXTRACT-FILE                                      00380000
           RECORDING MODE IS F                                          00390000
           BLOCK CONTAINS 0  RECORDS.                                   00400000
       01  SU-PNA-SLA-EXTRACT-RECORD       PIC  X(1050).                00410000
      *                                                                 00420000
                                                                        00430000
       WORKING-STORAGE SECTION.                                         00440000
      *                                                                 00450000
       01  ABEND-CODE                      PIC S9(04)    VALUE +0       00460000
                                                         COMP SYNC.     00470000
      *                                                                 00480000
       01  PROGRAM-SWITCHES.                                            00490000
           05  PS-EOF-PNA-CURSOR-SW        PIC  X(01)    VALUE 'N'.     00500000
               88  PS-EOF-PNA-CSR                        VALUE 'Y'.     00510000
               88  PS-NOT-EOF-PNA-CSR                    VALUE 'N'.     00520000
           05  PS-EOF-PNA-SLA-CURSOR-SW    PIC  X(01)    VALUE 'N'.     00530000
               88  PS-EOF-PNA-SLA-CSR                    VALUE 'Y'.     00540000
               88  PS-NOT-EOF-PNA-SLA-CSR                VALUE 'N'.     00550000
      *                                                                 00560000
       01  PC-PROGRAM-CONSTANTS.                                        00570000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE          00580000
                                                         'SUKUT143'.    00590000
      *                                                                 00600000
       01  PV-PROGRAM-VARIABLES.                                        00610000
           05  PV-START-TIME-STAMP         PIC  X(26).                  00620000
           05  PV-END-TIME-STAMP           PIC  X(26).                  00630000
           05  PV-PROCESS-DATE-START-TMST.                              00640000
               10 PV-PROCESS-DATE-START    PIC  X(10).                  00650000
               10 PV-PROCESS-DATE-STAT-BAL PIC  X(16) VALUE             00660000
                                           '-00.00.00.000001'.          00670000
           05  PV-PROCESS-DATE-END-TMST.                                00680000
               10 PV-PROCESS-DATE-END      PIC  X(10).                  00690000
               10 PV-PROCESS-DATE-END-BAL  PIC  X(16) VALUE             00700000
                                           '-23.59.59.999999'.          00710000
           05  PV-PROCESS-DATE             PIC  X(10).                  00720000
           05  PV-PROC-DATE.                                            00730000
               10 PV-PROC-DATE-CCYY        PIC  X(04).                  00740000
               10 PV-PROC-DATE-MM          PIC  X(02).                  00750000
               10 PV-PROC-DATE-DD          PIC  X(02).                  00760000
           05  PV-FISCAL-YEAR              PIC  X(04).                  00770000
           05  PV-FISCAL-MONTH             PIC  X(02).                  00780000
           05  PV-FISCAL-WEEK              PIC  X(02).                  00790000
                                                                        00800000
           05  PV-ABEND-PARAGRAPH          PIC  X(30)  VALUE SPACES.    00810000
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.    00820000
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)  VALUE SPACES.    00830000
                                                                        00840000
           05  PV-PNA-FETCH-CNT            PIC  9(09)  VALUE ZERO.      00850000
           05  PV-PNA-SLA-FETCH-CNT        PIC  9(09)  VALUE ZERO.      00860000
           05  PV-TOTAL-CNT                PIC  9(09)  VALUE ZERO.      00870000
           05  PV-TOT-COUNT                PIC S9(09)  COMP VALUE ZERO. 00880001
           05  PV-GT-TOT-COUNT             PIC S9(09)  COMP VALUE ZERO. 00890001
           05  PV-TABLE-MAX                PIC 9(5)    VALUE 1000.      00900000
      *                                                                 00910000
       01  PV-TABLE-VARIABLES.                                          00920000
           05  PV-PNA-SLA-TABLE.                                        00930000
               10  PV-TABLE-SIZE           PIC  9(5).                   00940000
               10  PV-PNA-SLA-DTL          OCCURS 1000 TIMES            00950000
                                           DEPENDING ON PV-TABLE-SIZE   00960000
                                           INDEXED BY PV-PNA-SLA-IDX.   00970000
                   15  PV-DC-NBR           PIC  9(04).                  00980000
                   15  PV-STA-NBR          PIC  9(04).                  00990000
                   15  PV-COUNT-OF-TOT-SCANS                            01000000
                                           PIC S9(09)  COMP.            01010000
                   15  PV-COUNT-OF-GT-SLA-SCANS                         01020000
                                           PIC S9(09)  COMP.            01030000
       01  VENDOR-EXTRACT-FILE.                                         01040000
      *                                                                 01050000
           COPY SUWS103.                                                01060000
      *                                                                 01070000
      ******************************************************************01080000
      *  DB2 FORMATTED MESSAGE AREA                                     01090000
      ******************************************************************01100000
           COPY DPWS004.                                                01110000
      ******************************************************************01120000
      *  DB2 COMMUNICATION AREA                                         01130000
      ******************************************************************01140000
           EXEC SQL                                                     01150000
               INCLUDE SQLCA                                            01160000
           END-EXEC.                                                    01170000
      ******************************************************************01180000
      *  DB2 COMMUNICATION AREA2                                        01190000
      ******************************************************************01200000
           COPY SQLCA2.                                                 01210000
      ******************************************************************01220000
      *  COPY BOOK FOR THE COMPANY CONTROL TABLE                        01230000
      ******************************************************************01240000
           EXEC SQL                                                     01250000
                INCLUDE TCOCNTL                                         01260000
           END-EXEC.                                                    01270000
      ******************************************************************01280000
      *  COPY BOOK FOR THE ATTRIBUTE TABLE                              01290000
      ******************************************************************01300000
           EXEC SQL                                                     01310000
                INCLUDE TDTATTR                                         01320000
           END-EXEC.                                                    01330000
      ******************************************************************01340000
      *  COPY BOOK FOR THE LABEL REQUEST MESSAGE DB2 TABLE              01350000
      ******************************************************************01360000
           EXEC SQL                                                     01370000
                INCLUDE SUT00043                                        01380000
           END-EXEC.                                                    01390000
      *                                                                 01400000
           EXEC SQL                                                     01410000
             DECLARE PNA-CSR CURSOR FOR                                 01420000
               SELECT DC_NBR                                            01430000
                     ,ACM_LBL_STA_NBR                                   01440000
                     ,COUNT(DC_NBR)                                     01450000
                FROM  SUT_LBL_REQ_MSG                                   01460000
                WHERE ACM_MSG_RCV_TMST >= :PV-START-TIME-STAMP          01470000
                  AND ACM_MSG_RCV_TMST <= :PV-END-TIME-STAMP            01480000
                GROUP BY DC_NBR                                         01490000
                         ,ACM_LBL_STA_NBR                               01500000
                ORDER BY 1,2                                            01510000
           END-EXEC.                                                    01520000
      *                                                                 01530000
           EXEC SQL                                                     01540000
             DECLARE PNA-SLA-CSR CURSOR FOR                             01550000
               SELECT DC_NBR                                            01560000
                     ,ACM_LBL_STA_NBR                                   01570000
                     ,COUNT(DC_NBR)                                     01580000
                FROM  SUT_LBL_REQ_MSG                                   01590000
                WHERE ACM_MSG_RCV_TMST >= :PV-START-TIME-STAMP          01600000
                  AND ACM_MSG_RCV_TMST <= :PV-END-TIME-STAMP            01610000
                  AND (ACM_MSG_SNT_TMST - ACM_MSG_RCV_TMST) > 1.9       01620000
                GROUP BY DC_NBR                                         01630000
                         ,ACM_LBL_STA_NBR                               01640000
                ORDER BY 1,2                                            01650000
           END-EXEC.                                                    01660000
      *                                                                 01670000
       PROCEDURE DIVISION.                                              01680000
      *                                                                 01690000
      ******************************************************************01700000
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **01710000
      ******************************************************************01720000
      *                                                                 01730000
       0000-MAINLINE-PROCESSING.                                        01740000
      *                                                                 01750000
           PERFORM 1000-INITIALIZATION.                                 01760000
      *                                                                 01770000
           PERFORM 2000-PROCESS-PNA.                                    01780000
      *                                                                 01790000
           DISPLAY '                                        '.          01791000
           DISPLAY '** SUKUT143-TOTAL NO OF SCANS           : '         01800000
                   PV-PNA-FETCH-CNT.                                    01801000
           DISPLAY '** SUKUT143-TOTAL NO OF SCANS > SLA     : '         01810000
                   PV-PNA-SLA-FETCH-CNT.                                01811000
           DISPLAY '** SUKUT143-TOTAL OUTPUT RECS WRITTEN   : '         01830000
                   PV-TOTAL-CNT.                                        01831000
           DISPLAY '                                        '.          01840000
           DISPLAY '** PROGRAM SUKUT143 EXECUTION ENDED   **'.          01841000
      *                                                                 01850000
           CLOSE SU-PNA-SLA-EXTRACT-FILE.                               01860000
      *                                                                 01870000
           GOBACK.                                                      01880000
      *                                                                 01890000
      ******************************************************************01900000
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **01910000
      ** 01.FETCHES THE PROCESSING DATE FROM TCOCNTL &  TDTATTR TABLES**01920000
      ** 02.FETCHES THE TOTAL NUMBER OF SCANS FROM SUT_LBL_REQ_MSG    **01930000
      ** 03.FETCHES THE NO OF SCANS WHICH DID NOT MEET THE SLA FOR P&A**01940000
      ******************************************************************01950000
      *                                                                 01960000
       1000-INITIALIZATION.                                             01970000
      *                                                                 01980000
           DISPLAY '** PROGRAM SUKUT143 EXECUTION STARTED **'.          01990000
      *                                                                 02000000
           OPEN OUTPUT SU-PNA-SLA-EXTRACT-FILE.                         02010000
      *                                                                 02020000
           PERFORM 1100-GET-DATE-PARMS.                                 02030000
           DISPLAY '** PROCESSING DATE : ' PV-PROCESS-DATE.             02040000
      *                                                                 02050000
           MOVE PV-PROCESS-DATE(1:4) TO PV-PROC-DATE-CCYY               02060000
           MOVE PV-PROCESS-DATE(6:2) TO PV-PROC-DATE-MM                 02070000
           MOVE PV-PROCESS-DATE(9:2) TO PV-PROC-DATE-DD                 02080000
           SET  PS-NOT-EOF-PNA-CSR   TO TRUE.                           02090000
      *                                                                 02100000
           MOVE ZEROES TO PV-PNA-SLA-TABLE                              02110000
           MOVE ZEROES TO PV-TABLE-SIZE                                 02120000
      *                                                                 02130000
           PERFORM 1200-OPEN-PNA-CSR.                                   02140000
           PERFORM 1300-FETCH-PNA-CSR VARYING PV-PNA-SLA-IDX FROM 1 BY 102150000
                   UNTIL PV-PNA-SLA-IDX > PV-TABLE-MAX  OR              02160000
                         PS-EOF-PNA-CSR.                                02170000
           IF PV-PNA-SLA-IDX > PV-TABLE-MAX                             02180000
             MOVE '1000-INITIALIZATION'                                 02180100
                                     TO PV-PARAGRAPH-NAME               02180200
             DISPLAY '  '                                               02181000
             DISPLAY '*********** ABEND ************************'       02190000
             DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME       02191000
             DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME             02192000
             DISPLAY '** TABLE OVERFLOW ON PV-PNA-SLA-DTL       '       02200000
             DISPLAY '** INCREASE PV-PNA-SLA-DTL MAX LIMIT      '       02210000
             DISPLAY '** CURRENT LIMIT IS ** ' PV-TABLE-MAX             02220000
             DISPLAY '******************************************'       02230000
             DISPLAY '  '                                               02240000
             MOVE +4004 TO ABEND-CODE                                   02250000
             CALL 'ILBOABN0' USING ABEND-CODE                           02251000
           END-IF.                                                      02270000
           PERFORM 1400-CLOSE-PNA-CSR.                                  02280000
      *                                                                 02290000
           PERFORM 1500-OPEN-PNA-SLA-CSR.                               02300000
           SET PS-NOT-EOF-PNA-SLA-CSR TO TRUE.                          02310000
           PERFORM 1600-FETCH-PNA-SLA-CSR UNTIL                         02320000
                   PS-EOF-PNA-SLA-CSR.                                  02330000
           PERFORM 1700-CLOSE-PNA-SLA-CSR.                              02340000
      *                                                                 02350000
      ******************************************************************02360000
      **  THIS PARAGRAPH FETCHES THE PROCESSING DATE FROM THE TCOCNTL **02370000
      **  TDTATTR DB2 TABLES. THIS WILL RETURN ONE ROW FROM THE QUERY.**02380000
      ******************************************************************02390000
      *                                                                 02400000
       1100-GET-DATE-PARMS.                                             02410000
      *                                                                 02420000
            EXEC SQL                                                    02430000
                 SELECT   A.BTCH_PRC_DTE                                02440000
                         ,B.KY_DTE                                      02450000
                         ,B.FSCL_YR_NBR                                 02460000
                         ,B.FSCL_MN_NBR                                 02470000
                         ,B.FSCL_WK_NBR                                 02480000
                    INTO  :COCNTL-BTCH-PRC-DTE                          02490000
                         ,:DTATTR-KY-DTE                                02500000
                         ,:DTATTR-FSCL-YR-NBR                           02510000
                         ,:DTATTR-FSCL-MN-NBR                           02520000
                         ,:DTATTR-FSCL-WK-NBR                           02530000
                  FROM    TCOCNTL A                                     02540000
                         ,TDTATTR B                                     02550000
                  WHERE A.OPER_UNT_ID = 90                              02560000
                    AND A.BTCH_PRC_DTE = B.KY_DTE                       02570000
            END-EXEC.                                                   02580000
      *                                                                 02590000
           EVALUATE TRUE                                                02600000
               WHEN SQLCODE = ZEROS                                     02610000
                    MOVE COCNTL-BTCH-PRC-DTE  TO  PV-PROCESS-DATE       02620000
                                                  PV-PROCESS-DATE-START 02630000
                                                  PV-PROCESS-DATE-END   02640000
                    MOVE DTATTR-FSCL-YR-NBR   TO  PV-FISCAL-YEAR        02650000
                    MOVE DTATTR-FSCL-MN-NBR   TO  PV-FISCAL-MONTH       02660000
                    MOVE DTATTR-FSCL-WK-NBR   TO  PV-FISCAL-WEEK        02670000
                    MOVE PV-PROCESS-DATE-START-TMST                     02680000
                                              TO  PV-START-TIME-STAMP   02690000
                    MOVE PV-PROCESS-DATE-END-TMST                       02700000
                                              TO  PV-END-TIME-STAMP     02710000
               WHEN SQLCODE = +100                                      02720000
               WHEN SQLWARN0 NOT = SPACES                               02730000
               WHEN SQLCODE < ZERO                                      02740000
               WHEN SQLCODE > ZERO                                      02750000
                   MOVE '1100-GET-DATE-PARMS'                           02760000
                                   TO PV-PARAGRAPH-NAME                 02770000
                   MOVE 'GET DATE FROM TCOCNTL & TDTATTR'               02780000
                                   TO PV-DB2-OPERATION-ATTEMPTED        02790000
                   PERFORM 9100-SQL-ABEND                               02800000
           END-EVALUATE.                                                02810000
      *                                                                 02820000
      ******************************************************************02830000
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL COLLECT THE NO   **02840000
      **  OF SCANS DONE AT EACH P & A LANE IN EVERY DC                **02850000
      ******************************************************************02860000
      *                                                                 02870000
       1200-OPEN-PNA-CSR.                                               02880000
      *                                                                 02890000
           EXEC SQL                                                     02900000
                OPEN PNA-CSR                                            02910000
           END-EXEC.                                                    02920000
      *                                                                 02930000
           EVALUATE TRUE                                                02940000
               WHEN SQLCODE = 0                                         02950000
                   CONTINUE                                             02960000
               WHEN SQLWARN0 NOT = SPACES                               02970000
               WHEN SQLCODE < ZERO                                      02980000
               WHEN SQLCODE > ZERO                                      02990000
                   MOVE '1200-OPEN-PNA-CSR'                             03000000
                                   TO PV-PARAGRAPH-NAME                 03010000
                   MOVE 'OPEN THE PNA_CURSOR            '               03020000
                                   TO PV-DB2-OPERATION-ATTEMPTED        03030000
                   PERFORM 9100-SQL-ABEND                               03040000
           END-EVALUATE.                                                03050000
      *                                                                 03060000
      ******************************************************************03070000
      **  THIS PARAGRAPH FETCH THE RECORDS WHICH WILL COLLECT THE NO  **03080000
      **  OF SCANS DONE AT EACH P & A LANE IN EVERY DC                **03090000
      ******************************************************************03100000
      *                                                                 03110000
       1300-FETCH-PNA-CSR.                                              03120000
      *                                                                 03130000
           EXEC SQL                                                     03140000
                FETCH PNA-CSR                                           03150000
                INTO  :SUT00043-DC-NBR                                  03160000
                    , :SUT00043-ACM-LBL-STA-NBR                         03170000
                    , :PV-TOT-COUNT                                     03180000
           END-EXEC.                                                    03190000
      *                                                                 03200000
           EVALUATE TRUE                                                03210000
             WHEN SQLCODE = ZEROS                                       03220000
                  ADD  1  TO  PV-PNA-FETCH-CNT                          03230000
                              PV-TABLE-SIZE                             03240000
                  INITIALIZE PV-COUNT-OF-GT-SLA-SCANS(PV-PNA-SLA-IDX)   03241000
                  MOVE SUT00043-DC-NBR  TO PV-DC-NBR(PV-PNA-SLA-IDX)    03250000
                  MOVE SUT00043-ACM-LBL-STA-NBR                         03260000
                                        TO PV-STA-NBR(PV-PNA-SLA-IDX)   03270000
                  MOVE PV-TOT-COUNT     TO                              03280000
                       PV-COUNT-OF-TOT-SCANS(PV-PNA-SLA-IDX)            03290000
             WHEN SQLCODE = +100                                        03300000
                  SET PS-EOF-PNA-CSR  TO TRUE                           03310000
             WHEN SQLWARN0 NOT = SPACES                                 03320000
             WHEN SQLCODE < ZERO                                        03330000
             WHEN SQLCODE > ZERO                                        03340000
                 MOVE '1300-FETCH-PNA-CSR'                              03350000
                                     TO PV-PARAGRAPH-NAME               03360000
                 MOVE 'FETCH PNA CURSOR '                               03370000
                                     TO PV-DB2-OPERATION-ATTEMPTED      03380000
                 PERFORM 9100-SQL-ABEND                                 03390000
           END-EVALUATE.                                                03400000
      *                                                                 03410000
      ******************************************************************03420000
      **  THIS PARAGRAPH CLOSES THE CURSOR WHICH WILL COLLECT THE NO  **03430000
      **  OF SCANS DONE AT EACH P & A LANE IN EVERY DC                **03440000
      ******************************************************************03450000
      *                                                                 03460000
       1400-CLOSE-PNA-CSR.                                              03470000
      *                                                                 03480000
           EXEC SQL                                                     03490000
               CLOSE PNA-CSR                                            03500000
           END-EXEC.                                                    03510000
      *                                                                 03520000
           EVALUATE TRUE                                                03530000
               WHEN SQLCODE = ZERO                                      03540000
               WHEN SQLCODE = -904                                      03550000
               WHEN SQLCODE = -911                                      03560000
                    CONTINUE                                            03570000
               WHEN SQLWARN0 NOT = SPACES                               03580000
               WHEN SQLCODE < ZERO                                      03590000
               WHEN SQLCODE > ZERO                                      03600000
                    MOVE '1400-CLOSE-PNA-CSR'                           03610000
                                    TO PV-PARAGRAPH-NAME                03620000
                    MOVE 'CLOSE THE PNA-CSR              '              03630000
                                    TO PV-DB2-OPERATION-ATTEMPTED       03640000
                    PERFORM 9100-SQL-ABEND                              03650000
           END-EVALUATE.                                                03660000
      *                                                                 03670000
      ******************************************************************03680000
      **  THIS PARAGRAPH OPENS THE CURSOR WHICH WILL COLLECT THE NO   **03690000
      **  OF SCANS WHICH DID NOT MEET THE SLA FOR P & A OF EACH LANE  **03700000
      **  IN EVERY DC.                                                **03710000
      ******************************************************************03720000
      *                                                                 03730000
       1500-OPEN-PNA-SLA-CSR.                                           03740000
      *                                                                 03750000
           EXEC SQL                                                     03760000
                OPEN PNA-SLA-CSR                                        03770000
           END-EXEC.                                                    03780000
      *                                                                 03790000
           EVALUATE TRUE                                                03800000
               WHEN SQLCODE = 0                                         03810000
                   CONTINUE                                             03820000
               WHEN SQLWARN0 NOT = SPACES                               03830000
               WHEN SQLCODE < ZERO                                      03840000
               WHEN SQLCODE > ZERO                                      03850000
                   MOVE '1500-OPEN-PNA-SLA-CSR'                         03860000
                                   TO PV-PARAGRAPH-NAME                 03870000
                   MOVE 'OPEN THE PNA-SLA-_CSR               '          03880000
                                   TO PV-DB2-OPERATION-ATTEMPTED        03890000
                   PERFORM 9100-SQL-ABEND                               03900000
           END-EVALUATE.                                                03910000
      *                                                                 03920000
      ******************************************************************03930000
      **  THIS PARAGRAPH FETCHES RECORDS  WHICH WILL COLLECT THE NO   **03940000
      **  OF SCANS WHICH DID NOT MEET THE SLA FOR P & A OF EACH LANE  **03950000
      **  IN EVERY DC.                                                **03960000
      ******************************************************************03970000
      *                                                                 03980000
       1600-FETCH-PNA-SLA-CSR.                                          03990000
      *                                                                 04000000
           EXEC SQL                                                     04010000
                FETCH PNA-SLA-CSR                                       04020000
                INTO  :SUT00043-DC-NBR                                  04030000
                    , :SUT00043-ACM-LBL-STA-NBR                         04040000
                    , :PV-GT-TOT-COUNT                                  04050000
           END-EXEC.                                                    04060000
      *                                                                 04070000
           EVALUATE TRUE                                                04080000
             WHEN SQLCODE = ZEROS                                       04090000
                  ADD  1  TO  PV-PNA-SLA-FETCH-CNT                      04100000
                  PERFORM 1610-UPDATE-GT-SLA-SCAN                       04110000
             WHEN SQLCODE = +100                                        04120000
                  SET PS-EOF-PNA-SLA-CSR TO TRUE                        04130000
             WHEN SQLWARN0 NOT = SPACES                                 04140000
             WHEN SQLCODE < ZERO                                        04150000
             WHEN SQLCODE > ZERO                                        04160000
                 MOVE '1600-FETCH-PNA-SLA-CSR'                          04170000
                                     TO PV-PARAGRAPH-NAME               04180000
                 MOVE 'FETCH PNA-SLA- CURSOR '                          04190000
                                     TO PV-DB2-OPERATION-ATTEMPTED      04200000
                 PERFORM 9100-SQL-ABEND                                 04210000
           END-EVALUATE.                                                04220000
      *                                                                 04230000
      ******************************************************************04240000
      **  THIS PARAGRAPH UPDATE THE NUMBER OF SCANS WHICH DID NOT MEET**04250000
      **  THE SLA FOR PRINT & APPLY OF EACH LANE IN EVERY DISTRIBUTION**04260000
      **  CENTERS.                                                    **04270000
      ******************************************************************04280000
      *                                                                 04290000
       1610-UPDATE-GT-SLA-SCAN.                                         04300000
      *                                                                 04310000
           SET PV-PNA-SLA-IDX TO 1.                                     04320000
      *                                                                 04330000
           SEARCH PV-PNA-SLA-DTL                                        04340000
               VARYING PV-PNA-SLA-IDX                                   04350000
               WHEN PV-DC-NBR(PV-PNA-SLA-IDX)  EQUAL SUT00043-DC-NBR AND04360000
                   (PV-STA-NBR(PV-PNA-SLA-IDX) EQUAL                    04370000
                    SUT00043-ACM-LBL-STA-NBR)                           04380000
                    MOVE PV-GT-TOT-COUNT TO                             04390000
                         PV-COUNT-OF-GT-SLA-SCANS(PV-PNA-SLA-IDX)       04400000
           END-SEARCH.                                                  04410000
      *                                                                 04420000
      ******************************************************************04430000
      **  THIS PARAGRAPH CLOSE THE CURSOR WHICH WILL COLLECT THE NO   **04440000
      **  OF SCANS WHICH DID NOT MEET THE SLA FOR P & A OF EACH LANE  **04450000
      **  IN EVERY DC.                                                **04460000
      ******************************************************************04470000
      *                                                                 04480000
       1700-CLOSE-PNA-SLA-CSR.                                          04490000
      *                                                                 04500000
           EXEC SQL                                                     04510000
               CLOSE PNA-SLA-CSR                                        04520000
           END-EXEC.                                                    04530000
      *                                                                 04540000
           EVALUATE TRUE                                                04550000
               WHEN SQLCODE = ZERO                                      04560000
               WHEN SQLCODE = -904                                      04570000
               WHEN SQLCODE = -911                                      04580000
                    CONTINUE                                            04590000
               WHEN SQLWARN0 NOT = SPACES                               04600000
               WHEN SQLCODE < ZERO                                      04610000
               WHEN SQLCODE > ZERO                                      04620000
                    MOVE '1700-CLOSE-PNA-SLA-CSR'                       04630000
                                    TO PV-PARAGRAPH-NAME                04640000
                    MOVE 'CLOSE THE PNA-SLA-CSR              '          04650000
                                    TO PV-DB2-OPERATION-ATTEMPTED       04660000
                    PERFORM 9100-SQL-ABEND                              04670000
           END-EVALUATE.                                                04680000
      *                                                                 04690000
      ******************************************************************04700000
      **  01.THIS PARAGRAPH READS THE RECORDS FROM THE INTERNAL TABLE **04710000
      **  WHICH HAS THE DC NUMBER, LANE NUMBER, TOATL NO OF SCAN DONE **04720000
      **  @ EACH LANE IN EVERY DC & TOTAL NO OF SCANS WHICH DID NOT   **04730000
      **  MEET THE P & A SLA FOR EACH LANE IN EVERY DC.               **04740000
      **                                                              **04750000
      **  02. WRITE THE RECORD IN TO OUTPUT FILE IN SUWS103 LAYOUT.   **04760000
      ******************************************************************04770000
      *                                                                 04780000
       2000-PROCESS-PNA.                                                04790000
      *                                                                 04800000
           INITIALIZE  SU103-PNA-SLA-INFO.                              04810000
      *                                                                 04820000
           PERFORM VARYING PV-PNA-SLA-IDX FROM 1 BY 1                   04830000
                   UNTIL PV-DC-NBR(PV-PNA-SLA-IDX) = ZEROES             04840000
                   OR PV-PNA-SLA-IDX > 1000                             04850000
             MOVE PV-PROC-DATE           TO  SU103-PROCESS-DATE         04860000
             MOVE PV-DC-NBR(PV-PNA-SLA-IDX)                             04870000
                                         TO  SU103-OPER-UNT-ID          04880000
             MOVE PV-STA-NBR(PV-PNA-SLA-IDX)                            04890000
                                         TO  SU103-LANE-NUMBER          04900000
             MOVE PV-COUNT-OF-TOT-SCANS(PV-PNA-SLA-IDX)                 04910000
                                         TO  SU103-TOTL-CARTON-SCANS    04920000
             MOVE PV-COUNT-OF-GT-SLA-SCANS(PV-PNA-SLA-IDX)              04930000
                                         TO  SU103-TOTL-GT-SLA          04940000
             MOVE PV-FISCAL-YEAR         TO  SU103-FISCAL-YR            04950000
             MOVE PV-FISCAL-MONTH        TO  SU103-FISCAL-MO            04960000
             MOVE PV-FISCAL-WEEK         TO  SU103-FISCAL-WK            04970000
             PERFORM 8000-WRITE-EXTRACT-RECORD                          04980000
           END-PERFORM.                                                 04990000
      *                                                                 05000000
      ******************************************************************05010000
      **  THIS PARAGRAPH WRITES THE RECORDS IN TO THE OUTPUT FILE     **05020000
      **  IN SUWS103 LAYOUT.                                          **05030000
      ******************************************************************05040000
      *                                                                 05050000
       8000-WRITE-EXTRACT-RECORD.                                       05060000
      *                                                                 05070000
            WRITE SU-PNA-SLA-EXTRACT-RECORD                             05080000
             FROM SU103-PNA-SLA-INFO.                                   05090000
              ADD 1 TO PV-TOTAL-CNT.                                    05100000
      *                                                                 05110000
      ******************************************************************05120000
      *  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL   *05130000
      *  ERROR OR WARNING CODE OCCURS.                                 *05140000
      ******************************************************************05150000
      *                                                                 05160000
       9100-SQL-ABEND.                                                  05170000
      *                                                                 05180000
           DISPLAY '9100-SQL-ABEND'.                                    05190000
           DISPLAY '** ABEND     **'.                                   05200000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        05210000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05220000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.     05230000
      *                                                                 05240000
      ******************************************************************05250000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *05260000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *05270000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *05280000
      * FORMAT.                                                        *05290000
      ******************************************************************05300000
      *                                                                 05310000
           COPY DPPD004.                                                05320000
      *                                                                 05330000
           MOVE +4013         TO ABEND-CODE.                            05340000
           CALL 'ILBOABN0' USING ABEND-CODE.                            05350000
      *                                                                 05360000
