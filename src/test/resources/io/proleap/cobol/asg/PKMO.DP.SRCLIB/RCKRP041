       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    RCKRP041.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  MAY 2005.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      * THIS REPORT LISTS DATA ON VERIFIED, OR POST VERIFIED ADJUSTED           
      * RECEIVERS BASED ON SELECT RECEIVER TYPES.  THE DETAIL IS BY             
      * PO/RECEIVER.                                                            
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXTRACT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VERIFIED-RECEIPTS-RPT                                         
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           DATA RECORD IS STORE-EXTRACT-RECORD.                                 
                                                                                
       01  STORE-EXTRACT-RECORD        PIC X(300).                              
                                                                                
       FD  VERIFIED-RECEIPTS-RPT                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS VERIFIED-RECEIPTS-LINE.                               
                                                                                
       01  VERIFIED-RECEIPTS-LINE      PIC X(132).                              
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE             PIC S9(4) COMP SYNC VALUE ZERO.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-EXTRACT-FILE-IND PIC X(01) VALUE 'N'.                  
               88  PS-NOT-END-OF-EXTRACT-FILE       VALUE 'N'.                  
               88  PS-END-OF-EXTRACT-FILE           VALUE 'Y'.                  
           05  PS-LAST-PAGE-OF-RPT-IND  PIC X(1)    VALUE 'N'.                  
               88  PS-NOT-LAST-PAGE                 VALUE 'N'.                  
               88  PS-LAST-PAGE                     VALUE 'Y'.                  
           05  PS-FIRST-LINE-ON-RPT-IND  PIC X(1)   VALUE 'N'.                  
               88  PS-NOT-FIRST-LINE                VALUE 'N'.                  
               88  PS-FIRST-LINE                    VALUE 'Y'.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
010000     05  PC-MAX-RETRIES          PIC S9(04) VALUE  +3                     
010100                                 COMP SYNC.                               
           05  PC-PAGE-SIZE            PIC S9(4)    VALUE +63                   
                                       COMP SYNC.                               
           05  PC-DEFAULT-DTE          PIC X(10)    VALUE '9999-09-09'.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
006630     05  PV-NULL-IND             PIC S9(04)  VALUE ZEROS                  
006640                                 COMP SYNC.                               
016500     05  PV-RETRY-COUNTER        PIC S9(04) VALUE +0                      
016600                                 COMP SYNC.                               
           05  PV-LINE-COUNT           PIC S9(4)    VALUE +70                   
                                       COMP SYNC.                               
           05  PV-NBR-OF-LINES         PIC S9(3)    VALUE +1                    
                                       COMP-3.                                  
           05  PV-PAGE-COUNT           PIC S9(5)    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-EXTENDED-COST        PIC S9(09)V99                            
                                       COMP-3.                                  
           05  PV-EXTENDED-RETAIL      PIC S9(09)V99                            
                                       COMP-3.                                  
           05  PV-TOT-VE-UNITS-PO-RCVR PIC S9(07)   VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-VE-UNITS-DEPT    PIC S9(07)   VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-VE-UNITS-FINAL   PIC S9(07)   VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-COST-PO-RCVR PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-COST-DEPT    PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-COST-FINAL   PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-RETAIL-PO-RCVR                                        
                                       PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-RETAIL-DEPT  PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-TOT-EXT-RETAIL-FINAL PIC S9(11)V99                            
                                                    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-RPT-LINE-SEQ         PIC 9(5)     VALUE ZERO                  
                                       COMP-3.                                  
                                                                                
005000     05  PV-HOLD-REC-SEQ-NBR     PIC 9(09).                               
           05  PV-HOLD-PO-NBR          PIC 9(08).                               
           05  PV-HOLD-DEPT-NBR        PIC 9(04).                               
                                                                                
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC X(30)    VALUE SPACES.               
           05  PV-DATA                 PIC 9(09).                               
           05  PV-DATE-WITH-DASHES.                                             
               10  PV-YYYY-WITH-DASHES PIC X(4).                                
               10  FILLER              PIC X(1).                                
               10  PV-MM-WITH-DASHES   PIC X(2).                                
               10  FILLER              PIC X(1).                                
               10  PV-DD-WITH-DASHES   PIC X(2).                                
                                                                                
       01  CD-RECEIVER-TYPE-DATA.                                               
           05  FILLER                  PIC X(11).                               
           05  CD-RECEIVER-TYPE-DESC   PIC X(35).                               
                                                                                
       01  CD-CURRENT-DATE.                                                     
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CD-CCYYMMDD-HOLD.                                                    
           05  CD-CCYYMMDD-HOLD-CC     PIC 9(2)     VALUE ZERO.                 
           05  CD-CCYYMMDD-HOLD-YY     PIC 9(2)     VALUE ZERO.                 
           05  CD-CCYYMMDD-HOLD-MM     PIC 9(2)     VALUE ZERO.                 
           05  CD-CCYYMMDD-HOLD-DD     PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CT-CURRENT-TIME.                                                     
           05  CT-CURRENT-HOUR         PIC 9(2)     VALUE ZERO.                 
           05  CT-CURRENT-MINUTE       PIC 9(2)     VALUE ZERO.                 
           05  FILLER                  PIC 9(4)     VALUE ZERO.                 
                                                                                
       01  HEADING-LINE-1.                                                      
           05  FILLER                  PIC X(30)    VALUE SPACES.               
           05  FILLER                  PIC X(38)    VALUE                       
               'SUMMARY LISTING OF UNITS VERIFIED FOR '.                        
           05  HL1-RECEIVER-TYPE-DESC  PIC X(35).                               
           05  FILLER                  PIC X(31)    VALUE SPACES.               
                                                                                
       01  HEADING-LINE-2.                                                      
           05  FILLER                  PIC X(52)    VALUE SPACES.               
           05  FILLER                  PIC X(19)    VALUE                       
               'PROCESSING DATE OF '.                                           
           05  HL2-PROCESS-MM          PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE                       
               '/'.                                                             
           05  HL2-PROCESS-DD          PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE                       
               '/'.                                                             
           05  HL2-PROCESS-YYYY        PIC X(04).                               
           05  FILLER                  PIC X(51)    VALUE SPACES.               
                                                                                
       01  HEADING-LINE-3.                                                      
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(23)    VALUE                       
               'DEPARTMENT NUMBER:     '.                                       
           05  HL3-DEPT-NBR            PIC ZZZ9.                                
           05  FILLER                  PIC X(03)    VALUE                       
               ' - '.                                                           
           05  HL3-DEPT-NAME           PIC X(20)    VALUE SPACES.               
           05  FILLER                  PIC X(81)    VALUE SPACES.               
                                                                                
       01  HEADING-LINE-4.                                                      
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(15)    VALUE SPACES.               
           05  FILLER                  PIC X(35)    VALUE                       
               'VRFY  VERIFY     VERIFY  CANCEL NOT'.                           
           05  VILLER                  PIC X(27)    VALUE SPACES.               
           05  FILLER                  PIC X(51)    VALUE                       
               'PK VR   VERIFIED        EXTENDED           EXTENDED'.           
           05  FILLER                  PIC X(04)    VALUE SPACES.               
                                                                                
       01  HEADING-LINE-5.                                                      
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(40)    VALUE                       
               'PO/RECEIVER     DC    DATE     USER-ID  '.                      
           05  FILLER                  PIC X(37)    VALUE                       
               'SHIP DATE  VENDOR NAME               '.                         
           05  FILLER                  PIC X(50)    VALUE                       
               'TY TY     UNITS           COST              RETAIL'.            
           05  FILLER                  PIC X(04)    VALUE SPACES.               
                                                                                
       01  DETAIL-LINE.                                                         
W26600     05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  DL-PO-NBR               PIC ZZZZZZZZ9.                           
           05  FILLER                  PIC X(01)    VALUE                       
               '-'.                                                             
           05  DL-REC-SEQ-NBR          PIC 9(04).                               
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  DL-CURR-OPER-UNIT-ID    PIC ZZZ9.                                
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  DL-VE-MM                PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE                       
               '/'.                                                             
           05  DL-VE-DD                PIC X(02).                               
           05  FILLER                  PIC X(03)    VALUE                       
               '/20'.                                                           
           05  DL-VE-YY                PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE SPACE.                
           05  DL-VRFD-USR-ID          PIC X(08).                               
           05  FILLER                  PIC X(01)    VALUE SPACE.                
           05  DL-CANCEL-MM            PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE                       
               '/'.                                                             
           05  DL-CANCEL-DD            PIC X(02).                               
           05  FILLER                  PIC X(01)    VALUE                       
               '/'.                                                             
           05  DL-CANCEL-YYYY          PIC X(04).                               
           05  FILLER                  PIC X(01)    VALUE SPACE.                
           05  DL-VENDOR-NAME          PIC X(25).                               
           05  FILLER                  PIC X(01)    VALUE SPACE.                
           05  DL-PKGNG-CDE            PIC X(01).                               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-VE-TYPE              PIC X(02).                               
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  DL-VE-UNITS             PIC ZZZ,ZZ9-.                            
           05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  DL-EXTENDED-COST        PIC ZZZ,ZZZ,ZZ9.99-.                     
           05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  DL-EXTENDED-RETAIL      PIC ZZZ,ZZZ,ZZ9.99-.                     
                                                                                
       01  TOTAL-LINE.                                                          
W26600     05  FILLER                  PIC X(62)    VALUE SPACES.               
W26600     05  TL-TOTAL-TYPE-DESC      PIC X(20).                               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  TL-VE-UNITS             PIC Z,ZZZ,ZZ9.                           
           05  FILLER                  PIC X(3)     VALUE SPACES.               
           05  TL-EXTENDED-COST        PIC Z,ZZZ,ZZZ,ZZ9.99-.                   
           05  FILLER                  PIC X(2)     VALUE SPACES.               
           05  TL-EXTENDED-RETAIL      PIC Z,ZZZ,ZZZ,ZZZ.99-                    
                                                    VALUE ZEROES.               
           05  FILLER                  PIC X(2)     VALUE SPACES.               
                                                                                
       01  END-OF-REPORT-LINE.                                                  
W26600     05  FILLER                  PIC X(56)    VALUE SPACES.               
W26600     05  FILLER                  PIC X(20)    VALUE                       
               ' ** END OF REPORT **'.                                          
W26600     05  FILLER                  PIC X(56)    VALUE SPACES.               
                                                                                
       01  EMPTY-INPUT-LINE.                                                    
           05  FILLER                  PIC X(27)    VALUE SPACES.               
           05  FILLER                  PIC X(14)    VALUE                       
               'THERE WERE NO '.                                                
           05  EI-RECEIVER-TYPE-DESC   PIC X(35).                               
           05  FILLER                  PIC X(27)    VALUE                       
               'TO REPORT ON FOR THIS RUN. '.                                   
           05  FILLER                  PIC X(29)    VALUE SPACES.               
                                                                                
      /*****************************************************************        
      *  FILE LAYOUT FOR STORE EXTRACT DATA                                     
      ******************************************************************        
W26600     COPY STOREEXT.                                                       
                                                                                
011801******************************************************************01180113
011802** COPYBOOK USED FOR CALLED MODULE XLKUT004                       01180213
011804******************************************************************01180413
011805     COPY XLWS004.                                                01180513
                                                                                
      /*****************************************************************        
      *  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT                         
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      /*****************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  LAYOUT USED FOR COMPANY CONTROL TABLE                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCOCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 - RECEIVING                                                         
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRECEIV                                                  
           END-EXEC.                                                            
                                                                                
046300*****************************************************************         
046400*    DB2 AREA FOR THE PLANNED SHIPMENTS TABLE              4     *        
046500*****************************************************************         
046600     EXEC SQL                                                             
046700          INCLUDE TPLNSHP                                                 
046800     END-EXEC.                                                            
      /*****************************************************************        
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           IF PS-NOT-END-OF-EXTRACT-FILE                                        
               PERFORM 2000-PROCESS-EXTRACT                                     
                  UNTIL PS-END-OF-EXTRACT-FILE                                  
               PERFORM 3000-END-OF-REPORT                                       
               PERFORM 9000-EOJ-ROUTINE                                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      * 1000-INTIALIZATION ROUTINE.                                             
      *  OPEN FILES, FILL STANDARD REPORT HEADER FIELDS,                        
      *  READ THE FIRST IN STORE EXTRACT RECORD.                                
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  STORE-EXTRACT-FILE                                       
                OUTPUT VERIFIED-RECEIPTS-RPT.                                   
                                                                                
           ACCEPT CD-RECEIVER-TYPE-DATA                                         
                                    FROM SYSIN.                                 
           MOVE CD-RECEIVER-TYPE-DESC                                           
                                    TO HL1-RECEIVER-TYPE-DESC                   
                                       EI-RECEIVER-TYPE-DESC                    
                                                                                
           ACCEPT CD-CURRENT-DATE   FROM DATE.                                  
           MOVE CD-CURRENT-YEAR     TO DP132O-RUN-YEAR.                         
           MOVE CD-CURRENT-MONTH    TO DP132O-RUN-MONTH.                        
           MOVE CD-CURRENT-DAY      TO DP132O-RUN-DAY.                          
                                                                                
           ACCEPT CT-CURRENT-TIME   FROM TIME.                                  
           MOVE CT-CURRENT-HOUR     TO DP132O-RUN-HOUR.                         
           MOVE CT-CURRENT-MINUTE   TO DP132O-RUN-MINUTE.                       
                                                                                
           MOVE 'RCKRP041'          TO DP132O-PROGRAM-NAME.                     
           MOVE 1                   TO DP132O-REPORT-NUMBER.                    
           MOVE PC-PAGE-SIZE        TO PV-LINE-COUNT.                           
           MOVE ZEROS               TO PV-PAGE-COUNT.                           
                                                                                
           PERFORM 6000-PROCESS-TCOCNTL.                                        
                                                                                
           SET PS-NOT-END-OF-EXTRACT-FILE TO TRUE.                              
           PERFORM 7100-READ-EXTRACT-FILE                                       
                                                                                
           IF PS-END-OF-EXTRACT-FILE                                            
               MOVE ZERO          TO HL3-DEPT-NBR                               
               ADD 1 TO PV-PAGE-COUNT                                           
               MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER                         
               WRITE VERIFIED-RECEIPTS-LINE                                     
                   FROM DP132O-STANDRD-HEADER-132-COLS                          
                   AFTER ADVANCING PAGE                                         
               WRITE VERIFIED-RECEIPTS-LINE                                     
                   FROM HEADING-LINE-1                                          
                   AFTER ADVANCING 2 LINES                                      
               WRITE VERIFIED-RECEIPTS-LINE                                     
                   FROM HEADING-LINE-2                                          
                   AFTER ADVANCING 1 LINES                                      
               WRITE VERIFIED-RECEIPTS-LINE                                     
                   FROM HEADING-LINE-3                                          
                   AFTER ADVANCING 2 LINES                                      
               WRITE VERIFIED-RECEIPTS-LINE                                     
                   FROM EMPTY-INPUT-LINE                                        
                   AFTER ADVANCING 1 LINES                                      
           ELSE                                                                 
               MOVE SE-DEPT        TO HL3-DEPT-NBR                              
                                      PV-HOLD-DEPT-NBR                          
005000         MOVE SE-REC-SEQ-NBR TO PV-HOLD-REC-SEQ-NBR                       
               MOVE SE-PO          TO PV-HOLD-PO-NBR                            
      * * GET DEPARTMENT NAME                                                   
               PERFORM 6100-CALL-XLKUT004                                       
               PERFORM 8000-PRINT-HEADERS                                       
               PERFORM 4000-LOAD-DETAIL-LINE                                    
           END-IF.                                                              
                                                                                
       2000-PROCESS-EXTRACT.                                                    
           IF SE-DEPT           = PV-HOLD-DEPT-NBR                              
             AND SE-PO          = PV-HOLD-PO-NBR                                
             AND SE-REC-SEQ-NBR = PV-HOLD-REC-SEQ-NBR                           
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-TOT-VE-UNITS-PO-RCVR                                     
                                   TO DL-VE-UNITS                               
               MOVE PV-TOT-EXT-COST-PO-RCVR                                     
                                   TO DL-EXTENDED-COST                          
               MOVE PV-TOT-EXT-RETAIL-PO-RCVR                                   
                                   TO DL-EXTENDED-RETAIL                        
               PERFORM 7000-PRINT-REPORT                                        
               PERFORM 4000-LOAD-DETAIL-LINE                                    
005000         MOVE SE-REC-SEQ-NBR TO PV-HOLD-REC-SEQ-NBR                       
               MOVE SE-PO          TO PV-HOLD-PO-NBR                            
               MOVE ZERO           TO PV-TOT-VE-UNITS-PO-RCVR                   
                                      PV-TOT-EXT-COST-PO-RCVR                   
                                      PV-TOT-EXT-RETAIL-PO-RCVR                 
           END-IF                                                               
                                                                                
           IF SE-DEPT NOT = PV-HOLD-DEPT-NBR                                    
               MOVE SE-DEPT        TO HL3-DEPT-NBR                              
                                      PV-HOLD-DEPT-NBR                          
               PERFORM 6100-CALL-XLKUT004                                       
               PERFORM 2100-DEPT-BREAK                                          
               PERFORM 4000-LOAD-DETAIL-LINE                                    
005000         MOVE SE-REC-SEQ-NBR TO PV-HOLD-REC-SEQ-NBR                       
               MOVE SE-PO          TO PV-HOLD-PO-NBR                            
           END-IF.                                                              
                                                                                
           ADD SE-RETAIL-UNITS-MARK                                             
                                  TO PV-TOT-VE-UNITS-PO-RCVR                    
                                     PV-TOT-VE-UNITS-DEPT                       
                                     PV-TOT-VE-UNITS-FINAL                      
           ADD SE-COST-EXTENSION  TO PV-TOT-EXT-COST-PO-RCVR                    
                                     PV-TOT-EXT-COST-DEPT                       
                                     PV-TOT-EXT-COST-FINAL                      
           ADD SE-RETAIL-EXTENSION                                              
                                  TO PV-TOT-EXT-RETAIL-PO-RCVR                  
                                     PV-TOT-EXT-RETAIL-DEPT                     
                                     PV-TOT-EXT-RETAIL-FINAL                    
                                                                                
           PERFORM 7100-READ-EXTRACT-FILE.                                      
                                                                                
       2100-DEPT-BREAK.                                                         
           ADD +2 TO PV-LINE-COUNT                                              
           IF PV-LINE-COUNT > PC-PAGE-SIZE                                      
               PERFORM 8000-PRINT-HEADERS                                       
           END-IF                                                               
           MOVE ' DEPARTMENT TOTAL:  '                                          
                                     TO TL-TOTAL-TYPE-DESC                      
           MOVE PV-TOT-VE-UNITS-DEPT TO TL-VE-UNITS                             
           MOVE PV-TOT-EXT-COST-DEPT TO TL-EXTENDED-COST                        
           MOVE PV-TOT-EXT-RETAIL-DEPT                                          
                                     TO TL-EXTENDED-RETAIL                      
           WRITE VERIFIED-RECEIPTS-LINE                                         
                FROM TOTAL-LINE                                                 
                AFTER +2 LINES                                                  
                                                                                
           ADD +6 TO PV-LINE-COUNT                                              
           IF PV-LINE-COUNT > PC-PAGE-SIZE                                      
               PERFORM 8000-PRINT-HEADERS                                       
           ELSE                                                                 
               PERFORM 8100-PRINT-SUB-HEADERS                                   
           END-IF                                                               
                                                                                
           MOVE ZERO TO PV-TOT-VE-UNITS-PO-RCVR                                 
                        PV-TOT-VE-UNITS-DEPT                                    
                        PV-TOT-EXT-COST-PO-RCVR                                 
                        PV-TOT-EXT-COST-DEPT                                    
                        PV-TOT-EXT-RETAIL-PO-RCVR                               
                        PV-TOT-EXT-RETAIL-DEPT.                                 
                                                                                
       3000-END-OF-REPORT.                                                      
           MOVE PV-TOT-VE-UNITS-PO-RCVR                                         
                                  TO DL-VE-UNITS                                
           MOVE PV-TOT-EXT-COST-PO-RCVR                                         
                                  TO DL-EXTENDED-COST                           
           MOVE PV-TOT-EXT-RETAIL-PO-RCVR                                       
                                  TO DL-EXTENDED-RETAIL                         
           PERFORM 7000-PRINT-REPORT                                            
           ADD +4 TO PV-LINE-COUNT                                              
           IF PV-LINE-COUNT > PC-PAGE-SIZE                                      
               PERFORM 8000-PRINT-HEADERS                                       
           END-IF.                                                              
           MOVE ' DEPARTMENT TOTAL:  '                                          
                                  TO TL-TOTAL-TYPE-DESC                         
           MOVE PV-TOT-VE-UNITS-DEPT                                            
                                  TO TL-VE-UNITS                                
           MOVE PV-TOT-EXT-COST-DEPT                                            
                                  TO TL-EXTENDED-COST                           
           MOVE PV-TOT-EXT-RETAIL-DEPT                                          
                                  TO TL-EXTENDED-RETAIL                         
           WRITE VERIFIED-RECEIPTS-LINE                                         
                FROM TOTAL-LINE                                                 
                AFTER 2 LINES.                                                  
           MOVE '      FINAL TOTAL:  '                                          
                                  TO TL-TOTAL-TYPE-DESC                         
           MOVE PV-TOT-VE-UNITS-FINAL                                           
                                  TO TL-VE-UNITS                                
           MOVE PV-TOT-EXT-COST-FINAL                                           
                                  TO TL-EXTENDED-COST                           
           MOVE PV-TOT-EXT-RETAIL-FINAL                                         
                                  TO TL-EXTENDED-RETAIL                         
           WRITE VERIFIED-RECEIPTS-LINE                                         
                FROM TOTAL-LINE                                                 
                AFTER 2 LINES.                                                  
                                                                                
       4000-LOAD-DETAIL-LINE.                                                   
           PERFORM 6200-SELECT-TRECEIV                                          
           PERFORM 6300-GET-CANCEL-DATE                                         
           MOVE SE-PO            TO DL-PO-NBR                                   
           MOVE SE-REC-SEQ-NBR   TO DL-REC-SEQ-NBR                              
           MOVE SE-CURR-OPER-UNT-ID                                             
                                 TO DL-CURR-OPER-UNIT-ID                        
           MOVE SE-DV-MM         TO DL-VE-MM                                    
           MOVE SE-DV-DD         TO DL-VE-DD                                    
           MOVE SE-DV-YY         TO DL-VE-YY                                    
           MOVE SE-VENDOR-NAME-INHOUSE                                          
                                 TO DL-VENDOR-NAME.                             
           IF SE-REC-SRC-CDE = 'A'                                              
               MOVE 'PV'         TO DL-VE-TYPE                                  
           ELSE                                                                 
               MOVE 'RC'         TO DL-VE-TYPE                                  
           END-IF.                                                              
                                                                                
       6000-PROCESS-TCOCNTL.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT BTCH_PRC_DTE                                              
W26600           INTO :COCNTL-BTCH-PRC-DTE                                      
W26600           FROM TCOCNTL                                                   
                WHERE OPER_UNT_ID = 90                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE COCNTL-BTCH-PRC-DTE                                     
                       TO PV-DATE-WITH-DASHES                                   
                   MOVE PV-MM-WITH-DASHES                                       
                       TO HL2-PROCESS-MM                                        
                   MOVE PV-DD-WITH-DASHES                                       
                       TO HL2-PROCESS-DD                                        
                   MOVE PV-YYYY-WITH-DASHES                                     
                       TO HL2-PROCESS-YYYY                                      
               WHEN OTHER                                                       
                   MOVE '6000-PROCESS-TCOCNTL'                                  
                       TO PV-PARAGRAPH-NAME                                     
                   MOVE 'SELECT TCOCNTL'                                        
                       TO PV-DB2-OPERATION-ATTEMPTED                            
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       6100-CALL-XLKUT004.                                                      
318000     INITIALIZE XL004-COMMAREA                                    31800013
318100     MOVE SE-DEPT                  TO XL004-DEPT-NBR              31810013
           CALL XL004-PGM     USING XL004-DEPT-COMMAREA                         
318500     EVALUATE TRUE                                                31850013
318600         WHEN XL004-SUCCESSFUL                                    31860013
318700             MOVE XL004-DEPT-NM    TO HL3-DEPT-NAME               31870013
318800         WHEN XL004-NOT-FOUND                                     31880013
318900              MOVE 'NOT FOUND'     TO HL3-DEPT-NAME               31890013
319000         WHEN OTHER                                               31900013
                    STRING 'SQL ERROR '  DELIMITED BY SIZE                      
                           XL004-SQLCODE DELIMITED BY SIZE                      
                      INTO HL3-DEPT-NAME                                        
           END-EVALUATE.                                                        
                                                                                
       6200-SELECT-TRECEIV.                                                     
178100     MOVE SE-PO          TO RECEIV-ORD-NBR                                
178200     MOVE SE-REC-SEQ-NBR TO RECEIV-REC-SEQ-NBR                            
171200     PERFORM                                                              
171300         WITH TEST AFTER                                                  
171400         VARYING PV-RETRY-COUNTER                                         
171500         FROM 1 BY 1                                                      
171600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
171700            OR (SQLCODE = +0)                                             
171800            OR (SQLCODE = +100))                                          
171900                                                                          
172000         EXEC SQL                                                         
174600            SELECT VRFD_USR_ID                                            
                        ,PKGNG_CDE                                              
177400              INTO :RECEIV-VRFD-USR-ID                                    
                        ,:RECEIV-PKGNG-CDE                                      
177700              FROM  TRECEIV                                               
178100             WHERE ORD_NBR     = :RECEIV-ORD-NBR                          
178200               AND REC_SEQ_NBR = :RECEIV-REC-SEQ-NBR                      
178600         END-EXEC                                                         
178700                                                                          
178800         EVALUATE TRUE                                                    
178900             WHEN SQLCODE = +0                                            
                       MOVE RECEIV-VRFD-USR-ID                                  
                                         TO DL-VRFD-USR-ID                      
                       MOVE RECEIV-PKGNG-CDE                                    
                                         TO DL-PKGNG-CDE                        
179000             WHEN SQLCODE = +100                                          
                       MOVE 'NOT FOUND'  TO DL-VRFD-USR-ID                      
                       MOVE SPACES       TO DL-PKGNG-CDE                        
179100             WHEN SQLCODE = -904                                          
179200             WHEN SQLCODE = -913                                          
179300                 CONTINUE                                                 
179400                                                                          
179500             WHEN SQLWARN0 NOT = SPACE                                    
179600             WHEN SQLCODE < +0                                            
179700             WHEN SQLCODE > +0                                            
                   MOVE '6200'           TO PV-PARAGRAPH-NAME                   
                   MOVE 'SELECT TRECEIV' TO PV-DB2-OPERATION-ATTEMPTED          
180900             PERFORM 9999-SQL-ABEND                                       
181000         END-EVALUATE                                                     
181100     END-PERFORM.                                                         
181200                                                                          
       6300-GET-CANCEL-DATE.                                                    
           MOVE SE-PO TO PLNSHP-PO-NBR                                          
171200     PERFORM                                                              
171300         WITH TEST AFTER                                                  
171400         VARYING PV-RETRY-COUNTER                                         
171500         FROM 1 BY 1                                                      
171600         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
171700            OR (SQLCODE = +0)                                             
171800            OR (SQLCODE = +100))                                          
171900                                                                          
172000         EXEC SQL                                                         
174600            SELECT CAN_IFNOT_SHIP_DTE                                     
177400              INTO :PLNSHP-CAN-IFNOT-SHIP-DTE                             
177700              FROM  TPLNSHP                                               
178100             WHERE PO_NBR         = :PLNSHP-PO-NBR                        
178200               AND VER_STATUS_CDE = 'A'                                   
178600         END-EXEC                                                         
178700                                                                          
178800         EVALUATE TRUE                                                    
178900             WHEN SQLCODE = +0                                            
                   MOVE PLNSHP-CAN-IFNOT-SHIP-DTE                               
                                         TO PV-DATE-WITH-DASHES                 
                   MOVE PV-MM-WITH-DASHES                                       
                                         TO DL-CANCEL-MM                        
                   MOVE PV-DD-WITH-DASHES                                       
                                         TO DL-CANCEL-DD                        
                   MOVE PV-YYYY-WITH-DASHES                                     
                                         TO DL-CANCEL-YYYY                      
179000             WHEN SQLCODE = +100                                          
                       MOVE SPACES       TO DL-CANCEL-MM                        
                                            DL-CANCEL-DD                        
                                            DL-CANCEL-YYYY                      
179100             WHEN SQLCODE = -904                                          
179200             WHEN SQLCODE = -913                                          
179300                 CONTINUE                                                 
179400                                                                          
179500             WHEN SQLWARN0 NOT = SPACE                                    
179600             WHEN SQLCODE < +0                                            
179700             WHEN SQLCODE > +0                                            
                   MOVE '6300'           TO PV-PARAGRAPH-NAME                   
                   MOVE 'SELECT TPLNSHP' TO PV-DB2-OPERATION-ATTEMPTED          
180900             PERFORM 9999-SQL-ABEND                                       
181000         END-EVALUATE                                                     
181100     END-PERFORM.                                                         
                                                                                
                                                                                
       7000-PRINT-REPORT.                                                       
           ADD +1 TO PV-LINE-COUNT                                              
           IF PV-LINE-COUNT > PC-PAGE-SIZE                                      
               PERFORM 8000-PRINT-HEADERS                                       
           END-IF                                                               
           WRITE VERIFIED-RECEIPTS-LINE                                         
                FROM DETAIL-LINE                                                
                AFTER PV-NBR-OF-LINES.                                          
           MOVE +1 TO PV-NBR-OF-LINES.                                          
                                                                                
      /*****************************************************************        
      * 7100-READ-EXTRACT-FILE.                                                 
      ******************************************************************        
       7100-READ-EXTRACT-FILE.                                                  
           READ STORE-EXTRACT-FILE                                              
               INTO STORE-EXTRACT-LINE                                          
               AT END                                                           
                   SET PS-END-OF-EXTRACT-FILE TO TRUE                           
           END-READ.                                                            
                                                                                
                                                                                
      /*****************************************************************        
      * 8000-PRINT-HEADERS.                                                     
      *  PRINT TITLE LINES ON THE REPORT AT EACH PAGE BREAK.  UPDATE            
      *  THE PAGE NUMBER.                                                       
      ******************************************************************        
       8000-PRINT-HEADERS.                                                      
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
                                                                                
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM HEADING-LINE-1                                              
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM HEADING-LINE-2                                              
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           PERFORM 8100-PRINT-SUB-HEADERS.                                      
                                                                                
           MOVE 13 TO PV-LINE-COUNT.                                            
                                                                                
       8100-PRINT-SUB-HEADERS.                                                  
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM HEADING-LINE-3                                              
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM HEADING-LINE-4                                              
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM HEADING-LINE-5                                              
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           MOVE +2  TO PV-NBR-OF-LINES.                                         
                                                                                
      /*****************************************************************        
      * 9000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
           WRITE VERIFIED-RECEIPTS-LINE                                         
               FROM END-OF-REPORT-LINE                                          
               AFTER ADVANCING 1 LINES.                                         
           CLOSE STORE-EXTRACT-FILE                                             
                 VERIFIED-RECEIPTS-RPT.                                         
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = RCKRP041'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
