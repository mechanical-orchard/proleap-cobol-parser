      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    MLKUT487.                                                 
       AUTHOR.        RONNA MCDONALD                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08/11/03.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      *****************************************************************         
      *    CREATE A SORT CONTROL CARD TO BE USED TO SORT OUT RECORDS            
      *    BY DATE AND ORDER RECORDS BY CURRENT DEPT/CLASS/SKU.                 
      *****************************************************************         
      *****************************************************************         
                                                                                
      ******************************************************************        
      *                                                                *        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
      *                                                                         
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  INPUT-RECORD                       PIC X(80).                        
      *                                                                         
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  OUTPUT-RECORD                      PIC X(80).                        
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES       PIC S9(04)  VALUE +3                        
                                                COMP SYNC.                      
           05  PC-CALENDAR-ROUTINE               PIC  X(08) VALUE               
                                                     'DPKUT500'.                
           05  PC-PROGRAM-NAME      PIC  X(08)  VALUE 'MLKUT487'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-MAX-RETRIES                  PIC S9(3)   VALUE +3.            
                                                                                
       01  CC-CONTROL-CARD                      VALUE SPACE.                    
           05  CC-DATE                          PIC X(10).                      
           05  FILLER                           PIC X(70).                      
                                                                                
       01  SC-SORT-RECORD-1.                                                    
           05  FILLER                     PIC X(43) VALUE                       
           '    SORT FIELDS=(83,12,CH,A)               '.                       
           05  FILLER                     PIC X(37) VALUE                       
                          '    * DEPT/CL/SCL                    '.              
       01  SC-INCLUDE-FIELDS-1.                                                 
           05  FILLER                     PIC X(29) VALUE                       
           '   INCLUDE COND=((1,3,CH,EQ,C'.                                     
           05  FILLER                     PIC X(01) VALUE QUOTE.                
           05  FILLER                     PIC X(03) VALUE '010'.                
           05  FILLER                     PIC X(01) VALUE QUOTE.                
           05  FILLER                     PIC X(09) VALUE                       
                                              '),AND,   '.                      
           05  FILLER                     PIC X(37) VALUE                       
                          '    * STYLE RECLASS REC              '.              
                                                                                
       01  SC-INCLUDE-FIELDS-2.                                                 
           05  FILLER                     PIC X(29) VALUE                       
           '                (4,10,CH,EQ,C'.                                     
           05  FILLER                     PIC X(01) VALUE QUOTE.                
           05  SC-DATE                    PIC X(10).                            
           05  FILLER                     PIC X(01) VALUE QUOTE.                
           05  FILLER                     PIC X(02) VALUE                       
                                              '))'.                             
           05  FILLER                     PIC X(37) VALUE                       
                          '    * DATE                           '.              
                                                                                
       01  SC-SORT-RECORD-END.                                                  
           05  FILLER                     PIC X(08) VALUE                       
           '     END'.                                                          
                                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
                                                                                
           COPY DPWS500.                                                        
                                                                                
                                                                                
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *                                                                         
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM B100-OPEN-FILE.                                              
                                                                                
           PERFORM B200-READ-INPUT-DATE.                                        
                                                                                
           PERFORM B300-VALIDATE-DATE.                                          
                                                                                
           PERFORM C300-WRITE-SORT-CARDS.                                       
                                                                                
           PERFORM D400-CLOSE-FILE.                                             
      *                                                                         
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  B100 - OPENS INPUT FILE                                                
      ******************************************************************        
       B100-OPEN-FILE.                                                          
                                                                                
            OPEN INPUT INPUT-FILE                                               
                 OUTPUT OUTPUT-FILE.                                            
                                                                                
      ******************************************************************        
      *  B200  READS DATE FROM INPUT FILE                                       
      ******************************************************************        
                                                                                
       B200-READ-INPUT-DATE.                                                    
            READ INPUT-FILE                                                     
               INTO CC-CONTROL-CARD.                                            
                                                                                
                                                                                
      ******************************************************************        
      *   CHECKS DATE IS NOT SPACES AND IS A VALID CALENDAR DATE                
      ******************************************************************        
                                                                                
       B300-VALIDATE-DATE.                                                      
                                                                                
           DISPLAY '*** MLKUT487 INPUT CONTROL CARD:' CC-CONTROL-CARD.          
                                                                                
           IF CC-DATE = SPACE                                                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = MLKUT487'                                      
               DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                      
               DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'                 
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-DATE                  TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL PC-CALENDAR-ROUTINE                                             
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = MLKUT487'                                  
                   DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                  
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
                   DISPLAY '** USING CONTROL CARD DATE AS INPUT.'               
                   DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
                   DISPLAY '** DPG51 = ' DPG51                                  
                   DISPLAY '** DPG52 = ' DPG52                                  
                   DISPLAY '** DPG53 = ' DPG53                                  
                   DISPLAY '** DPG54 = ' DPG54                                  
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  C300  CREATE THE SYCSORT CONTROL CARDS.                                
      ******************************************************************        
                                                                                
       C300-WRITE-SORT-CARDS.                                                   
                                                                                
            MOVE CC-DATE                  TO SC-DATE.                           
                                                                                
            WRITE OUTPUT-RECORD                                                 
               FROM SC-SORT-RECORD-1.                                           
            DISPLAY SC-SORT-RECORD-1.                                           
                                                                                
            WRITE OUTPUT-RECORD                                                 
               FROM SC-INCLUDE-FIELDS-1.                                        
            DISPLAY SC-INCLUDE-FIELDS-1.                                        
                                                                                
            WRITE OUTPUT-RECORD                                                 
               FROM SC-INCLUDE-FIELDS-2.                                        
            DISPLAY SC-INCLUDE-FIELDS-2.                                        
                                                                                
            WRITE OUTPUT-RECORD                                                 
               FROM SC-SORT-RECORD-END.                                         
            DISPLAY SC-SORT-RECORD-END.                                         
                                                                                
                                                                                
      ****************************************************************          
      *  D400 - CLOSES INPUT FILE                                               
      ****************************************************************          
                                                                                
       D400-CLOSE-FILE.                                                         
            CLOSE INPUT-FILE.                                                   
                                                                                
