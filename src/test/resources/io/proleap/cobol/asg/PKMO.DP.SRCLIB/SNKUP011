00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP011
00003  PROGRAM-ID.    SNKUP011.                                            LV003
00004                                                                   SNKUP011
00005  AUTHOR.        ANNE YEKO.                                        SNKUP011
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP011
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP011
00008  DATE-COMPILED.                                                   SNKUP011
00009                                                                   SNKUP011
00010 ******************************************************************SNKUP011
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP011
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP011
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASOADT TABLE.          **SNKUP011
00014 ******************************************************************SNKUP011
00015 * CHANGED __/__/__ BY ____________                              **SNKUP011
00016 *   P_________  ________________________________________________**SNKUP011
00017 *               ________________________________________________**SNKUP011
00018 *                                                               **SNKUP011
00019 ******************************************************************SNKUP011
00020                                                                   SNKUP011
00021      EJECT                                                        SNKUP011
00022                                                                   SNKUP011
00023  ENVIRONMENT DIVISION.                                            SNKUP011
00024                                                                   SNKUP011
00025  INPUT-OUTPUT SECTION.                                            SNKUP011
00026                                                                   SNKUP011
00027  FILE-CONTROL.                                                    SNKUP011
00028                                                                   SNKUP011
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP011
00030             ASSIGN TO INP01.                                      SNKUP011
00031                                                                   SNKUP011
00032  DATA DIVISION.                                                   SNKUP011
00033                                                                   SNKUP011
00034  FILE SECTION.                                                    SNKUP011
00035                                                                   SNKUP011
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP011
00037      RECORDING MODE IS F                                          SNKUP011
00038      LABEL RECORDS ARE STANDARD                                   SNKUP011
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP011
00041                                                                   SNKUP011
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP011
00044      EJECT                                                        SNKUP011
00045  WORKING-STORAGE SECTION.                                         SNKUP011
00046                                                                   SNKUP011
00047  01  FILLER.                                                      SNKUP011
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP011
00049          ' ***** WORKING STORAGE PGM=SNKUP011 '.                  SNKUP011
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP011
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP011
00052          ' LOCATION='.                                            SNKUP011
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP011
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP011
00055          ' *****'.                                                SNKUP011
00056                                                                   SNKUP011
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP011
00058                                              COMP SYNC.           SNKUP011
00059                                                                   SNKUP011
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP011
00061                                                                   SNKUP011
00062      EJECT                                                        SNKUP011
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP011
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP011
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP011
00066                                                                   SNKUP011
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP011
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP011
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP011
00070                                                                   SNKUP011
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP011
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP011
00073                                              COMP-3.              SNKUP011
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP011
00075                                              COMP-3.              SNKUP011
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP011
00077                                              COMP-3.              SNKUP011
00078                                                                   SNKUP011
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP011
00080                                                                   SNKUP011
00081      COPY SNRD002I.                                               SNKUP011
00082      EJECT                                                        SNKUP011
00083 ******************************************************************SNKUP011
00084 *    DB2 DATABASE AREA                                            SNKUP011
00085 ******************************************************************SNKUP011
00086                                                                   SNKUP011
00087 *    ASN ORDER ADDRESS TEXT                                       SNKUP011
00088                                                                   SNKUP011
00089      EXEC SQL                                                     SNKUP011
00090          INCLUDE TASOADT                                          SNKUP011
00091      END-EXEC.                                                    SNKUP011
00092                                                                   SNKUP011
00093      EJECT                                                        SNKUP011
00094 *    SQL COMMUNICATION AREA                                       SNKUP011
00095                                                                   SNKUP011
00096      EXEC SQL                                                     SNKUP011
00097          INCLUDE SQLCA                                            SNKUP011
00098      END-EXEC.                                                    SNKUP011
00099      EJECT                                                        SNKUP011
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP011
00101                                                                   SNKUP011
00102      COPY SQLCA2.                                                 SNKUP011
00103      EJECT                                                        SNKUP011
00104                                                                   SNKUP011
00105 ***************************************************************** SNKUP011
00106  PROCEDURE DIVISION.                                              SNKUP011
00107 ***************************************************************** SNKUP011
00108                                                                   SNKUP011
00109  A000-MAIN-DRIVER.                                                SNKUP011
00110                                                                   SNKUP011
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP011
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP011
00113                                                                   SNKUP011
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP011
00115                                                                   SNKUP011
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP011
00117                                                                   SNKUP011
00118      PERFORM B100-DELETE-ROWS                                     SNKUP011
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP011
00120                                                                   SNKUP011
00121      DISPLAY 'SNKUP011 RECORDS READ:    ' PV-RECS-READ.           SNKUP011
00122      DISPLAY 'SNKUP011 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP011
00123                                                                   SNKUP011
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP011
00125                                                                   SNKUP011
00126      GOBACK.                                                      SNKUP011
00127                                                                   SNKUP011
00128      EJECT                                                        SNKUP011
00129                                                                   SNKUP011
00130 ***************************************************************** SNKUP011
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP011
00132 ***************************************************************** SNKUP011
00133  B100-DELETE-ROWS.                                                SNKUP011
00134                                                                   SNKUP011
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP011
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP011
00137                                  TO ABEND-PARAGRAPH.              SNKUP011
00138                                                                   SNKUP011
00139      PERFORM B300-DELETE-ASOADT.                                  SNKUP011
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP011
00142          MOVE ZERO               TO PV-DELETE-COUNTER                CL**2
00143      END-IF.                                                      SNKUP011
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP011
00145                                                                   SNKUP011
00146      EJECT                                                        SNKUP011
00147                                                                   SNKUP011
00148 ***************************************************************** SNKUP011
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP011
00150 ***************************************************************** SNKUP011
00151  B200-READ-INPUT-FILE.                                            SNKUP011
00152                                                                   SNKUP011
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP011
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP011
00155                                  TO ABEND-PARAGRAPH.              SNKUP011
00156                                                                   SNKUP011
00157      READ ASNHDR-DELETE-FILE                                      SNKUP011
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP011
00159          AT END                                                   SNKUP011
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP011
00161                                                                   SNKUP011
00162      IF NOT PS-END-OF-INPUT                                       SNKUP011
00163          ADD 1                   TO PV-RECS-READ                  SNKUP011
00164      END-IF.                                                      SNKUP011
00165                                                                   SNKUP011
00166      EJECT                                                        SNKUP011
00167                                                                   SNKUP011
00168 ***************************************************************** SNKUP011
00169 *    THIS PARAGRAPH DELETES ALL ASOADT ROWS THAT ARE EQUAL TO     SNKUP011
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP011
00171 ***************************************************************** SNKUP011
00172  B300-DELETE-ASOADT.                                              SNKUP011
00173                                                                   SNKUP011
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP011
00175      MOVE 'B300-DELETE-ASOADT        '                            SNKUP011
00176                                  TO ABEND-PARAGRAPH.              SNKUP011
00177                                                                   SNKUP011
00178      EXEC SQL                                                     SNKUP011
00179          DELETE                                                   SNKUP011
00180          FROM   TASOADT                                           SNKUP011
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP011
00183                                                                   SNKUP011
00184      IF SQLCODE = ZERO                                            SNKUP011
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP011
00186          ADD +1                  TO PV-DELETE-COUNTER                CL**2
00187      ELSE                                                         SNKUP011
00188          IF SQLCODE = +100                                        SNKUP011
00189              CONTINUE                                             SNKUP011
00190          ELSE                                                     SNKUP011
00191              PERFORM Z998-SQL-ERROR                               SNKUP011
00192          END-IF                                                   SNKUP011
00193      END-IF.                                                      SNKUP011
00194                                                                   SNKUP011
00195      EJECT                                                        SNKUP011
00196                                                                   SNKUP011
00197 ***************************************************************** SNKUP011
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP011
00199 ***************************************************************** SNKUP011
00200  B400-COMMIT.                                                     SNKUP011
00201                                                                   SNKUP011
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP011
00203      MOVE 'B400-COMMIT               '                            SNKUP011
00204                                  TO ABEND-PARAGRAPH.              SNKUP011
00205                                                                   SNKUP011
00206      EXEC SQL                                                     SNKUP011
00207          COMMIT                                                   SNKUP011
00208      END-EXEC.                                                    SNKUP011
00209                                                                   SNKUP011
00210      IF SQLCODE = ZERO                                            SNKUP011
00211          CONTINUE                                                 SNKUP011
00212      ELSE                                                         SNKUP011
00213          PERFORM Z998-SQL-ERROR                                   SNKUP011
00214      END-IF.                                                      SNKUP011
00215                                                                   SNKUP011
00216      EJECT                                                        SNKUP011
00217                                                                   SNKUP011
00218 ***************************************************************** SNKUP011
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP011
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP011
00221 ***************************************************************** SNKUP011
00222  Z998-SQL-ERROR.                                                  SNKUP011
00223                                                                   SNKUP011
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP011
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP011
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP011
00227      DISPLAY '** PROGRAM          ** = SNKUP011'.                 SNKUP011
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP011
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP011
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP011
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP011
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP011
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP011
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP011
