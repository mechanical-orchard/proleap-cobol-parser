       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    IMKUP017.                                                 
       AUTHOR.        TESS BESTE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09/06/2001.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *    IMKUP017 - STORED PROCEDURE FOR TMDARVS UPDATE TRIGGER               
      *                                                                         
      * THIS STORED PROCEDURE IS CALLED FROM AN UPDATE TRIGGER ON               
      * TMDARVS. IT DETERMINES WHETHER A STYLE ADD, STYLE ATTRIBUTE             
      * CHANGE, OR STYLE DELETE BUSINESS EVENT HAS TAKEN PLACE, AND             
      * INSERTS MESSAGES FOR THE BUSINESS EVENTS TO THE ITEM EVENT              
      * MESSAGE BRIDGE TABLE.                                                   
      *                                                                         
      * *** THE STYLE ATTRIBUTES CHANGE 2 MESSAGE CREATION IS COMMENTED         
      * *** OUT UNTIL AN ITEM DOMAIN NEEDS TO SUBSCRIBE TO THIS MESSAGE.        
      *                                                                         
      ******************************************************************        
      *                                                                         
      * PROJECT HISTORY:                                                        
      *   DATE: 02/06/2002     CHANGE ID: WR22746    WHO: B. MCASEY             
      *   REASON FOR CHANGE : ADDED LOGIC TO PROCESS A STYLE EXPIRE,            
      *   WHERE THE ACTV_IND CHANGES FROM 'Y' TO 'N'.  TWO NEW RECORD           
      *   TYPES WILL NOW BE WRITTEN: 030 - STYLE DELETE/EXPIRE, AND             
      *   031 - STYLE DELETE/EXPIRE SKU LEVEL INFO.                             
      *                                                                         
      *   DATE: 08/13/2002     CHANGE ID: WR23861    WHO: D. THEEL              
      *   REASON FOR CHANGE : ADDED LOGIC TO STYLE ADD PROCESS TO               
      *   WRITE OUT THE TMDARVS ESL SEQ NBR.                                    
      *                                                                         
      *   DATE: 10/04/2002     CHANGE ID: WR23974    WHO: P. KEBER              
      *   REASON FOR CHANGE : ADDED LOGIC TO THE STYLE ADD AND STYLE            
      *   ATTRIBUTE CHANGE PROCESSES TO WRITE OUT THE TMDARVS RETAIL            
      *   CLASSIFICATION SEQUENCE NUMBER, AND THE TICKET TYPE                   
      *   DESCRIPTION, HANG FOLD DESCRIPTION, LABEL DESCRIPTION, ESL            
      *   DESCRIPTION, AND RETAIL CLASSIFICATION DESCRIPTION FROM               
      *   TVSCHVL.  RTL-CLSN-SEQ-NBR, DEPT-NBR, MAJ-CL-NBR, AND SUB-CL-         
      *   NBR WILL BE NEW INPUT PARAMETERS WITH THE LAST THREE BEING            
      *   USED TO REPLACE A CALL TO TMDSEAR.                                    
      *                                                                         
      *   DATE: 01/15/2003     CHANGE ID: WR23974    WHO: P. KEBER              
      *   REASON FOR CHANGE: CHANGED THE STYLE SKU DELETES TUPC CURSOR          
      *   TO REMOVE JOINS TO TUPCPLS AND TSKXREF SINCE SKU NUMBER CAN BE        
      *   DERIVED FROM THE UPC NUMBER.  ALSO ADDED RTL-CLSN-SEQ-NBR             
      *   CHECK FOR CHANGES TO DETERMINE IF A STYLE ATTRIBUTE CHANGE            
      *   RECORD SHOULD BE CREATED.                                             
      *                                                                         
      *   DATE: 01/16/2003     CHANGE ID: WR23974    WHO: P. KEBER              
      *   REASON FOR CHANGE: CREATE SKU DELETE MESSAGES PRIOR TO THE            
      *   STYLE DELETE MESSAGE FOR THE STYLE BEING DELETED.                     
      *                                                                         
      *   DATE: 10/30/2003     CHANGE ID: WR25951    WHO: P. KEBER              
      *   REASON FOR CHANGE: CREATE STYLE ADD 2 MESSAGE AND STYLE REPORT        
      *   HIERARCHY CHANGE MESSAGE.  REPLACED SLHTT WITH KY_CLSN AND            
      *   AND FBRCN WITH CLLCTN AND ADDED FNSH DESC IN THE STYLE ADD AND        
      *   STYLE ATTRIBUTES CHANGE MESSAGES.                                     
      *                                                                         
      *   DATE: 01/28/2004     CHANGE ID: WR24363    WHO: P. KEBER              
      *   REASON FOR CHANGE: DISPLAY MORE INFORMATION IN ERROR MESSAGES.        
      *                                                                         
      *   DATE: 04/13/2004     CHANGE ID: WR26741    WHO: P. KEBER              
      *   REASON FOR CHANGE: ADD NEW MAP RETAIL COLUMN TO THE STYLE ADD         
      *   AND STYLE ATTRIBUTES CHANGE MESSAGES.                                 
      *                                                                         
      *   DATE: 12/21/2005     CHANGE ID: WR29786    WHO: P. KEBER              
      *   REASON FOR CHANGE: ADD NEW PRICE TIER AND LIFESTYLE COLUMNS           
      *   TO THE STYLE ADD 2 AND STYLE ATTRIBUTES CHANGE 2 MESSAGES.            
      *   ADD THE OLD LABEL SEQUENCE NUMBER ON THE STYLE ATTRIBUTE              
      *   CHANGE MESSAGE.                                                       
      *                                                                         
      *   DATE: 03/07/2006     CHANGE ID: WR28545    WHO: P. KEBER              
      *   REASON FOR CHANGE : ADDED LOGIC TO CREATE A NEW NON-INVENTORY         
      *   VENDOR UPC DELETE MESSAGE (BUSINESS EVENT ID 125) AS PART OF          
      *   A STYLE DELETE.  ADD PLU_DWNLD_IND TO THE SKU DELETE MESSAGE.         
      *                                                                         
      *   DATE: 08/07/2006     CHANGE ID: WR30627    WHO: D. THEEL              
      *   REASON FOR CHANGE : ADDED LOGIC TO SEND THE LABEL TYPE CODE           
      *   ON STYLE ADD AND STYLE ATTRIBUTES CHANGE MESSAGES (005,015)           
      *   FOR THE PRIVATE BRAND ACCOUNTING PROJECT.                             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *****************************************************************         
      *****************************************************************         
                                                                                
           COPY IMRD008.                                                        
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-ESL-CHARTC-CDE       PIC S9(04)     VALUE +19 COMP.           
           05  PC-HNG-FLD-CHARTC-CDE   PIC S9(04)     VALUE +10 COMP.           
           05  PC-LBL-CHARTC-CDE       PIC S9(04)     VALUE +3  COMP.           
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2  COMP.           
           05  PC-MORE-THAN-MAX-RETRY  PIC S9(04)     VALUE +3  COMP.           
           05  PC-RTL-CLSN-CHARTC-CDE  PIC S9(04)     VALUE +6  COMP.           
           05  PC-TKT-TYP-CHARTC-CDE   PIC S9(04)     VALUE +2  COMP.           
           05  PC-FBRCN-CHARTC-CDE     PIC S9(04)     VALUE +1  COMP.           
           05  PC-PATRN-CHARTC-CDE     PIC S9(04)     VALUE +7  COMP.           
           05  PC-KY-CLSN-CHARTC-CDE   PIC S9(04)     VALUE +8  COMP.           
           05  PC-FNSH-CHARTC-CDE      PIC S9(04)     VALUE +9  COMP.           
           05  PC-CLLCTN-CHARTC-CDE    PIC S9(04)     VALUE +20 COMP.           
           05  PC-RPT-HIER-CHARTC-CDE  PIC S9(04)     VALUE +21 COMP.           
           05  PC-FUNC-CHARTC-CDE      PIC S9(04)     VALUE +22 COMP.           
           05  PC-SLHTT-CHARTC-CDE     PIC S9(04)     VALUE +23 COMP.           
           05  PC-BDY-STYL-CHARTC-CDE  PIC S9(04)     VALUE +24 COMP.           
           05  PC-CONST-CHARTC-CDE     PIC S9(04)     VALUE +25 COMP.           
           05  PC-GNDR-CHARTC-CDE      PIC S9(04)     VALUE +26 COMP.           
           05  PC-PRIC-TIER-CHARTC-CDE PIC S9(04)     VALUE +31 COMP.           
           05  PC-LFSTYL-CHARTC-CDE    PIC S9(04)     VALUE +32 COMP.           
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUP017'.         
           05  PC-ORIGIN-CNTRY         PIC  X(03)     VALUE 'US '.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-SQL-ERROR-SW         PIC X       VALUE 'N'.                   
               88  PS-SQL-ERROR                    VALUE 'Y'.                   
               88  PS-NO-SQL-ERROR                 VALUE 'N'.                   
           05  PS-END-OF-TUPC-CSR-SW        PIC X(01)  VALUE 'N'.               
               88  END-OF-TUPC-CSR                     VALUE 'Y'.               
               88  NOT-END-OF-TUPC-CSR                 VALUE 'N'.               
           05  PS-END-OF-VENDOR-UPC-CSR-SW  PIC X(01)  VALUE 'N'.               
               88  END-OF-VENDOR-UPC-CSR               VALUE 'Y'.               
               88  NOT-END-OF-VENDOR-UPC-CSR           VALUE 'N'.               
           05  PS-STYLE-VALID-DELETE-SW     PIC X(01)  VALUE 'N'.               
               88  PS-STYLE-VALID-DELETE               VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACE.                 
           05  PV-DB2-OPERATION        PIC  X(50)  VALUE SPACE.                 
           05  PV-DB2-KEY-INFO         PIC  X(50)  VALUE SPACE.                 
           05  PV-RETRY-COUNT          PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
           05  PV-MDSEAR-ID            PIC S9(06)V COMP-3 VALUE 0.              
           05  PV-VS-ID                PIC S9(09)  COMP   VALUE 0.              
           05  PV-UPC-COMP-10          PIC S9(10)  COMP   VALUE 0.              
           05  PV-IMT00000-LBL-TYP-CDE PIC X(01)   VALUE SPACE.                 
           05  PV-ITEM-UPC-NBR         PIC  9(15)  VALUE ZERO.                  
           05  PV-ITEM-UPC-RENUM REDEFINES PV-ITEM-UPC-NBR.                     
               10  FILLER              PIC  9(06).                              
               10  PV-ITEM-UPC-SKU-NUM PIC  9(08).                              
               10  PV-ITEM-UPC-CHKDGT  PIC  9(01).                              
                                                                                
           05  PV-TUPC-KEY-INFO.                                           CL**1
               10  FILLER              PIC X(18)   VALUE                   CL**1
                   'SUB_CL_MDSEAR_ID: '.                                        
               10  PV-TUPC-KEY-MDSEAR-ID                                   CL**1
                                       PIC Z(06)   VALUE ZERO.             CL**1
               10  FILLER              PIC X(08)   VALUE ' VS_ID: '.       CL**1
               10  PV-TUPC-KEY-VS-ID   PIC Z(09)   VALUE ZERO.             CL**1
                                                                                
           05  PV-TUPCSKU-KEY-INFO.                                        CL**1
               10  FILLER              PIC X(09)   VALUE                   CL**1
                   'UPC_NBR: '.                                                 
               10  PV-TUPCSKU-KEY-UPC-NBR                                  CL**1
                                       PIC Z(15)   VALUE ZERO.             CL**1
                                                                                
           05  PV-TVNDSTL-KEY-INFO.                                        CL**1
               10  FILLER              PIC X(07)   VALUE 'VS_ID: '.        CL**1
               10  PV-TVNDSTL-KEY-VS-ID                                    CL**1
                                       PIC Z(09)   VALUE ZERO.             CL**1
                                                                                
           05  PV-TVSCHVL-KEY-INFO.                                        CL**1
               10  FILLER              PIC X(12)   VALUE 'CHARTC_CDE: '.   CL**1
               10  PV-TVSCHVL-KEY-CDE  PIC Z(04)   VALUE ZERO.             CL**1
               10  FILLER              PIC X(17)   VALUE                   CL**1
                   ' CHARTC_SEQ_NBR: '.                                         
               10 PV-TVSCHVL-KEY-SEQ-NBR                                   CL**1
                                       PIC Z(04)   VALUE ZERO.             CL**1
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-COUNT                PIC S9(7)    VALUE ZERO                  
                                                    COMP-3.                     
      ******************************************************************        
      *  VENDOR STYLE TABLE                                                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  EXTERNAL ENTITY TABLE                                                  
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TEXTENT                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TUPC TABLE                                                             
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TUPCSKU TABLE                                                          
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TSKXREF TABLE                                                          
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TVSCHVL TABLE                                                          
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  ITEM LABEL (BRAND) TABLE                                               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE IMT00000                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DB2 COMMUNICATION AREA                                               
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TUPC CURSOR DECLARATION                                                
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE TUPC_CSR CURSOR FOR                                        
              SELECT DISTINCT UPC.UPC_ID                                        
                   , UPC.UPC_NBR                                                
                   , UPCSKU.PLU_DWNLD_IND                                       
                FROM TUPC UPC                                                   
                    ,TUPCSKU UPCSKU                                             
               WHERE UPC.SUB_CL_MDSEAR_ID = :PV-MDSEAR-ID                       
                 AND UPC.VS_ID            = :PV-VS-ID                           
                 AND UPC.UPC_TYP_CDE      = 'I'                                 
                 AND UPC.EFF_END_DTE      IS NULL                               
                 AND UPCSKU.UPC_NBR       = UPC.UPC_NBR                         
               FOR FETCH ONLY                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  VENDOR UPC CURSOR DECLARATION                                          
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE VENDOR_UPC_CSR CURSOR FOR                                  
              SELECT DISTINCT UPC.UPC_ID                                        
                   , UPC.INTR_UPC_ID                                            
                   , UPC.UPC_NBR                                                
                   , SKXREF.SKU                                                 
                FROM TUPC UPC                                                   
                    ,TUPCSKU UPCSKU                                             
                    ,TSKXREF SKXREF                                             
               WHERE UPC.SUB_CL_MDSEAR_ID = :PV-MDSEAR-ID                       
                 AND UPC.VS_ID            = :PV-VS-ID                           
                 AND UPC.UPC_TYP_CDE      = 'V'                                 
                 AND UPC.EFF_END_DTE      IS NULL                               
                 AND UPCSKU.UPC_NBR       = UPC.UPC_NBR                         
                 AND UPCSKU.PLU_DWNLD_IND = 'N'                                 
                 AND SKXREF.ITM_NBR       = UPCSKU.ITEM_NBR                     
               FOR FETCH ONLY                                                   
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
       01  LS-SUB-CL-MDSEAR-ID          PIC  S9(06) COMP-3.                     
       01  LS-VS-ID                     PIC  S9(09) COMP.                       
       01  LS-ACTV-IND-OLD              PIC   X(01).                            
       01  LS-ACTV-IND-NEW              PIC   X(01).                            
       01  LS-STKR-NBR-OLD              PIC  S9(04) COMP.                       
       01  LS-STKR-NBR-NEW              PIC  S9(04) COMP.                       
       01  LS-VS-DESC-OLD               PIC   X(30).                            
       01  LS-VS-DESC-NEW               PIC   X(30).                            
       01  LS-TKT-TYP-SEQ-NBR-OLD       PIC  S9(04) COMP.                       
       01  LS-TKT-TYP-SEQ-NBR-NEW       PIC  S9(04) COMP.                       
       01  LS-LBL-SEQ-NBR-OLD           PIC  S9(04) COMP.                       
       01  LS-LBL-SEQ-NBR-NEW           PIC  S9(04) COMP.                       
       01  LS-HNG-FOLD-SEQ-NBR-OLD      PIC  S9(04) COMP.                       
       01  LS-HNG-FOLD-SEQ-NBR-NEW      PIC  S9(04) COMP.                       
       01  LS-KY-CLSN-SEQ-NBR-OLD       PIC  S9(04) COMP.                       
       01  LS-KY-CLSN-SEQ-NBR-NEW       PIC  S9(04) COMP.                       
       01  LS-FBRCN-SEQ-NBR-OLD         PIC  S9(04) COMP.                       
       01  LS-FBRCN-SEQ-NBR-NEW         PIC  S9(04) COMP.                       
       01  LS-FNSH-SEQ-NBR-OLD          PIC  S9(04) COMP.                       
       01  LS-FNSH-SEQ-NBR-NEW          PIC  S9(04) COMP.                       
       01  LS-RMS-STYL-ID               PIC  S9(08) COMP-3.                     
       01  LS-STYL-RECLS-IND            PIC   X(01).                            
       01  LS-ESL-SEQ-NBR               PIC  S9(04) COMP.                       
       01  LS-RTL-CLSN-SEQ-NBR-OLD      PIC  S9(04) COMP.                       
       01  LS-RTL-CLSN-SEQ-NBR-NEW      PIC  S9(04) COMP.                       
       01  LS-DEPT-NBR                  PIC  S9(04) COMP-3.                     
       01  LS-MAJ-CL-NBR                PIC  S9(04) COMP-3.                     
       01  LS-SUB-CL-NBR                PIC  S9(04) COMP-3.                     
       01  LS-RPT-HIER-SEQ-NBR-OLD      PIC  S9(04) COMP.                       
       01  LS-RPT-HIER-SEQ-NBR-NEW      PIC  S9(04) COMP.                       
       01  LS-BDY-STYL-SEQ-NBR-OLD      PIC  S9(04) COMP.                       
       01  LS-BDY-STYL-SEQ-NBR-NEW      PIC  S9(04) COMP.                       
       01  LS-CONST-SEQ-NBR-OLD         PIC  S9(04) COMP.                       
       01  LS-CONST-SEQ-NBR-NEW         PIC  S9(04) COMP.                       
       01  LS-FUNC-SEQ-NBR-OLD          PIC  S9(04) COMP.                       
       01  LS-FUNC-SEQ-NBR-NEW          PIC  S9(04) COMP.                       
       01  LS-GNDR-SEQ-NBR-OLD          PIC  S9(04) COMP.                       
       01  LS-GNDR-SEQ-NBR-NEW          PIC  S9(04) COMP.                       
       01  LS-PATRN-SEQ-NBR-OLD         PIC  S9(04) COMP.                       
       01  LS-PATRN-SEQ-NBR-NEW         PIC  S9(04) COMP.                       
       01  LS-SLHTT-SEQ-NBR-OLD         PIC  S9(04) COMP.                       
       01  LS-SLHTT-SEQ-NBR-NEW         PIC  S9(04) COMP.                       
       01  LS-CLLCTN-SEQ-NBR-OLD        PIC  S9(04) COMP.                       
       01  LS-CLLCTN-SEQ-NBR-NEW        PIC  S9(04) COMP.                       
       01  LS-MAP-UNT-RTL-AMT-OLD       PIC  S9(7)V9(2) COMP-3.                 
       01  LS-MAP-UNT-RTL-AMT-NEW       PIC  S9(7)V9(2) COMP-3.                 
       01  LS-PRIC-TIER-SEQ-NBR-OLD     PIC  S9(04) COMP.                       
       01  LS-PRIC-TIER-SEQ-NBR-NEW     PIC  S9(04) COMP.                       
       01  LS-LFSTYL-SEQ-NBR-OLD        PIC  S9(04) COMP.                       
       01  LS-LFSTYL-SEQ-NBR-NEW        PIC  S9(04) COMP.                       
                                                                                
                                                                                
       PROCEDURE DIVISION USING  LS-SUB-CL-MDSEAR-ID                            
                               , LS-VS-ID                                       
                               , LS-ACTV-IND-OLD                                
                               , LS-ACTV-IND-NEW                                
                               , LS-STKR-NBR-OLD                                
                               , LS-STKR-NBR-NEW                                
                               , LS-VS-DESC-OLD                                 
                               , LS-VS-DESC-NEW                                 
                               , LS-TKT-TYP-SEQ-NBR-OLD                         
                               , LS-TKT-TYP-SEQ-NBR-NEW                         
                               , LS-LBL-SEQ-NBR-OLD                             
                               , LS-LBL-SEQ-NBR-NEW                             
                               , LS-HNG-FOLD-SEQ-NBR-OLD                        
                               , LS-HNG-FOLD-SEQ-NBR-NEW                        
                               , LS-KY-CLSN-SEQ-NBR-OLD                         
                               , LS-KY-CLSN-SEQ-NBR-NEW                         
                               , LS-FBRCN-SEQ-NBR-OLD                           
                               , LS-FBRCN-SEQ-NBR-NEW                           
                               , LS-FNSH-SEQ-NBR-OLD                            
                               , LS-FNSH-SEQ-NBR-NEW                            
                               , LS-RMS-STYL-ID                                 
                               , LS-STYL-RECLS-IND                              
                               , LS-ESL-SEQ-NBR                                 
                               , LS-RTL-CLSN-SEQ-NBR-OLD                        
                               , LS-RTL-CLSN-SEQ-NBR-NEW                        
                               , LS-DEPT-NBR                                    
                               , LS-MAJ-CL-NBR                                  
                               , LS-SUB-CL-NBR                                  
                               , LS-RPT-HIER-SEQ-NBR-OLD                        
                               , LS-RPT-HIER-SEQ-NBR-NEW                        
                               , LS-BDY-STYL-SEQ-NBR-OLD                        
                               , LS-BDY-STYL-SEQ-NBR-NEW                        
                               , LS-CONST-SEQ-NBR-OLD                           
                               , LS-CONST-SEQ-NBR-NEW                           
                               , LS-FUNC-SEQ-NBR-OLD                            
                               , LS-FUNC-SEQ-NBR-NEW                            
                               , LS-GNDR-SEQ-NBR-OLD                            
                               , LS-GNDR-SEQ-NBR-NEW                            
                               , LS-PATRN-SEQ-NBR-OLD                           
                               , LS-PATRN-SEQ-NBR-NEW                           
                               , LS-SLHTT-SEQ-NBR-OLD                           
                               , LS-SLHTT-SEQ-NBR-NEW                           
                               , LS-CLLCTN-SEQ-NBR-OLD                          
                               , LS-CLLCTN-SEQ-NBR-NEW                          
                               , LS-MAP-UNT-RTL-AMT-OLD                         
                               , LS-MAP-UNT-RTL-AMT-NEW                         
                               , LS-PRIC-TIER-SEQ-NBR-OLD                       
                               , LS-PRIC-TIER-SEQ-NBR-NEW                       
                               , LS-LFSTYL-SEQ-NBR-OLD                          
                               , LS-LFSTYL-SEQ-NBR-NEW.                         
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
                                                                                
      *    THE TIMESTAMP WILL BE FILLED IN BY THE PROGRAM THAT READS            
      *    THE EVENT MSG TABLE AND WRITES TO MQ.                                
           MOVE SPACE TO IM008-CRE-TMST.                                        
                                                                                
      *    EXAMINE THE INPUT ARGUMENTS TO DETERMINE WHAT KIND OF                
      *    BUSINESS EVENT HAS OCCURRED.                                         
           INITIALIZE IM008-BUSINESS-EVENT-ID.                                  
                                                                                
           IF  LS-ACTV-IND-OLD = 'N'                                            
             AND LS-ACTV-IND-NEW = 'Y'                                          
               SET IM008-BUS-EVENT-STYLE-ADD TO TRUE                            
               PERFORM 2100-PROCESS-STYLE-ADD                                   
           ELSE                                                                 
               IF LS-ACTV-IND-NEW = 'Y'                                         
                   IF LS-STKR-NBR-OLD NOT = LS-STKR-NBR-NEW                     
                     OR LS-VS-DESC-OLD NOT = LS-VS-DESC-NEW                     
                     OR LS-TKT-TYP-SEQ-NBR-OLD NOT =                            
                        LS-TKT-TYP-SEQ-NBR-NEW                                  
                     OR LS-LBL-SEQ-NBR-OLD NOT = LS-LBL-SEQ-NBR-NEW             
                     OR LS-HNG-FOLD-SEQ-NBR-OLD NOT =                           
                        LS-HNG-FOLD-SEQ-NBR-NEW                                 
                     OR LS-KY-CLSN-SEQ-NBR-OLD NOT =                            
                        LS-KY-CLSN-SEQ-NBR-NEW                                  
                     OR LS-FNSH-SEQ-NBR-OLD NOT = LS-FNSH-SEQ-NBR-NEW           
                     OR LS-RTL-CLSN-SEQ-NBR-OLD NOT =                           
                        LS-RTL-CLSN-SEQ-NBR-NEW                                 
                     OR LS-CLLCTN-SEQ-NBR-OLD NOT =                             
                        LS-CLLCTN-SEQ-NBR-NEW                                   
                     OR LS-MAP-UNT-RTL-AMT-OLD NOT =                            
                        LS-MAP-UNT-RTL-AMT-NEW                                  
                       SET IM008-BUS-EVENT-STYLE-ATTR-CHG TO TRUE               
                       PERFORM 2200-PROCESS-STYLE-ATTR-CHG                      
                   END-IF                                                       
                   IF LS-FBRCN-SEQ-NBR-OLD NOT = LS-FBRCN-SEQ-NBR-NEW           
                     OR LS-RPT-HIER-SEQ-NBR-OLD NOT =                           
                        LS-RPT-HIER-SEQ-NBR-NEW                                 
                     OR LS-BDY-STYL-SEQ-NBR-OLD NOT =                           
                        LS-BDY-STYL-SEQ-NBR-NEW                                 
                     OR LS-CONST-SEQ-NBR-OLD NOT = LS-CONST-SEQ-NBR-NEW         
                     OR LS-FUNC-SEQ-NBR-OLD NOT = LS-FUNC-SEQ-NBR-NEW           
                     OR LS-GNDR-SEQ-NBR-OLD NOT = LS-GNDR-SEQ-NBR-NEW           
                     OR LS-PATRN-SEQ-NBR-OLD NOT = LS-PATRN-SEQ-NBR-NEW         
                     OR LS-SLHTT-SEQ-NBR-OLD NOT = LS-SLHTT-SEQ-NBR-NEW         
                       SET IM008-BUS-EVENT-RPT-HIER-CHG TO TRUE                 
                       PERFORM 3200-PROCESS-STYLE-RPTHIER-CHG                   
                   END-IF                                                       
                   IF LS-PRIC-TIER-SEQ-NBR-OLD NOT =                            
                        LS-PRIC-TIER-SEQ-NBR-NEW                                
                     OR LS-LFSTYL-SEQ-NBR-OLD NOT =                             
                        LS-LFSTYL-SEQ-NBR-NEW                                   
                       SET IM008-BUS-EVENT-STYL-ATTR-CHG2 TO TRUE               
      * *** THE STYLE ATTRIBUTES CHANGE 2 MESSAGE CREATION IS COMMENTED         
      * *** OUT UNTIL AN ITEM DOMAIN NEEDS TO SUBSCRIBE TO THIS MESSAGE.        
      *                PERFORM 3300-PROCESS-STYLE-ATTR-CHG2                     
                   END-IF                                                       
               ELSE                                                             
                   IF LS-ACTV-IND-OLD = 'Y'                                     
                     AND LS-ACTV-IND-NEW = 'N'                                  
                     AND LS-STYL-RECLS-IND = 'N'                                
                       SET IM008-BUS-EVENT-STYLE-DELETE TO TRUE                 
                       PERFORM 3100-PROCESS-STYLE-DEL                           
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE STYLE ADD MESSAGES FOR THE STYLE                             
      *----------------------------------------------------------------*        
       2100-PROCESS-STYLE-ADD.                                                  
                                                                                
           PERFORM 2300-BUILD-MSG-KEY.                                          
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 2400-BUILD-STYLE-ADD-DATA                                
               IF PS-NO-SQL-ERROR                                               
                   COMPUTE IMT00001-MSG-TXT-LEN                                 
                         = LENGTH OF IMRD008-RECORD                             
                         - LENGTH OF IM008-STYLE-ADD-FILLER                     
                   PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               SET IM008-BUS-EVENT-STYLE-ADD2 TO TRUE                           
               PERFORM 2410-BUILD-STYLE-ADD2-DATA                               
               IF PS-NO-SQL-ERROR                                               
                   COMPUTE IMT00001-MSG-TXT-LEN                                 
                         = LENGTH OF IMRD008-RECORD                             
                         - LENGTH OF IM008-STYLE-ADD2-FILLER                    
                   PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE STYLE ATTRIBUTES CHANGE MESSAGE FOR THE STYLE                
      *----------------------------------------------------------------*        
       2200-PROCESS-STYLE-ATTR-CHG.                                             
                                                                                
           PERFORM 2300-BUILD-MSG-KEY.                                          
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 2500-BUILD-STYLE-CHG-DATA                                
               IF PS-NO-SQL-ERROR                                               
                   COMPUTE IMT00001-MSG-TXT-LEN                                 
                         = LENGTH OF IMRD008-RECORD                             
                         - LENGTH OF IM008-STYL-ATTR-FILLER                     
                   PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE IMT_EVNT_MSG_BRG RECORD MESSAGE KEY                           
      *----------------------------------------------------------------*        
       2300-BUILD-MSG-KEY.                                                      
                                                                                
           PERFORM 8000-GET-TVNDSTL-TEXTENT.                                    
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               INITIALIZE IM008-ITEM-KEY-DATA                                   
               MOVE LS-VS-ID            TO IM008-ITEM-KEY-VS-ID                 
               MOVE VNDSTL-ENT-ID       TO IM008-ITEM-KEY-ENT-ID                
               MOVE EXTENT-DUN-NBR      TO IM008-ITEM-KEY-DUN-NBR               
               MOVE VNDSTL-VS-NBR       TO IM008-ITEM-KEY-VS-NBR                
               MOVE LS-SUB-CL-MDSEAR-ID TO IM008-ITEM-KEY-SUBCL-MDSE-ID         
               MOVE LS-DEPT-NBR         TO IM008-ITEM-KEY-DEPT                  
               MOVE LS-MAJ-CL-NBR       TO IM008-ITEM-KEY-CLASS                 
               MOVE LS-SUB-CL-NBR       TO IM008-ITEM-KEY-SUBCL                 
               MOVE LS-RMS-STYL-ID      TO IM008-ITEM-KEY-RMS-STYL-ID           
               MOVE PC-ORIGIN-CNTRY     TO IM008-ITEM-KEY-ORIGIN-CNTRY          
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE STYLE ADD MESSAGE FOR THE STYLE                               
      *----------------------------------------------------------------*        
       2400-BUILD-STYLE-ADD-DATA.                                               
                                                                                
           INITIALIZE IM008-STYLE-ADD-DATA.                                     
           MOVE LS-STKR-NBR-NEW         TO IM008-STYLE-ADD-STKR-NBR.            
           MOVE LS-VS-DESC-NEW          TO IM008-STYLE-ADD-VS-DESC.             
           MOVE LS-TKT-TYP-SEQ-NBR-NEW  TO IM008-STYLE-ADD-TKT-TYP.             
           MOVE LS-LBL-SEQ-NBR-NEW      TO IM008-STYLE-ADD-LBL.                 
           MOVE LS-HNG-FOLD-SEQ-NBR-NEW TO IM008-STYLE-ADD-HNG-FOLD.            
           MOVE LS-KY-CLSN-SEQ-NBR-NEW  TO IM008-STYLE-ADD-KY-CLSN.             
           MOVE LS-CLLCTN-SEQ-NBR-NEW   TO IM008-STYLE-ADD-CLLCTN.              
           MOVE LS-FNSH-SEQ-NBR-NEW     TO IM008-STYLE-ADD-FNSH.                
           MOVE VNDSTL-RTL-UM-CDE       TO IM008-STYLE-ADD-RTL-UM-CDE.          
           MOVE LS-ESL-SEQ-NBR          TO IM008-STYLE-ADD-ESL.                 
           MOVE LS-RTL-CLSN-SEQ-NBR-NEW TO IM008-STYLE-ADD-RTL-CLSN.            
           MOVE LS-MAP-UNT-RTL-AMT-NEW  TO IM008-STYLE-ADD-MAP-UNT-RTL.         
           IF LS-TKT-TYP-SEQ-NBR-NEW = ZERO                                     
               MOVE SPACES              TO IM008-STYLE-ADD-TKT-TYP-DESC         
           ELSE                                                                 
               MOVE PC-TKT-TYP-CHARTC-CDE                                       
                                        TO VSCHVL-CHARTC-CDE                    
               MOVE LS-TKT-TYP-SEQ-NBR-NEW                                      
                                        TO VSCHVL-CHARTC-SEQ-NBR                
               PERFORM 8100-GET-TVSCHVL-DESC                                    
               MOVE VSCHVL-CHARTC-VAL-DESC                                      
                                        TO IM008-STYLE-ADD-TKT-TYP-DESC         
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-LBL-SEQ-NBR-NEW = ZERO                                     
                   MOVE SPACES          TO IM008-STYLE-ADD-LBL-DESC             
                   MOVE SPACE           TO IM008-STYLE-ADD-LBL-TYP-CDE          
               ELSE                                                             
                   MOVE PC-LBL-CHARTC-CDE                                       
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-LBL-SEQ-NBR-NEW                                      
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-LBL-DESC             
                   PERFORM 8150-GET-NEW-LBL-TYP-CDE                             
                   MOVE IMT00000-LBL-TYP-CDE                                    
                                        TO IM008-STYLE-ADD-LBL-TYP-CDE          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-HNG-FOLD-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES          TO IM008-STYLE-ADD-HNG-FOLD-DESC        
               ELSE                                                             
                   MOVE PC-HNG-FLD-CHARTC-CDE                                   
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-HNG-FOLD-SEQ-NBR-NEW                                 
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-HNG-FOLD-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-ESL-SEQ-NBR = ZERO                                         
                   MOVE SPACES          TO IM008-STYLE-ADD-ESL-DESC             
               ELSE                                                             
                   MOVE PC-ESL-CHARTC-CDE                                       
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-ESL-SEQ-NBR  TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-ESL-DESC             
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-RTL-CLSN-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES          TO IM008-STYLE-ADD-RTL-CLSN-DESC        
               ELSE                                                             
                   MOVE PC-RTL-CLSN-CHARTC-CDE                                  
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-RTL-CLSN-SEQ-NBR-NEW                                 
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-RTL-CLSN-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-KY-CLSN-SEQ-NBR-NEW = ZERO                                 
                   MOVE SPACES          TO IM008-STYLE-ADD-KY-CLSN-DESC         
               ELSE                                                             
                   MOVE PC-KY-CLSN-CHARTC-CDE                                   
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-KY-CLSN-SEQ-NBR-NEW                                  
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-KY-CLSN-DESC         
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-CLLCTN-SEQ-NBR-NEW = ZERO                                  
                   MOVE SPACES          TO IM008-STYLE-ADD-CLLCTN-DESC          
               ELSE                                                             
                   MOVE PC-CLLCTN-CHARTC-CDE                                    
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-CLLCTN-SEQ-NBR-NEW                                   
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-CLLCTN-DESC          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-FNSH-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYLE-ADD-FNSH-DESC            
               ELSE                                                             
                   MOVE PC-FNSH-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-FNSH-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD-FNSH-DESC            
               END-IF                                                           
           END-IF.                                                              
           MOVE LOW-VALUES              TO IM008-STYLE-ADD-FILLER.              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE STYLE ADD 2 MESSAGE FOR THE STYLE CONTAINING REPORT           
      * HIERARCHY ATTRIBUTES INFORMATION                                        
      *----------------------------------------------------------------*        
       2410-BUILD-STYLE-ADD2-DATA.                                              
                                                                                
           INITIALIZE IM008-STYLE-ADD2-DATA.                                    
           MOVE LS-FBRCN-SEQ-NBR-NEW    TO IM008-STYLE-ADD2-FBRCN.              
           MOVE LS-RPT-HIER-SEQ-NBR-NEW TO IM008-STYLE-ADD2-RPT-HIER.           
           MOVE LS-BDY-STYL-SEQ-NBR-NEW TO IM008-STYLE-ADD2-BDY-STYL.           
           MOVE LS-CONST-SEQ-NBR-NEW    TO IM008-STYLE-ADD2-CONST.              
           MOVE LS-FUNC-SEQ-NBR-NEW     TO IM008-STYLE-ADD2-FUNC.               
           MOVE LS-GNDR-SEQ-NBR-NEW     TO IM008-STYLE-ADD2-GNDR.               
           MOVE LS-PATRN-SEQ-NBR-NEW    TO IM008-STYLE-ADD2-PATRN.              
           MOVE LS-SLHTT-SEQ-NBR-NEW    TO IM008-STYLE-ADD2-SLHTT.              
           MOVE LS-PRIC-TIER-SEQ-NBR-NEW                                        
                                        TO IM008-STYLE-ADD2-PRIC-TIER.          
           MOVE LS-LFSTYL-SEQ-NBR-NEW   TO IM008-STYLE-ADD2-LFSTYL.             
           IF LS-FBRCN-SEQ-NBR-NEW = ZERO                                       
               MOVE SPACES              TO IM008-STYLE-ADD2-FBRCN-DESC          
           ELSE                                                                 
               MOVE PC-FBRCN-CHARTC-CDE TO VSCHVL-CHARTC-CDE                    
               MOVE LS-FBRCN-SEQ-NBR-NEW                                        
                                        TO VSCHVL-CHARTC-SEQ-NBR                
               PERFORM 8100-GET-TVSCHVL-DESC                                    
               MOVE VSCHVL-CHARTC-VAL-DESC                                      
                                        TO IM008-STYLE-ADD2-FBRCN-DESC          
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-RPT-HIER-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES         TO IM008-STYLE-ADD2-RPT-HIER-DESC        
               ELSE                                                             
                   MOVE PC-RPT-HIER-CHARTC-CDE                                  
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-RPT-HIER-SEQ-NBR-NEW                                 
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYLE-ADD2-RPT-HIER-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-BDY-STYL-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES         TO IM008-STYLE-ADD2-BDY-STYL-DESC        
               ELSE                                                             
                   MOVE PC-BDY-STYL-CHARTC-CDE                                  
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-BDY-STYL-SEQ-NBR-NEW                                 
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYLE-ADD2-BDY-STYL-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-CONST-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYLE-ADD2-CONST-DESC          
               ELSE                                                             
                   MOVE PC-CONST-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-CONST-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-CONST-DESC          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-FUNC-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYLE-ADD2-FUNC-DESC           
               ELSE                                                             
                   MOVE PC-FUNC-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-FUNC-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-FUNC-DESC           
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-GNDR-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYLE-ADD2-GNDR-DESC           
               ELSE                                                             
                   MOVE PC-GNDR-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-GNDR-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-GNDR-DESC           
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-PATRN-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYLE-ADD2-PATRN-DESC          
               ELSE                                                             
                   MOVE PC-PATRN-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-PATRN-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-PATRN-DESC          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-SLHTT-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYLE-ADD2-SLHTT-DESC          
               ELSE                                                             
                   MOVE PC-SLHTT-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-SLHTT-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-SLHTT-DESC          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-PRIC-TIER-SEQ-NBR-NEW = ZERO                               
                   MOVE SPACES         TO IM008-STYLE-ADD2-PRICTIER-DESC        
               ELSE                                                             
                   MOVE PC-PRIC-TIER-CHARTC-CDE                                 
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-PRIC-TIER-SEQ-NBR-NEW                                
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYLE-ADD2-PRICTIER-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-LFSTYL-SEQ-NBR-NEW = ZERO                                  
                   MOVE SPACES          TO IM008-STYLE-ADD2-LFSTYL-DESC         
               ELSE                                                             
                   MOVE PC-LFSTYL-CHARTC-CDE                                    
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-LFSTYL-SEQ-NBR-NEW                                   
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYLE-ADD2-LFSTYL-DESC         
               END-IF                                                           
           END-IF.                                                              
           MOVE LOW-VALUES              TO IM008-STYLE-ADD2-FILLER.             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE STYLE ATTRIBUTES CHANGE MESSAGE FOR THE STYLE                 
      *----------------------------------------------------------------*        
       2500-BUILD-STYLE-CHG-DATA.                                               
                                                                                
           INITIALIZE IM008-STYLE-ATTR-CHG-DATA.                                
                                                                                
           IF LS-STKR-NBR-OLD NOT =                                             
              LS-STKR-NBR-NEW                                                   
               SET IM008-STYL-ATTR-STKR-NBR-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-STKR-NBR-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-VS-DESC-OLD NOT =                                              
              LS-VS-DESC-NEW                                                    
               SET IM008-STYL-ATTR-VS-DESC-CHG   TO TRUE                        
           ELSE                                                                 
               SET IM008-STYL-ATTR-VS-DESC-NOCHG TO TRUE                        
           END-IF.                                                              
                                                                                
           IF LS-TKT-TYP-SEQ-NBR-OLD NOT =                                      
              LS-TKT-TYP-SEQ-NBR-NEW                                            
               SET IM008-STYL-ATTR-TKT-TYP-CHG   TO TRUE                        
               SET IM008-STYL-ATTR-TKT-DSC-CHG   TO TRUE                        
           ELSE                                                                 
               SET IM008-STYL-ATTR-TKT-TYP-NOCHG TO TRUE                        
               SET IM008-STYL-ATTR-TKT-DSC-NOCHG TO TRUE                        
           END-IF.                                                              
                                                                                
           IF LS-LBL-SEQ-NBR-OLD NOT =                                          
              LS-LBL-SEQ-NBR-NEW                                                
               SET IM008-STYL-ATTR-LBL-CHG        TO TRUE                       
               SET IM008-STYL-ATTR-LBL-DESC-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-LBL-NOCHG      TO TRUE                       
               SET IM008-STYL-ATTR-LBL-DESC-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-HNG-FOLD-SEQ-NBR-OLD NOT =                                     
              LS-HNG-FOLD-SEQ-NBR-NEW                                           
               SET IM008-STYL-ATTR-HNG-FOLD-CHG   TO TRUE                       
               SET IM008-STYL-ATTR-HNG-DSC-CHG    TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-HNG-FOLD-NOCHG TO TRUE                       
               SET IM008-STYL-ATTR-HNG-DSC-NOCHG  TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-KY-CLSN-SEQ-NBR-OLD NOT =                                      
              LS-KY-CLSN-SEQ-NBR-NEW                                            
               SET IM008-STYL-ATTR-KY-CLSN-CHG TO TRUE                          
               SET IM008-STYL-ATTR-KY-CLS-DSC-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-KY-CLSN-NOCHG TO TRUE                        
               SET IM008-STYL-ATTR-KY-CLS-DSC-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-CLLCTN-SEQ-NBR-OLD NOT =                                       
              LS-CLLCTN-SEQ-NBR-NEW                                             
               SET IM008-STYL-ATTR-CLLCTN-CHG TO TRUE                           
               SET IM008-STYL-ATTR-CLLCTN-DSC-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-CLLCTN-NOCHG TO TRUE                         
               SET IM008-STYL-ATTR-CLLCTN-DSC-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-FNSH-SEQ-NBR-OLD NOT =                                         
              LS-FNSH-SEQ-NBR-NEW                                               
               SET IM008-STYL-ATTR-FNSH-CHG   TO TRUE                           
               SET IM008-STYL-ATTR-FNSH-DSC-CHG TO TRUE                         
           ELSE                                                                 
               SET IM008-STYL-ATTR-FNSH-NOCHG TO TRUE                           
               SET IM008-STYL-ATTR-FNSH-DSC-NOC TO TRUE                         
           END-IF.                                                              
                                                                                
           IF LS-RTL-CLSN-SEQ-NBR-OLD NOT =                                     
              LS-RTL-CLSN-SEQ-NBR-NEW                                           
               SET IM008-STYL-ATTR-RTL-CLSN-CHG   TO TRUE                       
               SET IM008-STYL-ATTR-RTL-CL-DSC-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR-RTL-CLSN-NOCHG TO TRUE                       
               SET IM008-STYL-ATTR-RTL-CL-DSC-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-MAP-UNT-RTL-AMT-OLD NOT =                                      
              LS-MAP-UNT-RTL-AMT-NEW                                            
               SET IM008-STYL-ATTR-MAP-RTL-CHG   TO TRUE                        
           ELSE                                                                 
               SET IM008-STYL-ATTR-MAP-RTL-NOCHG TO TRUE                        
           END-IF.                                                              
                                                                                
           MOVE LS-STKR-NBR-NEW         TO IM008-STYL-ATTR-STKR-NBR.            
           MOVE LS-VS-DESC-NEW          TO IM008-STYL-ATTR-VS-DESC.             
           MOVE LS-TKT-TYP-SEQ-NBR-NEW  TO IM008-STYL-ATTR-TKT-TYP.             
           MOVE LS-LBL-SEQ-NBR-NEW      TO IM008-STYL-ATTR-LBL.                 
           MOVE LS-LBL-SEQ-NBR-OLD      TO IM008-STYL-ATTR-OLD-LBL.             
           MOVE LS-HNG-FOLD-SEQ-NBR-NEW TO IM008-STYL-ATTR-HNG-FOLD.            
           MOVE LS-KY-CLSN-SEQ-NBR-NEW  TO IM008-STYL-ATTR-KY-CLSN.             
           MOVE LS-CLLCTN-SEQ-NBR-NEW   TO IM008-STYL-ATTR-CLLCTN.              
           MOVE LS-FNSH-SEQ-NBR-NEW     TO IM008-STYL-ATTR-FNSH.                
           MOVE LS-RTL-CLSN-SEQ-NBR-NEW TO IM008-STYL-ATTR-RTL-CLSN.            
           MOVE LS-MAP-UNT-RTL-AMT-NEW  TO IM008-STYL-ATTR-MAP-UNT-RTL.         
           IF LS-TKT-TYP-SEQ-NBR-NEW = ZERO                                     
               MOVE SPACES              TO IM008-STYL-ATTR-TKT-TYP-DESC         
           ELSE                                                                 
               MOVE PC-TKT-TYP-CHARTC-CDE                                       
                                        TO VSCHVL-CHARTC-CDE                    
               MOVE LS-TKT-TYP-SEQ-NBR-NEW                                      
                                        TO VSCHVL-CHARTC-SEQ-NBR                
               PERFORM 8100-GET-TVSCHVL-DESC                                    
               MOVE VSCHVL-CHARTC-VAL-DESC                                      
                                        TO IM008-STYL-ATTR-TKT-TYP-DESC         
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-LBL-SEQ-NBR-NEW = ZERO                                     
                   MOVE SPACES         TO IM008-STYL-ATTR-LBL-DESC              
                   MOVE SPACE          TO IM008-STYL-ATTR-LBL-TYP-CDE           
                   SET IM008-STYL-ATTR-LBL-TYP-NOCHG TO TRUE                    
               ELSE                                                             
                   MOVE PC-LBL-CHARTC-CDE                                       
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-LBL-SEQ-NBR-NEW                                      
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-LBL-DESC             
                   PERFORM 8150-GET-NEW-LBL-TYP-CDE                             
                   MOVE IMT00000-LBL-TYP-CDE                                    
                                    TO IM008-STYL-ATTR-LBL-TYP-CDE              
                   IF IM008-STYL-ATTR-LBL-CHG                                   
                       MOVE LS-LBL-SEQ-NBR-OLD                                  
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                       PERFORM 8175-GET-OLD-LBL-TYP-CDE                         
                       IF PV-IMT00000-LBL-TYP-CDE =                             
                          IMT00000-LBL-TYP-CDE                                  
                           SET IM008-STYL-ATTR-LBL-TYP-NOCHG TO TRUE            
                       ELSE                                                     
                           SET IM008-STYL-ATTR-LBL-TYP-CHG TO TRUE              
                       END-IF                                                   
                   ELSE                                                         
                       SET IM008-STYL-ATTR-LBL-TYP-NOCHG TO TRUE                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-HNG-FOLD-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES          TO IM008-STYL-ATTR-HNG-FOLD-DESC        
               ELSE                                                             
                   MOVE PC-HNG-FLD-CHARTC-CDE                                   
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-HNG-FOLD-SEQ-NBR-NEW                                 
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-HNG-FOLD-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-RTL-CLSN-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES          TO IM008-STYL-ATTR-RTL-CLSN-DESC        
               ELSE                                                             
                   MOVE PC-RTL-CLSN-CHARTC-CDE                                  
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-RTL-CLSN-SEQ-NBR-NEW                                 
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-RTL-CLSN-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-KY-CLSN-SEQ-NBR-NEW = ZERO                                 
                   MOVE SPACES          TO IM008-STYL-ATTR-KY-CLSN-DESC         
               ELSE                                                             
                   MOVE PC-KY-CLSN-CHARTC-CDE                                   
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-KY-CLSN-SEQ-NBR-NEW                                  
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-KY-CLSN-DESC         
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-CLLCTN-SEQ-NBR-NEW = ZERO                                  
                   MOVE SPACES          TO IM008-STYL-ATTR-CLLCTN-DESC          
               ELSE                                                             
                   MOVE PC-CLLCTN-CHARTC-CDE                                    
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-CLLCTN-SEQ-NBR-NEW                                   
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-CLLCTN-DESC          
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-FNSH-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYL-ATTR-FNSH-DESC            
               ELSE                                                             
                   MOVE PC-FNSH-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-FNSH-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-ATTR-FNSH-DESC            
               END-IF                                                           
           END-IF.                                                              
           MOVE LOW-VALUES              TO IM008-STYL-ATTR-FILLER.              
                                                                                
      *----------------------------------------------------------------*        
      * BUILD NON-INVENTORY VENDOR UPC DELETE MESSAGES FOR THE VENDOR           
      * UPC'S UNDER THE STYLE HAVING A PLU_DWNLD_IND EQUAL TO 'N',              
      * BUILD SKU DELETE MESSAGES FOR THE SKU(S) UNDER THE STYLE, THEN          
      * BUILD THE MESSAGE FOR THE STYLE DELETE/EXPIRE.                 *        
      *----------------------------------------------------------------*        
       3100-PROCESS-STYLE-DEL.                                                  
                                                                                
           MOVE LS-SUB-CL-MDSEAR-ID  TO PV-MDSEAR-ID.                           
           MOVE LS-VS-ID             TO PV-VS-ID.                               
           PERFORM 2300-BUILD-MSG-KEY.                                          
                                                                                
      *    BUILD NON-INVENTORY VENDOR UPC DELETE MESSAGES FOR THE VENDOR        
      *    UPC'S UNDER THE STYLE HAVING A PLU_DWNLD_IND EQUAL TO 'N'.           
           MOVE LOW-VALUES  TO IM008-ITEM-ACTION-DATA.                          
           COMPUTE IMT00001-MSG-TXT-LEN                                         
                 = LENGTH OF IMRD008-RECORD                                     
                 - LENGTH OF IM008-ITEM-ACTION-DATA.                            
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4400-OPEN-VENDOR-UPC-CSR                                 
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4500-FETCH-VENDOR-UPC-CSR                                
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               SET IM008-BUS-EVENT-NONINV-UPC-DEL TO TRUE                       
               PERFORM 4600-PROCESS-VENDOR-UPC-CSR                              
                 UNTIL END-OF-VENDOR-UPC-CSR                                    
                    OR PS-SQL-ERROR                                             
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4700-CLOSE-VENDOR-UPC-CSR                                
           END-IF.                                                              
                                                                                
      *    BUILD SKU DELETE MESSAGES FOR THE SKU(S) UNDER THE STYLE             
           MOVE LOW-VALUES  TO IM008-SKU-DEL-FILLER.                            
           COMPUTE IMT00001-MSG-TXT-LEN                                         
                 = LENGTH OF IMRD008-RECORD                                     
                 - LENGTH OF IM008-SKU-DEL-FILLER.                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4000-OPEN-TUPC-CSR                                       
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4100-FETCH-TUPC-CSR                                      
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               SET IM008-BUS-EVENT-STYLE-SKU-DEL TO TRUE                        
               PERFORM 4200-PROCESS-TUPC-CSR                                    
                 UNTIL END-OF-TUPC-CSR                                          
                    OR PS-SQL-ERROR                                             
           END-IF.                                                              
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4300-CLOSE-TUPC-CSR                                      
           END-IF.                                                              
                                                                                
      *    BUILD THE MESSAGE FOR THE STYLE DELETE                               
           IF PS-NO-SQL-ERROR                                                   
               SET IM008-BUS-EVENT-STYLE-DELETE TO TRUE                         
               MOVE LOW-VALUES  TO IM008-ITEM-ACTION-DATA                       
               COMPUTE IMT00001-MSG-TXT-LEN                                     
                     = LENGTH OF IMRD008-RECORD                                 
                     - LENGTH OF IM008-ITEM-ACTION-DATA                         
               MOVE ZEROES      TO IM008-ITEM-KEY-SKU                           
                                   IM008-ITEM-KEY-INTR-UPC-ID                   
                                   IM008-ITEM-KEY-UPC-ID                        
                                   IM008-ITEM-KEY-UPC-NBR                       
               PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE STYLE REPORT HIERARCHY ATTRIBUTES CHANGE MESSAGE             
      *----------------------------------------------------------------*        
       3200-PROCESS-STYLE-RPTHIER-CHG.                                          
                                                                                
           PERFORM 2300-BUILD-MSG-KEY.                                          
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 3250-BUILD-RPT-HIER-CHG-DATA                             
               IF PS-NO-SQL-ERROR                                               
                   COMPUTE IMT00001-MSG-TXT-LEN                                 
                         = LENGTH OF IMRD008-RECORD                             
                         - LENGTH OF IM008-STYL-RPT-CHG-FILLER                  
                   PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE STYLE REPORT HIERARCHY ATTRIBUTES CHANGE MESSAGE              
      *----------------------------------------------------------------*        
       3250-BUILD-RPT-HIER-CHG-DATA.                                            
                                                                                
           INITIALIZE IM008-STYLE-RPT-HIER-CHG-DATA.                            
                                                                                
           IF LS-FBRCN-SEQ-NBR-OLD NOT = LS-FBRCN-SEQ-NBR-NEW                   
               SET IM008-STYL-RPT-CHG-FBRCN-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-FBRCN-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-RPT-HIER-SEQ-NBR-OLD NOT = LS-RPT-HIER-SEQ-NBR-NEW             
               SET IM008-STYL-RPT-CHG-RPTHIER-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-RPTHIER-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-BDY-STYL-SEQ-NBR-OLD NOT = LS-BDY-STYL-SEQ-NBR-NEW             
               SET IM008-STYL-RPT-CHG-BDYSTYL-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-BDYSTYL-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-CONST-SEQ-NBR-OLD NOT = LS-CONST-SEQ-NBR-NEW                   
               SET IM008-STYL-RPT-CHG-CONST-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-CONST-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-FUNC-SEQ-NBR-OLD NOT = LS-FUNC-SEQ-NBR-NEW                     
               SET IM008-STYL-RPT-CHG-FUNC-CHG    TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-FUNC-NOCHG  TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-GNDR-SEQ-NBR-OLD NOT = LS-GNDR-SEQ-NBR-NEW                     
               SET IM008-STYL-RPT-CHG-GNDR-CHG    TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-GNDR-NOCHG  TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-PATRN-SEQ-NBR-OLD NOT = LS-PATRN-SEQ-NBR-NEW                   
               SET IM008-STYL-RPT-CHG-PATRN-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-PATRN-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-SLHTT-SEQ-NBR-OLD NOT = LS-SLHTT-SEQ-NBR-NEW                   
               SET IM008-STYL-RPT-CHG-SLHTT-CHG   TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-RPT-CHG-SLHTT-NOCHG TO TRUE                       
           END-IF.                                                              
                                                                                
           MOVE LS-FBRCN-SEQ-NBR-NEW    TO IM008-STYL-RPT-CHG-FBRCN.            
           MOVE LS-RPT-HIER-SEQ-NBR-NEW TO IM008-STYL-RPT-CHG-RPT-HIER.         
           MOVE LS-BDY-STYL-SEQ-NBR-NEW TO IM008-STYL-RPT-CHG-BDY-STYL.         
           MOVE LS-CONST-SEQ-NBR-NEW    TO IM008-STYL-RPT-CHG-CONST.            
           MOVE LS-FUNC-SEQ-NBR-NEW     TO IM008-STYL-RPT-CHG-FUNC.             
           MOVE LS-GNDR-SEQ-NBR-NEW     TO IM008-STYL-RPT-CHG-GNDR.             
           MOVE LS-PATRN-SEQ-NBR-NEW    TO IM008-STYL-RPT-CHG-PATRN.            
           MOVE LS-SLHTT-SEQ-NBR-NEW    TO IM008-STYL-RPT-CHG-SLHTT.            
           IF LS-FBRCN-SEQ-NBR-NEW = ZERO                                       
               MOVE SPACES              TO IM008-STYL-RPT-CHG-FBRCN-DESC        
           ELSE                                                                 
               MOVE PC-FBRCN-CHARTC-CDE TO VSCHVL-CHARTC-CDE                    
               MOVE LS-FBRCN-SEQ-NBR-NEW                                        
                                        TO VSCHVL-CHARTC-SEQ-NBR                
               PERFORM 8100-GET-TVSCHVL-DESC                                    
               MOVE VSCHVL-CHARTC-VAL-DESC                                      
                                        TO IM008-STYL-RPT-CHG-FBRCN-DESC        
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-RPT-HIER-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES         TO IM008-STYL-RPT-CHG-RPTHIER-DSC        
               ELSE                                                             
                   MOVE PC-RPT-HIER-CHARTC-CDE                                  
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-RPT-HIER-SEQ-NBR-NEW                                 
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYL-RPT-CHG-RPTHIER-DSC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-BDY-STYL-SEQ-NBR-NEW = ZERO                                
                   MOVE SPACES         TO IM008-STYL-RPT-CHG-BDYSTYL-DSC        
               ELSE                                                             
                   MOVE PC-BDY-STYL-CHARTC-CDE                                  
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-BDY-STYL-SEQ-NBR-NEW                                 
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYL-RPT-CHG-BDYSTYL-DSC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-CONST-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYL-RPT-CHG-CONST-DESC        
               ELSE                                                             
                   MOVE PC-CONST-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-CONST-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-RPT-CHG-CONST-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-FUNC-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYL-RPT-CHG-FUNC-DESC         
               ELSE                                                             
                   MOVE PC-FUNC-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-FUNC-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-RPT-CHG-FUNC-DESC         
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-GNDR-SEQ-NBR-NEW = ZERO                                    
                   MOVE SPACES          TO IM008-STYL-RPT-CHG-GNDR-DESC         
               ELSE                                                             
                   MOVE PC-GNDR-CHARTC-CDE                                      
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-GNDR-SEQ-NBR-NEW                                     
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-RPT-CHG-GNDR-DESC         
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-PATRN-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYL-RPT-CHG-PATRN-DESC        
               ELSE                                                             
                   MOVE PC-PATRN-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-PATRN-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-RPT-CHG-PATRN-DESC        
               END-IF                                                           
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-SLHTT-SEQ-NBR-NEW = ZERO                                   
                   MOVE SPACES          TO IM008-STYL-RPT-CHG-SLHTT-DESC        
               ELSE                                                             
                   MOVE PC-SLHTT-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
                   MOVE LS-SLHTT-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                        TO IM008-STYL-RPT-CHG-SLHTT-DESC        
               END-IF                                                           
           END-IF.                                                              
           MOVE LOW-VALUES              TO IM008-STYL-RPT-CHG-FILLER.           
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE STYLE ATTRIBUTES CHANGE 2 MESSAGE FOR THE STYLE              
      *----------------------------------------------------------------*        
       3300-PROCESS-STYLE-ATTR-CHG2.                                            
                                                                                
           PERFORM 2300-BUILD-MSG-KEY.                                          
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 3350-BUILD-STYLE-CHG2-DATA                               
               IF PS-NO-SQL-ERROR                                               
                   COMPUTE IMT00001-MSG-TXT-LEN                                 
                         = LENGTH OF IMRD008-RECORD                             
                         - LENGTH OF IM008-STYL-ATTR2-FILLER2                   
                   PERFORM 8200-INSERT-TO-EVNT-MSG-TBL                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * BUILD THE STYLE ATTRIBUTES CHANGE 2 MESSAGE                             
      *----------------------------------------------------------------*        
       3350-BUILD-STYLE-CHG2-DATA.                                              
                                                                                
           INITIALIZE IM008-STYLE-ATTR-CHG2-DATA.                               
                                                                                
           IF LS-PRIC-TIER-SEQ-NBR-OLD NOT =                                    
                 LS-PRIC-TIER-SEQ-NBR-NEW                                       
               SET IM008-STYL-ATTR2-PRIC-TIER-CHG TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR2-PRIC-TIER-NOC TO TRUE                       
           END-IF.                                                              
                                                                                
           IF LS-LFSTYL-SEQ-NBR-OLD NOT = LS-LFSTYL-SEQ-NBR-NEW                 
               SET IM008-STYL-ATTR2-LFSTYL-CHG    TO TRUE                       
           ELSE                                                                 
               SET IM008-STYL-ATTR2-LFSTYL-NOCHG  TO TRUE                       
           END-IF.                                                              
                                                                                
           MOVE LS-PRIC-TIER-SEQ-NBR-NEW                                        
                                        TO IM008-STYL-ATTR2-PRIC-TIER.          
           MOVE LS-LFSTYL-SEQ-NBR-NEW   TO IM008-STYL-ATTR2-LFSTYL.             
                                                                                
           IF LS-PRIC-TIER-SEQ-NBR-NEW = ZERO                                   
               MOVE SPACES              TO IM008-STYL-ATTR2-PRIC-TIER-D         
           ELSE                                                                 
               MOVE PC-PRIC-TIER-CHARTC-CDE                                     
                                        TO VSCHVL-CHARTC-CDE                    
               MOVE LS-PRIC-TIER-SEQ-NBR-NEW                                    
                                        TO VSCHVL-CHARTC-SEQ-NBR                
               PERFORM 8100-GET-TVSCHVL-DESC                                    
               MOVE VSCHVL-CHARTC-VAL-DESC                                      
                                        TO IM008-STYL-ATTR2-PRIC-TIER-D         
           END-IF.                                                              
           IF PS-NO-SQL-ERROR                                                   
               IF LS-LFSTYL-SEQ-NBR-NEW = ZERO                                  
                   MOVE SPACES         TO IM008-STYL-ATTR2-LFSTYL-DESC          
               ELSE                                                             
                   MOVE PC-LFSTYL-CHARTC-CDE                                    
                                       TO VSCHVL-CHARTC-CDE                     
                   MOVE LS-LFSTYL-SEQ-NBR-NEW                                   
                                       TO VSCHVL-CHARTC-SEQ-NBR                 
                   PERFORM 8100-GET-TVSCHVL-DESC                                
                   MOVE VSCHVL-CHARTC-VAL-DESC                                  
                                       TO IM008-STYL-ATTR2-LFSTYL-DESC          
               END-IF                                                           
           END-IF.                                                              
           MOVE LOW-VALUES              TO IM008-STYL-ATTR2-FILLER1             
                                           IM008-STYL-ATTR2-FILLER2.            
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE OPENS THE TUPC CURSOR                              *        
      *----------------------------------------------------------------*        
       4000-OPEN-TUPC-CSR.                                                      
                                                                                
      *    DISPLAY ' 4000-OPEN-TUPC-CSR.'.                                      
                                                                                
           EXEC SQL                                                             
               OPEN TUPC_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4000-OPEN-TUPC-CSR'                                    
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'OPEN CSR' TO PV-DB2-OPERATION                          
                   MOVE PV-MDSEAR-ID                                       CL**1
                                   TO PV-TUPC-KEY-MDSEAR-ID                CL**1
                   MOVE PV-VS-ID   TO PV-TUPC-KEY-VS-ID                    CL**1
                   MOVE PV-TUPC-KEY-INFO                                   CL*11
                                   TO PV-DB2-KEY-INFO                      CL**1
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE FETCHES THE TUPC DATA                              *        
      *----------------------------------------------------------------*        
       4100-FETCH-TUPC-CSR.                                                     
                                                                                
      *    DISPLAY ' 4100-FETCH-TUPC-CSR.'.                                     
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
                                                                                
             EXEC SQL                                                           
               FETCH TUPC_CSR                                                   
                INTO :UPC-UPC-ID                                                
                   , :UPC-UPC-NBR                                               
                   , :TUPCSKU-PLU-DWNLD-IND                                     
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                      CONTINUE                                                  
                 WHEN SQLCODE = +100                                            
                      SET END-OF-TUPC-CSR TO TRUE                               
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                      MOVE '4100-FETCH-TUPC-CSR'                                
                                     TO PV-PARAGRAPH-NAME                       
                      MOVE 'FETCH CSR' TO PV-DB2-OPERATION                      
                      MOVE PV-MDSEAR-ID                                        C
                                     TO PV-TUPC-KEY-MDSEAR-ID                  C
                      MOVE PV-VS-ID  TO PV-TUPC-KEY-VS-ID                      C
                      MOVE PV-TUPC-KEY-INFO                                    C
                                     TO PV-DB2-KEY-INFO                        C
                      PERFORM 9998-SQL-ERROR                                    
             END-EVALUATE                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE CALLS THE INSERT TO THE EVENT BRIDGE FOR THE       *        
      * SKU MESSAGES (031) AND FETCHES DATA FROM THE CURSOR            *        
      *----------------------------------------------------------------*        
       4200-PROCESS-TUPC-CSR.                                                   
                                                                                
      *    DISPLAY ' 4200-PROCESS-TUPC-CSR.'                                    
           MOVE UPC-UPC-NBR     TO PV-ITEM-UPC-NBR.                             
           MOVE PV-ITEM-UPC-SKU-NUM                                             
                                TO IM008-ITEM-KEY-SKU.                          
           MOVE UPC-UPC-ID      TO PV-UPC-COMP-10.                              
           MOVE PV-UPC-COMP-10  TO IM008-ITEM-KEY-UPC-ID                        
                                   IM008-ITEM-KEY-INTR-UPC-ID.                  
           MOVE UPC-UPC-NBR     TO IM008-ITEM-KEY-UPC-NBR.                      
           MOVE TUPCSKU-PLU-DWNLD-IND                                           
                                TO IM008-SKU-DEL-PLU-DWNLD-IND.                 
                                                                                
           PERFORM 8200-INSERT-TO-EVNT-MSG-TBL.                                 
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4100-FETCH-TUPC-CSR                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------*              
      * THIS MODULE CLOSES THE TUPC CURSOR                       *              
      *----------------------------------------------------------*              
       4300-CLOSE-TUPC-CSR.                                                     
                                                                                
           EXEC SQL                                                             
              CLOSE TUPC_CSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4300-CLOSE-TUPC-CSR'                                   
                                  TO PV-PARAGRAPH-NAME                          
                   MOVE 'CLOSE-CSR' TO PV-DB2-OPERATION                         
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE OPENS THE VENDOR UPC CURSOR                        *        
      *----------------------------------------------------------------*        
       4400-OPEN-VENDOR-UPC-CSR.                                                
                                                                                
           EXEC SQL                                                             
               OPEN VENDOR_UPC_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4400-OPEN-VENDOR-UPC-CSR'                              
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE 'OPEN CSR' TO PV-DB2-OPERATION                          
                   MOVE PV-MDSEAR-ID                                       CL**1
                                   TO PV-TUPC-KEY-MDSEAR-ID                CL**1
                   MOVE PV-VS-ID   TO PV-TUPC-KEY-VS-ID                    CL**1
                   MOVE PV-TUPC-KEY-INFO                                   CL*11
                                   TO PV-DB2-KEY-INFO                      CL**1
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE FETCHES THE VENDOR UPC DATA                        *        
      *----------------------------------------------------------------*        
       4500-FETCH-VENDOR-UPC-CSR.                                               
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
                                                                                
             EXEC SQL                                                           
               FETCH VENDOR_UPC_CSR                                             
                INTO :UPC-UPC-ID                                                
                   , :UPC-INTR-UPC-ID                                           
                   , :UPC-UPC-NBR                                               
                   , :SKXREF-SKU                                                
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                      CONTINUE                                                  
                 WHEN SQLCODE = +100                                            
                      SET END-OF-VENDOR-UPC-CSR TO TRUE                         
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                      MOVE '4500-FETCH-VENDOR-UPC-CSR'                          
                                     TO PV-PARAGRAPH-NAME                       
                      MOVE 'FETCH CSR' TO PV-DB2-OPERATION                      
                      MOVE PV-MDSEAR-ID                                        C
                                     TO PV-TUPC-KEY-MDSEAR-ID                  C
                      MOVE PV-VS-ID  TO PV-TUPC-KEY-VS-ID                      C
                      MOVE PV-TUPC-KEY-INFO                                    C
                                     TO PV-DB2-KEY-INFO                        C
                      PERFORM 9998-SQL-ERROR                                    
             END-EVALUATE                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
      *----------------------------------------------------------------*        
      * THIS MODULE CALLS THE INSERT TO THE EVENT BRIDGE FOR THE VENDOR*        
      * UPC DELETE MESSAGES (125) AND FETCHES DATA FROM THE CURSOR.             
      *----------------------------------------------------------------*        
       4600-PROCESS-VENDOR-UPC-CSR.                                             
                                                                                
           MOVE SKXREF-SKU      TO IM008-ITEM-KEY-SKU.                          
           MOVE UPC-INTR-UPC-ID TO PV-UPC-COMP-10.                              
           MOVE PV-UPC-COMP-10  TO IM008-ITEM-KEY-INTR-UPC-ID.                  
           MOVE UPC-UPC-ID      TO PV-UPC-COMP-10.                              
           MOVE PV-UPC-COMP-10  TO IM008-ITEM-KEY-UPC-ID.                       
           MOVE UPC-UPC-NBR     TO IM008-ITEM-KEY-UPC-NBR.                      
                                                                                
           PERFORM 8200-INSERT-TO-EVNT-MSG-TBL.                                 
                                                                                
           IF PS-NO-SQL-ERROR                                                   
               PERFORM 4500-FETCH-VENDOR-UPC-CSR                                
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------*              
      * THIS MODULE CLOSES THE VENDOR UPC CURSOR                 *              
      *----------------------------------------------------------*              
       4700-CLOSE-VENDOR-UPC-CSR.                                               
                                                                                
                                                                                
           EXEC SQL                                                             
              CLOSE VENDOR_UPC_CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '4700-CLOSE-VENDOR-UPC-CSR'                             
                                  TO PV-PARAGRAPH-NAME                          
                   MOVE 'CLOSE-CSR' TO PV-DB2-OPERATION                         
                   PERFORM 9998-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      *-----------------------------------------------------------------        
      * GET ADDITIONAL STYLE AND VENDOR INFORMATION NEEDED TO BUILD             
      * THE MESSAGE KEY. AN UNCOMMITTED READ IS DONE HERE. THIS IS              
      * NECESSARY IN THE CASE OF A STYLE ADD BECAUSE THERE WILL BE AN           
      * UNCOMMITTED UPDATE TO TVNDSTL TO SET THE EFF_BEG_DTE TO A VALID         
      * DATE. ATTEMPTING TO READ THE ROW WILL RESULT IN A DEADLOCK              
      * UNLESS AN UNCOMMITTED READ IS DONE. IN THE CASE OF A STYLE              
      * CHANGE, THE UNCOMMITTED READ IS NOT REALLY NECESSARY, BUT IT            
      * WON'T HURT ANYTHING BECAUSE THE USE OF VENDOR STYLE RESERVATIONS        
      * IN VSM PREVENTS CONCURRENT UPDATES TO A STYLE.                          
      *-----------------------------------------------------------------        
       8000-GET-TVNDSTL-TEXTENT.                                                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                   SELECT  V.ENT_ID                                             
                         , E.DUN_NBR                                            
                         , V.VS_NBR                                             
                         , V.RTL_UM_CDE                                         
                     INTO :VNDSTL-ENT-ID                                        
                         ,:EXTENT-DUN-NBR                                       
                         ,:VNDSTL-VS-NBR                                        
                         ,:VNDSTL-RTL-UM-CDE                                    
                     FROM TVNDSTL V                                             
                         ,TEXTENT E                                             
                    WHERE V.VS_ID = :LS-VS-ID                                   
                      AND E.ENT_ID = V.ENT_ID                                   
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8000-GET-TVNDSTL-TEXTENT'                          
                                                   TO PV-PARAGRAPH-NAME         
                       MOVE 'SELECT FROM TVNDSTL'  TO PV-DB2-OPERATION          
                       MOVE LS-VS-ID            TO PV-TVNDSTL-KEY-VS-ID    CL**1
                       MOVE PV-TVNDSTL-KEY-INFO TO PV-DB2-KEY-INFO         CL**1
                       PERFORM 9998-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8000-GET-TVNDSTL-TEXTENT' TO PV-PARAGRAPH-NAME             
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION              
               MOVE LS-VS-ID                   TO PV-TVNDSTL-KEY-VS-ID     CL**1
               MOVE PV-TVNDSTL-KEY-INFO        TO PV-DB2-KEY-INFO          CL**1
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      * SELECT A STYLE ATTRIBUTE DESCRIPTION FROM TVSCHVL USING THE             
      * STYLE ATTRIBUTE SEQUENCE NUMBER.                                        
      *-----------------------------------------------------------------        
       8100-GET-TVSCHVL-DESC.                                                   
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                   SELECT  CHARTC_VAL_DESC                                      
                     INTO :VSCHVL-CHARTC-VAL-DESC                               
                     FROM TVSCHVL                                               
                    WHERE CHARTC_CDE     = :VSCHVL-CHARTC-CDE                   
                      AND CHARTC_SEQ_NBR = :VSCHVL-CHARTC-SEQ-NBR               
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8100-GET-TVSCHVL-DESC'                             
                                                   TO PV-PARAGRAPH-NAME         
                       MOVE 'SELECT FROM TVSCHVL'  TO PV-DB2-OPERATION          
                       MOVE VSCHVL-CHARTC-CDE      TO PV-TVSCHVL-KEY-CDE   CL**1
                       MOVE VSCHVL-CHARTC-SEQ-NBR                          CL**1
                                              TO PV-TVSCHVL-KEY-SEQ-NBR    CL**1
                       MOVE PV-TVSCHVL-KEY-INFO TO PV-DB2-KEY-INFO         CL**1
                       PERFORM 9998-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8100-GET-TVSCHVL-DESC'    TO PV-PARAGRAPH-NAME             
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION              
               MOVE VSCHVL-CHARTC-CDE          TO PV-TVSCHVL-KEY-CDE       CL**1
               MOVE VSCHVL-CHARTC-SEQ-NBR      TO PV-TVSCHVL-KEY-SEQ-NBR   CL**1
               MOVE PV-TVSCHVL-KEY-INFO        TO PV-DB2-KEY-INFO          CL**1
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------------        
      * SELECT THE LABEL TYPE CODE FOR THIS STYLES BRAND USING THE              
      * LABEL SEQUENCE NUMBER.                                                  
      *-----------------------------------------------------------------        
       8150-GET-NEW-LBL-TYP-CDE.                                                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                   SELECT  LBL_TYP_CDE                                          
                     INTO :IMT00000-LBL-TYP-CDE                                 
                     FROM IMT_LBL                                               
                    WHERE LBL_SEQ_NBR    = :VSCHVL-CHARTC-SEQ-NBR               
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8150-GET-NEW-LBL-TYP-CDE'                          
                                                   TO PV-PARAGRAPH-NAME         
                       MOVE 'SELECT FROM IMT_LBL'  TO PV-DB2-OPERATION          
                       MOVE VSCHVL-CHARTC-CDE      TO PV-TVSCHVL-KEY-CDE   CL**1
                       MOVE VSCHVL-CHARTC-SEQ-NBR                          CL**1
                                              TO PV-TVSCHVL-KEY-SEQ-NBR    CL**1
                       MOVE PV-TVSCHVL-KEY-INFO TO PV-DB2-KEY-INFO         CL**1
                       PERFORM 9998-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8150-GET-NEW-LBL-TYP-CDE' TO PV-PARAGRAPH-NAME             
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION              
               MOVE VSCHVL-CHARTC-CDE          TO PV-TVSCHVL-KEY-CDE       CL**1
               MOVE VSCHVL-CHARTC-SEQ-NBR      TO PV-TVSCHVL-KEY-SEQ-NBR   CL**1
               MOVE PV-TVSCHVL-KEY-INFO        TO PV-DB2-KEY-INFO          CL**1
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      * SELECT THE LABEL TYPE CODE FOR THIS STYLES OLD BRAND USING THE          
      * OLD LABEL SEQUENCE NUMBER.                                              
      *-----------------------------------------------------------------        
       8175-GET-OLD-LBL-TYP-CDE.                                                
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                   SELECT  LBL_TYP_CDE                                          
                     INTO :PV-IMT00000-LBL-TYP-CDE                              
                     FROM IMT_LBL                                               
                    WHERE LBL_SEQ_NBR    = :VSCHVL-CHARTC-SEQ-NBR               
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = +100                                          
                       MOVE SPACES TO PV-IMT00000-LBL-TYP-CDE                   
                   WHEN OTHER                                                   
                       MOVE '8175-GET-OLD-LBL-TYP-CDE'                          
                                                   TO PV-PARAGRAPH-NAME         
                       MOVE 'SELECT FROM IMT_LBL'  TO PV-DB2-OPERATION          
                       MOVE VSCHVL-CHARTC-CDE      TO PV-TVSCHVL-KEY-CDE   CL**1
                       MOVE VSCHVL-CHARTC-SEQ-NBR                          CL**1
                                              TO PV-TVSCHVL-KEY-SEQ-NBR    CL**1
                       MOVE PV-TVSCHVL-KEY-INFO TO PV-DB2-KEY-INFO         CL**1
                       PERFORM 9998-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8175-GET-OLD-LBL-TYP-CDE' TO PV-PARAGRAPH-NAME             
               MOVE 'MAX RETRIES EXCEEDED'     TO PV-DB2-OPERATION              
               MOVE VSCHVL-CHARTC-CDE          TO PV-TVSCHVL-KEY-CDE       CL**1
               MOVE VSCHVL-CHARTC-SEQ-NBR      TO PV-TVSCHVL-KEY-SEQ-NBR   CL**1
               MOVE PV-TVSCHVL-KEY-INFO        TO PV-DB2-KEY-INFO          CL**1
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       8200-INSERT-TO-EVNT-MSG-TBL.                                             
                                                                                
           MOVE IM008-ITEM-KEY-RMS-STYL-ID TO IMT00001-RMS-STYL-ID.             
           MOVE IM008-BUSINESS-EVENT-ID    TO IMT00001-BUS-EVNT-TYP-CDE.        
           MOVE IMRD008-RECORD             TO IMT00001-MSG-TXT-TEXT.            
                                                                                
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL SQLCODE = +0                                             
                    OR PS-SQL-ERROR                                             
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                          
               EXEC SQL                                                         
                   INSERT INTO IMT_EVNT_MSG_BRG                                 
                       ( CRE_TMST                                               
                        ,RMS_STYL_ID                                            
                        ,BUS_EVNT_TYP_CDE                                       
                        ,MSG_TXT )                                              
                   VALUES                                                       
                       (  CURRENT TIMESTAMP                                     
                        ,:IMT00001-RMS-STYL-ID                                  
                        ,:IMT00001-BUS-EVNT-TYP-CDE                             
                        ,:IMT00001-MSG-TXT)                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '8200-INSERT-TO-EVNT-MSG-TBL'                       
                                                   TO PV-PARAGRAPH-NAME         
                       MOVE 'INSERT TO IMT_EVNT_MSG_BRG'                        
                                                   TO PV-DB2-OPERATION          
                       PERFORM 9998-SQL-ERROR                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '8200-INSERT-TO-EVNT-MSG-TBL' TO PV-PARAGRAPH-NAME          
               MOVE 'MAX RETRIES EXCEEDED'        TO PV-DB2-OPERATION           
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       9998-SQL-ERROR.                                                          
                                                                                
           SET PS-SQL-ERROR TO TRUE.                                            
                                                                                
           DISPLAY '** '                                                        
                   PC-PGM-NAME                                                  
                   ' ABEND IN PARAGRAPH '                                       
                   PV-PARAGRAPH-NAME.                                           
           DISPLAY '** '                                                        
                   PC-PGM-NAME                                                  
                   ' OPERATION = ' PV-DB2-OPERATION.                            
           DISPLAY '** '                                                        
                   PC-PGM-NAME                                                  
                   ' KEY INFO = ' PV-DB2-KEY-INFO.                              
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT             
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.                       
           COPY DPPD004.                                                        
      *                                                                         
           EXEC SQL                                                             
               ROLLBACK                                                         
           END-EXEC.                                                            
