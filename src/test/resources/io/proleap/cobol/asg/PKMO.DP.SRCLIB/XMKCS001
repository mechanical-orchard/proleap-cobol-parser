      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.       XMKCS001.                                      00050004
       AUTHOR.           KOHLS.                                         00060000
       INSTALLATION.     KOHLS.                                         00070000
       DATE-WRITTEN      JULY, 2004.                                    00080001
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      *  PROGRAM REQUESTS FOR SKU/STORE ONHAND AND ONORDER:             00120001
      *      PROGRAMS WILL CALL THIS DRIVER MODULE WHEN REQUESTING      00130000
      *      ONHAND AND ONORDER DATA. THIS MODULE WILL THEN             00140001
      *      CALL THE APPROPRIATE MODULE(S) TO ACQUIRE THE DATA.        00150000
      *                                                                 00151007
      *  PLEASE NOTE THAT WHEN LINKING TO THIS MODULE THE CALLING       00152007
      *  PROGRAM'S DB2 PLAN MUST INCLUDE THE XM COLLECTION ID.          00153007
      *                                                                 00154007
      ******************************************************************00160000
                                                                        00170000
      ******************************************************************00180000
       ENVIRONMENT DIVISION.                                            00190000
      ******************************************************************00200000
                                                                        00210000
      ******************************************************************00220000
       DATA DIVISION.                                                   00230000
      ******************************************************************00240000
                                                                        00250000
       WORKING-STORAGE SECTION.                                         00260000
                                                                        00280000
       01  PC-PROGRAM-CONSTANTS.                                        00330000
           05  PC-ONORDER-PROGRAM     PIC  X(08)  VALUE 'XMKCS003'.     00350004
           05  PC-ONHAND-PROGRAM      PIC  X(08)  VALUE 'XMKCS002'.     00360004
                                                                        00380000
       01  PV-PROGRAM-VARIABLES.                                        00390000
           05  PV-COMMAREA-LENGTH     PIC S9(04)  VALUE +0              00400000
                                                  COMP SYNC.            00410000
           05  PV-CICS-RESPONSE       PIC S9(08)  VALUE ZEROS           00420000
                                                  COMP SYNC.            00430000
           05  PV-SKU                 PIC S9(8)V  VALUE ZEROS           00440002
                                                  COMP-3.               00450002
                                                                        00490000
      *---------------------------------------------------------------  00581000
      *  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE NI-SKU XREF TABLE  00582000
      *---------------------------------------------------------------  00583000
           EXEC SQL                                                     00584000
               INCLUDE TSKXREF                                          00585000
           END-EXEC.                                                    00586000
                                                                        00587000
      *----------------------------------------------------------------*00750000
      * COMMAREA USED WHEN LINKING FROM A CALLING PROGRAM               00760000
      *----------------------------------------------------------------*00770000
           COPY XMWS003.                                                00780009
                                                                        00790008
      *---------------------------------------------------------------  00810000
      *  COMMUNICATIONS AREA FOR DB2                                    00820000
      *---------------------------------------------------------------  00830000
           EXEC SQL                                                     00840000
               INCLUDE SQLCA                                            00850000
           END-EXEC.                                                    00860000
                                                                        00880000
       LINKAGE SECTION.                                                 00890000
       01  DFHCOMMAREA.                                                 00900000
           05  FILLER                         OCCURS  1 TO 4072 TIMES   00910000
                                            DEPENDING ON EIBCALEN.      00920000
               10  FILLER                   PIC X.                      00930000
                                                                        00950000
      ******************************************************************00960000
       PROCEDURE DIVISION.                                              00970000
      ******************************************************************00980000
                                                                        00990000
           INITIALIZE                      XM003-ITEM-COMMAREA.         01000009
           MOVE DFHCOMMAREA            TO  XM003-ITEM-COMMAREA.         01010009
           SET XM003-SUCCESSFUL        TO TRUE.                         01020009
           SET XM003-ONORDER-ERROR-N   TO TRUE.                         01040009
           SET XM003-ONHAND-ERROR-N    TO TRUE.                         01050009
                                                                        01050106
           PERFORM 2000-SELECT-TSKXREF.                                 01051006
                                                                        01051106
           IF XM003-NOT-SUCCESSFUL                                      01052009
               IF XM003-ONORDER-YES                                     01070009
                   SET XM003-ONORDER-ERROR-Y TO TRUE                    01071009
               END-IF                                                   01072001
               IF XM003-ONHAND-YES                                      01073009
                   SET XM003-ONHAND-ERROR-Y TO TRUE                     01074009
               END-IF                                                   01075001
           END-IF.                                                      01076006
                                                                        01077001
           IF XM003-ONORDER-YES                                         01080009
               IF XM003-SUCCESSFUL                                      01140009
                   PERFORM 6000-CALL-ONORDER-MODULE                     01150001
               END-IF                                                   01180000
           END-IF.                                                      01190000
                                                                        01200000
           IF XM003-ONHAND-YES                                          01300009
               IF XM003-SUCCESSFUL                                      01320009
                   PERFORM 8000-CALL-ONHAND-MODULE                      01330000
               END-IF                                                   01360000
           END-IF.                                                      01370000
                                                                        01380000
           MOVE XM003-ITEM-COMMAREA TO DFHCOMMAREA.                     01390009
                                                                        01400000
           EXEC CICS                                                    01410000
                RETURN                                                  01420000
           END-EXEC.                                                    01430000
                                                                        01440000
      *---------------------------------------------------------------  01450006
      *  SELECT ITEM NUMBER FROM TSKXREF.                               01460006
      *---------------------------------------------------------------  01470006
       2000-SELECT-TSKXREF.                                             01820100
                                                                        01820206
           INITIALIZE DCLTSKXREF.                                       01820300
           MOVE XM003-KEY-SKU     TO PV-SKU.                            01820409
                                                                        01820502
           EXEC SQL                                                     01821000
               SELECT  ITM_NBR                                          01822001
                 INTO  :SKXREF-ITM-NBR                                  01825001
                 FROM  TSKXREF                                          01828000
                WHERE  SKU  =  :PV-SKU                                  01829002
           END-EXEC.                                                    01829100
                                                                        01829200
           EVALUATE TRUE                                                01830000
               WHEN SQLCODE = ZERO                                      01840000
                   SET XM003-SUCCESSFUL   TO TRUE                       01850009
               WHEN SQLCODE = +100                                      01860000
                   SET XM003-NOT-SUCCESSFUL TO TRUE                     01870009
                   MOVE SQLCODE TO XM003-RETURN-SQLCODE                 01871009
               WHEN SQLWARN0 NOT = SPACES                               01880000
               WHEN SQLCODE < ZERO                                      01890000
               WHEN SQLCODE > ZERO                                      01900000
                   SET XM003-NOT-SUCCESSFUL TO TRUE                     01910009
                   MOVE SQLCODE TO XM003-RETURN-SQLCODE                 01911009
           END-EVALUATE.                                                01920000
                                                                        01930006
      *---------------------------------------------------------------  02181006
      *  LINK TO THE ON-ORDER XM MODULE.                                02182006
      *---------------------------------------------------------------  02183006
       6000-CALL-ONORDER-MODULE.                                        02190001
                                                                        02200000
           MOVE SKXREF-ITM-NBR       TO XM003-ITEM-NBR.                 02231009
           MOVE LENGTH OF XM003-ITEM-COMMAREA TO PV-COMMAREA-LENGTH.    02270009
                                                                        02280000
           EXEC CICS                                                    02290000
               LINK                                                     02300000
                   PROGRAM   (PC-ONORDER-PROGRAM)                       02310001
                   COMMAREA  (XM003-ITEM-COMMAREA)                      02320009
                   LENGTH    (PV-COMMAREA-LENGTH)                       02330000
                   RESP      (PV-CICS-RESPONSE)                         02340000
           END-EXEC.                                                    02350000
                                                                        02360000
           EVALUATE TRUE                                                02370000
               WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 02380000
                 EVALUATE TRUE                                          02390000
                   WHEN XM003-SUCCESSFUL                                02400009
                     CONTINUE                                           02410000
                   WHEN OTHER                                           02420000
                       SET XM003-ONORDER-ERROR-Y TO TRUE                02430009
                   END-EVALUATE                                         02440000
               WHEN OTHER                                               02450000
                   SET XM003-ONORDER-ERROR-Y TO TRUE                    02460009
           END-EVALUATE.                                                02470000
                                                                        02760000
      *---------------------------------------------------------------  02761006
      *  LINK TO THE ON-HAND XM MODULE.                                 02762006
      *---------------------------------------------------------------  02763006
       8000-CALL-ONHAND-MODULE.                                         02770000
                                                                        02771006
           MOVE SKXREF-ITM-NBR       TO XM003-ITEM-NBR.                 02780009
           MOVE LENGTH OF XM003-ITEM-COMMAREA TO PV-COMMAREA-LENGTH.    02820009
                                                                        02830000
           EXEC CICS                                                    02840000
               LINK                                                     02850000
                   PROGRAM   (PC-ONHAND-PROGRAM)                        02860000
                   COMMAREA  (XM003-ITEM-COMMAREA)                      02870009
                   LENGTH    (PV-COMMAREA-LENGTH)                       02880000
                   RESP      (PV-CICS-RESPONSE)                         02890000
           END-EXEC.                                                    02900000
                                                                        02910000
           EVALUATE TRUE                                                02920000
               WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 02930000
                 EVALUATE TRUE                                          02940000
                   WHEN XM003-SUCCESSFUL                                02950009
                     CONTINUE                                           02960000
                   WHEN OTHER                                           02970000
                       SET XM003-ONHAND-ERROR-Y TO TRUE                 02980009
                   END-EVALUATE                                         02990000
               WHEN OTHER                                               03000000
                       SET XM003-ONHAND-ERROR-Y TO TRUE                 03010009
           END-EVALUATE.                                                03020000
