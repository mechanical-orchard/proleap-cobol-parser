000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.   TRKUP502.                                                  
000600 AUTHOR.       TKMA497.                                                   
000700 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000800 DATE-WRITTEN. AUGUST, 2012.                                              
000900 DATE-COMPILED.                                                           
001000                                                                          
001002******************************************************************        
001003*                                                                *        
001004*  READ AN INPUT FILE CONSISTING OF ALL OF THE DUPLICATE RATES   *        
001005*  DETECTED IN TRKUT501 AND DELETE THEM FROM TRT_VND_TRANS_RT.   *        
001014*                                                                *        
002401******************************************************************        
COMMIT* 06/22/2022    JIM HUNTER      CHG0271378                                
COMMIT* ADDING LOGIC TO PERFORM A COMMIT EVERY 3 SECONDS.                       
COMMIT* DB2 LOGS REFLECT WARNINGS WHEN THIS JOB RUNS BECAUSE IT IS              
COMMIT* DELETING THOUSANDS OF ROWS WITHOUT PERFORMING COMMITS.                  
COMMIT* FIND 'COMMIT' TO SEE CHANGES.                                           
COMMIT*****************************************************************         
002402 ENVIRONMENT DIVISION.                                                    
002403******************************************************************        
002404                                                                          
002405 CONFIGURATION SECTION.                                                   
002406                                                                          
002407 SOURCE-COMPUTER.        IBM-3090.                                        
002408 OBJECT-COMPUTER.        IBM-3090.                                        
002409                                                                          
002410 INPUT-OUTPUT SECTION.                                                    
002411                                                                          
002412 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT DUPE-RATES-FILE                                               
002700         ASSIGN TO INP01.                                                 
003400                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  DUPE-RATES-FILE                                                      
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  DUPE-RATES-RECORD                PIC X(184).                         
004700                                                                          
006200******************************************************************        
006300 WORKING-STORAGE SECTION.                                                 
005844******************************************************************        
005845*  VENDOR TRANSPORTATION RATES RECORD LAYOUT                              
005846******************************************************************        
005847     COPY TRRD272.                                                        
005843                                                                          
006600******************************************************************        
006700*  DB2 FORMATTED MESSAGE AREA                                             
005847     COPY DPWS004.                                                        
005848*                                                                         
011601******************************************************************        
011602*  COMMUNICATIONS AREA FOR DB2                                            
011603******************************************************************        
011604     EXEC SQL                                                             
011605          INCLUDE SQLCA                                                   
011640     END-EXEC.                                                            
011626******************************************************************        
011627*  DB2 TABLE DECLARATION - TRT_VND_TRANS_RT                               
011628******************************************************************        
011629     EXEC SQL                                                             
011630          INCLUDE TRT00050                                                
011640     END-EXEC.                                                            
127313                                                                          
127314 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
127315                                                   COMP SYNC.             
127316 01  PA-PROGRAM-ACCUMULATORS.                                             
127317     05  PA-TOTALS.                                                       
127318         10 PA-INPUT-COUNT            PIC  S9(09)  VALUE +0               
127319                                                     COMP-3.              
127320         10 PA-DELETE-COUNT           PIC  S9(09)  VALUE +0               
127321                                                     COMP-3.              
127322         10 PA-NOT-FOUND-COUNT        PIC  S9(09)  VALUE +0               
127321                                                     COMP-3.              
127342                                                                          
127343 01  PS-PROGRAM-SWITCHES.                                                 
127344     05  PS-END-OF-DUPS-SW             PIC  X(01)    VALUE 'N'.           
127345         88  PS-END-OF-DUPES-YES                     VALUE 'Y'.           
127346         88  PS-END-OF-DUPES-NO                      VALUE 'N'.           
127435                                                                          
127436 01  PC-PROGRAM-CONSTANTS.                                                
127437     05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE                  
127438         'TRKUP502'.                                                      
127473                                                                          
127474 01 PV-PROGRAM-VARIABLES.                                                 
127475     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
127476     05  PV-DB2-OPERATION-MESSAGE    PIC X(79)     VALUE SPACES.          
127477     05  PV-DB2-TABLE-NAME-1         PIC X(18)     VALUE SPACES.          
           05  PV-DISPLAY-COUNT            PIC 9(09).                           
127480     05  PV-COUNTER                  PIC S9(04)    VALUE +0               
127481                                                      COMP SYNC.          
COMMIT     05  PV-COMMIT-COUNT             PIC  9(9)     VALUE ZEROES.          
COMMIT     05  PV-COMMIT-TIMESTAMP         PIC X(26)     VALUE SPACES.          
COMMIT     05  PV-CURRENT-TIMESTAMP        PIC X(26)     VALUE SPACES.          
247465                                                                          
247466 LINKAGE SECTION.                                                         
247467 PROCEDURE DIVISION.                                                      
247468******************************************************************        
247469*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
247470******************************************************************        
247480 0000-PROGRAM-MAINLINE.                                                   
494665*    DISPLAY '0000-PROGRAM-MAINLINE'.                                     
494666                                                                          
494667     PERFORM 1000-INITIALIZE.                                             
494668                                                                          
494669     PERFORM 2000-MAIN-LINE-PROCESS                                       
494670         UNTIL PS-END-OF-DUPES-YES.                                       
494671                                                                          
494672     PERFORM 8000-EOJ-ROUTINE.                                            
494673     STOP RUN.                                                            
494674                                                                          
494675******************************************************************        
494676*  OPENS INPUT FILE AND PERFORMS OTHER BEGINNING OF                       
494677*  JOB PROCESSES.                                                *        
494678******************************************************************        
494679                                                                          
494680 1000-INITIALIZE.                                                         
494681*    DISPLAY '1000-INITIALIZE'.                                           
494682                                                                          
494683     OPEN INPUT DUPE-RATES-FILE.                                          
494686                                                                          
494693*    PERFORM INITIAL READ                                                 
494694     PERFORM 6000-READ-DUPE-RATES-FILE.                                   
COMMIT     PERFORM 7100-GET-COMMIT-TIMESTAMP.                                   
494695                                                                          
494696 EJECT                                                                    
494764******************************************************************        
COMMIT* MAIN PROCESSING PARAGRAPH, INCLUDING COMMIT LOGIC.             *        
494766******************************************************************        
494767 2000-MAIN-LINE-PROCESS.                                                  
494768                                                                          
494769*    DISPLAY '2000-MAIN-LINE-PROCESS'.                                    
843026         EXEC SQL                                                         
843027              DELETE FROM TRT_VND_TRANS_RT                                
                     WHERE VND_TRANS_RT_ID = :TR272-VND-TRANS-RT-ID             
843028         END-EXEC.                                                        
843029                                                                          
843030     EVALUATE TRUE                                                        
843031         WHEN SQLCODE = ZERO                                              
843033             ADD +1 TO PA-DELETE-COUNT                                    
                   DISPLAY 'DELETED REC IS  ' TR272-VND-TRANS-RT-ID             
                   DISPLAY 'DELETED REC IS  ' TR272-VND-NBR                     
                   DISPLAY 'DELETED REC IS  ' TR272-TRANS-RT-TYP-CDE            
COMMIT             PERFORM 7000-GET-CURRENT-TIMESTAMP                           
COMMIT             IF  PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP               
COMMIT                 PERFORM 7500-COMMIT                                      
COMMIT                 DISPLAY  'COMMITTING FOR ' PV-CURRENT-TIMESTAMP          
COMMIT                 ADD +1   TO PV-COMMIT-COUNT                              
COMMIT                 PERFORM 7100-GET-COMMIT-TIMESTAMP                        
COMMIT             END-IF                                                       
843032         WHEN SQLCODE = +100                                              
843033             ADD +1 TO PA-NOT-FOUND-COUNT                                 
843034         WHEN OTHER                                                       
843037             MOVE '2000-MAIN-LINE-PROCESS'                                
843038                                     TO PV-PARAGRAPH-NAME                 
843039             MOVE 'ATTEMPTING TO DELETE'                                  
843040                                     TO PV-DB2-OPERATION-MESSAGE          
843041             MOVE 'TRT_VND_TRANS_RT' TO PV-DB2-TABLE-NAME-1               
843044             PERFORM 9100-USER-ABEND                                      
843045     END-EVALUATE.                                                        
843046                                                                          
495656     PERFORM 6000-READ-DUPE-RATES-FILE.                                   
495667******************************************************************        
495666                                                                          
809220 6000-READ-DUPE-RATES-FILE.                                               
809221*    DISPLAY '6000-READ-DUPE-RATES-FILE'.                                 
809230                                                                          
809233     READ DUPE-RATES-FILE                                                 
809234         INTO TR272-VENDOR-TRANS-RATE-REC                                 
809235         AT END                                                           
809236           SET PS-END-OF-DUPES-YES TO TRUE.                               
           IF PS-END-OF-DUPES-NO                                                
               ADD +1 TO PA-INPUT-COUNT.                                        
                                                                                
COMMIT******************************************************************        
COMMIT*  GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL TABLE             
COMMIT******************************************************************        
COMMIT 7000-GET-CURRENT-TIMESTAMP.                                              
COMMIT                                                                          
COMMIT     EXEC SQL                                                             
COMMIT       SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                      
COMMIT     END-EXEC.                                                            
COMMIT                                                                          
COMMIT     IF SQLCODE = 0                                                       
COMMIT         CONTINUE                                                         
COMMIT     ELSE                                                                 
COMMIT         MOVE '7000-GET-CURRENT-TIMESTAMP '  TO PV-PARAGRAPH-NAME         
COMMIT         MOVE 'ERROR ON SELECT OF TIMESTAMP '                             
COMMIT                                      TO PV-DB2-OPERATION-MESSAGE         
COMMIT         PERFORM 9100-USER-ABEND                                          
COMMIT     END-IF.                                                              
COMMIT                                                                          
COMMIT******************************************************************        
COMMIT*  GET THE COMMIT TIMESTAMP                                               
COMMIT******************************************************************        
COMMIT 7100-GET-COMMIT-TIMESTAMP.                                               
COMMIT                                                                          
COMMIT     EXEC SQL                                                             
COMMIT       SET :PV-COMMIT-TIMESTAMP = CURRENT TIMESTAMP + 3 SECONDS           
COMMIT     END-EXEC.                                                            
COMMIT                                                                          
COMMIT     IF SQLCODE = 0                                                       
COMMIT         CONTINUE                                                         
COMMIT     ELSE                                                                 
COMMIT         MOVE '7100-GET-COMMIT-TIMESTAMP '   TO PV-PARAGRAPH-NAME         
COMMIT         MOVE 'ERROR ON SELECT OF TIMESTAMP '                             
COMMIT                                      TO PV-DB2-OPERATION-MESSAGE         
COMMIT         PERFORM 9100-USER-ABEND                                          
COMMIT     END-IF.                                                              
COMMIT                                                                          
COMMIT******************************************************************        
COMMIT* COMMIT UNIT OF WORK                                                     
COMMIT******************************************************************        
COMMIT 7500-COMMIT.                                                             
COMMIT                                                                          
COMMIT     EXEC SQL                                                             
COMMIT        COMMIT                                                            
COMMIT     END-EXEC.                                                            
COMMIT                                                                          
COMMIT     EVALUATE TRUE                                                        
COMMIT        WHEN SQLCODE = 0                                                  
COMMIT           CONTINUE                                                       
COMMIT        WHEN OTHER                                                        
COMMIT             MOVE '7500-COMMIT'           TO PV-PARAGRAPH-NAME            
COMMIT             MOVE 'ERROR DELETING SALES'                                  
COMMIT                                      TO PV-DB2-OPERATION-MESSAGE         
COMMIT             PERFORM 9100-USER-ABEND                                      
COMMIT     END-EVALUATE.                                                        
COMMIT                                                                          
495667******************************************************************        
836300 8000-EOJ-ROUTINE.                                                        
836310*    DISPLAY '8000-EOJ-ROUTINE'.                                          
842200                                                                          
842300     CLOSE DUPE-RATES-FILE.                                               
843002                                                                          
843003     DISPLAY '*************************************************'.         
843004     DISPLAY '** TRKUP502 SUMMARY                            **'.         
843005     DISPLAY '*************************************************'.         
           MOVE PA-INPUT-COUNT     TO PV-DISPLAY-COUNT                          
843006     DISPLAY 'TOTAL RECORDS READ     = ' PV-DISPLAY-COUNT.                
           MOVE PA-DELETE-COUNT    TO PV-DISPLAY-COUNT                          
843008     DISPLAY 'TOTAL RATE DELETES     = ' PV-DISPLAY-COUNT.                
           MOVE PA-NOT-FOUND-COUNT TO PV-DISPLAY-COUNT                          
843010     DISPLAY 'TOTAL NOT FOUND        = ' PV-DISPLAY-COUNT.                
COMMIT     MOVE PV-COMMIT-COUNT    TO PV-DISPLAY-COUNT                          
COMMIT     DISPLAY 'TOTAL COMMITS          = ' PV-DISPLAY-COUNT.                
843016     DISPLAY '*************************************************'.         
843017                                                                          
843091******************************************************************        
843092* PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    *        
843093* ERROR OR WARNING CODE OCCURS.                                  *        
843094******************************************************************        
843095 9100-USER-ABEND.                                                         
843096******************************************************************        
843097* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
843098* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
843099* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
843100* FORMAT.                                                        *        
843101******************************************************************        
843102                                                                          
843104     DISPLAY '9100-USER-ABEND'.                                           
843105     DISPLAY '** ABEND           **'.                                     
843106     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
843107     DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME-1.              
843110     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MESSAGE.         
843111                                                                          
843112     COPY DPPD004.                                                        
843113                                                                          
843310     MOVE +4013 TO ABEND-CODE.                                            
843320     CALL 'ILBOABN0' USING ABEND-CODE.                                    
