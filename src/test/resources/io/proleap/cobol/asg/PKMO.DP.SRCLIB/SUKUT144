      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
       PROGRAM-ID.             SUKUT144.                                00040000
       AUTHOR.                 V.SUDHAN.                                00050000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060000
       DATE-WRITTEN.           SEPTEMBER, 2009.                         00070000
       DATE-COMPILED.                                                   00080000
      ***************************************************************** 00090000
      *                                                               * 00100000
      *  THIS PROGRAM WILL KEEP THE PNA SLA FILE TRIMMED. THIS MODULE * 00110000
      *  LOOK ONLY FOR EXTRACT ROWS WHICH ARE LESS THAN 370 DAYS OLD. * 00120000
      *  THE SELECTED RECORDS WILL BE WRITTEN TO THE OUTPUT FILE.     * 00130000
      *  THE RECORDS WHICH ARE MORE THAN 370 DAYS OLD WILL BE TRIMMED.* 00140000
      *                                                               * 00150000
      ***************************************************************** 00160000
       ENVIRONMENT DIVISION.                                            00170000
      ***************************************************************** 00180000
      *                                                                 00190000
       CONFIGURATION SECTION.                                           00200000
      *                                                                 00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
      *                                                                 00240000
       INPUT-OUTPUT SECTION.                                            00250000
      *                                                                 00260000
       FILE-CONTROL.                                                    00270000
      *                                                                 00280000
           SELECT SU-PNA-SLA-EXTRACT-FILE                               00290000
                  ASSIGN TO IN01.                                       00300000
      *                                                                 00310000
           SELECT SU-PNA-SLA-EXT-TRIM-FILE                              00320000
                  ASSIGN TO OUT01.                                      00330000
      *                                                                 00340000
      ***************************************************************** 00350000
       DATA DIVISION.                                                   00360000
      ***************************************************************** 00370000
      *                                                                 00380000
       FILE SECTION.                                                    00390000
      *                                                                 00400000
       FD  SU-PNA-SLA-EXTRACT-FILE                                      00410000
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0  RECORDS.                                   00430000
       01  SU-PNA-SLA-EXTRACT-RECORD       PIC  X(1050).                00440000
      *                                                                 00450000
       FD  SU-PNA-SLA-EXT-TRIM-FILE                                     00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0  RECORDS.                                   00480000
       01  SU-PNA-SLA-EXT-TRIM-RECORD      PIC  X(1050).                00490000
      *                                                                 00500000
                                                                        00510000
       WORKING-STORAGE SECTION.                                         00520000
      *                                                                 00530000
       01  ABEND-CODE                      PIC S9(04)  VALUE +0         00540000
                                                       COMP SYNC.       00550000
      *                                                                 00560000
       01  PROGRAM-SWITCHES.                                            00570000
           05  PS-EOF-PNA-SLA-EXT-SW       PIC  X(01)  VALUE 'N'.       00580000
               88  PS-EOF-PNA-SLA-EXT                  VALUE 'Y'.       00590000
               88  PS-NOT-EOF-PNA-SLA-EXT              VALUE 'N'.       00600000
      *                                                                 00610000
       01  PC-PROGRAM-CONSTANTS.                                        00620000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)  VALUE            00630000
                                                       'SUKUT144'.      00640000
           05  PC-DAYS-TO-INCREMENT        PIC S9(03)  VALUE +370.      00650000
           05  PC-CURRENT-DATE.                                         00660000
               10  PC-CURRENT-CC           PIC 9(2)    VALUE ZERO.      00670000
               10  PC-CURRENT-YEAR         PIC 9(2)    VALUE ZERO.      00680000
               10  PC-CURRENT-MONTH        PIC 9(2)    VALUE ZERO.      00690000
               10  PC-CURRENT-DAY          PIC 9(2)    VALUE ZERO.      00700000
                                                                        00710000
      *                                                                 00720000
       01  PV-PROGRAM-VARIABLES.                                        00730000
           05  PV-PROCESS-DATE             PIC  X(06).                  00740000
           05  PV-DATE-RETURNED-FROM-ROUTINE                            00750000
                                           PIC  9(08).                  00760000
           05  PV-READ-COUNTER             PIC  9(09)  VALUE ZERO.      00770000
           05  PV-PNA-SLA-SKIP-CNT         PIC  9(09)  VALUE ZERO.      00780000
           05  PV-TOTAL-CNT                PIC  9(09)  VALUE ZERO.      00790000
      *                                                                 00800000
       01  VENDOR-EXTRACT-FILE.                                         00810000
      *                                                                 00820000
           COPY SUWS103.                                                00830000
      *                                                                 00840000
           COPY DPWS500.                                                00850000
      *                                                                 00860000
       PROCEDURE DIVISION.                                              00870000
      *                                                                 00880000
      ******************************************************************00890000
      **  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                    **00900000
      ******************************************************************00910000
      *                                                                 00920000
       0000-MAINLINE-PROCESSING.                                        00930000
      *                                                                 00940000
           PERFORM 1000-INITIALIZATION.                                 00950000
      *                                                                 00960000
           PERFORM 2000-PROCESS-PNA UNTIL PS-EOF-PNA-SLA-EXT.           00970000
      *                                                                 00980000
           PERFORM 3000-END-OF-JOB-ROUTINE.                             00990000
      *                                                                 01000000
           STOP RUN.                                                    01120000
      *                                                                 01130000
      ******************************************************************01140000
      ** PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.              **01150000
      ** 01.ACCEPTS THE CURRENT DATE FROM SYSTEM DATE.                **01160000
      ** 02.OPENS THE INPUT & OUTPUT FILES.                           **01170000
      ** 03.READS THE FIRST RECORD FROM THE INPUT FILE.               **01180000
      ******************************************************************01190000
      *                                                                 01200000
       1000-INITIALIZATION.                                             01210000
      *                                                                 01220000
           DISPLAY '** PROGRAM SUKUT144 EXECUTION STARTED **'.          01230000
      *                                                                 01240000
           ACCEPT PV-PROCESS-DATE FROM DATE.                            01250000
            MOVE PV-PROCESS-DATE(1:2) TO PC-CURRENT-YEAR.               01260000
            MOVE PV-PROCESS-DATE(3:2) TO PC-CURRENT-MONTH.              01270000
            MOVE PV-PROCESS-DATE(5:2) TO PC-CURRENT-DAY.                01280000
           IF PC-CURRENT-YEAR < 99                                      01290000
              MOVE 20 TO PC-CURRENT-CC                                  01300000
            ELSE                                                        01310000
              MOVE 21 TO PC-CURRENT-CC                                  01320000
            END-IF                                                      01330000
           DISPLAY '** PROGRAM SUKUT144 PROCESSING DATE : **'           01340000
                   PC-CURRENT-DATE.                                     01350000
      *                                                                 01360000
            OPEN INPUT  SU-PNA-SLA-EXTRACT-FILE                         01370000
                 OUTPUT SU-PNA-SLA-EXT-TRIM-FILE.                       01380000
      *                                                                 01390000
            SET PS-NOT-EOF-PNA-SLA-EXT TO TRUE.                         01400000
            PERFORM 8000-READ-EXTRACT-FILE.                             01410000
      *                                                                 01420000
      ******************************************************************01430000
      **  01.THIS PARAGRAPH CALLS THE KOHL'S DATE ROUTINE AND ADDS    **01440000
      **  370 DAYS TO THE PROCESS DATE WHICH WAS READ FROM THE INPUT  **01450000
      **  RECORD.                                                     **01460000
      **                                                              **01470000
      **  02. COMPARES THE PROCESS DATE + 370 WITH THE CURRENT DATE   **01480000
      **  AND IF THE GREATER THAN OR EQUAL TO THE CURRENT DATE THE    **01490000
      **  THAT RECORD WILL BE KEPT. ELSE IT WILL BE TRIMMED FROM THE  **01500000
      **  PNA SLA EXTRACT FILE.                                       **01510000
      ******************************************************************01520000
      *                                                                 01530000
       2000-PROCESS-PNA.                                                01540000
      *                                                                 01550000
           INITIALIZE DPG51 DPG52 DPG53                                 01560000
                      DPG54 DPG55 DPG56.                                01570000
                                                                        01580000
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      01590000
           SET DPG51-INCREMENT-DATE       TO TRUE.                      01600000
           MOVE PC-DAYS-TO-INCREMENT      TO DPG51-INCR-DECR-DAYS-9.    01610000
           MOVE ZEROES                    TO DPG51-INCR-DECR-BUS-DAYS-9.01620000
                                                                        01630000
           MOVE SU103-PROCESS-DATE        TO DPG52-LK-DATE-INPUT.       01640000
           SET DPG52-LK-DTE-ISO           TO TRUE.                      01650000
                                                                        01660000
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            01670000
             USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                 01680000
                                                                        01690000
           EVALUATE TRUE                                                01700000
               WHEN DPG54-SEVERE-ERROR                                  01710000
               WHEN DPG54-DATE-INVALID                                  01720000
                   DISPLAY ' '                                          01730000
                   DISPLAY '*********** ABEND *************************'01740000
                   DISPLAY '** PROGRAM   **  = ' PC-CURRENT-PROGRAM-NAME01750000
                   DISPLAY '** PARAGRAPH **  = 2000-PROCESS-PNA **'     01760000
                   DISPLAY '** BAD CALL TO CALENDAR MODULE '            01770000
                   DISPLAY '** ADDING 370 DAYS TO PROCESS DATE    '     01780000
                   DISPLAY '** ' DPG54-ERROR-MESSAGE  SU103-PROCESS-DATE01790000
                   DISPLAY '*******************************************'01800000
                   DISPLAY ' '                                          01801000
                   PERFORM 3000-END-OF-JOB-ROUTINE                      01802000
                   MOVE +4019 TO ABEND-CODE                             01810000
                   CALL 'ILBOABN0' USING ABEND-CODE                     01820000
           END-EVALUATE.                                                01830000
                                                                        01840000
           MOVE DPG55-DB2-ISO-DATE     TO PV-DATE-RETURNED-FROM-ROUTINE.01850000
           IF PV-DATE-RETURNED-FROM-ROUTINE >= PC-CURRENT-DATE          01860000
              PERFORM 8100-WRITE-EXTRACT-RECORD                         01870000
           ELSE                                                         01880000
              ADD +1 TO PV-PNA-SLA-SKIP-CNT                             01890000
           END-IF                                                       01900000
           PERFORM 8000-READ-EXTRACT-FILE.                              01910000
      *                                                                 01920000
      ******************************************************************01921000
      **  THIS PARAGRAPH CLOSES THE INPUT & OUTPT FILES AND THEN      **01922000
      **  DISPLAYS THE RECORD COUNT IN THE SYSOUT.                    **01922100
      ******************************************************************01923000
      *                                                                 01924000
       3000-END-OF-JOB-ROUTINE.                                         01925000
      *                                                                 01926000
           CLOSE SU-PNA-SLA-EXTRACT-FILE                                01926100
                 SU-PNA-SLA-EXT-TRIM-FILE.                              01926200
      *                                                                 01926300
           DISPLAY '                                                   '01927000
           DISPLAY '** SUKUT144-TOTAL NO OF PNA SLA EXT RECS READ     :'01928000
                    PV-READ-COUNTER.                                    01929000
           DISPLAY '** SUKUT144-TOTAL NO OF PNA SLA EXT RECS TRIMMED  :'01929100
                   PV-PNA-SLA-SKIP-CNT.                                 01929200
           DISPLAY '** SUKUT144-TOTAL NO OF PNA SLA EXT OUTPUT WRITTEN:'01929300
                   PV-TOTAL-CNT.                                        01929400
           DISPLAY '                                        '.          01929500
           DISPLAY '** PROGRAM SUKUT144 EXECUTION ENDED   **'.          01929600
      *                                                                 01929700
      ******************************************************************01930000
      ** THIS PARAGRAPGH READS THE INPUT PNA SLA EXTRACT FILE IN01 IN **01940000
      ** SUWS103 LAYOUT.                                              **01950000
      ******************************************************************01960000
      *                                                                 01970000
       8000-READ-EXTRACT-FILE.                                          01980000
      *                                                                 01990000
           INITIALIZE   SU103-PNA-SLA-INFO.                             02000000
      *                                                                 02010000
           READ SU-PNA-SLA-EXTRACT-FILE INTO SU103-PNA-SLA-INFO         02020000
                AT END                                                  02030000
                 SET PS-EOF-PNA-SLA-EXT TO TRUE                         02040000
              NOT AT END                                                02050000
                 ADD +1 TO PV-READ-COUNTER                              02060000
           END-READ.                                                    02070000
      *                                                                 02080000
      ******************************************************************02090000
      **  THIS PARAGRAPH WRITES THE RECORDS IN TO THE OUTPUT FILE     **02100000
      **  IN SUWS103 LAYOUT.                                          **02110000
      ******************************************************************02120000
      *                                                                 02130000
       8100-WRITE-EXTRACT-RECORD.                                       02140000
      *                                                                 02150000
            WRITE SU-PNA-SLA-EXT-TRIM-RECORD                            02160000
             FROM SU103-PNA-SLA-INFO.                                   02170000
              ADD 1 TO PV-TOTAL-CNT.                                    02180000
      *                                                                 02190000
