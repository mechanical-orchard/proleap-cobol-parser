000100 TITLE 'REFORMAT AUDIT EXTRACT'.                                          
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    SUKUT064.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 DATE-WRITTEN.  WINTER 2002.                                              
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
000900 FILE-CONTROL.                                                            
001000     SELECT OLD-AUDIT-EXTRACT-FILE      ASSIGN TO INP01.                  
001100     SELECT NEW-AUDIT-EXTRACT-FILE      ASSIGN TO OUT01.                  
001200                                                                          
001300 DATA DIVISION.                                                           
001400 FILE SECTION.                                                            
001500                                                                          
001600                                                                          
001700 FD  OLD-AUDIT-EXTRACT-FILE                                               
001800     RECORDING MODE IS F                                                  
001900     LABEL RECORDS ARE STANDARD                                           
002000     RECORD CONTAINS 150                                                  
002100     BLOCK CONTAINS 0 RECORDS                                             
002200     DATA RECORD IS OLD-AUDIT-EXTRACT-RECORD.                             
002300 01  OLD-AUDIT-EXTRACT-RECORD         PIC X(150).                         
002400                                                                          
002500 FD  NEW-AUDIT-EXTRACT-FILE                                               
002600     RECORDING MODE IS F                                                  
002700     LABEL RECORDS ARE STANDARD                                           
002800     RECORD CONTAINS 150                                                  
002900     BLOCK CONTAINS 0 RECORDS                                             
003000     DATA RECORD IS NEW-AUDIT-EXTRACT-RECORD.                             
003100 01  NEW-AUDIT-EXTRACT-RECORD         PIC X(150).                         
003200                                                                          
003300 WORKING-STORAGE SECTION.                                                 
003400                                                                          
003500 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003600                                                   COMP SYNC.             
003700                                                                          
003800 01  PS-PROGRAM-SWITCHES.                                                 
003900     05  PS-END-EXTRACT-SW            PIC X(1)   VALUE 'N'.               
004000         88  PS-END-EXTRACT                      VALUE 'Y'.               
004100     05  PS-END-CARTON-CSR-SW         PIC X(1)   VALUE 'N'.               
004200         88  PS-EOF-CARTON-CSR                   VALUE 'Y'.               
004210         88  PS-NOT-EOF-CARTON-CSR               VALUE 'N'.               
004300                                                                          
004400                                                                          
004500 01  PV-PROGRAM-VARIABLES.                                                
004600     05  PV-RETRY-COUNT               PIC S9(04)  VALUE ZERO              
004700                                                  COMP SYNC.              
004800     05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACES.           
004900     05  PV-ERROR-MESSAGE-1           PIC X(60)   VALUE SPACES.           
005000     05  PV-ERROR-MESSAGE-2           PIC X(60)   VALUE SPACES.           
005100     05  PV-SQLCODE                   PIC 9(9)    VALUE ZERO.             
005700     05  PV-DISP-NBR                  PIC Z(8)9.                          
006200     05  PV-AUDIT-DURATION            PIC S9(9) COMP-3 VALUE ZERO.        
006210     05  PV-CARTON-DURATION           PIC S9(9) COMP-3 VALUE ZERO.        
006300                                                                          
006400 01  PV-COUNTERS COMP-3.                                                  
006500     05  PV-READ-COUNT                PIC S9(9)    VALUE ZERO.            
006600     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006710     05  PV-A-COUNT                   PIC S9(9)    VALUE ZERO.            
006720     05  PV-B-COUNT                   PIC S9(9)    VALUE ZERO.            
006730     05  PV-C-COUNT                   PIC S9(9)    VALUE ZERO.            
006800                                                                          
006900 01  PC-PROGRAM-CONSTANTS.                                                
007000     05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'SUKUT064'.        
007100     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
007200                                                 COMP SYNC.               
007210     05  PC-DEFAULT-TMST              PIC  X(26) VALUE                    
007220                                     '9999-09-09-09.09.09.999999'.        
007230                                                                          
007300 01  PV-ABEND-FIELDS.                                                     
007400     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'SUKUT064'.        
007500     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
007600     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
007700     05  PV-ABEND-STRUCTURE-1         PIC X(15)  VALUE SPACES.            
007800     05  PV-ABEND-STRUCTURE-2         PIC X(15)  VALUE SPACES.            
007900     05  PV-ABEND-STRUCTURE-3         PIC X(15)  VALUE SPACES.            
008000     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
008100     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
008200     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
008300     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
008400     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
008500                                                                          
008600/                                                                         
008700*    REFORMATTED AUDIT EXTRACT                                            
008900     COPY SURD030.                                                        
009000                                                                          
009100*    AUDIT-EXTRACT RECORD                                                 
009200     COPY SURD031.                                                        
009300                                                                          
009400*    DATE CONTROL CARD                                                    
009500     COPY SURD032.                                                        
009600                                                                          
009700/                                                                         
009800*    SHIP UNIT AUDITS                                                     
009900     EXEC SQL                                                             
010000         INCLUDE TSUNAUD                                                  
010100     END-EXEC                                                             
010200/                                                                         
010210*    SHIP UNIT AUDITS - CARTON RECORDS                                    
010220     EXEC SQL                                                             
010230         INCLUDE TSUATRN                                                  
010240     END-EXEC                                                             
010241                                                                          
010242* CARTON CURSOR FOR CALCULATING AUDIT DURATION.                           
010243                                                                          
010244     EXEC SQL                                                             
010253         DECLARE CARTON-CSR CURSOR FOR                                    
010254             SELECT                                                       
010255                SHIPT_UNT_ID                                              
010256               ,( (( DAYS(UNT_AUD_STP_TMST) -                             
010257                     DAYS(UNT_AUD_STRT_TMST) )                            
010258                     * 1440)                                              
010259             +                                                            
010260                  (( HOUR(UNT_AUD_STP_TMST) -                             
010261                     HOUR(UNT_AUD_STRT_TMST) )                            
010262                     * 60 )                                               
010263             +                                                            
010264                  ( MINUTE(UNT_AUD_STP_TMST) -                            
010265                    MINUTE(UNT_AUD_STRT_TMST) )                           
010266                                                                          
010267             -      UNT_PSPND_MNIT_QTY                                    
010268             -      UNT_CLO_MNIT_QTY )                                    
010283             FROM                                                         
010284                   TSUATRN                                                
010286             WHERE                                                        
010288                    AUD_CNTL_NBR      = :SU031-AUD-CNTL-NBR               
010289               AND  UNT_AUD_STP_TMST <> :PC-DEFAULT-TMST                  
010291     END-EXEC.                                                            
010292                                                                          
010293/                                                                         
010300*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
010400     COPY DPWS004.                                                        
010500                                                                          
010600*   DB2 SQL COMMUNICATION AREA                                            
010700     EXEC SQL                                                             
010800          INCLUDE SQLCA                                                   
010900     END-EXEC.                                                            
011000/                                                                         
011100 PROCEDURE DIVISION.                                                      
011200                                                                          
011300     PERFORM 1000-INIT.                                                   
011400                                                                          
011500     PERFORM 2000-PROCESS UNTIL PS-END-EXTRACT.                           
011600                                                                          
011700     PERFORM 9000-TERMINATE.                                              
011800                                                                          
011900     GOBACK.                                                              
012000                                                                          
012100 1000-INIT.                                                               
012200                                                                          
012220     DISPLAY '************************'.                                  
012230     DISPLAY '******* SUKUT064 *******'.                                  
012240     DISPLAY '************************'.                                  
012250     DISPLAY SPACES                                                       
012260                                                                          
012300     OPEN INPUT  OLD-AUDIT-EXTRACT-FILE                                   
012400          OUTPUT NEW-AUDIT-EXTRACT-FILE.                                  
012500                                                                          
012600     ACCEPT SU032-DATE-CONTROL-REC FROM SYSIN                             
012700                                                                          
012800     PERFORM 8000-READ-OLD-AUDIT-REC.                                     
012900                                                                          
013000 2000-PROCESS.                                                            
013100                                                                          
013200     PERFORM 3000-BUILD-EXTRACT.                                          
013300                                                                          
013400     PERFORM 8100-WRITE-EXTRACT-REC.                                      
013500                                                                          
013600     PERFORM 8000-READ-OLD-AUDIT-REC.                                     
013700/                                                                         
013800 3000-BUILD-EXTRACT.                                                      
013900                                                                          
014000     INITIALIZE SU030-RECORD                                              
014100                SU030-PROMPT-EXTRACT-REC                                  
014200                                                                          
014300     PERFORM 3010-COMMON-FIELDS.                                          
014400                                                                          
014500     EVALUATE SU031-RECORD-TYPE                                           
014600         WHEN 'A'                                                         
014700             PERFORM 3100-BUILD-A-REC                                     
014800         WHEN 'B'                                                         
014900             PERFORM 3200-BUILD-B-REC                                     
015000         WHEN 'C'                                                         
015100             PERFORM 3300-BUILD-C-REC                                     
015200     END-EVALUATE.                                                        
015300                                                                          
015400 3010-COMMON-FIELDS.                                                      
015500     SET SU030-AUDIT-RECORD           TO TRUE                             
015600     MOVE SU032-TO-DATE               TO SU030-WEEK-END-DATE              
015700     MOVE SU031-FISCAL-YEAR           TO SU030-FISCAL-YEAR                
015800     MOVE SU031-FISCAL-WEEK           TO SU030-FISCAL-WEEK                
015900     MOVE SU031-FISCAL-SEASON         TO SU030-FISCAL-SEASON              
016000     MOVE SU031-FISCAL-MONTH          TO SU030-FISCAL-MONTH               
016100     MOVE SU031-OPER-UNT-ID           TO SU030-OPER-UNT-ID                
016200     MOVE SU031-PKGNG-TYPE            TO SU030-PKGNG-TYPE                 
016300     MOVE SU031-VENDOR-RANK           TO SU030-VENDOR-RANK                
016400     MOVE SU031-AUD-CNT-COMPLT-REOPEN TO                                  
016500                                 SU030-AUD-CNT-COMPLT-REOPEN.             
016510     MOVE SU031-AUD-CNTL-NBR          TO SU030-AUD-CNTL-NBR.              
016600                                                                          
016610                                                                          
016700*-------------------------------------------------------------------      
016710* THESE ARE AUDITS THAT WERE STARTED AND COMPLETED DURING THE             
016720* THIS WEEK.                                                              
016721* ----------------------------------------                                
016722* SU030-AUD-CNT-PRMT-CMPL-WEEK = AUDIT PROMPT AND COMPLETE THIS WEEK      
016723* SU030-AUD-CNT-COMPLT         = # AUDITS COMPLETED THIS WEEK             
016724* SU030-AUD-CNT-PROMPT         = # AUDITS PROMPTED THIS WEEK              
016725* SU030-AUD-CNT-COMPLT-PASS    = # COMPLTD AUDITS THIS WK THAT PASS       
016726* SU030-AUD-CNT-COMPLT-FAIL    = # COMPLTD AUDITS THIS WK THAT FAIL       
016728* SU030-UNIT-QTY-PRMT-NOND-WEEK = UNITS FOR PROMPT AND NOT NEEDED         
016729* SU030-AUD-CRTN-QTY-COMPLT     = CRTNS AUDITED COMPLETED THIS WEEK       
016730* SU030-AUD-UNIT-QTY-COMPLT     = UNITS AUDITED COMPLETED THIS WEEK       
016731* SU030-UNIT-QTY-PRMT-CMPL-WEEK = UNITS AUDITED PROMP/COMPLT THIS WK      
016732* SU030-UNIT-QTY-PROMPT         = UNITS PROMPTED THIS WEEK                
016733*                                                                         
016740*-------------------------------------------------------------------      
016800 3100-BUILD-A-REC.                                                        
016900     ADD 1               TO PV-A-COUNT                                    
017000     MOVE 1                 TO SU030-AUD-CNT-PRMT-CMPL-WEEK               
017100                               SU030-AUD-CNT-COMPLT                       
017200                               SU030-AUD-CNT-PROMPT                       
017300                                                                          
017400     EVALUATE SU031-AUD-STAT-CDE                                          
017500        WHEN  'P'                                                         
017600             MOVE 1         TO SU030-AUD-CNT-COMPLT-PASS                  
017700        WHEN  'F'                                                         
017800             MOVE 1         TO SU030-AUD-CNT-COMPLT-FAIL                  
017900        WHEN 'N'                                                          
018000             MOVE SU031-AUD-UNT-QTY                                       
018100                            TO SU030-UNIT-QTY-PRMT-NOND-WEEK              
018200     END-EVALUATE.                                                        
018300                                                                          
018400     MOVE SU031-AUD-CART-QTY     TO SU030-AUD-CRTN-QTY-COMPLT             
018500     MOVE SU031-AUD-UNT-QTY      TO SU030-AUD-UNIT-QTY-COMPLT             
018600                                    SU030-UNIT-QTY-PRMT-CMPL-WEEK         
018700     MOVE SU031-UNT-QTY          TO SU030-UNIT-QTY-PROMPT                 
018800                                                                          
018900     PERFORM 4000-AUDIT-DURATION.                                         
019000     MOVE PV-AUDIT-DURATION           TO SU030-TOTAL-MINUTES.             
019100                                                                          
019200                                                                          
019210*-------------------------------------------------------------------      
019220* THESE ARE AUDITS THAT WERE PROMPTED DURING THIS WEEK BUT HAVE           
019230* NOT BEEN STARTED.                                                       
019250* ----------------------------------------                                
019260* SU030-AUD-CNT-PROMPT         = # AUDITS PROMPTED THIS WEEK              
019261* SU030-UNIT-QTY-PROMPT        = UNITS PROMPTED THIS WEEK                 
019262* SU030-UNIT-QTY-PRMT-INPR-WEEK = UNITS PROMPTED & INPROCESS THIS WK      
019270*-------------------------------------------------------------------      
019300 3200-BUILD-B-REC.                                                        
019400     ADD 1                            TO PV-B-COUNT                       
019500     MOVE 1                           TO SU030-AUD-CNT-PROMPT             
019600     MOVE SU031-UNT-QTY               TO SU030-UNIT-QTY-PROMPT            
019700     MOVE SU031-AUD-UNT-QTY           TO                                  
019800                               SU030-UNIT-QTY-PRMT-INPR-WEEK.             
019900                                                                          
019910                                                                          
019920*-------------------------------------------------------------------      
019930* THESE ARE AUDITS THAT WERE PROMPTED DURING A PREVIOUS WEEK BUT          
019940* WERE COMPLETED THIS WEEK                                                
019960* ----------------------------------------                                
019970* SU030-AUD-CNT-PRMT-CMPL-MULT = # AUDITS PROMPT/COMPLET DIFF WEEKS       
019971* SU030-AUD-CNT-COMPLT         = # AUDITS COMPLETED THIS WEEK             
019972* SU030-AUD-CNT-COMPLT-PASS    = # COMPLTD AUDITS THIS WK THAT PASS       
019973* SU030-AUD-CNT-COMPLT-FAIL    = # COMPLTD AUDITS THIS WK THAT FAIL       
019974* SU030-AUD-CRTN-QTY-COMPLT     = CRTNS AUDITED COMPLETED THIS WEEK       
019975* SU030-AUD-UNIT-QTY-COMPLT     = UNITS AUDITED COMPLETED THIS WEEK       
019976* SU030-UNIT-QTY-PRMT-CMPL-MULT = UNITS AUDITED PROMP/COMPL DIFF WK       
019977* SU030-UNIT-QTY-PRMT-NOND-MULT = UNITS PROMPT/NOT NEEDED DIFF WEEK       
019980*-------------------------------------------------------------------      
020000 3300-BUILD-C-REC.                                                        
020100                                                                          
020200     ADD 1                            TO PV-C-COUNT                       
020300     MOVE 1                 TO SU030-AUD-CNT-PRMT-CMPL-MULT               
020400                               SU030-AUD-CNT-COMPLT                       
020500                                                                          
020600     EVALUATE SU031-AUD-STAT-CDE                                          
020700        WHEN  'P'                                                         
020800             MOVE 1         TO SU030-AUD-CNT-COMPLT-PASS                  
020900        WHEN  'F'                                                         
021000             MOVE 1         TO SU030-AUD-CNT-COMPLT-FAIL                  
021010        WHEN 'N'                                                          
021020             MOVE SU031-AUD-UNT-QTY                                       
021030                            TO SU030-UNIT-QTY-PRMT-NOND-MULT              
021100     END-EVALUATE.                                                        
021200     MOVE SU031-AUD-CART-QTY     TO SU030-AUD-CRTN-QTY-COMPLT             
021300     MOVE SU031-AUD-UNT-QTY      TO SU030-AUD-UNIT-QTY-COMPLT             
021400                                    SU030-UNIT-QTY-PRMT-CMPL-MULT         
021500                                                                          
021600     PERFORM 4000-AUDIT-DURATION.                                         
021700     MOVE PV-AUDIT-DURATION           TO SU030-TOTAL-MINUTES.             
021800                                                                          
021900                                                                          
021910*-------------------------------------------------------------------      
021920* CALCULATE THE NUMBER OF MINUTES IT TOOK TO COMPLETE THE AUDIT    *      
021921* CALCULATE NUMBER OF MINUTES FOR EACH CARTON                      *      
021930*-------------------------------------------------------------------      
022000 4000-AUDIT-DURATION.                                                     
022100                                                                          
022101     INITIALIZE  PV-AUDIT-DURATION                                        
022102                 PV-CARTON-DURATION.                                      
022103     SET  PS-NOT-EOF-CARTON-CSR  TO  TRUE.                                
022105                                                                          
022110     PERFORM 5000-OPEN-CARTON-CSR.                                        
022120     PERFORM 5010-FETCH-CARTON-CSR.                                       
022130                                                                          
022170     IF  PS-EOF-CARTON-CSR                                                
022180         CONTINUE                                                         
022190     ELSE                                                                 
022191         PERFORM 5020-CARTON-DURATION                                     
022192           UNTIL PS-EOF-CARTON-CSR                                        
022193     END-IF.                                                              
022194                                                                          
022195     PERFORM 5030-CLOSE-CARTON-CSR.                                       
026000                                                                          
026001                                                                          
026002 5000-OPEN-CARTON-CSR.                                                    
026003                                                                          
026004     EXEC SQL                                                             
026005         OPEN CARTON-CSR                                                  
026006     END-EXEC.                                                            
026007                                                                          
026008     EVALUATE TRUE                                                        
026009         WHEN SQLCODE = ZERO                                              
026010             CONTINUE                                                     
026011         WHEN SQLWARN0 NOT = SPACE                                        
026012         WHEN SQLCODE  NOT = ZERO                                         
026013             MOVE '5000-OPEN-CARTON-CSR'                                  
026014                                       TO PV-ABEND-PARAGRAPH              
026015             MOVE 'OPEN'               TO PV-ABEND-OPERATION              
026016             MOVE 'TSUATRN'            TO PV-ABEND-STRUCTURE-1            
026018             PERFORM 9900-DB2-ABEND                                       
026019     END-EVALUATE.                                                        
026020                                                                          
026021                                                                          
026022 5010-FETCH-CARTON-CSR.                                                   
026023                                                                          
026024     EXEC SQL                                                             
026025         FETCH CARTON-CSR                                                 
026026         INTO                                                             
026027             :SUATRN-SHIPT-UNT-ID                                         
026028         ,   :PV-CARTON-DURATION                                          
026043     END-EXEC.                                                            
026044                                                                          
026045     EVALUATE TRUE                                                        
026046         WHEN SQLCODE = ZERO                                              
026047              IF  PV-CARTON-DURATION  = 0                                 
026048                  MOVE  1             TO PV-CARTON-DURATION               
026049              END-IF                                                      
026051                                                                          
026052         WHEN SQLCODE = +100                                              
026053              SET PS-EOF-CARTON-CSR    TO TRUE                            
026054                                                                          
026055         WHEN SQLWARN0 NOT = SPACE                                        
026056         WHEN SQLCODE  NOT = ZERO                                         
026057             MOVE '5010-FETCH-CARTON-CSR'                                 
026058                                       TO PV-ABEND-PARAGRAPH              
026059             MOVE 'FETCH'              TO PV-ABEND-OPERATION              
026060             MOVE 'TSUATRN'            TO PV-ABEND-STRUCTURE-1            
026062             PERFORM 9900-DB2-ABEND                                       
026063     END-EVALUATE.                                                        
026065                                                                          
026066                                                                          
026067 5020-CARTON-DURATION.                                                    
026068                                                                          
026069     ADD  PV-CARTON-DURATION TO PV-AUDIT-DURATION.                        
026070                                                                          
026071     PERFORM 5010-FETCH-CARTON-CSR.                                       
026072                                                                          
026073                                                                          
026074 5030-CLOSE-CARTON-CSR.                                                   
026075                                                                          
026076     EXEC SQL                                                             
026077         CLOSE CARTON-CSR                                                 
026078     END-EXEC.                                                            
026079                                                                          
026080     EVALUATE TRUE                                                        
026081         WHEN SQLCODE = ZERO                                              
026082             CONTINUE                                                     
026083                                                                          
026084         WHEN SQLWARN0 NOT = SPACE                                        
026085         WHEN SQLCODE  NOT = ZERO                                         
026086             MOVE '5030-CLOSE-CARTON-CSR'                                 
026087                                       TO PV-ABEND-PARAGRAPH              
026088             MOVE 'CLOSE'              TO PV-ABEND-OPERATION              
026089             MOVE 'TSUATRN'            TO PV-ABEND-STRUCTURE-1            
026090             PERFORM 9900-DB2-ABEND                                       
026091     END-EVALUATE.                                                        
026092                                                                          
026093                                                                          
026100 8000-READ-OLD-AUDIT-REC.                                                 
026200                                                                          
026300     READ OLD-AUDIT-EXTRACT-FILE INTO                                     
026400           SU031-RECORD                                                   
026500     AT END                                                               
026600         SET PS-END-EXTRACT           TO TRUE                             
026700     NOT AT END                                                           
026800         ADD 1                        TO PV-READ-COUNT                    
026900     END-READ.                                                            
027000                                                                          
027100 8100-WRITE-EXTRACT-REC.                                                  
027200                                                                          
027300     ADD 1                         TO PV-WRITE-COUNT                      
027400                                                                          
027500     WRITE NEW-AUDIT-EXTRACT-RECORD FROM  SU030-RECORD.                   
027600                                                                          
027610                                                                          
027700 9000-TERMINATE.                                                          
027800                                                                          
027900     CLOSE OLD-AUDIT-EXTRACT-FILE                                         
028000           NEW-AUDIT-EXTRACT-FILE                                         
028600                                                                          
028700     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
028800     DISPLAY 'OLD AUDIT EXTRACTS READ      = ' PV-DISP-NBR                
028900                                                                          
028910     MOVE PV-A-COUNT                  TO PV-DISP-NBR                      
028920     DISPLAY 'A-RECS WRITTEN               = ' PV-DISP-NBR                
028921     MOVE PV-B-COUNT                  TO PV-DISP-NBR                      
028922     DISPLAY 'B-RECS WRITTEN               = ' PV-DISP-NBR                
028923     MOVE PV-C-COUNT                  TO PV-DISP-NBR                      
028924     DISPLAY 'C-RECS WRITTEN               = ' PV-DISP-NBR                
028925     DISPLAY SPACES                                                       
028926     DISPLAY '                                _________'                  
028930                                                                          
028940                                                                          
029000     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
029100     DISPLAY 'REFORMATTED EXTRACTS WRITTEN = ' PV-DISP-NBR                
029200                                                                          
029300     DISPLAY SPACES                                                       
029400     DISPLAY '************************'.                                  
029500                                                                          
029600 9900-DB2-ABEND.                                                          
029700                                                                          
029800     MOVE SQLCODE TO PV-SQLCODE.                                          
029900     DISPLAY '*** DB2 ERROR '.                                            
030000     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
030100     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
030200     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
030300     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
030400     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
030500     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
030600     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
030700     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
031000     DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2                      
031200                                                                          
031300                                                                          
031400     COPY DPPD004.                                                        
031500                                                                          
031600     MOVE +4013                  TO PV-ABEND-CODE.                        
031700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
031800                                                                          
031900*9200-ABEND.                                                              
032000******************************************************************        
032100*  PERFORMS A NON-SQL ABEND FOR THE PROGRAM                               
032200******************************************************************        
032300*    DISPLAY '***************************'.                               
032400*    DISPLAY '**       ABEND           **'.                               
032500*    DISPLAY '**  IN PROGRAM SUKUT064  **'.                               
032600*    DISPLAY '***************************'.                               
032700*    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
032800*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-1.               
032900*    DISPLAY '**                 ** = ' PV-ERROR-MESSAGE-2.               
033000*                                                                         
033100*    MOVE +4014                  TO PV-ABEND-CODE.                        
033200*    CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
033300                                                                          
