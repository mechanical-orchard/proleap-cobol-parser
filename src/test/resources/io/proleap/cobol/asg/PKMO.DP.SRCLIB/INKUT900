       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT900.                                                 
       AUTHOR.        RYAN STAUFFACHER.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-28-08.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      * INKUT900 -                                                              
      *                                                                         
      * THIS PROGRAM WILL COMPARE IN AN VA SHORTAGE EXTRACT FILES AND           
      * CREATE A REPORT FILE OF THE DIFFERENCES.                                
      *                                                                         
      * THE SAME CONTROL CARD WILL BE READ BY THE REPORT PROGRAM SO THAT        
      * IT CAN PUT THE APPROPRIATE TABLE NAMES ON THE REPORT.                   
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-DATA-SHORTAGE-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VA-DATA-SHORTAGE-FILE                                         
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATA-INTEGRITY-DIFF-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IN-DATA-SHORTAGE-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  IN-DATA-SHORTAGE-REC        PIC X(140).                              
                                                                                
       FD  VA-DATA-SHORTAGE-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VA-DATA-SHORTAGE-REC        PIC X(140).                              
                                                                                
       FD  DATA-INTEGRITY-DIFF-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATA-INTEGRITY-DIFF-REC     PIC X(80).                               
                                                                                
      *                                                                         
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-DPKUT500                  PIC X(08) VALUE 'DPKUT500'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-IN-SHTG-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-IN-SHTG-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-IN-SHTG-RECS         VALUE 'N'.                   
           05  PS-MORE-VA-SHTG-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-VA-SHTG-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-VA-SHTG-RECS         VALUE 'N'.                   
           05  PS-BYPASS-0-DOLLARS-SW  PIC X       VALUE 'Y'.                   
               88  PS-BYPASS-0-DOLLARS             VALUE 'Y'.                   
               88  PS-NO-BYPASS-DOLLARS            VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-IN-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-VA-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-KEY-MATCHES          PIC S9(13)  VALUE ZERO.                  
           05  PA-REPORT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-IN-FILE-KEY.                                                  
               10  PV-IN-DEPT-NBR      PIC X(03).                               
               10  PV-IN-MAJ-CL-NBR    PIC X(02).                               
               10  PV-IN-SUB-CL-NBR    PIC X(02).                               
               10  PV-IN-STR-NBR       PIC S9(04) COMP.                         
           05  PV-VA-FILE-KEY.                                                  
               10  PV-VA-DEPT-NBR      PIC X(03).                               
               10  PV-VA-MAJ-CL-NBR    PIC X(02).                               
               10  PV-VA-SUB-CL-NBR    PIC X(02).                               
               10  PV-VA-STR-NBR       PIC S9(04) COMP.                         
           05  PV-WORK-ADJ-DTE.                                                 
               10  PV-WORK-ADJ-MM      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-DD      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-YY      PIC  X(2) VALUE '00'.                    
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: INKUT900'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** IN DATA INTEGRITY EXTRACT FILE                                        
           COPY INRD014.                                                        
                                                                                
      *** VA DATA INTEGRITY EXTRACT FILE                                        
           COPY INRD014 REPLACING IN014 BY VA014.                               
                                                                                
      *** DATA INTEGRITY DIFFERENCE REPORT FILE                                 
           COPY INRD900.                                                        
      *                                                                         
      ******************************************************************        
      *    CALENDAR ROUTINE                                                     
      ******************************************************************        
           COPY DPWS500.                                                        
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-INKUT216.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
      *                                                                         
           OPEN INPUT  IN-DATA-SHORTAGE-FILE                                    
                       VA-DATA-SHORTAGE-FILE.                                   
           OPEN OUTPUT DATA-INTEGRITY-DIFF-FILE.                                
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
           MOVE SPACES TO IN900-FSCL-MN-END-DTE                                 
                          IN900-FSCL-MN-NBR.                                    
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 4100-READ-IN-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-IN-SHTG-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY IN EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4002              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-VA-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-VA-SHTG-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY VA EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-IN-SHTG-RECS                                      
               AND PS-NO-MORE-VA-SHTG-RECS                                      
               PERFORM 2100-COMPARE-FILES                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2100-COMPARE-FILES.                                                      
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-FILE-KEYS.                                        
                                                                                
           IF PV-IN-FILE-KEY = PV-VA-FILE-KEY                                   
               ADD 1                   TO PA-KEY-MATCHES                        
               PERFORM 5100-COMPARE-BOTH-FILE-AMTS                              
               PERFORM 4100-READ-IN-EXTRACT-FILE                                
               PERFORM 4200-READ-VA-EXTRACT-FILE                                
           ELSE                                                                 
               IF PV-IN-FILE-KEY < PV-VA-FILE-KEY                               
                   PERFORM 5200-PROCESS-ONLY-IN-FILE                            
                   PERFORM 4100-READ-IN-EXTRACT-FILE                            
               ELSE                                                             
                   PERFORM 5300-PROCESS-ONLY-VA-FILE                            
                   PERFORM 4200-READ-VA-EXTRACT-FILE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3000-BUILD-FILE-KEYS.                                                    
      ******************************************************************        
      *                                                                         
           INITIALIZE PV-IN-FILE-KEY.                                           
           INITIALIZE PV-VA-FILE-KEY.                                           
                                                                                
           IF IN014-SHORTAGE-EXTR-REC = HIGH-VALUES                             
               MOVE HIGH-VALUES        TO PV-IN-FILE-KEY                        
           ELSE                                                                 
               MOVE IN014-DEPT-NBR   TO PV-IN-DEPT-NBR                          
               MOVE IN014-MAJ-CL-NBR TO PV-IN-MAJ-CL-NBR                        
               MOVE IN014-SUB-CL-NBR TO PV-IN-SUB-CL-NBR                        
               MOVE IN014-STR-NBR    TO PV-IN-STR-NBR                           
           END-IF.                                                              
           IF VA014-SHORTAGE-EXTR-REC = HIGH-VALUES                             
               MOVE HIGH-VALUES        TO PV-VA-FILE-KEY                        
           ELSE                                                                 
               MOVE VA014-DEPT-NBR   TO PV-VA-DEPT-NBR                          
               MOVE VA014-MAJ-CL-NBR TO PV-VA-MAJ-CL-NBR                        
               MOVE VA014-SUB-CL-NBR TO PV-VA-SUB-CL-NBR                        
               MOVE VA014-STR-NBR    TO PV-VA-STR-NBR                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4100-READ-IN-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-IN-SHTG-RECS                                              
               READ IN-DATA-SHORTAGE-FILE                                       
                 INTO IN014-SHORTAGE-EXTR-REC                                   
                   AT END                                                       
                       SET PS-NO-MORE-IN-SHTG-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO IN014-SHORTAGE-EXTR-REC               
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-IN-SHTG-RECS                                              
               ADD 1                   TO PA-IN-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4200-READ-VA-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-VA-SHTG-RECS                                              
               READ VA-DATA-SHORTAGE-FILE                                       
                 INTO VA014-SHORTAGE-EXTR-REC                                   
                   AT END                                                       
                       SET PS-NO-MORE-VA-SHTG-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO VA014-SHORTAGE-EXTR-REC               
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-VA-SHTG-RECS                                              
               ADD 1                   TO PA-VA-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5100-COMPARE-BOTH-FILE-AMTS.                                             
      ******************************************************************        
      *                                                                         
           MOVE VA014-DEPT-NBR      TO IN900-DEPT-NBR.                          
           MOVE VA014-MAJ-CL-NBR    TO IN900-MAJ-CL-NBR.                        
           MOVE VA014-SUB-CL-NBR    TO IN900-SUB-CL-NBR.                        
           MOVE VA014-STR-NBR       TO IN900-STR-NBR.                           
           MOVE SPACES              TO IN900-DKEY-NBR.                          
           IF IN014-SHORTAGE-RTL-AMT NOT = VA014-SHORTAGE-RTL-AMT               
               MOVE IN014-SHORTAGE-RTL-AMT TO IN900-IN-AMT                      
               MOVE VA014-SHORTAGE-RTL-AMT TO IN900-VA-AMT                      
               MOVE 5                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5200-PROCESS-ONLY-IN-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5210-BYPASS-IN-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-PROCESS-NO-VA-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5210-BYPASS-IN-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (IN014-SHORTAGE-RTL-AMT = 0)                                      
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5220-PROCESS-NO-VA-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           MOVE IN014-DEPT-NBR         TO IN900-FSCL-MN-NBR.                    
           MOVE IN014-DEPT-NBR         TO IN900-DEPT-NBR.                       
           MOVE IN014-MAJ-CL-NBR       TO IN900-MAJ-CL-NBR                      
           MOVE IN014-SUB-CL-NBR       TO IN900-SUB-CL-NBR.                     
           MOVE IN014-STR-NBR          TO IN900-STR-NBR                         
           MOVE SPACES                 TO IN900-DKEY-NBR                        
                                                                                
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 1                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5300-PROCESS-ONLY-VA-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5310-BYPASS-VA-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5320-PROCESS-NO-IN-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5310-BYPASS-VA-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (VA014-SHORTAGE-RTL-AMT = 0)                                      
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5320-PROCESS-NO-IN-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           MOVE VA014-DEPT-NBR         TO IN900-FSCL-MN-NBR.                    
           MOVE VA014-DEPT-NBR         TO IN900-DEPT-NBR.                       
           MOVE VA014-MAJ-CL-NBR       TO IN900-MAJ-CL-NBR                      
           MOVE VA014-SUB-CL-NBR       TO IN900-SUB-CL-NBR.                     
           MOVE VA014-STR-NBR          TO IN900-STR-NBR                         
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 2                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
      ******************************************************************        
       8000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM INKUT216 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-IN-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'IN RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-VA-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'VA RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-KEY-MATCHES         TO CD-COUNT-HOLD1.                       
           DISPLAY 'KEY MATCHES =            ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-REPORT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
           DISPLAY 'REPORT RECORDS WRITTEN = ' CD-COUNT-HOLD1.                  
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE IN-DATA-SHORTAGE-FILE.                                         
           CLOSE VA-DATA-SHORTAGE-FILE.                                         
           CLOSE DATA-INTEGRITY-DIFF-FILE.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
