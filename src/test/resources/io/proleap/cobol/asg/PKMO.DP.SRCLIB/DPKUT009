       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    DPKUT009.                                                 
       AUTHOR.        CAL BUSH.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/24/97.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      **                                                             **         
      ** THIS PROGRAM REFORMATS COBOL SOURCE FILES FOR DOWNLOAD TO   **         
      ** MICROFOCUS.  THE 'MARGIN A' NUMBERS ARE CLEARED AND THE     **         
      ** FILE IS SHORTENED TO 72 BYTE LRECL.                         **         
      **                                                             **         
      **-------------------------------------------------------------**         
      ** JJR 02/04/99  HANDLE COPY REPLACING AND SUPPRESS CORRECTLY. **         
      ** JJR 05/04/99  REPLACE BMS TYPE=MAP/DSECT WITH TYPE=&SYSPARM.**         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-SOURCE-FILE         ASSIGN TO INP01.                    
           SELECT OUTPUT-SOURCE-FILE        ASSIGN TO OUT01.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-SOURCE-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS.                                       
       01  INPUT-SOURCE-RECORD              PIC  X(80).                         
                                                                                
       FD  OUTPUT-SOURCE-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           DATA RECORD IS OUTPUT-SOURCE-RECORD.                                 
       01  OUTPUT-SOURCE-RECORD             PIC  X(72).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  IR-INPUT-RECORD.                                                     
           05  IR-COL-1-72.                                                     
               10  IR-LINE-NUM           PIC  X(06)  VALUE SPACE.               
               10  IR-AREA-B.                                                   
                   15  IR-COMMENT-IND    PIC  X(01)  VALUE SPACE.               
                       88 IR-COMMENT                 VALUE '*'.                 
                   15  IR-CODE           PIC  X(65)  VALUE SPACE.               
           05  FILLER                    PIC  X(08)  VALUE SPACE.               
                                                                                
       01  OR-OUTPUT-RECORD.                                                    
           05  OR-LINE-NUM               PIC  X(06)  VALUE SPACE.               
           05  OR-AREA-B.                                                       
               10  FILLER                PIC  X(05)  VALUE SPACE.               
               10  OR-COPY-IND           PIC  X(04)  VALUE SPACE.               
                   88  OR-COPY                       VALUE 'COPY'.              
               10  FILLER                PIC  X(01)  VALUE SPACE.               
               10  OR-COPY-MBR           PIC  X(56)  VALUE SPACE.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-FILE-EXT-LENGTH        PIC S9(04)  COMP                       
                                                     VALUE +3.                  
      *---------------------------------------------------------------          
      * THIS GOOFY REDEFINE IS REQUIRED BECAUSE YOU CAN NOT HAVE THE            
      * WORD IT REPRESENTS IN YOUR PROGRAM OR YOU WILL BE TOLD TO USE           
      * A PRECOMPILER TO HANDLE THE "COPY SUPPR..." COMMAND, EVEN               
      * THOUGH IT IS NOT A "COPY SUPPR..." COMMAND.  ALSO, THE REDEFINE         
      * NEEDED BECAUSE THE INSPECT COMMAND REQUIRES AN ELEMENTARY ITEM          
      * BE USED (I.E. PC-SUPRS NOT PC-SUPRS-GROUP).                             
       01  PC-SUPRS-GROUP.                                                      
           05  PC-SUPP                   PIC  X(05)  VALUE ' SUPP'.             
           05  PC-RESS                   PIC  X(04)  VALUE  'RESS'.             
       01  PC-SUPRS  REDEFINES PC-SUPRS-GROUP                                   
                                         PIC  X(09).                            
      *---------------------------------------------------------------          
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SW                 PIC  X(01)  VALUE 'N'.                 
               88  PS-EOF                            VALUE 'Y'.                 
           05  PS-CLEAR-SEQ-SW           PIC  X(01)  VALUE 'Y'.                 
               88  PS-CLEAR-SEQ                      VALUE 'Y'.                 
               88  PS-NO-CLEAR-SEQ                   VALUE 'N'.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECORD-COUNT           PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-INCLUDE-CNT            PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-COPY-CNT               PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-REPLACING-CNT          PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-SUPP-CNT               PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-FETCH-FROM-CNT         PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-DSECT-CNT              PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-MAP-CNT                PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-MAPSET-CNT             PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-FINAL-CNT              PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-TYPE-CNT               PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-SINGLE-QUOTE-CNT       PIC  9(04)  VALUE ZERO.                
           05  FILLER  REDEFINES PV-SINGLE-QUOTE-CNT.                           
               10                        PIC  9(03).                            
               10                        PIC  9(01).                            
                   88  EVEN-NUMBER-OF-SINGLE-QUOTES  VALUE 0 2 4 6 8.           
           05  PV-UNSTRING-PTR           PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-COPY-MBR               PIC  X(09)  VALUE SPACE.               
           05  PV-JUNK                   PIC  X(66)  VALUE SPACE.               
           05  PV-SPACES                 PIC  9(04)  VALUE ZERO.                
           05  PV-NEW                    PIC  X(72)  VALUE SPACE.               
           05  PV-NEW-CNT                PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-NEW-SPACES             PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-BEGIN                  PIC  X(72)  VALUE SPACE.               
           05  PV-BEGIN-CNT              PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-BEGIN-SPACES           PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-END                    PIC  X(72)  VALUE SPACE.               
           05  PV-END-CNT                PIC  9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-CONT-CHAR              PIC  X(01)  VALUE SPACE.               
           05  PV-SUB                    PIC  9(02)  VALUE ZERO.                
                                                                                
       01  PV-CURRENT-DATE.                                                     
           05  PV-CURRENT-MM             PIC  X(02).                            
           05                            PIC  X(01)  VALUE '/'.                 
           05  PV-CURRENT-DD             PIC  X(02).                            
           05                            PIC  X(01)  VALUE '/'.                 
           05  PV-CURRENT-CCYY           PIC  X(04).                            
                                                                                
       01  PV-CHANGE-COMMENTS.                                                  
           05  PV-MAPSET-RENAME-1        PIC  X(02)                             
               VALUE '* '.                                                      
           05  PV-MAPSET-RENAME-2        PIC  X(33)                             
               VALUE '  DPKUT009  TO FOLLOW STANDARDS, '.                       
           05  PV-MAPSET-RENAME-3        PIC  X(23)                             
               VALUE 'MAPSET ON NEXT LINE WAS'.                                 
           05  PV-MAPSET-RENAME-4        PIC  X(43)                             
               VALUE '*                       AUTO-RENAMED FROM '''.            
           05  PV-MAPSET-RENAME-5        PIC  X(06)                             
               VALUE ''' TO '''.                                                
           05  PV-MAPSET-RENAME-6        PIC  X(02)                             
               VALUE '''.'.                                                     
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  LS-LINKAGE-SECTION-PARAMETERS.                                       
           05  LS-PARAMETER-LENGTH       PIC S9(4)                              
                                         COMP SYNC.                             
           05  LS-TYPE-PARAMETER.                                               
               10  LS-FILE-TYPE          PIC  X(03).                            
                   88  LS-NO-CLEAR-SEQ               VALUES 'BMS'               
                                                            'ASM'.              
               10  LS-FILE-NAME          PIC  X(08).                            
                                                                                
                                                                                
       PROCEDURE DIVISION USING LS-LINKAGE-SECTION-PARAMETERS.                  
      *****************************************************************         
      **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.  **         
      *****************************************************************         
       0000-MAIN-DRIVER.                                                        
           PERFORM 1000-INITIALIZATION.                                         
           IF  PS-CLEAR-SEQ                                                     
               PERFORM 2000-CLEAR-AND-TRUNC                                     
                   UNTIL PS-EOF                                                 
           ELSE                                                                 
               PERFORM 3000-TRUNC-ONLY                                          
                   UNTIL PS-EOF                                                 
           END-IF.                                                              
                                                                                
           PERFORM 5000-TERMINATION.                                            
                                                                                
           GOBACK.                                                              
      ****************************************************************          
      ** OPEN FILES AND DO THE PRIMING READ.                        **          
      ****************************************************************          
       1000-INITIALIZATION.                                                     
           OPEN INPUT  INPUT-SOURCE-FILE                                        
                OUTPUT OUTPUT-SOURCE-FILE.                                      
                                                                                
           IF  LS-PARAMETER-LENGTH >= PC-FILE-EXT-LENGTH                        
               IF  LS-NO-CLEAR-SEQ                                              
                   SET  PS-NO-CLEAR-SEQ TO TRUE                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-INPUT-RECORD.                                      
      ****************************************************************          
      ** MOVE 'AREA B' OF THE INPUT SOURCE CODE TO 'AREA B' OF THE  **          
      ** OUTPUT SOURCE CODE AND THEN WRITE THE RECORD.              **          
      ****************************************************************          
       2000-CLEAR-AND-TRUNC.                                                    
           MOVE IR-LINE-NUM         TO OR-LINE-NUM.                             
           INSPECT IR-LINE-NUM REPLACING ALL SPACE BY '0'.                      
           IF  IR-LINE-NUM NUMERIC                                              
               MOVE SPACE           TO OR-LINE-NUM                              
           END-IF.                                                              
                                                                                
      * REMOVE ASTERICK ON LINES WITHOUT COMMENTS.                              
      **** IF  IR-COMMENT                                                       
      **** AND IR-CODE = SPACES                                                 
      ****     MOVE SPACE           TO IR-COMMENT-IND                           
      **** END-IF.                                                              
                                                                                
           MOVE IR-AREA-B           TO OR-AREA-B.                               
           ADD +1                   TO PV-RECORD-COUNT.                         
           IF  IR-COMMENT                                                       
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2100-CHECK-FOR-COPY                                      
               PERFORM 2200-CHECK-FOR-FETCH-FROM                                
           END-IF.                                                              
           PERFORM 4100-WRITE-OUTPUT-RECORD.                                    
                                                                                
           PERFORM 4000-READ-INPUT-RECORD.                                      
      ***************************************************************           
      ** IF THERE IS A COPY OR INCLUDE STATEMENT, REFORMAT TO                   
      ** TO INSURE PROPER FORMATTING.  ANY UNSTRING IS INCLUDED HERE            
      ** IN CASE THE COPY OR INCLUDE STRING IS QUOTED.                          
      ***************************************************************           
       2100-CHECK-FOR-COPY.                                                     
           MOVE ZERO TO PV-COPY-CNT                                             
                        PV-REPLACING-CNT                                        
                        PV-SUPP-CNT                                             
                        PV-INCLUDE-CNT.                                         
           INSPECT IR-AREA-B                                                    
               TALLYING                                                         
                   PV-INCLUDE-CNT                                               
                   FOR ALL '++INCLUDE '                                         
                   PV-COPY-CNT                                                  
                   FOR ALL ' COPY '                                             
                   PV-REPLACING-CNT                                             
                   FOR ALL ' REPLACING '                                        
                   PV-SUPP-CNT                                                  
                   FOR ALL PC-SUPRS.                                            
                                                                                
           IF PV-COPY-CNT > ZERO                                                
           OR IR-AREA-B (1:5) = 'COPY '                                         
               MOVE SPACE     TO PV-JUNK                                        
               UNSTRING IR-AREA-B                                               
                   DELIMITED BY 'COPY'                                          
                   INTO PV-JUNK                                                 
               IF  PV-JUNK = SPACE                                              
                   PERFORM 2110-FORMAT-COPY                                     
               END-IF                                                           
           ELSE                                                                 
               IF  PV-INCLUDE-CNT > ZERO                                        
                   MOVE SPACE     TO PV-JUNK                                    
                   UNSTRING IR-AREA-B                                           
                       DELIMITED BY '++INCLUDE'                                 
                       INTO PV-JUNK                                             
                   IF  PV-JUNK = SPACE                                          
                       PERFORM 2120-FORMAT-INCLUDE                              
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       2110-FORMAT-COPY.                                                        
           MOVE 1     TO PV-UNSTRING-PTR.                                       
           MOVE SPACE TO OR-AREA-B.                                             
           MOVE ZERO  TO PV-SPACES.                                             
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY 'COPY'                                              
               INTO PV-JUNK                                                     
               WITH POINTER PV-UNSTRING-PTR.                                    
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY ALL SPACES                                          
               INTO PV-JUNK                                                     
               WITH POINTER PV-UNSTRING-PTR.                                    
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY ALL SPACES                                          
                         OR '.'                                                 
               INTO PV-COPY-MBR                                                 
               WITH POINTER PV-UNSTRING-PTR.                                    
           SET OR-COPY        TO TRUE.                                          
           MOVE PV-COPY-MBR   TO OR-COPY-MBR.                                   
           IF PV-REPLACING-CNT > ZERO                                           
               INSPECT PV-COPY-MBR                                              
                   TALLYING PV-SPACES FOR ALL SPACES                            
               MOVE IR-AREA-B  (PV-UNSTRING-PTR:66 - PV-UNSTRING-PTR)           
                 TO OR-COPY-MBR (11 - PV-SPACES:66 - PV-UNSTRING-PTR)           
           ELSE                                                                 
           IF PV-SUPP-CNT > ZERO                                                
               MOVE 'SUPPRESS.' TO OR-COPY-MBR (9:9)                            
           ELSE                                                                 
               INSPECT OR-COPY-MBR                                              
                   REPLACING FIRST SPACE BY '.'.                                
                                                                                
                                                                                
       2120-FORMAT-INCLUDE.                                                     
           MOVE 1     TO PV-UNSTRING-PTR.                                       
           MOVE SPACE TO OR-AREA-B.                                             
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY '++INCLUDE'                                         
               INTO PV-JUNK                                                     
               WITH POINTER PV-UNSTRING-PTR.                                    
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY ALL SPACES                                          
               INTO PV-JUNK                                                     
               WITH POINTER PV-UNSTRING-PTR.                                    
           UNSTRING IR-AREA-B                                                   
               DELIMITED BY ALL SPACES                                          
                         OR '.'                                                 
               INTO PV-COPY-MBR                                                 
               WITH POINTER PV-UNSTRING-PTR.                                    
           INSPECT PV-COPY-MBR                                                  
               REPLACING FIRST SPACE BY '.'.                                    
           SET OR-COPY        TO TRUE.                                          
           MOVE PV-COPY-MBR   TO OR-COPY-MBR.                                   
                                                                                
      ***************************************************************           
      ** IF THERE IS A "FETCH FROM" STATEMENT, REMOVE THE "FROM" SO             
      ** MFE WILL COMPILE IT CORRECTLY.                                         
      ***************************************************************           
       2200-CHECK-FOR-FETCH-FROM.                                               
           MOVE ZERO TO PV-FETCH-FROM-CNT.                                      
           INSPECT IR-AREA-B                                                    
               TALLYING                                                         
                   PV-FETCH-FROM-CNT                                            
                   FOR ALL ' FETCH FROM '.                                      
                                                                                
           IF PV-FETCH-FROM-CNT > ZERO                                          
               UNSTRING IR-AREA-B                                               
                   DELIMITED BY ' FETCH FROM '                                  
                   INTO PV-BEGIN COUNT IN PV-BEGIN-CNT                          
                        PV-END   COUNT IN PV-END-CNT                            
               MOVE SPACES TO OR-AREA-B                                         
               STRING PV-BEGIN (1:PV-BEGIN-CNT)                                 
                      ' FETCH '                                                 
                      PV-END (1:PV-END-CNT)                                     
                 DELIMITED BY SIZE INTO OR-AREA-B.                              
                                                                                
      ****************************************************************          
      ** MOVE 'AREA B' OF THE INPUT SOURCE CODE TO 'AREA B' OF THE  **          
      ** OUTPUT SOURCE CODE AND THEN WRITE THE RECORD.              **          
      ****************************************************************          
       3000-TRUNC-ONLY.                                                         
           IF LS-FILE-TYPE = 'BMS'                                              
               PERFORM 3100-CHECK-MAP-TYPE                                      
               PERFORM 3200-CHECK-MAP-CONT                                      
               PERFORM 3300-CHECK-MAP-SET.                                      
                                                                                
           MOVE IR-COL-1-72         TO OR-OUTPUT-RECORD.                        
           ADD +1                   TO PV-RECORD-COUNT.                         
           PERFORM 4100-WRITE-OUTPUT-RECORD.                                    
                                                                                
           PERFORM 4000-READ-INPUT-RECORD.                                      
                                                                                
                                                                                
      ****************************************************************          
      ** REPLACE TYPE=MAP/DSECT WITH TYPE=&SYSPARM.                 **          
      ****************************************************************          
       3100-CHECK-MAP-TYPE.                                                     
           MOVE ZERO TO PV-DSECT-CNT                                            
                        PV-MAP-CNT                                              
                        PV-TYPE-CNT.                                            
           INSPECT IR-COL-1-72                                                  
             TALLYING PV-DSECT-CNT FOR ALL 'TYPE=DSECT'                         
                      PV-MAP-CNT   FOR ALL 'TYPE=MAP'.                          
           INSPECT IR-COL-1-72                                                  
             TALLYING PV-TYPE-CNT  FOR ALL ',TYPE='.                            
                                                                                
           IF PV-DSECT-CNT > ZERO                                               
               UNSTRING IR-COL-1-72                                             
                   DELIMITED BY 'TYPE=DSECT'                                    
                   INTO PV-BEGIN COUNT IN PV-BEGIN-CNT                          
                        PV-END   COUNT IN PV-END-CNT.                           
                                                                                
           IF PV-MAP-CNT > ZERO                                                 
               UNSTRING IR-COL-1-72                                             
                   DELIMITED BY 'TYPE=MAP'                                      
                   INTO PV-BEGIN COUNT IN PV-BEGIN-CNT                          
                        PV-END   COUNT IN PV-END-CNT.                           
                                                                                
           IF PV-DSECT-CNT > ZERO                                               
           OR PV-MAP-CNT > ZERO                                                 
      *        CHECK IF WE NEED TO SPLIT LINE TO FIT NEW CODE.                  
               IF IR-COL-1-72 (67:5) <= SPACES                                  
                   MOVE IR-COL-1-72 (72:1) TO PV-CONT-CHAR                      
                   MOVE SPACES           TO IR-COL-1-72                         
                   STRING PV-BEGIN (1:PV-BEGIN-CNT)                             
                          'TYPE=&SYSPARM'                                       
                          PV-END (1:PV-END-CNT - 1)                             
                     DELIMITED BY SIZE INTO IR-COL-1-72                         
                   MOVE PV-CONT-CHAR     TO IR-COL-1-72 (72:1)                  
               ELSE                                                             
                   MOVE IR-COL-1-72 (72:1) TO PV-CONT-CHAR                      
                   MOVE SPACES             TO IR-COL-1-72                       
      *        CHECK TO SPLIT LINE BEFORE OR AFTER "TYPE=".                     
                   IF PV-TYPE-CNT > ZERO                                        
                       STRING PV-BEGIN (1:PV-BEGIN-CNT)                         
                         DELIMITED BY SIZE INTO IR-COL-1-72                     
                       MOVE 'C'              TO IR-COL-1-72 (72:1)              
                       MOVE IR-COL-1-72      TO OR-OUTPUT-RECORD                
                       PERFORM 4100-WRITE-OUTPUT-RECORD                         
                       MOVE SPACES             TO IR-COL-1-72                   
                       STRING '               '                                 
                              'TYPE=&SYSPARM,'                                  
                              PV-END (2:PV-END-CNT - 2)                         
                         DELIMITED BY SIZE INTO IR-COL-1-72                     
                       MOVE PV-CONT-CHAR     TO IR-COL-1-72 (72:1)              
                   ELSE                                                         
                       STRING PV-BEGIN (1:PV-BEGIN-CNT)                         
                              'TYPE=&SYSPARM,'                                  
                         DELIMITED BY SIZE INTO IR-COL-1-72                     
                       MOVE 'C'              TO IR-COL-1-72 (72:1)              
                       MOVE IR-COL-1-72      TO OR-OUTPUT-RECORD                
                       PERFORM 4100-WRITE-OUTPUT-RECORD                         
                       MOVE SPACES             TO IR-COL-1-72                   
                       STRING '               '                                 
                              PV-END (2:PV-END-CNT - 2)                         
                         DELIMITED BY SIZE INTO IR-COL-1-72                     
                       MOVE PV-CONT-CHAR     TO IR-COL-1-72 (72:1).             
                                                                                
                                                                                
      ****************************************************************          
      ** IF CONTINUATION CHAR IS NOT SET, MAKE SURE LINE DOESN'T    **          
      ** END WITH A COMMA AND VICE VERSA.                           **          
      ****************************************************************          
       3200-CHECK-MAP-CONT.                                                     
           IF IR-COL-1-72 (1:1) = '*'                                           
               MOVE ZERO TO PV-SUB                                              
           ELSE                                                                 
               PERFORM  VARYING PV-SUB FROM 71 BY -1                            
                          UNTIL IR-COL-1-72 (PV-SUB:1) > SPACE                  
                             OR PV-SUB <= ZERO                                  
                   CONTINUE                                                     
               END-PERFORM                                                      
               MOVE ZERO TO PV-SINGLE-QUOTE-CNT                                 
               INSPECT IR-COL-1-72 TALLYING PV-SINGLE-QUOTE-CNT                 
                   FOR ALL ''''.                                                
                                                                                
           IF PV-SUB > ZERO                                                     
               IF  IR-COL-1-72 (72:1) = SPACE                                   
               AND IR-COL-1-72 (PV-SUB:1) = ','                                 
                   MOVE SPACE TO IR-COL-1-72 (PV-SUB:1)                         
               END-IF                                                           
               IF  IR-COL-1-72 (72:1) > SPACE                                   
               AND IR-COL-1-72 (PV-SUB:1) NOT = ','                             
               AND EVEN-NUMBER-OF-SINGLE-QUOTES                                 
                   MOVE ',' TO IR-COL-1-72 (PV-SUB + 1:1)                       
               END-IF.                                                          
                                                                                
                                                                                
      ****************************************************************          
      ** IF MAPSET NAME DOES NOT MATCH SOURCE NAME, CHANGE IT TO    **          
      ** MATCH.                                                     **          
      ****************************************************************          
       3300-CHECK-MAP-SET.                                                      
           IF LS-FILE-NAME > SPACES                                             
               MOVE ZERO TO PV-MAPSET-CNT                                       
               INSPECT IR-COL-1-72                                              
                 TALLYING PV-MAPSET-CNT FOR ALL 'DFHMSD'                        
                          PV-FINAL-CNT  FOR ALL 'TYPE=FINAL'.                   
                                                                                
           IF  PV-MAPSET-CNT > ZERO                                             
           AND PV-FINAL-CNT  = ZERO                                             
               UNSTRING IR-COL-1-72                                             
                   DELIMITED BY 'DFHMSD'                                        
                   INTO PV-BEGIN COUNT IN PV-BEGIN-CNT                          
                        PV-END   COUNT IN PV-END-CNT                            
               INSPECT LS-FILE-NAME                                             
                   REPLACING FIRST LOW-VALUE BY SPACE                           
               UNSTRING LS-FILE-NAME                                            
                   DELIMITED BY LOW-VALUES                                      
                   INTO PV-NEW COUNT IN PV-NEW-CNT                              
               IF PV-NEW NOT = PV-BEGIN                                         
                   MOVE IR-COL-1-72 (72:1) TO PV-CONT-CHAR                      
      * ADD A COMMENT ABOUT CHANGE.                                             
                   MOVE FUNCTION CURRENT-DATE (1:4) TO PV-CURRENT-CCYY          
                   MOVE FUNCTION CURRENT-DATE (5:2) TO PV-CURRENT-MM            
                   MOVE FUNCTION CURRENT-DATE (7:2) TO PV-CURRENT-DD            
                   MOVE SPACES           TO IR-COL-1-72                         
                   MOVE ZERO TO PV-BEGIN-SPACES                                 
                   INSPECT PV-BEGIN (1:PV-BEGIN-CNT)                            
                     TALLYING PV-BEGIN-SPACES FOR ALL SPACES                    
                   STRING PV-MAPSET-RENAME-1                                    
                          PV-CURRENT-DATE                                       
                          PV-MAPSET-RENAME-2                                    
                          PV-MAPSET-RENAME-3                                    
                     DELIMITED BY SIZE INTO IR-COL-1-72                         
                   MOVE IR-COL-1-72      TO OR-OUTPUT-RECORD                    
                   PERFORM 4100-WRITE-OUTPUT-RECORD                             
                                                                                
                   MOVE SPACES           TO IR-COL-1-72                         
                   MOVE ZERO TO PV-BEGIN-SPACES                                 
                   INSPECT PV-BEGIN (1:PV-BEGIN-CNT)                            
                     TALLYING PV-BEGIN-SPACES FOR ALL SPACES                    
                   INSPECT PV-NEW (1:PV-NEW-CNT)                                
                     TALLYING PV-NEW-SPACES FOR ALL SPACES                      
                   STRING PV-MAPSET-RENAME-4                                    
                          PV-BEGIN (1:PV-BEGIN-CNT - PV-BEGIN-SPACES)           
                          PV-MAPSET-RENAME-5                                    
                          PV-NEW (1:PV-NEW-CNT - PV-NEW-SPACES)                 
                          PV-MAPSET-RENAME-6                                    
                     DELIMITED BY SIZE INTO IR-COL-1-72                         
                   MOVE IR-COL-1-72      TO OR-OUTPUT-RECORD                    
                   PERFORM 4100-WRITE-OUTPUT-RECORD                             
      * MAKE THE CHANGE.                                                        
                   IF PV-NEW-SPACES = ZERO                                      
                       ADD 1 TO PV-NEW-CNT                                      
                   END-IF                                                       
                   MOVE SPACES           TO IR-COL-1-72                         
                   IF PV-NEW-CNT < PV-BEGIN-CNT                                 
                       MOVE PV-BEGIN-CNT TO PV-NEW-CNT                          
                   END-IF                                                       
                   STRING PV-NEW (1:PV-NEW-CNT)                                 
                          'DFHMSD'                                              
                          PV-END (1:PV-END-CNT - 1)                             
                     DELIMITED BY SIZE INTO IR-COL-1-72                         
                   MOVE PV-CONT-CHAR     TO IR-COL-1-72 (72:1).                 
                                                                                
                                                                                
      ****************************************************************          
      ** READ A RECORD FROM THE INPUT FILE.                         **          
      ****************************************************************          
       4000-READ-INPUT-RECORD.                                                  
           READ INPUT-SOURCE-FILE INTO IR-INPUT-RECORD                          
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
      ****************************************************************          
      ** WRITE THE RECORD TO THE OUTPUT FILE.                       **          
      ****************************************************************          
       4100-WRITE-OUTPUT-RECORD.                                                
           WRITE OUTPUT-SOURCE-RECORD FROM OR-OUTPUT-RECORD.                    
                                                                                
      ****************************************************************          
      ** CLOSE FILES                                                **          
      ****************************************************************          
       5000-TERMINATION.                                                        
           CLOSE INPUT-SOURCE-FILE                                              
                 OUTPUT-SOURCE-FILE.                                            
