       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT311.                                                 
       AUTHOR.        MARK A FRAMNESS.                                          
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  JULY 2011.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT311 - PHYSICAL INVENTORY SHORTAGE                                  
      * CREATE DATA FOR LPT_PHY_INV_SHTG_WRK                                    
      *                                                                         
      * THIS PROGRAM WILL CREATE A FILE TO BE LOADED TO A WORK TABLE            
      * LPT_PHY_INV_SHTG_WRK VIA INFORMATICA.                                   
      *                                                                         
      * CHANGE LOG:  MARIBETH ZAMORA    WR33304 (HILO)                          
      *              NEW                                                        
      *                                                                         
      * CHANGE LOG:  MARIBETH ZAMORA    RFC 12685 / PKE000000004083             
      * 06/08/2010   REMOVE UNNECESSARY DISPLAYS IN THE PROGRAM                 
      *                                                                         
3033D * APRIL 2011:  MARK FRAMNESS      3033D - MDSE EDW                        
3033D *              PORT TO IN TO CREATE THE SHORTAGE EXTRACT FOR              
3033D *              TERADATA EDW STAGING CLONE FROM MWKUT697                   
3033D *              CONVERT MWRD697 TO INRD182 OTHERWISE NO CHANGES            
3033D *              BEYOND THAT AND COMMENTS & DOCUMENTATION.                  
3033D *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  080 CHARACTERS.                                     
       01  INPUT-RECORD                PIC X(080).                              
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  146 CHARACTERS.                                     
       01  OUTPUT-RECORD               PIC X(146).                              
                                                                                
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUT311'.          
       01  ABEND-CODE                  PIC S9(04)    VALUE ZEROES               
                                                     COMP SYNC.                 
       01  PV-COUNTS.                                                           
           05 PV-IN-COUNT              PIC  9(09)    VALUE ZEROES.              
           05 PV-OUT-COUNT             PIC  9(09)    VALUE ZEROES.              
                                                                                
       01  PV-INPUT-SW                 PIC  X(01)    VALUE 'N'.                 
           88  NOT-END-INPUT                         VALUE 'N'.                 
           88  END-INPUT                             VALUE 'Y'.                 
                                                                                
       01  PV-TEMP.                                                             
           05  PV-PHY-SITE-HIST-ID     PIC S9(05)    VALUE 0.                   
           05  PV-ENT-ID               PIC S9(09)    VALUE 0.                   
           05  PV-INV-CYC-ID           PIC S9(09)    VALUE 0.                   
           05  PV-BK-INV-RTL-AMT       PIC S9(13)V99 VALUE 0.                   
           05  PV-BSE-SLS-RTL-AMT      PIC S9(13)V99 VALUE 0.                   
           05  PV-INV-SHNKG-AMT        PIC S9(13)V99 VALUE 0.                   
           05  PV-PHY-INV-RTL-AMT      PIC S9(13)V99 VALUE 0.                   
           05  PV-EST-INV-ADJ-AMT      PIC S9(13)V99 VALUE 0.                   
           05  PV-SHTG-RTL-AMT         PIC S9(13)V99 VALUE 0.                   
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR INPUT FILE (PI)                                          
      ****************************************************************          
           COPY INRD028.                                                        
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR OUTPUT FILE                                              
      ****************************************************************          
3033D      COPY INRD182.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-SECTION.                                                   
      ******************************************************************        
                                                                                
           PERFORM 1000-BEG-OF-JOB.                                             
                                                                                
           PERFORM 3000-MAIN-PROCESS                                            
             UNTIL END-INPUT.                                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       1000-BEG-OF-JOB.                                                         
      ******************************************************************        
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '** PROGRAM :       ' PC-PROGRAM-NAME                        
           DISPLAY '************************************************'.          
                                                                                
           OPEN INPUT  INPUT-FILE                                               
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESS.                                                       
      *****************************************************************         
                                                                                
           PERFORM 6000-FORMAT-OUTPUT-RECORD.                                   
           PERFORM 6100-WRITE-OUTPUT-RECORD.                                    
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       4000-READ-INPUT-FILE.                                                    
      *****************************************************************         
                                                                                
           READ INPUT-FILE                                                      
               INTO  IN028-SHORTAGE-EXTR-REC                                    
               AT END                                                           
                   SET END-INPUT     TO TRUE                                    
           END-READ.                                                            
                                                                                
           IF NOT-END-INPUT                                                     
              ADD +1 TO PV-IN-COUNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
       6000-FORMAT-OUTPUT-RECORD.                                               
      *****************************************************************         
                                                                                
           INITIALIZE IN182-SHORTAGE-RECORD.                                    
                                                                                
           MOVE IN028-DEPT-NBR         TO IN182-DEPT-NBR                        
           MOVE IN028-MAJ-CL-NBR       TO IN182-MAJ-CL-NBR                      
           MOVE IN028-SUB-CL-NBR       TO IN182-SUB-CL-NBR                      
           MOVE IN028-PSTG-MTH-BEG-DTE TO IN182-TM-DIM-KY-DTE                   
           MOVE IN028-STR-NBR          TO IN182-PHY-SITE-HIST-ID                
                                                                                
           MOVE IN028-INV-SRVC-ENT-ID  TO PV-ENT-ID                             
           MOVE PV-ENT-ID              TO IN182-ENT-ID                          
                                                                                
           MOVE IN028-INV-ID           TO PV-INV-CYC-ID                         
           MOVE PV-INV-CYC-ID          TO IN182-INV-CYC-ID                      
                                                                                
           MOVE IN028-SCAN-IND         TO IN182-ACTL-PHY-INV-IND                
                                                                                
           MOVE IN028-BOOK-INV-RTL-AMT TO PV-BK-INV-RTL-AMT                     
           MOVE PV-BK-INV-RTL-AMT      TO IN182-BK-INV-RTL-AMT                  
                                                                                
           MOVE IN028-SLS-BSE-RTL-AMT  TO PV-BSE-SLS-RTL-AMT                    
           MOVE PV-BSE-SLS-RTL-AMT     TO IN182-BSE-SLS-RTL-AMT                 
                                                                                
           MOVE IN028-SHNK-RTL-AMT     TO PV-INV-SHNKG-AMT                      
           MOVE PV-INV-SHNKG-AMT       TO IN182-INV-SHNKG-AMT                   
                                                                                
           MOVE IN028-PHY-INV-RTL-AMT  TO PV-PHY-INV-RTL-AMT                    
           MOVE PV-PHY-INV-RTL-AMT     TO IN182-PHY-INV-RTL-AMT                 
                                                                                
           MOVE IN028-ESTIMATED-INV-ADJ-AMT TO PV-EST-INV-ADJ-AMT               
           MOVE PV-EST-INV-ADJ-AMT     TO IN182-EST-INV-ADJ-AMT                 
                                                                                
           MOVE IN028-SHORTAGE-RTL-AMT TO PV-SHTG-RTL-AMT                       
           MOVE PV-SHTG-RTL-AMT        TO IN182-SHTG-RTL-AMT.                   
                                                                                
                                                                                
      *****************************************************************         
       6100-WRITE-OUTPUT-RECORD.                                                
      *****************************************************************         
                                                                                
           WRITE OUTPUT-RECORD         FROM IN182-SHORTAGE-RECORD.              
           ADD +1 TO PV-OUT-COUNT.                                              
                                                                                
                                                                                
      *****************************************************************         
       9000-END-OF-JOB.                                                         
      *****************************************************************         
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '========== INKUT311 PROGRAM SUMMARY =========='.            
           DISPLAY '  INPUT  RECS READ        : ' PV-IN-COUNT                   
           DISPLAY '  OUTPUT RECS WRITTEN     : ' PV-OUT-COUNT                  
           DISPLAY '************************************************'.          
                                                                                
      ******************************************************************        
      ******************************************************************        
