       IDENTIFICATION DIVISION.                                         00010003
       PROGRAM-ID.    XSKUP086.                                         00020003
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030003
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040003
       DATE-WRITTEN.  09/10/2004.                                       00050003
       DATE-COMPILED.                                                   00060003
      ******************************************************************00070003
      **       MERCHANDISE HIERARCHY MESSAGE FOR TABLE XST_MAJ_CL       00080003
      ******************************************************************00090003
      **THIS IS A CALLED MODULE FOR SUBCLASS MERCH HIER TABLES.  THIS   00100003
      **     PROGRAM WILL PERFORM UPDATES/INSERTS/DELETES FOR THIS      00110003
      **     TABLE.  IT DOES THE FOLLOWING WITH THESE RETURN CODES:     00120003
      **     0000 - END PROGRAM                                         00130003
      **    -0530 - END PROGRAM                                         00140003
      **    -0803 - COMPARE TIMESTAMPS AND UPDATE IF MESSAGE IS NEWER   00150003
      **    -0911 - RETRY INSERT UP TO 5 TIMES AND THEN END PROGRAM     00160003
      ******************************************************************00170003
      **09/10/2004 CHANGED BY:JULIE TEWES          PROGRAM WRITE        00180003
      ******************************************************************00190003
       ENVIRONMENT DIVISION.                                            00200003
       CONFIGURATION SECTION.                                           00210003
       SOURCE-COMPUTER.  IBM-3090.                                      00220003
       OBJECT-COMPUTER.  IBM-3090.                                      00230003
       DATA DIVISION.                                                   00240003
                                                                        00250003
       WORKING-STORAGE SECTION.                                         00260003
      ******************************************************************00270003
      **PROGRAM SWITCHES                                                00280003
      ******************************************************************00290003
       01  PS-PROGRAM-SWITCHES.                                         00300003
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00310003
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00320003
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00330003
      ******************************************************************00340003
      **PROGRAM COUNTERS                                                00350003
      ******************************************************************00360003
       01  PC-PROGRAM-COUNTERS.                                         00370003
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00380003
           05  PC-RETRY-COUNTER-2          PIC 9(01)  VALUE ZEROS.      00390003
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00400003
      ******************************************************************00410003
      **PROGRAM VARIABLES                                               00420003
      ******************************************************************00430003
       01  PV-PROGRAM-VARIABLES.                                        00440003
           05  PV-PARAGRAPH-NAME               PIC X(20)  VALUE SPACES. 00450003
           05  PV-TIME-EVALUATE-INSERT.                                 00460003
               10  PV-TIME-HOUR-IN             PIC  9(02) VALUE ZEROS.  00470003
               10  PV-TIME-MINUTE-IN           PIC  9(02) VALUE ZEROS.  00480003
               10  PV-TIME-SECOND-IN           PIC  9(02) VALUE ZEROS.  00490003
               10  PV-TIME-MICROSEC-IN         PIC  9(06) VALUE ZEROS.  00500003
           05  PV-TIME-EVALUATE-UPDATE.                                 00510003
               10  PV-TIME-HOUR-UP             PIC  9(02) VALUE ZEROS.  00520003
               10  PV-TIME-MINUTE-UP           PIC  9(02) VALUE ZEROS.  00530003
               10  PV-TIME-SECOND-UP           PIC  9(02) VALUE ZEROS.  00540003
               10  PV-TIME-MICROSEC-UP         PIC  9(06) VALUE ZEROS.  00550003
      ******************************************************************00560003
      **TIMESTAMP VARIABLES                                             00570003
      ******************************************************************00580003
       01  SD-TIMESTAMP-COMPARE                PIC  X(26)  VALUE SPACES.00590003
       01  SD-TIMESTAMP.                                                00600003
           05  SD-CURR-DTE                     PIC  X(10).              00610003
           05  FILLER                          PIC  X(01) VALUE '-'.    00620003
           05  SD-HOUR                         PIC  9(02).              00630003
           05  FILLER                          PIC  X(01) VALUE '.'.    00640003
           05  SD-MINUTE                       PIC  9(02).              00650003
           05  FILLER                          PIC  X(01) VALUE '.'.    00660003
           05  SD-SECOND                       PIC  9(02).              00670003
           05  FILLER                          PIC  X(01) VALUE '.'.    00680003
           05  SD-MICROSEC                     PIC  9(06).              00690003
      ******************************************************************00700003
      **COPYBOOKS                                                       00710003
      ******************************************************************00720003
      ***DB2 MESSAGE AREA                                               00730003
           COPY SQLCA2.                                                 00740003
      ***MESSAGE COPYBOOK                                               00750003
           COPY HMRD001.                                                00760003
      ***DATE ROUTINE COPYBOOK                                          00770003
           COPY DPWS500.                                                00780003
      ******************************************************************00790003
      **TABLE INCLUSIONS                                                00800003
      ******************************************************************00810003
      ***DB2 COMMUNICATIONS AREA                                        00820003
           EXEC SQL                                                     00830003
                INCLUDE SQLCA                                           00840003
           END-EXEC.                                                    00850003
      ***XST_MAJ_CL TABLE                                               00860003
           EXEC SQL                                                     00870003
                INCLUDE XST00071                                        00880005
           END-EXEC.                                                    00890003
                                                                        00900003
       LINKAGE SECTION.                                                 00910003
      ******************************************************************00920003
      **LINKAGE VARIABLES                                               00930003
      ******************************************************************00940003
       01  LV-HMRD001-RECORD               PIC X(500).                  00950003
       01  LV-RETURN-CODE                  PIC S9(04).                  00960003
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00970003
                                                                        00980003
       PROCEDURE DIVISION USING LV-HMRD001-RECORD,                      00990003
                                LV-RETURN-CODE,                         01000003
                                LV-CURRENT-TIMESTAMP.                   01010003
      ******************************************************************01020003
       0000-MAINLINE.                                                   01030003
                                                                        01040003
            PERFORM 1000-INITIALIZATION.                                01050003
            PERFORM 2000-PROCESS-MESSAGE                                01060003
                    UNTIL PS-PROCESS-COMPLETE.                          01070003
            PERFORM 9999-WRAPUP.                                        01080003
                                                                        01090003
       EJECT.                                                           01100003
      ***************************************************************** 01110003
      **INITIALIZATION AREA - ALL MOVES TO UPDATE/INSERT                01120003
      ***************************************************************** 01130003
       1000-INITIALIZATION.                                             01140003
                                                                        01150003
           MOVE '1000-INITIALIZATION'      TO PV-PARAGRAPH-NAME.        01160003
                                                                        01170003
           SET PS-PROCESS-CONTINUE         TO TRUE.                     01180003
           INITIALIZE PC-RETRY-COUNTER.                                 01190003
           INITIALIZE SD-TIMESTAMP.                                     01200003
                                                                        01210003
           MOVE LV-HMRD001-RECORD          TO HMRD001-RECORD.           01220003
           MOVE HM001-CLASS-DEPT           TO XST00071-DEPT-NBR.        01230005
           MOVE HM001-CLASS-CLASS          TO XST00071-MAJ-CL-NBR.      01240005
           MOVE HM001-CLASS-CLASS-NAME     TO XST00071-MAJ-CL-NM.       01250005
                                                                        01260003
           MOVE LV-CURRENT-TIMESTAMP       TO XST00071-CRE-TMST.        01270005
           MOVE LV-CURRENT-TIMESTAMP       TO XST00071-LAST-UPD-TMST.   01280005
                                                                        01290003
      ***MERCH HIER MESSAGES ONLY CONTAIN DATE SO DEFAULT TIME TO 12AM  01300003
010103     MOVE HM001-CRE-TMST           TO SD-CURR-DTE.                01310003
010103     MOVE SD-TIMESTAMP             TO XST00071-SOR-LAST-MSG-TMST. 01320005
010103     INITIALIZE SD-TIMESTAMP.                                     01330003
                                                                        01340003
       EJECT.                                                           01350003
      ***************************************************************** 01360003
      **MAIN PROGRAM LOGIC - READS ACTION TYPE ON HMRD001 RECORD        01370003
      ***************************************************************** 01380003
       2000-PROCESS-MESSAGE.                                            01390003
                                                                        01400003
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01410003
                                                                        01420003
           EVALUATE TRUE                                                01430003
              WHEN HM001-ACTN-TYP-INSERT                                01440003
                   PERFORM 2100-INSERT-DATA                             01450003
              WHEN HM001-ACTN-TYP-UPDATE                                01460003
                   PERFORM 2200-UPDATE-DATA                             01470003
              WHEN HM001-ACTN-TYP-DELETE                                01480003
                   PERFORM 2300-DELETE-DATA                             01490003
           END-EVALUATE.                                                01500003
                                                                        01510003
       EJECT.                                                           01520003
      ***************************************************************** 01530003
      **INSERT ACTION TYPE                                              01540003
      ***************************************************************** 01550003
       2100-INSERT-DATA.                                                01560003
                                                                        01570003
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01580003
                                                                        01590003
           EXEC SQL                                                     01600003
                INSERT INTO XST_MAJ_CL (DEPT_NBR                        01610005
                                      , MAJ_CL_NBR                      01620003
                                      , MAJ_CL_NM                       01630004
                                      , CRE_TMST                        01640004
                                      , LAST_UPD_TMST                   01650004
                                      , SOR_LAST_MSG_TMST)              01660004
                VALUES (:XST00071-DEPT-NBR                              01670005
                      , :XST00071-MAJ-CL-NBR                            01680005
                      , :XST00071-MAJ-CL-NM                             01690005
                      , :XST00071-CRE-TMST                              01700005
                      , :XST00071-LAST-UPD-TMST                         01710005
                      , :XST00071-SOR-LAST-MSG-TMST)                    01720005
           END-EXEC.                                                    01730003
                                                                        01740003
           EVALUATE TRUE                                                01750003
      ***SUCCESSFUL PROCESSING                                          01760003
              WHEN SQLCODE = ZEROS                                      01770003
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01780003
      ***REFERENTIAL INTEGRITY ISSUES                                   01790003
              WHEN SQLCODE = -530                                       01800003
              WHEN SQLCODE = -532                                       01810003
                   SET PS-PROCESS-COMPLETE   TO TRUE                    01820003
      ***ROW EXISTS ON TABLE                                            01830003
              WHEN SQLCODE = -803                                       01840003
                   PERFORM 2150-SELECT-DATA                             01850003
      ***ROW/TABLE LOCK                                                 01860003
              WHEN SQLCODE = -913                                       01870003
              WHEN SQLCODE = -911                                       01880003
                   IF  PC-RETRY-COUNTER >= PC-RETRY-MAX                 01890003
                       SET PS-PROCESS-COMPLETE TO TRUE                  01900003
                   ELSE                                                 01910003
                       ADD +1                  TO PC-RETRY-COUNTER      01920003
                   END-IF                                               01930003
              WHEN OTHER                                                01940003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01950003
           END-EVALUATE.                                                01960003
                                                                        01970003
       EJECT.                                                           01980003
      ******************************************************************01990003
      *SELECT DATE FROM EXISTING RECORD FOR COMPARISON TO MESSAGE DATE  02000003
      ******************************************************************02010003
       2150-SELECT-DATA.                                                02020003
                                                                        02030003
           MOVE '2150-SELECT-DATA' TO PV-PARAGRAPH-NAME                 02040003
                                                                        02050003
           EXEC SQL                                                     02060003
                SELECT LAST_UPD_TMST                                    02070003
                INTO  :SD-TIMESTAMP-COMPARE                             02080003
                FROM   XST_MAJ_CL                                       02090004
                WHERE DEPT_NBR   = :XST00071-DEPT-NBR                   02100005
                  AND MAJ_CL_NBR = :XST00071-MAJ-CL-NBR                 02110005
           END-EXEC.                                                    02120003
                                                                        02130003
           EVALUATE TRUE                                                02140003
      ***SUCCESSFUL PROCESSING                                          02150003
              WHEN SQLCODE = ZEROS                                      02160003
                   PERFORM 2153-DATE-COMPARE                            02170003
              WHEN OTHER                                                02180003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02190003
           END-EVALUATE.                                                02200003
                                                                        02210003
       EJECT.                                                           02220003
      ******************************************************************02230003
      **COMPARE DATE OF EXISTING ROW TO MESSAGE AND UPDATE IF MESSAGE   02240003
      **        HAS A NEWER TIMESTAMP.                                  02250003
      ******************************************************************02260003
       2153-DATE-COMPARE.                                               02270003
                                                                        02280003
           MOVE '2153-DATE-COMPARE' TO PV-PARAGRAPH-NAME                02290003
                                                                        02300003
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02310003
                                                                        02320003
           SET DPG53-LK-CMPR-DTE-ISO-FMT TO TRUE.                       02330003
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      02340003
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      02350003
           MOVE HM001-CRE-TMST           TO DPG52-LK-DATE-INPUT.        02360003
           MOVE SD-TIMESTAMP-COMPARE     TO DPG53-LK-CMPR-DATE-INPUT.   02370003
                                                                        02380003
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                02390003
                                                   DPG52                02400003
                                                   DPG53                02410003
                                                   DPG54                02420003
                                                   DPG55                02430003
                                                   DPG56.               02440003
                                                                        02450003
           EVALUATE TRUE                                                02460003
              WHEN DPG55-DAYS-DIFFERENCE = 0                            02470003
                   PERFORM 2200-UPDATE-DATA                             02480003
                           UNTIL PS-PROCESS-COMPLETE                    02490003
              WHEN DPG55-DAYS-DIFFERENCE < ZERO                         02500003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02510003
              WHEN DPG55-DAYS-DIFFERENCE > ZERO                         02520003
                   PERFORM 2200-UPDATE-DATA                             02530003
                           UNTIL PS-PROCESS-COMPLETE                    02540003
           END-EVALUATE.                                                02550003
                                                                        02560003
       EJECT.                                                           02570003
      ******************************************************************02580003
      **UPDATE ACTION TYPE                                              02590003
      ******************************************************************02600003
       2200-UPDATE-DATA.                                                02610003
                                                                        02620003
           MOVE '2200-UPDATE-DATA' TO PV-PARAGRAPH-NAME                 02630003
                                                                        02640003
           EXEC SQL                                                     02650003
              UPDATE XST_MAJ_CL                                         02660004
                  SET   MAJ_CL_NM     = :XST00071-MAJ-CL-NM             02670005
                      , LAST_UPD_TMST = :XST00071-LAST-UPD-TMST         02680005
                      , SOR_LAST_MSG_TMST = :XST00071-SOR-LAST-MSG-TMST 02690005
                  WHERE DEPT_NBR      = :XST00071-DEPT-NBR              02700005
                    AND MAJ_CL_NBR    = :XST00071-MAJ-CL-NBR            02710005
           END-EXEC.                                                    02720003
                                                                        02730003
           EVALUATE TRUE                                                02740003
      ***SUCCESSFUL PROCESSING                                          02750003
              WHEN SQLCODE = ZEROS                                      02760003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02770003
      ***ROW/TABLE LOCK                                                 02780003
              WHEN SQLCODE = -911                                       02790003
              WHEN SQLCODE = -913                                       02800003
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               02810003
                       SET PS-PROCESS-COMPLETE TO TRUE                  02820003
                   ELSE                                                 02830003
                       ADD +1                  TO PC-RETRY-COUNTER-2    02840003
                   END-IF                                               02850003
              WHEN OTHER                                                02860003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  02870003
           END-EVALUATE.                                                02880003
                                                                        02890003
       EJECT.                                                           02900003
      ******************************************************************02910003
      **DELETE TYPE ACTION                                              02920003
      ******************************************************************02930003
       2300-DELETE-DATA.                                                02940003
                                                                        02950003
           MOVE '2300-DELETE-DATA' TO PV-PARAGRAPH-NAME                 02960003
                                                                        02970003
           EXEC SQL                                                     02980003
                DELETE FROM XST_MAJ_CL                                  02990004
                WHERE DEPT_NBR   = :XST00071-DEPT-NBR                   03000005
                  AND MAJ_CL_NBR = :XST00071-MAJ-CL-NBR                 03010005
           END-EXEC.                                                    03020003
                                                                        03030003
           EVALUATE TRUE                                                03040003
      ***SUCCESSFUL PROCESSING                                          03050003
              WHEN SQLCODE = ZEROS                                      03060003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03070003
      ***NO ROW TO DELETE                                               03080003
              WHEN SQLCODE = +100                                       03090003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03100003
              WHEN SQLCODE = -911                                       03110003
              WHEN SQLCODE = -913                                       03120003
                   IF  PC-RETRY-COUNTER-2 >= PC-RETRY-MAX               03130003
                       SET PS-PROCESS-COMPLETE TO TRUE                  03140003
                   ELSE                                                 03150003
                       ADD +1                  TO PC-RETRY-COUNTER-2    03160003
                   END-IF                                               03170003
              WHEN OTHER                                                03180003
                   SET PS-PROCESS-COMPLETE     TO TRUE                  03190003
           END-EVALUATE.                                                03200003
                                                                        03210003
       EJECT.                                                           03220003
      ******************************************************************03230003
      **PASS SQLCODE TO CALLING AND EXIT PROGRAM                        03240003
      ******************************************************************03250003
       9999-WRAPUP.                                                     03260003
                                                                        03270003
           MOVE '9999-WRAPUP' TO PV-PARAGRAPH-NAME                      03280003
                                                                        03290003
           MOVE SQLCODE       TO LV-RETURN-CODE.                        03300003
           DISPLAY 'XSKUP086 - DEPT NBR: ' XST00071-DEPT-NBR            03310005
                           ' MAJ CLASS: ' XST00071-MAJ-CL-NBR           03320005
                            ' WITH RETURN-CODE ' LV-RETURN-CODE.        03330003
           EXIT PROGRAM.                                                03340003
                                                                        03350003
       EJECT.                                                           03360003
