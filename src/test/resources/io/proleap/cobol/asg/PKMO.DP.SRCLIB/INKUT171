       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT171.                                       
       AUTHOR.                  J SEIDLER.                                      
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/12/93.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  DETERMINE WHICH STORES ARE READY TO HAVE THEIR SALE                    
      *  ADJUSTMENT CALCULATED.                                                 
      *                                                                         
      *  DC LOCATIONS AND "FAKE OUT" STORES ARE EXCLUDED FROM THIS              
      *  PROCESS.  DC LOCATIONS DO NOT HAVE SALES. "FAKE OUT" STORES   *        
      *  DO NOT HAVE PHYSICAL COUNT DATA.                                       
      *                                                                         
      *  ..ALL INVALIDS MUST BE CORRECTED                              *        
      *  ..CURRENT DATE MUST BE EQUAL TO OR LESS THAN THE STOCKROOM    *        
      *    WITHDRAWAL ENTRY DATE                                                
      ******************************************************************        
      ******************************************************************        
      *  CHANGED: 00/01/28    J. SEIDLER       WR# XXXX                *        
      *           SINCE STORES "USUALLY" ENTER STOCKROOM SHEETS        *        
      *           WITHIN 2 DAYS OF COMPLETING THE INVENTORY, THE       *        
      *           PROGRAM WILL BE CHANGED TO CONSIDER A STORE A        *        
      *           CANDIDATE 2 DAYS AFTER THE STORES INVENTORY DATE.    *        
      *           (PGM WILL RUN BE RUNNING BEFORE MIDNIGHT)                     
      *                                                                *        
      *           AT THIS TIME, STOCKROOM DAYS WAS SET TO 3 DAYS.      *        
      ******************************************************************        
      *  CHANGED: 06/22/00    J. SEIDLER       WR# XXXX                *        
      *           CHANGED THE NUMBER OF CANDIDATE OUTPUT FILES         *        
      *           FROM 3 TO 5.                                         *        
      *                                                                *        
      *           ADDED LOGIC TO CREATE SORT INCLUDE CONTROL CARDS     *        
      *           FOR THE CANDIDATE STORES. THE SORT CARDS WILL BE              
      *           USED TO EXTRACT DATA FOR THE TSUMINV PLAT UNLOAD              
      *           FILE.                                                         
      ******************************************************************        
21015 *  CHANGED: 02/20/2001  ROD JANSSEN      WR# 21015               *        
21015 *           CHANGE THE DERIVED BOOK DATE # OF DAYS AND DATE TO   *        
21015 *           REFLECT THE ACTL_INV_DTE + 2 DAY.......              *        
21015 ******************************************************************        
W05231*  CHANGED: 05/23/2001  ROD JANSSEN      WR# 99999               *        
W05231*           CHANGE THE DERIVED BOOK DATE # OF DAYS AND DATE TO   *        
W05231*           REFLECT THE ACTL_INV_DTE + 1 DAY.......              *        
      ******************************************************************        
W21732* 11/18/2002        CHANGED BY SUKUMAR,IGSI    WR# W21732        *        
W21732* CHANGED SORT CARD DATA THAT WILL BE USED IN LATER JOBS.        *        
W21732******************************************************************        
W24521*  CHANGED: 03/21/2003  RONNA BUTZ       WR# 24521               *        
W24521* CHANGED THE TINVPAR_CURSOR TO ONLY SELECT RECORDS WHERE        *        
W24521* TIMESTAMP EDITS HAVE BEEN PROCESSED AND THERE ARE NO ERRORS    *        
W24521******************************************************************        
W26600*  CHANGED: 06/09/2004  D. THEEL         WR# 26600               *        
W26600*  STORE EXPANSION CHANGES.                                      *        
W26600******************************************************************        
28545 *                                                                         
28545 * 08/10/2006 B. GRAHAM - STRATAGEM      WR# W28545                        
28545 *            DEPARTMENT LEVEL INVENTORY PROJECT                           
28545 *            JOIN TINVPAR TO TINCNTL                                      
                                                                                
W33304*                                                                         
W33304* 08/26/2008 THERESE RAMSEYER           WR# W33304                        
W33304*            HILO - HOLD INVENTORY LEDGER OPEN                            
W33304*             --> IN TINVPAR_CURSOR: CHANGE "IND" OF                      
W33304*                 SCHD_PHY_CNT_IND TO "CDE"                               
W33304*             --> TO TINVPAR_CURSOR:                                      
W33304*                 ADD SCHD_PHY_CNT_CDE TO SELECT                          
W33304*                 ADD SCHD_PHY_CNT_CDE = 'PI' TO WHERE CLAUSE             
W33304*                 ADD TMST_EDT_STAT_CDE = 'TP' TO WHERE CLAUSE            
W33304*                 ADD LOC_INV_STAT_CDE = 'IN' TO WHERE CLAUSE             
W33304*             --> ADD :INVPAR-SCHD-PHY-CNT-CDE TO FETCH IN Z100-          
W33304******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PREV-SELECTED-STORES                                          
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORES-TO-CALC-FILE1                                          
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STORES-TO-CALC-FILE2                                          
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT STORES-TO-CALC-FILE3                                          
               ASSIGN TO OUT03.                                                 
                                                                                
           SELECT STORES-TO-CALC-FILE4                                          
               ASSIGN TO OUT04.                                                 
                                                                                
           SELECT STORES-TO-CALC-FILE5                                          
               ASSIGN TO OUT05.                                                 
                                                                                
           SELECT SELECTED-STORES                                               
               ASSIGN TO OUT06.                                                 
                                                                                
           SELECT SORT-CARDS                                                    
               ASSIGN TO OUT07.                                                 
                                                                                
W26600     SELECT DB-UNLOAD-CARDS                                               
W26600         ASSIGN TO OUT08.                                                 
                                                                                
W33304     SELECT ACTV-STORES                                                   
W33304         ASSIGN TO OUT09.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PREV-SELECTED-STORES                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PREV-SELECTED-STORES-REC   PIC X(80).                                
                                                                                
       FD  STORES-TO-CALC-FILE1                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUTPUT-REC1.                                          
       01  OUTPUT-REC1           PIC X(80).                                     
                                                                                
       FD  STORES-TO-CALC-FILE2                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUTPUT-REC2.                                          
       01  OUTPUT-REC2           PIC X(80).                                     
                                                                                
       FD  STORES-TO-CALC-FILE3                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUTPUT-REC3.                                          
       01  OUTPUT-REC3           PIC X(80).                                     
                                                                                
       FD  STORES-TO-CALC-FILE4                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUTPUT-REC4.                                          
       01  OUTPUT-REC4           PIC X(80).                                     
                                                                                
       FD  STORES-TO-CALC-FILE5                                                 
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS OUTPUT-REC5.                                          
       01  OUTPUT-REC5           PIC X(80).                                     
                                                                                
       FD  SELECTED-STORES                                                      
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           RECORDING MODE IS F                                                  
           DATA RECORD IS SELECTED-STORES-REC.                                  
       01  SELECTED-STORES-REC   PIC X(80).                                     
                                                                                
       FD  SORT-CARDS                                                           
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80  CHARACTERS                                       
           RECORDING MODE IS F                                                  
           DATA RECORD IS SORT-CARDS-REC.                                       
       01  SORT-CARDS-REC        PIC X(80).                                     
                                                                                
W26600 FD  DB-UNLOAD-CARDS                                                      
W26600     LABEL RECORDS ARE OMITTED                                            
W26600     RECORD CONTAINS 80  CHARACTERS                                       
W26600     RECORDING MODE IS F                                                  
W26600     DATA RECORD IS DB-UNLOAD-CARDS-REC.                                  
W26600 01  DB-UNLOAD-CARDS-REC   PIC X(80).                                     
                                                                                
W33304 FD  ACTV-STORES                                                          
W33304     LABEL RECORDS ARE OMITTED                                            
W33304     RECORD CONTAINS 80  CHARACTERS                                       
W33304     RECORDING MODE IS F                                                  
W33304     DATA RECORD IS DB-UNLOAD-CARDS-REC.                                  
W33304 01  ACTV-STORES-REC        PIC X(80).                                    
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
W26600 01  PT-TABLE-AREA.                                                       
W26600     05  PT-PROCESSED-LOC-TBL OCCURS 99999.                               
W26600         10  PT-PROCESSED-LOC        PIC S9(04)      VALUE ZEROES         
W26600                         COMP.                                            
                                                                                
W26600     05  PT-QUALIFIED-INDEX          PIC S9(04)      VALUE ZEROES         
W26600                         COMP.                                            
W26600     05  PT-QUALIFIED-COUNT          PIC S9(04)      VALUE ZEROES         
W26600                         COMP.                                            
W26600     05  PT-QUALIFIED-LOC-TBL OCCURS 99999.                               
W26600         10  PT-QUALIFIED-LOC        PIC S9(04)      VALUE ZEROES         
W26600                         COMP.                                            
W26600         10  PT-QUALIFIED-FILE-NBR   PIC 9(02)       VALUE ZEROES.        
                                                                                
      *****************************************************************         
      *  SELECTED STORES THAT HAD CALCULATED SALES ADJ (INCLUDE PREV)           
      *****************************************************************         
W26600*01  IN018-PREV-CALC-STORES.                                              
           COPY INRD018.                                                        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
           COPY DPWS133O.                                                       
                                                                                
       01  PS-SWITCHES.                                                         
           05  END-OF-TINVPAR-SW           PIC X       VALUE 'N'.               
               88  END-OF-TINVPAR                      VALUE 'Y'.               
W26600     05  PS-PROCESSED-STORE-SW       PIC X       VALUE 'N'.               
W26600         88  PS-END-OF-PROCESSED-STORES          VALUE 'Y'.               
JS0600     05  PS-DONE-SW                  PIC X       VALUE 'N'.               
JS0600         88  PS-DONE                             VALUE 'Y'.               
JS0600         88  PS-NOT-DONE                         VALUE 'N'.               
W33304     05  PS-ACTV-STORE-SW            PIC X       VALUE 'N'.               
W33304         88  PS-ACTV-STORE                       VALUE 'Y'.               
W33304         88  PS-NON-ACTV-STORE                   VALUE 'N'.               
W33304     05  PS-END-OF-TINVPAR-CSR-SW    PIC X       VALUE 'N'.               
W33304         88  PS-END-OF-TINVPAR-CSR               VALUE 'Y'.               
W33304         88  PS-NON-END-OF-TINVPAR-CSR           VALUE 'N'.               
                                                                                
W33304 01  PC-PROGRAM-CONSTANTS.                                        INKUT169
029700     05  PC-MAX-RETRIES             PIC S9(03) VALUE +5 COMP-3.           
W33304     05  PC-DEFAULT-NINES           PIC  X(10) VALUE '9999-09-09'.INKUT169
W33304     05  PC-INITIAL-STATUS            PIC X(02) VALUE 'IN'.               
W33304     05  PC-PHYSICAL-INVENTORY        PIC X(02) VALUE 'PI'.               
W33304     05  PC-TRANS-POSTED              PIC X(02) VALUE 'TP'.               
W33304     05  PC-ACTIVE                    PIC X(01) VALUE 'Y'.                
                                                                                
W33304 01  PA-PROGRAM-ACCUMULATORS.                                     INKUT169
W33304     05  PA-TOT-NON-ACTV-STR-RECS-BYP  PIC S9(07) COMP-3 VALUE +0.INKUT169
W33304     05  PA-TOT-INV-STR-RECS           PIC S9(07) COMP-3 VALUE +0.INKUT169
W33304     05  PA-TOT-RECS-READ              PIC S9(07) COMP-3 VALUE +0.INKUT169
W33304     05  PA-TOT-ACTV-STR-RECS-WRIT     PIC S9(07) COMP-3 VALUE +0.INKUT169
W33304                                                                  INKUT169
       01  PV-PROGRAM-VARIABLES.                                                
W33304     05  PV-RETRY-COUNTER           PIC S9(04) VALUE +0 COMP SYNC.        
           05  PV-PREV-ACTL-INV-DTE        PIC X(10)       VALUE SPACES.        
           05  PV-WHICH-FILE-NBR           PIC 9(02)       VALUE ZEROES.        
           05  PV-UNLOAD-CARDS-COUNT       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PROCESSING-DATE-DAYS     PIC S9(9)       VALUE ZEROES         
                               COMP.                                            
           05  PV-ACTL-PLUS-STKRM-DAYS     PIC S9(9)       VALUE ZEROES         
                               COMP.                                            
           05  PV-CHECK                    PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-REF.                                          
               10  PV-PROCESSING-CC        PIC X(02)       VALUE SPACES.        
               10  PV-PROCESSING-YY        PIC X(02)       VALUE SPACES.        
               10  PV-PROC-SL1             PIC X(01)       VALUE '-'.           
               10  PV-PROCESSING-MM        PIC X(02)       VALUE SPACES.        
               10  PV-PROC-SL2             PIC X(01)       VALUE '-'.           
               10  PV-PROCESSING-DD        PIC X(02)       VALUE SPACES.        
           05  PV-PROCESSING-DATE REDEFINES PV-PROCESSING-DATE-REF              
                                           PIC X(10).                           
           05  PV-TARGET-DATE              PIC X(10)       VALUE SPACES.        
           05  PV-TARGET-DATE-RDF REDEFINES PV-TARGET-DATE.                     
               10  FILLER                  PIC X(02).                           
               10  PV-TARGET-YY            PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-TARGET-MM            PIC X(02).                           
               10  FILLER                  PIC X(01).                           
               10  PV-TARGET-DD            PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               15  PV-YY                   PIC 9(02)       VALUE ZEROES.        
               10  PV-MM                   PIC 9(02)       VALUE ZEROES.        
               10  PV-DD                   PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
W26600******************************************************************        
W26600*  CREATE A LIST OF STORE FOR USE BY BMC TO UNLOAD ONLY                   
W26600*  THE REQUIRED STORES FROM TSUMINV                                       
W26600******************************************************************        
W26600 01  SL-STORE-LIST-CARDS.                                                 
W26600     05  FILLER                      PIC X(05)       VALUE SPACES.        
W26600     05  FILLER                      PIC X(01)       VALUE SPACE.         
W26600         88  SL-LEADING-COMMA                        VALUE ','.           
W26600         88  SL-NO-LEADING-COMMA                     VALUE SPACE.         
W26600     05  SL-STORE-NUMBER             PIC 9(05)       VALUE ZEROES.        
W26600     05  FILLER                      PIC X(01)       VALUE SPACE.         
W26600         88  SL-TRAILING-COMMA                       VALUE ','.           
W26600         88  SL-NO-TRAILING-COMMA                    VALUE SPACE.         
W26600     05  FILLER                      PIC X(68)       VALUE SPACES.        
                                                                                
      ******************************************************************        
      *  CREATE SORT CONTROL CARDS TO SORT THE INVENTORY DATA                   
      *  FROM A PLATINUM UNLOAD FILE OF TINVSUMV BASED ON THE 5                 
      *  CANDIDATE FILE                                                         
      *    .. CONTROL CARDS BASED ON THE STORE'S ACTUAL INVENTORY DATE          
      *    .. SORT ON:  STORE/DEPT/CLASS/SEQ                                    
      *    .. INCLUDE AREAS 0 - 99999                                           
      ******************************************************************        
       01  SC-SORT-CARDS.                                                       
W21732     05  SC-SORT-FIELDS              PIC X(80)       VALUE                
W21732*        '  SORT FIELDS=(1,3,CH,A,4,14,PD,A)'.                            
W26600         '  SORT FIELDS=(1,2,BI,A,3,14,PD,A)'.                            
W26600     05  SC-SORT-OUTFIL.                                                  
W26600         10  FILLER                  PIC X(15)       VALUE                
W26600             '  OUTFIL FILES='.                                           
W26600         10  SC-OUTFIL-FILE-NBR      PIC 9(01)       VALUE ZEROES.        
W26600         10  FILLER                  PIC X(31)       VALUE                
W26600             ',            * CANDIDATES SET #'.                           
W26600         10  SC-OUTFIL-COMMENT-NBR   PIC 9(02)       VALUE ZEROES.        
W26600         10  FILLER                  PIC X(31)       VALUE SPACES.        
W26600     05  SC-SORT-INCLUDE-COND-1.                                          
W26600         10  FILLER                  PIC X(14)       VALUE SPACES.        
W26600             88  SC-INCLUDE                          VALUE                
W26600                 '    INCLUDE=(('.                                        
W26600             88  SC-NO-INCLUDE                       VALUE SPACES.        
W26600         10  FILLER                  PIC X(12)       VALUE                
W26600             '01,02,BI,EQ,'.                                              
W26600         10  SC-INCLUDE-STORE-NBR    PIC 9(05)       VALUE ZEROES.        
W26600         10  FILLER                  PIC X(06)       VALUE SPACES.        
W26600             88  SC-INCLUDE-OR                       VALUE                
W26600                 ',OR,'.                                                  
W26600             88  SC-INCLUDE-AND                      VALUE                
W26600                 '),AND,'.                                                
W26600         10  FILLER                  PIC X(43)       VALUE                
W26600             '             * STORE/LOCATION'.                             
W26600     05  SC-SORT-INCLUDE-COND-2.                                          
W26600         10  FILLER                  PIC X(13)       VALUE SPACES.        
W26600         10  FILLER                  PIC X(13)       VALUE                
W26600             '(17,04,BI,LE,'.                                             
W26600         10  SC-INCLUDE-AREA         PIC X(05)       VALUE                
W26600             '99999'.                                                     
W26600         10  FILLER                  PIC X(49)       VALUE                
W26600             '))           * AREA'.                                       
W26600     05  SC-SORT-END                 PIC X(80)       VALUE                
W26600         '  END'.                                                         
                                                                                
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVDTL                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                         
W26600         SELECT LOC_NBR                                                   
                     ,ACTL_INV_DTE                                              
                     ,PLND_UNIT_BK_DTE                                          
                     ,ACTL_INV_RECD_DTE                                         
28545                ,A.STKRM_DAY_QTY                                           
                     ,SCAN_INV_IND                                              
      *-->            ** DERIVED BOOK DATE (IN #DAYS)                           
W05231               ,DAYS(ACTL_INV_DTE) + 1                                    
      *-->            ** DERIVED BOOK DATE (AS A DATE)                          
W05231               ,DATE(DAYS(ACTL_INV_DTE) + 1)                              
      *-->            ** PROCESSING DATE (IN #DAYS)                             
                     ,DAYS(:PV-PROCESSING-DATE)                                 
W33304               ,SCHD_PHY_CNT_CDE                                          
28545            FROM TINVPAR A                                                 
28545                ,TINCNTL B                                                 
                WHERE ACTL_INV_RECD_DTE <> '9999-09-09'                         
                  AND ACTL_UNIT_BK_DTE = '9999-09-09'                           
W33304            AND ACTL_FIN_BK_DTE = '9999-09-09'                            
                  AND SCAN_INV_IND = 'Y'                                        
W33304            AND SCHD_PHY_CNT_CDE = :PC-PHYSICAL-INVENTORY                 
                  AND SLS_ADJ_CPLD_IND = 'N'                                    
W33304            AND TMST_EDT_STAT_CDE = :PC-TRANS-POSTED                      
W33304            AND A.LOC_INV_STAT_CDE = :PC-INITIAL-STATUS                   
28545             AND A.INV_ID = B.INV_ID                                       
28545             AND B.ACTV_IND = 'Y'                                          
             ORDER BY ACTL_INV_DTE                                              
W26600               ,LOC_NBR                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W26600     PERFORM Z200-READ-PROCESSED                                          
W33304       UNTIL PS-ACTV-STORE                                                
W33304          OR PS-END-OF-PROCESSED-STORES.                                  
                                                                                
W26600     PERFORM C100-LOAD-PROCESSED                                          
W26600         UNTIL PS-END-OF-PROCESSED-STORES.                                
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
           IF END-OF-TINVPAR                                                    
               DISPLAY ' * * * * * * * * * * * * * * * * * * * * * * '          
               DISPLAY ' NO STORES SELECTED FOR SALES CALC PROCESSING'          
               DISPLAY ' * * * * * * * * * * * * * * * * * * * * * * *'         
           ELSE                                                                 
               PERFORM B200-PROCESS-TINVPAR                                     
                   UNTIL END-OF-TINVPAR                                         
           END-IF.                                                              
                                                                                
W26600     IF PT-QUALIFIED-COUNT = ZEROES                                       
               DISPLAY ' * * * * * * * * * * * * * * * * * * * * * * '          
               DISPLAY ' NO STORES QUALIFIED FOR SELECTION           '          
               DISPLAY ' * * * * * * * * * * * * * * * * * * * * * *'           
JS0600     ELSE                                                                 
JS0600         PERFORM Z600-WRITE-SORT-CARDS                                    
JS0600     END-IF.                                                              
                                                                                
           PERFORM B300-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      *                                                                         
       B100-HOUSEKEEPING.                                                       
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
                                                                                
           IF PV-YY > '80'                                                      
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC.                                   
                                                                                
           MOVE PV-YY   TO PV-PROCESSING-YY                                     
           MOVE PV-MM   TO PV-PROCESSING-MM                                     
           MOVE PV-DD   TO PV-PROCESSING-DD.                                    
                                                                                
           OPEN  INPUT PREV-SELECTED-STORES                                     
                OUTPUT SELECTED-STORES                                          
                       STORES-TO-CALC-FILE1                                     
                       STORES-TO-CALC-FILE2                                     
                       STORES-TO-CALC-FILE3                                     
                       STORES-TO-CALC-FILE4                                     
                       STORES-TO-CALC-FILE5                                     
W26600                 SORT-CARDS                                               
W26600                 DB-UNLOAD-CARDS                                          
W33304                 ACTV-STORES.                                             
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               DISPLAY 'PROCESS DTE ' PV-PROCESSING-DATE                        
               MOVE 'OPEN TINVPAR_CURSOR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ***************************************************************           
      **  CHECK IF STORE HAS BEEN PREVIOUSLY PROCESSED                          
      ***************************************************************           
       B200-PROCESS-TINVPAR.                                                    
                                                                                
W26600     IF  PT-PROCESSED-LOC(INVPAR-LOC-NBR) EQUAL ZERO                      
               PERFORM B210-REAL-PROCESS                                        
           END-IF.                                                              
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
       B210-REAL-PROCESS.                                                       
                                                                                
                                                                                
           IF INVPAR-ACTL-INV-RECD-DTE > PV-PROCESSING-DATE                     
               CONTINUE                                                         
           ELSE                                                                 
               IF PV-ACTL-PLUS-STKRM-DAYS > PV-PROCESSING-DATE-DAYS             
                   CONTINUE                                                     
               ELSE                                                             
                   PERFORM B220-CHK-INVALID-IND                                 
                   IF SQLCODE = +100                                            
      *-->             ** NO DETAILS W/ INVALID-IND = 'Y', SO GO AHEAD          
      *-->             ** AND SETUP FOR SELECTIONS PROCESS                      
                       PERFORM Z500-WRITE-OUTPUT-REC                            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       B220-CHK-INVALID-IND.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT 1                                                         
                 INTO :PV-CHECK                                                 
                 FROM TINVDTL                                                   
W26600          WHERE LOC_NBR         = :INVPAR-LOC-NBR                         
                  AND INVALID_REC_IND = 'Y'                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN +100                                                        
               WHEN -811                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVDTL_CURSOR'                                  
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       B300-EOJ.                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
                                                                                
W33304     DISPLAY '*************************************'.             INKUT169
W33304     DISPLAY '***  SUMMARY STATISTICS FOR       ***'.             INKUT169
W33304     DISPLAY '** NON ACTIVE STORE BYPASS LOGIC  ***'.             INKUT169
W33304     DISPLAY '*************************************'.             INKUT169
W33304     DISPLAY 'TOT INV RECS.....:' PA-TOT-INV-STR-RECS.            INKUT169
W33304     DISPLAY 'TOT RECS BYPASSED:' PA-TOT-NON-ACTV-STR-RECS-BYP.   INKUT169
W33304     DISPLAY 'TOT RECS READ....:' PA-TOT-RECS-READ.               INKUT169
W33304     DISPLAY 'TOT RECS.WRIT....:' PA-TOT-ACTV-STR-RECS-WRIT.      INKUT169
                                                                                
           CLOSE PREV-SELECTED-STORES                                           
                 SELECTED-STORES                                                
                 STORES-TO-CALC-FILE1                                           
                 STORES-TO-CALC-FILE2                                           
                 STORES-TO-CALC-FILE3                                           
                 STORES-TO-CALC-FILE4                                           
                 STORES-TO-CALC-FILE5                                           
W26600           SORT-CARDS                                                     
W26600           DB-UNLOAD-CARDS                                                
W33304           ACTV-STORES.                                                   
                                                                                
      ******************************************************************        
      *    LOAD THE PREVIOUSLY SELECTED STORES TABLE.                           
      ******************************************************************        
       C100-LOAD-PROCESSED.                                                     
                                                                                
W26600     MOVE IN018-STR-NBR TO PT-PROCESSED-LOC(IN018-STR-NBR).               
W26600     DISPLAY ' PREV PROCESSED STORE ' IN018-STR-NBR                       
W26600             ', INV DATE ' IN018-ACTL-INV-DTE                             
W26600             ', ADJ DATE ' IN018-SALE-ADJ-DTE.                            
W26600                                                                          
W26600     PERFORM Z200-READ-PROCESSED.                                         
                                                                                
      ******************************************************************        
      * GET THE NEXT LOCATION TO CHECK FOR QUALIFICATION                        
      ******************************************************************        
       Z100-FETCH-TINVPAR.                                                      
      *                                                                         
           EXEC SQL FETCH TINVPAR_CURSOR                                        
W26600*       INTO :INVPAR-STORE-NBR                                            
W26600        INTO :INVPAR-LOC-NBR                                              
                  ,:INVPAR-ACTL-INV-DTE                                         
                  ,:INVPAR-PLND-UNIT-BK-DTE                                     
                  ,:INVPAR-ACTL-INV-RECD-DTE                                    
                  ,:INVPAR-STKRM-DAY-QTY                                        
                  ,:INVPAR-SCAN-INV-IND                                         
                  ,:PV-ACTL-PLUS-STKRM-DAYS                                     
                  ,:PV-TARGET-DATE                                              
                  ,:PV-PROCESSING-DATE-DAYS                                     
W33304            ,:INVPAR-SCHD-PHY-CNT-CDE                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-TINVPAR TO TRUE                                   
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR_CURSOR'                                  
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * READ PREVIOUSLY SELECTED STORE CONTROL CARD                             
      ******************************************************************        
       Z200-READ-PROCESSED.                                                     
                                                                                
           READ PREV-SELECTED-STORES                                            
                INTO IN018-PREV-CALC-STORES                                     
                AT END SET PS-END-OF-PROCESSED-STORES TO TRUE                   
W33304          NOT AT END                                                      
W33304          ADD +1 TO PA-TOT-RECS-READ                                      
           END-READ.                                                            
                                                                                
W33304     IF NOT PS-END-OF-PROCESSED-STORES                                    
W33304        PERFORM Z210-CHECK-FOR-ACTV-STR                                   
W33304     END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* CHECK TINVPAR TO MAKE SURE THE STORE IS IN AN ACTIVE CYCLE,             
W33304* IS IN 'IN' STATUS (STORE IS ACTIVE WITHIN THE CYCLE) AND                
W33304* THAT THE STORE HAS NOT FINANCIALLY BOOKED (WHERE THE                    
W33304* ACTL_FIN_BK_DTE = '9999-09-09'). IF THE STORE MEETS THESE               
W33304* CRITERIA, WRITE IT OUT TO OUT09 - THIS WAY ANY SOFT-DELETED             
W33304* OR PREVIOUSLY BOOKED STORES DO NOT GET PASSED FURTHER                   
W33304* DOWN THE JOBSTREAM FOR PROCESSING.                                      
W33304******************************************************************        
W33304 Z210-CHECK-FOR-ACTV-STR.                                                 
W33304                                                                          
W33304     SET PS-NON-ACTV-STORE TO TRUE.                                       
W33304                                                                          
W33304     MOVE IN018-STR-NBR TO INVPAR-LOC-NBR.                                
W33304                                                                          
W33304     PERFORM WITH TEST AFTER                                              
W33304             VARYING PV-RETRY-COUNTER FROM 1 BY 1                         
W33304             UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES OR                 
W33304                     SQLCODE = ZERO OR                                    
W33304                     SQLCODE = +100 OR                                    
W33304                     SQLCODE = -811                                       
W33304                                                                          
W33304        EXEC SQL                                                          
W33304          SELECT A.ACTL_INV_DTE                                           
W33304           INTO :INVPAR-ACTL-INV-DTE                                      
W33304            FROM TINVPAR A,                                               
W33304                 TINCNTL B                                                
W33304            WHERE B.ACTV_IND = 'Y'                                        
W33304            AND A.INV_ID     = B.INV_ID                                   
W33304            AND A.LOC_NBR    = :INVPAR-LOC-NBR                            
W33304            AND ((A.LOC_INV_STAT_CDE <> :PC-INITIAL-STATUS                
W33304            AND A.ACTL_FIN_BK_DTE = :PC-DEFAULT-NINES)                    
W33304             OR (A.LOC_INV_STAT_CDE = :PC-INITIAL-STATUS                  
W33304            AND A.ACTL_FIN_BK_DTE <> :PC-DEFAULT-NINES))                  
W33304        END-EXEC                                                          
W33304                                                                          
W33304        EVALUATE TRUE                                                     
W33304            WHEN SQLCODE = +0                                             
W33304             OR  SQLCODE = -811                                           
W33304                 SET PS-NON-ACTV-STORE TO TRUE                            
W33304                 ADD +1 TO PA-TOT-NON-ACTV-STR-RECS-BYP                   
W33304                 DISPLAY '*****************************'                  
W33304                 DISPLAY '* BYPASSED STORE ' INVPAR-LOC-NBR               
W33304                 DISPLAY '* HAS BEEN SOFT DELETED OR   '                  
W33304                 DISPLAY '* FINANCIALLY BOOKED AND REMOVED'               
W33304                 DISPLAY '* FROM PREVIOUSLY SELECTED STORES'              
W33304                 DISPLAY '*****************************'                  
W33304                 DISPLAY ' '                                              
W33304            WHEN SQLCODE = +100                                           
W33304                  PERFORM Z240-WRITE-ACTV-STR                             
W33304                  SET PS-ACTV-STORE TO TRUE                               
W33304                  ADD +1 TO PA-TOT-INV-STR-RECS                           
W33304            WHEN OTHER                                                    
W33304                 DISPLAY '******************************'                 
W33304                 DISPLAY '**    TINVPAR ABEND INFO    **'                 
W33304                 DISPLAY '**    LOC NBR: ' INVPAR-LOC-NBR                 
W33304                 DISPLAY '******************************'                 
W33304                 MOVE 'Z210-CHECK-FOR-ACTV-STR'                           
W33304                                   TO  PV-OPER-ATTEMPTED                  
W33304                 PERFORM Z999-SQL-ABEND                                   
W33304        END-EVALUATE                                                      
W33304     END-PERFORM.                                                         
W33304                                                                          
W33304     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
W33304         SQLCODE NOT = ZERO AND                                           
W33304         SQLCODE NOT = +100 AND                                           
W33304         SQLCODE NOT = -811                                               
W33304         DISPLAY '***********************************************'        
W33304         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO         *'        
W33304         DISPLAY '** SELECT FROM TINVPAR / TINCNTL              *'        
W33304         DISPLAY '** PARAGRAPH = Z210-CHECK-FOR-ACTV-STR        *'        
W33304         DISPLAY '** TINVPAR LOC ' INVPAR-LOC-NBR                         
W33304         DISPLAY '***********************************************'        
W33304         PERFORM Z999-SQL-ABEND                                           
W33304     END-IF.                                                              
W33304                                                                          
W33304*****************************************************************         
W33304* READ THE INPUT TO BYPASS RECS WHOSE LOC_NBR IS A NON ACTV STR.          
W33304*****************************************************************         
W33304 Z220-BYPASS-NON-ACTV-STR-RECS.                                           
W33304                                                                          
W33304     PERFORM Z230-READ-INPUT.                                             
W33304                                                                          
W33304     ADD +1 TO PA-TOT-NON-ACTV-STR-RECS-BYP.                              
W33304                                                                          
W33304     IF NOT PS-END-OF-PROCESSED-STORES                                    
W33304        ADD +1 TO PA-TOT-RECS-READ                                        
W33304     END-IF.                                                              
W33304                                                                          
W33304******************************************************************INKUT169
W33304*                                                                *INKUT169
W33304*    READ THE INPUT FILE.                                        *INKUT169
W33304*                                                                *INKUT169
W33304******************************************************************INKUT169
W33304 Z230-READ-INPUT.                                                 INKUT169
W33304                                                                  INKUT169
W33304     INITIALIZE IN018-PREV-CALC-STORES.                           INKUT169
W33304                                                                  INKUT169
W33304     READ PREV-SELECTED-STORES                                            
W33304          INTO IN018-PREV-CALC-STORES                                     
W33304          AT END SET PS-END-OF-PROCESSED-STORES TO TRUE                   
W33304     END-READ.                                                            
W33304                                                                  INKUT169
W33304******************************************************************        
W33304* WRITE OUT ACTV STR TO OUTPUT FOR FURTHER PROCESSING DOWNSTREAM          
W33304******************************************************************        
W33304 Z240-WRITE-ACTV-STR.                                                     
W33304                                                                          
W33304     WRITE ACTV-STORES-REC                                                
W33304     FROM  IN018-PREV-CALC-STORES.                                        
W33304                                                                          
W33304     ADD +1 TO PA-TOT-ACTV-STR-RECS-WRIT.                                 
W33304                                                                          
      ******************************************************************        
      * WRITE OUT CANDIDATE FILES AND SELECTED STORE FILE                       
      ******************************************************************        
       Z500-WRITE-OUTPUT-REC.                                                   
                                                                                
           IF INVPAR-ACTL-INV-DTE > PV-PREV-ACTL-INV-DTE                        
               MOVE INVPAR-ACTL-INV-DTE TO PV-PREV-ACTL-INV-DTE                 
               ADD 1 TO PV-WHICH-FILE-NBR                                       
           END-IF.                                                              
                                                                                
W26600     IF PV-WHICH-FILE-NBR < 1 OR PV-WHICH-FILE-NBR > 5                    
W26600         DISPLAY 'STORE ' INVPAR-LOC-NBR                                  
W26600                 ' (INV DTE: ' INVPAR-ACTL-INV-DTE ')'                    
W26600                 ' WILL BE PROCESSED IN NEXT RUN'                         
W26600     ELSE                                                                 
W26600         MOVE INVPAR-LOC-NBR      TO IN018-STR-NBR                        
W26600         MOVE INVPAR-ACTL-INV-DTE TO IN018-ACTL-INV-DTE                   
W26600         MOVE PV-PROCESSING-DATE  TO IN018-SALE-ADJ-DTE                   
W26600                                                                          
W26600         DISPLAY ' WILL PROCESS STORE ' IN018-STR-NBR                     
W26600                 ', INV DATE ' IN018-ACTL-INV-DTE                         
W26600                 ', ADJ DATE ' IN018-SALE-ADJ-DTE                         
W26600                                                                          
W26600         EVALUATE PV-WHICH-FILE-NBR                                       
W26600             WHEN 1                                                       
W26600                 WRITE OUTPUT-REC1 FROM IN018-PREV-CALC-STORES            
W26600             WHEN 2                                                       
W26600                 WRITE OUTPUT-REC2 FROM IN018-PREV-CALC-STORES            
W26600             WHEN 3                                                       
W26600                 WRITE OUTPUT-REC3 FROM IN018-PREV-CALC-STORES            
W26600             WHEN 4                                                       
W26600                 WRITE OUTPUT-REC4 FROM IN018-PREV-CALC-STORES            
W26600             WHEN 5                                                       
W26600                 WRITE OUTPUT-REC5 FROM IN018-PREV-CALC-STORES            
W26600         END-EVALUATE                                                     
W26600                                                                          
W26600         WRITE SELECTED-STORES-REC FROM IN018-PREV-CALC-STORES            
W26600         ADD 1 TO PT-QUALIFIED-COUNT                                      
W26600         MOVE INVPAR-LOC-NBR                                              
W26600             TO PT-QUALIFIED-LOC      (PT-QUALIFIED-COUNT)                
W26600         MOVE PV-WHICH-FILE-NBR                                           
W26600             TO PT-QUALIFIED-FILE-NBR (PT-QUALIFIED-COUNT)                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * GENERATE THE SORT/SPLIT CONTROL CARD                                    
      ******************************************************************        
       Z600-WRITE-SORT-CARDS.                                                   
           WRITE SORT-CARDS-REC FROM SC-SORT-FIELDS.                            
                                                                                
           SET PS-NOT-DONE TO TRUE.                                             
W26600     MOVE ZEROES     TO PV-WHICH-FILE-NBR                                 
W26600                        PV-UNLOAD-CARDS-COUNT.                            
                                                                                
W26600     PERFORM Z650-BUILD-OUTFIL                                            
W26600         VARYING PT-QUALIFIED-INDEX FROM 1 BY 1                           
W26600         UNTIL PS-DONE.                                                   
W26600     PERFORM Z675-FINALIZE-COND.                                          
W26600     WRITE SORT-CARDS-REC FROM SC-SORT-END.                               
                                                                                
W26600******************************************************************        
W26600* BUILD THE OUTFIL RECORD AND START THE INCLUDE CONDITION                 
W26600******************************************************************        
W26600 Z650-BUILD-OUTFIL.                                                       
W26600                                                                          
W26600     IF PT-QUALIFIED-FILE-NBR (PT-QUALIFIED-INDEX)                        
W26600         NOT = PV-WHICH-FILE-NBR                                          
W26600         OR PT-QUALIFIED-LOC (PT-QUALIFIED-INDEX) = ZEROES                
W26600         IF PT-QUALIFIED-FILE-NBR (PT-QUALIFIED-INDEX) = ZEROES           
W26600             ADD 1 TO PV-WHICH-FILE-NBR                                   
W26600         ELSE                                                             
W26600             MOVE PT-QUALIFIED-FILE-NBR (PT-QUALIFIED-INDEX)              
W26600                 TO PV-WHICH-FILE-NBR                                     
W26600         END-IF                                                           
W26600                                                                          
W26600         IF PV-WHICH-FILE-NBR > 5                                         
W26600*  GET OUT                                                                
W26600             SET PS-DONE          TO TRUE                                 
W26600         ELSE                                                             
W26600             IF PV-WHICH-FILE-NBR > 1                                     
W26600*  COMPLETES PREVIOUS OUTFIL/CONDITION, IF NOT THE FIRST                  
W26600                 PERFORM Z675-FINALIZE-COND                               
W26600             END-IF                                                       
W26600             MOVE PV-WHICH-FILE-NBR  TO SC-OUTFIL-FILE-NBR                
W26600                                        SC-OUTFIL-COMMENT-NBR             
W26600             WRITE SORT-CARDS-REC FROM SC-SORT-OUTFIL                     
W26600                                                                          
W26600*  SETUP FOR THE FIRST CONDITION WITH INCLUDE, BUT DON'T WRITE            
W26600             SET SC-INCLUDE       TO TRUE                                 
W26600             MOVE PT-QUALIFIED-LOC (PT-QUALIFIED-INDEX)                   
W26600                 TO SC-INCLUDE-STORE-NBR                                  
W26600         END-IF                                                           
W26600     ELSE                                                                 
W26600*  WRITE OUT THE PREVIOUS CONDITION                                       
W26600         SET SC-INCLUDE-OR   TO TRUE                                      
W26600         WRITE SORT-CARDS-REC FROM SC-SORT-INCLUDE-COND-1                 
W26600         PERFORM Z700-WRITE-DB-UNLOAD-CARDS                               
W26600                                                                          
W26600*  SETUP SUBSEQUENT CONDITION WITHOUT INCLUDE, BUT DON'T WRITE            
W26600         SET SC-NO-INCLUDE    TO TRUE                                     
W26600                                                                          
W26600         MOVE PT-QUALIFIED-LOC (PT-QUALIFIED-INDEX)                       
W26600             TO SC-INCLUDE-STORE-NBR                                      
W26600     END-IF.                                                              
W26600                                                                          
W26600******************************************************************        
W26600* FINALIZE THE INCLUDE CONDITIONS                                         
W26600******************************************************************        
W26600 Z675-FINALIZE-COND.                                                      
W26600                                                                          
W26600     SET SC-INCLUDE-AND  TO TRUE.                                         
W26600     WRITE SORT-CARDS-REC FROM SC-SORT-INCLUDE-COND-1.                    
W26600     PERFORM Z700-WRITE-DB-UNLOAD-CARDS.                                  
W26600                                                                          
W26600     WRITE SORT-CARDS-REC FROM SC-SORT-INCLUDE-COND-2.                    
W26600                                                                          
W26600******************************************************************        
W26600* WRITE OUT THE LIST OF STORE FOR DB UNLOAD                               
W26600******************************************************************        
W26600 Z700-WRITE-DB-UNLOAD-CARDS.                                              
W26600     IF SC-INCLUDE-STORE-NBR = ZEROES                                     
W26600         CONTINUE                                                         
W26600     ELSE                                                                 
W26600         ADD +1                     TO PV-UNLOAD-CARDS-COUNT              
W26600         IF PV-UNLOAD-CARDS-COUNT > 1                                     
W26600             SET SL-LEADING-COMMA       TO TRUE                           
W26600         ELSE                                                             
W26600             SET SL-NO-LEADING-COMMA    TO TRUE                           
W26600         END-IF                                                           
W26600         MOVE SC-INCLUDE-STORE-NBR   TO SL-STORE-NUMBER                   
W26600         WRITE DB-UNLOAD-CARDS-REC   FROM SL-STORE-LIST-CARDS             
W26600     END-IF.                                                              
W26600                                                                          
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT171'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
