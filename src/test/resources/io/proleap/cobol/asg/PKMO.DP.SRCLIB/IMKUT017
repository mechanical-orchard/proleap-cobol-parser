       IDENTIFICATION DIVISION.                                         09/16/94
                                                                           LV020
       PROGRAM-ID.             IMKUT017.                                   CL**1
       AUTHOR.                 D.THEEL.                                    CL**1
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**1
       DATE-WRITTEN.           03/11/2009.                                 CL**1
       DATE-COMPILED.                                                      CL**1
                                                                           CL**1
      ******************************************************************   CL**1
      *  IMKUT017 - GET INFO FOR NON VSM ITEM SKU/UPC LEVEL CHANGES    *   CL**1
      ******************************************************************   CL**1
      *                                                                         
      * THIS PROGRAM WILL READ IN SIZE DESCRIPTION CHANGE MESSAGES         CL**1
      * AND GATHER ALL ACTIVE INTERNAL AND VENDOR UPCS AND WRITE TO        CL**1
      * OUTPUT FILE.                                                       CL**1
      *                                                                    CL**1
      *****************************************************************    CL**1
      * WR/PITS#    DATE       DESCRIPTION                              00180061
      * --------  --------  ------------------------------------------- 00190061
      * WR#36002  03/12/09  D. THEEL - NEW PROGRAM.                     00200061
      *****************************************************************    CL**1
                                                                                
       ENVIRONMENT DIVISION.                                            00270025
                                                                        00290025
       CONFIGURATION SECTION.                                           00300025
                                                                        00310025
       SOURCE-COMPUTER.       IBM-3090.                                 00320025
       OBJECT-COMPUTER.       IBM-3090.                                 00330025
                                                                        00340025
       INPUT-OUTPUT SECTION.                                            00350025
                                                                        00360025
       FILE-CONTROL.                                                    00370025
           SELECT SIZE-DESC-IN-FILE ASSIGN TO INP01.                    00380025
                                                                        00490025
           SELECT SIZE-DESC-OUT-FILE ASSIGN TO OUT01.                   00500025
                                                                        00490025
      * ------------------------------------------------------------- * 00510025
       DATA DIVISION.                                                   00520025
      * ------------------------------------------------------------- * 00530025
       FILE SECTION.                                                    00540025
                                                                        00550025
       FD  SIZE-DESC-IN-FILE                                            00560025
           RECORDING MODE F                                             00570025
           BLOCK CONTAINS 0 RECORDS                                     00580025
           LABEL RECORDS ARE OMITTED.                                   00590025
       01  SIZE-DESC-IN-REC              PIC  X(100).                   00600025
                                                                        00610025
       FD  SIZE-DESC-OUT-FILE                                           00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  SIZE-DESC-OUT-REC             PIC  X(60).                    00480024
                                                                        00610025
      * ------------------------------------------------------------- * 00850025
       WORKING-STORAGE SECTION.                                         00860025
      * ------------------------------------------------------------- * 00870025
                                                                        00880025
      *----------------------------------------------------------------*01290025
      *    PROGRAM ACCUMULATORS                                         01300025
      *----------------------------------------------------------------*01310025
       01  PA-PROGRAM-ACCUMULATORS.                                     01320025
           05  PA-RECS-IN                PIC 9(07) VALUE 0.             01400025
           05  PA-RECS-OUT               PIC 9(07) VALUE 0.             01370025
           05  PA-FETCH-COUNT            PIC 9(09) VALUE 0.                     
                                                                        01840025
      *----------------------------------------------------------------*01850025
      *    PROGRAM CONSTANTS                                            01860025
      *----------------------------------------------------------------*01870025
       01  PC-PROGRAM-CONSTANTS.                                        01880025
           05  PC-DEADLOCK               PIC S9(04) VALUE -913 COMP.    02350025
           05  PC-MAX-RETRIES            PIC S9(04) VALUE +3   COMP.    02320025
           05  PC-ROW-NOT-FOUND          PIC S9(04) VALUE +100 COMP.    02340025
           05  PC-UNAVAILABLE-RESOURCES  PIC S9(04) VALUE -904 COMP.    02360025
           05  PC-PGM-NAME               PIC X(08)  VALUE 'IMKUT017'.   01890025
           05  PC-JOB-NAME               PIC X(08)  VALUE 'IMK929'.     01890025
                                                                        02370025
      *----------------------------------------------------------------*02380025
      *    PROGRAM SWITCHES                                             02390025
      *----------------------------------------------------------------*02400025
       01  PS-PROGRAM-SWITCHES.                                         02410025
           05  PS-INPUT-FILE-EOF-SW         PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00920024
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00930024
           05  PS-ROW-FOUND-SW           PIC X(01) VALUE 'Y'.           02620025
               88  PS-ROW-FOUND                    VALUE 'Y'.           02630025
               88  PS-ROW-NOT-FOUND                VALUE 'N'.           02640025
      *----------------------------------------------------------------*02660025
      *    PROGRAM VARIABLES                                            02670025
      *----------------------------------------------------------------*02680025
       01  PV-PROGRAM-VARIABLES.                                        02690025
           05  PV-RETRY-COUNT            PIC S9(03) VALUE +0 COMP.      03420025
           05  PV-SQL-SUB                PIC S9(4)  VALUE +0 COMP.      03060025
                                                                        03580025
           05  PV-ACTION                 PIC X(08)  VALUE SPACES.       03140025
           05  PV-ERROR-MESSAGE          PIC X(48)  VALUE SPACES.       03570025
           05  PV-MESSAGE                PIC X(100) VALUE SPACES.       03530025
           05  PV-PARAGRAPH              PIC X(50)  VALUE SPACES.       03130025
       01  ERROR-CODE                    PIC S9(04) VALUE +0                    
                                                         COMP SYNC.             
                                                                        03580025
      *****************************************************************         
      *  NON-VSM ITEM MAINTENANCE INFO COPYBOOK                                 
      ******************************************************************        
           COPY IMRD018.                                                        
                                                                                
      *****************************************************************         
      *  NON-VSM SIZE DESC INFO COPYBOOK                                        
      ******************************************************************        
           COPY IMRD098.                                                        
                                                                           CL**1
      *----------------------------------------------------------------*03730025
      *    ABEND CODES THE ARE APPLICATION DEFINED                      03740025
      *----------------------------------------------------------------*03750025
       01  ABEND-CODE                  PIC S9(4)    VALUE ZEROS         03760025
                                                    COMP SYNC.          03770025
           88  AC-DB2-ERROR                         VALUE +4013.        03820025
                                                                        03840025
      ******************************************************************        
      *  UPC TABLE                                                              
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COMMUNICATION AREAS FOR DB2                                    03860025
      ******************************************************************        
           EXEC SQL                                                     03880025
               INCLUDE SQLCA                                            03890025
           END-EXEC.                                                    03900025
                                                                        03910025
      ******************************************************************        
      *  ERROR MESSAGE AREA FOR DB2                                   * 03930025
      ******************************************************************        
           COPY DPWS004.                                                03950025
                                                                        03960025
      ***************************************************************** 04640025
      *    SIZE DESCRIPTION CHANGES CURSOR                              04650025
      ***************************************************************** 04660025
           EXEC SQL                                                     04670025
               DECLARE SIZE_DESC_CHGS_CSR CURSOR FOR                    04680025
                   SELECT  UPC_ID                                               
                          ,UPC_TYP_CDE                                          
                     FROM  TUPC                                                 
                    WHERE  KOHLS_SIZE_CDE = :UPC-KOHLS-SIZE-CDE                 
                      AND  UPC_TYP_CDE IN ('I','V')                             
                      AND  EFF_END_DTE IS NULL                                  
                    FOR FETCH ONLY                                              
                    WITH UR                                                     
           END-EXEC.                                                    04760025
                                                                        04770025
                                                                        04770025
                                                                        04770025
      * ------------------------------------------------------------- * 04780025
       PROCEDURE DIVISION.                                              04790025
      * ------------------------------------------------------------- * 04800025
       0000-IMKUT017.                                                   04810025
                                                                        04820025
           PERFORM 1000-INITIALIZE.                                     04830025
                                                                        04840025
           PERFORM 2000-PROCESS-RECS                                    0025    
               UNTIL PS-NO-MORE-INPUT-RECORDS.                          0025    
                                                                        04910025
           PERFORM 2500-EOJ-PROCESSING.                                 04920025
                                                                        08290025
           STOP RUN.                                                    04930025
                                                                        04940025
      *----------------------------------------------------------------*04950025
      *  INITIALIZATION ROUTINE.                                        04960025
      *----------------------------------------------------------------*04970025
       1000-INITIALIZE.                                                 04980025
                                                                        04990025
           OPEN INPUT SIZE-DESC-IN-FILE                                 05060025
                OUTPUT SIZE-DESC-OUT-FILE.                              05110025
           PERFORM 8000-READ-INPUT-FILE.                                05120025
           IF PS-NO-MORE-INPUT-RECORDS                                          
               MOVE 4095 TO ERROR-CODE                                          
               DISPLAY ' '                                                      
               DISPLAY '***** INPUT FILE IS EMPTY *****'                        
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                        05640025
                                                                        07100025
      * ------------------------------------------------------------- * 07110025
      *  PROCESS EACH SIZE DESCRIPTION CHANGE INPUT REC                 07120025
      * ------------------------------------------------------------- * 07130025
       2000-PROCESS-RECS.                                               07150025
                                                                        07170025
           MOVE IM018-SIZE-DESC-CHG-SIZE-CDE TO UPC-KOHLS-SIZE-CDE.             
           SET PS-ROW-FOUND TO TRUE.                                            
           PERFORM 3000-OPEN-SZ-DESC-CHGS-CURSOR.                       43420025
           PERFORM 3100-FETCH-SZ-DESC-CHGS-CURSOR                               
             UNTIL PS-ROW-NOT-FOUND.                                            
           PERFORM 3200-CLOSE-SZ-DESC-CHGS-CURSOR.                      44330025
           PERFORM 8000-READ-INPUT-FILE.                                05120025
                                                                        07570025
                                                                                
      * ------------------------------------------------------------- * 07110025
      *  PERFORM END OF JOB PROCESSING                                  07120025
      * ------------------------------------------------------------- * 07130025
       2500-EOJ-PROCESSING.                                             07670025
                                                                        07680025
           MOVE ERROR-CODE TO RETURN-CODE.                                      
           DISPLAY '*-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-*'.   07690025
           DISPLAY 'END OF JOB RECORD COUNTERS'.                        07700025
           DISPLAY '*-%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%-*'.             07720025
           DISPLAY 'SIZE DESC RECS READ    ' PA-RECS-IN.                07730025
           DISPLAY 'SIZE DESC RECS WRITTEN ' PA-RECS-OUT.               07740025
           DISPLAY 'SIZE DESC FETCH COUNT  ' PA-FETCH-COUNT.            07740025
           DISPLAY '*-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-#-*'.   08200025
                                                                        08310025
                                                                        07680025
      * ------------------------------------------------------------- * 43390025
      * OPEN THE SIZE DESCRIPTION CHANGES CURSOR                        43400025
      * ------------------------------------------------------------- * 43410025
       3000-OPEN-SZ-DESC-CHGS-CURSOR.                                   43420025
                                                                        43430025
           MOVE '3000-OPEN-SZ-DESC-CHGS-CURSOR' TO PV-PARAGRAPH.        43440025
                                                                        43450025
           PERFORM                                                      43460025
               WITH TEST AFTER                                          43470025
               VARYING PV-SQL-SUB                                       43480025
               FROM 1 BY 1                                              43490025
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                        43500025
                  OR SQLCODE = ZERO                                     43510025
                                                                        43530025
                 EXEC SQL                                               43540025
                     OPEN SIZE_DESC_CHGS_CSR                            43550025
                 END-EXEC                                               43560025
                                                                        43570025
               EVALUATE TRUE                                            43580025
                   WHEN SQLCODE = PC-UNAVAILABLE-RESOURCES              43590025
                   WHEN SQLCODE = PC-DEADLOCK                           43600025
                   WHEN SQLCODE = ZERO                                  43610025
                        CONTINUE                                        43620025
                   WHEN SQLWARN0 NOT = SPACE                            43630025
                   WHEN SQLCODE NOT = ZERO                              43640025
                       MOVE 'BAD SQLCODE FOR OPEN SIZE_DESC_CHGS_CSR'   43650025
                                     TO  PV-MESSAGE                     43660025
                       DISPLAY '**SIZE CODE** = '                       48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
                       PERFORM 9900-SQL-ABEND                           43680025
               END-EVALUATE                                             43700025
           END-PERFORM.                                                 43710025
                                                                        43720025
           IF PV-SQL-SUB > PC-MAX-RETRIES                               43730025
               MOVE 'MAX RETRIES EXCEEDED FOR OPEN SIZE_DESC_CHGS_CSR'  43740025
                                     TO  PV-MESSAGE                     43750025
               DISPLAY '**SIZE CODE** = '                               48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
               PERFORM 9900-SQL-ABEND                                   43770025
           END-IF.                                                      43790025
                                                                        08380025
      * ------------------------------------------------------------- * 43810025
      * FETCH A ROW FROM THE SIZE DESCRIPTION CHANGES CURSOR            43820025
      * ------------------------------------------------------------- * 43830025
       3100-FETCH-SZ-DESC-CHGS-CURSOR.                                  43840025
                                                                        43850025
           MOVE '3100-FETCH-SZ-DESC-CHGS-CURSOR' TO PV-PARAGRAPH.       43860025
                                                                        43870025
           PERFORM                                                      43880025
               WITH TEST AFTER                                          43890025
               VARYING PV-SQL-SUB                                       43900025
               FROM 1 BY 1                                              43910025
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                        43920025
                  OR SQLCODE = ZERO                                     43930025
                  OR SQLCODE = PC-ROW-NOT-FOUND                         43940025
                                                                        43960025
                 EXEC SQL                                               43970025
                     FETCH  SIZE_DESC_CHGS_CSR                          43980025
                     INTO  :UPC-UPC-ID                                  43990025
                          ,:UPC-UPC-TYP-CDE                             44020025
                 END-EXEC                                               44030025
                                                                        44040025
               EVALUATE TRUE                                            44050025
                   WHEN SQLCODE = PC-UNAVAILABLE-RESOURCES              44060025
                   WHEN SQLCODE = PC-DEADLOCK                           44070025
                   WHEN SQLCODE = PC-ROW-NOT-FOUND                      44080025
                       SET PS-ROW-NOT-FOUND TO TRUE                     44090025
                   WHEN SQLCODE = ZERO                                  44100025
                        ADD 1 TO PA-FETCH-COUNT                                 
                        PERFORM 4000-WRITE-RECORD                       44110025
                   WHEN SQLWARN0 NOT = SPACE                            44120025
                   WHEN SQLCODE NOT = ZERO                              44130025
                       MOVE 'BAD SQLCODE FOR FETCH SIZE_DESC_CHGS_CSR'  43650025
                                     TO  PV-MESSAGE                     44150025
                       DISPLAY '**SIZE CODE** = '                       48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
                       PERFORM 9900-SQL-ABEND                           44170025
               END-EVALUATE                                             44190025
           END-PERFORM.                                                 44200025
                                                                        44210025
           IF PV-SQL-SUB > PC-MAX-RETRIES                               44220025
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH SIZE_DESC_CHGS_CSR' 44230025
                                     TO  PV-MESSAGE                     44240025
               DISPLAY '**SIZE CODE** = '                               48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
               PERFORM 9900-SQL-ABEND                                   44260025
           END-IF.                                                      44280025
                                                                        44290025
      * ------------------------------------------------------------- * 44300025
      * CLOSE THE SIZE DESCRIPTION CHANGES CURSOR                       44310025
      * ------------------------------------------------------------- * 44320025
       3200-CLOSE-SZ-DESC-CHGS-CURSOR.                                  44330025
      *                                                                 44340025
           MOVE '3200-CLOSE-SZ-DESC-CHGS-CURSOR' TO PV-PARAGRAPH.       44350025
                                                                        44360025
           PERFORM                                                      44370025
               WITH TEST AFTER                                          44380025
               VARYING PV-SQL-SUB                                       44390025
               FROM 1 BY 1                                              44400025
               UNTIL PV-SQL-SUB > PC-MAX-RETRIES                        44410025
                  OR SQLCODE = ZERO                                     44420025
                                                                        44440025
                 EXEC SQL                                               44450025
                     CLOSE SIZE_DESC_CHGS_CSR                           44460025
                 END-EXEC                                               44470025
                                                                        44480025
               EVALUATE TRUE                                            44490025
                   WHEN SQLCODE = PC-UNAVAILABLE-RESOURCES              44500025
                   WHEN SQLCODE = PC-DEADLOCK                           44510025
                   WHEN SQLCODE = ZERO                                  44520025
                        CONTINUE                                        44530025
                   WHEN SQLWARN0 NOT = SPACE                            44540025
                   WHEN SQLCODE NOT = ZERO                              44550025
                       MOVE 'BAD SQLCODE FOR CLOSE SIZE_DESC_CHGS_CSR'  43650025
                                     TO  PV-MESSAGE                     44570025
                       DISPLAY '**SIZE CODE** = '                       48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
                       PERFORM 9900-SQL-ABEND                           44590025
               END-EVALUATE                                             44610025
           END-PERFORM.                                                 44620025
                                                                        44630025
           IF PV-SQL-SUB > PC-MAX-RETRIES                               44640025
               MOVE 'MAX RETRIES EXCEEDED FOR CLOSE SIZE_DESC_CHGS_CSR' 44650025
                                     TO  PV-MESSAGE                     44660025
               DISPLAY '**SIZE CODE** = '                               48020025
                                      IM018-SIZE-DESC-CHG-SIZE-CDE      48020025
               PERFORM 9900-SQL-ABEND                                   44680025
           END-IF.                                                      44700025
                                                                        21120025
      * ------------------------------------------------------------- * 45970051
      * WRITE EACH RECORD BROUGHT BACK FROM SIZE CURSOR                 45980051
      * ------------------------------------------------------------- * 45990051
       4000-WRITE-RECORD.                                               44110025
           MOVE IM018-BUSINESS-EVENT-ID     TO IM098-BUS-EVENT.                 
           MOVE IM018-SIZE-DESC-CHG-SIZE-CDE TO IM098-SIZE-CODE.                
           MOVE IM018-SIZE-DESC-CHG-KOHLS-DESC TO IM098-SIZE-DESC.              
           MOVE UPC-UPC-ID             TO IM098-UPC-ID.                         
           MOVE UPC-UPC-TYP-CDE        TO IM098-UPC-TYP-CDE.                    
           WRITE SIZE-DESC-OUT-REC FROM IM098-SIZE-DESC-REC.                    
           ADD 1 TO PA-RECS-OUT.                                                
                                                                                
      * ------------------------------------------------------------- * 45970051
      * READ INPUT FILE OF SIZE DESC CHANGE RECS                        45980051
      * ------------------------------------------------------------- * 45990051
       8000-READ-INPUT-FILE.                                            46000051
           READ SIZE-DESC-IN-FILE INTO IMRD018-RECORD                   46010051
               AT END                                                   46020051
                   SET PS-NO-MORE-INPUT-RECORDS TO TRUE                 00930024
               NOT AT END                                               46040051
                   ADD 1 TO PA-RECS-IN                                  47920025
           END-READ.                                                            
                                                                                
      *----------------------------------------------------------------*47930025
      *  FORMAT A DB2 ERROR MESSAGE                                    *47940025
      *----------------------------------------------------------------*47950025
                                                                                
       9900-SQL-ABEND.                                                  47970025
                                                                        47980025
           DISPLAY '***************************************************'47990025
           DISPLAY '**  ERROR     **'                                   48000030
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                  48010025
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH.                 48020025
           DISPLAY '**  MESSAGE   **  = ' PV-MESSAGE.                   48030025
           DISPLAY '**  SIZE CODE **  = ' IM018-SIZE-DESC-CHG-SIZE-CDE. 48020025
                                                                        48020025
      *                                                                 48050025
      *  ROUTINE TO CONVERT THE SQLCA INTO A READABLE FORMAT, USING     48060025
      *  THE CALLED MODULE DSNTIAR.                                     48070025
      *                                                                 48080025
           COPY DPPD004.                                                48090025
                                                                        48100025
           EXEC SQL                                                     08680024
                ROLLBACK                                                08690024
           END-EXEC.                                                    08700024
                                                                        08710024
           SET AC-DB2-ERROR TO TRUE.                                    47900025
           CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
