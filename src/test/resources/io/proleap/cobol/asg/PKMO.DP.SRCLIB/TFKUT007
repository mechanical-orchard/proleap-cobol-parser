       IDENTIFICATION DIVISION.                                         00010001
                                                                        00020001
       PROGRAM-ID.      TFKUT007.                                       00030001
       AUTHOR.          CHERI PARMLEY.                                  00040001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050001
       DATE-WRITTEN.    07/11/11.                                       00060002
       DATE-COMPILED.                                                   00070001
                                                                        00080001
      ******************************************************************00090001
      *  MODULE TYPE:   COBOL2 BATCH. MUST BE COMPILED WITH 'N' = NO    00100006
      *                 OPTION, USES DB2 WITHIN THE DP MODULE ONLY      00110006
      *                 JOB STEP WILL HAVE DB CONTROL CARD FOR TFKUT007 00120006
      *                                                                 00130001
      * THIS PROGRAM PREPARES THE GENCO TRANSFER FILE TO BE PROCESSED   00140001
      * BY TFKUP004 UNDER THE DB2 CHECKPOINT RESTART SYSTEM.            00150002
      *                                                                 00160001
      * THE PROGRAM READS THE GENCO RECEIVE TRANSFER UNLOAD FILE,FORMATS00170002
      * RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED     00180001
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.    00190001
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE         00200001
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE  00210001
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS    00220001
      * OUT TO A SEQUENTIAL FILE.                                       00230001
      *                                                                 00240001
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION       00250001
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS   00260001
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE  00270001
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING   00280001
      * THE BAD RETURN CODE IS DISPLAYED ON SYSOUT AND THE PROGRAM      00290001
      * ABENDS.                                                         00300001
      *                                                                 00310001
      *========================== CHANGE LOG ===========================00320001
      *  AUTH        BY        DATE                 DESCRIPTION         00330001
      *=================================================================00340001
      * WR40189   C. PARMLEY  07-11-11   INITIAL IMPLEMENTATION.        00350002
      *                                                                 00360001
260107* CHG0260107 JIM HUNTER 08-19-21   ADD COPYBOOK DPWS990 TO        00361008
260107*                                  MAKE DPKUT900 CALL DYNAMIC.    00362008
      ******************************************************************00370001
                                                                        00380001
       ENVIRONMENT DIVISION.                                            00390001
       CONFIGURATION SECTION.                                           00400001
                                                                        00410001
       SOURCE-COMPUTER.    IBM-3090.                                    00420001
       OBJECT-COMPUTER.    IBM-3090.                                    00430001
                                                                        00440001
       INPUT-OUTPUT SECTION.                                            00450001
       FILE-CONTROL.                                                    00460001
                                                                        00470001
           SELECT UNLOAD-FILE   ASSIGN TO INP01.                        00480001
                                                                        00490001
       DATA DIVISION.                                                   00500001
       FILE SECTION.                                                    00510001
                                                                        00520001
       FD  UNLOAD-FILE                                                  00530001
           RECORDING MODE IS F                                          00540001
           LABEL RECORDS ARE STANDARD                                   00550001
           RECORD CONTAINS 130 CHARACTERS                               00560002
           BLOCK CONTAINS 0 RECORDS                                     00570001
           DATA RECORD IS UNLOAD-REC.                                   00580001
       01  UNLOAD-REC                       PIC  X(130).                00590002
                                                                        00600001
      ***************************************************************** 00610001
      *                       WORKING STORAGE                           00620001
      ***************************************************************** 00630001
       WORKING-STORAGE SECTION.                                         00640001
                                                                        00650001
       01  W-START                          PIC  X(40)                  00660001
           VALUE 'TFKUT007 - WORKING STORAGE BEGINS HERE'.              00670002
                                                                        00680001
                                                                        00690001
      ***************************************************************** 00700001
      *                         SWITCHES                                00710001
      ***************************************************************** 00720001
       01  PROGRAM-SWITCHES.                                            00730001
           05  PS-INPUT-UNLOAD-EOF-SW       PIC  X(01) VALUE 'N'.       00740001
               88  PS-INPUT-UNLOAD-EOF                 VALUE 'Y'.       00750001
           05  PS-FIRST-TRANS-READ-SW       PIC  X(01) VALUE 'Y'.       00760001
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00770001
                                                                        00780001
      ******************************************************************00790001
      *                       ACCUMULATORS                              00800001
      ******************************************************************00810001
       01  PROGRAM-ACCUMULATORS.                                        00820001
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00830001
           05  PA-WRITTEN                   PIC  9(11) VALUE ZEROS.     00840001
                                                                        00850001
      ******************************************************************00860001
      *                           CONSTANTS                             00870001
      ******************************************************************00880001
       01  PROGRAM-CONSTANTS.                                           00890001
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'TFKUT007'.00900002
                                                                        00910001
           05  PC-TRAN-PREP-FUNC-CODES.                                 00920001
               10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00930001
               10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   00940001
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00950001
                                                                        00960001
           05  PC-JOB-NAME                  PIC  X(08) VALUE 'TFK006  '.00970002
           05  PC-PLAN-NAME                 PIC  X(08) VALUE 'TFKUP004'.00980005
           05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +130 COMP. 00990004
                                                                        01000001
      ******************************************************************01010001
      *                          VARIABLES                              01020001
      ******************************************************************01030001
       01  PROGRAM-VARIABLES.                                           01040001
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.    01050001
           05  PV-OPERATION-MSG             PIC  X(60) VALUE SPACES.    01060001
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     01070001
                                                                        01080001
           05  PV-CHECKPOINT-TYPE           PIC  X(01) VALUE SPACE.     01090001
               88  CONTROL-BREAK                       VALUE 'C'.       01100001
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       01110001
               88  TIME-INTERVAL                       VALUE 'T'.       01120001
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       01130001
                                                                        01140001
      *--->UNLOAD RECORD KEYS                                           01150001
           05  PV-XFER-DKEY-ID              PIC  S9(9) COMP.            01160001
           05  PV-FR-LOC-NBR                PIC  S9(4) COMP.            01170001
                                                                        01180001
           05  PV-INTEGER                   PIC S9(11) VALUE +0.        01190001
           05  PV-DECIMAL                   PIC S9(11) VALUE +0.        01200001
                                                                        01210001
      ******************************************************************01220001
      *               SYSTEM TIME AND DATE VARIABLES                    01230001
      ******************************************************************01240001
       01  SYS-DATE-TIME.                                               01250001
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01260001
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01270001
                                                                        01280001
       01  ST-BEG-TIME.                                                 01290001
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01300001
           05  FILLER                       PIC  X(01) VALUE ':'.       01310001
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01320001
                                                                        01330001
       01  ST-END-TIME.                                                 01340001
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01350001
           05  FILLER                       PIC  X(01) VALUE ':'.       01360001
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01370001
                                                                        01380001
       01  SD-EDIT-DATE.                                                01390001
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01400001
           05  FILLER                       PIC  X(01) VALUE '-'.       01410001
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01420001
           05  FILLER                       PIC  X(01) VALUE '-'.       01430001
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01440001
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01450001
                                                                        01460001
      ******************************************************************01470001
      *                        ERROR HANDLING                           01480001
      ******************************************************************01490001
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01500001
                                                       COMP SYNC.       01510001
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01520001
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01530001
                                                                        01540001
                                                                        01550001
      ******************************************************************01560001
      * RECORD LAYOUT FOR UNLOAD KEY                                    01570001
      ******************************************************************01580001
           COPY TFRD068.                                                01590002
                                                                        01600001
      ******************************************************************01610001
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                 01620001
      ******************************************************************01630001
260107     COPY DPWS990.                                                01640008
                                                                        01650001
           COPY DPLS000.                                                01651008
                                                                        01652008
      ******************************************************************01660001
      *  SQL COMMUNICATIONS WORK AREA                                   01670001
      ******************************************************************01680001
           COPY SQLCA2.                                                 01690001
                                                                        01700001
                                                                        01710001
       PROCEDURE DIVISION.                                              01720001
      ******************************************************************01730001
      * MAINLINE-PROCESSING                                             01740001
      ******************************************************************01750001
                                                                        01760001
           PERFORM A100-INITIALIZE.                                     01770001
                                                                        01780001
           PERFORM B100-PROCESS-UNLOAD-INPUT-FILE                       01790001
             UNTIL PS-INPUT-UNLOAD-EOF.                                 01800001
                                                                        01810001
           PERFORM Z990-END-OF-JOB-ROUTINE.                             01820001
                                                                        01830001
           GOBACK.                                                      01840001
                                                                        01850001
                                                                        01860001
      ******************************************************************01870001
      * PREFORMS TASK INITIATION                                        01880001
      ******************************************************************01890001
       A100-INITIALIZE.                                                 01900001
                                                                        01910001
           OPEN INPUT UNLOAD-FILE.                                      01920001
                                                                        01930001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01940001
                                                                        01950001
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01960001
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01970001
                                                                        01980001
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01990001
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02000001
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02010001
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02020001
                                                                        02030001
           PERFORM C100-ISSUE-OPEN-REQUEST.                             02040001
                                                                        02050001
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         02060001
                                                                        02070001
           IF PS-INPUT-UNLOAD-EOF                                       02080001
               MOVE 'C200-READ-UNLOAD-INPUT-FILE' TO PV-PARAGRAPH-NAME  02090001
               MOVE '** EMPTY UNLOAD FILE - NO DATA **'                 02100001
                                                  TO PV-OPERATION-MSG   02110001
           END-IF.                                                      02120001
                                                                        02130001
                                                                        02140001
      ******************************************************************02150001
      * PROCESS THE UNLOAD FILE. IF A RECORD IS READ, GATHER WHATEVER   02160001
      * OTHER DATA IS NECESSARY TO TAKE CHECKPOINTS AND ISSUE A WRITE   02170001
      * REQUEST TO THE TRANSACTION PREPARATION PROGRAM.                 02180001
      ******************************************************************02190001
       B100-PROCESS-UNLOAD-INPUT-FILE.                                  02200001
                                                                        02210001
           IF FIRST-TRANS-READ                                          02220001
               MOVE TF068-DKEY-ID       TO PV-XFER-DKEY-ID              02230002
               MOVE 'N'                   TO PS-FIRST-TRANS-READ-SW     02250001
           END-IF.                                                      02260001
                                                                        02270001
           IF CONTROL-BREAK                                             02280001
               PERFORM C300-CKPT-BY-CNTL-BREAK                          02290001
           ELSE                                                         02300001
               IF LOGICAL-TRANSACTION                                   02310001
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   02320001
               ELSE                                                     02330001
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 02340001
               END-IF                                                   02350001
           END-IF.                                                      02360001
                                                                        02370001
           PERFORM C200-READ-UNLOAD-INPUT-FILE.                         02380001
                                                                        02390001
                                                                        02400001
      ******************************************************************02410001
      * ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION MODULE   02420001
      ******************************************************************02430001
       C100-ISSUE-OPEN-REQUEST.                                         02440001
                                                                        02450001
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              02460001
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.               02470001
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.              02480001
                                                                        02490001
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02500001
                                                                        02510001
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.           02520001
                                                                        02530001
                                                                        02540001
      ******************************************************************02550001
      * READ THE UNLOAD INPUT FILE                                      02560001
      ******************************************************************02570001
       C200-READ-UNLOAD-INPUT-FILE.                                     02580001
                                                                        02590001
           READ UNLOAD-FILE INTO TF068-TRANSFER-REC                     02600002
             AT END SET PS-INPUT-UNLOAD-EOF TO TRUE.                    02610001
                                                                        02620001
           IF PS-INPUT-UNLOAD-EOF                                       02630001
               CONTINUE                                                 02640001
           ELSE                                                         02650001
               ADD +1 TO PA-READ                                        02660001
           END-IF.                                                      02670001
                                                                        02680001
                                                                        02690001
      ******************************************************************02700001
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACATION  02710001
      * RECORD "ON" IF THERE IS A CHANGE IN THE KEY. AFTER              02720001
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   02730001
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   02740001
      ******************************************************************02750001
       C300-CKPT-BY-CNTL-BREAK.                                         02760001
                                                                        02770001
           IF  TF068-DKEY-ID   = PV-XFER-DKEY-ID                        02780002
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02800001
           ELSE                                                         02810001
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02820001
               MOVE TF068-DKEY-ID     TO PV-XFER-DKEY-ID                02830002
           END-IF.                                                      02850001
                                                                        02860001
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 02870001
           MOVE TF068-TRANSFER-REC           TO DP000-TRANS-REC.        02880002
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        02890001
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         02900001
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        02910001
                                                                        02920001
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         02930001
                                                                        02940001
           ADD +1 TO PA-WRITTEN.                                        02950001
                                                                        02960001
                                                                        02970001
      ******************************************************************02980001
      * THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSACTION   02990001
      * RECORD "ON".  THE UNLOAD PROGRAM WILL CHECK THE CHECKPOINT      03000001
      * FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE TAKEN. AFTER   03010001
      * THE TRANSACTION RECORD HAS BEEN FORMATTED, A WRITE REQUEST IS   03020001
      * ISSUED TO THE TRANSACTION PREPARATION MODULE.                   03030001
      ******************************************************************03040001
       C400-CKPT-BY-LOGICAL-TRANS.                                      03050001
                                                                        03060001
           MOVE 'Y'                          TO DP000-CHECKPOINT-IND.   03070001
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 03080001
           MOVE TF068-TRANSFER-REC           TO DP000-TRANS-REC.        03090002
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03100001
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03110001
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03120001
                                                                        03130001
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03140001
                                                                        03150001
           ADD +1 TO PA-WRITTEN.                                        03160001
                                                                        03170001
                                                                        03180001
      ******************************************************************03190001
      * THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A WRITE   03200001
      * REQUEST TO THE TRANSACTION PREPARATION MODULE IF CHECKPOINTS ARE03210001
      * TAKEN BY TIME INTERVAL (SECONDS) OR REQUESTED BY THE MAINTENANCE03220001
      * PROGRAM.                                                        03230001
      ******************************************************************03240001
       C500-CKPT-BY-TIME-OR-REQUEST.                                    03250001
                                                                        03260001
           MOVE 'N' TO DP000-CHECKPOINT-IND.                            03270001
                                                                        03280001
           MOVE PC-TRANS-RECORD-LENGTH       TO DP000-TRANS-REC-LENGTH. 03290001
           MOVE TF068-TRANSFER-REC           TO DP000-TRANS-REC.        03300002
           MOVE PC-TRAN-PREP-FUNC-WRITE      TO DP000-FUNC-CODE.        03310001
           MOVE PC-JOB-NAME                  TO DP000-JOB-NAME.         03320001
           MOVE PC-PLAN-NAME                 TO DP000-PLAN-NAME.        03330001
                                                                        03340001
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03350001
                                                                        03360001
           ADD +1 TO PA-WRITTEN.                                        03370001
                                                                        03380001
                                                                        03390001
      ******************************************************************03400001
      * ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION MODULE   03410001
      ******************************************************************03420001
       C600-ISSUE-CLOSE-REQUEST.                                        03430001
                                                                        03440001
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             03450001
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              03460001
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             03470001
                                                                        03480001
           PERFORM Z100-CALL-TRANS-PREP-MODULE.                         03490001
                                                                        03500001
                                                                        03510001
      ******************************************************************03520001
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     03530001
      ******************************************************************03540001
       Z100-CALL-TRANS-PREP-MODULE.                                     03550001
                                                                        03560001
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         03570001
                                                                        03580001
           IF  DP000-GOOD-RET-CODE                                      03590001
               CONTINUE                                                 03600001
                                                                        03610001
           ELSE                                                         03620001
               MOVE 'Z100-CALL-TRANS-PREP-MODULE' TO PV-PARAGRAPH-NAME  03630001
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               03640001
                                                  TO PV-OPERATION-MSG   03650001
               MOVE DP000-RET-CODE                TO PV-RET-CODE        03660001
                                                                        03670001
               SET ABEND-4444-FORCED-ABEND TO TRUE                      03680001
                                                                        03690001
               PERFORM Z200-ABEND                                       03700001
           END-IF.                                                      03710001
                                                                        03720001
                                                                        03730001
      ******************************************************************03740001
      * NON-DB2 ABEND                                                   03750001
      ******************************************************************03760001
       Z200-ABEND.                                                      03770001
                                                                        03780001
           PERFORM Z999-CONTROLS.                                       03790001
                                                                        03800001
           DISPLAY '                     '.                             03810001
           DISPLAY '** ABEND           **'.                             03820001
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          03830001
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03840001
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         03850001
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              03860001
                                                                        03870001
           CALL 'ILBOABN0' USING ABEND-CODE.                            03880001
                                                                        03890001
                                                                        03900001
      ******************************************************************03910001
      * PREFORMS TASK TERMINATION PROCESSING                            03920001
      ******************************************************************03930001
       Z990-END-OF-JOB-ROUTINE.                                         03940001
                                                                        03950001
           CLOSE UNLOAD-FILE.                                           03960001
                                                                        03970001
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            03980001
                                                                        03990001
           PERFORM Z999-CONTROLS.                                       04000001
                                                                        04010001
                                                                        04020001
      ******************************************************************04030001
      * DISPLAY CONTROLS FROM THE PROGRAM                               04040001
      ******************************************************************04050001
       Z999-CONTROLS.                                                   04060001
                                                                        04070001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04080001
                                                                        04090001
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04100001
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04110001
                                                                        04120001
           DISPLAY '                    '.                              04130001
           DISPLAY '**********************'.                            04140001
           DISPLAY '   TFKUT007 SUMMARY   '.                            04150002
           DISPLAY '   TRANS PREP DETAILS '.                            04160001
           DISPLAY '**********************'.                            04170001
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04180001
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04190001
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04200001
           DISPLAY '======================'.                            04210001
           DISPLAY 'RECORDS READ    = ' PA-READ.                        04220001
           DISPLAY '                  '.                                04230001
           DISPLAY 'RECORDS WRITTEN = ' PA-WRITTEN.                     04240001
           DISPLAY '                  '.                                04250001
                                                                        04260001
                                                                        04270001
      ******************************************************************04280001
      * TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES          04290001
      ******************************************************************04300001
           COPY DPPD000.                                                04310001
                                                                        04320001
      ******************************************************************04330001
