00001 ******************************************************************01/05/95
00002 *              PEPD003 - IN-STORE DOCUMENT FORMAT PROCEDURE               
00003 *   THIS PROCEDURE DIVISION WILL BE USED WITH PEWS053 TO FORMAT           
00004 *   A DOCUMENT.                                                           
00005 *   THE PROGRAMS WHICH USE THIS PROCEDURE MUST SUPPLY THE                 
00006 *   FOLLOWING:                                                            
00007 *     1.  MOVE CURRENT DATE TO PE053-PV-DATE.                             
00008 *     2.  MOVE CURRENT TIME TO PE053-PV-TIME.                             
00009 *     3.  MOVE PROGRAM NAME TO PE053-PC-PROGRAM-NAME.                     
00010 *     4.  MOVE DOCUMENT NUMBER TO PE053-PV-DOCUMENT-TO-PROCESS.           
00011 *     5.  MOVE 3 DIGIT STORE NUMBER TO                                    
00012 *         PE053-PV-STORE-TO-PROCESS-NUM.                                  
00013 *     6.  MOVE STORE NAME   TO PE053-PV-STORE-NAME                        
00014 *     7.  THE FOLLOWING MUST BE MOVED TO PE053-PV-PRINT-INDICATION        
00015 *         IN THE FOLLOWING CASES:                                         
00016 *             - IF THE DOCUMENT IS A IN-STORE DOC PRINT,                  
00017 *               MOVE ' '.                                                 
00018 *             - IF THE DOCUMENT IS A IN-STORE DOC REPRINT,                
00019 *               MOVE 'R'.                                                 
00020 *             - IF THE DOCUMENT IS A GENERATED IN-STORE DOC,              
00021 *               MOVE 'G'.                                                 
00022 *             - IF THE DOCUMENT IS A GENERATED IN-STORE DOC REPRIN        
00023 *               MOVE 'X'.                                                 
00024 *     8.  RETRIEVE AND MOVE VALUE OF CONTROL NUMBER 29 (TPPCCTL)          
00025 *         TO PE048-INSTR-PAGES-PER-DOC-AMT.                               
00026 *     9.  THE PROGRAM MUST INCLUDE THE 132 WIDE REPORT HEADER             
00027 *         DPWS132O.                                                       
00028 *    10.  THE PROGRAM MUST INCLUDE THE DCLGENS FOR THE FOLLOWING          
00029 *         TABLES:                                                         
00030 *                 TSTRPPC                                                 
00031 *                 TSPPCDT                                                 
00032 *    11.  THE MAXIMUM NUMBER OF PAGES TO PRINT MUST BE MOVED TO           
00033 *         PE053-PV-PAGES-TO-PRINT.                                        
00034 *                                                                         
00035 ******************************************************************        
010970*   08/28/97 - JIM ARENDT       WR 5541                          *        
010970*    - CHANGED HEADING TEXT TO SAY: 'GRAND TOTAL PIECES' INSTEAD *        
010970*      OF 'TOTAL QUANTITY'.                                      *        
010970*    - ADDED A SUBTOTAL LINE 'TOTAL THIS PAGE__________' TO THE  *        
010970*      DOCUMENT PRINT.                                           *        
010970*    - MOVED HEADING TEXT '* - REDUCTION IN UNITS' TO THE LEFT   *        
010970*      MARGIN.                                                   *        
W24472                                                                          
W24472*   03/03/03 - ABRAHAM JACOB    WR 24472                         *        
W24472*    - NI-SKU PROJECT.  DEPARTMENT, CLASS AND SEQUENCE REPLCAED  *        
W24472*      8 DIGIT NON-INTELLIGENT SKU.                              *        
      *                                                                *        
      *   01/26/05 - TODD C. MICHALEK WR NO. 27559                     *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH AND REMOVED A 3 DIGIT ALPHA STORE NUMBER *        
      *      FIELD                                                     *        
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE *        
      *      CALLS                                                     *        
      *    - ADDED THE NEW REASON CODE ("87") FOR                      *        
      *      "COSMETICS TESTERS ONLY" IN-STORE DOCUMENTS               *        
      *                                                                *        
      *   10/26/05 - TODD C. MICHALEK WR NO. 28048                     *        
      *    - ADDED THE NEW REASON CODE ("89") FOR                      *        
      *      "MARK OUT-OF-STOCK - SALVAGE" IN-STORE DOCUMENTS          *        
      *                                                                *        
233646*   10/14/19 - JIM HUNTER       WR NO. CHG0233646                *        
233646*    - MAINFRAME REFACTORING - ADD "WITH UR" TO SELECT STATEMENT.*        
233646*      REFACTORED CODE DOESN'T WORK WITHOUT IT.                  *        
00035 ******************************************************************        
00036 * PE003-A000-FORMAT-DOCUMENT - MAIN PROCESSING TO FORMAT A       *        
00037 * DOCUMENT.                                                               
00038 ******************************************************************        
00039  PE003-A000-FORMAT-DOCUMENT.                                              
00040                                                                           
00041                                                                           
00042      PERFORM PE003-A100-INITIALIZATION.                                   
00043                                                                           
00044      PERFORM PE003-B000-HEADER-PROCESSING.                                
00045                                                                           
00046      IF (PE053-PS-HEADER-FOUND                                            
00047       AND PE053-PS-NO-ABEND-ERROR)                                        
00048          PERFORM PE003-C000-DETAIL-PROCESSING                             
00049      END-IF.                                                              
00050                                                                           
00051      EJECT                                                                
00052                                                                           
00053                                                                           
00054  PE003-A100-INITIALIZATION.                                               
00055 ******************************************************************        
00056 *   PE003-A100-INITIALIZATION - INITIALIZES VARIOUS WORKING               
00057 *   STORAGE FIELDS.                                                       
00058 *                                                                         
00059 *     PERFORMED BY : PE003-A000-FORMAT-DOCUMENT                           
00060 ******************************************************************        
00061      INITIALIZE PE053-DT-DOCUMENT-TABLE.                                  
00062      SET PE053-PS-FIRST-TIME-THRU                                         
00063          PE053-PS-NOT-END-OF-DETAIL                                       
00064          PE053-PS-NO-HEADER-FOUND                                         
00065          PE053-PS-NO-ABEND-ERROR TO TRUE.                                 
00066      MOVE ZEROS TO PE053-PV-PAGE-NUMBER.                                  
00067      EJECT                                                                
00068  PE003-B000-HEADER-PROCESSING.                                            
00069 ******************************************************************        
00070 *   PE003-B000-HEADER-PROCESSING - RETRIEVES THE HEADER                   
00071 *   INFORMATION AND FORMATS IT.                                           
00072 *                                                                         
00073 *     PERFORMED BY : PE003-A000-FORMAT-DOCUMENT                           
00074 ******************************************************************        
00075      PERFORM PE003-F100-GET-HEADER-INFO.                                  
00076                                                                           
00077      IF PE053-PS-NO-ABEND-ERROR                                           
00078          IF PE053-PS-HEADER-FOUND                                         
00079              PERFORM PE003-B100-FORMAT-HEADER-INFO                        
00080          END-IF                                                           
00081      END-IF.                                                              
00082                                                                           
00083      EJECT                                                                
00084                                                                           
00085  PE003-B100-FORMAT-HEADER-INFO.                                           
00086 ******************************************************************        
00087 *   PE003-B100-FORMAT-HEADER-INFO - FORMATS THE PAGE                      
00088 *   HEADINGS OF THE DOCUMENT.                                             
00089 *                                                                         
00090 *   PERFORMED BY : PE003-A000-FORMAT-DOCUMENT                             
00091 ******************************************************************        
00092 *                                                                         
00093      MOVE PE053-PV-STORE-TO-PROCESS                                       
00094                                  TO PE053-RD-LINE1-STORE-NO.              
00095      MOVE PE053-PV-STORE-NAME    TO PE053-RD-LINE1-STORE-DESC.            
00096      MOVE STRPPC-CRE-ID          TO PE053-RD-LINE1-ISSUED-BY.             
00097      MOVE STRPPC-CRE-DTE         TO PE053-PV-HOLD-ISO-FMT-DATE.           
00098      MOVE PE053-PV-HOLD-ISO-MM   TO PE053-RD-LINE1-MONTH.                 
00099      MOVE PE053-PV-HOLD-ISO-DD   TO PE053-RD-LINE1-DAY.                   
00100      MOVE PE053-PV-HOLD-ISO-YY   TO PE053-RD-LINE1-YEAR.                  
00101 *                                                                         
00102      IF STRPPC-RSN-CDE = ZEROS                                            
00103          MOVE '___'              TO PE053-RD-LINE2-RSN-CODE               
00104          MOVE '_'                TO PE053-RD-LINE2-TYP-CODE               
00105          MOVE SPACES             TO PE053-RD-LINE2-RSN-DESC               
00106                                     PE053-RD-LINE3-PRC-CHG-DESC           
00107      ELSE                                                                 
00108          MOVE STRPPC-RSN-CDE     TO PE053-RD-LINE2-RSN-CODE               
00109          MOVE STRPPC-TYP-CDE     TO PE053-RD-LINE2-TYP-CODE               
00110          PERFORM PE003-B200-GET-TYP-RSN-DESC                              
00111      END-IF.                                                              
00112 *                                                                         
00113      MOVE STRPPC-DEPT-AREA-DEST  TO PE053-RD-LINE4-DPT-AREA.              
00114 *                                                                         
00115      MOVE PE053-PV-MM            TO DP132O-RUN-MONTH.                     
00116      MOVE PE053-PV-DD            TO DP132O-RUN-DAY.                       
00117      MOVE PE053-PV-YY            TO DP132O-RUN-YEAR.                      
00118      MOVE PE053-PV-HH            TO DP132O-RUN-HOUR.                      
00119      MOVE PE053-PV-MIN           TO DP132O-RUN-MINUTE.                    
00120      MOVE PE053-PC-PROGRAM       TO DP132O-PROGRAM-NAME.                  
00121      MOVE 1                      TO DP132O-REPORT-NUMBER.                 
00122      EJECT                                                                
00123                                                                           
00124  PE003-B200-GET-TYP-RSN-DESC.                                             
00125 ******************************************************************        
00126 *   PE003-B200-DETERMINE-TYP-RSN-DESC - DETERMINES THE TYPE CODE          
00127 *   AND REASON CODE DESCRIPTIONS, BASED ON THE CODES.                     
00128 *                                                                         
00129 *   PERFORMED BY : PE003-B100-FORMAT-HEADER-INFO                          
00130 ******************************************************************        
00131 *                                                                         
00132      MOVE STRPPC-TYP-CDE         TO PE049-WORKING-TYPE-CDE.               
00133      EVALUATE TRUE                                                        
00134          WHEN PE049-MARKDOWN                                              
00135              MOVE PE049-SHORT-MKDN-DESC1                                  
00136                                  TO PE053-RD-LINE3-PRC-CHG-DESC           
00137          WHEN PE049-MARKUP                                                
00138              MOVE PE049-SHORT-MKUP-DESC1                                  
00139                                  TO PE053-RD-LINE3-PRC-CHG-DESC           
00140          WHEN PE049-MARKDOWN-CANCEL                                       
00141              MOVE PE049-SHORT-MKDN-CANCEL-DESC                            
00142                                  TO PE053-RD-LINE3-PRC-CHG-DESC           
00143          WHEN PE049-MARKUP-CANCEL                                         
00144              MOVE PE049-SHORT-MKUP-CANCEL-DESC                            
00145                                  TO PE053-RD-LINE3-PRC-CHG-DESC           
00146          WHEN OTHER                                                       
00147              MOVE SPACES         TO PE053-RD-LINE3-PRC-CHG-DESC           
00148      END-EVALUATE.                                                        
00149 *                                                                         
           MOVE SPACES                 TO PE053-RD-LINE2-UNIT-FIN.              
00150      MOVE STRPPC-RSN-CDE         TO PE049-WORKING-INSTR-RSN-CDE.          
                                                                                
00152      EVALUATE TRUE                                                        
00153          WHEN PE049-IS-ZERO-OUT-RSN                                       
00154              MOVE PE049-IS-SHORT-ZERO-OUT-DESC                            
00155                                  TO PE053-RD-LINE2-RSN-DESC               
00156          WHEN PE049-IS-CORRECTION-RSN                                     
00157              MOVE PE049-IS-SHORT-CORRECTION-DESC                          
00158                                  TO PE053-RD-LINE2-RSN-DESC               
00159          WHEN PE049-IS-BUYER-GEN-MISSED-RSN                               
00160              MOVE PE049-IS-SHORT-BYR-MISSED-DESC                          
00161                                  TO PE053-RD-LINE2-RSN-DESC               
                   MOVE PE049-IS-FINANCIAL-DESC                                 
                                       TO PE053-RD-LINE2-UNIT-FIN               
00162          WHEN PE049-IS-CUSTOMER-RET-RSN                                   
00163              MOVE PE049-IS-SHORT-CUST-RET-DESC                            
00164                                  TO PE053-RD-LINE2-RSN-DESC               
00165          WHEN PE049-IS-KNOWN-THEFT-RSN                                    
00166              MOVE PE049-IS-SHORT-THEFT-DESC                               
00167                                  TO PE053-RD-LINE2-RSN-DESC               
00168          WHEN PE049-IS-INSTR-DAMAGE-RSN                                   
00169              MOVE PE049-IS-SHORT-INSTR-DMG-DESC                           
00170                                  TO PE053-RD-LINE2-RSN-DESC               
                   MOVE PE049-IS-UNIT-FIN-DESC                                  
                                       TO PE053-RD-LINE2-UNIT-FIN               
               WHEN PE049-IS-COSMETICS-TESTER-RSN                               
                   MOVE PE049-IS-SHORT-CSMTCS-TST-DESC                          
                                       TO PE053-RD-LINE2-RSN-DESC               
                   MOVE PE049-IS-UNIT-FIN-DESC                                  
                                       TO PE053-RD-LINE2-UNIT-FIN               
               WHEN PE049-IS-MOS-SALVAGE-RSN                                    
                   MOVE PE049-IS-SHORT-MOS-SALVAGE                              
                                       TO PE053-RD-LINE2-RSN-DESC               
                   MOVE PE049-IS-UNIT-FIN-DESC                                  
                                       TO PE053-RD-LINE2-UNIT-FIN               
00171          WHEN PE049-IS-FINANCIAL-ADJ-RSN                                  
00172              MOVE PE049-IS-SHORT-FIN-ADJ-DESC                             
00173                                  TO PE053-RD-LINE2-RSN-DESC               
00174          WHEN OTHER                                                       
00175              MOVE SPACES         TO PE053-RD-LINE2-RSN-DESC               
00176      END-EVALUATE.                                                        
00177  PE003-C000-DETAIL-PROCESSING.                                            
00178 ******************************************************************        
00179 *   PE003-C000-DETAIL-PROCESSING - RETRIEVES THE DETAIL                   
00180 *   INFORMATION AND FORMATS IT.                                           
00181 *                                                                         
00182 *     PERFORMED BY : PE003-A000-FORMAT-DOCUMENT                           
00183 ******************************************************************        
00184                                                                           
00185      IF (PE053-PV-GENERATED                                               
00186       OR PE053-PV-GENERATED-REPRINT)                                      
00187          PERFORM PE003-F600-OPEN-DETAIL-CURSOR                            
00188      END-IF.                                                              
00189                                                                           
00190      MOVE 1                      TO PE053-PV-PAGE-NUMBER                  
00191                                     PE053-PV-LINE-NUMBER.                 
00192      IF (PE053-PV-GENERATED                                               
00193       OR PE053-PV-GENERATED-REPRINT)                                      
00194          PERFORM PE003-F200-FETCH-DETAIL                                  
00195      END-IF.                                                              
00196                                                                           
00197      PERFORM                                                              
00198          UNTIL (PE053-PV-PAGE-NUMBER > PE053-PV-PAGES-TO-PRINT            
00199             OR  PE053-PS-END-OF-DETAIL                                    
00200             OR  PE053-PS-ABEND-ERROR)                                     
00201                                                                           
00202          PERFORM PE003-C200-MOVE-HEADINGS-TO-DT                           
00203                                                                           
00204          PERFORM                                                          
00205            VARYING PE053-PV-LINE-CNTR                                     
00206            FROM 1 BY 1                                                    
00207            UNTIL PE053-PV-LINE-CNTR > PE053-PC-LINES-PER-PAGE             
00208              MOVE SPACES         TO PE053-RD-LINE9-LINENO                 
00209              SET PE053-DT-INDEX UP BY 1                                   
00210              ADD +1              TO PE053-PV-LINES-PER-PAGE               
00211              MOVE PE053-PC-SPACE1                                         
00212                                  TO PE053-DT-CTL (PE053-DT-INDEX)         
00213              MOVE PE053-RD-DETAIL-LINE9                                   
00214                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00215 *                                                                         
00216              IF (PE053-PV-GENERATED                                       
00217               OR PE053-PV-GENERATED-REPRINT)                              
00218                  IF  PE053-PS-END-OF-DETAIL                               
00219                      MOVE SPACES TO PE053-RD-LINE9-LINENO                 
00220                      SET PE053-DT-INDEX UP BY 1                           
00221                      ADD +1      TO PE053-PV-LINES-PER-PAGE               
00222                      MOVE PE053-PC-SPACE1                                 
00223                                  TO PE053-DT-CTL (PE053-DT-INDEX)         
00224                      MOVE PE053-RD-DETAIL-LINE9                           
00225                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00226                  ELSE                                                     
00227                      MOVE PE053-PV-LINE-NUMBER                            
00228                                  TO PE053-RD-LINE10-LINENO                
W24472                     MOVE SPPCDT-SKU-NBR                                  
W24472                                 TO PE053-RD-LINE10-SKU                   
00235                      IF  SPPCDT-UPC-NBR = ZEROS                           
00236                          MOVE SPACES                                      
00237                                  TO PE053-RD-LINE10-UPC                   
00238                      ELSE                                                 
00239                          MOVE SPPCDT-UPC-NBR                              
00240                                  TO PE053-RD-LINE10-UPC                   
00241                      END-IF                                               
00242                      MOVE SPPCDT-OLD-PR                                   
00243                                  TO PE053-RD-LINE10-ORIG-PR               
00244                      MOVE SPPCDT-NEW-PR                                   
00245                                  TO PE053-RD-LINE10-NEW-PR                
00246                      COMPUTE PE053-PV-PRICE-DIFF =                        
00247                          SPPCDT-OLD-PR - SPPCDT-NEW-PR                    
00248                      MOVE PE053-PV-PRICE-DIFF                             
00249                                  TO PE053-RD-LINE10-PR-DIFF               
00250                      MOVE SPPCDT-ACTL-PR-CHG-QTY                          
00251                                  TO PE053-RD-LINE10-QTY                   
00252                      MOVE SPACES TO PE053-RD-LINE10-DESC                  
00253                      SET PE053-DT-INDEX UP BY 1                           
00254                      ADD +1      TO PE053-PV-LINES-PER-PAGE               
00255                      MOVE PE053-PC-SPACE1                                 
00256                                  TO PE053-DT-CTL (PE053-DT-INDEX)         
00257                      MOVE PE053-RD-DETAIL-LINE10                          
00258                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00259                  END-IF                                                   
00260              ELSE                                                         
00261                  MOVE PE053-PV-LINE-NUMBER                                
00262                                  TO PE053-RD-LINE9-LINENO                 
00263                  SET PE053-DT-INDEX UP BY 1                               
00264                  ADD +1          TO PE053-PV-LINES-PER-PAGE               
00265                  MOVE PE053-PC-SPACE1                                     
00266                                  TO PE053-DT-CTL (PE053-DT-INDEX)         
00267                  MOVE PE053-RD-DETAIL-LINE9                               
00268                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00269              END-IF                                                       
00270 *                                                                         
00271              SET PE053-DT-INDEX UP BY 1                                   
00272              ADD +1              TO PE053-PV-LINES-PER-PAGE               
00273              MOVE PE053-PC-SPACE1                                         
00274                                  TO PE053-DT-CTL (PE053-DT-INDEX)         
00275              MOVE PE053-RD-DETAIL-LINE11                                  
00276                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00277              IF (PE053-PV-GENERATED                                       
00278               OR PE053-PV-GENERATED-REPRINT)                              
00279                  IF  PE053-PS-NOT-END-OF-DETAIL                           
00280                      PERFORM PE003-F200-FETCH-DETAIL                      
00281                  END-IF                                                   
00282              END-IF                                                       
00283              ADD  1              TO PE053-PV-LINE-NUMBER                  
00284         END-PERFORM                                                       
00285         SET PE053-DT-INDEX UP BY 1                                        
00286         ADD +1                   TO PE053-PV-LINES-PER-PAGE               
00287         MOVE PE053-PC-SPACE1     TO PE053-DT-CTL (PE053-DT-INDEX)         
00288         MOVE PE053-RD-DETAIL-LINE12                                       
00289                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00290         SET PE053-DT-INDEX UP BY 1                                        
00291         ADD +1                   TO PE053-PV-LINES-PER-PAGE               
00292         MOVE PE053-PC-SPACE1     TO PE053-DT-CTL (PE053-DT-INDEX)         
00293         MOVE PE053-RD-DETAIL-LINE13                                       
00294                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00295         SET PE053-DT-INDEX UP BY 1                                        
00296         ADD +1                   TO PE053-PV-LINES-PER-PAGE               
00297         MOVE PE053-PC-SPACE1     TO PE053-DT-CTL (PE053-DT-INDEX)         
00298         MOVE PE053-RD-DETAIL-LINE14                                       
00299                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00300         PERFORM PE003-C100-DETERMINE-REASON                               
00301         ADD +1                   TO PE053-PV-PAGE-NUMBER                  
00302      END-PERFORM.                                                         
00303 *                                                                         
00304                                                                           
00305      IF (PE053-PV-GENERATED                                               
00306       OR PE053-PV-GENERATED-REPRINT)                                      
00307          PERFORM PE003-F700-CLOSE-DETAIL-CURSOR                           
00308      END-IF.                                                              
00309                                                                           
00310      SET PE053-PV-DT-ENTRIES     TO PE053-DT-INDEX.                       
00311                                                                           
00312  PE003-C100-DETERMINE-REASON.                                             
00313 ******************************************************************        
00314 *   PE003-C100-DETERMINE-REASON - DETERMINES THE REASON CODES AND         
00315 *   TYPE CODE DESCRIPTIONS WHICH WILL BE PLACED ON THE BOTTOM OF          
00316 *   THE DOCUMENT.                                                         
00317 *                                                                         
00318 *   PERFORMED BY : PE003-C000-DETAIL-PROCESSING                           
00319 ******************************************************************        
00320 *                                                                         
00321      SET PE049-TYPE-IDX                                                   
00322          PE049-RSN-IDX TO 1.                                              
00323      MOVE PE049-INSTR-RSN-VALUE-TABLE                                     
00324                                  TO PE049-INSTR-RSN-TABLE.                
00325 *MED                                                                      
00326 *MED  -- THE FOLLOWING INVENTORY CHANGES WERE MADE:                       
00327 *MED  --     PE049-RSN-IDX > 5 WAS CHANGED TO > 6                         
00328 *MED  --     SPACES WILL BE MOVED TO THE MKDN RSN AND DESC                
00329 *MED  --     FOR A PE049-INV-MISSD-RSN-CDE ('75').:                       
00327 *                                                                         
00327 *     --     PE049-RSN-IDX > 6 WAS CHANGED TO > 7 ... MP 4/22/97          
00330 *                                                                         
00331      PERFORM                                                              
00332       VARYING PE049-RSN-IDX                                               
00333       FROM 1 BY 1                                                         
00334       UNTIL PE049-RSN-IDX > 9                                             
00334****   UNTIL PE049-RSN-IDX > 7                                             
00335          MOVE PE049-REASON-CODE (1 PE049-RSN-IDX)                         
00336                                  TO PE053-PV-HOLD-TEMP-RSN-CONV           
00337          IF (PE053-PV-HOLD-TEMP-RSN = ZERO                                
00338           OR PE053-PV-HOLD-TEMP-RSN = PE049-LOSS-PREV-RSN-CODE            
00339           OR PE053-PV-HOLD-TEMP-RSN = PE049-INV-MISSD-RSN-CODE            
MP0416          OR PE053-PV-HOLD-TEMP-RSN = PE049-RTN-TO-VNDR-RSN-CODE          
00352           OR PE053-PV-HOLD-TEMP-RSN = PE049-RF-ZERO-OUT-RSN-CODE          
00352           OR PE053-PV-HOLD-TEMP-RSN = PE049-ZERO-OUT-RSN-CODE             
00358           OR PE053-PV-HOLD-TEMP-RSN = PE049-CUST-RTN-RSN-CODE)            
00340              MOVE SPACES         TO PE053-RD-LINE15-MKDN-RSN              
00341                                     PE053-RD-LINE15-MKDN-DESC             
00342          ELSE                                                             
00343              MOVE PE049-TYPE-CODE(1)                                      
00344                                  TO PE053-PV-HOLD-TYPE                    
00345              MOVE PE053-PV-HOLD-TEMP-RSN                                  
00346                                  TO PE053-PV-HOLD-RSN                     
00347              MOVE PE053-PV-HOLD-TYPE-RSN                                  
00348                                  TO PE053-RD-LINE15-MKDN-RSN              
00349              MOVE PE053-PV-HOLD-RSN                                       
00350                                  TO PE049-WORKING-INSTR-RSN-CDE           
00351              EVALUATE TRUE                                                
00352****              WHEN PE049-IS-ZERO-OUT-RSN                               
00353****                  MOVE PE049-IS-DOC-ZERO-OUT-DESC                      
00354****                              TO PE053-RD-LINE15-MKDN-DESC             
00355                  WHEN PE049-IS-BUYER-GEN-MISSED-RSN                       
00356                      MOVE PE049-IS-DOC-MISSED-MKDN-DESC                   
00357                                  TO PE053-RD-LINE15-MKDN-DESC             
00358****              WHEN PE049-IS-CUSTOMER-RET-RSN                           
00359****                  MOVE PE049-IS-DOC-CUST-RET-DESC                      
00360****                              TO PE053-RD-LINE15-MKDN-DESC             
00361                  WHEN PE049-IS-KNOWN-THEFT-RSN                            
00362                      MOVE PE049-IS-DOC-THEFT-DESC                         
00363                                  TO PE053-RD-LINE15-MKDN-DESC             
00364                  WHEN PE049-IS-INSTR-DAMAGE-RSN                           
00365                      MOVE PE049-IS-DOC-INSTR-DMG-DESC                     
00366                                  TO PE053-RD-LINE15-MKDN-DESC             
                       WHEN PE049-IS-COSMETICS-TESTER-RSN                       
                           MOVE PE049-IS-DOC-CSMTCS-TST-DESC                    
                                       TO PE053-RD-LINE15-MKDN-DESC             
                       WHEN PE049-IS-MOS-SALVAGE-RSN                            
                           MOVE PE049-IS-DOC-MOS-SALVAGE-DESC                   
                                       TO PE053-RD-LINE15-MKDN-DESC             
00367                  WHEN OTHER                                               
00368                      MOVE SPACES TO PE053-RD-LINE15-MKDN-DESC             
00369              END-EVALUATE                                                 
00370          END-IF                                                           
00371          MOVE PE049-REASON-CODE (2 PE049-RSN-IDX)                         
00372                                  TO PE053-PV-HOLD-TEMP-RSN-CONV           
00373          IF (PE053-PV-HOLD-TEMP-RSN = ZERO                                
00374           OR PE053-PV-HOLD-TEMP-RSN = PE049-LOSS-PREV-RSN-CODE)           
00375              MOVE SPACES         TO PE053-RD-LINE15-MKDNCAN-RSN           
00376                                     PE053-RD-LINE15-MKDNCAN-DESC          
00377          ELSE                                                             
00378              MOVE PE049-TYPE-CODE(2)                                      
00379                                  TO PE053-PV-HOLD-TYPE                    
00380              MOVE PE053-PV-HOLD-TEMP-RSN                                  
00381                                  TO PE053-PV-HOLD-RSN                     
00382              MOVE PE053-PV-HOLD-TYPE-RSN                                  
00383                                  TO PE053-RD-LINE15-MKDNCAN-RSN           
00384              MOVE PE053-PV-HOLD-RSN                                       
00385                                  TO PE049-WORKING-INSTR-RSN-CDE           
00386              EVALUATE TRUE                                                
00387                  WHEN PE049-IS-CORRECTION-RSN                             
00388                      MOVE PE049-IS-DOC-CORRECTION-DESC                    
00389                                  TO PE053-RD-LINE15-MKDNCAN-DESC          
00390                  WHEN OTHER                                               
00391                      MOVE SPACES TO PE053-RD-LINE15-MKDNCAN-DESC          
00392              END-EVALUATE                                                 
00393          END-IF                                                           
00394          MOVE PE049-REASON-CODE (3 PE049-RSN-IDX)                         
00395                                  TO PE053-PV-HOLD-TEMP-RSN-CONV           
00396          IF (PE053-PV-HOLD-TEMP-RSN = ZERO                                
00397           OR PE053-PV-HOLD-TEMP-RSN = PE049-LOSS-PREV-RSN-CODE)           
00398              MOVE SPACES         TO PE053-RD-LINE15-MKUP-RSN              
00399                                     PE053-RD-LINE15-MKUP-DESC             
00400          ELSE                                                             
00401              MOVE PE049-TYPE-CODE(3)                                      
00402                                  TO PE053-PV-HOLD-TYPE                    
00403              MOVE PE053-PV-HOLD-TEMP-RSN                                  
00404                                  TO PE053-PV-HOLD-RSN                     
00405              MOVE PE053-PV-HOLD-TYPE-RSN                                  
00406                                  TO PE053-RD-LINE15-MKUP-RSN              
00407              MOVE PE053-PV-HOLD-RSN                                       
00408                                  TO PE049-WORKING-INSTR-RSN-CDE           
00409              EVALUATE TRUE                                                
00410                  WHEN PE049-IS-BUYER-GEN-MISSED-RSN                       
00411                      MOVE PE049-IS-DOC-MISSED-MKUP-DESC                   
00412                                  TO PE053-RD-LINE15-MKUP-DESC             
00413                  WHEN OTHER                                               
00414                      MOVE SPACES TO PE053-RD-LINE15-MKUP-DESC             
00415              END-EVALUATE                                                 
00416          END-IF                                                           
00417 *                                                                         
00418          SET PE053-DT-INDEX UP BY 1                                       
00419          ADD +1                  TO PE053-PV-LINES-PER-PAGE               
00420          MOVE PE053-PC-SPACE1    TO PE053-DT-CTL (PE053-DT-INDEX)         
00421          MOVE PE053-RD-DETAIL-LINE15                                      
00422                                  TO PE053-DT-DATA (PE053-DT-INDEX)        
00423      END-PERFORM.                                                         
JA0828*                                                                         
JA0828     SET PE053-DT-INDEX UP BY 1.                                          
JA0828     ADD +1                      TO PE053-PV-LINES-PER-PAGE.              
JA0828     MOVE PE053-PC-SPACE1        TO PE053-DT-CTL(PE053-DT-INDEX).         
JA0828     MOVE PE053-PC-PAGE-SUBTOT-LINE                                       
JA0828                                 TO PE053-DT-DATA(PE053-DT-INDEX).        
00424 *                                                                         
00425      SET PE053-DT-INDEX UP BY 1.                                          
JA0828     ADD +2                      TO PE053-PV-LINES-PER-PAGE.              
JA0828     MOVE PE053-PC-SPACE2        TO PE053-DT-CTL (PE053-DT-INDEX).        
00428      MOVE PE053-RD-DETAIL-LINE16 TO                                       
00429                                  PE053-DT-DATA (PE053-DT-INDEX).          
00430                                                                           
00431  PE003-C200-MOVE-HEADINGS-TO-DT.                                          
00432 ******************************************************************        
00433 *   PE003-C200-MOVE-HEADINGS-TO-DT - MOVES THE HEADING LINES TO           
00434 *   THE DETAIL TABLE.                                                     
00435 *                                                                         
00436 *   PERFORMED BY : PE003-C000-DETAIL-PROCESSING                           
00437 ******************************************************************        
00438 *                                                                         
00439      IF PE053-PS-FIRST-TIME-THRU                                          
00440          SET PE053-DT-INDEX TO 1                                          
00441          MOVE +1                 TO PE053-PV-LINES-PER-PAGE               
00442          SET PE053-PS-NOT-FIRST-TIME-THRU TO TRUE                         
00443      ELSE                                                                 
00444          MOVE +1                 TO PE053-PV-LINES-PER-PAGE               
00445          SET PE053-DT-INDEX UP BY 1                                       
00446      END-IF.                                                              
00447      MOVE PE053-PC-PAGE        TO PE053-DT-CTL                            
00448                                             (PE053-DT-INDEX).             
00449      MOVE DP132O-STANDRD-HEADER-132-COLS                                  
00450                                TO PE053-DT-DATA                           
00451                                             (PE053-DT-INDEX).             
00452      MOVE SPACES               TO PE053-DT-WORD-PAGE                      
00453                                             (PE053-DT-INDEX)              
00454                                   PE053-DT-PAGE-LINE                      
00455                                             (PE053-DT-INDEX).             
00456 *                                                                         
00457      SET PE053-DT-INDEX UP BY 1.                                          
00458      ADD +1                    TO PE053-PV-LINES-PER-PAGE.                
00459      MOVE PE053-PV-DOCUMENT-TO-PROCESS                                    
00460                                TO PE053-RH-HDR1-DOC-NUMBER.               
00461      MOVE PE053-PV-PAGE-NUMBER TO PE053-RH-HDR1-FIRST-PAGE                
00462      MOVE PE053-PV-PAGES-TO-PRINT                                         
00463                                TO PE053-RH-HDR1-LAST-PAGE.                
00464      IF (PE053-PV-REPRINT                                                 
00465      OR  PE053-PV-GENERATED-REPRINT)                                      
00466          MOVE PE053-PC-REPRINT-LINE                                       
00467                                TO PE053-RH-HDR1-REPRINT1                  
00468      ELSE                                                                 
00469          MOVE SPACES           TO PE053-RH-HDR1-REPRINT1                  
00470      END-IF.                                                              
00471      MOVE PE053-PC-SPACE1      TO PE053-DT-CTL (PE053-DT-INDEX).          
00472      MOVE PE053-RH-HEADING-1   TO PE053-DT-DATA (PE053-DT-INDEX).         
00473 *                                                                         
00474      SET PE053-DT-INDEX UP BY 1.                                          
00475      ADD +2                    TO PE053-PV-LINES-PER-PAGE.                
00476      MOVE PE053-PC-SPACE2      TO PE053-DT-CTL (PE053-DT-INDEX).          
00477      MOVE PE053-RD-DETAIL-LINE1                                           
00478                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00479 *                                                                         
00480      SET PE053-DT-INDEX UP BY 1.                                          
00481      ADD +2                    TO PE053-PV-LINES-PER-PAGE.                
00482      MOVE PE053-PC-SPACE2      TO PE053-DT-CTL (PE053-DT-INDEX).          
00483      MOVE PE053-RD-DETAIL-LINE2                                           
00484                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00485 *                                                                         
00486      SET PE053-DT-INDEX UP BY 1.                                          
00487      ADD +2                    TO PE053-PV-LINES-PER-PAGE.                
00488      MOVE PE053-PC-SPACE2      TO PE053-DT-CTL (PE053-DT-INDEX).          
00489      MOVE PE053-RD-DETAIL-LINE3                                           
00490                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00491 *                                                                         
00492      SET PE053-DT-INDEX UP BY 1.                                          
00493      ADD +2                    TO PE053-PV-LINES-PER-PAGE.                
00494      MOVE PE053-PC-SPACE2      TO PE053-DT-CTL (PE053-DT-INDEX).          
00495      MOVE PE053-RD-DETAIL-LINE4                                           
00496                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00497 *                                                                         
00498      SET PE053-DT-INDEX UP BY 1.                                          
00499      ADD +1                    TO PE053-PV-LINES-PER-PAGE.                
JA0828     IF PE053-PV-PAGE-NUMBER = 1                                          
JA0828         MOVE PE053-PC-SPACE1  TO PE053-DT-CTL (PE053-DT-INDEX)           
JA0828         MOVE PE053-RD-DETAIL-LINE5                                       
JA0828                               TO PE053-DT-DATA (PE053-DT-INDEX)          
JA0828     ELSE                                                                 
JA0828         MOVE PE053-PC-SPACE1  TO PE053-DT-CTL (PE053-DT-INDEX)           
JA0828         MOVE SPACES           TO PE053-DT-DATA (PE053-DT-INDEX).         
00503 *                                                                         
00504      SET PE053-DT-INDEX UP BY 1.                                          
00505      ADD +2                    TO PE053-PV-LINES-PER-PAGE.                
00506      MOVE PE053-PC-SPACE2      TO PE053-DT-CTL (PE053-DT-INDEX).          
00507      MOVE PE053-RD-DETAIL-LINE6                                           
00508                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00509 *                                                                         
00510      SET PE053-DT-INDEX UP BY 1.                                          
00511      ADD +1                    TO PE053-PV-LINES-PER-PAGE.                
00512      MOVE PE053-PC-SPACE1      TO PE053-DT-CTL (PE053-DT-INDEX).          
00513      MOVE PE053-RD-DETAIL-LINE7                                           
00514                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00515 *                                                                         
00516      SET PE053-DT-INDEX UP BY 1.                                          
00517      ADD +1                    TO PE053-PV-LINES-PER-PAGE.                
00518      MOVE PE053-PC-SPACE1      TO PE053-DT-CTL (PE053-DT-INDEX).          
00519      MOVE PE053-RD-DETAIL-LINE8                                           
00520                                TO PE053-DT-DATA (PE053-DT-INDEX).         
00521 *                                                                         
00522   EJECT                                                                   
00523  PE003-F100-GET-HEADER-INFO.                                              
00524 ******************************************************************        
00525 *   PE003-F100-GET-HEADER-INFO - GET PERM HEADER INFO TSTRPPC FOR         
00526 *   THE DOCUMENT.                                                         
00527 *                                                                         
00528 *   PERFORMED BY : PE003-B000-HEADER-PROCESSING                           
00529 ******************************************************************        
00530      EXEC SQL                                                             
00531         SELECT RSN_CDE,                                                   
00532                TYP_CDE,                                                   
00533                DEPT_AREA_DEST,                                            
00534                CRE_DTE,                                                   
00535                CRE_ID                                                     
00536         INTO  :STRPPC-RSN-CDE,                                            
00537               :STRPPC-TYP-CDE,                                            
00538               :STRPPC-DEPT-AREA-DEST,                                     
00539               :STRPPC-CRE-DTE,                                            
00540               :STRPPC-CRE-ID                                              
00541         FROM TSTRPPC                                                      
00543         WHERE (DEST_LOC_NBR = :PE053-PV-STORE-TO-PROCESS)    AND          
00544               (DOC_NBR      = :PE053-PV-DOCUMENT-TO-PROCESS) AND          
00545               (DOC_TYP_CDE  = :PE053-PC-DOC-TYPE-CDE)                     
233646        WITH UR                                                           
00546      END-EXEC.                                                            
00547                                                                           
00548      IF SQLCODE = +0                                                      
00549          SET PE053-PS-HEADER-FOUND     TO TRUE                            
00550      ELSE                                                                 
00551          IF SQLCODE = PE053-PC-NOT-FOUND                                  
00552              SET PE053-PS-NO-HEADER-FOUND  TO TRUE                        
00553          ELSE                                                             
00554             SET PE053-PS-ABEND-ERROR TO TRUE                              
00555             MOVE +4013        TO PE053-PV-ABEND-CODE                      
00556             MOVE SQLCODE      TO PE053-PV-ABEND-SQLCODE                   
00557             MOVE 'BAD GET HEADER CALL'                                    
00558                               TO PE053-PV-ABEND-MESSAGE                   
00559             MOVE 'PE003-F100-GET-HEADER-INFO'                             
00560                               TO PE053-PV-ABEND-PARAGRAPH                 
00561           END-IF                                                          
00562      END-IF.                                                              
00563   EJECT                                                                   
00564  PE003-F200-FETCH-DETAIL.                                                 
00565 ******************************************************************        
00566 *   PE003-F200-FETCH-DETAIL - FETCHES THE DETAIL ROWS FOR THE IN-         
00567 *   STORE DOC FROM TSPPCDT.                                               
00568 *                                                                         
00569 *     PERFORMED BY : PE003-C000-DETAIL-PROCESSING                         
00570 ******************************************************************        
00570 ******************************************************************        
00571      EXEC SQL                                                             
00572          FETCH DETAIL_CSR                                                 
00573          INTO :SPPCDT-ROW-NBR,                                            
W24472              :SKXREF-DEPT-NBR,                                           
W24472              :SKXREF-CL-NBR,                                             
W24472              :SPPCDT-SKU-NBR,                                            
00577               :SPPCDT-ACTL-PR-CHG-QTY,                                    
00578               :SPPCDT-OLD-PR,                                             
00579               :SPPCDT-NEW-PR,                                             
00580               :SPPCDT-UPC-NBR                                             
00581      END-EXEC.                                                            
00582                                                                           
00583      EVALUATE TRUE                                                        
00584          WHEN SQLCODE = 0                                                 
00585             CONTINUE                                                      
00586          WHEN SQLCODE = +100                                              
00587             SET PE053-PS-END-OF-DETAIL TO TRUE                            
00588          WHEN OTHER                                                       
00589             SET PE053-PS-ABEND-ERROR TO TRUE                              
00590             MOVE +4013               TO PE053-PV-ABEND-CODE               
00591             MOVE SQLCODE             TO PE053-PV-ABEND-SQLCODE            
00592             MOVE 'BAD GET DETAIL CALL'                                    
00593                                      TO PE053-PV-ABEND-MESSAGE            
00594             MOVE 'PE003-F200-FETCH-DETAIL'                                
00595                                      TO PE053-PV-ABEND-PARAGRAPH          
00596      END-EVALUATE.                                                        
00597                                                                           
00598   EJECT                                                                   
00599  PE003-F600-OPEN-DETAIL-CURSOR.                                           
00600 ******************************************************************        
00601 *   PE003-F600-OPEN-DETAIL-CURSOR - OPENS THE CURSOR FOR THE              
00602 *   DETAIL INFORMATION.                                                   
00603 *                                                                         
00604 *    PERFORMED BY:  PE003-C000-DETAIL-PROCESSING                          
00605 ******************************************************************        
W24472     MOVE PE053-PV-STORE-TO-PROCESS    TO SPPCDT-DEST-LOC-NBR             
W24472     MOVE PE053-PV-DOCUMENT-TO-PROCESS TO SPPCDT-DOC-NBR                  
00606      EXEC SQL                                                             
00607          OPEN DETAIL_CSR                                                  
00608      END-EXEC.                                                            
00609                                                                           
00610      EVALUATE TRUE                                                        
00611          WHEN SQLCODE = ZEROS                                             
00612              CONTINUE                                                     
00613          WHEN OTHER                                                       
00614             SET PE053-PS-ABEND-ERROR TO TRUE                              
00615             MOVE +4013        TO PE053-PV-ABEND-CODE                      
00616             MOVE SQLCODE      TO PE053-PV-ABEND-SQLCODE                   
00617             MOVE 'OPEN DETAIL CURSOR'                                     
00618                               TO PE053-PV-ABEND-MESSAGE                   
00619             MOVE 'PE003-F600-OPEN-DETAIL-CURSOR'                          
00620                               TO PE053-PV-ABEND-PARAGRAPH                 
00621      END-EVALUATE.                                                        
00622      EJECT                                                                
00623                                                                           
00624  PE003-F700-CLOSE-DETAIL-CURSOR.                                          
00625 ******************************************************************        
00626 *   PE003-F700-CLOSE-DETAIL-CURSOR - CLOSES THE CURSOR FOR THE            
00627 *   DETAIL INFORMATION.                                                   
00628 *                                                                         
00629 *    PERFORMED BY:  PE003-C000-DETAIL-PROCESSING                          
00630 ******************************************************************        
00631      EXEC SQL                                                             
00632          CLOSE DETAIL_CSR                                                 
00633      END-EXEC                                                             
00634                                                                           
00635      EVALUATE TRUE                                                        
00636          WHEN SQLCODE = ZEROS                                             
00637              CONTINUE                                                     
00638          WHEN OTHER                                                       
00639             SET PE053-PS-ABEND-ERROR TO TRUE                              
00640             MOVE +4013        TO PE053-PV-ABEND-CODE                      
00641             MOVE SQLCODE      TO PE053-PV-ABEND-SQLCODE                   
00642             MOVE 'CLOSE DETAIL CURSOR'                                    
00643                               TO PE053-PV-ABEND-MESSAGE                   
00644             MOVE 'PE003-F700-CLOSE-DETAIL-CURSOR'                         
00645                               TO PE053-PV-ABEND-PARAGRAPH                 
00646      END-EVALUATE.                                                        
