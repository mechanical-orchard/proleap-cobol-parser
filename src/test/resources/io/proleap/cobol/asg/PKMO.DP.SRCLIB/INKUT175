       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT175.                                       
       AUTHOR.                  J SEIDLER.                                      
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            09/28/98.                                       
       DATE-COMPILED.                                                           
      *                                                                         
      ******************************************************************        
      *  THIS PROGRAM WILL DO THE BALANCING OF THE TOTALS EXPECTED              
      *  FROM THE STORE AND WHAT WAS ACTUALLY TRANSMITTED VIA PROCOMM.          
      *  THIS PROCESSES THE STORE-TO-STORE TRANSFER DATA                        
      *                                                                         
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
25248 * 25248    12-09-2003  RGIS TRANSFER USING NEW MLWS003.                   
25248 *                      -ROD JANSSEN                                       
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT IN-STR-TO-STR-XFER                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-TOTALS-FILE                                               
               ASSIGN TO OUT01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
       FD  IN-STR-TO-STR-XFER                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  IN-STR-TO-STR-REC              PIC X(80).                            
      *                                                                         
      *                                                                         
       FD  OUT-TOTALS-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  OUT-TOTALS-REC                 PIC X(133).                           
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  CD-CURRENT-DATE.                                                     
           05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.                 
           05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.                 
                                                                                
       01  CT-CURRENT-TIME.                                                     
           05  CT-CURRENT-HOUR         PIC 9(2)     VALUE ZERO.                 
           05  CT-CURRENT-MINUTE       PIC 9(2)     VALUE ZERO.                 
           05  FILLER                  PIC 9(4)     VALUE ZERO.                 
                                                                                
       01  WS-STORE-AREA.                                                       
25248          10  WS-STR-NBR          PIC X(4).                                
               10  WS-STR-EXT-PRICE    PIC 9(10)V99.                            
               10  WS-STR-QTY          PIC 9(07).                               
                                                                                
       01  PS-PROGRAM-SUBSTRIPTS.                                               
           05  STR-SUB                    PIC 9(04)       VALUE ZERO.           
           05  STR-SUB-X REDEFINES                                              
               STR-SUB                    PIC X(04).                            
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-XFER-FILE-SW           PIC X(01)   VALUE 'N'.          
               88  PS-END-OF-XFER-FILE                      VALUE 'Y'.          
               88  PS-NOT-END-OF-XFER-FILE                  VALUE 'N'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-LINE-COUNT           PIC S9(4)    VALUE +59                   
                                       COMP SYNC.                               
           05  PV-PAGE-SIZE            PIC S9(4)    VALUE +58                   
                                       COMP SYNC.                               
           05  PV-PAGE-COUNT           PIC S9(5)    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-PROGRAM-NAME            PIC X(08)   VALUE 'INKUT175'.         
           05  PV-PARAGRAPH-NAME          PIC X(30)   VALUE SPACE.              
           05  PV-ABEND-MESSAGE           PIC X(75)   VALUE SPACE.              
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)   VALUE SPACE.              
           05  PV-RETRY-COUNT             PIC S9(04)  VALUE +0                  
                                                      COMP.                     
                                                                                
           05  PV-EXT-PRICE               PIC 9(10)V99 VALUE ZERO.              
           05  PV-CUM-EXT-PRICE           PIC 9(10)V99 VALUE ZERO.              
           05  PV-CUM-QTY                 PIC 9(07)    VALUE ZERO.              
           05  PV-CURRENT-DATE            PIC X(10)   VALUE SPACE.              
                                                                                
           05  PV-CURRENT-YEAR            PIC X(04)   VALUE SPACE.              
                                                                                
           05  PV-PREV-INV-REC            PIC X(48)   VALUE SPACE.              
W26603     05  PV-FROM-STORE              PIC X(04)  VALUE SPACES.              
W26603     05  PV-TO-STORE                PIC X(04)  VALUE SPACES.              
                                                                                
       01  ABEND-CODE                     PIC S9(04)  COMP SYNC                 
                                                      VALUE ZERO.               
                                                                                
      ******************************************************************        
      * WORKING STORAGE AREA FOR THE STORE-TO-STORE XFER FILE                   
      ******************************************************************        
                                                                                
           COPY MLWS003.                                                        
                                                                                
      ******************************************************************        
      * KOHLS STANDARD REPORT HEADING                                           
      ******************************************************************        
                                                                                
       01  DL-REPORT.                                                           
25248      05  DL-FROM-STR-NBR            PIC X(04)   VALUE SPACE.              
           05  FILLER                     PIC X(09)   VALUE SPACE.              
25248      05  DL-TO-STR-NBR              PIC X(04)   VALUE SPACE.              
           05  FILLER                     PIC X(03)   VALUE SPACE.              
           05  DL-QTY                     PIC Z,ZZZ,ZZZ VALUE SPACE.            
           05  FILLER                     PIC X(03)   VALUE SPACE.              
           05  DL-EXT-PRICE          PIC $Z,ZZZ,ZZZ,ZZZ.99 VALUE SPACE.         
                                                                                
       01  DL-REPORT-DASH.                                                      
           05  FILLER                     PIC X(50)   VALUE ALL '-'.            
                                                                                
       01  HL-REPORT-HEADING.                                                   
           05  FILLER                     PIC X(08)   VALUE 'FROM STR'.         
           05  FILLER                     PIC X(02)   VALUE SPACE.              
           05  FILLER                     PIC X(06)   VALUE 'TO STR'.           
           05  FILLER                     PIC X(02)   VALUE SPACE.              
           05  HL-QTY                     PIC X(3)    VALUE 'QTY'.              
           05  FILLER                     PIC X(10)   VALUE SPACE.              
           05  HL-EXT-PRICE               PIC X(09)   VALUE 'EXT PRICE'.        
                                                                                
      ******************************************************************        
      * KOHLS STANDARD REPORT HEADING                                           
      ******************************************************************        
           COPY DPWS132O.                                                       
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      *                                                                         
      ******************************************************************        
      * MAIN MODULE                                                             
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 7000-READ-XFER-DATA-FILE.                                    
           IF PS-END-OF-XFER-FILE                                               
               NEXT SENTENCE                                                    
           ELSE                                                                 
W26603         MOVE ML003-FROM-STORE TO PV-FROM-STORE                           
W26603         MOVE ML003-TO-STORE   TO PV-TO-STORE                             
               PERFORM 2000-PROCESS-XFER-FILE                                   
                   UNTIL PS-END-OF-XFER-FILE.                                   
                                                                                
           PERFORM 8100-WRITE-VALID-INV-DATA-FILE                               
                                                                                
           CLOSE IN-STR-TO-STR-XFER                                             
                 OUT-TOTALS-FILE.                                               
                                                                                
           GOBACK.                                                              
      *                                                                         
      ******************************************************************        
      * OPEN THE FILES USED IN THE PROGRAM AND PERFORM SOME INITIAL             
      * FILE CHECKING.                                                          
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT IN-STR-TO-STR-XFER                                        
                OUTPUT OUT-TOTALS-FILE.                                         
                                                                                
           ACCEPT CD-CURRENT-DATE FROM DATE.                                    
           MOVE CD-CURRENT-YEAR TO DP132O-RUN-YEAR.                             
           MOVE CD-CURRENT-MONTH TO DP132O-RUN-MONTH.                           
           MOVE CD-CURRENT-DAY TO DP132O-RUN-DAY.                               
           ACCEPT CT-CURRENT-TIME FROM TIME.                                    
           MOVE CT-CURRENT-HOUR TO DP132O-RUN-HOUR.                             
           MOVE CT-CURRENT-MINUTE TO DP132O-RUN-MINUTE.                         
           MOVE 'INKUT175' TO DP132O-PROGRAM-NAME.                              
           MOVE 1 TO DP132O-REPORT-NUMBER.                                      
           MOVE PV-PAGE-SIZE TO PV-LINE-COUNT.                                  
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
                                                                                
           WRITE OUT-TOTALS-REC                                                 
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE OUT-TOTALS-REC                                                 
               FROM HL-REPORT-HEADING                                           
               AFTER ADVANCING 2.                                               
           WRITE OUT-TOTALS-REC                                                 
               FROM DL-REPORT-DASH.                                             
                                                                                
           MOVE 4 TO PV-PAGE-COUNT.                                             
                                                                                
      *                                                                         
      ******************************************************************        
      * PROCESS THE FILE OF INV RECORDS                                         
      ******************************************************************        
       2000-PROCESS-XFER-FILE.                                                  
                                                                                
                                                                                
           COMPUTE PV-EXT-PRICE                                                 
               = ML003-RETAIL-PRICE * ML003-UNITS.                              
           ADD  PV-EXT-PRICE      TO PV-CUM-EXT-PRICE.                          
                                                                                
           ADD  ML003-UNITS       TO PV-CUM-QTY.                                
                                                                                
           PERFORM 7000-READ-XFER-DATA-FILE.                                    
      *                                                                         
      ******************************************************************        
      *  READ THE INV DATA FILE.                                       *        
      ******************************************************************        
       7000-READ-XFER-DATA-FILE.                                                
                                                                                
           READ IN-STR-TO-STR-XFER                                              
               INTO ML003-TRANSFER-RECORD                                       
               AT END                                                           
                   SET PS-END-OF-XFER-FILE TO TRUE                              
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *  WRITE A RECORD TO THE OUTPUT FILE.                            *        
      ******************************************************************        
       8100-WRITE-VALID-INV-DATA-FILE.                                          
                                                                                
W26603         MOVE PV-FROM-STORE             TO DL-FROM-STR-NBR.               
W26603         MOVE PV-TO-STORE               TO DL-TO-STR-NBR.                 
               MOVE PV-CUM-EXT-PRICE          TO DL-EXT-PRICE.                  
               MOVE PV-CUM-QTY                TO DL-QTY.                        
               WRITE OUT-TOTALS-REC FROM DL-REPORT                              
               AFTER ADVANCING 2                                                
               ADD 2 TO PV-PAGE-COUNT.                                          
                                                                                
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *                                                                         
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
      *9999-SQL-ABEND.                                                          
      *                                                                         
      *    MOVE +4013                  TO ABEND-CODE.                           
      *    DISPLAY '**  ABEND     **'.                                          
      *    DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
      *    DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
      *    DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
      *    COPY DPPD004.                                                        
      *                                                                         
      *    CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
