      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.      POKUT104.                                               
       AUTHOR.          RYAN STAUFFACHER.                                       
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    02/18/04.                                               
       DATE-COMPILED.                                                           
      ****************************************************************          
      * PROCESS THE NEW STORE REPORT INPUT FILE FROM RMS             *          
      ****************************************************************          
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      ******************************************************************        
PK3075*  09/02/2010    COGNIZANT               CHANGE ID:  PKE3075              
PK3075*  CHANGES TO HANDLE CONDITION WHEN NON PACK LINE NUMBER IS NOT           
PK3075*  FOUND.                                                                 
PK3075******************************************************************        
P5207G*  08/11/2014    PRITHIVI (COGNIZANT)                                     
P5207G*  CHANGES TO HANDLE DUPLICATE SKU'S FOR A PO IN TPURITM TABLE.           
P5207G*                CHANGE ID: CHG0089293   CHANGE TAG: P5207G               
P5207G******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT RMS-PO-FILE                                                   
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT NEW-STORE-PO-FILE                                             
               ASSIGN                       TO OUT01.                           
                                                                                
           EJECT                                                                
      *                                                                         
      *                                                                         
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  RMS-PO-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 29 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS PO-REC.                                               
       01  RMS-PO-REC                       PIC X(29).                          
                                                                                
       FD  NEW-STORE-PO-FILE                                                    
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 19 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS NEW-STORE-PO-REC.                                     
       01  NEW-STORE-PO-REC                 PIC X(19).                          
                                                                                
           EJECT                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01 WS-RMS-PO-REC.                                                        
           05  WS-RMS-PO-NBR                    PIC X(8).                       
           05  WS-RMS-STORE-NBR                 PIC X(4).                       
           05  WS-RMS-SKU-NBR                   PIC X(8).                       
           05  WS-RMS-STR-DISTRO-QTY            PIC X(9).                       
                                                                                
       01 WS-OUTPUT-REC.                                                        
           05  WS-PO-NBR                    PIC S9(9) COMP.                     
           05  WS-STR-NBR                   PIC S9(4) COMP.                     
           05  WS-PO-LNE-NBR                PIC S9(9) COMP.                     
           05  WS-STR-DISTRO-QTY            PIC S9(9) COMP.                     
           05  WS-CHAR5-ABBR-NAME           PIC X(5).                           
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-Y                         PIC X(01) VALUE 'Y'.                
           05  PC-N                         PIC X(01) VALUE 'N'.                
           05  PC-PROGRAM-NAME              PIC X(08) VALUE 'POKUT104'.         
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-RMS-PO-EOF-SW             PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-RMS-PO                        VALUE 'Y'.           
               88  PS-NOT-END-OF-RMS-PO                    VALUE 'N'.           
           05  PS-PACK-PO-SW                PIC X(01)      VALUE 'N'.           
               88  PS-PACK-PO                              VALUE 'Y'.           
               88  PS-NOT-PACK-PO                          VALUE 'N'.           
           05  PS-END-OF-PACK-ITEM-CSR-SW   PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-PK-ITEM-CSR                   VALUE 'Y'.           
               88  PS-NOT-END-OF-PK-ITEM-CSR               VALUE 'N'.           
           05  PS-STORE-FOUND-SW            PIC X(01)      VALUE 'N'.           
               88  PS-STORE-FOUND                          VALUE 'Y'.           
               88  PS-STORE-NOT-FOUND                      VALUE 'N'.           
PK3075********* PKE3075 - START *****************                               
PK3075     05  PS-BAD-REC-SW                PIC X(01)      VALUE 'N'.           
PK3075         88  PS-BAD-REC-FOUND                        VALUE 'Y'.           
PK3075         88  PS-BAD-REC-NOT-FOUND                    VALUE 'N'.           
PK3075********* PKE3075 - END   *****************                               
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)  VALUE SPACES.            
           05  PV-PACK-COMP-COUNT           PIC S9(9)  COMP VALUE ZERO.         
           05  PV-STORE-COUNT               PIC S9(9)  COMP VALUE ZERO.         
           05  PV-PO-LNE-NBR                PIC S9(9)  COMP VALUE ZERO.         
           05  PV-PO-NUMBER                 PIC X(8).                           
           05  PV-LAST-PO-NBR               PIC X(8).                           
           05  PV-LAST-RMS-SKU              PIC X(8).                           
           05  PV-LAST-RMS-STORE            PIC X(4).                           
           05  PV-STORE-5CHAR               PIC X(5).                           
           05  PV-STORE-DISTRO-QTY          PIC S9(9) COMP.                     
           05  PV-RMS-STR-QTY-N             PIC S9(9) COMP.                     
           05  PV-DB-PO-NBR                 PIC S9(9)  COMP.                    
           05  PV-DB-RMS-SKU-NBR            PIC S9(8)V COMP-3.                  
PK3075********* PKE3075 - START *****************                               
PK3075     05  PV-DESCRIPTION.                                                  
PK3075         10  PV-PACK-LINE-ERRDESC     PIC X(35) VALUE                     
PK3075                   'NON PACK LINE NUMBER NOT FOUND    '.                  
PK3075********* PKE3075 - END   *****************                               
           05  PV-DB-RMS-STORE              PIC S9(4) COMP.                     
           05  PV-PK-CMP-TABLE.                                                 
               10  PV-PK-CMP-ENTRY OCCURS 200 TIMES.                            
                   15  PV-PC-COMP-LINE        PIC  9(08) VALUE ZERO.            
                   15  PV-PC-PREPK-RATO-QTY   PIC  S9(9) COMP                   
                                              VALUE ZERO.                       
           05  PV-STORE-TABLE.                                                  
               10  PV-STORE-TABLE-ENTRY OCCURS 100 TIMES.                       
                   15  PV-ST-STORE-NBR        PIC  X(04) VALUE SPACES.          
                   15  PV-ST-STORE-5CHAR      PIC  X(05) VALUE SPACES.          
      *                                                                         
       01  PV-ABEND-CODE                    PIC S9(04)     VALUE +0             
                                                           COMP SYNC.           
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
      *                                                                         
           COPY DPWS004.                                                        
           COPY DPWS013.                                                        
      *                                                                         
      * PO HEADER TABLE.                                                        
           EXEC SQL                                                             
               INCLUDE TPURCHS                                                  
           END-EXEC.                                                            
      *                                                                         
      * PO LINE ITEM TABLE.                                                     
           EXEC SQL                                                             
               INCLUDE TPURITM                                                  
           END-EXEC.                                                            
      *                                                                         
      * PO PREPACK TABLE.                                                       
           EXEC SQL                                                             
               INCLUDE TPOPRPK                                                  
           END-EXEC.                                                            
      *                                                                         
      * LOCATION TABLE.                                                         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *                                                                         
      * DB2 COMMUNICATION AREA                                                  
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       EJECT                                                                    
      *                                                                         
      * DB2 COMMUNICATION AREA2                                                 
           COPY SQLCA2.                                                         
       EJECT                                                                    
      *                                                                         
      *****************************************************************         
      *    CURSOR TO SELECT PACK ITEMS FOR PACK                       *         
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE PK-ITEM-CSR CURSOR FOR                                  
                SELECT PO_LNE_NBR                                               
                      ,PREPK_RATO_QTY                                           
                  FROM TPURITM ITEM                                             
                      ,TPOPRPK PACK                                             
                 WHERE ITEM.PO_NBR         = :PV-DB-PO-NBR                      
                   AND PACK.PO_NBR         = ITEM.PO_NBR                        
                   AND ITEM.VER_STATUS_CDE = 'A'                                
                   AND PACK.PK_SKU_NBR     = :PV-DB-RMS-SKU-NBR                 
                   AND PACK.PO_PREPK_ID    = ITEM.PO_PREPK_ID                   
           END-EXEC.                                                            
      *                                                                         
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ****************************************************************          
      * A100-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *          
      * PROGRAM.                                                     *          
      ****************************************************************          
      *                                                                         
       A000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM A100-INITIALIZATION.                                         
                                                                                
           PERFORM B000-PROCESS-INPUT                                           
            UNTIL PS-END-OF-RMS-PO.                                             
                                                                                
           PERFORM Z100-END-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
           EJECT                                                                
      *                                                                         
      ****************************************************************          
      * A100-INITIALIZATION OPENS ALL FILES                          *          
      ****************************************************************          
       A100-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  RMS-PO-FILE                                              
                OUTPUT NEW-STORE-PO-FILE.                                       
                                                                                
      ****************************************************************          
      * B000-PROCESS THE INPUT FILE.                                 *          
      ****************************************************************          
       B000-PROCESS-INPUT.                                                      
                                                                                
           READ RMS-PO-FILE                                                     
             AT END                                                             
              SET PS-END-OF-RMS-PO TO TRUE.                                     
                                                                                
           IF PS-END-OF-RMS-PO                                                  
               CONTINUE                                                         
           ELSE                                                                 
               MOVE RMS-PO-REC TO WS-RMS-PO-REC                                 
               IF WS-RMS-PO-NBR = PV-LAST-PO-NBR                                
                 CONTINUE                                                       
               ELSE                                                             
                 PERFORM C100-CHECK-PACK-PO                                     
                 MOVE WS-RMS-PO-NBR TO PV-LAST-PO-NBR                           
                 MOVE ZERO          TO PV-LAST-RMS-SKU                          
               END-IF                                                           
               PERFORM B100-PROCESS-ITEM                                        
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B100-PROCESS-ITEM.                                            *         
      *     PERFORM THE NECESSARY PROCESSING FOR THE ITEM.            *         
      *****************************************************************         
       B100-PROCESS-ITEM.                                                       
           IF WS-RMS-STORE-NBR NOT EQUAL PV-LAST-RMS-STORE                      
              PERFORM B400-GET-STR-CHAR5                                        
           END-IF.                                                              
           MOVE WS-RMS-SKU-NBR        TO PV-DB-RMS-SKU-NBR.                     
           MOVE WS-RMS-STR-DISTRO-QTY TO PV-RMS-STR-QTY-N                       
           IF WS-RMS-SKU-NBR EQUAL PV-LAST-RMS-SKU                              
PK3075********* PKE3075 - START *****************                               
PK3075        IF PS-BAD-REC-NOT-FOUND                                           
PK3075            PERFORM B110-PROCESS-SAME-SKU                                 
PK3075        END-IF                                                            
PK3075********* PKE3075 - END   *****************                               
           ELSE                                                                 
PK3075********* PKE3075 - START *****************                               
PK3075        SET PS-BAD-REC-NOT-FOUND TO TRUE                                  
PK3075********* PKE3075 - END   *****************                               
              MOVE WS-RMS-SKU-NBR TO PV-LAST-RMS-SKU                            
              PERFORM B120-PROCESS-DIFF-SKU                                     
           END-IF.                                                              
      *****************************************************************         
      * B110-PROCESS-SAME-SKU.                                        *         
      *     PERFORM THE NECESSARY PROCESSING WHEN THE SKU ON THE RMS  *         
      *     INPUT RECORD IS THE SAME AS THE PREVIOUS SKU.             *         
      *****************************************************************         
       B110-PROCESS-SAME-SKU.                                                   
           IF PS-PACK-PO                                                        
              PERFORM B300-PROCESS-COMP-TABLE                                   
           ELSE                                                                 
              MOVE WS-RMS-STR-DISTRO-QTY TO PV-STORE-DISTRO-QTY                 
              PERFORM D100-WRITE-OUTPUT                                         
           END-IF.                                                              
      *****************************************************************         
      * B120-PROCESS-DIFF-SKU.                                        *         
      *     PERFORM THE NECESSARY PROCESSING WHEN THE SKU ON THE RMS  *         
      *     INPUT RECORD DIFFERENT THAN THE PREVIOUS SKU.             *         
      *****************************************************************         
       B120-PROCESS-DIFF-SKU.                                                   
           IF PS-PACK-PO                                                        
              INITIALIZE PV-PK-CMP-TABLE                                        
              MOVE 1 TO PV-PACK-COMP-COUNT                                      
              SET PS-NOT-END-OF-PK-ITEM-CSR TO TRUE                             
              PERFORM C200-OPEN-PACK-CSR                                        
              PERFORM B200-PROCESS-PACK-CSR                                     
                 UNTIL PS-END-OF-PK-ITEM-CSR                                    
              PERFORM C220-CLOSE-PACK-CSR                                       
              PERFORM B300-PROCESS-COMP-TABLE                                   
           ELSE                                                                 
              MOVE WS-RMS-STR-DISTRO-QTY TO PV-STORE-DISTRO-QTY                 
              PERFORM C300-GET-NONPK-LNE-NBR                                    
PK3075********* PKE3075 - START *****************                               
PK3075        IF PS-BAD-REC-NOT-FOUND                                           
PK3075            PERFORM D100-WRITE-OUTPUT                                     
PK3075        END-IF                                                            
PK3075********* PKE3075 - END   *****************                               
           END-IF.                                                              
      *****************************************************************         
      * B200-PROCESS-PACK-CSR                                         *         
      *     PROCESS THE PACK CURSOR                                   *         
      *****************************************************************         
       B200-PROCESS-PACK-CSR.                                                   
           PERFORM C210-FETCH-PACK-CSR                                          
           IF PS-NOT-END-OF-PK-ITEM-CSR                                         
              MOVE PURITM-PO-LNE-NBR     TO PV-PC-COMP-LINE                     
                                               (PV-PACK-COMP-COUNT)             
              MOVE PURITM-PREPK-RATO-QTY TO PV-PC-PREPK-RATO-QTY                
                                               (PV-PACK-COMP-COUNT)             
           END-IF.                                                              
                                                                                
           ADD 1 TO PV-PACK-COMP-COUNT.                                         
      *****************************************************************         
      * B300-PROCESS-COMP-TABLE                                       *         
      *     PROCESS THE PACK COMPONENT TABLE, CREATING OUTPUT FOR EACH*         
      *     ITEM ON THE PACK.                                         *         
      *****************************************************************         
       B300-PROCESS-COMP-TABLE.                                                 
           MOVE 1 TO PV-PACK-COMP-COUNT                                         
           PERFORM VARYING PV-PACK-COMP-COUNT FROM 1 BY 1                       
                   UNTIL   PV-PC-COMP-LINE(PV-PACK-COMP-COUNT) = ZERO           
                   MOVE PV-PC-COMP-LINE(PV-PACK-COMP-COUNT)                     
                                                     TO PV-PO-LNE-NBR           
                   COMPUTE PV-STORE-DISTRO-QTY EQUAL                            
                            PV-PC-PREPK-RATO-QTY(PV-PACK-COMP-COUNT) *          
                            PV-RMS-STR-QTY-N                                    
                   PERFORM D100-WRITE-OUTPUT                                    
           END-PERFORM.                                                         
      *****************************************************************         
      * B400-GET-STR-CHAR5.                                           *         
      *     GET THE FIVE CHARACTER ABBREVIATION FOR THE STORE.        *         
      *****************************************************************         
       B400-GET-STR-CHAR5.                                                      
           SET PS-STORE-NOT-FOUND     TO TRUE.                                  
           PERFORM VARYING PV-STORE-COUNT FROM 1 BY 1                           
                   UNTIL   PV-ST-STORE-NBR(PV-STORE-COUNT) = SPACES             
                   IF WS-RMS-STORE-NBR = PV-ST-STORE-NBR(PV-STORE-COUNT)        
                      MOVE PV-ST-STORE-5CHAR(PV-STORE-COUNT) TO                 
                                          PV-STORE-5CHAR                        
                      SET PS-STORE-FOUND TO TRUE                                
                   END-IF                                                       
           END-PERFORM.                                                         
           IF PS-STORE-NOT-FOUND                                                
              PERFORM C400-GET-STR5-DB                                          
              MOVE WS-RMS-STORE-NBR TO PV-ST-STORE-NBR(PV-STORE-COUNT)          
              MOVE XMT00001-LOC-5-ABBR-NM                                       
                                    TO PV-ST-STORE-5CHAR(PV-STORE-COUNT)        
                                       PV-STORE-5CHAR                           
           END-IF.                                                              
           MOVE WS-RMS-STORE-NBR TO PV-LAST-RMS-STORE.                          
      *****************************************************************         
      * C100-CHECK-PACK-PO.                                           *         
      *     CHECK TO SEE IF THE PURCHASE ORDER IS A PREPACK PURCHASE  *         
      *     ORDER.                                                    *         
      *****************************************************************         
       C100-CHECK-PACK-PO.                                                      
           MOVE 'C100-CHECK-PACK-PO' TO PV-PARAGRAPH-NAME.                      
           MOVE  WS-RMS-PO-NBR       TO PV-DB-PO-NBR.                           
           SET PS-NOT-PACK-PO TO TRUE.                                          
           EXEC SQL                                                             
               SELECT   'Y'                                                     
                  INTO :PS-PACK-PO-SW                                           
                  FROM TPURCHS                                                  
                 WHERE PO_NBR         = :PV-DB-PO-NBR                           
                   AND VER_STATUS_CDE = 'A'                                     
                   AND PKGNG_CDE      IN ('M','I')                              
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
               WHEN SQLCODE = 100                                               
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE 'CHECK FOR PACK PO'                                    
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    MOVE 'C100-CHECK-PACK-PO'                                   
                      TO PV-PARAGRAPH-NAME                                      
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * C200-OPEN-PACK-CSR.                                           *         
      *     OPEN THE PACK CURSOR.                                     *         
      *****************************************************************         
       C200-OPEN-PACK-CSR.                                                      
           MOVE 'C200-OPEN-PACK-CSR' TO PV-PARAGRAPH-NAME.                      
           EXEC SQL                                                             
               OPEN PK-ITEM-CSR                                                 
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE 'OPEN PACK CURSOR'                                     
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * C210-FETCH-PACK-CSR.                                          *         
      *     FETCH THE PACK CURSOR.                                    *         
      *****************************************************************         
       C210-FETCH-PACK-CSR.                                                     
           MOVE 'C210-FETCH-PACK-CSR' TO PV-PARAGRAPH-NAME.                     
           EXEC SQL                                                             
               FETCH PK-ITEM-CSR                                                
                INTO :PURITM-PO-LNE-NBR                                         
                    ,:PURITM-PREPK-RATO-QTY                                     
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE = 100                                               
                   SET PS-END-OF-PK-ITEM-CSR TO TRUE                            
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE 'FETCH PACK CURSOR'                                    
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * C220-CLOSE-PACK-CSR.                                          *         
      *     CLOSE THE PACK CURSOR.                                    *         
      *****************************************************************         
       C220-CLOSE-PACK-CSR.                                                     
           MOVE 'C220-CLOSE-PACK-CSR' TO PV-PARAGRAPH-NAME.                     
           EXEC SQL                                                             
               CLOSE PK-ITEM-CSR                                                
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE 'CLOSE PACK CURSOR'                                    
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * C300-GET-NONPK-LNE-NBR.                                       *         
      *     PERFORM THE NECESSARY PROCESSING FOR THE ITEM.            *         
      *****************************************************************         
       C300-GET-NONPK-LNE-NBR.                                                  
           MOVE 'C300-GET-NONPK-LNE-NBR' TO PV-PARAGRAPH-NAME.                  
           EXEC SQL                                                             
               SELECT  PO_LNE_NBR                                               
                  INTO :PV-PO-LNE-NBR                                           
                  FROM TPURITM                                                  
                 WHERE PO_NBR         = :PV-DB-PO-NBR                           
                   AND VER_STATUS_CDE = 'A'                                     
                   AND RMS_PO_SKU_NBR = :PV-DB-RMS-SKU-NBR                      
P5207G             AND  PO_PREPK_ID = 0                                         
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
PK3075********* PKE3075 - START *****************                               
PK3075         WHEN SQLCODE = 100                                               
PK3075              DISPLAY PV-PACK-LINE-ERRDESC                                
PK3075              DISPLAY 'PO NUMBER: 'PV-DB-PO-NBR                           
PK3075              DISPLAY 'SKU NUMBER: ' PV-DB-RMS-SKU-NBR                    
PK3075              SET PS-BAD-REC-FOUND   TO TRUE                              
PK3075********* PKE3075 - END   *****************                               
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
PK3075********* PKE3075 - START *****************                               
PK3075         WHEN SQLCODE > ZERO AND                                          
PK3075              SQLCODE NOT = 100                                           
PK3075********* PKE3075 - END   *****************                               
                    MOVE 'GET LINE NBR FOR NON PACK LINE'                       
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * C400-GET-STR5-DB.                                             *         
      *     GET THE FIVE CHARACTER ABBREVIATION FOR THE STORE.        *         
      *****************************************************************         
       C400-GET-STR5-DB.                                                        
           MOVE 'C400-GET-STR5-DB' TO PV-PARAGRAPH-NAME.                        
           MOVE WS-RMS-STORE-NBR   TO PV-DB-RMS-STORE                           
           EXEC SQL                                                             
               SELECT  LOC_5_ABBR_NM                                            
                  INTO :XMT00001-LOC-5-ABBR-NM                                  
                  FROM XMT_LOC                                                  
                 WHERE LOC_NBR        = :PV-DB-RMS-STORE                        
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                    MOVE 'GET 5 CHAR STORE ABBREVIATION'                        
                      TO PV-DB2-OPERATION-ATTEMPTED                             
                    PERFORM 9900-DB2-ABEND-ROUTINE                              
           END-EVALUATE.                                                        
           EJECT                                                                
      *****************************************************************         
      * D100-WRITE-OUTPUT                                             *         
      *****************************************************************         
       D100-WRITE-OUTPUT.                                                       
           MOVE WS-RMS-PO-NBR         TO WS-PO-NBR.                             
           MOVE WS-RMS-STORE-NBR      TO WS-STR-NBR.                            
           MOVE PV-PO-LNE-NBR         TO WS-PO-LNE-NBR.                         
           MOVE PV-STORE-DISTRO-QTY   TO WS-STR-DISTRO-QTY.                     
           MOVE PV-STORE-5CHAR        TO WS-CHAR5-ABBR-NAME.                    
                                                                                
           WRITE NEW-STORE-PO-REC                                               
                FROM WS-OUTPUT-REC.                                             
      *****************************************************************         
      * Z100-END-ROUTINE                                              *         
      *****************************************************************         
       Z100-END-ROUTINE.                                                        
           CLOSE RMS-PO-FILE                                                    
                 NEW-STORE-PO-FILE.                                             
                                                                                
      *                                                                         
      ******************************************************************        
      *     SQL WARNING OR ERROR PROCESSING                            *        
      *                                                                *        
      ******************************************************************        
       9900-DB2-ABEND-ROUTINE.                                                  
           COPY DPPD004.                                                        
      *                                                                         
            SET ABEND-4013-DB2-ERROR TO TRUE.                                   
            DISPLAY  '*************** ABEND *********************'.             
            DISPLAY  'PURCHASE ORDER = ' WS-RMS-PO-NBR                          
            DISPLAY  'STORE NUMBER   = ' WS-RMS-STORE-NBR                       
            DISPLAY  'SKU            = ' WS-RMS-SKU-NBR                         
            DISPLAY  'PROGRAM   = ' PC-PROGRAM-NAME.                            
            DISPLAY  'PARAGRAPH = ' PV-PARAGRAPH-NAME.                          
            DISPLAY  'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                 
                                                                                
            CALL 'ILBOABN0'  USING PV-ABEND-CODE.                               
      *                                                                         
      ******************************************************************        
