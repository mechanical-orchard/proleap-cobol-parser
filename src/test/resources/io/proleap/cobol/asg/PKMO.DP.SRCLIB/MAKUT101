       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      MAKUT101.                                               
       AUTHOR.          SCOTT CIESIELCZYK                                       
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    12/02/96.                                               
       DATE-COMPILED.                                                           
      ****************************************************************          
      * THIS PROGRAM WILL QUERY THE SKU LOG TABLE (TSKULOG) AND      *          
      * CREATE A SEQUENTIAL FILE FOR A SPECIFIED PERIOD OF TIME.     *          
      ****************************************************************          
      * 001 SCOTT CIESIELCZYK 12/02/96                               *          
      *   - INITIAL DEVELOPMENT                                      *          
      * 002 SCOTT CIESIELCZYK 06/16/97                               *          
      *   - ADDED CODE TO MAKE THIS PROGRAM READ ONLY.               *          
      *   - A NEW OUTPUT FILE WILL BE CREATED WITH THE UPDATE SQL    *          
      *     TO UPDATE THE TRPLCTL TABLE.  THIS SQL WILL BE RUN       *          
      *     AFTER THE ITEM BRIDGE PROGRAM RUNS, SO THAT IF THEIR     *          
      *     ARE ANY ABENDS IN THE ITEM BRIDGE THIS PROGRAM CAN       *          
      *     SIMPLY BE RERUN.                                         *          
      * SAC003                                                       *          
      *     SCOTT CIESIELCZYK 08/11/97                               *          
      *   - ADDED AN INCLUDE FILE TO HOLD THE TSKURNMBR FILE LAYOUT. *          
      *     MARD025 (MA025).                                         *          
      * AS001                                                        *          
      *     ANN SUN  05/20/1998                                      *          
      *   - MODIFIED THE CODE DUE TO THE RNMBR_DATE  AND RNMBR_TIME  *          
      *     CHANGED UNDER TSKURNMBR TABLE. ALSO CHANGED THE MARD025  *          
      *     LAYOUT.                                                             
      * WR21120                                                      *          
      *     LINDA GRIESBACH 04/14/2003                               *          
      *   - CHANGES CODE SO THAT IT NO LONGER POPULATES THE FSRD500I *          
      *     PROGRAM-NAME, TSK-DEPT, TSK-CLASS, TSK-SEQ, USER-ID,     *          
      *     AND TERMINAL-ID FIELDS.  THE TSKULOG FETCH WILL NOW      *          
      *     POPULATE THE REMAINING FIELDS AS WELL AS INTR-UPC-ID.    *          
      ****************************************************************          
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT CONTROL-FILE                                                  
               ASSIGN TO CCFILE.                                                
      *                                                                         
           SELECT SKU-LOG-EXTRACT-FILE                                          
               ASSIGN TO XSKULOG.                                               
      *                                                                         
           SELECT SKU-REN-EXTRACT-FILE                                          
               ASSIGN TO XSKUREN.                                               
      *                                                                         
SAC002     SELECT RPL-CTL-UPDATE-FILE                                           
SAC002         ASSIGN TO UPDTCTL.                                               
      *                                                                         
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-RECORD                  PIC  X(80).                          
      *                                                                         
      *                                                                         
       FD  SKU-LOG-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
21120  01  SKU-LOG-EXTRACT-REC             PIC  X(182).                         
      *                                                                         
      *                                                                         
       FD  SKU-REN-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-REN-EXTRACT-REC             PIC  X(37).                          
      *                                                                         
      *                                                                         
       FD  RPL-CTL-UPDATE-FILE                                                  
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RPL-CTL-UPDATE-REC              PIC  X(80).                          
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO             
                                                         COMP SYNC.             
           88  ABEND-4001                                VALUE +4001.           
           88  ABEND-4002                                VALUE +4002.           
           88  ABEND-4019                                VALUE +4019.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
21120      05  PV-INTR-UPC-ID-1       PIC S9(10)     COMP-3.                    
21120      05  PV-INTR-UPC-ID-2       PIC 9(10).                                
           05  PV-INPUT-DTE           PIC X(10)      VALUE SPACES.              
           05  PV-INITIAL-BEG-TIME        PIC X(26)      VALUE                  
                                   '0000-00-00-00.00.00.000000'.                
           05  PV-INITIAL-END-TIME        PIC X(26)      VALUE                  
                                   '0000-00-00-23.59.59.000000'.                
           05  PV-DB2-FMT-TMST.                                                 
               10  PV-DB2-CC          PIC 9(02)    VALUE ZEROES.                
               10  PV-DB2-YY          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-MM          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-DD          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-HH          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-MN          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-SS          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  PV-DB2-ZEROS       PIC 9(06)    VALUE ZEROES.                
           05  FILLER  REDEFINES PV-DB2-FMT-TMST.                               
               10  PV-DB2-FMT-DATE         PIC X(10).                           
               10  PV-DB2-FMT-TIME         PIC X(16).                           
           05  PV-CNV-TIME2NUM-T.                                               
               10  PV-T2N-HH-T             PIC X(02)    VALUE ZEROES.           
AS001          10  FILLER                  PIC X(01)    VALUE ':'.              
               10  PV-T2N-MN-T             PIC X(02)    VALUE ZEROES.           
AS001          10  FILLER                  PIC X(01)    VALUE ':'.              
               10  PV-T2N-SS-T             PIC X(02)    VALUE ZEROES.           
           05  FILLER REDEFINES PV-CNV-TIME2NUM-T.                              
               10  PV-CNV-TIME2NUM-N       PIC 9(08).                           
           05  PV-REC-COUNT                PIC  9(10)    VALUE ZERO.            
           05  PV-SQL-SUB                  PIC S9(04)    COMP.                  
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.           
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)    VALUE SPACE.           
           05  PV-PRINT-LINE               PIC  X(80)    VALUE SPACE.           
AS001 * ADDED THE VARIABLES TO HOLD THE SKU AND HIST SKU INFORMATION.           
AS001      05  PV-SKU-NMBR.                                                     
AS001          10  PV-DEPT-NMBR            PIC  X(03)    VALUE SPACES.          
AS001          10  PV-CLASS-NMBR           PIC  X(02)    VALUE SPACES.          
AS001          10  PV-SEQ-NMBR             PIC  X(03)    VALUE SPACES.          
AS001      05  PV-HIST-SKU-NMBR.                                                
AS001          10  PV-HIST-DEPT-NMBR       PIC  X(03)    VALUE SPACES.          
AS001          10  PV-HIST-CLASS-NMBR      PIC  X(02)    VALUE SPACES.          
AS001          10  PV-HIST-SEQ-NMBR        PIC  X(03)    VALUE SPACES.          
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)     VALUE                 
                                                          'MAKUT101'.           
           05  PC-APPL-ID                  PIC  X(02)     VALUE                 
                                                          'MA'.                 
           05  PC-EXTR-PRC-ID              PIC  X(08)     VALUE                 
                                                          'MAKUT101'.           
           05  PC-UNLD-PRC-ID              PIC  X(08)     VALUE                 
                                                          'MAKUT103'.           
SAC002     05  PC-TBL-OWNER-ID             PIC  X(08)     VALUE                 
                                                          'DB2PDBA.'.           
SAC002     05  PC-SINGLE-QUOTE             PIC  X(01)     VALUE                 
                                                          ''''.                 
           05  PC-LOW-DB2-TMST             PIC  X(26)                           
                  VALUE '0001-01-01-00.00.00.000000'.                           
           05  PC-F4                       PIC  X(02)    VALUE 'F4'.            
           05  PC-ONE                      PIC  9(02)    VALUE  1 COMP.         
           05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3 COMP.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-SKU-LOG-CSR-SW       PIC  X(01)    VALUE 'N'.             
               88  PS-END-SKU-LOG-CSR                    VALUE 'Y'.             
           05  PS-END-SKU-REN-CSR-SW       PIC  X(01)    VALUE 'N'.             
               88  PS-END-SKU-REN-CSR                    VALUE 'Y'.             
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-MODE-IND            PIC X(01).                                
           05  CC-LOG-TBL-NME         PIC X(10).                                
           05  CC-BEG-TMST.                                                     
               10  CC-BEG-CC          PIC 9(02)    VALUE ZEROES.                
               10  CC-BEG-YY          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-MM          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-DD          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-HH          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-MN          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-SS          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-BEG-ZEROS       PIC 9(06)    VALUE ZEROES.                
           05  FILLER             PIC X(01).                                    
           05  CC-END-TMST.                                                     
               10  CC-END-CC          PIC 9(02)    VALUE ZEROES.                
               10  CC-END-YY          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-MM          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-DD          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-HH          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-MN          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-SS          PIC 9(02)    VALUE ZEROES.                
               10  FILLER             PIC X(01).                                
               10  CC-END-ZEROS       PIC 9(06)    VALUE ZEROES.                
           05  FILLER             PIC X(16).                                    
      *                                                                         
00138  01  CC-CONTROL-CARD2.                                                    
00139      05  CC-PGMNAME                   PIC X(08)    VALUE SPACES.          
00140      05  FILLER                       PIC X(1)     VALUE SPACE.           
00141      05  CC-RUN-TYPE                  PIC X(07).                          
00147      05  FILLER                       PIC X(1)     VALUE SPACE.           
00148      05  CC-INPUT-DATA-DATE                        VALUE SPACES.          
00149          10  CC-INPDD-YYYY            PIC X(4).                           
00150          10  CC-INPDD-HYP1            PIC X(1).                           
00151          10  CC-INPDD-MM              PIC X(2).                           
00152          10  CC-INPDD-HYP2            PIC X(1).                           
00153          10  CC-INPDD-DD              PIC X(2).                           
00154      05  FILLER                       PIC X(53)    VALUE SPACES.          
00155 *                                                                         
      *                                                                         
      ******************************************************************        
      *  RECORD LAYOUT FOR THE SKU LOG EXTRACT OUTPUT FILE.                     
      ******************************************************************        
           COPY FSRD500I.                                                       
      *                                                                         
SAC003*******************************************************************       
      *  RECORD LAYOUT FOR THE SKU REN EXTRACT OUTPUT FILE.                     
      ******************************************************************        
           COPY MARD025.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *    WORK AREA FOR THE ABEND ROUTINE.                                     
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *    CALENDAR ROUTINE VARIABLES.                                          
      ******************************************************************        
      *                                                                         
           COPY DPWS500.                                                        
      *                                                                         
AS001 *    COPY DPWS060.                                                        
      *                                                                         
      *                                                                         
       01  SQL-VARIBLES.                                                        
           05  SV-APPL-ID                  PIC  X(02).                          
           05  SV-EXTR-PRC-ID              PIC  X(08).                          
           05  SV-LOG-TBL-NME              PIC  X(10).                          
      ***  YYYY-MM-DD-HH.MN.SS.000000                                           
           05  SV-BEG-TMST                 PIC  X(26).                          
           05  SV-END-TMST                 PIC  X(26).                          
           05  SV-DELAY-TMST               PIC  X(26).                          
      ***  YYJJJ                                                                
AS001      05  SV-BEG-DATE                 PIC  X(10)   VALUE SPACES.           
AS001      05  SV-END-DATE                 PIC  X(10)   VALUE SPACES.           
AS001      05 SV-SAVE-DATE                 PIC  X(10)   VALUE SPACES.           
      ***  HHMMSS                                                               
AS001      05  SV-BEG-TIME                 PIC  X(08)   VALUE SPACES.           
AS001      05  SV-END-TIME                 PIC  X(08)   VALUE SPACES.           
                                                                                
      *----------------------------------------------------------------*        
      *    REPLICATION CONTROL TABLE                                   *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TRPLCTL                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SKU LOG TABLE                                               *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TSKULOG                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SKU RENUMBER TABLE                                          *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               INCLUDE TSKURNMB                                                 
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA                                     *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SKU LOG       CURSOR                                        *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               DECLARE SKU-LOG-CSR CURSOR FOR                                   
               SELECT                                                           
                       TRN_TMST                                                 
21120                 ,INTR_UPC_ID                                              
                      ,FUNC_CDE                                                 
                      ,MSG_SEQ_NBR                                              
                      ,MSG_TXT                                                  
                FROM   TSKULOG                                                  
                WHERE                                                           
                      TRN_TMST >  :SV-BEG-TMST                                  
                  AND TRN_TMST <= :SV-END-TMST                                  
               ORDER BY TRN_TMST                                                
                                                                                
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SKU RENUMBER  CURSOR                                        *        
      *----------------------------------------------------------------*        
      *                                                                         
           EXEC SQL                                                             
               DECLARE SKU-REN-CSR CURSOR FOR                                   
               SELECT                                                           
                   MERCH_DEPT_NMBR                                              
                   ,MERCH_CLASS_NMBR                                            
                   ,SEQ_NMBR                                                    
                   ,RNMBR_DATE                                                  
                   ,RNMBR_TIME                                                  
                   ,HIST_DEPT_NMBR                                              
                   ,HIST_CLASS_NMBR                                             
                   ,HIST_SEQ_NMBR                                               
               FROM TSKURNMBR                                                   
               WHERE ( RNMBR_DATE =  :SV-END-DATE                               
                   AND RNMBR_TIME <= :SV-END-TIME                               
                     ) OR                                                       
                     ( RNMBR_DATE =  :SV-BEG-DATE                               
                   AND RNMBR_TIME >  :SV-BEG-TIME                               
                     ) OR                                                       
                     ( RNMBR_DATE <  :SV-END-DATE                               
                   AND RNMBR_DATE >  :SV-BEG-DATE                               
                     )                                                          
               ORDER BY                                                         
                    RNMBR_DATE                                                  
                   ,RNMBR_TIME                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
      ******************************************************************        
      *  CONTROLS THE ENTIRE FLOW OF THE PROGRAM.                               
      *    -- OPEN FILES                                                        
      *    -- PERFORM MAIN PROCESSING LOOP                                      
      *    -- CLOSE FILES                                                       
      *    -- DISPLAY COUNT AND ENDING TIMESTAMP                                
      *    -- RETURNS CONTROL TO THE SYSTEM                                     
      ******************************************************************        
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           IF CC-LOG-TBL-NME = 'TSKULOG'                                        
           OR CC-LOG-TBL-NME = 'ALL'                                            
              MOVE ZERO TO PV-REC-COUNT                                         
              MOVE 'TSKULOG'      TO SV-LOG-TBL-NME                             
              PERFORM 7000-SET-BEG-END-TMST                                     
              PERFORM 2000-OPEN-SKU-LOG-CSR                                     
              MOVE 'N' TO PS-END-SKU-LOG-CSR-SW                                 
              DISPLAY ' EXTRACTING TSKULOG ROWS...'                             
              PERFORM 3002-FETCH-SKU-LOG-CSR                                    
              PERFORM 3000-PROC-SKU-LOG                                         
                UNTIL PS-END-SKU-LOG-CSR                                        
              PERFORM 6000-CLOSE-LOG-CSR                                        
              DISPLAY '   SKU LOG RECORDS WRITTEN = '                           
                       PV-REC-COUNT                                             
              IF CC-MODE-IND = 'N'                                              
              OR CC-MODE-IND = 'C'                                              
                  PERFORM 7700-UPD-RPLCTL-ROW                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF CC-LOG-TBL-NME = 'TSKURNMBR'                                      
           OR CC-LOG-TBL-NME = 'ALL'                                            
              MOVE ZERO TO PV-REC-COUNT                                         
              MOVE 'TSKURNMBR'    TO SV-LOG-TBL-NME                             
              PERFORM 7000-SET-BEG-END-TMST                                     
              PERFORM 2500-OPEN-SKU-REN-CSR                                     
              MOVE 'N' TO PS-END-SKU-REN-CSR-SW                                 
              DISPLAY ' EXTRACTING TSKURNMBR ROWS...'                           
              PERFORM 5002-FETCH-SKU-REN-CSR                                    
              PERFORM 5000-PROC-SKU-REN                                         
                UNTIL PS-END-SKU-REN-CSR                                        
              PERFORM 6500-CLOSE-REN-CSR                                        
              DISPLAY '   SKU RENUMBER RECORDS WRITTEN = '                      
                       PV-REC-COUNT                                             
              IF CC-MODE-IND = 'N'                                              
              OR CC-MODE-IND = 'C'                                              
                  PERFORM 7700-UPD-RPLCTL-ROW                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           DISPLAY ' CLOSING FILES ...'.                                        
           CLOSE CONTROL-FILE                                                   
                 SKU-LOG-EXTRACT-FILE                                           
                 SKU-REN-EXTRACT-FILE                                           
SAC002           RPL-CTL-UPDATE-FILE.                                           
                                                                                
SAC002*    PERFORM 9000-SQL-COMMIT.                                             
           DISPLAY ' PROCESSING FOR MAKUT101 COMPLETED SUCCESSFULLY. '.         
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'.         
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       1000-INITIALIZE.                                                         
      *     -- OPEN FILES.                                             *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'          
           DISPLAY ' INITIALIZING ' PC-CURRENT-PROGRAM-NAME '...'.              
                                                                                
           OPEN INPUT  CONTROL-FILE                                             
                OUTPUT SKU-LOG-EXTRACT-FILE                                     
                       SKU-REN-EXTRACT-FILE                                     
SAC002                 RPL-CTL-UPDATE-FILE.                                     
                                                                                
           PERFORM 1100-READ-CONTROL-CARD.                                      
                                                                                
           ACCEPT CC-CONTROL-CARD2.                                             
           DISPLAY 'CC-CONTROL-CARD2:' CC-CONTROL-CARD2.                        
                                                                                
           IF NOT (CC-INPDD-YYYY IS NUMERIC                                     
                   AND CC-INPDD-MM IS NUMERIC                                   
                   AND CC-INPDD-DD IS NUMERIC                                   
                   AND CC-INPDD-HYP1 = '-'                                      
                   AND CC-INPDD-HYP2 = '-')                                     
               DISPLAY 'CONTROL CARD INPUT DATA DATE ERROR : '                  
                        CC-CONTROL-CARD2                                        
               SET ABEND-4002             TO TRUE                               
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               MOVE CC-INPUT-DATA-DATE TO PV-INPUT-DTE                          
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *                                                                *        
       1100-READ-CONTROL-CARD.                                                  
      *    READ IN THE CONTROL CARD.                                   *        
      *                                                                *        
      *----------------------------------------------------------------*        
           READ CONTROL-FILE                                                    
                   INTO CC-CONTROL-CARD                                         
           END-READ.                                                            
           DISPLAY '   CONTROL CARD = ' CC-CONTROL-CARD.                        
      *----------------------------------------------------------------*        
      *                                                                *        
       2000-OPEN-SKU-LOG-CSR.                                                   
      *    OPEN THE CURSOR TO EXTRACT ROWS FROM THE TSKULOG TABLE.     *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY ' OPENING THE TSKULOG CURSOR...'.                            
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                  OPEN  SKU-LOG-CSR                                             
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '2000-OPEN-SKU-LOG-CSR'                            
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'OPEN SKU LOG CURSOR'                              
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '2000-OPEN-SKU-LOG-CURSOR'                                  
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *                                                                *        
       2500-OPEN-SKU-REN-CSR.                                                   
      *    OPEN THE CURSOR TO EXTRACT ROWS FROM THE TSKURNMBR TABLE.   *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY ' OPENING THE TSKURNMBR CURSOR...'.                          
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                  OPEN  SKU-REN-CSR                                             
               END-EXEC                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '2500-OPEN-SKU-REN-CSR'                            
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'OPEN SKU RENUMBER CURSOR'                         
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '2500-OPEN-SKU-REN-CURSOR'                                  
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *                                                                *        
       3000-PROC-SKU-LOG.                                                       
      *    READ THE SKU LOG TABLE FOR A SPECIFIED TIME FRAME.          *        
      *    EXTRACT ALL THE ROWS WITHIN THE SPECIFIED TIME FRAME.       *        
      *    FORMAT EACH ROW TO MATCH THE FORMAT EXPECTED BY MAKUT102.   *        
      *    WRITE EACH FORMATTED RECORD TO THE SKU LOG EXTRACT FILE.    *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3001-FORMAT-N-WRITE-LOG-REC.                                 
           PERFORM 3002-FETCH-SKU-LOG-CSR.                                      
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       3001-FORMAT-N-WRITE-LOG-REC.                                             
      *   FORMAT THE SKU LOG DATA AND WRITE IT TO THE SKU LOG FILE.    *        
      *                                                                *        
      *----------------------------------------------------------------*        
           MOVE SPACES          TO FS500I-MAINTENANCE-LOG-RECORD.               
           MOVE SKULOG-TRN-TMST TO PV-DB2-FMT-TMST.                             
           MOVE PV-DB2-YY     TO FS500I-TRANSACTION-YEAR.                       
           MOVE PV-DB2-MM     TO FS500I-TRANSACTION-MONTH.              FSRD500I
           MOVE PV-DB2-DD     TO FS500I-TRANSACTION-DAY.                FSRD500I
           MOVE PV-DB2-HH     TO FS500I-TRANSACTION-HOUR.               FSRD500I
           MOVE PV-DB2-MN     TO FS500I-TRANSACTION-MINUTE.             FSRD500I
           MOVE PV-DB2-SS     TO FS500I-TRANSACTION-SECOND.             FSRD500I
      ***                                                                       
      ***  THE TRANSACTION ID AND TABLE ID ARE USED IN THE SORT/MERGE           
      ***  PROCESS THAT OCCURS AFTER THIS PROGRAM. DO NOT CHANGE!               
      ***                                                                       
           MOVE 'M101'             TO FS500I-TRANSACTION-ID.            FSRD500I
           MOVE 01                 TO FS500I-TABLE-ID.                  FSRD500I
      ***                                                                       
           MOVE SPACES             TO FS500I-TABLE-KEY-VALUE               CL**2
                                      FS500I-USER-ID                    FSRD500I
                                      FS500I-TERMINAL-ID                FSRD500I
                                      FS500I-PROGRAM-NAME.                      
21120      MOVE ZEROS              TO FS500I-TSK-DEPT                   FSRD500I
21120                                 FS500I-TSK-CLASS                  FSRD500I
21120                                 FS500I-TSK-SEQ.                   FSRD500I
21120      MOVE SKULOG-INTR-UPC-ID   TO PV-INTR-UPC-ID-1.                       
21120      MOVE PV-INTR-UPC-ID-1     TO PV-INTR-UPC-ID-2.                       
21120      MOVE PV-INTR-UPC-ID-2     TO FS500I-INTR-UPC-ID1.                    
21120      MOVE SKULOG-INTR-UPC-ID   TO FS500I-INTR-UPC-ID2.                    
           MOVE SKULOG-FUNC-CDE      TO FS500I-FUNCTION-CODE.           FSRD500I
           MOVE SKULOG-MSG-SEQ-NBR   TO FS500I-OUTPUT-MESSAGE-SEQ.      FSRD500I
           MOVE SKULOG-MSG-TXT-TEXT  TO FS500I-OUTPUT-MESSAGE.          FSRD500I
                                                                                
           WRITE SKU-LOG-EXTRACT-REC                                            
                 FROM FS500I-MAINTENANCE-LOG-RECORD.                            
           ADD PC-ONE           TO  PV-REC-COUNT.                               
           MOVE SPACES          TO  DCLTSKULOG.                                 
                                                                                
      *----------------------------------------------------------------*        
      *                                                                         
       3002-FETCH-SKU-LOG-CSR.                                                  
      *   FETCH ONE ROW FROM THE SKU LOG TABLE                         *        
      *                                                                         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               FETCH SKU-LOG-CSR                                                
               INTO                                                             
                     :SKULOG-TRN-TMST                                           
21120              , :SKULOG-INTR-UPC-ID                                        
                   , :SKULOG-FUNC-CDE                                           
                   , :SKULOG-MSG-SEQ-NBR                                        
                   , :SKULOG-MSG-TXT                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    IF PV-REC-COUNT EQUAL ZERO                                  
                        DISPLAY '   THE FIRST TSKULOG ROW RETRIEVED IS'         
                        DISPLAY 'TRN_TMST    = '   SKULOG-TRN-TMST              
21120                   DISPLAY 'INTR_UPC_ID = '   SKULOG-INTR-UPC-ID           
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    IF PV-REC-COUNT EQUAL ZERO                                  
                        DISPLAY '**  PROGRAM   **  =  '                         
                                 PC-CURRENT-PROGRAM-NAME                        
                        DISPLAY '**  NO ROWS RETURNED '                         
                                'FROM SKU LOG CURSOR  **'                       
                    END-IF                                                      
                    SET PS-END-SKU-LOG-CSR    TO  TRUE                          
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '3002-FETCH-SKU-LOG-CSR'                               
                                      TO  PV-PARAGRAPH-NAME                     
                    MOVE 'FETCH SKU LOG CURSOR'                                 
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                    PERFORM 9900-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       5000-PROC-SKU-REN.                                                       
      *    READ THE SKU RENUMBER TABLE FOR A SPECIFIC TIME FRAME.      *        
      *    EXTRACT ALL THE ROWS WITHIN THE SPECIFIED TIME FRAME.       *        
      *    FORMAT EACH ROW TO MATCH THE FORMAT EXPECTED BY MAKUT102.   *        
      *    WRITE EACH FORMATTED RECORD TO THE SKU REN EXTRACT FILE.    *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 5001-FORMAT-N-WRITE-REN-REC.                                 
           PERFORM 5002-FETCH-SKU-REN-CSR.                                      
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       5001-FORMAT-N-WRITE-REN-REC.                                             
      *   FORMAT THE SKU REN DATA AND WRITE IT TO THE SKU REN FILE.    *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
SAC003     MOVE PV-SKU-NMBR            TO MA025-SKU-NMBR.                       
AS001      MOVE SKURNMB-RNMBR-DATE     TO MA025-RNMBR-DATE.                     
AS001      MOVE SKURNMB-RNMBR-TIME     TO MA025-RNMBR-TIME.                     
AS001      MOVE PV-HIST-SKU-NMBR       TO MA025-HIST-SKU-NMBR.                  
           WRITE SKU-REN-EXTRACT-REC                                            
                 FROM MA025-RECORD.                                             
           ADD PC-ONE           TO  PV-REC-COUNT.                               
                                                                                
      *----------------------------------------------------------------*        
      *                                                                         
       5002-FETCH-SKU-REN-CSR.                                                  
      *   FETCH ONE ROW FROM THE SKU RENUMBER TABLE                    *        
      *                                                                *        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
               FETCH SKU-REN-CSR                                                
               INTO                                                             
                    :SKURNMB-MERCH-DEPT-NMBR                                    
                  , :SKURNMB-MERCH-CLASS-NMBR                                   
                  , :SKURNMB-SEQ-NMBR                                           
                  , :SKURNMB-RNMBR-DATE                                         
                  , :SKURNMB-RNMBR-TIME                                         
                  , :SKURNMB-HIST-DEPT-NMBR                                     
                  , :SKURNMB-HIST-CLASS-NMBR                                    
                  , :SKURNMB-HIST-SEQ-NMBR                                      
                  , :SKURNMB-VNDR-IN-HSE-NMBR                                   
                  , :SKURNMB-APLD-SUM-TBL-IND                                   
           END-EXEC.                                                            
                                                                                
AS001 * MOVE THE DEPT, CLASS, SEQ TO SKU                                        
AS001      MOVE SKURNMB-MERCH-DEPT-NMBR   TO PV-DEPT-NMBR.                      
AS001      MOVE SKURNMB-MERCH-CLASS-NMBR  TO PV-CLASS-NMBR.                     
AS001      MOVE SKURNMB-SEQ-NMBR          TO PV-SEQ-NMBR.                       
AS001      MOVE SKURNMB-HIST-DEPT-NMBR    TO PV-HIST-DEPT-NMBR.                 
AS001      MOVE SKURNMB-HIST-CLASS-NMBR   TO PV-HIST-CLASS-NMBR.                
AS001      MOVE SKURNMB-HIST-SEQ-NMBR     TO PV-HIST-SEQ-NMBR.                  
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                    IF PV-REC-COUNT EQUAL ZERO                                  
                       DISPLAY '  THE FIRST TSKURNMBR ROW RETURNED IS'          
                       DISPLAY '    SKU_NMBR   = ' PV-SKU-NMBR                  
                       DISPLAY '    RNMBR_DATE = ' SKURNMB-RNMBR-DATE           
                       DISPLAY '    RNMBR_TIME = ' SKURNMB-RNMBR-TIME           
                    END-IF                                                      
               WHEN SQLCODE = +100                                              
                    IF PV-REC-COUNT EQUAL ZERO                                  
                        DISPLAY '**  PROGRAM   **  =  '                         
                                 PC-CURRENT-PROGRAM-NAME                        
                        DISPLAY '**  NO ROWS RETURNED '                         
                                'FROM SKU REN CURSOR  **'                       
                    END-IF                                                      
                    SET PS-END-SKU-REN-CSR    TO  TRUE                          
               WHEN SQLWARN NOT = SPACES                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '5002-FETCH-SKU-REN-CSR'                               
                                      TO  PV-PARAGRAPH-NAME                     
                    MOVE 'FETCH SKU REN CURSOR'                                 
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                    PERFORM 9900-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       6000-CLOSE-LOG-CSR.                                                      
      *    CLOSE THE SKU LOG CURSOR                                    *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY ' CLOSING THE TSKULOG CURSOR...'.                            
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE SKU-LOG-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '6000-CLOSE-LOG-CSR'                               
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'CLOSE SKU LOG CURSOR'                             
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '6000-CLOSE-LOG-CSR'                                        
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'CLOSE SKU LOG CSR RETRIES EXCEEDED'                        
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *                                                                *        
       6500-CLOSE-REN-CSR.                                                      
      *    CLOSE THE SKU RENUMBER CURSOR                               *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY ' CLOSING THE TSKURNMBR CURSOR...'.                          
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE SKU-REN-CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '6500-CLOSE-REN-CSR'                               
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'CLOSE SKU RENUMBER CURSOR'                        
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '6500-CLOSE-REN-CSR'                                        
                                      TO  PV-PARAGRAPH-NAME                     
               MOVE 'CLOSE SKU REN CSR RETRIES EXCEEDED'                        
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *                                                                *        
       7000-SET-BEG-END-TMST.                                                   
      *    DETERMINE HOW TO PROCESS THIS REQUEST, THEN SET THE TMST'S  *        
      *                                                                *        
      *----------------------------------------------------------------*        
           DISPLAY '* * *'.                                                     
           IF CC-MODE-IND = 'N'                                                 
             DISPLAY 'PROCESSING ' SV-LOG-TBL-NME ' IN NORMAL MODE'             
             MOVE PC-EXTR-PRC-ID TO SV-EXTR-PRC-ID                              
             MOVE PV-INITIAL-BEG-TIME  TO SV-DELAY-TMST                         
             PERFORM 7100-GET-RPLCTL-ROW                                        
             MOVE RPLCTL-EXTR-END-TMST TO SV-BEG-TMST                           
             MOVE SV-DELAY-TMST        TO SV-END-TMST                           
           ELSE                                                                 
           IF CC-MODE-IND = 'C'                                                 
             DISPLAY 'PROCESSING ' SV-LOG-TBL-NME ' IN CONVERSION MODE'         
             MOVE PV-INITIAL-BEG-TIME  TO PV-DB2-FMT-TMST                       
             MOVE PV-INPUT-DTE         TO PV-DB2-FMT-DATE                       
             MOVE PV-DB2-FMT-TMST      TO SV-BEG-TMST                           
             MOVE PV-INITIAL-END-TIME  TO PV-DB2-FMT-TMST                       
             MOVE PV-INPUT-DTE         TO PV-DB2-FMT-DATE                       
             MOVE PV-DB2-FMT-TMST      TO SV-END-TMST                           
           ELSE                                                                 
              IF CC-MODE-IND = 'V'                                              
                DISPLAY 'PROCESSING ' SV-LOG-TBL-NME ' IN VERIFY MODE'          
                MOVE PC-UNLD-PRC-ID TO SV-EXTR-PRC-ID                           
                PERFORM 7100-GET-RPLCTL-ROW                                     
                MOVE RPLCTL-EXTR-END-TMST TO SV-BEG-TMST                        
                MOVE PC-EXTR-PRC-ID TO SV-EXTR-PRC-ID                           
                PERFORM 7100-GET-RPLCTL-ROW                                     
                MOVE RPLCTL-EXTR-END-TMST TO SV-END-TMST                        
              ELSE                                                              
              IF CC-MODE-IND = 'O'                                              
                DISPLAY 'PROCESSING ' SV-LOG-TBL-NME ' IN OVERRIDE MODE'        
                MOVE CC-BEG-TMST          TO SV-BEG-TMST                        
                MOVE CC-END-TMST          TO SV-END-TMST                        
              END-IF.                                                           
              DISPLAY '      USING BEG TIMESTAMP = ' SV-BEG-TMST.               
              DISPLAY '      USING END TIMESTAMP = ' SV-END-TMST.               
      ***                                                                       
      ***     SET UP A NUMERIC DATE AND TIME FIELD FOR THE CALL                 
      ***     TO THE SKU RENUMBER TABLE.                                        
      ***                                                                       
              MOVE SV-BEG-TMST          TO PV-DB2-FMT-TMST.                     
              PERFORM 7200-CNV-TMST-TO-NUMBERS.                                 
AS001         MOVE SV-SAVE-DATE  TO SV-BEG-DATE.                        FSRD500I
              MOVE PV-CNV-TIME2NUM-N    TO SV-BEG-TIME.                 FSRD500I
              DISPLAY '      RENUMBERED BEG DATE = ' SV-BEG-DATE.               
              DISPLAY '      RENUMBERED BEG TIME = ' SV-BEG-TIME.               
      ***                                                                       
              MOVE SV-END-TMST          TO PV-DB2-FMT-TMST.                     
              PERFORM 7200-CNV-TMST-TO-NUMBERS.                                 
AS001         MOVE SV-SAVE-DATE  TO SV-END-DATE.                        FSRD500I
              MOVE PV-CNV-TIME2NUM-N    TO SV-END-TIME.                 FSRD500I
              DISPLAY '      RENUMBERED END DATE = ' SV-END-DATE.               
              DISPLAY '      RENUMBERED END TIME = ' SV-END-TIME.               
      *----------------------------------------------------------------*        
      *                                                                *        
       7100-GET-RPLCTL-ROW.                                                     
      *    RETRIEVE THE LAST BEGIN AND END TMST FOR THE CURRENT TABLE  *        
      *    THIS TABLE TELLS US THE LAST TIME FRAME OF DATA THAT WAS    *        
      *    RETRIEVED BY THIS PROGRAM FOR A SPECIFIED TABLE.            *        
      *                                                                *        
      *----------------------------------------------------------------*        
           MOVE PC-APPL-ID     TO SV-APPL-ID.                                   
           DISPLAY '   SELECTING FROM TRPLCTL FOR '.                            
           DISPLAY '       APPL_ID     = ' SV-APPL-ID.                          
           DISPLAY '       EXTR_PRC_ID = ' SV-EXTR-PRC-ID.                      
           DISPLAY '       LOG_TBL_NME = ' SV-LOG-TBL-NME.                      
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                   SELECT                                                       
                           EXTR_BEG_TMST                                        
                          ,EXTR_END_TMST                                        
                          ,(CURRENT_TIMESTAMP - 1 HOUR)                         
                    INTO                                                        
                           :RPLCTL-EXTR-BEG-TMST                                
                          ,:RPLCTL-EXTR-END-TMST                                
                          ,:SV-DELAY-TMST                                       
                    FROM                                                        
                          TRPLCTL                                               
                    WHERE                                                       
                          APPL_ID     = :SV-APPL-ID                             
                      AND EXTR_PRC_ID = :SV-EXTR-PRC-ID                         
                      AND LOG_TBL_NME = :SV-LOG-TBL-NME                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                        CONTINUE                                                
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                        MOVE '7100-GET-RPL-CTL'                                 
                                      TO  PV-PARAGRAPH-NAME                     
                        MOVE 'SELECT FROM TRPLCTL TABLE'                        
                                      TO  PV-DB2-OPER-ATTEMPTED                 
                        PERFORM 9900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '7100-GET-RPL-CTL' TO  PV-PARAGRAPH-NAME                    
               MOVE 'RETRIES EXCEEDED FOR TRPLCTL'                              
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9900-SQL-ABEND                                           
           ELSE                                                                 
               DISPLAY '    CURRENT TMST - 1 HOUR = '                           
                   SV-DELAY-TMST                                                
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
       7200-CNV-TMST-TO-NUMBERS.                                                
AS001 *   CONVERT A DB2 TIMESTAMP TO A NUMBERIC DATE FIELD (CCYY-MM-DD)*        
AS001 *                           AND A NUMBERIC TIME FIELD (HH:MN:SS).*        
      *   THIS CONVERSION IS NEEDED TO PROPERLY COMPARE THE NUMERIC    *        
      *   DATE AND TIME FIELDS ON THE SKU RENUMBER TABLE.              *        
      *                                                                *        
      *----------------------------------------------------------------*        
AS001 *    SET  DPG52-LK-DTE-ISO-FMT  TO TRUE.                          03      
AS001 *    MOVE PV-DB2-FMT-DATE       TO DPG52-LK-DATE-INPUT.           03      
AS001 *    MOVE PC-F4                 TO DPG51-CALENDAR-TYPE.           03      
AS001 *    MOVE SPACES                TO DPG51-INCREMENT-DECREMENT-FLAG.03      
                                                                                
AS001 * GET THE DATE FOR THE RENUMBER DATE.                                     
AS001      MOVE PV-DB2-FMT-DATE(1:10) TO SV-SAVE-DATE.                          
      ***  SET UP NUMERIC TIME FIELD PV-CNV-TIME2NUM-N                          
           MOVE PV-DB2-HH     TO PV-T2N-HH-T.                           FSRD500I
           MOVE PV-DB2-MN     TO PV-T2N-MN-T.                           FSRD500I
           MOVE PV-DB2-SS     TO PV-T2N-SS-T.                           FSRD500I
      *----------------------------------------------------------------*        
      *                                                                *        
       7700-UPD-RPLCTL-ROW.                                                     
      *    UPDATE THE TRPLCTL TABLE WITH THE CURRENT EXTRACT TIME      *        
      *    FRAME FOR THE SPECIFIED TABLE.                              *        
      *                                                                *        
      *----------------------------------------------------------------*        
           MOVE PC-APPL-ID     TO SV-APPL-ID.                                   
           MOVE PC-EXTR-PRC-ID TO SV-EXTR-PRC-ID.                               
SAC002     STRING  'UPDATE '              DELIMITED BY SIZE                     
                   PC-TBL-OWNER-ID        DELIMITED BY SIZE                     
                   'TRPLCTL  SET '        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           STRING  '   EXTR_BEG_TMST='    DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                   SV-BEG-TMST            DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           STRING  '  ,EXTR_END_TMST='    DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                   SV-END-TMST            DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           STRING  ' WHERE APPL_ID = '     DELIMITED BY SIZE                    
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                   SV-APPL-ID             DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           STRING  '   AND EXTR_PRC_ID = ' DELIMITED BY SIZE                    
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                   SV-EXTR-PRC-ID         DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           STRING  '   AND LOG_TBL_NME = ' DELIMITED BY SIZE                    
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                   SV-LOG-TBL-NME         DELIMITED BY SIZE                     
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE                     
                                     INTO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
           MOVE '  ;'                  TO  PV-PRINT-LINE.                       
           PERFORM 9100-WRITE-RPLCLT-REC.                                       
      *----------------------------------------------------------------*        
       9100-WRITE-RPLCLT-REC.                                                   
      *    WRITE THE UPDATE STATEMENT LINES.                           *        
      *----------------------------------------------------------------*        
           DISPLAY PV-PRINT-LINE.                                               
           WRITE RPL-CTL-UPDATE-REC                                             
                     FROM PV-PRINT-LINE.                                        
           MOVE SPACES TO PV-PRINT-LINE.                                        
      *----------------------------------------------------------------*        
      *                                                                *        
       9900-SQL-ABEND.                                                          
      *  SQLCA ABEND ROUTINE                                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
           SET ABEND-4001             TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-CURRENT-PROGRAM-NAME.              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
