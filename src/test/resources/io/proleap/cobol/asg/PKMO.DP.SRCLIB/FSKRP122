000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400*                                                                         
000500 PROGRAM-ID.    FSKRP122.                                                 
000600 AUTHOR.        LINDA FRIESS.                                             
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  03-17-2003.                                               
000900 DATE-COMPILED.                                                           
001000*                                                                         
001100******************************************************************        
001200*       FSKRP122 - DAILY SALES AUDIT REPORT                               
001300******************************************************************        
001400*                                                                         
001500*   THIS PROGRAM WILL PRINT A AN AUDIT REPORT FOR ALL SALES               
001600*   ON A GIVEN DAY.  THE REPORT WILL BREAK ON DEPARTMENT AND              
001700*   MAJOR CLASS.                                                          
001800*                                                                         
001900******************************************************************        
002000*  CHANGE MADE: STORE NUMBER EXPANSION PROJECT.                  *        
002200*      MADE BY:  MARIA KLAMIK                                    *        
002300*         DATE:  04/22/2004               WR# 26355              *        
002400******************************************************************        
002500 ENVIRONMENT DIVISION.                                                    
002600******************************************************************        
002700 CONFIGURATION SECTION.                                                   
002800******************************************************************        
002900 SOURCE-COMPUTER.    IBM-3090.                                            
003000 OBJECT-COMPUTER.    IBM-3090.                                            
003100******************************************************************        
003200 INPUT-OUTPUT SECTION.                                                    
003300******************************************************************        
003400 FILE-CONTROL.                                                            
003500******************************************************************        
003600*                                                                         
003700     SELECT CONTROL-FILE   ASSIGN TO INP01.                               
003800*                                                                         
008300     SELECT PRINT-REPORT   ASSIGN TO OUT01.                               
008400*                                                                         
008500******************************************************************        
008600 DATA DIVISION.                                                           
009200*****************************************************************         
009300*                                                                         
009400 FILE SECTION.                                                            
009500*                                                                         
009600 FD  CONTROL-FILE                                                         
009700     RECORDING MODE IS F                                                  
009800     DATA RECORD IS CONTROL-RECORD.                                       
009900                                                                          
010000 01  CONTROL-RECORD                    PIC X(80).                         
010100*                                                                         
010200*                                                                         
011000 FD  PRINT-REPORT                                                         
011100     LABEL RECORDS ARE STANDARD                                           
011200     RECORDING MODE IS F                                                  
011300     RECORD CONTAINS 132 CHARACTERS                                       
011400     BLOCK CONTAINS 0 RECORDS.                                            
011500                                                                          
011600 01  PRINT-LINE                       PIC X(132).                         
011700*                                                                         
011800******************************************************************        
011900 WORKING-STORAGE SECTION.                                                 
012000*                                                                         
012700 01  CONTROL-DATA-IN.                                                     
012800     05  CTL-PROCESSING-DATE           PIC X(10).                         
013500     05  FILLER                        PIC X(70).                         
013600*                                                                         
014200******************************************************                    
014300** STANDARD HEADER LINE - KOHLS 132 COL                                   
014400******************************************************                    
014500     COPY DPWS132.                                                        
014600*                                                                         
014700 01  HEADING-LINE-2.                                                      
015300     05  FILLER                       PIC X(57)  VALUE SPACES.            
015400     05  FILLER                       PIC X(17)  VALUE                    
015500         ' SKU SALES AUDIT '.                                             
015600     05  FILLER                       PIC X(58)  VALUE SPACES.            
015700*                                                                         
015800 01  HEADING-LINE-3.                                                      
015900     05  FILLER                       PIC X(02)  VALUE SPACES.            
016000     05  FILLER                       PIC X(06)  VALUE 'DEPT: '.          
016100     05  HL3-DEPT                     PIC ZZZ9.                           
016200     05  FILLER                       PIC X(02)  VALUE SPACES.            
016300     05  FILLER                       PIC X(13)  VALUE                    
016400                                                 'MAJOR CLASS: '.         
016500     05  HL3-MAJOR-CLASS              PIC ZZZ9.                           
016600     05  FILLER                       PIC X(02)  VALUE SPACES.            
016601     05  FILLER                       PIC X(10)  VALUE                    
016602                                                 'SUBCLASS: '.            
016700     05  HL3-SUBCLASS                 PIC ZZZ9.                           
016800     05  FILLER                       PIC X(85)  VALUE SPACES.            
017210*                                                                         
017400 01  HEADING-LINE-4.                                                      
017500     05  FILLER                       PIC X(03)  VALUE SPACES.            
017700     05  FILLER                       PIC X(51)  VALUE                    
017901         'SKU NUMBER   STORE NUMBER   SOLD QUANTITY      DATE'.           
017902     05  FILLER                       PIC X(78)  VALUE SPACES.            
019700*                                                                         
019800 01  DETAIL-LINE-1.                                                       
019900     05  FILLER                       PIC X(03)  VALUE SPACES.            
020000     05  DL1-SKU-NBR                  PIC ZZZZZZZ9.                       
MK         05  FILLER                       PIC X(07)  VALUE SPACES.            
MK         05  DL1-STORE-NBR                PIC ZZZZ9.                          
020700     05  FILLER                       PIC X(08)  VALUE SPACES.            
020800     05  DL1-SOLD-QTY                 PIC -------9.                       
020900     05  FILLER                       PIC X(09)  VALUE SPACES.            
021000     05  DL1-DATE                     PIC X(10).                          
021700     05  FILLER                       PIC X(74)  VALUE SPACES.            
022700*                                                                         
027600*                                                                         
027700 01  BLANK-LINE.                                                          
027800     05  FILLER                       PIC X(132) VALUE SPACES.            
027900*                                                                         
028000 01  END-OF-REPORT-LINE.                                                  
028100     05  FILLER                       PIC X(25)      VALUE                
028200         ' ** END OF REPORT **     '.                                     
028300*                                                                         
028400*                                                                         
028500 01  PROGRAM-SWITCHES.                                                    
028600     05  PS-EOF-CONTROL-FILE         PIC  X(01)    VALUE 'N'.             
028700         88  EOF-CONTROL-FILE                      VALUE 'Y'.             
028800     05  PS-EOF-SLS-AUD-CURSOR       PIC  X(01)    VALUE 'N'.             
028900         88  EOF-SLS-AUD-CURSOR                    VALUE 'Y'.             
028901     05  PS-FIRST-TIME-THRU          PIC  X(01)    VALUE 'Y'.             
028902         88  FIRST-TIME-THRU                       VALUE 'Y'.             
031200*                                                                         
031300 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
031400                                                     COMP SYNC.           
031500     88  ABEND-4013                                  VALUE +4013.         
031600*                                                                         
031700 01  PC-CONSTANTS.                                                        
031800     05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'FSKRP122'.         
033100     05  PC-1                        PIC S9(03) VALUE +1                  
033200                                                COMP-3.                   
033300     05  PC-2                        PIC S9(03) VALUE +2                  
033400                                                COMP-3.                   
033401     05  PC-MAX-LINES                PIC S9(3)  VALUE +57                 
033402                                                COMP-3.                   
033403*                                                                         
033404 01  PV-PROGRAM-VARIABLES.                                                
046200     05  PV-PREV-DEPT                PIC 9(04).                           
046201     05  PV-PREV-MAJ-CL              PIC 9(04).                           
046202     05  PV-PREV-SUB-CL              PIC 9(04).                           
046203     05  PV-SOLD-QTY                 PIC S9(07) COMP-3 VALUE +0.          
046204     05  PV-NULL-IND-QTY             PIC S9(04) COMP.                     
046205     05  PV-RECS-PROCESSED           PIC S9(09) COMP-3 VALUE +0.          
046206     05  PV-DISP-RECS-PROC           PIC ZZZ,ZZZ,ZZ9.                     
084609*                                                                         
084610     05  PV-SALES-DATE.                                                   
084620         10  PV-SALES-CCYY           PIC X(04).                           
084630         10  FILLER                  PIC X(01).                           
084640         10  PV-SALES-MONTH          PIC X(02).                           
084650         10  FILLER                  PIC X(01).                           
084660         10  PV-SALES-DAY            PIC X(02).                           
084670*                                                                         
084680     05  PV-TRANS-DATE.                                                   
084690         10  PV-TRANS-MONTH          PIC X(02).                           
084700         10  FILLER                  PIC X(01)     VALUE '/'.             
084800         10  PV-TRANS-DAY            PIC X(02).                           
084900         10  FILLER                  PIC X(01)     VALUE '/'.             
084901         10  PV-TRANS-CCYY           PIC X(04).                           
084902*                                                                         
084903     05  PV-PAGE-CNT                 PIC S9(5)     VALUE +0               
084904                                                   COMP-3.                
084905     05  PV-LINE-CNT                 PIC S9(3)     VALUE +0               
084906                                                   COMP-3.                
084907     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
084908     05  PV-DB2-OPER-ATTEMPTED       PIC X(30)     VALUE SPACES.          
084909*                                                                         
084910 01  CD-CPU-DATE                      PIC 9(08).                          
084911 01  FILLER REDEFINES CD-CPU-DATE.                                        
084912     05  CD-CPU-YEAR                  PIC 9(04).                          
084913     05  CD-CPU-MON                   PIC 9(02).                          
084914     05  CD-CPU-DAY                   PIC 9(02).                          
084915*                                                                         
084916 01  CT-CPU-TIME.                                                         
084917     05  CT-CPU-HR                    PIC 9(02)     VALUE ZEROS.          
084918     05  CT-CPU-MIN                   PIC 9(02)     VALUE ZEROS.          
084919     05  CT-CPU-SEC                   PIC 9(02)     VALUE ZEROS.          
084920     05  CT-CPU-HSEC                  PIC 9(02)     VALUE ZEROS.          
084921*                                                                         
084922***********************                                                   
084923*  DB2 TABLE LAYOUTS  *                                                   
084924***********************                                                   
084925*                                                                         
084926     EXEC SQL                                                             
084927         INCLUDE TSKXREF                                                  
084928     END-EXEC.                                                            
084929*                                                                         
084930*****************************************                                 
084931*   ONHAND SALES AUDIT TEMPORARY TABLE                                    
084932*****************************************                                 
084933     EXEC SQL                                                             
084934         INCLUDE FST00005                                                 
084935     END-EXEC.                                                            
084936*                                                                         
084937*                                                                         
084938* DB2 COMMUNICATION AREA                                                  
084939*                                                                         
084940     EXEC SQL                                                             
084941         INCLUDE SQLCA                                                    
084942     END-EXEC.                                                            
084943*                                                                         
084944* DB2 COMMUNICATION AREA2                                                 
084945*                                                                         
084946     COPY SQLCA2.                                                         
084947 EJECT                                                                    
084948*                                                                         
084949* CURSOR FOR SALES AUDIT TRANSACTIONS                                     
084950*                                                                         
084951     EXEC SQL                                                             
084952       DECLARE SALES_AUD_CSR CURSOR FOR                                   
084953         SELECT A.DEPT_NBR                                                
084954               ,B.CLASS                                                   
084955               ,A.SUB_CL_NBR                                              
084956               ,B.SKU                                                     
084957               ,A.LOC_NBR                                                 
084958               ,SUM(A.SLD_QTY)                                            
084959               ,A.SLS_DTE                                                 
085200           FROM FSTT_OH_SLS_AUD A                                         
085300               ,TSKXREF B                                                 
085400          WHERE A.ITM_NBR = B.ITM_NBR                                     
085500            AND A.PRCG_DTE = :CTL-PROCESSING-DATE                         
085600            AND A.TRN_STAT_CDE = 'C'                                      
085700            AND B.SKU_STAT_CDE > '00'                                     
085800          GROUP BY A.DEPT_NBR                                             
085900                  ,B.CLASS                                                
086000                  ,A.SUB_CL_NBR                                           
086100                  ,B.SKU                                                  
086200                  ,A.LOC_NBR                                              
086300                  ,A.SLS_DTE                                              
087600          ORDER BY A.DEPT_NBR                                             
087700                  ,B.CLASS                                                
087800                  ,A.SUB_CL_NBR                                           
087900                  ,B.SKU                                                  
088000                  ,A.LOC_NBR                                              
088001                  ,A.SLS_DTE                                              
088002       WITH UR                                                            
088003       FOR FETCH ONLY                                                     
088004     END-EXEC.                                                            
088005*                                                                         
088006*---------------------------------------------------------------*         
088007*   ERROR MESSAGE AREA FOR DB2                                            
088008*---------------------------------------------------------------*         
088009     COPY DPWS004.                                                        
088010*                                                                         
088011 EJECT                                                                    
088012*                                                                         
088013 LINKAGE SECTION.                                                         
088014*****************************************************************         
088015*PROCEDURE DIVISION.                                                      
088016*****************************************************************         
088017 PROCEDURE DIVISION.                                                      
088018*                                                                         
088019 A100-MAINLINE-PROCESSING.                                                
088020*                                                                         
088021     PERFORM B100-INITIALIZATION.                                         
088022                                                                          
088023     PERFORM B200-PROCESS-SALES-AUD-CURSOR                                
088024             UNTIL EOF-SLS-AUD-CURSOR.                                    
088025*                                                                         
088026     PERFORM B300-END-ROUTINE.                                            
088027*                                                                         
088028     STOP RUN.                                                            
088029*                                                                         
088030 EJECT                                                                    
088031*****************************************************************         
088032*                                                                         
088033*  - GET SYSTEM DATE & TIME AND MOVE TO REPORT HEADER                     
088034*  - READ CONTROL CARD WITH PROCESSING DATE                               
088035*  - OPEN CURSOR                                                          
088036*                                                                         
088037*****************************************************************         
088038 B100-INITIALIZATION.                                                     
088039*                                                                         
088040     ACCEPT CT-CPU-TIME FROM TIME.                                        
088041     ACCEPT CD-CPU-DATE FROM DATE YYYYMMDD.                               
088042*                                                                         
088043     MOVE CT-CPU-HR     TO DP132-RUN-HOUR.                                
088044     MOVE CT-CPU-MIN    TO DP132-RUN-MINUTE.                              
088045*                                                                         
088046     MOVE CD-CPU-MON    TO DP132-RUN-MONTH.                               
088047     MOVE CD-CPU-DAY    TO DP132-RUN-DAY.                                 
088048     MOVE CD-CPU-YEAR   TO DP132-RUN-YEAR.                                
088049*                                                                         
088050     MOVE 1  TO DP132-REPORT-NUMBER                                       
088051                DP132-PAGE-NUMBER.                                        
088052     MOVE PC-PROGRAM-NAME TO DP132-PROGRAM-NAME.                          
088053*                                                                         
088054     OPEN INPUT  CONTROL-FILE                                             
088055          OUTPUT PRINT-REPORT.                                            
088056*                                                                         
088057     PERFORM Z100-READ-CONTROL-FILE.                                      
088058*                                                                         
088059     PERFORM Z200-OPEN-SALES-AUD-CURSOR.                                  
088060                                                                          
088061 EJECT                                                                    
088062*****************************************************************         
088063*                                                                         
088064*  - FETCH ROW FROM CURSOR                                                
088065*  - CHECK FOR BREAKS                                                     
088066*  - PRINT DETAIL LINE                                                    
088067*                                                                         
088068*****************************************************************         
088069 B200-PROCESS-SALES-AUD-CURSOR.                                           
088070*                                                                         
088071     PERFORM C100-FETCH-SALES-AUD-CSR.                                    
088072*                                                                         
088073     IF EOF-SLS-AUD-CURSOR                                                
088074        CONTINUE                                                          
088075     ELSE                                                                 
088076        IF FIRST-TIME-THRU                                                
088077           PERFORM C200-DEPT-MCL-BREAK                                    
088078           MOVE 'N' TO PS-FIRST-TIME-THRU                                 
088079        ELSE                                                              
088080           IF (FST00005-DEPT-NBR NOT = PV-PREV-DEPT) OR                   
088081              (SKXREF-CLASS NOT = PV-PREV-MAJ-CL)                         
088082               PERFORM C200-DEPT-MCL-BREAK                                
088083           ELSE                                                           
088084              IF (FST00005-SUB-CL-NBR NOT = PV-PREV-SUB-CL)               
088085                 PERFORM C300-SUBCLASS-BREAK                              
088090              END-IF                                                      
088100           END-IF                                                         
088200        END-IF                                                            
088300                                                                          
088400        PERFORM C400-FORMAT-DETAIL-LINE                                   
088500     END-IF.                                                              
088501                                                                          
088502 EJECT                                                                    
088503*****************************************************************         
088504*                                                                         
088505*  - PRINT CONTROL TOTALS, CLOSE FILES                                    
088506*                                                                         
088507*****************************************************************         
088508 B300-END-ROUTINE.                                                        
088509*                                                                         
088510     WRITE PRINT-LINE FROM END-OF-REPORT-LINE                             
088511         AFTER ADVANCING PC-2.                                            
088512*                                                                         
088513     MOVE PV-RECS-PROCESSED TO PV-DISP-RECS-PROC.                         
088514     DISPLAY '*************************************************'.         
088515     DISPLAY 'FSKRP122 - RECORDS PROCESSED: ' PV-DISP-RECS-PROC.          
088516     DISPLAY '*************************************************'.         
088517*                                                                         
088518     CLOSE CONTROL-FILE                                                   
210700           PRINT-REPORT.                                                  
210701*                                                                         
210702     PERFORM Z300-CLOSE-SALES-AUD-CURSOR.                                 
210703                                                                          
210704 EJECT                                                                    
211000*****************************************************************         
211100*                                                                         
211200*  - FETCH ROW FROM SALES AUDIT CURSOR                                    
211600*                                                                         
211700*****************************************************************         
211800 C100-FETCH-SALES-AUD-CSR.                                                
211801*                                                                         
211802     EXEC SQL                                                             
211803         FETCH SALES_AUD_CSR                                              
211804          INTO :FST00005-DEPT-NBR,                                        
211805               :SKXREF-CLASS,                                             
211806               :FST00005-SUB-CL-NBR,                                      
211807               :SKXREF-SKU,                                               
211808               :FST00005-LOC-NBR,                                         
211811               :PV-SOLD-QTY:PV-NULL-IND-QTY,                              
211812               :FST00005-SLS-DTE                                          
211815     END-EXEC.                                                            
211816*                                                                         
211817     EVALUATE TRUE                                                        
211818         WHEN SQLCODE = ZERO                                              
211819             IF (PV-NULL-IND-QTY = -1)                                    
211820                MOVE ZERO TO PV-SOLD-QTY                                  
211821             END-IF                                                       
211822         WHEN SQLCODE = +100                                              
211823             SET EOF-SLS-AUD-CURSOR TO TRUE                               
211824         WHEN SQLWARN0 NOT = SPACES                                       
211825         WHEN SQLCODE  NOT = ZERO                                         
211826             PERFORM Z999-SQL-ABEND                                       
211827     END-EVALUATE.                                                        
211828                                                                          
211829 EJECT                                                                    
211830*****************************************************************         
211831*                                                                         
211832*  - PAGE BREAK FOR NEW DEPT OR MAJOR CLASS                               
211833*                                                                         
211834*****************************************************************         
211835 C200-DEPT-MCL-BREAK.                                                     
211836*                                                                         
211837     MOVE FST00005-DEPT-NBR   TO HL3-DEPT.                                
211838     MOVE SKXREF-CLASS        TO HL3-MAJOR-CLASS.                         
211839     MOVE FST00005-SUB-CL-NBR TO HL3-SUBCLASS.                            
211840*                                                                         
211841     PERFORM Z400-PRINT-REPORT-HEADERS.                                   
211842*                                                                         
211850     MOVE FST00005-DEPT-NBR   TO PV-PREV-DEPT.                            
211860     MOVE SKXREF-CLASS        TO PV-PREV-MAJ-CL.                          
211870     MOVE FST00005-SUB-CL-NBR TO PV-PREV-SUB-CL.                          
211871*                                                                         
211872*****************************************************************         
211873*                                                                         
211874*  - PRINT DEPT/MAJOR CLASS/SUBCLASS LINE WHEN SUBCLASS CHANGES           
211875*                                                                         
211876*****************************************************************         
211877 C300-SUBCLASS-BREAK.                                                     
211878*                                                                         
211879     MOVE FST00005-SUB-CL-NBR TO HL3-SUBCLASS.                            
215900*                                                                         
216000     WRITE PRINT-LINE FROM HEADING-LINE-3                                 
216100         AFTER ADVANCING PC-2.                                            
216200*                                                                         
216300     MOVE FST00005-SUB-CL-NBR TO PV-PREV-SUB-CL.                          
216301                                                                          
216302 EJECT                                                                    
216303*****************************************************************         
216304*                                                                         
216305*  - MOVE DATA TO DETAIL LINE & PRINT                                     
216306*                                                                         
216307*****************************************************************         
216308 C400-FORMAT-DETAIL-LINE.                                                 
216309*                                                                         
216310     MOVE SKXREF-SKU       TO DL1-SKU-NBR.                                
216320     MOVE FST00005-LOC-NBR TO DL1-STORE-NBR.                              
216380     MOVE PV-SOLD-QTY      TO DL1-SOLD-QTY.                               
216500*                                                                         
216600     MOVE FST00005-SLS-DTE TO PV-SALES-DATE.                              
216700     MOVE PV-SALES-CCYY    TO PV-TRANS-CCYY.                              
216800     MOVE PV-SALES-MONTH   TO PV-TRANS-MONTH.                             
216900     MOVE PV-SALES-DAY     TO PV-TRANS-DAY.                               
217000     MOVE PV-TRANS-DATE    TO DL1-DATE.                                   
220900*                                                                         
221000     PERFORM D100-PRINT-DETAIL-LINE.                                      
221100     ADD +1 TO PV-RECS-PROCESSED.                                         
221101                                                                          
221102 EJECT                                                                    
221103*****************************************************************         
221104 D100-PRINT-DETAIL-LINE.                                                  
221105*                                                                         
221106     ADD PC-2 TO PV-LINE-CNT.                                             
221107*                                                                         
221108     IF PV-LINE-CNT > PC-MAX-LINES                                        
221109        PERFORM Z400-PRINT-REPORT-HEADERS                                 
221110        ADD PC-2 TO PV-LINE-CNT                                           
221111     END-IF.                                                              
221112*                                                                         
221113     WRITE PRINT-LINE FROM DETAIL-LINE-1                                  
221114         AFTER ADVANCING PC-2.                                            
221115*                                                                         
221116     MOVE SPACES TO PRINT-LINE.                                           
221117*                                                                         
221118                                                                          
221119 EJECT                                                                    
221120*****************************************************************         
221121*                                                                         
221122*  - READ CONTROL CARD CONTAINING PROCESSING DATE                         
221123*                                                                         
221124*****************************************************************         
221125 Z100-READ-CONTROL-FILE.                                                  
221126*                                                                         
221127     READ CONTROL-FILE INTO CONTROL-DATA-IN                               
221128          AT END SET EOF-CONTROL-FILE TO TRUE.                            
221129*                                                                         
221130     IF EOF-CONTROL-FILE                                                  
221500        DISPLAY '** ABEND **'                                             
221600        DISPLAY '** PROGRAM    = ' PC-PROGRAM-NAME                        
221700        DISPLAY '** PARAGRAPH  = Z100-READ-CONTROL-FILE'                  
221800        DISPLAY '** MESSAGE    = CONTROL FILE DATE NOT FOUND'             
222000        MOVE +4003      TO ABEND-CODE                                     
222100        CALL 'ILBOABN0' USING ABEND-CODE                                  
222200     END-IF.                                                              
222201                                                                          
222202 EJECT                                                                    
222203******************************************************************        
222204 Z200-OPEN-SALES-AUD-CURSOR.                                              
222205*                                                                         
222206     MOVE 'Z200-OPEN-SALES-AUD-CURSOR' TO PV-PARAGRAPH-NAME.              
222207     MOVE 'OPEN CURSOR '               TO PV-DB2-OPER-ATTEMPTED.          
222208*                                                                         
222209     EXEC SQL                                                             
222210         OPEN SALES_AUD_CSR                                               
222211     END-EXEC.                                                            
222212                                                                          
222213     EVALUATE TRUE                                                        
222214         WHEN SQLCODE = ZERO                                              
222215             CONTINUE                                                     
222216         WHEN OTHER                                                       
222217             PERFORM Z999-SQL-ABEND                                       
222218     END-EVALUATE.                                                        
222219                                                                          
222220*                                                                         
222230******************************************************************        
222240 Z300-CLOSE-SALES-AUD-CURSOR.                                             
222241*                                                                         
222242     MOVE 'Z300-CLOSE-SALES-AUD-CURSOR' TO PV-PARAGRAPH-NAME.             
222243     MOVE 'CLOSE CURSOR '               TO PV-DB2-OPER-ATTEMPTED.         
222244*                                                                         
222245     EXEC SQL                                                             
222246         CLOSE SALES_AUD_CSR                                              
222247     END-EXEC.                                                            
222248                                                                          
222249     EVALUATE TRUE                                                        
222250         WHEN SQLCODE = ZERO                                              
222251             CONTINUE                                                     
222252         WHEN OTHER                                                       
222253             PERFORM Z999-SQL-ABEND                                       
222254     END-EVALUATE.                                                        
222255                                                                          
222256 EJECT                                                                    
222257*                                                                         
222258*****************************************************************         
222259 Z400-PRINT-REPORT-HEADERS.                                               
222260*                                                                         
222261     ADD PC-1 TO PV-PAGE-CNT.                                             
222262     MOVE PV-PAGE-CNT TO DP132-PAGE-NUMBER.                               
222263*                                                                         
222264     WRITE PRINT-LINE FROM DP132-STANDRD-HEADER-132-COLS                  
222265         AFTER ADVANCING PAGE.                                            
222266*                                                                         
222267     WRITE PRINT-LINE FROM HEADING-LINE-2                                 
222268         AFTER ADVANCING PC-1.                                            
222269*                                                                         
222270     WRITE PRINT-LINE FROM HEADING-LINE-3                                 
222271         AFTER ADVANCING PC-1.                                            
222272*                                                                         
222273     WRITE PRINT-LINE FROM HEADING-LINE-4                                 
222274         AFTER ADVANCING PC-2.                                            
222275*                                                                         
222279     MOVE ZERO TO PV-LINE-CNT.                                            
222280                                                                          
222300*                                                                         
222400*****************************************************************         
222500 Z999-SQL-ABEND.                                                          
222600*                                                                         
222700     SET ABEND-4013            TO TRUE.                                   
222800     DISPLAY '**  ABEND     **'.                                          
222900     DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
223000     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
223100     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
223200                                                                          
223300*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
223400*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
223500                                                                          
223600     COPY DPPD004.                                                        
223700                                                                          
223800     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
223900*                                                                         
224000*****************************************************************         
