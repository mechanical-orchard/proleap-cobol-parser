      *-----------------------------------------------------------------        
       IDENTIFICATION DIVISION.                                                 
      *-----------------------------------------------------------------        
       PROGRAM-ID.      INKRP151.                                               
       AUTHOR.          JAN BLAZICH.                                            
       DATE-WRITTEN.    02/02/06.                                               
      *                                                                         
      *-----------------------------------------------------------------        
      * PURPOSE. SKU RECLASS WEEKLY DEPT REPORTS- DEPT, STORE, INV DATE         
      *     INPUT RECORDS FROM PROGRAM INKUP143 WILL BE                         
      *     SORTED PRIOR TO THIS PROGRAM.  THE REPORT INDICATOR                 
      *     INSERTED BY THE SORT DEFINES WHICH REPORT(S) WILL BE                
      *     PRODUCED.                                                           
      *     REPORTS                                                             
      *         1 - WEEKLY DEPT SUMMARY - BY STORE                              
      *         2 - WEEKLY DEPT SUMMARY - ALL STORES                            
      *                                                                         
      *     DB2 TABLES:  TDTATTR                                                
      *                                                                         
      *     SORT SEQUENCE FOR BOTH REPORTS:                                     
      *         INV DATE                                                        
      *         DEPT                                                            
      *         STORE                                                           
      *-----------------------------------------------------------------        
      *       DB2 TABLES: NONE                                                  
      *      DB2 CURSORS: NONE                                                  
      *-----------------------------------------------------------------        
W28545* 06-29-2004  J BLAZICH    LATE RECLASS CHANGES --                        
W28545*                          MAKE SURE DEPT/MCL/SCL VALUES ARE              
W28545*                          SET ACCORDING TO THE VALUES IN EFFECT          
W28545*                          ON THE PLANNED INVENTORY DATE                  
W28545*                                                                         
W28545*-----------------------------------------------------------------        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT SKU-RECLASS-COMBO-FILE  ASSIGN TO INP01.                      
           SELECT SKU-RECLS-SUM-RPT1-FILE ASSIGN TO RPT01.                      
           SELECT SKU-RECLS-SUM-RPT2-FILE ASSIGN TO RPT02.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SKU-RECLASS-COMBO-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RECLS-EXTRACT-REC                PIC  X(119).                        
                                                                                
       FD  SKU-RECLS-SUM-RPT1-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-RECLS-SUM-RPT1-REC           PIC  X(132).                        
                                                                                
       FD  SKU-RECLS-SUM-RPT2-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-RECLS-SUM-RPT2-REC           PIC  X(132).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  ABEND-CODE                       PIC S9(4) COMP   VALUE +0.          
W28545 01  WS-SPACE                         PIC X     VALUE SPACE.              
                                                                                
       01  PROGRAM-ACCUMULATORS COMP-3.                                         
           05  PA-LINE-CNT                  PIC S9(3)  VALUE +0.                
           05  PA-PAGE-CNT                  PIC S9(5)  VALUE +0.                
           05  PA-RECS-READ                 PIC S9(11) VALUE +0.                
      * FIRST LEVEL TOTALS                                                      
           05  PA-T1-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * SECOND LEVEL TOTALS                                                     
           05  PA-T2-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * THIRD LEVEL TOTALS                                                      
           05  PA-T3-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
      * FOURTH LEVEL TOTALS                                                     
           05  PA-T4-EXTD-AMT               PIC S9(11)V99 VALUE +0.             
                                                                                
      * SUMMARY ACCUMS                                                          
           05  PA-SUM-LINE-CNT              PIC S9(3)  VALUE +0.                
           05  PA-SUM-PAGE-CNT              PIC S9(5)  VALUE +0.                
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-LINES-PER-PAGE            PIC S9(3) COMP-3 VALUE +60.         
           05  PC-PROGRAM-NAME              PIC X(08) VALUE 'INKRP151'.         
           05  PC-MAIN-HEAD-2          PIC X(43)  VALUE                         
               'SKU RECLASS - WEEKLY DEPT SUMMARY REPORT - '.                   
           05  PC-MAIN-HEAD-2B         PIC X(13)  VALUE                         
               'BY LOCATION'.                                                   
           05  PC-MAIN-HEAD-2C         PIC X(13)  VALUE                         
               'ALL LOCATIONS'.                                                 
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +2.                
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-PARAGRAPH-NAME      PIC X(31)  VALUE SPACES.                  
           05  PV-RETRY-COUNTER       PIC S9(04) VALUE +0 COMP SYNC.            
           05  PV-DB2-OPER-ATTEMPTED  PIC  X(30) VALUE SPACE.                   
           05  PV-CURRENT-DATE        PIC 9(6).                                 
           05  PV-CURRENT-DATE-R  REDEFINES PV-CURRENT-DATE.                    
               10  PV-CURR-YY         PIC 99.                                   
               10  PV-CURR-MM         PIC 99.                                   
               10  PV-CURR-DD         PIC 99.                                   
                                                                                
           05  PV-CURRENT-TIME        PIC 9(8).                                 
           05  PV-CURRENT-TIME-R  REDEFINES PV-CURRENT-TIME.                    
               10  PV-CURR-HR         PIC 99.                                   
               10  PV-CURR-MN         PIC 99.                                   
               10  FILLER             PIC X(4).                                 
                                                                                
           05  PV-PROCESS-DATE.                                                 
               10  PV-PROCESS-CCYY    PIC X(4).                                 
               10  FILLER             PIC X.                                    
               10  PV-PROCESS-MM      PIC X(2).                                 
               10  FILLER             PIC X.                                    
               10  PV-PROCESS-DD      PIC X(2).                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-FILE-SW      PIC X  VALUE 'N'.                         
               88 PS-END-OF-FILE             VALUE 'Y'.                         
               88 PS-END-OF-FILE-NO          VALUE 'N'.                         
           05  PS-FIRST-ONE-SW        PIC X  VALUE 'Y'.                         
               88  PS-FIRST-ONE              VALUE 'Y'.                         
               88  PS-FIRST-ONE-NO           VALUE 'N'.                         
           05  PS-DOUBLE-SPACE-SW     PIC X  VALUE SPACE.                       
               88  PS-DOUBLE-SPACE           VALUE 'Y'.                         
               88  PS-SINGLE-SPACE           VALUE 'N'.                         
           05  PS-SUM-DOUBLE-SPACE-SW PIC X  VALUE SPACE.                       
               88  PS-SUM-DOUBLE-SPACE       VALUE 'Y'.                         
               88  PS-SUM-SINGLE-SPACE       VALUE 'N'.                         
           05  PS-TDTATTR-ROW-IS-THERE-SW    PIC X     VALUE 'N'.               
               88  PS-TDTATTR-ROW-IS-THERE             VALUE 'Y'.               
               88  PS-TDTATTR-ROW-IS-THERE-NO          VALUE 'N'.               
       01  WORK-AREAS.                                                          
           05  CONTROL-BREAK-SW             PIC 9(2)  VALUE ZEROS.              
               88  NO-BREAK                 VALUE  ZEROS.                       
               88  BREAK-1                  VALUES ARE 1 THRU 99.               
               88  BREAK-2                  VALUES ARE 2 THRU 99.               
               88  BREAK-3                  VALUES ARE 3 THRU 99.               
               88  END-OF-PROCESS           VALUE  99.                          
                                                                                
       01  IN-KEY-INFO.                                                         
           05  IN-KEY.                                                          
               10  IN-FLD-DEPT              PIC  9(4).                          
               10  IN-FLD-LOC               PIC  9(4).                          
W28545     05  IN-LATE-IND                  PIC X.                              
           05  IN-EXTD-AMT                  PIC S9(9)V99 COMP-3.                
                                                                                
W28545 01  PREV-IN-KEY                      PIC X(08).                          
                                                                                
       01  CURR-KEY-INFO.                                                       
           05  CURR-KEY.                                                        
               10 CURR-FLD-DEPT             PIC  9(4).                          
               10 CURR-FLD-LOC              PIC  9(4).                          
W28545     05  CURR-LATE-IND                PIC X.                              
                                                                                
       01  NEXT-KEY-INFO.                                                       
           05  NEXT-KEY.                                                        
               10  NEXT-FLD-DEPT            PIC  9(4).                          
               10  NEXT-FLD-LOC             PIC  9(4).                          
W28545     05  NEXT-LATE-IND                PIC X.                              
                                                                                
                                                                                
      *----------------------------------------------------------------         
      *    REPORTING AREA.                                            -         
      *----------------------------------------------------------------         
                                                                                
      *01  DP132O-STANDRD-HEADER-132-COLS.                                      
           COPY DPWS132O.                                                       
                                                                                
       01  REPORT-HEAD-01.                                                      
           05  FILLER                  PIC X(39)  VALUE SPACES.                 
           05  HD01-HEAD-1             PIC X(43).                               
           05  HD01-HEAD-2             PIC X(37).                               
                                                                                
       01  REPORT-HEAD-02.                                                      
           05  FILLER                  PIC X(50)  VALUE SPACES.                 
           05  FILLER                  PIC X(23)  VALUE                         
               'FOR THE WEEK BEGINNING '.                                       
           05  HD02-DATE               PIC X(46).                               
                                                                                
       01  REPORT-HEAD-A1.                                                      
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'DEPARTMENT'.            
           05  FILLER                  PIC X(28) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'LOCATION'.              
           05  FILLER                  PIC X(32) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
               'EXTENDED AMOUNT'.                                               
                                                                                
       01  REPORT-HEAD-A2.                                                      
           05  FILLER                  PIC X(43) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'DEPARTMENT'.            
           05  FILLER                  PIC X(32) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
               'EXTENDED AMOUNT'.                                               
                                                                                
       01  REPORT-LOC-A1.                                                       
W28545     05  FILLER                  PIC X(06) VALUE SPACES.                  
W28545     05  DTA1-LATE-1             PIC X.                                   
           05  DTA1-DEPT               PIC Z999  BLANK WHEN ZERO.               
           05  FILLER                  PIC X(34) VALUE SPACES.                  
           05  DTA1-LOC                PIC Z999.                                
           05  FILLER                  PIC X(33) VALUE SPACES.                  
           05  DTA1-EXTD-AMT           PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  REPORT-DEPT-TOTS-LINE.                                               
           05  FILLER                  PIC X(14)  VALUE SPACES.                 
           05  FILLER                  PIC X(13)  VALUE                         
               'DEPT TOTALS ('.                                                 
           05  TA1-DEPT                PIC Z999.                                
           05  FILLER                  PIC X(51)  VALUE ') :'.                  
           05  TA1-DEPT-EXTD-AMT       PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  REPORT-SUM-DEPT.                                                     
           05  FILLER                  PIC X(20)  VALUE SPACES.                 
           05  FILLER                  PIC X(25)  VALUE                         
               'DEPT TOTALS :'.                                                 
           05  SUM-HEAD-DEPT           PIC Z999.                                
           05  FILLER                  PIC X(33)  VALUE SPACES.                 
           05  SUM-DEPT-EXTD-AMT       PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
       01  REPORT-NET-TOTALS.                                                   
           05  FILLER                  PIC X(47) VALUE SPACES.                  
           05  FILLER                  PIC X(35) VALUE                          
               'CORPORATE NET TOTALS'.                                          
           05  SUM-NET-EXTD-AMT        PIC $$$,$$$,$$$,$$$.99-.                 
                                                                                
                                                                                
       01  BLANK-LINE                  PIC XX     VALUE SPACES.                 
                                                                                
      *01  IN145-SKU-RECLASS-COMBO.                                             
           COPY INRD145.                                                        
                                                                                
      ******************************************************************        
      *  DB2 FORMATTED MESSAGE AREA                                             
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------         
      *   PROGRAM MAINLINE                                                      
      *----------------------------------------------------------------         
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-SETUP-PROGRAM.                                          
           PERFORM 2200-PROCESS-BRK-2                                           
               UNTIL IN-KEY = HIGH-VALUES.                                      
           PERFORM 6400-PRINT-FINAL-TOTALS                                      
           PERFORM 1200-CLEANUP-PROGRAM.                                        
           GOBACK.                                                              
                                                                                
       1000-SETUP-PROGRAM.                                                      
           OPEN INPUT  SKU-RECLASS-COMBO-FILE                                   
                OUTPUT SKU-RECLS-SUM-RPT1-FILE                                  
                       SKU-RECLS-SUM-RPT2-FILE.                                 
                                                                                
           ACCEPT PV-CURRENT-DATE         FROM DATE.                            
           ACCEPT PV-CURRENT-TIME         FROM TIME.                            
           MOVE PV-CURR-YY                  TO DP132O-RUN-YEAR.                 
           MOVE PV-CURR-MM                  TO DP132O-RUN-MONTH                 
           MOVE PV-CURR-DD                  TO DP132O-RUN-DAY.                  
           MOVE PV-CURR-HR                  TO DP132O-RUN-HOUR.                 
           MOVE PV-CURR-MN                  TO DP132O-RUN-MINUTE.               
           MOVE PC-PROGRAM-NAME             TO DP132O-PROGRAM-NAME.             
           INITIALIZE CURR-KEY-INFO.                                            
           MOVE LOW-VALUES                  TO PREV-IN-KEY.                     
           PERFORM 5000-READ-IN-REC.                                            
           IF IN-KEY NOT = HIGH-VALUES                                          
               PERFORM 4600-GET-NEXT-KEY                                        
               MOVE NEXT-KEY-INFO               TO CURR-KEY-INFO                
               MOVE IN145-INV-DATE              TO PV-PROCESS-DATE              
               MOVE CURR-FLD-LOC                TO DTA1-LOC                     
               MOVE CURR-FLD-DEPT               TO DTA1-DEPT                    
W28545         MOVE CURR-LATE-IND               TO DTA1-LATE-1                  
                                                                                
               PERFORM 1300-GET-BEG-END-DATES                                   
               PERFORM 7000-PRINT-HEADINGS                                      
               PERFORM 4900-CHECK-FOR-CONTROL-BREAK                             
               PERFORM 7500-PRINT-SUM-HEADS-1                                   
W28545         MOVE SPACES               TO DTA1-LATE-1                         
                                                                                
           END-IF.                                                              
                                                                                
       1200-CLEANUP-PROGRAM.                                                    
           CLOSE SKU-RECLASS-COMBO-FILE                                         
                 SKU-RECLS-SUM-RPT1-FILE                                        
                 SKU-RECLS-SUM-RPT2-FILE.                                       
                                                                                
      ******************************************************************        
      *    GET BEGINNING AND END OF WEEK DATES FROM TDTATTR                     
      ******************************************************************        
       1300-GET-BEG-END-DATES.                                                  
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
               UNTIL   PV-RETRY-COUNTER > PC-MAX-RETRIES                        
                  OR   SQLCODE = ZERO                                           
                  OR   SQLCODE = +100                                           
                                                                                
             EXEC SQL                                                           
               SELECT FSCL_WK_BEG_DTE                                           
                     ,FSCL_WK_END_DTE                                           
                 INTO                                                           
                     :DTATTR-FSCL-WK-BEG-DTE                                    
                    ,:DTATTR-FSCL-WK-END-DTE                                    
                 FROM TDTATTR                                                   
                WHERE KY_DTE = :IN145-INV-DATE                                  
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = ZERO                                            
                     SET PS-TDTATTR-ROW-IS-THERE TO TRUE                        
                 WHEN SQLCODE = +100                                            
                     SET PS-TDTATTR-ROW-IS-THERE-NO TO TRUE                     
                                                                                
                 WHEN SQLWARN0 NOT EQUAL SPACE                                  
                 WHEN SQLCODE NOT EQUAL ZERO                                    
                     DISPLAY '****************************************'         
                     DISPLAY '** ABEND                              **'         
                     DISPLAY '** PROGRAM = INKUT143                 **'         
                     DISPLAY '** PARAGRAPH = 1300-GET-BEG-END-DATES **'         
                     DISPLAY '** SELECT TDTATTR                     **'         
                     DISPLAY '****************************************'         
                     PERFORM 9999-SQL-ABEND                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
               SQLCODE NOT = ZERO                                               
               DISPLAY '****************************************'               
               DISPLAY '** MAX RETRIES EXCEEDED ON SELECT     **'               
               DISPLAY '** FROM TDTATTR                       **'               
               DISPLAY '** PARAGRAPH = 1300-GET-BEG-END-DATES **'               
               DISPLAY '****************************************'               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
      *----------------------------------------------------------------         
      *  DEPT BREAK - DETAIL AND SUMMARY REPORTS                                
      *    DEPENDING ON IN145-REPORT-IND                                        
      *    PRINT DEPT TOTALS                                                    
      *----------------------------------------------------------------         
       2200-PROCESS-BRK-2.                                                      
           PERFORM 3200-INITIALIZE-BRK-2                                        
           PERFORM 2300-PROCESS-BRK-1 UNTIL BREAK-2                             
           PERFORM 6200-PRINT-DEPT-TOTAL                                        
W28545     MOVE SPACES                   TO DTA1-LATE-1                         
                                                                                
           PERFORM 4200-BRK-2-SUMMARY.                                          
                                                                                
      *----------------------------------------------------------------         
      *  STORE(LOC) BREAK - DETAIL AND SUMMARY REPORTS                          
      *----------------------------------------------------------------         
       2300-PROCESS-BRK-1.                                                      
           PERFORM 3300-INITIALIZE-BRK-1.                                       
           PERFORM 2400-PROCESS-DETAIL UNTIL BREAK-1                            
           PERFORM 6300-PRINT-STORE-TOTALS.                                     
           PERFORM 4300-BRK-1-SUMMARY.                                          
                                                                                
      *----------------------------------------------------------------         
      *  PROCESS DETAIL                                                         
      *----------------------------------------------------------------         
       2400-PROCESS-DETAIL.                                                     
                                                                                
           MOVE IN-KEY                      TO PREV-IN-KEY                      
           ADD IN-EXTD-AMT                  TO PA-T1-EXTD-AMT.                  
           PERFORM 5000-READ-IN-REC.                                            
                                                                                
           PERFORM 4600-GET-NEXT-KEY.                                           
           PERFORM 4900-CHECK-FOR-CONTROL-BREAK.                                
                                                                                
      *----------------------------------------------------------------         
      *  DEPT BREAK                                                             
      *----------------------------------------------------------------         
       3200-INITIALIZE-BRK-2.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-DEPT               TO CURR-FLD-DEPT.                   
W28545     MOVE NEXT-LATE-IND               TO CURR-LATE-IND.                   
                                                                                
      *----------------------------------------------------------------         
      *  STORE(LOC) BREAK                                                       
      *----------------------------------------------------------------         
       3300-INITIALIZE-BRK-1.                                                   
           MOVE ZEROS                       TO CONTROL-BREAK-SW.                
           MOVE NEXT-FLD-LOC                TO CURR-FLD-LOC.                    
                                                                                
      *----------------------------------------------------------------         
      *  DEPT BREAK                                                             
      *----------------------------------------------------------------         
       4200-BRK-2-SUMMARY.                                                      
           MOVE CURR-FLD-DEPT           TO SUM-HEAD-DEPT.                       
           MOVE PA-T2-EXTD-AMT          TO SUM-DEPT-EXTD-AMT.                   
           MOVE ZEROS                   TO PA-T2-EXTD-AMT                       
                                                                                
           IF PS-SUM-DOUBLE-SPACE                                               
               SET PS-SUM-SINGLE-SPACE  TO TRUE                                 
               WRITE SKU-RECLS-SUM-RPT2-REC                                     
                 FROM REPORT-SUM-DEPT                                           
                   AFTER ADVANCING 2 LINES                                      
               ADD 2                    TO PA-SUM-LINE-CNT                      
           ELSE                                                                 
               WRITE SKU-RECLS-SUM-RPT2-REC                                     
                 FROM REPORT-SUM-DEPT                                           
                   AFTER ADVANCING 1 LINES                                      
               ADD 1                    TO PA-SUM-LINE-CNT                      
           END-IF.                                                              
                                                                                
           IF PA-SUM-LINE-CNT > PC-LINES-PER-PAGE                               
               PERFORM 7500-PRINT-SUM-HEADS-1.                                  
                                                                                
       4300-BRK-1-SUMMARY.                                                      
                                                                                
W28545* THERES NOTHING TO DO HERE.                                              
W28545     MOVE SPACE                   TO WS-SPACE.                            
                                                                                
                                                                                
      *----------------------------------------------------------------         
      * SET NEXT KEY                                                            
      *----------------------------------------------------------------         
       4600-GET-NEXT-KEY.                                                       
           MOVE IN-KEY-INFO                 TO NEXT-KEY-INFO.                   
      *                                                                         
       4900-CHECK-FOR-CONTROL-BREAK.                                            
           EVALUATE TRUE                                                        
               WHEN NEXT-KEY         IS EQUAL TO HIGH-VALUES                    
                    MOVE 99                 TO CONTROL-BREAK-SW                 
               WHEN NEXT-FLD-DEPT    IS NOT EQUAL TO CURR-FLD-DEPT              
                    MOVE 2                  TO CONTROL-BREAK-SW                 
               WHEN NEXT-FLD-LOC     IS NOT EQUAL TO CURR-FLD-LOC               
                    MOVE 1                  TO CONTROL-BREAK-SW                 
               WHEN OTHER                                                       
                    MOVE ZEROS              TO CONTROL-BREAK-SW                 
           END-EVALUATE.                                                        
                                                                                
       5000-READ-IN-REC.                                                        
           IF IN-KEY IS NOT EQUAL TO HIGH-VALUES                                
               READ SKU-RECLASS-COMBO-FILE                                      
                   INTO IN145-SKU-RECLASS-COMBO                                 
               AT END                                                           
                   MOVE HIGH-VALUES                 TO IN-KEY.                  
                                                                                
           IF IN-KEY IS NOT EQUAL TO HIGH-VALUES                                
               ADD 1                            TO PA-RECS-READ                 
               EVALUATE IN145-REPORT-IND                                        
                 WHEN '1'                                                       
                 WHEN '2'                                                       
                     PERFORM 5100-BUILD-KEY-RPT-1                               
                 WHEN OTHER                                                     
                     DISPLAY 'REPORT INDICATOR INVALID'                         
               END-EVALUATE                                                     
               PERFORM 5900-SEQUENCE-ERR UNTIL                                  
                   IN-KEY IS NOT LESS THAN PREV-IN-KEY                          
           END-IF.                                                              
                                                                                
       5100-BUILD-KEY-RPT-1.                                                    
           MOVE IN145-DEPT                  TO IN-FLD-DEPT.                     
           MOVE IN145-LOC-NBR               TO IN-FLD-LOC.                      
           MOVE IN145-EXTD-AMT              TO IN-EXTD-AMT.                     
W28545     MOVE IN145-LATE-RECLS-IND        TO IN-LATE-IND.                     
                                                                                
       5900-SEQUENCE-ERR.                                                       
           DISPLAY '-- INKRP151 - SEQUENCE ERROR --'.                           
           DISPLAY '-- INKRP151 - KEY AREA      = ' IN-KEY                      
           DISPLAY '-- INKRP151 - PREV KEY AREA = ' PREV-IN-KEY.                
                                                                                
      *----------------------------------------------------------------         
      *  DEPT TOTALS                                                            
      *----------------------------------------------------------------         
       6200-PRINT-DEPT-TOTAL.                                                   
           MOVE CURR-FLD-DEPT            TO TA1-DEPT                            
           MOVE PA-T2-EXTD-AMT           TO TA1-DEPT-EXTD-AMT.                  
                                                                                
           ADD  PA-T2-EXTD-AMT           TO PA-T3-EXTD-AMT                      
                                                                                
           WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-DEPT-TOTS-LINE              
               AFTER ADVANCING 2 LINES.                                         
           SET PS-DOUBLE-SPACE         TO TRUE                                  
           SET PS-FIRST-ONE            TO TRUE.                                 
           IF NEXT-FLD-DEPT NUMERIC                                             
               MOVE NEXT-FLD-DEPT      TO DTA1-DEPT                             
           END-IF.                                                              
           ADD 2                       TO PA-LINE-CNT.                          
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
      *----------------------------------------------------------------         
      *  STORE TOTALS                                                           
      *----------------------------------------------------------------         
       6300-PRINT-STORE-TOTALS.                                                 
           MOVE CURR-FLD-DEPT            TO DTA1-DEPT                           
           MOVE CURR-FLD-LOC             TO DTA1-LOC                            
W28545     MOVE CURR-LATE-IND            TO DTA1-LATE-1                         
                                                                                
           MOVE PA-T1-EXTD-AMT           TO DTA1-EXTD-AMT                       
           IF PS-FIRST-ONE                                                      
           OR PS-DOUBLE-SPACE                                                   
               SET PS-FIRST-ONE-NO       TO TRUE                                
               SET PS-SINGLE-SPACE       TO TRUE                                
               WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-LOC-A1                  
                   AFTER ADVANCING 2 LINES                                      
               ADD 2                     TO PA-LINE-CNT                         
           ELSE                                                                 
               MOVE ZERO                 TO DTA1-DEPT                           
               WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-LOC-A1                  
                   AFTER ADVANCING 1 LINES                                      
               ADD 1                     TO PA-LINE-CNT                         
           END-IF.                                                              
                                                                                
           ADD PA-T1-EXTD-AMT            TO PA-T2-EXTD-AMT.                     
           MOVE ZEROS                    TO PA-T1-EXTD-AMT.                     
                                                                                
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
      *----------------------------------------------------------------         
      *  FINAL TOTALS                                                           
      *----------------------------------------------------------------         
       6400-PRINT-FINAL-TOTALS.                                                 
           MOVE PA-T3-EXTD-AMT           TO SUM-NET-EXTD-AMT                    
                                                                                
           WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-NET-TOTALS                  
               AFTER ADVANCING 3 LINES.                                         
           ADD 3                         TO PA-LINE-CNT                         
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
           WRITE SKU-RECLS-SUM-RPT2-REC FROM REPORT-NET-TOTALS                  
               AFTER ADVANCING 3 LINES.                                         
           ADD 3                         TO PA-LINE-CNT                         
           IF PA-LINE-CNT > PC-LINES-PER-PAGE                                   
               PERFORM 7000-PRINT-HEADINGS.                                     
                                                                                
      *----------------------------------------------------------------         
      *    REPORT ROUTINES                                                      
      *----------------------------------------------------------------         
       7000-PRINT-HEADINGS.                                                     
           ADD 1                       TO PA-PAGE-CNT.                          
           MOVE 1                      TO PA-LINE-CNT.                          
           MOVE PA-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
           MOVE PC-PROGRAM-NAME        TO DP132O-PROGRAM-NAME.                  
           MOVE IN145-REPORT-IND-R     TO DP132O-REPORT-NUMBER.                 
                                                                                
           WRITE SKU-RECLS-SUM-RPT1-REC                                         
             FROM DP132O-STANDRD-HEADER-132-COLS                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           EVALUATE IN145-REPORT-IND                                            
             WHEN '1'                                                           
             WHEN '2'                                                           
               PERFORM 7010-PRINT-HEADINGS-1                                    
             WHEN OTHER                                                         
               MOVE '7000-PRINT-HEADINGS   '  TO PV-PARAGRAPH-NAME              
               PERFORM 9999-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'BY STORE' REPORT                                          
      *----------------------------------------------------------------         
       7010-PRINT-HEADINGS-1.                                                   
           MOVE PC-MAIN-HEAD-2         TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-2B        TO HD01-HEAD-2                           
                                                                                
           WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-HEAD-01                     
               AFTER ADVANCING 1 LINES.                                         
           ADD 1                       TO PA-LINE-CNT.                          
                                                                                
           MOVE DTATTR-FSCL-WK-BEG-DTE TO HD02-DATE.                            
           WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-HEAD-02                     
               AFTER ADVANCING 1 LINES.                                         
           ADD 1                       TO PA-LINE-CNT.                          
                                                                                
           IF IN-KEY NOT EQUAL HIGH-VALUES                                      
               PERFORM 7090-GENERAL-HEADINGS.                                   
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'BY STORE' REPORT                                          
      *----------------------------------------------------------------         
       7090-GENERAL-HEADINGS.                                                   
                                                                                
           WRITE SKU-RECLS-SUM-RPT1-REC FROM REPORT-HEAD-A1                     
               AFTER ADVANCING 3 LINES.                                         
           ADD 3                       TO PA-LINE-CNT.                          
                                                                                
           SET PS-DOUBLE-SPACE         TO TRUE.                                 
      *----------------------------------------------------------------         
      *  HEADINGS FOR 'FROM AND TO DEPT' SUMMARY REPORTS                        
      *----------------------------------------------------------------         
       7500-PRINT-SUM-HEADS-1.                                                  
                                                                                
           EVALUATE IN145-REPORT-IND                                            
             WHEN '1'                                                           
             WHEN '2'                                                           
               MOVE 2               TO DP132O-REPORT-NUMBER                     
             WHEN OTHER                                                         
               MOVE '7500-PRINT-SUM-HEADS-1'  TO PV-PARAGRAPH-NAME              
               PERFORM 9999-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
           WRITE SKU-RECLS-SUM-RPT2-REC                                         
             FROM DP132O-STANDRD-HEADER-132-COLS                                
               AFTER ADVANCING PAGE.                                            
           ADD 1                       TO PA-SUM-PAGE-CNT.                      
           MOVE 1                      TO PA-SUM-LINE-CNT.                      
                                                                                
           EVALUATE IN145-REPORT-IND                                            
             WHEN '1'                                                           
             WHEN '2'                                                           
               PERFORM 7510-PRINT-SUM-HEADS-1                                   
             WHEN OTHER                                                         
               CONTINUE                                                         
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR 'TO DEPT' SUMMARY REPORTS                                  
      *----------------------------------------------------------------         
       7510-PRINT-SUM-HEADS-1.                                                  
           MOVE PC-MAIN-HEAD-2         TO HD01-HEAD-1.                          
           MOVE PC-MAIN-HEAD-2C        TO HD01-HEAD-2.                          
           WRITE SKU-RECLS-SUM-RPT2-REC FROM REPORT-HEAD-01                     
               AFTER ADVANCING 1 LINES.                                         
           MOVE 1                      TO PA-SUM-LINE-CNT.                      
                                                                                
           MOVE DTATTR-FSCL-WK-BEG-DTE TO HD02-DATE.                            
           WRITE SKU-RECLS-SUM-RPT2-REC FROM REPORT-HEAD-02                     
               AFTER ADVANCING 1 LINES.                                         
           ADD 1                       TO PA-SUM-LINE-CNT.                      
           PERFORM 7550-GENL-SUM-HEADINGS.                                      
                                                                                
      *----------------------------------------------------------------         
      * HEADINGS FOR SUMMARY REPORTS                                            
      *----------------------------------------------------------------         
       7550-GENL-SUM-HEADINGS.                                                  
                                                                                
           WRITE SKU-RECLS-SUM-RPT2-REC FROM REPORT-HEAD-A2                     
               AFTER ADVANCING 3 LINES.                                         
           ADD 3                       TO PA-SUM-LINE-CNT.                      
                                                                                
           WRITE SKU-RECLS-SUM-RPT2-REC FROM BLANK-LINE                         
               AFTER ADVANCING 1 LINES.                                         
                                                                                
           ADD 1                       TO PA-SUM-LINE-CNT.                      
                                                                                
       EJECT                                                                    
       9999-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND **         ** = '.                                 
           DISPLAY '** REPORT INDICATOR ** = ' IN145-REPORT-IND.                
           DISPLAY '** PROGRAM          ** = INKRP151'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           MOVE +4003                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
       EJECT                                                                    
                                                                                
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      *--------------------------------------------------------------*          
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      *--------------------------------------------------------------*          
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
