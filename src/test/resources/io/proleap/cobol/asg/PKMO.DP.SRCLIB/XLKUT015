      * ------------------------------------------------------------- *         
       IDENTIFICATION DIVISION.                                                 
      * ------------------------------------------------------------- *         
       PROGRAM-ID.      XLKUT015.                                               
       AUTHOR.          TKMA536.    .                                           
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    07-24-2002.                                             
                                                                                
      *****************************************************************         
      *********      COPY FILE TO MESSAGE QUEUE            ************         
      *****************************************************************         
      *  This program puts messages from sequential file to queue.    *         
      *                                                               *         
      *                       Program  Logic                          *         
      *                       --------------                          *         
      *  main                                                         *         
      *  ----                                                         *         
      *                                                               *         
      *   Connect to the queue manager.                               *         
      *   If connection failed then                                   *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE and exit           *         
      *                                                               *         
      *   Open the queue manager           (MQOPEN).                  *         
      *   If open failed then                                         *         
      *            Disconnect from queue manager                      *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE and exit           *         
      *   Endif.                                                      *         
      *                                                               *         
      *   Close the queue manager.                                    *         
      *   If close failed then                                        *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *                                                               *         
      *   Open the specified message queue (MQOPEN).                  *         
      *   If open failed then                                         *         
      *            Disconnect from queue manager                      *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE and exit           *         
      *   Endif.                                                      *         
      *                                                               *         
      *   Close the message queue.                                    *         
      *   If close failed then                                        *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *   Set the put message options.                                *         
      *            Put message to queue (MQPUT)                       *         
      *            If put failed                                      *         
      *                     Call 9000-DISPLAY-ERROR-MESSAGE           *         
      *                     Break from loop                           *         
      *            Endif                                              *         
      *                                                               *         
      *   Close the message queue.                                    *         
      *   If close failed then                                        *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *   Disconnect from the queue manager.                          *         
      *   If disconnect failed then                                   *         
      *            Call 9000-DISPLAY-ERROR-MESSAGE.                   *         
      *                                                               *         
      *   Exit program.                                               *         
      *                                                               *         
      *****************************************************************         
      * ------------------------------------------------------------- *         
       ENVIRONMENT DIVISION.                                                    
      * ------------------------------------------------------------- *         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.       IBM-3090.                                         
       OBJECT-COMPUTER.       IBM-3090.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT CONTROL-CARD-1                                                
                  ASSIGN TO INP01.                                              
           SELECT CONTROL-CARD-2                                                
                  ASSIGN TO INP02.                                              
           SELECT MSG-EXTRACT                                                   
                  ASSIGN TO INP03.                                              
                                                                                
      * ------------------------------------------------------------- *         
       DATA DIVISION.                                                           
      * ------------------------------------------------------------- *         
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-1                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-1-LINE              pic  x(80).                         
                                                                                
       FD  CONTROL-CARD-2                                                       
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  CONTROL-CARD-2-LINE              pic  x(80).                         
                                                                                
       FD  MSG-EXTRACT                                                          
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  MSG-EXTRACT-REC                  PIC X(450).                         
                                                                                
      * ------------------------------------------------------------- *         
       WORKING-STORAGE SECTION.                                                 
      * ------------------------------------------------------------- *         
                                                                                
      *  Abend codes                                                            
       01  PV-ABEND-VARIABLES.                                                  
           05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
           88  ABEND-4019-CAL-SUB-ERROR                    VALUE +4019.         
           88  ABEND-4020-MQ-ERROR                         VALUE +4020.         
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444.         
                                                                                
                                                                                
        1  PE-MQ-ABEND-VARIABLES.                                               
           10  PE-MQ-QMANAGER           PIC X(05) VALUE SPACES.                 
           10  PE-MQ-QNAME              PIC X(30) VALUE SPACES.                 
           10  PE-MQ-COMPLETION-CODE    PIC 9(04) VALUE ZEROS.                  
           10  PE-MQ-REASON-CODE        PIC 9(04) VALUE ZEROS.                  
                                                                                
                                                                                
       01  WS-MQCONN               PIC X(8)  VALUE 'CSQBCONN'.                  
       01  WS-MQOPEN               PIC X(8)  VALUE 'CSQBOPEN'.                  
       01  WS-MQINQ                PIC X(8)  VALUE 'CSQBINQ'.                   
       01  WS-MQPUT                PIC X(8)  VALUE 'CSQBPUT'.                   
       01  WS-MQCLOSE              PIC X(8)  VALUE 'CSQBCLOS'.                  
       01  WS-MQDISC               PIC X(8)  VALUE 'CSQBDISC'.                  
       01  WS-MQCHECK              PIC X(8)  VALUE 'MQR2C'.                     
       01  WS-MQCMIT               PIC X(8)  VALUE 'CSQBCOMM'.                  
       01  WS-MQBACK               PIC X(8)  VALUE 'CSQBBACK'.                  
      *                                                                         
      *    General work fields                                                  
      *                                                                         
       01  NUMEXTS                 PIC 9(9)  VALUE ZEROS.                       
       01  NUMRECS                 PIC 9(9)  VALUE ZEROS.                       
       01  NUMPUTS                 PIC S9(9) BINARY VALUE 0.                    
       01  NUMCMTS                 PIC 9(9)  VALUE ZEROS.                       
       01  ERROR-MESSAGE           PIC X(48) VALUE SPACES.                      
       01  PV-PARAGRAPH            PIC X(48) VALUE SPACES.                      
       01  WS-MQ-TEXT-REASON       PIC X(30) VALUE SPACES.                      
       01  WS-MSG-EXTRACT-REC      PIC X(450) VALUE SPACES.                     
                                                                                
      *   Parameter variables                                                   
                                                                                
       01  QM-NAME               PIC X(48).                                     
       01  QNAME                 PIC X(48).                                     
       01  PADCHAR               PIC X(1) VALUE '*'.                            
       01  MSGBUFFER             PIC X(450) VALUE SPACES.                       
       01  MSGLENGTH             PIC S9(9) BINARY.                              
       01  MQ-REASON-TEXT        PIC X(30) VALUE SPACES.                        
       01  HOLD-REASON           PIC S9(9) BINARY.                              
       01  HOLD-COMPLETION-CODE  PIC S9(9) BINARY.                              
                                                                                
      *    API fields                                                           
                                                                                
       01  HCONN                   PIC S9(9) BINARY VALUE 0.                    
       01  PQ-HANDLE               PIC S9(9) BINARY VALUE 0.                    
       01  OPEN-OPTIONS            PIC S9(9) BINARY.                            
       01  COMPLETION-CODE         PIC S9(9) BINARY.                            
       01  REASON                  PIC S9(9) BINARY.                            
       01  CON-REASON              PIC S9(9) BINARY.                            
      *                                                                         
      *    API control blocks                                                   
      *                                                                         
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
      *                                                                         
      *    MQV contains constants (for filling in the control blocks)           
      *    and return codes (for testing the result of a call)                  
      *                                                                         
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
      *                                                                         
      *----------------------------------------------------------------*        
      *    CONTROL CARD                                                         
      *----------------------------------------------------------------*        
       01  CC-CONTROL-CARD-1.                                                   
           05  CC-QMGR-NAME              PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
       01  CC-CONTROL-CARD-2.                                                   
           05  CC-DATA-COLLECT-QNAME     PIC X(48).                             
           05  FILLER                    PIC X(32).                             
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM CONSTANTS                                                    
      *----------------------------------------------------------------*        
           05  PC-PGM-NAME               PIC X(09) VALUE 'XLKUT015 '.           
           05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
                                                                                
      *----------------------------------------------------------------*        
      *    PROGRAM SWITCHES                                                     
      *----------------------------------------------------------------*        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-1-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-1-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-1-START         VALUE 'N'.                   
           05  PS-CONTROL-CARD-2-SW      PIC X(01) VALUE 'N'.                   
               88  PS-CONTROL-CARD-2-END           VALUE 'Y'.                   
               88  PS-CONTROL-CARD-2-START         VALUE 'N'.                   
           05  PS-END-OF-MSG-SW          PIC X(01) VALUE 'N'.                   
               88  PS-END-OF-MSG-YES               VALUE 'Y'.                   
               88  PS-END-OF-MSG-NO                VALUE 'N'.                   
      * ------------------------------------------------------------- *         
       PROCEDURE DIVISION.                                                      
      * ------------------------------------------------------------- *         
       1000-MAIN SECTION.                                                       
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET QUEUE MANAGER NAME                  *         
      * ------------------------------------------------------------- *         
            SET PS-CONTROL-CARD-1-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-1.                                          
            READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1                          
                AT END                                                          
                    SET PS-CONTROL-CARD-1-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-1-END                                            
                DISPLAY PC-PGM-NAME                                             
                    'CONTROL CARD MISSING - FOR QMGR NAME'                      
                GO TO 4000-END-PROGRAM                                          
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-1                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-1.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  READ CONTROL CARD TO GET DATA COLLECT QUEUE NAME             *         
      * ------------------------------------------------------------- *         
                                                                                
            SET PS-CONTROL-CARD-2-START TO TRUE.                                
            OPEN INPUT CONTROL-CARD-2.                                          
            READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2                          
                AT END                                                          
                    SET PS-CONTROL-CARD-2-END                                   
                                        TO TRUE.                                
                                                                                
            IF PS-CONTROL-CARD-2-END                                            
                DISPLAY PC-PGM-NAME                                             
                   'CONTROL CARD MISSING - FOR DATA COLLECT QUEUE NAME'         
                GO TO 4000-END-PROGRAM                                          
            ELSE                                                                
                DISPLAY PC-PGM-NAME                                             
                        '- '                                                    
                        PC-FIFTY-STARS                                          
                DISPLAY PC-PGM-NAME                                             
                        '- CONTROL CARD: '                                      
                        CC-CONTROL-CARD-2                                       
            END-IF.                                                             
                                                                                
            CLOSE CONTROL-CARD-2.                                               
                                                                                
      * ------------------------------------------------------------- *         
      *  OPEN THE MESSAGE EXTRACT FILE                                *         
      * ------------------------------------------------------------- *         
                                                                                
            OPEN INPUT MSG-EXTRACT.                                             
                                                                                
      * ------------------------------------------------------------- *         
      *                                                                         
      *    Connect to the queue manager                                         
      *                                                                         
           MOVE CC-QMGR-NAME TO QM-NAME.                                        
           CALL WS-MQCONN USING QM-NAME                                         
                               HCONN                                            
                               COMPLETION-CODE                                  
                               REASON.                                          
      *                                                                         
      *    If connection failed then display error message                      
      *    and exit                                                             
      *                                                                         
           MOVE REASON TO CON-REASON.                                           
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              MOVE 'MQCONN ERROR'   TO ERROR-MESSAGE                            
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 4000-END-PROGRAM                                            
           END-IF.                                                              
           DISPLAY 'MQCONN SUCCESSFUL TO ', QM-NAME.                            
                                                                                
      *    Open the queue for output                                            
                                                                                
           COMPUTE OPEN-OPTIONS = MQOO-OUTPUT +                                 
                                  MQOO-FAIL-IF-QUIESCING +                      
                                  MQOO-SET-IDENTITY-CONTEXT.                    
           MOVE CC-DATA-COLLECT-QNAME    TO MQOD-OBJECTNAME.                    
           MOVE QM-NAME                  TO MQOD-OBJECTQMGRNAME.                
                                                                                
           CALL WS-MQOPEN USING HCONN                                           
                               MQM-OBJECT-DESCRIPTOR                            
                               OPEN-OPTIONS                                     
                               PQ-HANDLE                                        
                               COMPLETION-CODE                                  
                               REASON.                                          
      *                                                                         
      *    If open failed then display error message                            
      *    and exit.                                                            
      *                                                                         
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              CALL WS-MQCHECK USING REASON                                      
                                    WS-MQ-TEXT-REASON                           
              MOVE 'MQOPEN ERROR'   TO ERROR-MESSAGE                            
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
              GO TO 3000-DISCONNECT-QMGR                                        
           END-IF.                                                              
           DISPLAY 'MQOPEN SUCCESSFUL'.                                         
                                                                                
           PERFORM 8000-READ-MSG-EXTRACT.                                       
           PERFORM 2000-MQPUT-MSG UNTIL PS-END-OF-MSG-YES.                      
                                                                                
      *    Close the queue                                                      
                                                                                
           CALL WS-MQCLOSE USING HCONN                                          
                                PQ-HANDLE                                       
                                MQCO-NONE                                       
                                COMPLETION-CODE                                 
                                REASON.                                         
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              CALL WS-MQCHECK USING REASON                                      
                                    WS-MQ-TEXT-REASON                           
              MOVE 'MQCLOSE ERROR'  TO ERROR-MESSAGE                            
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              DISPLAY 'MQCLOSE SUCCESSFUL'                                      
           END-IF.                                                              
      *                                                                         
           PERFORM 3000-DISCONNECT-QMGR.                                        
           GO TO 4000-END-PROGRAM.                                              
      *                                                                         
      * ------------------------------------------------------------- *         
      * PUT MESSAGE ON LOCAL QUEUE TO END THE DATA COLLECT PROCESS              
      * ------------------------------------------------------------- *         
       2000-MQPUT-MSG.                                                          
      *                                                                         
      *    Set persistence                                                      
      *                                                                         
           MOVE MQMI-NONE TO MQMD-MSGID.                                        
      *    MOVE MQCI-NONE TO MQMD-CORRELID.                                     
           MOVE 'XLKUT015 '            TO MQMD-CORRELID.                        
           MOVE 'USERIDENTFIR'         TO MQMD-USERIDENTIFIER.                  
           MOVE 'PUTAPPLNAME'          TO MQMD-PUTAPPLNAME.                     
           MOVE 'APPLIDENTITYDATA'     TO MQMD-APPLIDENTITYDATA.                
           MOVE +450                   TO MSGLENGTH.                            
           MOVE WS-MSG-EXTRACT-REC     TO MSGBUFFER.                            
           MOVE MQFMT-STRING           TO MQMD-FORMAT.                          
           MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.                     
           COMPUTE MQPMO-OPTIONS  = MQPMO-SYNCPOINT +                           
                                    MQPMO-SET-IDENTITY-CONTEXT.                 
      *                                                                         
           CALL WS-MQPUT USING HCONN                                            
                              PQ-HANDLE                                         
                              MQMD                                              
                              MQPMO                                             
                              MSGLENGTH                                         
                              MSGBUFFER                                         
                              COMPLETION-CODE                                   
                              REASON                                            
      *                                                                         
      *        If put failed then display error message                         
      *        and break out of loop                                            
      *                                                                         
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
              CALL WS-MQCHECK USING REASON                                      
                                    WS-MQ-TEXT-REASON                           
              MOVE 'MQPUT ERROR'     TO ERROR-MESSAGE                           
              PERFORM 9000-DISPLAY-ERROR-MESSAGE                                
           ELSE                                                                 
              ADD 1 TO NUMRECS                                                  
                  IF NUMRECS > 1000                                             
                      PERFORM 3600-MQ-COMMIT                                    
                      MOVE ZERO TO NUMRECS                                      
                  END-IF                                                        
              ADD 1 TO NUMPUTS                                                  
              PERFORM 8000-READ-MSG-EXTRACT                                     
           END-IF.                                                              
      *                                                                         
      * 2000-END-MQPUT-MSG.                                                     
      *                                                                         
      * ------------------------------------------------------------- *         
      * CLOSE AND DISCONNECT FROM THE MQ QUEUE MANAGER                          
      * ------------------------------------------------------------- *         
       3000-DISCONNECT-QMGR.                                                    
      *                                                                         
      *    Disconnect from the queue manager                                    
      *                                                                         
           IF CON-REASON IS NOT EQUAL TO MQRC-ALREADY-CONNECTED                 
              CALL WS-MQDISC USING HCONN                                        
                                  COMPLETION-CODE                               
                                  REASON                                        
              IF (COMPLETION-CODE NOT = MQCC-OK) THEN                           
                 MOVE 'MQDISC ERROR'   TO ERROR-MESSAGE                         
                 PERFORM 9000-DISPLAY-ERROR-MESSAGE                             
              ELSE                                                              
                 DISPLAY 'MQDISC SUCCESSFUL'                                    
              END-IF                                                            
           END-IF.                                                              
      *                                                                         
      * 3000-END-DISCONNECT.                                                    
      ******************************************************************        
      *  COMMIT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT                
      ******************************************************************        
       3600-MQ-COMMIT.                                                          
                                                                                
           CALL WS-MQCMIT USING HCONN                                           
                                COMPLETION-CODE                                 
                                REASON.                                         
                                                                                
           IF (COMPLETION-CODE NOT = MQCC-OK)                                   
               CALL WS-MQCHECK USING REASON                                     
                                     MQ-REASON-TEXT                             
              MOVE '3600-COMMIT-MQ'   TO PV-PARAGRAPH                           
              MOVE 'MQCMIT ERROR'     TO ERROR-MESSAGE                          
              MOVE COMPLETION-CODE    TO HOLD-COMPLETION-CODE                   
              MOVE REASON             TO HOLD-REASON                            
              PERFORM 3700-BACK-OUT-MQ                                          
              MOVE HOLD-COMPLETION-CODE TO COMPLETION-CODE                      
                                              PE-MQ-COMPLETION-CODE             
              MOVE HOLD-REASON          TO REASON                               
                                              PE-MQ-REASON-CODE                 
              PERFORM 9200-MQ-ABEND                                             
           ELSE                                                                 
              ADD +1 TO NUMCMTS                                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT               
      ******************************************************************        
       3700-BACK-OUT-MQ.                                                        
                                                                                
           CALL WS-MQBACK USING HCONN                                           
                                COMPLETION-CODE                                 
                                REASON.                                         
                                                                                
           IF (COMPLETION-CODE NOT = MQCC-OK) THEN                              
               CALL WS-MQCHECK USING REASON                                     
                                  MQ-REASON-TEXT                                
              MOVE '3700-BACK-OUT-MQ'  TO PV-PARAGRAPH                          
              MOVE 'MQBACK ERROR'      TO ERROR-MESSAGE                         
              PERFORM 9200-MQ-ABEND                                             
           END-IF.                                                              
      ******************************************************************        
      * ------------------------------------------------------------- *         
      * END THE PROGRAM                                                         
      * ------------------------------------------------------------- *         
       4000-END-PROGRAM.                                                        
                                                                                
      *    Close the message extract file.                                      
           CLOSE MSG-EXTRACT.                                                   
      *                                                                         
      *    Display the number of messages successfully put                      
      *    to the queue                                                         
      *                                                                         
           DISPLAY NUMEXTS, ' MSGS READ FROM FILE  '.                           
           DISPLAY NUMPUTS, ' MESSAGES PUT TO QUEUE'.                           
           DISPLAY NUMCMTS, ' NO. OF COMMITS TAKEN '.                           
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
      * ------------------------------------------------------------- *         
      * READ THE MESSAGE EXTRACT FILE                                           
      * ------------------------------------------------------------- *         
       8000-READ-MSG-EXTRACT.                                                   
           MOVE SPACE TO WS-MSG-EXTRACT-REC.                                    
                                                                                
           ADD 1 TO NUMEXTS.                                                    
                                                                                
           READ MSG-EXTRACT                                                     
               INTO WS-MSG-EXTRACT-REC                                          
                   AT END                                                       
                       SUBTRACT 1 FROM NUMEXTS                                  
                       SET PS-END-OF-MSG-YES TO TRUE.                           
      *                                                                         
      * ------------------------------------------------------------- *         
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND                           
      * ------------------------------------------------------------- *         
       9000-DISPLAY-ERROR-MESSAGE.                                              
      * ------------------------------------------------------------- *         
      *                                                                         
           DISPLAY '**** ABEND *************************************'.          
           DISPLAY '* ', PC-PGM-NAME.                                           
           DISPLAY '* ', ERROR-MESSAGE.                                         
           DISPLAY '* COMPLETION CODE : ', COMPLETION-CODE.                     
           DISPLAY '* REASON CODE     : ', REASON.                              
           DISPLAY '* TEXT REASON     : ', WS-MQ-TEXT-REASON.                   
           DISPLAY '************************************************'.          
           MOVE +4020 TO PV-ABEND-CODE.                                 04354099
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         04355099
      *                                                                         
      * 9000-DISPLAY-ERROR-MSG-END.                                             
      ****************************************************************          
      ** PERFORM MQ-SERIES ABEND PROCESSING                                     
      ****************************************************************          
       9200-MQ-ABEND.                                                           
                                                                                
           DISPLAY '**** ABEND *************************************'.          
           DISPLAY '** MQSERIES ERROR **'.                                      
           DISPLAY '** PROGRAM:    ', PC-PGM-NAME.                              
           DISPLAY '** ABEND CODE: ', PV-ABEND-CODE.                            
           DISPLAY '** PARAGRAPH:  ', PV-PARAGRAPH.                             
           DISPLAY '** MESSAGE:    ', ERROR-MESSAGE.                            
           DISPLAY '** COMP CODE:  ', PE-MQ-COMPLETION-CODE.                    
           DISPLAY '** RSN CODE:   ', PE-MQ-REASON-CODE.                        
           DISPLAY '** RSN TEXT:   ', MQ-REASON-TEXT.                           
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
                                                                                
