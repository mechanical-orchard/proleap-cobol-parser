       ID  DIVISION.                                                            
       PROGRAM-ID.         INKRP108                                             
       AUTHOR.             TROY BIRK.                                           
       INSTALLATION.       KOHLS DEPATMENT STORES.                              
       DATE-WRITTEN.       NOVEMBER 1993.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * 03/94  M BLAZICH                                                        
      *        CHANGED LRECLS FOR ADDITIONAL SCAN-LINE-NBR FIELD                
      ******************************************************************        
      * 02/12/98  SCOTT JACKSON  WR#9999                               *        
      *      ADDED A SWITCH TO CHECK IF THIS IS THE FIRST TIME THROUGH *        
      *      THE EXCEPTION REPORT TO ENSURE THE CORRECT DEPT DESC IS   *        
      *      DISPLAYED ON THE FIRST EXCEPTION.                         *        
      ******************************************************************        
      * 08/26/99  TESS BESTE     WR#20362                              *        
      *      CORRECTED THREE PROBLEMS WITH THE BUYER AND DEPARTMENT-   *        
      *      CLASS INFORMATION IN THE EXCEPTION REPORT HEADINGS:       *        
      *      1) THE HEADING FOR THE VERY FIRST DEPARTMENT-CLASS ON     *        
      *         THE REPORT CONTAINED THE DEPARTMENT DESCRIPTION        *        
      *         INSTEAD OF THE DEPARTMENT-CLASS DESCRIPTION.           *        
      *      2) WHEN THE FIRST DEPARTMENT-CLASS WITHIN A STORE HAD NO  *        
      *         EXCEPTIONS, ITS DESCRIPTION WOULD BE PRINTED IN THE    *        
      *         HEADING OF THE FIRST DEPARTMENT-CLASS WITHIN THAT      *        
      *         STORE THAT DID HAVE EXCEPTIONS.                        *        
      *      3) THE BUYER AND DEPARTMENT-CLASS INFORMATION WAS NOT     *        
      *         GETTING BLANKED OUT IN THE HEADING ON THE STORE        *        
      *         TOTAL PAGE.                                            *        
      *      CORRECTED THE PROBLEMS MOSTLY BY CHANGING THE CONTROL     *        
      *      BREAK LOGIC. THIS INCLUDED CONSOLIDATING PARTS OF THE     *        
      *      CONTROL BREAK LOGIC INTO THE D???-PREPARE-FOR-NEXT...     *        
      *      PARAGRAPHS.                                               *        
      ******************************************************************        
W21732* 11/13/2002 CHANGED BY IGSI           WR# W21732                *        
W21732*    CHANGED THE REPORT COLUMN HEADINGS FROM 'SEQ' TO 'SKU' AND  *        
W21732*    REPORT IS EXPANDED TO 150 CHARACTERS WIDE.AND ALSO THE      *        
W21732*    DEPARTMENT AND CLASS IN THE REPORT HEADER ARE EXPANDED TO   *        
W21732*    4 DIGITS.                                                   *        
25248 ******************************************************************        
25248 * 25248  10-06-2003  UPC CAPABILITIES                                     
25248 *                    -ROD JANSSEN                                         
W26600* W26600 06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                   
W26600*                    REPORT LOCATION ID (STORE NUMBER) CHANGED            
W26600*                    FROM 3 TO 4 BYTES.                                   
W26600*                    M JOHNSON                                            
W26600*                                                                         
W26006* 26006  02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.           
W26006*                    -ERIN SEARL                                          
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
SR3403* WR3403 06-17-2012 JALAJ KUMAR  - CHANGES FOR LARGE RETAIL               
SR3403*                                  EXPANSION                              
W26006******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT PHY-INVENTORY-FILE                                            
               ASSIGN TO INP01.                                                 
           SELECT PHY-INVENTORY-RPT-FILE                                        
               ASSIGN TO RPT01.                                                 
           SKIP3                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  PHY-INVENTORY-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
W21732 01  PHY-INVENTORY-RECORD         PIC X(100).                             
                                                                                
       FD  PHY-INVENTORY-RPT-FILE                                               
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
W21732 01  PHY-INVENTORY-RPT-RECORD     PIC X(150).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
       01  SA-SAVE-AREAS.                                                       
W26600     05  SA-STORE-NO             PIC X(04)   VALUE SPACES.                
25248      05  SA-STORE-NO-NUM REDEFINES                                INKRP101
W26600         SA-STORE-NO             PIC 9(04).                       INKRP101
           05  SA-BUYER-NUMBER         PIC X(03)   VALUE SPACES.                
           05  SA-DMA                  PIC X(02)   VALUE SPACES.                
           05  SA-DEPT-CLASS.                                                   
W21732         10  SA-DEPT             PIC X(04)   VALUE SPACES.                
W21732         10  SA-CLASS            PIC X(04)   VALUE SPACES.                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-SQL-SUB              PIC 9(03)    VALUE ZERO.                 
           05  PV-FIRST-NAME           PIC X(12)    VALUE SPACES.               
           05  FILLER REDEFINES PV-FIRST-NAME  OCCURS 12 TIMES.                 
               10 PV-FNAME-DIGIT       PIC X(01).                               
           05  PV-LAST-NAME            PIC X(18)    VALUE SPACES.               
           05  FILLER REDEFINES PV-LAST-NAME  OCCURS 18 TIMES.                  
               10 PV-LNAME-DIGIT       PIC X(01).                               
           05  PV-FULL-NAME            PIC X(20)    VALUE SPACES.               
           05  FILLER REDEFINES PV-FULL-NAME  OCCURS 20 TIMES.                  
               10 PV-FULLNAME-DIGIT    PIC X(01).                               
25248      05 PV-EDIT-LOC-NBR          PIC S9(04) COMP VALUE ZEROS.             
25248      05 PV-EDIT-UPC-CODE         PIC 9(15)     VALUE ZEROS.               
25248      05 PV-EDIT-UPC-CODE-ALPHA REDEFINES                                  
25248         PV-EDIT-UPC-CODE         PIC X(15).                               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                   COMP-3.                    
           05  PA-EXTENDED-RETAIL      PIC S9(09)V99 VALUE ZERO.                
           05  PA-CLASS-QTY            PIC S9(09)    VALUE ZERO.                
           05  PA-DEPT-QTY             PIC S9(09)    VALUE ZERO.                
           05  PA-BUYER-QTY            PIC S9(09)    VALUE ZERO.                
           05  PA-DMA-QTY              PIC S9(09)    VALUE ZERO.                
           05  PA-FINAL-QTY            PIC S9(09)    VALUE ZERO.                
           05  PA-CLASS-RETAIL         PIC S9(09)V99 VALUE ZERO.                
           05  PA-DEPT-RETAIL          PIC S9(09)V99 VALUE ZERO.                
           05  PA-BUYER-RETAIL         PIC S9(09)V99 VALUE ZERO.                
           05  PA-DMA-RETAIL           PIC S9(09)V99 VALUE ZERO.                
           05  PA-FINAL-RETAIL         PIC S9(09)V99 VALUE ZERO.                
           05  PA-PAGE-COUNT           PIC S9(05)    VALUE ZERO.                
JAN04      05  PA-PAGE-DIV             PIC S9(05)    VALUE ZERO.                
JAN04      05  PA-PAGE-REM             PIC S9(03)    VALUE ZERO.                
                                                                                
       01  FF-FETCH-FIELDS.                                                     
           05  FF-STORE-NAME           PIC X(25)   VALUE SPACES.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-SW      PIC X       VALUE 'N'.                   
               88  PS-END-OF-INPUT                 VALUE 'Y'.                   
           05  PS-DONE-SW              PIC X       VALUE 'N'.                   
               88  PS-DONE                         VALUE 'Y'.                   
               88  PS-NOT-DONE                     VALUE 'N'.                   
           05  PS-FIRST-EXCP-FOR-STORE-SW                                       
                                       PIC X       VALUE 'Y'.                   
               88  PS-FIRST-EXCP-FOR-STORE         VALUE 'Y'.                   
               88  PS-NOT-FIRST-EXCP-FOR-STORE     VALUE 'N'.                   
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-EXCEPTION-LIMIT              PIC 9(06)V99 VALUE 0.            
           05  FILLER                          PIC X(72)   VALUE SPACES.        
                                                                                
       01  CPU-DATE.                                                            
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.          
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.          
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.          
           05  FILLER                      PIC X(02)     VALUE SPACES.          
                                                                                
       01  DK-DATABASE-KEYS.                                                    
           05  DK-DEPT                      PIC X(03)   VALUE SPACES.           
                                                                                
       01  ID-INVENTORY-DATE.                                                   
           05  ID-MONTH                    PIC X(02)     VALUE SPACES.          
           05  ID-DAY                      PIC X(02)     VALUE SPACES.          
           05  ID-YEAR                     PIC X(02)     VALUE SPACES.          
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-ADVANCE              PIC S9(03)  VALUE ZERO COMP-3.           
           05  PC-LINE-COUNT           PIC S9(03)  VALUE ZERO COMP-3.           
           05  PC-LINE-MAX             PIC S9(03)  VALUE +57  COMP-3.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-OPERCO-NBR               PIC X(02)   VALUE '03'.              
           05  PC-BYR                      PIC X(03)   VALUE 'BYR'.             
           05  PC-DPT                      PIC X(03)   VALUE 'DPT'.             
           05  PC-MAX-RETRIES              PIC 9(03)   VALUE 3.                 
           05  PC-END-FIRST-NAME           PIC 9(02)   VALUE 12.                
           05  PC-END-LAST-NAME            PIC 9(02)   VALUE 18.                
25248      05  PC-ENTRY-MAX                PIC S9(02)    VALUE +02.             
           05  PC-DETAIL-MAX               PIC S9(02)    VALUE +50.             
           05  PC-CLASS                    PIC X(05)     VALUE                  
           'CLASS'.                                                             
           05  PC-DEPT                     PIC X(05)     VALUE                  
           'DEPT '.                                                             
           05  PC-BUYER                    PIC X(05)     VALUE                  
           'BUYER'.                                                             
           05  PC-DMA                      PIC X(05)     VALUE                  
           'DMA  '.                                                             
           05  PC-FINAL                    PIC X(05)     VALUE                  
           'FINAL'.                                                             
                                                                                
       01  DT-DETAIL-TABLE.                                                     
25248      05  DT-ENTRY OCCURS 2.                                               
               10  DT-DETAIL OCCURS 50.                                         
                   15  DT-AREA             PIC  9(06).                          
                   15  DT-LINE             PIC  9(06).                          
W26600             15  DT-SKU              PIC  X(08).                          
25248              15  DT-UPC-SKU          PIC  X(15).                          
SR3403             15  DT-PRICE            PIC  9(05)V99.                       
                   15  DT-QTY              PIC  9(04).                          
SR3403             15  DT-EXT-RTL          PIC S9(08)V99.                       
                                                                                
       01  SS-SUBSCRIPTS.                                                       
           05  SS-ENTRY-SUB        PIC S9(04)  VALUE ZERO COMP SYNC.            
           05  SS-DETAIL-SUB       PIC S9(04)  VALUE ZERO COMP SYNC.            
           05  SS-SUB              PIC S9(04)  VALUE ZERO COMP SYNC.            
           05  SS-FIRST-SUB                    PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
           05  SS-LAST-SUB                     PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
           05  SS-TEMP-SUB1                    PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
           05  SS-TEMP-SUB2                    PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
       01  H1-HEADING.                                                          
W26600     05  FILLER                      PIC  X(01)      VALUE SPACE.         
           05  H1-DEPT-CLASS.                                                   
W26600         10  H1-DEPT                 PIC  X(04)      VALUE SPACE.         
W26600         10  FILLER                  PIC  X(01)      VALUE '-'.           
W26600         10  H1-CLASS                PIC  X(04)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(01)      VALUE SPACE.         
W26600     05  H1-CLASS-DESC               PIC  X(20)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(06)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(27)      VALUE                
W26600         'EXCEPTION REPORT FOR STORE '.                                   
W26600     05  H1-STORE-NO                 PIC  X(04)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(01)      VALUE SPACE.         
W26600     05  H1-STORE-NAME               PIC  X(10)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(07)      VALUE                
W26600         ' TAKEN '.                                                       
W26600     05  H1-INV-DATE                 PIC  X(10)      VALUE SPACE.         
                                                                                
       01  H2-HEADING.                                                          
W26600     05  FILLER                      PIC  X(01)      VALUE SPACE.         
W26600     05  H2-BUYER-NO                 PIC  X(03)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(02)      VALUE SPACE.         
W26600     05  H2-BUYER-NAME               PIC  X(20)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(19)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(43)      VALUE                
W26600         'LISTING OF ITEMS BY DEPARTMENT & CLASS CODE'.                   
                                                                                
       01  H5-HEADING.                                                          
W26600     05  FILLER                      PIC  X(42)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(22)      VALUE                
W26600         'RETAIL        EXTENDED'.                                        
W26600     05  FILLER                      PIC  X(42)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(22)      VALUE                
W26600         'RETAIL        EXTENDED'.                                        
                                                                                
       01  H6-HEADING.                                                          
W26600     05  FILLER                      PIC  X(04)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(24)      VALUE                
W26600         'AREA   LINE SKU      UPC'.                                      
W26600     05  FILLER                      PIC  X(15)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(21)      VALUE                
W26600         'PRICE  QTY     RETAIL'.                                         
W26600     05  FILLER                      PIC  X(04)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(24)      VALUE                
W26600         'AREA   LINE SKU      UPC'.                                      
W26600     05  FILLER                      PIC  X(15)      VALUE SPACE.         
W26600     05  FILLER                      PIC  X(21)      VALUE                
W26600         'PRICE  QTY     RETAIL'.                                         
                                                                                
       01  D1-DETAIL-LINE.                                                      
25248      05  D1-ENTRY  OCCURS 2.                                              
SR3403         10  FILLER                  PIC  X(01).                          
W26600         10  D1-AREA                 PIC  ZZZZZZ.                         
W26600         10  FILLER                  PIC  X(01).                          
W26600         10  D1-LINE                 PIC  ZZZZZZ.                         
W26600         10  FILLER                  PIC  X(01).                          
W26600         10  D1-SKU                  PIC  X(08).                          
W26600         10  FILLER                  PIC  X(01).                          
W26600         10  D1-UPC-SKU              PIC  X(15).                          
W26600         10  FILLER                  PIC  X(01).                          
SR3403         10  D1-PRICE                PIC  ZZZZ9.99.                       
SR3403         10  D1-PRICE-RED REDEFINES D1-PRICE                              
SR3403                                     PIC  X(08).                          
W26600         10  FILLER                  PIC  X(01).                          
W26600         10  D1-QTY                  PIC  ZZZZ.                           
W26600         10  FILLER                  PIC  X(01).                          
W26600         10  D1-EXT-RTL              PIC  ZZZZZZ9.99.                     
W26600         10  D1-EXT-RTL-RED REDEFINES D1-EXT-RTL                          
W26600                                     PIC X(10).                           
                                                                                
       01  D2-DETAIL-LINE.                                                      
           05  FILLER              PIC X(35)   VALUE SPACES.                    
W21732     05  D2-DEPT             PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE '-'.                       
W21732     05  D2-CLASS            PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  D2-CLASS-DESC       PIC X(20)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  D2-RETAIL           PIC ZZZ,ZZZ,ZZ9.99.                          
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  D2-UNITS            PIC ZZZ,ZZZ,ZZ9.                             
W21732     05  FILLER              PIC X(43)   VALUE SPACES.                    
                                                                                
       01  T1-TOTAL.                                                            
           05  FILLER              PIC X VALUE SPACES.                          
W21732     05  T1-NAME             PIC X(15) VALUE SPACES.                      
           05  T1-NAME-A REDEFINES T1-NAME.                                     
               10  T1-NAME-B       PIC X(5).                                    
               10  FILLER          PIC X.                                       
W21732         10  T1-NAME-NO      PIC X(9).                                    
               10  FILLER  REDEFINES  T1-NAME-NO.                               
W21732             15  T1-DEPT     PIC XXXX.                                    
                   15  T1-DASH     PIC X.                                       
W21732             15  T1-CLASS    PIC XXXX.                                    
           05  FILLER              PIC X(7) VALUE ' TOTAL '.                    
           05  FILLER              PIC X(9) VALUE 'QUANTITY '.                  
           05  T1-QTY              PIC ZZ,ZZZ,ZZZ.                              
           05  FILLER              PIC X(8) VALUE '  TOTAL '.                   
           05  FILLER              PIC X(7) VALUE 'RETAIL '.                    
           05  T1-RETAIL           PIC ZZ,ZZZ,ZZZ.99.                           
      *                                                                         
JAN04  01  D3-DETAIL-LINE.                                              INKRP106
JAN04      05  FILLER              PIC X(48)   VALUE SPACES.            INKRP106
JAN04      05  FILLER              PIC X(37)   VALUE                    INKRP106
JAN04      'THIS PAGE IS INTENTIONALLY LEFT BLANK'.                     INKRP106
JAN04      05  FILLER              PIC X(47)   VALUE SPACES.            INKRP106
                                                                                
      ******************************************************************        
      * COPY FOR STANDARD HEADING                                               
      ** ***************************************************************        
           COPY DPWS132O.                                                       
      *                                                                         
      ******************************************************************        
      * SQL ABEND ROUTINE WORKING STORAGE                                       
      ** ***************************************************************        
           COPY DPWS004.                                                        
      *                                                                         
      ******************************************************************        
      * INPUT FILE COPY MEMBER                                                  
      ******************************************************************        
           COPY INWS014.                                                        
      *                                                                         
  |   ******************************************************************        
  |   * LOCATION TABLE DCLGEN                                                   
  |   ******************************************************************        
  |        EXEC SQL                                                             
  |            INCLUDE XMT00001                                                 
  |        END-EXEC.                                                            
25248                                                                           
P28545******************************************************************        
P28545* STORE TABLE DCLGEN                                                      
P28545******************************************************************        
P28545*    EXEC SQL                                                             
P28545*        INCLUDE TMDSEAR                                                  
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545******************************************************************        
P28545* STORE TABLE DCLGEN                                                      
P28545******************************************************************        
P28545*    EXEC SQL                                                             
P28545*        INCLUDE TRSPBAR                                                  
P28545*    END-EXEC.                                                            
P28545*                                                                         
      ******************************************************************        
      * DB2 COMMUNICATIONS AREA                                                 
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
P28545******************************************************************        
P28545* COPYBOOK - PRICING MODULE                                               
P28545******************************************************************        
P28545     COPY INWS400.                                                        
P28545                                                                          
           COPY SQLCA2.                                                         
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *  MAIN LINE LOGIC.                                                       
      ******************************************************************        
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B050-EDIT-CONTROL-CARD.                                      
           PERFORM B100-GET-DATE-TIME.                                          
           PERFORM B200-OPEN-FILES.                                             
           PERFORM Z100-READ-INVENTORY-FILE.                                    
                                                                                
           PERFORM B300-PROCESS-STORE  UNTIL                                    
                     PS-END-OF-INPUT.                                           
                                                                                
           PERFORM B400-CLOSE-FILES.                                            
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * ONE TIME ROUTINE TO FORMAT DATE AND TIME IN THE REPORT HEADINGS.        
      ******************************************************************        
       B050-EDIT-CONTROL-CARD.                                                  
                                                                                
           ACCEPT CC-CONTROL-CARD.                                              
W26600     DISPLAY 'INKRP108 - CONTROL CARD = ' CC-CONTROL-CARD.                
                                                                                
           IF CC-EXCEPTION-LIMIT NOT NUMERIC                                    
               DISPLAY '** ABEND ** IN INKRP108'                                
               DISPLAY 'INVALID EXCEPTION AMOUNT ENTERED'                       
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * ONE TIME ROUTINE TO FORMAT DATE AND TIME IN THE REPORT HEADINGS.        
      ******************************************************************        
       B100-GET-DATE-TIME.                                                      
                                                                                
           ACCEPT CPU-DATE FROM DATE.                                           
           MOVE CPU-YEAR TO DP132O-RUN-YEAR.                                    
           MOVE CPU-MONTH TO DP132O-RUN-MONTH.                                  
           MOVE CPU-DAY TO DP132O-RUN-DAY.                                      
           ACCEPT CPU-TIME FROM TIME.                                           
           MOVE CPU-HOUR TO DP132O-RUN-HOUR.                                    
           MOVE CPU-MINUTE TO DP132O-RUN-MINUTE.                                
           MOVE 'INKRP108' TO DP132O-PROGRAM-NAME.                              
                                                                                
                                                                                
       B200-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  PHY-INVENTORY-FILE                                       
                OUTPUT PHY-INVENTORY-RPT-FILE.                                  
                                                                                
                                                                                
       B300-PROCESS-STORE.                                                      
                                                                                
           SET PS-FIRST-EXCP-FOR-STORE TO TRUE.                                 
                                                                                
           MOVE IN014-STORE-NUMBER TO SA-STORE-NO                               
                                      H1-STORE-NO.                              
           PERFORM Z200-READ-STORE-DB.                                          
           MOVE FF-STORE-NAME      TO H1-STORE-NAME.                            
                                                                                
           MOVE IN014-INV-DATE     TO H1-INV-DATE.                              
                                                                                
           MOVE 1                  TO  SS-ENTRY-SUB.                            
           INITIALIZE DT-DETAIL-TABLE                                           
                      SS-DETAIL-SUB.                                            
                                                                                
           PERFORM C100-PRODUCE-DEPT-CLASS-REPORT UNTIL                         
                     PS-END-OF-INPUT OR                                         
                     IN014-STORE-NUMBER NOT = SA-STORE-NO.                      
                                                                                
           PERFORM Z600-PRINT-HEADINGS.                                         
           PERFORM D100-UNLOAD-DETAIL-TABLE.                                    
           PERFORM C200-PROCESS-CLASS-BREAK.                                    
           PERFORM C300-PROCESS-DEPT-BREAK.                                     
           PERFORM C400-PROCESS-BYR-BREAK.                                      
           PERFORM C500-PROCESS-DMA-BREAK.                                      
           PERFORM C600-PRINT-STORE-TOTAL.                                      
                                                                                
       B400-CLOSE-FILES.                                                        
                                                                                
           CLOSE  PHY-INVENTORY-FILE                                            
                  PHY-INVENTORY-RPT-FILE.                                       
                                                                                
                                                                                
       C100-PRODUCE-DEPT-CLASS-REPORT.                                          
                                                                                
           COMPUTE PA-EXTENDED-RETAIL =                                         
               IN014-QUANTITY * IN014-PRICE.                                    
           IF PA-EXTENDED-RETAIL >= CC-EXCEPTION-LIMIT                          
               PERFORM C150-PRODUCE-DEPT-CLASS-REPORT                           
           END-IF.                                                              
           PERFORM Z100-READ-INVENTORY-FILE.                                    
                                                                                
                                                                                
       C150-PRODUCE-DEPT-CLASS-REPORT.                                          
                                                                                
           IF PS-FIRST-EXCP-FOR-STORE                                           
               PERFORM D200-PREPARE-FOR-NEXT-CLASS                              
               PERFORM D300-PREPARE-FOR-NEXT-DEPT                               
               PERFORM D400-PREPARE-FOR-NEXT-BUYER                              
               PERFORM D500-PREPARE-FOR-NEXT-DMA                                
               SET PS-NOT-FIRST-EXCP-FOR-STORE TO TRUE                          
           END-IF.                                                              
                                                                                
           IF (IN014-CLASS = SA-CLASS) AND                                      
              (IN014-DEPT  = SA-DEPT)  AND                                      
              (IN014-BYR   = SA-BUYER-NUMBER)                                   
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM Z600-PRINT-HEADINGS                                      
               PERFORM D100-UNLOAD-DETAIL-TABLE                                 
               PERFORM C200-PROCESS-CLASS-BREAK                                 
               IF IN014-DEPT NOT = SA-DEPT                                      
                   PERFORM C300-PROCESS-DEPT-BREAK                              
               END-IF                                                           
               IF IN014-BYR NOT = SA-BUYER-NUMBER                               
                   PERFORM C400-PROCESS-BYR-BREAK                               
               END-IF                                                           
               IF IN014-DMA NOT = SA-DMA                                        
                   PERFORM C500-PROCESS-DMA-BREAK                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF SS-DETAIL-SUB = PC-DETAIL-MAX                                     
               IF SS-ENTRY-SUB IS EQUAL TO PC-ENTRY-MAX                         
                   PERFORM Z600-PRINT-HEADINGS                                  
                   PERFORM D100-UNLOAD-DETAIL-TABLE                             
                   INITIALIZE DT-DETAIL-TABLE                                   
                   MOVE 1 TO SS-ENTRY-SUB                                       
                             SS-DETAIL-SUB                                      
               ELSE                                                             
                   ADD  1 TO SS-ENTRY-SUB                                       
                   MOVE 1 TO SS-DETAIL-SUB                                      
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO SS-DETAIL-SUB                                           
           END-IF.                                                              
                                                                                
           MOVE IN014-AREA-NUMBER                                               
               TO DT-AREA (SS-ENTRY-SUB, SS-DETAIL-SUB).                        
           MOVE IN014-LINE-NUMBER                                               
               TO DT-LINE (SS-ENTRY-SUB, SS-DETAIL-SUB).                        
           MOVE IN014-PRICE                                                     
               TO DT-PRICE (SS-ENTRY-SUB, SS-DETAIL-SUB).                       
           MOVE IN014-QUANTITY                                                  
               TO DT-QTY (SS-ENTRY-SUB, SS-DETAIL-SUB).                         
           MOVE PA-EXTENDED-RETAIL                                              
               TO DT-EXT-RTL (SS-ENTRY-SUB, SS-DETAIL-SUB).                     
                                                                                
W26600     MOVE IN014-SKU                                                       
W26600         TO DT-SKU (SS-ENTRY-SUB, SS-DETAIL-SUB).                         
25248                                                                           
25248      IF IN014-UPC-CODE NOT EQUAL ZEROS                                    
25248        MOVE IN014-UPC-CODE                                                
25248          TO DT-UPC-SKU (SS-ENTRY-SUB, SS-DETAIL-SUB)                      
25248      ELSE                                                                 
W26600       MOVE SPACE                                                         
W26600         TO DT-UPC-SKU (SS-ENTRY-SUB, SS-DETAIL-SUB)                      
25248      END-IF.                                                              
25248                                                                           
           ADD IN014-QUANTITY     TO PA-CLASS-QTY                               
                                     PA-DEPT-QTY                                
                                     PA-BUYER-QTY                               
                                     PA-DMA-QTY                                 
                                     PA-FINAL-QTY.                              
           ADD PA-EXTENDED-RETAIL TO PA-CLASS-RETAIL                            
                                     PA-DEPT-RETAIL                             
                                     PA-BUYER-RETAIL                            
                                     PA-DMA-RETAIL                              
                                     PA-FINAL-RETAIL.                           
                                                                                
                                                                                
       C200-PROCESS-CLASS-BREAK.                                                
                                                                                
      *    BUILD AND PRINT THE CLASS TOTAL LINE FOR THE CURRENT CLASS.          
           MOVE PC-CLASS TO T1-NAME-B.                                          
           MOVE SA-DEPT  TO T1-DEPT                                             
                            D2-DEPT.                                            
           MOVE SA-CLASS TO T1-CLASS                                            
                            D2-CLASS.                                           
           MOVE '-'      TO T1-DASH.                                            
           MOVE PA-CLASS-QTY TO T1-QTY                                          
                                D2-UNITS.                                       
           MOVE PA-CLASS-RETAIL TO T1-RETAIL                                    
                                   D2-RETAIL.                                   
           MOVE 2 TO PC-ADVANCE.                                                
           MOVE T1-TOTAL TO                                                     
                  PHY-INVENTORY-RPT-RECORD.                                     
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
      *    GET READY TO PROCESS THE NEXT CLASS.                                 
           PERFORM D200-PREPARE-FOR-NEXT-CLASS.                                 
                                                                                
                                                                                
       C300-PROCESS-DEPT-BREAK.                                                 
                                                                                
      *    BUILD AND PRINT THE DEPT TOTAL LINE FOR THE CURRENT DEPT.            
           MOVE PC-DEPT        TO T1-NAME-B.                                    
           MOVE SA-DEPT        TO T1-NAME-NO.                                   
           MOVE PA-DEPT-QTY    TO T1-QTY.                                       
           MOVE PA-DEPT-RETAIL TO T1-RETAIL.                                    
           MOVE 2 TO PC-ADVANCE.                                                
           MOVE T1-TOTAL TO                                                     
                  PHY-INVENTORY-RPT-RECORD.                                     
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
      *    GET READY TO PROCESS THE NEXT  DEPT.                                 
           PERFORM D300-PREPARE-FOR-NEXT-DEPT.                                  
                                                                                
                                                                                
       C400-PROCESS-BYR-BREAK.                                                  
                                                                                
      *    BUILD AND PRINT THE BUYER TOTAL LINE FOR THE CURRENT BUYER.          
           MOVE PC-BUYER          TO T1-NAME-B.                                 
           MOVE SA-BUYER-NUMBER   TO T1-NAME-NO.                                
           MOVE PA-BUYER-QTY      TO T1-QTY.                                    
           MOVE PA-BUYER-RETAIL   TO T1-RETAIL.                                 
           MOVE 2 TO PC-ADVANCE.                                                
           MOVE T1-TOTAL          TO PHY-INVENTORY-RPT-RECORD.                  
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
      *    GET READY TO PROCESS THE NEXT BUYER.                                 
           PERFORM D400-PREPARE-FOR-NEXT-BUYER.                                 
                                                                                
                                                                                
       C500-PROCESS-DMA-BREAK.                                                  
                                                                                
      *    THERE IS NO DMA TOTAL LINE, SO JUST GET READY TO                     
      *    PROCESS THE NEXT DMA.                                                
           PERFORM D500-PREPARE-FOR-NEXT-DMA.                                   
                                                                                
                                                                                
      ******************************************************************        
      * THE STORE TOTAL COUNT AND RETAIL IS LIST.                               
      ******************************************************************        
       C600-PRINT-STORE-TOTAL.                                                  
           MOVE SPACES TO H1-DEPT-CLASS.                                        
           INITIALIZE H1-CLASS-DESC                                             
                      H2-BUYER-NO                                               
                      H2-BUYER-NAME.                                            
           PERFORM Z600-PRINT-HEADINGS.                                         
           MOVE PC-FINAL  TO T1-NAME.                                           
           MOVE PA-FINAL-QTY TO T1-QTY.                                         
           MOVE PA-FINAL-RETAIL TO T1-RETAIL                                    
           MOVE 3 TO PC-ADVANCE.                                                
           MOVE T1-TOTAL TO                                                     
                      PHY-INVENTORY-RPT-RECORD.                                 
           PERFORM Z400-WRITE-REPORT-LINE.                                      
           MOVE 0 TO PA-FINAL-QTY                                               
                     PA-FINAL-RETAIL.                                           
                                                                                
JAN04      DIVIDE 2 INTO PA-PAGE-COUNT                                          
JAN04          GIVING PA-PAGE-DIV                                               
JAN04          REMAINDER PA-PAGE-REM.                                           
JAN04                                                                           
JAN04      IF PA-PAGE-REM NOT EQUAL 0                                           
JAN04          PERFORM Z600-PRINT-HEADINGS                                      
JAN04          MOVE D3-DETAIL-LINE TO PHY-INVENTORY-RPT-RECORD                  
JAN04          PERFORM Z400-WRITE-REPORT-LINE                                   
JAN04      END-IF.                                                              
                                                                                
           INITIALIZE PA-PAGE-COUNT.                                            
                                                                                
      ******************************************************************        
      * D100-UNLOAD-DETAIL-TABLE  SUBSCRIPT FOR ROWS IS INCREMENTED             
      ******************************************************************        
       D100-UNLOAD-DETAIL-TABLE.                                                
                                                                                
           MOVE 1 TO PC-ADVANCE.                                                
           PERFORM                                                              
               VARYING SS-SUB FROM 1 BY 1                                       
               UNTIL   SS-SUB > PC-DETAIL-MAX                                   
               MOVE SPACES TO D1-DETAIL-LINE                                    
               EVALUATE TRUE                                                    
                   WHEN SS-ENTRY-SUB = 1                                        
                       IF SS-SUB > SS-DETAIL-SUB                                
                           MOVE +51 TO SS-SUB                                   
                       ELSE                                                     
                           MOVE DT-PRICE (1, SS-SUB)   TO D1-PRICE (1)          
                           MOVE DT-EXT-RTL (1, SS-SUB) TO D1-EXT-RTL (1)        
                           MOVE DT-AREA (1, SS-SUB)    TO D1-AREA (1)           
                           MOVE DT-LINE (1, SS-SUB)    TO D1-LINE (1)           
W26600                     MOVE DT-SKU (1, SS-SUB)                              
W26600                                     TO D1-SKU (1)                        
25248                      MOVE DT-UPC-SKU (1, SS-SUB)                          
25248                                           TO D1-UPC-SKU (1)               
                           MOVE DT-QTY  (1, SS-SUB)    TO D1-QTY (1)            
                                                                                
                           MOVE D1-DETAIL-LINE TO                               
                                PHY-INVENTORY-RPT-RECORD                        
                           PERFORM Z400-WRITE-REPORT-LINE                       
                       END-IF                                                   
                   WHEN SS-ENTRY-SUB = 2                                        
                       MOVE DT-EXT-RTL (1, SS-SUB) TO D1-EXT-RTL (1)            
                       MOVE DT-PRICE (1, SS-SUB) TO D1-PRICE (1)                
W26600                 MOVE DT-SKU (1, SS-SUB)                                  
W26600                                     TO D1-SKU (1)                        
25248                  MOVE DT-UPC-SKU (1, SS-SUB)                              
25248                                           TO D1-UPC-SKU (1)               
                       MOVE DT-QTY  (1, SS-SUB)    TO D1-QTY (1)                
                       MOVE DT-AREA  (1, SS-SUB)   TO D1-AREA  (1)              
                       MOVE DT-LINE  (1, SS-SUB)   TO D1-LINE  (1)              
                                                                                
                       IF SS-SUB <= SS-DETAIL-SUB                               
                           MOVE DT-EXT-RTL (2, SS-SUB) TO D1-EXT-RTL (2)        
                           MOVE DT-AREA (2, SS-SUB)    TO D1-AREA (2)           
                           MOVE DT-LINE (2, SS-SUB)    TO D1-LINE (2)           
W26600                     MOVE DT-SKU (2, SS-SUB)                              
W26600                                     TO D1-SKU (2)                        
25248                      MOVE DT-UPC-SKU (2, SS-SUB)                          
25248                                           TO D1-UPC-SKU (2)               
                           MOVE DT-QTY  (2, SS-SUB)    TO D1-QTY (2)            
                           MOVE DT-PRICE (2, SS-SUB)   TO D1-PRICE (2)          
                       END-IF                                                   
                       MOVE D1-DETAIL-LINE TO                                   
                            PHY-INVENTORY-RPT-RECORD                            
                       PERFORM Z400-WRITE-REPORT-LINE                           
25248 *            WHEN SS-ENTRY-SUB = 3                                        
  |   *                MOVE DT-PRICE (1, SS-SUB)   TO D1-PRICE (1)              
  |   *                MOVE DT-EXT-RTL (1, SS-SUB) TO D1-EXT-RTL (1)            
  |   *                MOVE DT-UPC-SKU (1, SS-SUB)                              
  |   *                                         TO D1-UPC-SKU (1)               
  |   *                MOVE DT-QTY  (1, SS-SUB)    TO D1-QTY (1)                
  |   *                MOVE DT-AREA  (1, SS-SUB)   TO D1-AREA  (1)              
  |   *                MOVE DT-LINE  (1, SS-SUB)   TO D1-LINE  (1)              
  |   *                MOVE DT-PRICE (2, SS-SUB)   TO D1-PRICE (2)              
  |   *                MOVE DT-EXT-RTL (2, SS-SUB) TO D1-EXT-RTL (2)            
  |   *                MOVE DT-UPC-SKU (2, SS-SUB)                              
  |   *                                         TO D1-UPC-SKU (2)               
 |    *                MOVE DT-QTY  (2, SS-SUB)    TO D1-QTY (2)                
 |    *                MOVE DT-AREA  (2, SS-SUB)   TO D1-AREA  (2)              
 |    *                MOVE DT-LINE  (2, SS-SUB)   TO D1-LINE  (2)              
 |    *                                                                         
 |    *                IF SS-SUB <= SS-DETAIL-SUB                               
 |    *                    MOVE DT-PRICE (3, SS-SUB)   TO D1-PRICE (3)          
 |    *                    MOVE DT-EXT-RTL (3, SS-SUB) TO D1-EXT-RTL (3)        
  |   *                    MOVE DT-UPC-SKU (3, SS-SUB)                          
  |   *                                         TO D1-UPC-SKU (3)               
 |    *                    MOVE DT-QTY  (3, SS-SUB)    TO D1-QTY (3)            
 |    *                    MOVE DT-AREA  (3, SS-SUB)   TO D1-AREA (3)           
 |    *                    MOVE DT-LINE  (3, SS-SUB)   TO D1-LINE (3)           
 |    *                END-IF                                                   
 |    *                MOVE D1-DETAIL-LINE TO                                   
 |    *                     PHY-INVENTORY-RPT-RECORD                            
25248 *                PERFORM Z400-WRITE-REPORT-LINE                           
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
       D150-GET-BYR-NAME.                                                       
                                                                                
W21732     MOVE IN014-DEPT(2:3) TO DK-DEPT.                                     
           PERFORM Z300-GET-MERCH-AREA-INFO.                                    
           PERFORM E100-FORMAT-NAME.                                            
                                                                                
       D200-PREPARE-FOR-NEXT-CLASS.                                             
                                                                                
      *    GET READY TO PROCESS THE NEXT CLASS.                                 
           MOVE ZEROES           TO PA-CLASS-QTY                                
                                    PA-CLASS-RETAIL.                            
           MOVE IN014-CLASS      TO SA-CLASS                                    
                                    H1-CLASS.                                   
           MOVE IN014-CLASS-DESC TO H1-CLASS-DESC.                              
                                                                                
           INITIALIZE DT-DETAIL-TABLE                                           
                      SS-DETAIL-SUB                                             
           MOVE 1 TO  SS-ENTRY-SUB.                                             
                                                                                
       D300-PREPARE-FOR-NEXT-DEPT.                                              
                                                                                
      *    GET READY TO PROCESS THE NEW DEPT.                                   
           MOVE ZEROES       TO PA-DEPT-QTY                                     
                                PA-DEPT-RETAIL.                                 
           MOVE IN014-DEPT   TO SA-DEPT                                         
                                H1-DEPT.                                        
           PERFORM D150-GET-BYR-NAME.                                           
           MOVE PV-FULL-NAME TO H2-BUYER-NAME.                                  
                                                                                
                                                                                
       D400-PREPARE-FOR-NEXT-BUYER.                                             
                                                                                
      *    GET READY TO PROCESS THE NEW BUYER.                                  
           MOVE ZEROES    TO PA-BUYER-QTY                                       
                             PA-BUYER-RETAIL.                                   
           MOVE IN014-BYR TO SA-BUYER-NUMBER                                    
                             H2-BUYER-NO.                                       
                                                                                
                                                                                
       D500-PREPARE-FOR-NEXT-DMA.                                               
                                                                                
      *    GET READY TO PROCESS THE NEW DMA.                                    
           MOVE ZEROES TO PA-DMA-QTY                                            
                          PA-DMA-RETAIL.                                        
           MOVE IN014-DMA TO SA-DMA.                                            
                                                                                
       E100-FORMAT-NAME.                                                        
                                                                                
           SET PS-NOT-DONE TO TRUE.                                             
           PERFORM VARYING SS-FIRST-SUB                                         
                   FROM PC-END-FIRST-NAME BY -1                                 
                   UNTIL (PS-DONE OR                                            
                          SS-FIRST-SUB < 1)                                     
              IF PV-FNAME-DIGIT(SS-FIRST-SUB) NOT = SPACES                      
                 SET PS-DONE TO TRUE                                            
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           SET PS-NOT-DONE TO TRUE                                              
           PERFORM VARYING SS-LAST-SUB                                          
                   FROM PC-END-LAST-NAME BY -1                                  
                   UNTIL (PS-DONE OR                                            
                          SS-LAST-SUB < 1)                                      
              IF PV-LNAME-DIGIT(SS-LAST-SUB) NOT = SPACES                       
                 SET PS-DONE TO TRUE                                            
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE PV-FIRST-NAME TO PV-FULL-NAME.                                  
                                                                                
25248      COMPUTE SS-FIRST-SUB = SS-FIRST-SUB + 2                              
           SET PS-NOT-DONE TO TRUE                                              
           MOVE 1 TO SS-TEMP-SUB2.                                              
           PERFORM VARYING SS-TEMP-SUB1                                         
                  FROM SS-FIRST-SUB BY 1                                        
                  UNTIL (SS-TEMP-SUB1 > 20 OR PS-DONE)                          
                                                                                
              IF SS-TEMP-SUB2 > SS-LAST-SUB                                     
                 SET PS-DONE TO TRUE                                            
              END-IF                                                            
                                                                                
              MOVE PV-LNAME-DIGIT(SS-TEMP-SUB2) TO                              
                                   PV-FULLNAME-DIGIT(SS-TEMP-SUB1)              
              ADD 1 TO SS-TEMP-SUB2                                             
           END-PERFORM.                                                         
                                                                                
                                                                                
       Z100-READ-INVENTORY-FILE.                                                
                                                                                
           READ PHY-INVENTORY-FILE                                              
               INTO IN014-VALID-REPORT-RECORD                                   
                   AT END                                                       
                      SET PS-END-OF-INPUT TO TRUE.                              
                                                                                
                                                                                
       Z200-READ-STORE-DB.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES) OR                           
                     (SQLCODE = 0)                                              
                                                                                
  |            MOVE SA-STORE-NO         TO PV-EDIT-LOC-NBR                      
  |            MOVE PV-EDIT-LOC-NBR     TO XMT00001-LOC-NBR                     
  |                                                                             
  |            EXEC SQL                                                         
  |                SELECT LOC_NM                                                
  |                INTO :FF-STORE-NAME                                          
  |                FROM XMT_LOC                                                 
  |                WHERE LOC_NBR = :XMT00001-LOC-NBR                            
  |                WITH UR                                                      
  |            END-EXEC                                                         
25248                                                                           
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE 'Z200-READ-STORE-DB'                               
                              TO PV-PARAGRAPH-NAME                              
                        MOVE 'READING STORE TABLE'                              
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM Z900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
                MOVE 'Z200-READ-STORE-DB' TO PV-PARAGRAPH-NAME                  
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM Z900-SQL-ABEND                                          
           END-IF.                                                              
                                                                                
       Z300-GET-MERCH-AREA-INFO.                                                
P28545                                                                          
P28545     INITIALIZE IN400-PV-INPUT-AREA.                                      
P28545                                                                          
P28545     SET IN400-PV-SKU-ATTRIB-LOOK TO TRUE.                                
P28545     MOVE IN014-STORE-NUMBER  TO IN400-PV-LOC-IN.                         
P28545     MOVE IN014-INV-DATE      TO IN400-PV-EFF-DTE-IN.                     
P28545     MOVE IN014-SKU           TO IN400-PV-SKU-IN.                         
P28545                                                                          
P28545     PERFORM IN400-LOOKUP-RETAIL.                                         
P28545                                                                          
P28545     IF IN400-PV-ATT-FOUND                                                
P28545         MOVE IN400-ATT-BYR-FIR-NAME  TO PV-FIRST-NAME                    
P28545         MOVE IN400-ATT-BYR-LAST-NAME TO PV-LAST-NAME                     
P28545     ELSE                                                                 
P28545         MOVE 'UNKNOWN'               TO PV-FIRST-NAME                    
P28545         MOVE SPACES                  TO PV-LAST-NAME                     
P28545     END-IF.                                                              
P28545                                                                          
P28545*    PERFORM                                                              
P28545*        WITH TEST AFTER                                                  
P28545*        VARYING PV-SQL-SUB                                               
P28545*        FROM 1 BY 1                                                      
P28545*        UNTIL (PV-SQL-SUB > PC-MAX-RETRIES)                              
P28545*        OR    (SQLCODE = 0)                                              
P28545*        OR    (SQLCODE = +100)                                           
P28545*                                                                         
P28545*        EXEC SQL                                                         
P28545*            SELECT WORKER_FIR_NAME,                                      
P28545*                   WORKER_LAST_NAME                                      
P28545*            INTO   :RSPBAR-WORKER-FIR-NAME,                              
P28545*                   :RSPBAR-WORKER-LAST-NAME                              
P28545*            FROM   TMDSEAR B,                                            
P28545*                   TRSPBAR A                                             
P28545*            WHERE (B.OPERCO_NBR = :PC-OPERCO-NBR)                        
P28545*              AND (B.DEPT_NBR = :DK-DEPT)                                
P28545*              AND (B.MDSELV_CDE = :PC-DPT)                               
P28545*              AND (B.MDSEAR_DKEY = A.MDSEAR_DKEY)                        
P28545*              AND (A.RSPLVL_CDE  = :PC-BYR)                              
P28545*              AND ((A.WORKER_STRT_DTE <=                                 
P28545*                              DATE (CURRENT TIMESTAMP)) AND              
P28545*                  (DATE (CURRENT TIMESTAMP) <=                           
P28545*                              A.WORKER_END_DTE))                         
P28545*              AND ((B.STRT_DTE <= DATE (CURRENT TIMESTAMP)) AND          
P28545*                  (DATE (CURRENT TIMESTAMP) <= B.END_DTE))               
P28545*            WITH UR                                                      
P28545*        END-EXEC                                                         
P28545*                                                                         
P28545*        EVALUATE TRUE                                                    
P28545*            WHEN SQLCODE = -904                                          
P28545*            WHEN SQLCODE = -911                                          
P28545*            WHEN SQLCODE = ZERO                                          
P28545*                CONTINUE                                                 
P28545*            WHEN SQLCODE = +100                                          
P28545*                MOVE 'UNKNOWN'      TO RSPBAR-WORKER-FIR-NAME            
P28545*                MOVE SPACE          TO RSPBAR-WORKER-LAST-NAME           
P28545*                                                                         
P28545*            WHEN SQLWARN0 NOT = SPACES                                   
P28545*            WHEN SQLCODE < 0                                             
P28545*            WHEN SQLCODE > 0                                             
P28545*                 MOVE 'Z300-GET-MERCH-AREA-INFO'                         
P28545*                       TO PV-PARAGRAPH-NAME                              
P28545*                 MOVE 'SELECT MERCH AREA INFO'                           
P28545*                       TO PV-DB2-OPER-ATTEMPTED                          
P28545*                 PERFORM Z900-SQL-ABEND                                  
P28545*        END-EVALUATE                                                     
P28545*    END-PERFORM.                                                         
P28545*    IF PV-SQL-SUB > PC-MAX-RETRIES                                       
P28545*         MOVE 'Z300-GET-MERCH-AREA-INFO' TO PV-PARAGRAPH-NAME            
P28545*         MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
P28545*         PERFORM Z900-SQL-ABEND                                          
P28545*    END-IF.                                                              
P28545*                                                                         
P28545*                                                                         
       Z400-WRITE-REPORT-LINE.                                                  
                                                                                
           WRITE PHY-INVENTORY-RPT-RECORD                                       
             AFTER ADVANCING PC-ADVANCE.                                        
           ADD PC-ADVANCE TO PC-LINE-COUNT.                                     
                                                                                
                                                                                
       Z600-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PA-PAGE-COUNT.                                              
           MOVE PA-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE 1   TO DP132O-REPORT-NUMBER.                                    
           MOVE DP132O-STANDRD-HEADER-132-COLS TO                               
                  PHY-INVENTORY-RPT-RECORD.                                     
           WRITE PHY-INVENTORY-RPT-RECORD                                       
                 AFTER ADVANCING PAGE.                                          
           MOVE ZERO TO PC-LINE-COUNT.                                          
                                                                                
           MOVE H1-HEADING TO                                                   
                  PHY-INVENTORY-RPT-RECORD.                                     
           MOVE 1 TO PC-ADVANCE.                                                
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE H2-HEADING TO                                                   
                  PHY-INVENTORY-RPT-RECORD.                                     
           MOVE 2 TO PC-ADVANCE.                                                
           PERFORM Z400-WRITE-REPORT-LINE.                                      
           MOVE H5-HEADING TO                                                   
                  PHY-INVENTORY-RPT-RECORD.                                     
           MOVE 2 TO PC-ADVANCE.                                                
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE H6-HEADING TO                                                   
                  PHY-INVENTORY-RPT-RECORD.                                     
           MOVE 1 TO PC-ADVANCE.                                                
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
           INITIALIZE PHY-INVENTORY-RPT-RECORD.                                 
           PERFORM Z400-WRITE-REPORT-LINE.                                      
                                                                                
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP108'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
P28545******************************************************************        
P28545*  PRICING MODULE.                                                        
P28545******************************************************************        
P28545     COPY INPD400.                                                        
P28545                                                                          
