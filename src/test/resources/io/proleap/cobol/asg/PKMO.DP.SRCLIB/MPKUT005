000100******************************************************************06/01/94
000200 IDENTIFICATION DIVISION.                                         MPKUT005
000300******************************************************************   LV001
000400                                                                  MPKUT005
000500 PROGRAM-ID.             MPKUT005.                                MPKUT005
000600 AUTHOR.                 RICK TULLOCH - DIVERSE COMPUTER CORP.    MPKUT005
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                 MPKUT005
000800 DATE-WRITTEN            2-26-88.                                 MPKUT005
000900 DATE-COMPILED.                                                   MPKUT005
001000                                                                  MPKUT005
001100******************************************************************MPKUT005
001200*       THIS PROGRAM WILL SEQUENTIALLY PROCESS THE SORTED AND    *MPKUT005
001300*  SUMMED MONTH STORE EXTRACT FILE, AND REFORMAT EACH RECORD     *MPKUT005
001400*  TO CREATE THE MONTHLY RECEIPT FILE.  THE SKU NUMBER, IN-HOUSE *MPKUT005
001500*  VENDOR NUMBER, VENDOR DUNNS NUMBER, AND ACCUMULATED RETAIL    *MPKUT005
001600*  UNITS MARKED FOR THE SKU/VENDOR COMBINATION WILL BE USED TO   *MPKUT005
001700*  CREATE EACH MONTHLY RECEIPTS RECORD.  A REPORT SHOWING THE    *MPKUT005
001800*  SKU/VENDOR RECEIPT DETAIL CAN BE REQUESTED VIA A CONTROL      *MPKUT005
001900*  CARD.  THE OVERALL MONTHLY TOTAL FOR RECEIPTS WILL BE         *MPKUT005
002000*  DISPLAYED IN THE SYSOUT, AS WELL AS ON THE REPORT.            *MPKUT005
002100*       MONTHLY STORE EXTRACT RECORDS MUST BE SORTED IN          *MPKUT005
002200*  ASCENDING DEPARTMENT, CLASS, SEQUENCE NUMBER, AND VENDOR      *MPKUT005
002300*  DUNNS NUMBER SEQUENCE.                                        *MPKUT005
002400******************************************************************MPKUT005
002500                                                                  MPKUT005
002600******************************************************************MPKUT005
002700 ENVIRONMENT DIVISION.                                            MPKUT005
002800******************************************************************MPKUT005
002900                                                                  MPKUT005
003000 CONFIGURATION SECTION.                                           MPKUT005
003100                                                                  MPKUT005
003200 SOURCE-COMPUTER.        IBM-3090.                                MPKUT005
003300 OBJECT-COMPUTER.        IBM-3090.                                MPKUT005
003400                                                                  MPKUT005
003500 INPUT-OUTPUT SECTION.                                            MPKUT005
003600                                                                  MPKUT005
003700 FILE-CONTROL.                                                    MPKUT005
003800                                                                  MPKUT005
003900     SELECT MONTHLY-STORE-EXTR-FILE                               MPKUT005
004000         ASSIGN TO INP01.                                         MPKUT005
004100                                                                  MPKUT005
004200     SELECT MONTHLY-RECEIPT-FILE                                  MPKUT005
004300         ASSIGN TO OUT01.                                         MPKUT005
004400                                                                  MPKUT005
004500     SELECT RECEIPT-REPORT-FILE                                   MPKUT005
004600         ASSIGN TO RPT01.                                         MPKUT005
004700                                                                  MPKUT005
004800******************************************************************MPKUT005
004900 DATA DIVISION.                                                   MPKUT005
005000******************************************************************MPKUT005
005100                                                                  MPKUT005
005200 FILE SECTION.                                                    MPKUT005
005300                                                                  MPKUT005
005400 FD  MONTHLY-STORE-EXTR-FILE                                      MPKUT005
005500     LABEL RECORDS ARE STANDARD                                   MPKUT005
005600     BLOCK CONTAINS 0 RECORDS                                     MPKUT005
005700     RECORD CONTAINS 300 CHARACTERS                               MPKUT005
005800     DATA RECORD IS MS-MONTHLY-STORE-EXTR-RECORD.                 MPKUT005
005900                                                                  MPKUT005
006000 01  MS-MONTHLY-STORE-EXTR-RECORD                                 MPKUT005
006100                                 PIC X(300).                      MPKUT005
006200                                                                  MPKUT005
006300 FD  MONTHLY-RECEIPT-FILE                                         MPKUT005
006400     LABEL RECORDS ARE STANDARD                                   MPKUT005
006500     BLOCK CONTAINS 0 RECORDS                                     MPKUT005
006600     RECORD CONTAINS 25 CHARACTERS                                MPKUT005
006700     DATA RECORD IS MR-MONTHLY-RECEIPT-RECORD.                    MPKUT005
006800                                                                  MPKUT005
006900 01  MR-MONTHLY-RECEIPT-RECORD   PIC X(25).                       MPKUT005
007000                                                                  MPKUT005
007100 FD  RECEIPT-REPORT-FILE                                          MPKUT005
007200     LABEL RECORDS ARE STANDARD                                   MPKUT005
007300     BLOCK CONTAINS 0 RECORDS                                     MPKUT005
007400     RECORD CONTAINS 132 CHARACTERS                               MPKUT005
007500     DATA RECORD IS RR-RECEIPT-REPORT-RECORD.                     MPKUT005
007600                                                                  MPKUT005
007700 01  RR-RECEIPT-REPORT-RECORD    PIC X(132).                      MPKUT005
007800                                                                  MPKUT005
007900 WORKING-STORAGE SECTION.                                         MPKUT005
008000                                                                  MPKUT005
008100*                                                                 MPKUT005
008200*  LAYOUT FOR THE MONTHLY STORE EXTRACT FILE                      MPKUT005
008300*                                                                 MPKUT005
008400                                                                  MPKUT005
008500     COPY STOREEXT.                                               MPKUT005
008600                                                                  MPKUT005
008700*                                                                 MPKUT005
008800*  LAYOUT FOR THE MONTHLY RECEIPTS FILE                           MPKUT005
008900*                                                                 MPKUT005
009000                                                                  MPKUT005
009100     COPY MPRD001O.                                               MPKUT005
009200                                                                  MPKUT005
009300*                                                                 MPKUT005
009400*  STANDARD REPORT HEADER FOR A 132 COLUMN REPORT                 MPKUT005
009500*                                                                 MPKUT005
009600                                                                  MPKUT005
009700     COPY DPWS132O.                                               MPKUT005
009800                                                                  MPKUT005
009900 01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          MPKUT005
010000                                 COMP SYNC.                       MPKUT005
010100                                                                  MPKUT005
010200 01  PS-PROGRAM-SWITCHES.                                         MPKUT005
010300     05  PS-END-OF-STORE-EXTR-FILE-IND                            MPKUT005
010400                                 PIC X(1)     VALUE 'N'.          MPKUT005
010500         88  PS-END-OF-STORE-EXTR-FILE        VALUE 'Y'.          MPKUT005
010600                                                                  MPKUT005
010700 01  PC-PROGRAM-CONSTANTS.                                        MPKUT005
010800     05  PC-PAGE-SIZE            PIC S9(4)    VALUE +63           MPKUT005
010900                                 COMP SYNC.                       MPKUT005
011000     05  PC-COLUMN-MAX           PIC S9(4)    VALUE +3            MPKUT005
011100                                 COMP SYNC.                       MPKUT005
011200                                                                  MPKUT005
011300 01  PV-PROGRAM-VARIABLES.                                        MPKUT005
011400     05  PV-PAGE-COUNT           PIC S9(5)    VALUE ZERO          MPKUT005
011500                                 COMP-3.                          MPKUT005
011600     05  PV-LINE-COUNT           PIC S9(4)    VALUE +64           MPKUT005
011700                                 COMP SYNC.                       MPKUT005
011800     05  PV-ADVANCE-COUNT        PIC S9(4)    VALUE ZERO          MPKUT005
011900                                 COMP SYNC.                       MPKUT005
012000     05  PV-TOTAL-RETAIL-UNITS-MARKED                             MPKUT005
012100                                 PIC S9(11)   VALUE ZERO          MPKUT005
012200                                 COMP-3.                          MPKUT005
012300                                                                  MPKUT005
012400 01  SN-SKU-NUMBER-WORK-AREA.                                     MPKUT005
012500     05  SN-CLASS-AREA.                                           MPKUT005
012600         10  SN-CLASS            PIC 9(2)     VALUE ZERO.         MPKUT005
012700         10  FILLER              PIC 9(1)     VALUE ZERO.         MPKUT005
012800     05  SN-SEQUENCE-NUMBER-AREA.                                 MPKUT005
012900         10  FILLER              PIC 9(1)     VALUE ZERO.         MPKUT005
013000         10  SN-SEQUENCE-NUMBER  PIC 9(3)     VALUE ZERO.         MPKUT005
013100         10  FILLER              PIC 9(1)     VALUE ZERO.         MPKUT005
013200                                                                  MPKUT005
013300 01  CD-CURRENT-DATE.                                             MPKUT005
013400     05  CD-CURRENT-YEAR         PIC 9(2)     VALUE ZERO.         MPKUT005
013500     05  CD-CURRENT-MONTH        PIC 9(2)     VALUE ZERO.         MPKUT005
013600     05  CD-CURRENT-DAY          PIC 9(2)     VALUE ZERO.         MPKUT005
013700                                                                  MPKUT005
013800 01  CD-CURRENT-TIME.                                             MPKUT005
013900     05  CD-CURRENT-HOUR         PIC 9(2)     VALUE ZERO.         MPKUT005
014000     05  CD-CURRENT-MINUTE       PIC 9(2)     VALUE ZERO.         MPKUT005
014100     05  FILLER                  PIC 9(4)     VALUE ZERO.         MPKUT005
014200                                                                  MPKUT005
014300 01  CC-CONTROL-CARD.                                             MPKUT005
014400     05  CC-PRODUCE-REPORT-IND   PIC X(1)     VALUE SPACES.       MPKUT005
014500         88  CC-PRODUCE-REPORT                VALUE 'Y'.          MPKUT005
014600         88  CC-DONT-PRODUCE-REPORT           VALUE 'N'.          MPKUT005
014700     05  FILLER                  PIC X(79)    VALUE SPACES.       MPKUT005
014800                                                                  MPKUT005
014900 01  HL2-HEADING-LINE-2.                                          MPKUT005
015000     05  FILLER                  PIC X(49)    VALUE SPACES.       MPKUT005
015100     05  FILLER                  PIC X(34)    VALUE               MPKUT005
015200         'MONTHLY RECEIPTS BY SKU AND VENDOR'.                    MPKUT005
015300     05  FILLER                  PIC X(49)    VALUE SPACES.       MPKUT005
015400                                                                  MPKUT005
015500 01  HL3-HEADING-LINE-3.                                          MPKUT005
015600     05  FILLER                  PIC X(5)     VALUE SPACES.       MPKUT005
015700     05  FILLER                  PIC X(10)    VALUE 'SKU'.        MPKUT005
015800     05  FILLER                  PIC X(9)     VALUE 'DUNNS'.      MPKUT005
015900     05  FILLER                  PIC X(8)     VALUE 'IN-HSE'.     MPKUT005
016000     05  FILLER                  PIC X(17)    VALUE 'RTL UNITS'.  MPKUT005
016100     05  FILLER                  PIC X(10)    VALUE 'SKU'.        MPKUT005
016200     05  FILLER                  PIC X(9)     VALUE 'DUNNS'.      MPKUT005
016300     05  FILLER                  PIC X(8)     VALUE 'IN-HSE'.     MPKUT005
016400     05  FILLER                  PIC X(17)    VALUE 'RTL UNITS'.  MPKUT005
016500     05  FILLER                  PIC X(10)    VALUE 'SKU'.        MPKUT005
016600     05  FILLER                  PIC X(9)     VALUE 'DUNNS'.      MPKUT005
016700     05  FILLER                  PIC X(8)     VALUE 'IN-HSE'.     MPKUT005
016800     05  FILLER                  PIC X(12)    VALUE 'RTL UNITS'.  MPKUT005
016900                                                                  MPKUT005
017000 01  HL4-HEADING-LINE-4.                                          MPKUT005
017100     05  FILLER                  PIC X(3)     VALUE SPACES.       MPKUT005
017200     05  FILLER                  PIC X(11)    VALUE 'NUMBER'.     MPKUT005
017300     05  FILLER                  PIC X(10)    VALUE 'NUMBER'.     MPKUT005
017400     05  FILLER                  PIC X(9)     VALUE 'VENDOR'.     MPKUT005
017500     05  FILLER                  PIC X(14)    VALUE 'MARKED'.     MPKUT005
017600     05  FILLER                  PIC X(11)    VALUE 'NUMBER'.     MPKUT005
017700     05  FILLER                  PIC X(10)    VALUE 'NUMBER'.     MPKUT005
017800     05  FILLER                  PIC X(9)     VALUE 'VENDOR'.     MPKUT005
017900     05  FILLER                  PIC X(14)    VALUE 'MARKED'.     MPKUT005
018000     05  FILLER                  PIC X(11)    VALUE 'NUMBER'.     MPKUT005
018100     05  FILLER                  PIC X(10)    VALUE 'NUMBER'.     MPKUT005
018200     05  FILLER                  PIC X(9)     VALUE 'VENDOR'.     MPKUT005
018300     05  FILLER                  PIC X(11)    VALUE 'MARKED'.     MPKUT005
018400                                                                  MPKUT005
018500 01  SV-SKU-VENDOR-ENTRY.                                         MPKUT005
018600     05  FILLER                  PIC X(1)     VALUE SPACES.       MPKUT005
018700     05  SV-SKU-NUMBER.                                           MPKUT005
018800         10  SV-DEPARTMENT       PIC 9(3)     VALUE ZERO.         MPKUT005
018900         10  FILLER              PIC X(1)     VALUE '-'.          MPKUT005
019000         10  SV-CLASS            PIC 9(2)     VALUE ZERO.         MPKUT005
019100         10  FILLER              PIC X(1)     VALUE '-'.          MPKUT005
019200         10  SV-SEQUENCE-NUMBER  PIC 9(3)     VALUE ZERO.         MPKUT005
019300     05  FILLER                  PIC X(2)     VALUE SPACES.       MPKUT005
019400     05  SV-VENDOR-DUNNS-NUMBER  PIC 9(9)     VALUE ZERO.         MPKUT005
019500     05  FILLER                  PIC X(3)     VALUE SPACES.       MPKUT005
019600     05  SV-IN-HSE-VENDOR-NUMBER PIC 9(3)     VALUE ZERO.         MPKUT005
019700     05  FILLER                  PIC X(2)     VALUE SPACES.       MPKUT005
019800     05  SV-RETAIL-UNITS-MARKED  PIC ZZZ,ZZZ,ZZ9-                 MPKUT005
019900                                              VALUE ZERO.         MPKUT005
020000     05  FILLER                  PIC X(2)     VALUE SPACES.       MPKUT005
020100                                                                  MPKUT005
020200 01  RT-RECEIPT-TOTAL-ENTRY.                                      MPKUT005
020300     05  FILLER                  PIC X(1)     VALUE SPACES.       MPKUT005
020400     05  FILLER                  PIC X(25)    VALUE               MPKUT005
020500         'TOTAL RETAIL UNITS MARKED'.                             MPKUT005
020600     05  FILLER                  PIC X(1)     VALUE SPACES.       MPKUT005
020700     05  RT-RETAIL-UNITS-MARKED  PIC ZZ,ZZZ,ZZZ,ZZ9-              MPKUT005
020800                                              VALUE ZERO.         MPKUT005
020900     05  FILLER                  PIC X(2)     VALUE SPACES.       MPKUT005
021000                                                                  MPKUT005
021100 01  DL-NORPT-DETAIL-LINE.                                                
021200     05  FILLER                  PIC X(25) VALUE SPACES.                  
021210     05  FILLER                  PIC X(50) VALUE                          
021300     '**** T H I S  R E P O R T  I S  N O  L O N G E R  '.                
021400     05  FILLER                  PIC X(50) VALUE                          
021500     'B E I N G  C R E A T E D ****'.                                     
021600                                                                          
021700 01  DL-NORPT-DETAIL-LINE2.                                               
021710     05  FILLER                  PIC X(25) VALUE SPACES.                  
021800     05  FILLER                  PIC X(50) VALUE                          
021900     '     C O N T A C T  R O N  K R I E R  A T  E X T. '.                
022000     05  FILLER                  PIC X(50) VALUE                          
022100     '1360  I F  N E E D E D '.                                           
022101                                                                          
022102 01  DL-DETAIL-LINE.                                              MPKUT005
022103     05  DL-DETAIL-LINE-ENTRY    OCCURS 3 TIMES                   MPKUT005
022104                                 INDEXED BY DL-DETAIL-INDEX       MPKUT005
022105                                 PIC X(44).                       MPKUT005
022106                                                                  MPKUT005
022107******************************************************************MPKUT005
022108 PROCEDURE DIVISION.                                              MPKUT005
022109******************************************************************MPKUT005
022110                                                                  MPKUT005
022111 0000-MAINLINE.                                                   MPKUT005
022112     PERFORM 1000-INITIALIZE.                                     MPKUT005
022200                                                                  MPKUT005
022300     PERFORM 2000-PROCESS-STORE-EXTRACT-RCD                       MPKUT005
022400         UNTIL PS-END-OF-STORE-EXTR-FILE.                         MPKUT005
022500                                                                  MPKUT005
022600     MOVE PV-TOTAL-RETAIL-UNITS-MARKED TO RT-RETAIL-UNITS-MARKED. MPKUT005
022700     DISPLAY 'MPKUT005 -'  RT-RECEIPT-TOTAL-ENTRY.                MPKUT005
022710*** ADDED BY SLH ON 09/24/02                                              
022800*    IF CC-PRODUCE-REPORT                                         MPKUT005
022900*        PERFORM 4000-PRINT-TOTAL-RECEIPTS                        MPKUT005
023000*    END-IF.                                                      MPKUT005
023010*** END                                                                   
023100                                                                  MPKUT005
023200     PERFORM 9000-EOJ-ROUTINE.                                    MPKUT005
023300     STOP RUN.                                                    MPKUT005
023400                                                                  MPKUT005
023500******************************************************************MPKUT005
023600*  SET UP FOR PROGRAM PROCESSING BY OPENING ALL FILES, ACCEPTING *MPKUT005
023700*  AND VALIDATING THE CONTROL CARD AND                           *MPKUT005
023800*  FILLING THE FIELDS OF THE STANDARD REPORT HEADER, IF A REPORT *MPKUT005
023900*  WILL BE PRODUCED), AND READING THE FIRST MONTHLY STORE        *MPKUT005
024000*  EXTRACT FILE RECORD.                                          *MPKUT005
024100******************************************************************MPKUT005
024200                                                                  MPKUT005
024300 1000-INITIALIZE.                                                 MPKUT005
024400     OPEN INPUT  MONTHLY-STORE-EXTR-FILE                          MPKUT005
024500          OUTPUT MONTHLY-RECEIPT-FILE                             MPKUT005
024600                 RECEIPT-REPORT-FILE.                             MPKUT005
024700                                                                  MPKUT005
024800     ACCEPT CC-CONTROL-CARD.                                      MPKUT005
024900     DISPLAY 'MPKUT005 - PRODUCE REPORT = '                       MPKUT005
025000             CC-PRODUCE-REPORT-IND.                               MPKUT005
025100     EVALUATE TRUE                                                MPKUT005
025200         WHEN CC-PRODUCE-REPORT                                   MPKUT005
025300             ACCEPT CD-CURRENT-DATE FROM DATE                     MPKUT005
025400             MOVE CD-CURRENT-YEAR TO DP132O-RUN-YEAR              MPKUT005
025500             MOVE CD-CURRENT-MONTH TO DP132O-RUN-MONTH            MPKUT005
025600             MOVE CD-CURRENT-DAY TO DP132O-RUN-DAY                MPKUT005
025700             ACCEPT CD-CURRENT-TIME FROM TIME                     MPKUT005
025800             MOVE CD-CURRENT-HOUR TO DP132O-RUN-HOUR              MPKUT005
025900             MOVE CD-CURRENT-MINUTE TO DP132O-RUN-MINUTE          MPKUT005
026000             MOVE 'MPKUT005' TO DP132O-PROGRAM-NAME               MPKUT005
026100             MOVE 1 TO DP132O-REPORT-NUMBER                       MPKUT005
026500             INITIALIZE DL-DETAIL-LINE                            MPKUT005
026600             SET DL-DETAIL-INDEX TO 1                             MPKUT005
026610*** ADDED BY SLH 09/24/02                                                 
026620             PERFORM 8100-PRINT-TITLE-LINES                               
026630             WRITE RR-RECEIPT-REPORT-RECORD                           MPKU
026640                 FROM DL-NORPT-DETAIL-LINE                            MPKU
026650                 AFTER ADVANCING 2 LINES                                KU
026660             WRITE RR-RECEIPT-REPORT-RECORD                           MPKU
026670                 FROM DL-NORPT-DETAIL-LINE2                           MPKU
026680                 AFTER ADVANCING 2 LINES                                KU
026681*** END                                                                   
026700         WHEN CC-DONT-PRODUCE-REPORT                              MPKUT005
026800             CONTINUE                                             MPKUT005
026900         WHEN OTHER                                               MPKUT005
027000             DISPLAY '** ABEND **'                                MPKUT005
027100             DISPLAY 'MPKUT005 - INVALID REPORT INDICATOR'        MPKUT005
027200             MOVE +4003 TO ABEND-CODE                             MPKUT005
027300             CALL 'ILBOABN0' USING ABEND-CODE                     MPKUT005
027400     END-EVALUATE.                                                MPKUT005
027500                                                                  MPKUT005
027600     PERFORM 7000-READ-STORE-EXTRACT-FILE.                        MPKUT005
027700*1000-EXIT.                                                       MPKUT005
027800                                                                          
027900******************************************************************MPKUT005
028000*  PROCESS A SUMMED STORE EXTRACT RECORD.  REFORMAT THE RECORD   *MPKUT005
028100*  INTO THE RECEIPTS RECORD LAYOUT, AND WRITE THE RECEIPTS FILE  *MPKUT005
028200*  RECORD.  ACCUMULATE THE TOTAL RETAIL UNITS MARKED, FOR THE    *MPKUT005
028300*  COMPANY TOTAL.  IF A DETAIL REPORT HAS BEEN REQUESTED, FORMAT *MPKUT005
028400*  DETAIL LINE ENTRY.                                            *MPKUT005
028500******************************************************************MPKUT005
028600                                                                  MPKUT005
028700 2000-PROCESS-STORE-EXTRACT-RCD.                                  MPKUT005
028800     INITIALIZE MP001O-MONTHLY-RECEIPT-RECORD.                    MPKUT005
028900     MOVE SE-DEPT TO MP001O-DEPARTMENT.                           MPKUT005
029000     MOVE SE-CLASS TO SN-CLASS-AREA.                              MPKUT005
029100     MOVE SN-CLASS TO MP001O-CLASS.                               MPKUT005
029200     MOVE SE-STYLE TO SN-SEQUENCE-NUMBER-AREA.                    MPKUT005
029300     MOVE SN-SEQUENCE-NUMBER TO MP001O-SEQUENCE-NUMBER.           MPKUT005
029400     MOVE SE-VENDOR-NO-INHOUSE TO MP001O-IN-HSE-VENDOR-NUMBER.    MPKUT005
029500     MOVE SE-VENDOR-NO-DUNNS TO MP001O-VENDOR-DUNNS-NUMBER.       MPKUT005
029600     MOVE SE-RETAIL-UNITS-MARK TO MP001O-RETAIL-UNITS-MARKED.     MPKUT005
029700                                                                  MPKUT005
029800     PERFORM 8000-WRITE-RECEIPT-RECORD.                           MPKUT005
029900                                                                  MPKUT005
030000     ADD SE-RETAIL-UNITS-MARK TO PV-TOTAL-RETAIL-UNITS-MARKED.    MPKUT005
030100                                                                  MPKUT005
030200*    IF CC-PRODUCE-REPORT                                         MPKUT005
033004*        PERFORM 3000-PRINT-SKU-VENDOR-RECEIPTS                   MPKUT005
033005*    END-IF.                                                      MPKUT005
033006                                                                  MPKUT005
033007     PERFORM 7000-READ-STORE-EXTRACT-FILE.                        MPKUT005
033008*2000-EXIT.                                                       MPKUT005
033009                                                                  MPKUT005
033010******************************************************************MPKUT005
033011*  FORMAT A SKU/VENDOR ENTRY ON THE MONTHLY RECEIPT REPORT, AND  *MPKUT005
033012*  LOAD IT INTO THE DETAIL LINE.  IF A DETAIL LINE IS FULL WRITE *MPKUT005
033013*  IT TO THE REPORT FILE, BEFORE LOADING THE CURRENT SKU/VENDOR  *MPKUT005
033014*  ENTRY.                                                        *MPKUT005
033015******************************************************************MPKUT005
033016                                                                  MPKUT005
033017 3000-PRINT-SKU-VENDOR-RECEIPTS.                                  MPKUT005
033018     MOVE SE-DEPT TO SV-DEPARTMENT.                               MPKUT005
033019     MOVE SN-CLASS TO SV-CLASS.                                   MPKUT005
033020     MOVE SN-SEQUENCE-NUMBER TO SV-SEQUENCE-NUMBER.               MPKUT005
033021     MOVE SE-VENDOR-NO-DUNNS TO SV-VENDOR-DUNNS-NUMBER.           MPKUT005
033022     MOVE SE-VENDOR-NO-INHOUSE TO SV-IN-HSE-VENDOR-NUMBER.        MPKUT005
033023     MOVE SE-RETAIL-UNITS-MARK TO SV-RETAIL-UNITS-MARKED.         MPKUT005
033024                                                                  MPKUT005
033025     IF DL-DETAIL-INDEX > PC-COLUMN-MAX                           MPKUT005
033026         IF PV-LINE-COUNT > PC-PAGE-SIZE                          MPKUT005
033027             PERFORM 8100-PRINT-TITLE-LINES                       MPKUT005
033028         END-IF                                                   MPKUT005
033029         WRITE RR-RECEIPT-REPORT-RECORD                           MPKUT005
033030             FROM DL-DETAIL-LINE                                  MPKUT005
033040             AFTER ADVANCING PV-ADVANCE-COUNT LINES               MPKUT005
033100         ADD PV-ADVANCE-COUNT TO PV-LINE-COUNT                    MPKUT005
033200         MOVE 1 TO PV-ADVANCE-COUNT                               MPKUT005
033300         SET DL-DETAIL-INDEX TO 1                                 MPKUT005
033400         INITIALIZE DL-DETAIL-LINE                                MPKUT005
033500     END-IF.                                                      MPKUT005
033600                                                                  MPKUT005
033700     MOVE SV-SKU-VENDOR-ENTRY TO                                  MPKUT005
033800               DL-DETAIL-LINE-ENTRY (DL-DETAIL-INDEX).            MPKUT005
033900     SET DL-DETAIL-INDEX UP BY 1.                                 MPKUT005
034000*3200-EXIT.                                                       MPKUT005
034100                                                                  MPKUT005
034200******************************************************************MPKUT005
034300*  WRITE THE LAST DETAIL LINE THAT WAS BEING FORMATTED WHEN      *MPKUT005
034400*  END-OF-FILE WAS REACHED.  WRITE THE TOTAL LINE TO THE REPORT  *MPKUT005
034500*  FILE.                                                         *MPKUT005
034600******************************************************************MPKUT005
034700                                                                  MPKUT005
034800 4000-PRINT-TOTAL-RECEIPTS.                                       MPKUT005
034900     IF PV-LINE-COUNT > PC-PAGE-SIZE                              MPKUT005
035000         PERFORM 8100-PRINT-TITLE-LINES                           MPKUT005
035100     END-IF.                                                      MPKUT005
035200                                                                  MPKUT005
035300     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
035400         FROM DL-DETAIL-LINE                                      MPKUT005
035500         AFTER ADVANCING PV-ADVANCE-COUNT LINES.                  MPKUT005
035600                                                                  MPKUT005
035700     ADD PV-ADVANCE-COUNT TO PV-LINE-COUNT.                       MPKUT005
035800                                                                  MPKUT005
035900     INITIALIZE DL-DETAIL-LINE.                                   MPKUT005
036000     MOVE RT-RECEIPT-TOTAL-ENTRY TO DL-DETAIL-LINE-ENTRY (1).     MPKUT005
036100                                                                  MPKUT005
036200     IF PV-LINE-COUNT > PC-PAGE-SIZE                              MPKUT005
036300         PERFORM 8100-PRINT-TITLE-LINES                           MPKUT005
036400     END-IF.                                                      MPKUT005
036500                                                                  MPKUT005
036600     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
036700         FROM DL-DETAIL-LINE                                      MPKUT005
036800         AFTER ADVANCING 3 LINES.                                 MPKUT005
036900*4000-EXIT.                                                       MPKUT005
037000                                                                  MPKUT005
037100******************************************************************MPKUT005
037200*  READ A MONTHLY STORE EXTRACT RECORD.                          *MPKUT005
037300******************************************************************MPKUT005
037400                                                                  MPKUT005
037500 7000-READ-STORE-EXTRACT-FILE.                                    MPKUT005
037600     READ MONTHLY-STORE-EXTR-FILE                                 MPKUT005
037700         INTO STORE-EXTRACT-LINE                                  MPKUT005
037800         AT END                                                   MPKUT005
037900             SET PS-END-OF-STORE-EXTR-FILE TO TRUE                MPKUT005
038000     END-READ.                                                    MPKUT005
038100*7000-EXIT.                                                       MPKUT005
038200                                                                  MPKUT005
038300******************************************************************MPKUT005
038400*  WRITE A MONTHLY RECEIPT RECORD.                               *MPKUT005
038500******************************************************************MPKUT005
038600                                                                  MPKUT005
038700 8000-WRITE-RECEIPT-RECORD.                                       MPKUT005
038800     WRITE MR-MONTHLY-RECEIPT-RECORD                              MPKUT005
038900         FROM MP001O-MONTHLY-RECEIPT-RECORD.                      MPKUT005
039000*8000-EXIT.                                                       MPKUT005
039100                                                                  MPKUT005
039200******************************************************************MPKUT005
039300*  PRINT TITLE LINES ON THE REPORT.                              *MPKUT005
039400******************************************************************MPKUT005
039500                                                                  MPKUT005
039600 8100-PRINT-TITLE-LINES.                                          MPKUT005
040300                                                                  MPKUT005
040400     ADD 1 TO PV-PAGE-COUNT.                                      MPKUT005
040500     MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    MPKUT005
040600     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
040700         FROM DP132O-STANDRD-HEADER-132-COLS                      MPKUT005
040800         AFTER ADVANCING PAGE.                                    MPKUT005
040900     INITIALIZE DP132O-LASER-NEW-PAGE-INDICATR.                   MPKUT005
041000     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
041100         FROM HL2-HEADING-LINE-2                                  MPKUT005
041200         AFTER ADVANCING 2 LINES.                                 MPKUT005
041300     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
041400         FROM HL3-HEADING-LINE-3                                  MPKUT005
041500         AFTER ADVANCING 2 LINES.                                 MPKUT005
041600     WRITE RR-RECEIPT-REPORT-RECORD                               MPKUT005
041700         FROM HL4-HEADING-LINE-4                                  MPKUT005
041800         AFTER ADVANCING 1 LINE.                                  MPKUT005
041900                                                                  MPKUT005
042000     MOVE 6 TO PV-LINE-COUNT.                                     MPKUT005
042100     MOVE 2 TO PV-ADVANCE-COUNT.                                  MPKUT005
042200*8100-EXIT.                                                       MPKUT005
042300                                                                  MPKUT005
042400******************************************************************MPKUT005
042500*  CLOSE ALL FILES.                                              *MPKUT005
042600******************************************************************MPKUT005
042700                                                                  MPKUT005
042800 9000-EOJ-ROUTINE.                                                MPKUT005
042900     CLOSE MONTHLY-STORE-EXTR-FILE                                MPKUT005
043000           MONTHLY-RECEIPT-FILE                                   MPKUT005
043100           RECEIPT-REPORT-FILE.                                   MPKUT005
043200*9000-EXIT.                                                       MPKUT005
