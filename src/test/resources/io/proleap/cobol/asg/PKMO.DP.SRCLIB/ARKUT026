00001 ******************************************************************01/23/96
00002  IDENTIFICATION DIVISION.                                         ARKUT026
00003 ******************************************************************   LV035
00004                                                                      CL*13
00005  PROGRAM-ID.             ARKUT026.                                   CL*13
00006  AUTHOR.                 TAMARA KOLTUN.                              CL*13
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL*13
00008  DATE-WRITTEN            6-12-95.                                    CL*13
00009  DATE-COMPILED.                                                      CL*13
00010                                                                      CL*13
00011 *---------------------------------------------------------------*    CL*13
00012 * THIS PROGRAM PULLS OUT ALL THE CHECKS PRINTED FOR THE RUN DAY *    CL*13
00013 * FROM THE TCRRFND TABLE.                                       *    CL*13
00014 * USING THIS INFO IT PRINTS THE CHECK REGISTER REPORT, WRITES   *    CL*13
00015 * OUT THE GL UPDATE FILE, AND CHECK LOG FILE.                   *    CL*13
00016 * AT THE END THE PROGRAM INITIALIZES THE CONTROL CARD FILE, SO  *    CL*13
00017 * IT WILL BE PREPARED FOR THE NEXT RUN.                         *    CL*13
00018 *---------------------------------------------------------------*    CL*13
00020 ******************************************************************   CL*13
      *    DATE  07/10/2011                                            *        
      *    WRITTEN BY: JILL ZIMBAL             WR/IR #: 3231A          *        
      * - MADE THE ACCOUNT TYPE FIELD A FILLER.  WITH THE FIRST DATA   *        
      *   CONVERSION THE ACCOUNT TYPE FIELD WILL NO LONGER BE USED     *        
00020 ******************************************************************   CL*13
00019                                                                      CL*13
00020 ******************************************************************   CL*13
00021  ENVIRONMENT DIVISION.                                               CL*13
00022 ******************************************************************   CL*13
00023                                                                      CL*13
00024  CONFIGURATION SECTION.                                              CL*13
00025                                                                      CL*13
00026  SOURCE-COMPUTER.        IBM-3090.                                   CL*13
00027  OBJECT-COMPUTER.        IBM-3090.                                   CL*13
00028                                                                      CL*13
00029  INPUT-OUTPUT SECTION.                                               CL*13
00030                                                                      CL*13
00031  FILE-CONTROL.                                                       CL*13
00032                                                                      CL*13
00055       SELECT FIRST-CHECK-CARD-FILE       ASSIGN TO INP01.            CL**1
00034       SELECT CHECK-REGISTER-REPORT       ASSIGN TO OUT01.            CL*13
00035       SELECT CHECK-LOG                   ASSIGN TO OUT02.            CL*13
00038                                                                      CL*13
00039 ******************************************************************   CL*13
00040  DATA DIVISION.                                                      CL*13
00041 ******************************************************************   CL*13
00042                                                                      CL*13
00043  FILE SECTION.                                                       CL*13
00044  FD  FIRST-CHECK-CARD-FILE                                           CL*13
00045      LABEL RECORDS ARE STANDARD                                      CL*13
00046      BLOCK CONTAINS 0 RECORDS                                        CL*13
00047      RECORDING MODE IS F                                             CL*13
00048      RECORD CONTAINS  80 CHARACTERS                                  CL*13
00049      DATA RECORD IS FIRST-CHECK-CARD-FILE.                           CL*13
00050                                                                      CL*13
00051  01  FIRST-CHECK-CARD-RECORD         PIC X(80).                      CL*13
00052                                                                      CL*13
00053  FD  CHECK-REGISTER-REPORT                                           CL*13
00054      LABEL RECORDS ARE STANDARD                                      CL*13
00055      BLOCK CONTAINS 0 RECORDS                                        CL*13
00056      RECORDING MODE IS F                                             CL*13
00057      RECORD CONTAINS 132 CHARACTERS                                  CL*13
00058      DATA RECORD IS CR-CHECK-REGISTER-RECORD.                        CL*13
00059                                                                      CL*13
00060  01  CR-CHECK-REGISTER-RECORD        PIC X(132).                     CL*13
00070                                                                      CL*13
00071  FD  CHECK-LOG                                                       CL*13
00072      LABEL RECORDS ARE STANDARD                                      CL*13
00073      BLOCK CONTAINS 0 RECORDS                                        CL*13
00074      RECORDING MODE IS F                                             CL*13
00075      RECORD CONTAINS 39  CHARACTERS                                  CL*13
00076      DATA RECORD IS LF-CHECK-LOG-RECORD.                             CL*13
00077                                                                      CL*13
00078  01  LF-CHECK-LOG-RECORD             PIC X(39).                      CL*13
00079                                                                      CL*13
00088                                                                      CL*13
00089 *------------------------------------------------------*             CL*13
00090  WORKING-STORAGE SECTION.                                            CL*13
00091 *------------------------------------------------------*             CL*13
00092  01 PC-PROGRAM-CONSTANTS.                                            CL*13
00093     05 PC-PROGRAM-NAME               PIC X(08)  VALUE 'ARKUT026'.    CL*13
00094     05 PC-REG-REPORT-NBR             PIC 9(01)  VALUE 1.             CL*13
00105     05 PC-VOID-IND                   PIC X(01)  VALUE 'V'.           CL*13
00105     05 PC-NON-VOID-IND               PIC X(01)  VALUE SPACES.        CL*13
00106     05 PC-CHECK-PROCESSED            PIC X(01)  VALUE 'P'.           CL*13
00110                                                                      CL*13
00111  01 PV-PROGRAM-VARIABLES.                                            CL*13
010700    05 PV-CURRENT-DATE.                                           01710001
010800       10 PV-CURRENT-YY              PIC 9(2)   VALUE ZEROES.     01720001
010900       10 PV-CURRENT-MM              PIC 9(2)   VALUE ZEROES.     01730001
011000       10 PV-CURRENT-DD              PIC 9(2)   VALUE ZEROES.     01740001
011100    05 PV-CURRENT-TIME.                                           01750001
011200       10 PV-CURRENT-HOUR            PIC 9(2)   VALUE ZEROES.     01760001
011300       10 PV-CURRENT-MINUTE          PIC 9(2)   VALUE ZEROES.     01770001
00112     05 PV-ABEND-CODE                 PIC  9(04) VALUE ZEROES.        CL*13
00126     05 PV-REG-LINE-COUNT             PIC S9(04) VALUE 61     COMP.   CL*13
00128     05 PV-REG-PAGE-COUNT             PIC S9(04) VALUE ZEROES COMP.   CL*13
00130     05 PV-TOTAL-COUNT                PIC S9(04) VALUE ZEROES COMP.   CL*13
00130     05 PV-VOID-COUNT                 PIC S9(04) VALUE ZEROES COMP.   CL*13
00130     05 PV-NON-VOID-COUNT             PIC S9(04) VALUE ZEROES COMP.   CL*13
00132     05 PV-VOID-AMOUNT                PIC S9(09)V99                   CL*13
00133                                                 VALUE ZEROES COMP.   CL*13
00132     05 PV-NON-VOID-AMOUNT            PIC S9(09)V99                   CL*13
00133                                                 VALUE ZEROES COMP.   CL*13
00144     05 PV-ABEND-PARA-NAME            PIC X(30)  VALUE SPACES.        CL*13
00145     05 PV-DB2-OPERATION-MSG-1        PIC X(40)  VALUE SPACES.        CL*13
00146     05 PV-DB2-TABLE-NAME             PIC X(12)  VALUE SPACES.        CL*13
00147     05 PV-SQLCODE                    PIC Z(8)9- VALUE ZEROES.        CL*13
00148     05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.        CL*13
          05 PV-CHECK-DATE.                                                     
             10 PV-CHECK-CC                PIC X(02)  VALUE SPACES.             
             10 PV-CHECK-YY                PIC X(02)  VALUE SPACES.             
             10 FILLER                     PIC X(01)  VALUE SPACES.             
             10 PV-CHECK-MM                PIC X(02)  VALUE SPACES.             
             10 FILLER                     PIC X(01)  VALUE SPACES.             
             10 PV-CHECK-DD                PIC X(02)  VALUE SPACES.             
00149                                                                      CL*13
00150  01 PS-PROGRAM-SWITCHES.                                             CL*13
00165     05 PS-END-OF-TCRRFND-SW          PIC  X(01) VALUE 'N'.           CL*13
00166        88 PS-END-OF-TCRRFND                     VALUE 'Y'.           CL*13
00168                                                                      CL*13
00187  01 FIRST-CHECK-CARD-LAYOUT.                                         CL**1
00188     05  FIRST-CARD-CHECK-NBR         PIC 9(09)  VALUE ZEROES.        CL**1
          05  FILLER                       PIC X(01)  VALUE SPACES.             
          05  FIRST-CARD-ACCT-NBR          PIC 9(12)  VALUE ZEROES.             
00180                                                                      CL*13
00181 *** STANDARD HEADER LINE - 132 BYTES.                                CL*13
00182      COPY DPWS132O.                                                  CL*13
00183                                                                      CL*13
00184 ****                                                                 CL*13
00185  01 CR-CHECK-REPORT-AREA.                                            CL*13
00186     05 CR-HEADER-LINE-2.                                             CL*13
00187        10 FILLER                     PIC  X(52)      VALUE SPACE.    CL*13
00188        10 FILLER                     PIC  X(27)      VALUE           CL*13
00189        'C H E C K   R E G I S T E R'.                                CL*13
00190        10 FILLER                     PIC  X(53)      VALUE SPACE.    CL*13
00191                                                                      CL*13
00203     05 CR-HEADER-LINE-3.                                             CL*13
00204        10 FILLER                     PIC  X(02) VALUE SPACE.         CL*13
00205        10 FILLER                     PIC  X(05)      VALUE           CL*13
00206                                           'CHECK'.                   CL*13
00207        10 FILLER                     PIC  X(02)      VALUE SPACE.    CL*13
             10 FILLER                     PIC  X(04)      VALUE                
                                                'VOID'.                         
             10 FILLER                     PIC  X(02)      VALUE SPACES.        
00205        10 FILLER                     PIC  X(05)      VALUE           CL*13
00206                                           'CHECK'.                   CL*13
00207        10 FILLER                     PIC  X(05)      VALUE SPACE.    CL*13
00208        10 FILLER                     PIC  X(05)      VALUE           CL*13
00209                                           'CHECK'.                   CL*13
00210        10 FILLER                     PIC  X(03)      VALUE SPACE.    CL*13
00211        10 FILLER                     PIC  X(07)      VALUE           CL*13
00212                                           'ACCOUNT'.                 CL*13
00213        10 FILLER                     PIC  X(04)      VALUE SPACE.    CL*13
00214        10 FILLER                     PIC  X(14)      VALUE           CL*13
00215                                           'CUSTOMER  NAME'.          CL*13
00216        10 FILLER                     PIC  X(15)      VALUE SPACE.    CL*13
00217        10 FILLER                     PIC  X(48)      VALUE           CL*13
00218               'C  U  S  T  O  M  E  R       A  D  D  R  E  S  S'.    CL*13
00219        10 FILLER                     PIC  X(13)      VALUE SPACE.    CL*13
00220                                                                      CL*13
00221     05 CR-HEADER-LINE-4.                                             CL*13
00222        10 FILLER                     PIC  X(03)      VALUE SPACE.    CL*13
00223        10 FILLER                     PIC  X(03)      VALUE           CL*13
00224                                           'NUM'.                     CL*13
00225        10 FILLER                     PIC  X(03)      VALUE SPACE.    CL*13
             10 FILLER                     PIC  X(03)      VALUE                
                                                'IND'.                          
             10 FILLER                     PIC  X(04)      VALUE SPACES.        
00225        10 FILLER                     PIC  X(03)      VALUE           CL*13
                                                'DTE'.                          
00225        10 FILLER                     PIC  X(08)      VALUE SPACE.    CL*13
00226        10 FILLER                     PIC  X(03)      VALUE           CL*13
00227                                           'AMT'.                     CL*13
00228        10 FILLER                     PIC  X(05)      VALUE SPACE.    CL*13
00229        10 FILLER                     PIC  X(03)      VALUE           CL*13
00230                                           'NUM'.                     CL*13
00231        10 FILLER                     PIC  X(92)      VALUE SPACE.    CL*13
00242                                                                      CL*13
00243     05 CR-NO-CHECKS-PRODUCED-MSG.                                    CL*13
00244        10 FILLER                     PIC X(02)    VALUE SPACE.       CL*13
00245        10 FILLER                     PIC X(37)    VALUE              CL*13
00246                 '*** THERE WERE NO CHECKS PRODUCED ***'.             CL*13
00247        10 FILLER                     PIC X(93)    VALUE SPACE.       CL*13
00248                                                                      CL*13
00249     05 CR-DETAIL-LINE.                                               CL*13
00250        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00251        10 CR-CHECK-NUM               PIC  9(06)    VALUE ZEROES.     CL*13
00252        10 FILLER                     PIC  X(03)    VALUE SPACES.     CL*13
             10 CR-VOID-IND                PIC  X(01)    VALUE SPACES.          
             10 FILLER                     PIC  X(02)    VALUE SPACES.          
             10 CR-CHECK-DATE              PIC  X(08)    VALUE SPACES.          
             10 FILLER                     PIC  X(01)    VALUE SPACES.          
00253        10 CR-REFUND-AMT              PIC  Z(5).99  VALUE ZEROES.     CL*13
00254        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
JZ3231       10 CR-ACT-NUM                 PIC  9(10)    VALUE ZEROES.     CL*13
JZ3231       10 FILLER                     PIC  X(03)    VALUE SPACES.     CL*13
00257        10 CR-CUST-NAME               PIC  X(25)    VALUE ZEROES.     CL*13
00258        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00259        10 CR-CUST-ADDRESS            PIC  X(65)    VALUE SPACES.     CL*13
                                                                                
00249     05 CR-TOTAL-LINE-1.                                              CL*13
00261        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00262        10 FILLER                     PIC  X(32)    VALUE ALL '-'.    CL*13
                                                                                
00260     05 CR-TOTAL-LINE-2.                                              CL*13
00261        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00262        10 FILLER                     PIC  X(22)    VALUE             CL*13
00263                                    'TOTAL NON-VOID COUNT: '.         CL*13
00264        10 CR-NON-VOID-COUNT          PIC  Z(6)9    VALUE ZEROES.     CL*13
                                                                                
00260     05 CR-TOTAL-LINE-3.                                              CL*13
00261        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00262        10 FILLER                     PIC  X(22)    VALUE             CL*13
00263                                    'TOTAL NON-VOID AMT  : '.         CL*13
00264        10 CR-NON-VOID-AMT            PIC  Z(6)9.99 VALUE ZEROES.     CL*13
                                                                                
00265     05 CR-TOTAL-LINE-4.                                              CL*13
00261        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00262        10 FILLER                     PIC  X(22)    VALUE             CL*13
00263                                    'TOTAL VOID COUNT    : '.         CL*13
00264        10 CR-VOID-COUNT              PIC  Z(6)9    VALUE ZEROES.     CL*13
                                                                                
00265     05 CR-TOTAL-LINE-5.                                              CL*13
00261        10 FILLER                     PIC  X(01)    VALUE SPACES.     CL*13
00262        10 FILLER                     PIC  X(22)    VALUE             CL*13
00263                                    'TOTAL VOID AMT      : '.         CL*13
00264        10 CR-VOID-AMT                PIC  Z(6)9.99 VALUE ZEROES.     CL*13
                                                                                
00273                                                                      CL*13
00274  01 LF-CHECK-LOG-DATA.                                               CL*13
00275     05 LF-CHECK-NBR               PIC  9(06)       VALUE ZEROES.     CL*13
JZ3231    05 LF-ACCOUNT-NBR             PIC  X(10)       VALUE SPACES.     CL*13
JZ3231    05 FILLER                     PIC  X(02)       VALUE SPACES.          
00277     05 LF-CHECK-DATE              PIC  X(10)       VALUE SPACES.     CL*13
00278     05 LF-CHECK-AMT               PIC  S9(05)V9(4) VALUE ZEROES      CL*13
00279                                   SIGN IS LEADING SEPARATE.          CL*13
          05 LF-VOID-IND                PIC  X(1)        VALUE SPACES.          
00280      COPY ARWS051.                                                   CL*13
00281                                                                      CL*13
00284      COPY ARWS004.                                                   CL*13
00285 ******* DB2 MESSAGE AREAS ********                                   CL*13
00286      COPY DPWS004.                                                   CL*13
00287                                                                      CL*13
00289      EXEC SQL                                                        CL*13
00290           INCLUDE SQLCA                                              CL*13
00291      END-EXEC.                                                       CL*13
00292                                                                      CL*13
00293      EXEC SQL                                                        CL*13
00294           INCLUDE TCRRFND                                            CL*13
00295      END-EXEC.                                                       CL*13
00296                                                                      CL*13
00297      EXEC SQL                                                        CL*13
00298           DECLARE TCRRFND-CRSR CURSOR FOR                            CL*13
00299                SELECT  CK_NBR                                        CL*13
00300                       ,CK_DTE                                        CL*13
00301                       ,VOID_IND                                      CL*13
00302                       ,ACCT_NBR                                      CL*13
00303                       ,RFND_AMT                                      CL*13
00304                       ,CUST_NAME                                     CL*13
00305                       ,CUST_ADR_LNE1                                 CL*13
00306                       ,CUST_ADR_LNE2                                 CL*13
00307                       ,CUST_ADR_LNE3                                 CL*13
00308                       ,PRCD_CDE                                      CL*13
00309                 FROM TCRRFND                                         CL*13
                      WHERE CK_NBR   >= :CRRFND-CK-NBR                          
00312                 ORDER BY CK_NBR                                      CL*16
00313      END-EXEC.                                                       CL*13
00314                                                                      CL*16
00323 ********************************************************             CL*13
00324  PROCEDURE DIVISION.                                                 CL*13
00325 ********************************************************             CL*13
00326  0000-MAIN.                                                          CL*13
                                                                                
           PERFORM 1000-OPEN-FILES.                                             
                                                                                
00327      PERFORM 2000-INITIALIZATION.                                    CL*13
                                                                                
           PERFORM 3000-PROCESS-TCRRFND.                                        
                                                                                
           PERFORM 5000-WRITE-TOTALS.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
00366                                                                      CL*13
00291 ********************************************************             CL**1
00345  1000-OPEN-FILES.                                                    CL**1
00291 ********************************************************             CL**1
00386      OPEN INPUT  FIRST-CHECK-CARD-FILE.                              CL**1
                                                                                
00399      OPEN OUTPUT CHECK-REGISTER-REPORT                               CL*13
00400                  CHECK-LOG.                                          CL*13
                                                                                
00291 ********************************************************             CL**1
00367  2000-INITIALIZATION.                                                CL*13
00291 ********************************************************             CL**1
00370                                                                      CL*13
           ACCEPT PV-CURRENT-DATE FROM DATE.                                    
           ACCEPT PV-CURRENT-TIME FROM TIME.                                    
064600                                                                  06450066
063900     MOVE PV-CURRENT-YY            TO DP132O-RUN-YEAR.            06380066
064000     MOVE PV-CURRENT-MM            TO DP132O-RUN-MONTH.           06390066
064000     MOVE PV-CURRENT-DD            TO DP132O-RUN-DAY.             06390066
064200                                                                  06410066
064400     MOVE PV-CURRENT-HOUR          TO DP132O-RUN-HOUR.            06430066
064500     MOVE PV-CURRENT-MINUTE        TO DP132O-RUN-MINUTE.          06440066
00385                                                                      CL*13
00396      MOVE PC-PROGRAM-NAME          TO DP132O-PROGRAM-NAME.           CL*13
00397                                                                      CL*13
           READ FIRST-CHECK-CARD-FILE INTO                                      
              FIRST-CHECK-CARD-LAYOUT.                                          
                                                                                
00376      DISPLAY '------------------------------------'.                 CL**1
           DISPLAY '       ARKUT026 CNTL CARD           '.                      
           DISPLAY 'FIRST CK CNTL CARD: ' FIRST-CHECK-CARD-LAYOUT.              
00376      DISPLAY '------------------------------------'.                 CL**1
                                                                                
00380      MOVE FIRST-CARD-CHECK-NBR     TO CRRFND-CK-NBR.                 CL**1
00404                                                                      CL*13
00291 ********************************************************             CL**1
00433  3000-PROCESS-TCRRFND.                                               CL*13
00291 ********************************************************             CL**1
                                                                                
           INITIALIZE PS-END-OF-TCRRFND-SW.                                     
           MOVE 61                             TO PV-REG-LINE-COUNT.            
00329      PERFORM 7000-OPEN-TCRRFND-CRSR.                                 CL*13
                                                                                
00434      PERFORM 7100-FETCH-TCRRFND-CRSR.                                CL*13
                                                                                
00330      PERFORM 4000-PROCESS-CHECKS                                     CL*13
00331         UNTIL PS-END-OF-TCRRFND.                                     CL*13
                                                                                
00440      PERFORM 7200-CLOSE-TCRRFND-CRSR.                                CL*13
                                                                                
00291 ********************************************************             CL**1
00433  4000-PROCESS-CHECKS.                                                CL*13
00291 ********************************************************             CL**1
      * CHECK TO SEE IF CHECK HAS ALREADY BEEN PROCESSED.                       
00470      IF CRRFND-PRCD-CDE NOT = 'P'                                    CL*13
              ADD 1                  TO PV-TOTAL-COUNT                          
              IF CRRFND-VOID-IND = 'V'                                          
                 ADD 1               TO PV-VOID-COUNT                           
                 ADD CRRFND-RFND-AMT TO PV-VOID-AMOUNT                          
              ELSE                                                              
                 ADD 1               TO PV-NON-VOID-COUNT                       
                 ADD CRRFND-RFND-AMT TO PV-NON-VOID-AMOUNT                      
              END-IF                                                            
00444         PERFORM 4100-WRITE-CHECK-REGISTER                            CL*13
00445         PERFORM 4200-WRITE-CHECK-LOG                                 CL*13
              PERFORM 8000-UPDATE-TCRRFND                                       
00448      END-IF.                                                         CL*13
00451                                                                      CL*13
00434      PERFORM 7100-FETCH-TCRRFND-CRSR.                                CL*13
00491                                                                      CL*13
00291 ********************************************************             CL**1
00492  4100-WRITE-CHECK-REGISTER.                                          CL*13
00291 ********************************************************             CL**1
                                                                                
00493      PERFORM 6000-WRITE-HEADER.                                      CL*13
00494                                                                      CL*13
00495      ADD 1                         TO PV-REG-LINE-COUNT.             CL*13
00496      MOVE CRRFND-CK-NBR            TO CR-CHECK-NUM.                  CL*13
           MOVE CRRFND-VOID-IND          TO CR-VOID-IND.                        
           MOVE CRRFND-CK-DTE            TO PV-CHECK-DATE.                      
                                                                                
           MOVE SPACES                   TO CR-CHECK-DATE.                      
           STRING                                                               
              PV-CHECK-MM         DELIMITED BY SIZE                             
              '/'                 DELIMITED BY SIZE                             
              PV-CHECK-DD         DELIMITED BY SIZE                             
              '/'                 DELIMITED BY SIZE                             
              PV-CHECK-YY         DELIMITED BY SIZE                             
           INTO CR-CHECK-DATE                                                   
           END-STRING.                                                          
                                                                                
00497      MOVE CRRFND-RFND-AMT          TO CR-REFUND-AMT.                 CL*13
00498      MOVE CRRFND-ACCT-NBR          TO CR-ACT-NUM.                    CL*13
00499      MOVE CRRFND-CUST-NAME         TO CR-CUST-NAME.                  CL*13
                                                                                
           MOVE SPACES                   TO CR-CUST-ADDRESS.                    
           IF CRRFND-CUST-ADR-LNE3 = SPACES                                     
               STRING                                                          C
                  CRRFND-CUST-ADR-LNE1 DELIMITED BY SIZE                       C
                  ' '                  DELIMITED BY SIZE                       C
                  CRRFND-CUST-ADR-LNE2 DELIMITED BY SIZE                       C
               INTO CR-CUST-ADDRESS                                            C
               END-STRING                                                      C
           ELSE                                                                 
               STRING                                                          C
                  CRRFND-CUST-ADR-LNE1 DELIMITED BY SIZE                       C
                  ' '                  DELIMITED BY SIZE                       C
                  CRRFND-CUST-ADR-LNE3 DELIMITED BY SIZE                       C
               INTO CR-CUST-ADDRESS                                            C
               END-STRING                                                      C
           END-IF.                                                              
00507                                                                      CL*13
00508      WRITE CR-CHECK-REGISTER-RECORD                                  CL*13
              FROM CR-DETAIL-LINE                                               
00509      AFTER ADVANCING 1 LINE.                                         CL*13
                                                                                
00291 ********************************************************             CL**1
00567  4200-WRITE-CHECK-LOG.                                               CL*13
00291 ********************************************************             CL**1
00568      INITIALIZE LF-CHECK-LOG-DATA.                                   CL*13
00569      MOVE CRRFND-ACCT-NBR          TO LF-ACCOUNT-NBR.                CL*13
00570      MOVE CRRFND-CK-NBR            TO LF-CHECK-NBR.                  CL*13
00586      MOVE CRRFND-CK-DTE            TO LF-CHECK-DATE.                 CL*13
00572      MOVE CRRFND-RFND-AMT          TO LF-CHECK-AMT.                  CL*13
           MOVE CRRFND-VOID-IND          TO LF-VOID-IND.                        
00573      WRITE LF-CHECK-LOG-RECORD FROM LF-CHECK-LOG-DATA.               CL*13
00627                                                                      CL*13
00291 ********************************************************             CL**1
00433  5000-WRITE-TOTALS.                                                  CL*13
00291 ********************************************************             CL**1
00640                                                                      CL*13
00436      IF PV-TOTAL-COUNT = 0                                           CL*13
00437         PERFORM 6000-WRITE-HEADER                                    CL*13
00335         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00336            FROM CR-NO-CHECKS-PRODUCED-MSG                            CL*13
00337         AFTER ADVANCING 2 LINES                                      CL*13
00332      ELSE                                                            CL*13
00611         MOVE PV-NON-VOID-AMOUNT      TO CR-NON-VOID-AMT              CL*13
              MOVE PV-NON-VOID-COUNT       TO CR-NON-VOID-COUNT                 
00611         MOVE PV-VOID-AMOUNT          TO CR-VOID-AMT                  CL*13
              MOVE PV-VOID-COUNT           TO CR-VOID-COUNT                     
                                                                                
00612         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00613            FROM CR-TOTAL-LINE-1                                      CL*13
              AFTER ADVANCING 1 LINES                                           
00612         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00613            FROM CR-TOTAL-LINE-2                                      CL*13
              AFTER ADVANCING 1 LINE                                            
00612         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00613            FROM CR-TOTAL-LINE-3                                      CL*13
              AFTER ADVANCING 1 LINE                                            
00612         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00613            FROM CR-TOTAL-LINE-4                                      CL*13
              AFTER ADVANCING 1 LINE                                            
00612         WRITE CR-CHECK-REGISTER-RECORD                               CL*13
00613            FROM CR-TOTAL-LINE-5                                      CL*13
              AFTER ADVANCING 1 LINE                                            
           END-IF.                                                              
                                                                                
00291 ********************************************************             CL**1
00514  6000-WRITE-HEADER.                                                  CL*13
00291 ********************************************************             CL**1
00515      IF PV-REG-LINE-COUNT > 60                                       CL*13
00516         ADD  1                 TO PV-REG-PAGE-COUNT                  CL*13
00517         MOVE PV-REG-PAGE-COUNT                                       CL*13
00518                                TO DP132O-PAGE-NUMBER                 CL*13
00519         MOVE 0                 TO PV-REG-LINE-COUNT                  CL*13
00520         MOVE PC-REG-REPORT-NBR TO DP132O-REPORT-NUMBER               CL*13
00521         WRITE CR-CHECK-REGISTER-RECORD  FROM                         CL*13
00522                              DP132O-STANDRD-HEADER-132-COLS          CL*13
00523                AFTER ADVANCING PAGE                                  CL*13
00524         WRITE CR-CHECK-REGISTER-RECORD  FROM                         CL*13
00525                               CR-HEADER-LINE-2                       CL*13
00526                AFTER ADVANCING 1 LINE                                CL*13
00527         WRITE CR-CHECK-REGISTER-RECORD  FROM                         CL*13
00528                               CR-HEADER-LINE-3                       CL*13
00529                AFTER ADVANCING 2 LINES                               CL*13
00536         WRITE CR-CHECK-REGISTER-RECORD  FROM                         CL*13
00537                               CR-HEADER-LINE-4                       CL*13
00538                AFTER ADVANCING 1 LINE                                CL*13
00542         ADD  12                TO PV-REG-LINE-COUNT                  CL*13
00564      END-IF.                                                         CL*13
                                                                                
00291 ********************************************************             CL**1
00706  7000-OPEN-TCRRFND-CRSR.                                             CL*26
00291 ********************************************************             CL**1
                                                                                
029500     EXEC SQL                                                             
029600        OPEN TCRRFND-CRSR                                                 
029700     END-EXEC.                                                            
029800                                                                          
029900     EVALUATE TRUE                                                        
030000        WHEN SQLCODE = ZERO                                               
030100             CONTINUE                                                     
030200        WHEN OTHER                                                        
030300             MOVE '7000-OPEN-TCRRFND-CRSR'                                
                                            TO PV-ABEND-PARA-NAME               
030400             MOVE 'OPEN CURSOR'       TO PV-DB2-OPERATION-MSG-1           
030500             MOVE 'TCRRFND'           TO PV-DB2-TABLE-NAME                
030600             PERFORM 9998-SQL-ABEND                                       
030700     END-EVALUATE.                                                        
                                                                                
00291 ********************************************************             CL**1
00706  7100-FETCH-TCRRFND-CRSR.                                            CL*26
00291 ********************************************************             CL**1
00454      EXEC SQL                                                        CL*13
00455          FETCH TCRRFND-CRSR                                          CL*13
00456          INTO  :CRRFND-CK-NBR                                        CL*13
00457               ,:CRRFND-CK-DTE                                        CL*13
00458               ,:CRRFND-VOID-IND                                      CL*13
00459               ,:CRRFND-ACCT-NBR                                      CL*13
00460               ,:CRRFND-RFND-AMT                                      CL*13
00461               ,:CRRFND-CUST-NAME                                     CL*13
00462               ,:CRRFND-CUST-ADR-LNE1                                 CL*13
00463               ,:CRRFND-CUST-ADR-LNE2                                 CL*13
00464               ,:CRRFND-CUST-ADR-LNE3                                 CL*13
00465               ,:CRRFND-PRCD-CDE                                      CL*13
00466      END-EXEC.                                                       CL*13
00467                                                                      CL*13
00468      EVALUATE TRUE                                                   CL*13
00469          WHEN SQLCODE = ZERO                                         CL*13
00470            CONTINUE                                                  CL*13
00476          WHEN SQLCODE = +100                                         CL*13
00477              SET  PS-END-OF-TCRRFND        TO TRUE                   CL*13
00478          WHEN OTHER                                                  CL*13
00479              MOVE '0310-FETCH-PROCESS-ROW' TO PV-ABEND-PARA-NAME     CL*13
00480              MOVE 'FETCH CRRFND-CRS'  TO PV-DB2-OPERATION-MSG-1      CL*13
00481              MOVE 'TCRRFND'           TO PV-DB2-TABLE-NAME           CL*13
00482              PERFORM 9998-SQL-ABEND                                  CL*13
00483      END-EVALUATE.                                                   CL*13
                                                                                
00291 ********************************************************             CL**1
00706  7200-CLOSE-TCRRFND-CRSR.                                            CL*26
00291 ********************************************************             CL**1
029500     EXEC SQL                                                             
029600        CLOSE TCRRFND-CRSR                                                
029700     END-EXEC.                                                            
                                                                                
029900     EVALUATE TRUE                                                        
030000        WHEN SQLCODE = ZERO                                               
030100             CONTINUE                                                     
030200        WHEN OTHER                                                        
030300             MOVE '7200-CLOSE-TCRRFND-CRSR'                               
                                            TO PV-ABEND-PARA-NAME               
030400             MOVE 'CLOSE CURSOR'      TO PV-DB2-OPERATION-MSG-1           
030500             MOVE 'TCRRFND'           TO PV-DB2-TABLE-NAME                
030600             PERFORM 9998-SQL-ABEND                                       
030700     END-EVALUATE.                                                        
                                                                                
00291 ********************************************************             CL**1
00706  8000-UPDATE-TCRRFND.                                                CL*26
00291 ********************************************************             CL**1
00707      EXEC SQL                                                        CL*26
00708          UPDATE TCRRFND                                              CL*26
00709          SET    PRCD_CDE = :PC-CHECK-PROCESSED                       CL*26
00710          WHERE  CK_NBR = :CRRFND-CK-NBR                              CL*34
00711      END-EXEC.                                                       CL*26
00712                                                                      CL*26
00713      EVALUATE TRUE                                                   CL*26
00714          WHEN SQLCODE = ZEROES                                       CL*26
00715             CONTINUE                                                 CL*26
00716          WHEN OTHER                                                  CL*26
00717             MOVE '8000-UPDATE-TCRRFND' TO PV-ABEND-PARA-NAME         CL*26
00718             MOVE 'UPDATE TCRRFND  '    TO PV-DB2-OPERATION-MSG-1     CL*26
00719             MOVE 'TCRRFND'             TO PV-DB2-TABLE-NAME          CL*26
00720             PERFORM 9998-SQL-ABEND                                   CL*26
00721      END-EVALUATE.                                                   CL*26
                                                                                
00291 ********************************************************             CL**1
00706  9000-CLOSE-FILES.                                                   CL*26
00291 ********************************************************             CL**1
00360      CLOSE  FIRST-CHECK-CARD-FILE                                    CL*13
00361             CHECK-REGISTER-REPORT                                    CL*13
00362             CHECK-LOG.                                               CL*13
00723                                                                      CL*25
00291 ********************************************************             CL**1
00724  9998-SQL-ABEND.                                                     CL*13
00291 ********************************************************             CL**1
00725      MOVE SQLCODE     TO PV-SQLCODE.                                 CL*13
00726      DISPLAY '** ABEND           ** = SQLABEND'.                     CL*13
00727      DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.             CL*13
00728      DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.           CL*13
00729      DISPLAY '** TABLE AFFECTED  ** = ' PV-DB2-TABLE-NAME.           CL*13
00730      DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.      CL*13
00731      DISPLAY '** SQLCODE         ** = ' PV-SQLCODE.                  CL*13
00732      COPY DPPD004.                                                   CL*13
00733                                                                      CL*13
00734      EXEC SQL                                                        CL*13
00735           ROLLBACK                                                   CL*13
00736      END-EXEC.                                                       CL*13
00737                                                                      CL*13
00738      MOVE +4013                  TO PV-ABEND-CODE.                   CL*13
00739      CALL 'ILBOABN0'   USING PV-ABEND-CODE.                          CL*13
