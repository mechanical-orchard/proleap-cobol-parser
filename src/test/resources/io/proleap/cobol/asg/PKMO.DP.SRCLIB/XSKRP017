       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP017.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           01/29/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_BUS_GP REPORT                                  00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_BUS_GP ERROR DATA INTO A REPORT        00100000
      ******************************************************************00110000
      *DATE:01/29/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.                        IBM-3090.                00160000
       OBJECT-COMPUTER.                        IBM-3090.                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO RPT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS ARE STANDARD                                   00310000
           BLOCK CONTAINS 0 CHARACTERS.                                 00320000
       01  INPUT-RECORD                    PIC X(80).                   00330000
                                                                        00340000
       FD  REPORT-FILE                                                  00350000
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 CHARACTERS.                                 00380000
       01  REPORT-RECORD                   PIC X(133).                  00390000
                                                                        00400000
       WORKING-STORAGE SECTION.                                         00410000
      ******************************************************************00420000
      *SWITCHES                                                         00430000
      ******************************************************************00440000
       01  PS-PROGRAM-SWITCHES.                                         00450000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00460000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00470000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00480000
      ******************************************************************00490000
      **TIME FIELDS                                                     00500000
      ******************************************************************00510000
       01  DF-DATETIME-FIELDS.                                          00520000
           05  DF-ACCEPT-TIME.                                          00530000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00540000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00550000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00560000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00570000
           05  DF-ACCEPT-DATE.                                          00580000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00590000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00600000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00610000
      ******************************************************************00620000
      *PROGRAM CONSTANTS                                                00630000
      ******************************************************************00640000
       01  PC-PROGRAM-CONSTANTS.                                        00650000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP017'.   00660000
      ******************************************************************00670000
      **PROGRAM COUNTS                                                  00680000
      ******************************************************************00690000
       01  PC-PROGRAM-COUNTERS.                                         00700000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00710000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00720000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00730000
      ***************************************************************** 00740000
      **ABEND CODE FIELDS                                               00750000
      ***************************************************************** 00760000
       01  ABEND-CODE                     PIC S9(4)                     00770000
                                                 VALUE ZEROS COMP SYNC. 00780000
       01  PV-MISC-ABEND-FIELDS.                                        00790000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00800000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00810000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00820000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00830000
      ***************************************************************** 00840000
      **REPORT HEADINGS                                                 00850000
      ***************************************************************** 00860000
       01  REPORT-HEADING-1.                                            00870000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00880000
           05  FILLER                     PIC  X(22)                    00890000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00900000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00910000
       01  REPORT-HEADING-2.                                            00920000
           05  FILLER                     PIC  X(53)  VALUE SPACES.     00930000
           05  FILLER                     PIC  X(27)                    00940000
                                 VALUE 'XST_BUS_GP INTEGRITY REPORT'.   00950000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00960000
       01  REPORT-HEADING-3.                                            00970000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     00980000
           05  FILLER                     PIC  X(10)                    00990000
                                 VALUE 'BUS_GP_NBR'.                    01000000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01010000
           05  FILLER                     PIC  X(11)                    01020000
                                 VALUE 'BUS_GP_DESC'.                   01030000
           05  FILLER                     PIC  X(16)  VALUE SPACES.     01040000
           05  FILLER                     PIC  X(11)                    01041000
                                 VALUE 'BUS_AREA_ID'.                   01042000
       01  REPORT-HEADING-4.                                            01043000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01044000
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01045000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01046000
           05  FILLER                     PIC  X(25)  VALUE ALL '-'.    01047000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01048000
           05  FILLER                     PIC  X(11)  VALUE ALL '-'.    01049000
      ***************************************************************** 01050000
      **REPORT DETAIL                                                   01060000
      ***************************************************************** 01070000
       01  REPORT-DETAIL-1.                                             01080000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01090000
           05  RD-XS-BUS-GP-NBR           PIC  ZZ9.                     01100000
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01110000
           05  RD-XS-BUS-AREA-DESC        PIC  X(25).                   01120000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01130000
           05  RD-XS-BUS-AREA-ID          PIC  Z9.                      01140000
                                                                        01150000
       01  REPORT-DETAIL-2.                                             01160000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01170000
           05  RD-VSM-BUS-GP-NBR          PIC  ZZ9.                     01171000
           05  FILLER                     PIC  X(09)  VALUE SPACES.     01172000
           05  RD-VSM-BUS-AREA-DESC       PIC  X(25).                   01173000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01174000
           05  RD-VSM-BUS-AREA-ID         PIC  Z9.                      01175000
                                                                        01176000
       01  RF-END-OF-REPORT.                                            01177000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01178000
           05  FILLER                     PIC X(21)                     01179000
                                 VALUE '***END OF REPORT***'.           01180000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01190000
      ***************************************************************** 01200000
      **COPYBOOKS                                                       01210000
      ***************************************************************** 01220000
      ****ABEND ROUTINE INFORMATION                                     01230000
           COPY DPWS900.                                                01240000
      ****ABEND ROUTINE INFORMATION                                     01250000
           COPY DPWS004.                                                01260010
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01270000
           COPY DPWS132O.                                               01280000
      ***INPUT RECORD                                                   01290000
           COPY XSRD004.                                                01300000
                                                                        01310000
       PROCEDURE DIVISION.                                              01320000
      ******************************************************************01330000
       0000-MAINLINE.                                                   01340000
                                                                        01350000
           PERFORM 1000-INITIALIZATION.                                 01360000
           PERFORM 2000-PROCESS-FILES                                   01370000
                   UNTIL PS-EOF-INPUT-FILE.                             01380000
           PERFORM 9999-WRAPUP.                                         01390000
           GOBACK.                                                      01400000
                                                                        01410000
       EJECT.                                                           01420000
      ******************************************************************01430000
      *INITIALIZATION ROUTINE                                           01440000
      ******************************************************************01450000
       1000-INITIALIZATION.                                             01460000
                                                                        01470000
           OPEN INPUT   INPUT-FILE                                      01480000
                OUTPUT  REPORT-FILE.                                    01490000
                                                                        01500000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01510000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01520000
                                                                        01530000
           PERFORM 1100-SETUP-HEADINGS.                                 01540000
                                                                        01550000
           PERFORM 9999-READ-INPUT-FILE.                                01560000
                                                                        01570000
       EJECT.                                                           01580000
      ******************************************************************01590000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     01600000
      ******************************************************************01610000
       1100-SETUP-HEADINGS.                                             01620000
                                                                        01630000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             01640000
                                                                        01650000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               01660000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              01670000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                01680000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               01690000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             01700000
                                                                        01710000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                01720000
           MOVE 1                TO DP132O-REPORT-NUMBER.               01730000
                                                                        01740000
           PERFORM 4000-PRINT-HEADINGS.                                 01750000
                                                                        01760000
       EJECT.                                                           01770000
      ******************************************************************01780000
      *PROCESS BOTH FILES                                               01790000
      ******************************************************************01800000
       2000-PROCESS-FILES.                                              01810000
                                                                        01820000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              01830000
                                                                        01840000
           PERFORM 4100-PRINT-DETAIL.                                   01850000
                                                                        01860000
           IF  PC-LINE-COUNT > 60                                       01870000
               PERFORM 4000-PRINT-HEADINGS                              01880000
           END-IF.                                                      01890000
                                                                        01900000
           PERFORM 9999-READ-INPUT-FILE.                                01910000
                                                                        01920000
       EJECT.                                                           01930000
      ******************************************************************01940000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            01950000
      ******************************************************************01960000
       4000-PRINT-HEADINGS.                                             01970000
                                                                        01980000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             01990000
                                                                        02000000
           INITIALIZE PC-LINE-COUNT.                                    02010000
           ADD +1                     TO PC-PAGE-COUNT.                 02020000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            02030000
                                                                        02040000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      02050000
                                                  AFTER ADVANCING PAGE. 02060000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    02070000
                                               AFTER ADVANCING 2 LINES. 02080000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    02090000
                                               AFTER ADVANCING 1 LINES. 02100000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    02110000
                                               AFTER ADVANCING 3 LINES. 02120000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    02130000
                                               AFTER ADVANCING 1 LINES. 02140000
                                                                        02150000
           ADD +7                     TO PC-LINE-COUNT.                 02160000
                                                                        02170000
       EJECT.                                                           02180000
      ******************************************************************02190000
      *PRINTS DETAIL REPORT                                             02200000
      ******************************************************************02210000
       4100-PRINT-DETAIL.                                               02220000
                                                                        02230000
           MOVE '4100-PRINT-DETAIL' TO PV-PARAGRAPH-NAME.               02240000
                                                                        02250000
           MOVE XSRD004-XS-BUS-GP-NBR TO RD-XS-BUS-GP-NBR.              02260000
           MOVE XSRD004-XS-BUS-AREA-DESC TO RD-XS-BUS-AREA-DESC.        02270000
           MOVE XSRD004-XS-BUS-AREA-ID TO RD-XS-BUS-AREA-ID.            02280000
                                                                        02290000
           MOVE XSRD004-VSM-BUS-GP-NBR TO RD-VSM-BUS-GP-NBR.            02300000
           MOVE XSRD004-VSM-BUS-AREA-DESC TO RD-VSM-BUS-AREA-DESC.      02310000
           MOVE XSRD004-VSM-BUS-AREA-ID TO RD-VSM-BUS-AREA-ID.          02320000
                                                                        02330000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     02340000
                                            AFTER ADVANCING 2 LINES.    02350000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     02360000
                                            AFTER ADVANCING 1 LINE.     02370000
                                                                        02380000
           ADD +3                   TO PC-LINE-COUNT.                   02390000
                                                                        02400000
       EJECT.                                                           02410000
      ******************************************************************02420000
      *READS INPUT FILE                                                 02430000
      ******************************************************************02440000
       9999-READ-INPUT-FILE.                                            02450000
                                                                        02460000
           READ INPUT-FILE INTO XSRD004-BUSGP-ERR-RECORD                02470000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     02480000
           END-READ.                                                    02490000
                                                                        02500000
           ADD +1  TO PC-RECORDS-READ.                                  02510000
                                                                        02520000
       EJECT.                                                           02530000
      ******************************************************************02540000
      *PROGRAM END PROCESSING                                           02550000
      ******************************************************************02560000
       9999-WRAPUP.                                                     02570000
                                                                        02580000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    02590000
                                    AFTER ADVANCING 2 LINES.            02600000
                                                                        02610000
           CLOSE INPUT-FILE                                             02620000
                 REPORT-FILE.                                           02630000
                                                                        02640000
           DISPLAY '*************************************************'. 02650000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     02660000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      02670000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      02680000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          02690000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          02700000
           DISPLAY '*************************************************'. 02710000
                                                                        02720000
       EJECT.                                                           02730000
                                                                        02740000
       EJECT.                                                           02750000
