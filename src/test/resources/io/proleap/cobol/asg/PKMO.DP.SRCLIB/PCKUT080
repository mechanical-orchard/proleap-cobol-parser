000010******************************************************************        
000020 IDENTIFICATION DIVISION.                                                 
000030******************************************************************        
000200 TITLE 'PURGE STORE DATA BASED ON LAST PHY INV DATE FOR STORE'            
000300                                                                          
000400 PROGRAM-ID.             PCKUT080.                                        
000500 AUTHOR.                 AL PETERS.                                       
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            FEBRUARY 2008.                                   
000800 DATE-COMPILED.                                                           
000900                                                                          
000910******************************************************************        
000920 ENVIRONMENT DIVISION.                                                    
000930******************************************************************        
000940                                                                          
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500                                                                          
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT STORE-EXTRACT-FILE                                            
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002600     SELECT STORE-OUTPUT-FILE                                             
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002810******************************************************************        
002820 DATA DIVISION.                                                           
002830******************************************************************        
002840                                                                          
003000 FILE SECTION.                                                            
003100                                                                          
003200 FD  STORE-EXTRACT-FILE                                                   
003300     RECORDING MODE IS F                                                  
003400     LABEL RECORDS ARE STANDARD                                           
003500     BLOCK CONTAINS 0 RECORDS.                                            
003600                                                                          
003700 01  STORE-EXTRACT-RECORD             PIC X(150).                         
004500                                                                          
004600 FD  STORE-OUTPUT-FILE                                                    
004700     RECORDING MODE IS F                                                  
004800     LABEL RECORDS ARE STANDARD                                           
004900     BLOCK CONTAINS 0 RECORDS.                                            
005000                                                                          
005100 01  STORE-OUTPUT-RECORD              PIC X(150).                         
005200                                                                          
005210******************************************************************        
005220 WORKING-STORAGE SECTION.                                                 
005230******************************************************************        
005400                                                                          
005500 01  FILLER.                                                              
005600     05  FILLER                      PIC  X(36)    VALUE                  
005700         ' ***** WORKING STORAGE PGM=PCKUT080 '.                          
006400                                                                          
006410 01  DK-LOC-NBR                       PIC S9(04) VALUE ZERO               
006420                                                   COMP SYNC.             
006500 01  PS-SWITCHES.                                                         
006600     05  PS-ERROR-SW                  PIC X(01) VALUE 'N'.                
006700         88  PS-ERROR-YES                       VALUE 'Y'.                
006800         88  PS-ERROR-NO                        VALUE 'N'.                
006900     05  PS-STOP-SW                   PIC X(01) VALUE 'N'.                
007000         88  PS-STOP-YES                        VALUE 'Y'.                
007100         88  PS-STOP-NO                         VALUE 'N'.                
007200                                                                          
007300     05  PS-EOF-INPUT-SW              PIC X(01) VALUE 'N'.                
007400         88  PS-EOF-INPUT                       VALUE 'Y'.                
007410         88  PS-NOT-EOF-INPUT                   VALUE 'N'.                
007420                                                                          
007430     05  PS-PURGE-SW                  PIC X(01) VALUE 'N'.                
007440         88  PS-PURGE-YES                       VALUE 'Y'.                
007450         88  PS-PURGE-NO                        VALUE 'N'.                
007460                                                                          
007470     05  PS-KEEP-SW                   PIC X(01) VALUE 'N'.                
007480         88  PS-KEEP-YES                        VALUE 'Y'.                
007490         88  PS-KEEP-NO                         VALUE 'N'.                
007500                                                                          
007600     05  PS-FOUND-SW                  PIC X(01) VALUE 'N'.                
007700         88  PS-FOUND-YES                       VALUE 'Y'.                
007710         88  PS-FOUND-NO                        VALUE 'N'.                
007720                                                                          
007800 01  PC-CONSTANTS.                                                        
007900     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
008000                                                   'PCKUT080'.            
008100     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
008200                                                   'ILBOABN0'.            
008300     05  PC-NOT-FOUND-MSG            PIC  X(09)    VALUE                  
008400                                                'NOT FOUND'.              
008410     05  PC-PURGE-MSG                PIC  X(05)    VALUE                  
008420                                                'PURGE'.                  
008430     05  PC-KEEP-MSG                 PIC  X(05)    VALUE                  
008440                                                'KEEP '.                  
008500                                                                          
008600 01  PV-MISC-STUFF.                                                       
008601     05  PV-FUNC-QTY                  PIC  S9(9) VALUE ZERO               
008602                                                   COMP SYNC.             
008610     05  PV-PREV-LOC-NBR              PIC  9(04) VALUE ZERO               
008620                                                   COMP SYNC.             
008630     05  PV-UNIT-BK-DTE-DAYS          PIC  X(10) VALUE SPACES.            
008631                                                                          
008632     05  PV-CURR-DATE                 PIC  X(10).                         
008640                                                                          
008700     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
008900     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
009000     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
009100                                                   COMP SYNC.             
009400     05  PV-STORE-COUNT               PIC S9(09) COMP-3                   
009500                                              VALUE ZERO.                 
009910     05  PV-READ-COUNT                PIC S9(09) COMP-3                   
009920                                              VALUE ZERO.                 
009930     05  PV-WRITE-COUNT               PIC S9(09) COMP-3                   
009940                                              VALUE ZERO.                 
009950     05  PV-PURGE-COUNT               PIC S9(09) COMP-3                   
009960                                              VALUE ZERO.                 
009970     05  PV-TOTAL-REC-COUNT           PIC S9(09) COMP-3                   
009980                                              VALUE ZERO.                 
010300     05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
010400     05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
010500                                                                          
010510     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZ9.                    
010511     05  PV-DISP-STOR-CNT             PIC ZZZ,ZZZ,ZZ9.                    
010512     05  PV-DISP-DAYS                 PIC ZZZZZZZZ9.                      
010520                                                                          
010600 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
010700                                                     COMP SYNC.           
011700     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
011800                                                                          
011810*---------------------------------------------------------------*       00
011820*    COPYBOOK FOR PCRD038                                       *       00
011830*---------------------------------------------------------------*       00
012000     COPY PCRD038.                                                        
012100                                                                          
012110*---------------------------------------------------------------*       00
012120*    DB2 AREA FOR THE PARM TABLE                                *       00
012130*---------------------------------------------------------------*       00
012300     EXEC SQL                                                           03
012400         INCLUDE TKRPRPM                                                03
012500     END-EXEC.                                                          03
012600                                                                        03
012601*---------------------------------------------------------------*       00
012602*    DB2 AREA FOR THE LOGISTICS MERCHANDISE INVENTORY PARMS     *       00
012603*---------------------------------------------------------------*       00
012620     EXEC SQL                                                           03
012630         INCLUDE XLT00025                                               03
012640     END-EXEC.                                                          03
012650                                                                        03
012710******************************************************************        
012720*  COMMUNICATIONS AREA FOR DB2                                            
012730******************************************************************        
012800     EXEC SQL                                                             
012900         INCLUDE SQLCA                                                    
013000     END-EXEC.                                                            
013100                                                                          
013200     COPY SQLCA2.                                                         
013300                                                                          
013310******************************************************************        
013320*  DB2 FORMATTED MESSAGE AREA                                             
013330******************************************************************        
013400     COPY DPWS004.                                                        
013500                                                                          
013600******************************************************************        
013700 PROCEDURE DIVISION.                                                      
013800******************************************************************        
014400     PERFORM 1000-INITIALIZATION.                                         
014500                                                                          
014600     PERFORM 2000-PROCESS UNTIL PS-EOF-INPUT.                             
014700                                                                          
014800     PERFORM 9000-QUIT.                                                   
014900                                                                          
015000     GOBACK.                                                              
015100                                                                          
015101******************************************************************        
015102* INITIALIZATION                                                          
015103******************************************************************        
015200 1000-INITIALIZATION.                                                     
015300                                                                          
015400     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
015500                                                                          
015600     DISPLAY 'START PCKUT080'.                                            
015700                                                                          
016200     OPEN INPUT  STORE-EXTRACT-FILE.                                      
016400     OPEN OUTPUT STORE-OUTPUT-FILE.                                       
016500                                                                          
016510     PERFORM 1000-READ-INPUT-FILE.                                        
016511     MOVE PC038-LOC-NBR    TO PV-PREV-LOC-NBR.                            
016520                                                                          
016600     PERFORM 3000-SELECT-TKRPRPM.                                         
016700     PERFORM 4000-SELECT-MERCH-INV-DATES.                                 
016710     PERFORM 5000-CHECK-INV-DATES.                                        
016800                                                                          
016801     DISPLAY  ' '.                                                        
016802     DISPLAY  '********************************'.                         
016803     DISPLAY  '  INFO FROM TKRPRPM PARM TABLE  '.                         
016804     DISPLAY  '********************************'.                         
016810     DISPLAY  'CURRENT DATE   = ' PV-CURR-DATE.                           
016820     DISPLAY  'NUMBER OF DAYS = ' PV-DISP-DAYS.                           
016830     DISPLAY  '********************************'.                         
016840     DISPLAY  ' '.                                                        
016841     DISPLAY  ' '.                                                        
016842     DISPLAY  '********************************'.                         
016843     DISPLAY  '    STORES TO PURGE OR KEEP     '.                         
016844     DISPLAY  '********************************'.                         
016850                                                                          
016860******************************************************************        
016870* READS RECORD FROM THE INPUT FILE                                        
016880******************************************************************        
016900 1000-READ-INPUT-FILE.                                                    
016910                                                                          
017000     READ STORE-EXTRACT-FILE                                              
017100         INTO PC038-SE-UNBALANCED-RPT-REC                                 
017200             AT END                                                       
017300               SET PS-EOF-INPUT      TO TRUE                              
017310                                                                          
017400             NOT AT END                                                   
017500               SET PS-NOT-EOF-INPUT  TO TRUE                              
017600               ADD +1 TO PV-READ-COUNT                                    
017700     END-READ.                                                            
018200                                                                          
018210******************************************************************        
018220* PROCESS INPUT RECORDS                                          *        
018230******************************************************************        
018300 2000-PROCESS.                                                            
018600                                                                          
018700     IF PC038-LOC-NBR = PV-PREV-LOC-NBR                                   
018800*     SAME STORE NUMBER                                                   
018801         PERFORM 6000-SAME-STORE                                          
019300     ELSE                                                                 
019310*     DIFFERENT STORE NUMBER                                              
019311         PERFORM 6500-DIFF-STORE                                          
019312     END-IF.                                                              
019900                                                                          
019910*     READ ANOTHER RECORD                                                 
020000     PERFORM 1000-READ-INPUT-FILE.                                        
020200                                                                          
020511                                                                          
020512******************************************************************        
020513* SETS UP THE NUMBER OF DAYS TO BE USED AND CURRENT DATE         *        
020514******************************************************************        
020515 3000-SELECT-TKRPRPM.                                                     
020516                                                                          
020517     EXEC SQL                                                             
020518         SELECT                                                           
020519             FUNC_QTY                                                     
020520         ,   CURRENT DATE                                                 
020521         INTO                                                             
020522            :PV-FUNC-QTY                                                  
020523         ,  :PV-CURR-DATE                                                 
020524         FROM TKRPRPM                                                     
020525         WHERE                                                            
020526             LOC_ID = 90                                                  
020527         AND INTFC_SYS_CDE = 'KHL'                                        
020528         AND SYS_PRC_FUNC_CDE = 'SEPIPG'                                  
020529     END-EXEC.                                                            
020530                                                                          
020531     EVALUATE TRUE                                                        
020532         WHEN SQLCODE = +0                                                
020533             MOVE PV-FUNC-QTY TO PV-DISP-DAYS                             
020534         WHEN OTHER                                                       
020536            MOVE '3000-SELECT-TKRPRPM' TO PV-ABEND-PARAGRAPH              
020537            MOVE SQLCODE TO PV-SQL-CODE                                   
020538            MOVE 'SELECT' TO PV-ABEND-OPERATION                           
020539            MOVE 'TKRPRPM' TO PV-ABEND-STRUCTURE-1                        
020540            PERFORM 9900-SQL-ERROR                                        
020546     END-EVALUATE.                                                        
020690                                                                          
020691******************************************************************        
020692* SETS UP THE BOOK DATES FOR INDIVIDUAL STORE                    *        
020693******************************************************************        
020694 4000-SELECT-MERCH-INV-DATES.                                             
020695                                                                          
020696     MOVE PC038-LOC-NBR TO DK-LOC-NBR.                                    
020697                                                                        06
020698     EXEC SQL                                                           06
020699         SELECT                                                         06
020700             MAX (INV_DTE)                                              06
020701            ,MAX (UNIT_BK_DTE)                                          06
020702            ,MAX (UNIT_BK_DTE) + :PV-FUNC-QTY  DAYS                       
020703         INTO                                                             
020704             :XLT00025-INV-DTE                                            
020705            ,:XLT00025-UNIT-BK-DTE                                        
020706            ,:PV-UNIT-BK-DTE-DAYS                                         
020707          FROM XLT_MDSE_INV_PARM                                          
020708         WHERE                                                          06
020709             LOC_NBR = :DK-LOC-NBR                                      06
020710         AND (INV_DTE < CURRENT DATE                                    06
020711          OR  INV_DTE = '9999-09-09')                                     
020712         AND SCHD_PHY_CNT_CDE = 'PI'                                      
020713         AND SCN_INV_IND      = 'Y'                                       
020714         AND ACTV_IND         = 'Y'                                       
020715         AND LOC_INV_STAT_CDE = 'IN'                                      
020716     END-EXEC.                                                          06
020717                                                                        06
020718     EVALUATE TRUE                                                      07
020719         WHEN SQLCODE = +0                                              07
020720             SET PS-FOUND-YES   TO TRUE                                 07
020721         WHEN SQLCODE = +100                                              
020722             SET PS-FOUND-NO    TO TRUE                                   
020723         WHEN SQLCODE = -305                                              
020724             SET PS-FOUND-NO    TO TRUE                                   
020725         WHEN OTHER                                                     07
020726            MOVE '4000-SELECT-MERCH-INV-DATES'                          07
020727                               TO PV-ABEND-PARAGRAPH                      
020728            MOVE SQLCODE TO PV-SQL-CODE                                 07
020729            MOVE 'SELECT' TO PV-ABEND-OPERATION                         07
020730            MOVE 'XLT_MDSE_INV_PARM'  TO PV-ABEND-STRUCTURE-1             
020731            PERFORM 9900-SQL-ERROR                                      07
020732     END-EVALUATE.                                                      07
020733                                                                          
020734******************************************************************        
020735* CHECKS BOOK DATE PLUS 14 DAYS AGAINST CURR DATE                *        
020736* AND SETS SWITCH FOR PURGE OR KEEP                              *        
020737******************************************************************        
020740 5000-CHECK-INV-DATES.                                                    
020800                                                                          
020900     SET PS-PURGE-NO    TO TRUE.                                          
020910     SET PS-KEEP-NO     TO TRUE.                                          
020920                                                                          
021000*  SET PURGE SWITCH                                                       
021100     IF PV-UNIT-BK-DTE-DAYS < PV-CURR-DATE                                
021110         SET PS-PURGE-YES     TO TRUE                                     
021300     END-IF.                                                              
021301                                                                          
021310*  SET KEEP SWITCH                                                        
021400     IF (PV-UNIT-BK-DTE-DAYS > PV-CURR-DATE)                              
021500         OR (PV-UNIT-BK-DTE-DAYS = PV-CURR-DATE)                          
021600            SET PS-KEEP-YES   TO TRUE                                     
021700     END-IF.                                                              
022000                                                                          
022001******************************************************************        
022002* SAME STORE PROCESSING                                          *        
022003******************************************************************        
022010 6000-SAME-STORE.                                                         
022020     IF PS-PURGE-YES                                                      
022030         ADD +1 TO PV-PURGE-COUNT                                         
022040         ADD +1 TO PV-TOTAL-REC-COUNT                                     
022050         ADD +1 TO PV-STORE-COUNT                                         
022060     END-IF.                                                              
022061                                                                          
022070     IF PS-KEEP-YES                                                       
022080         PERFORM 8000-BUILD-OUTPUT-FILE                                   
022090         ADD +1 TO PV-WRITE-COUNT                                         
022091         ADD +1 TO PV-TOTAL-REC-COUNT                                     
022092         ADD +1 TO PV-STORE-COUNT                                         
022093     END-IF.                                                              
022094     MOVE PC038-LOC-NBR TO PV-PREV-LOC-NBR.                               
022095                                                                          
022096******************************************************************        
022097* DIFFERENT STORE PROCESSING                                     *        
022098******************************************************************        
022099 6500-DIFF-STORE.                                                         
022100     DISPLAY 'STORE NUMBER:         ' PV-PREV-LOC-NBR.                    
022101     IF PS-FOUND-NO                                                       
022102         DISPLAY 'UNIT BOOK DATE:       ' PC-NOT-FOUND-MSG                
022103         DISPLAY 'UNIT BOOK DATE +DAYS: ' PC-NOT-FOUND-MSG                
022104     ELSE                                                                 
022105         DISPLAY 'UNIT BOOK DATE:       ' XLT00025-UNIT-BK-DTE            
022106         DISPLAY 'UNIT BOOK DATE +DAYS: '  PV-UNIT-BK-DTE-DAYS            
022107     END-IF.                                                              
022108     IF PS-PURGE-YES                                                      
022109         DISPLAY 'PURGE/KEEP:           ' PC-PURGE-MSG                    
022110     END-IF.                                                              
022111     IF PS-KEEP-YES                                                       
022112         DISPLAY 'PURGE/KEEP:           ' PC-KEEP-MSG                     
022113     END-IF.                                                              
022114     MOVE PV-STORE-COUNT TO PV-DISP-STOR-CNT.                             
022115     DISPLAY 'NUMBER OF RECORDS:  ' PV-DISP-STOR-CNT.                     
022116     DISPLAY '*********************************'.                         
022118     MOVE ZERO TO PV-STORE-COUNT.                                         
022119     PERFORM 4000-SELECT-MERCH-INV-DATES.                                 
022120     PERFORM 5000-CHECK-INV-DATES.                                        
022121     PERFORM 6000-SAME-STORE.                                             
022122                                                                          
022123******************************************************************        
022124* CREATE OUTPUT FILE                                             *        
022125******************************************************************        
022130 8000-BUILD-OUTPUT-FILE.                                                  
022200                                                                          
022300     WRITE STORE-OUTPUT-RECORD                                            
022400         FROM                                                             
022500         PC038-SE-UNBALANCED-RPT-REC                                      
022600     END-WRITE.                                                           
022900                                                                          
023000******************************************************************        
023100* CREATE END OF JOB TOTAL RECORD COUNTS                          *        
023200******************************************************************        
024100 9000-QUIT.                                                               
024112                                                                          
024113     DISPLAY 'STORE NUMBER:         ' PV-PREV-LOC-NBR.                    
024114     IF PS-FOUND-NO                                                       
024115         DISPLAY 'UNIT BOOK DATE:       ' PC-NOT-FOUND-MSG                
024116         DISPLAY 'UNIT BOOK DATE +DAYS: ' PC-NOT-FOUND-MSG                
024117     ELSE                                                                 
024118         DISPLAY 'UNIT BOOK DATE:       ' XLT00025-UNIT-BK-DTE            
024119         DISPLAY 'UNIT BOOK DATE +DAYS: ' PV-UNIT-BK-DTE-DAYS             
024120     END-IF.                                                              
024121     IF PS-PURGE-YES                                                      
024122         DISPLAY 'PURGE/KEEP:           ' PC-PURGE-MSG                    
024123     END-IF.                                                              
024124     IF PS-KEEP-YES                                                       
024125         DISPLAY 'PURGE/KEEP:           ' PC-KEEP-MSG                     
024126     END-IF.                                                              
024127     MOVE PV-STORE-COUNT TO PV-DISP-STOR-CNT.                             
024128     DISPLAY 'NUMBER OF RECORDS:  ' PV-DISP-STOR-CNT.                     
024129     DISPLAY '********************************'.                          
024130     DISPLAY ' '.                                                         
024131     IF PV-STORE-COUNT = ZERO                                             
024140         DISPLAY '*************************************'                  
024300         DISPLAY '**        EMPTY INPUT FILE  **       '                  
024310         DISPLAY '*************************************'                  
024320     END-IF.                                                              
024400* PRINT FINIAL RECORD COUNTS                                              
024500                                                                          
024700     MOVE PV-READ-COUNT               TO PV-DISP-NBR                      
024800     DISPLAY 'TOTAL RECORDS READ     = ' PV-DISP-NBR                      
024900                                                                          
025000     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
025100     DISPLAY 'TOTAL RECORDS WRITTEN  = ' PV-DISP-NBR.                     
025200                                                                          
025300     MOVE PV-PURGE-COUNT              TO PV-DISP-NBR                      
025400     DISPLAY 'TOTAL RECORDS PURGED   = ' PV-DISP-NBR.                     
025401                                                                          
025410     MOVE PV-TOTAL-REC-COUNT          TO PV-DISP-NBR                      
025420     DISPLAY 'TOTAL RECORDS ON FILE  = ' PV-DISP-NBR.                     
025500                                                                          
025600     CLOSE STORE-EXTRACT-FILE                                             
025610           STORE-OUTPUT-FILE.                                             
025620                                                                          
029000******************************************************************        
029100* THIS MODULE PERFORMS THE SQL ERROR.                            *        
029200******************************************************************        
029300                                                                          
029400 9900-SQL-ERROR.                                                          
029500                                                                          
029600     MOVE SQLCODE               TO SQLCODE2.                              
029800     DISPLAY '** ABEND **       ** = SQLERROR'.                           
029900     DISPLAY '** PROGRAM        ** = PCKUT080'.                           
030000     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
030100     DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION.                
030200     DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1.              
030210     DISPLAY '** STORE NBR      ** = ' DK-LOC-NBR.                        
030300     DISPLAY SPACES                                                       
030400                                                                          
030500     COPY DPPD004.                                                        
030600     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
030700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
