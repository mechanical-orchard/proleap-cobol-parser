      ******************************************************************        
      *   MLPD500 - DETERMINE THE PARTITION NUMBER FOR EACH RECORD              
      ******************************************************************        
                                                                                
      ******************************************************************        
      * ML500-0100-LOAD-PARTN-TABLE.                                            
      ******************************************************************        
       ML500-0100-LOAD-PARTN-TABLE.                                             
                                                                                
      *** LOAD PARTITION TABLE                                                  
           INITIALIZE      ML500-WS-PARTN-TBL.                                  
           MOVE ZEROS      TO ML500-PV-PARTN-NDX.                               
           MOVE 'N'        TO ML500-PS-MLT-PARTN-DONE-SW.                       
           MOVE SPACES TO ML500-PV-PARAGRAPH-NAME.                              
           MOVE SPACES TO ML500-PV-OPERATION-ATTEMPTED.                         
                                                                                
           PERFORM ML500-0200-OPEN-MLT-PARTN-CSR                                
           IF ML500-PV-OPERATION-ATTEMPTED = SPACES                             
               PERFORM ML500-0300-FETCH-MLT-PARTN-CSR                           
                   UNTIL ML500-PARTN-PROCESS-COMPLETE OR                        
                         ML500-PV-PARTN-NDX GREATER THAN ML500-PC-MAX           
               IF ML500-PV-OPERATION-ATTEMPTED = SPACES                         
                   PERFORM ML500-0400-CLOSE-MLT-PARTN-CSR                       
               END-IF                                                           
           END-IF.                                                              
           DISPLAY ' '.                                                         
           DISPLAY '* '                                                         
                   ML500-PC-TOTAL                                               
                   ' PARTITION CONTROL ROWS LOADED *'.                          
                                                                                
      ******************************************************************        
      * ML500-0200-OPEN-MLT-PARTN-CSR.                                          
      ******************************************************************        
       ML500-0200-OPEN-MLT-PARTN-CSR.                                           
                                                                                
           EXEC SQL                                                             
               OPEN MLT_PARTN_CURSOR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE 'ML500-0200-OPEN-MLT-PARTN-CSR'                             
                                       TO ML500-PV-PARAGRAPH-NAME               
               MOVE ML500-PE-MSG-01    TO ML500-PV-OPERATION-ATTEMPTED          
               MOVE 'Y'                TO ML500-PS-MLT-PARTN-DONE-SW            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * ML500-0300-FETCH-MLT-PARTN-CSR.                                         
      ******************************************************************        
       ML500-0300-FETCH-MLT-PARTN-CSR.                                          
                                                                                
           EXEC SQL                                                             
               FETCH MLT_PARTN_CURSOR                                           
               INTO :MLT00003-TBL-TM-LVL-CDE                                    
                   ,:MLT00003-FSCL-DTE                                          
                   ,:MLT00002-DEPT-NBR                                          
                   ,:MLT00003-PARTN-NBR                                         
                   ,:MLT00003-DEPT-GP-CDE                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               ADD 1 TO ML500-PV-PARTN-NDX                                      
               MOVE MLT00003-TBL-TM-LVL-CDE                                     
                            TO ML500-WS-TM-LVL-CDE(ML500-PV-PARTN-NDX)          
               MOVE MLT00003-FSCL-DTE                                           
                            TO ML500-WS-FSCL-DTE(ML500-PV-PARTN-NDX)            
               MOVE MLT00002-DEPT-NBR                                           
                            TO ML500-WS-DEPT-NBR(ML500-PV-PARTN-NDX)            
               MOVE MLT00003-PARTN-NBR                                          
                            TO ML500-WS-PARTN-NBR(ML500-PV-PARTN-NDX)           
               MOVE MLT00003-DEPT-GP-CDE                                        
                            TO ML500-WS-DEPT-GP-CDE(ML500-PV-PARTN-NDX)         
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE 'Y' TO ML500-PS-MLT-PARTN-DONE-SW                           
               MOVE ML500-PV-PARTN-NDX TO ML500-PC-TOTAL                        
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE 'ML500-0300-FETCH-MLT-PARTN-CSR'                            
                                        TO ML500-PV-PARAGRAPH-NAME              
               MOVE ML500-PE-MSG-02     TO ML500-PV-OPERATION-ATTEMPTED         
               MOVE 'Y'        TO ML500-PS-MLT-PARTN-DONE-SW                    
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * ML500-0400-CLOSE-MLT-PARTN-CSR.                                         
      ******************************************************************        
       ML500-0400-CLOSE-MLT-PARTN-CSR.                                          
                                                                                
           EXEC SQL                                                             
               CLOSE MLT_PARTN_CURSOR                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE 'ML500-0400-CLOSE-MLT-PARTN-CSR'                             
                                         TO ML500-PV-PARAGRAPH-NAME             
              MOVE ML500-PE-MSG-03       TO ML500-PV-OPERATION-ATTEMPTED        
              MOVE 'Y'                   TO ML500-PS-MLT-PARTN-DONE-SW          
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * ML500-0500-FIND-PARTITION.                                              
      ******************************************************************        
       ML500-0500-FIND-PARTITION.                                               
                                                                                
           MOVE SPACES TO ML500-PV-PARAGRAPH-NAME.                              
           MOVE SPACES TO ML500-PV-OPERATION-ATTEMPTED.                         
                                                                                
           MOVE 'N' TO ML500-PS-PARTN-FOUND-SW.                                 
           MOVE ZEROES TO ML500-PV-PARTN-NDX.                                   
           MOVE ZEROES TO ML500-PV-PARTN-NBR.                                   
           MOVE SPACES TO ML500-PV-DEPT-GP-CDE.                                 
           PERFORM ML500-0600-SELECT-PARTITION                                  
               VARYING ML500-PV-PARTN-NDX FROM 1 BY 1                           
               UNTIL ML500-PV-PARTN-NDX > ML500-PC-TOTAL                        
                   OR ML500-PARTN-FOUND.                                        
                                                                                
           IF ML500-PARTN-FOUND                                                 
               MOVE ML500-PV-PARTN-NBR TO ML500-PV-PARTN-NBR-OUT                
               MOVE ML500-PV-DEPT-GP-CDE TO ML500-PV-DEPT-GP-CDE-OUT            
           ELSE                                                                 
               PERFORM ML500-0700-DETERMINE-PARTITION                           
               IF ML500-PARTN-FOUND                                             
                   MOVE ML500-PV-PARTN-NBR TO ML500-PV-PARTN-NBR-OUT            
                   MOVE ML500-PV-DEPT-GP-CDE TO ML500-PV-DEPT-GP-CDE-OUT        
               ELSE                                                             
                   PERFORM ML500-0800-ASSIGN-PARTN                              
                   IF ML500-BAL-ROW-INSERTED                                    
                       ADD +1     TO ML500-PV-BAL-ROW-INSERTED                  
                       PERFORM ML500-0700-DETERMINE-PARTITION                   
                       IF ML500-PARTN-FOUND                                     
                           MOVE ML500-PV-PARTN-NBR                              
                                  TO ML500-PV-PARTN-NBR-OUT                     
                           MOVE ML500-PV-DEPT-GP-CDE                            
                                  TO ML500-PV-DEPT-GP-CDE-OUT                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *ML500-0600-SELECT-PARTITION.                                             
      ******************************************************************        
       ML500-0600-SELECT-PARTITION.                                             
                                                                                
           IF ML500-WS-DEPT-NBR (ML500-PV-PARTN-NDX) =                          
                                          ML500-PV-DEPT-NBR                     
              AND ML500-WS-TM-LVL-CDE (ML500-PV-PARTN-NDX) =                    
                                          ML500-PV-TM-LVL-CDE                   
              AND ML500-WS-FSCL-DTE (ML500-PV-PARTN-NDX) =                      
                                          ML500-PV-FSCL-END-DTE                 
                MOVE ML500-WS-PARTN-NBR (ML500-PV-PARTN-NDX)                    
                                   TO ML500-PV-PARTN-NBR                        
                MOVE ML500-WS-DEPT-GP-CDE (ML500-PV-PARTN-NDX)                  
                                   TO ML500-PV-DEPT-GP-CDE                      
                MOVE 'Y' TO ML500-PS-PARTN-FOUND-SW                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * ML500-0700-DETERMINE-PARTITION.                                         
      ******************************************************************        
       ML500-0700-DETERMINE-PARTITION.                                          
                                                                                
           ADD +1     TO ML500-PV-BAL-ROW-RECHECK.                              
                                                                                
           EXEC SQL                                                             
               SELECT DISTINCT A.PARTN_NBR, A.DEPT_GP_CDE                       
               INTO :MLT00003-PARTN-NBR                                         
                   ,:MLT00003-DEPT-GP-CDE                                       
                   FROM MLT_PARTN_CNTL A                                        
                       ,MLT_PARTN_BAL  B                                        
                   WHERE A.TBL_TM_LVL_CDE = B.TBL_TM_LVL_CDE                    
                     AND B.TBL_TM_LVL_CDE = :ML500-PV-TM-LVL-CDE                
                     AND A.DEPT_GP_CDE = B.DEPT_GP_CDE                          
                     AND B.DEPT_NBR = :ML500-PV-DEPT-NBR                        
                     AND A.FSCL_DTE = :ML500-PV-FSCL-END-DTE                    
                     AND :ML500-PV-FSCL-END-DTE BETWEEN                         
                         B.EFF_BEG_DTE AND B.EFF_END_DTE                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               MOVE MLT00003-PARTN-NBR TO ML500-PV-PARTN-NBR                    
               MOVE MLT00003-DEPT-GP-CDE TO ML500-PV-DEPT-GP-CDE                
               ADD 1 TO ML500-PC-TOTAL                                          
               MOVE ML500-PV-TM-LVL-CDE                                         
                                 TO ML500-WS-TM-LVL-CDE(ML500-PC-TOTAL)         
               MOVE ML500-PV-FSCL-END-DTE                                       
                                 TO ML500-WS-FSCL-DTE(ML500-PC-TOTAL)           
               MOVE ML500-PV-DEPT-NBR                                           
                                 TO ML500-WS-DEPT-NBR(ML500-PC-TOTAL)           
               MOVE ML500-PV-PARTN-NBR                                          
                                 TO ML500-WS-PARTN-NBR(ML500-PC-TOTAL)          
               MOVE ML500-PV-DEPT-GP-CDE                                        
                                 TO ML500-WS-DEPT-GP-CDE(ML500-PC-TOTAL)        
               MOVE 'Y' TO ML500-PS-PARTN-FOUND-SW                              
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE 'N' TO ML500-PS-PARTN-FOUND-SW                              
              CONTINUE                                                          
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * ML500-0800-ASSIGN-PARTN.                                                
      ******************************************************************        
       ML500-0800-ASSIGN-PARTN.                                                 
                                                                                
           IF ML500-PV-TM-LVL-CDE = 'W'                                         
               MOVE '2' TO ML500-PV-DEPT-GP-CDE                                 
           ELSE IF ML500-PV-TM-LVL-CDE = 'M'                                    
               MOVE '8' TO ML500-PV-DEPT-GP-CDE                                 
           END-IF.                                                              
                                                                                
           PERFORM ML500-0900-GET-END-DATE.                                     
                                                                                
           IF ML500-PV-FUTURE-END-DTE = SPACES                                  
               MOVE 'N'   TO ML500-PS-BAL-ROW-INSERTED-SW                       
               MOVE 'N'   TO ML500-PS-PARTN-FOUND-SW                            
               MOVE 'ML500-0800-ASSIGN-PARTN'                                   
                   TO ML500-PV-PARAGRAPH-NAME                                   
               MOVE ML500-PE-MSG-06                                             
                   TO ML500-PV-OPERATION-ATTEMPTED                              
           ELSE                                                                 
               PERFORM ML500-0950-INSERT-PARTN                                  
           END-IF.                                                              
                                                                                
      *                                                                         
      ******************************************************************        
      * ML500-0900-GET-END-DATE.                                                
      ******************************************************************        
       ML500-0900-GET-END-DATE.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       MIN(EFF_BEG_DTE) - 1 DAY                                 
                 INTO                                                           
                       :ML500-PV-FUTURE-END-DTE                                 
                 FROM                                                           
                       MLT_PARTN_BAL                                            
                 WHERE                                                          
                       TBL_TM_LVL_CDE     = :ML500-PV-TM-LVL-CDE                
                   AND DEPT_NBR           = :ML500-PV-DEPT-NBR                  
                   AND EFF_BEG_DTE        > :ML500-PV-FSCL-END-DTE              
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
26006          WHEN -305                                                        
                   MOVE '9999-12-31'    TO ML500-PV-FUTURE-END-DTE              
               WHEN OTHER                                                       
                   MOVE SPACES          TO ML500-PV-FUTURE-END-DTE              
                   MOVE 'N'   TO ML500-PS-BAL-ROW-INSERTED-SW                   
                   MOVE 'N'   TO ML500-PS-PARTN-FOUND-SW                        
                   MOVE 'ML500-0900-GET-END-DATE'                               
                       TO ML500-PV-PARAGRAPH-NAME                               
                   MOVE ML500-PE-MSG-06                                         
                       TO ML500-PV-OPERATION-ATTEMPTED                          
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * ML500-0950-INSERT-PARTN.                                                
      ******************************************************************        
       ML500-0950-INSERT-PARTN.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING ML500-PV-SQL-SUB                                         
               FROM 1 BY 1                                                      
               UNTIL (ML500-PV-SQL-SUB > ML500-PC-MAX-RETRIES                   
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                    INSERT INTO MLT_PARTN_BAL                                   
                           (TBL_TM_LVL_CDE                                      
                           ,DEPT_NBR                                            
                           ,EFF_BEG_DTE                                         
                           ,EFF_END_DTE                                         
                           ,DEPT_GP_CDE)                                        
                    VALUES (:ML500-PV-TM-LVL-CDE                                
                           ,:ML500-PV-DEPT-NBR                                  
                           ,:ML500-PV-FSCL-END-DTE                              
                           ,:ML500-PV-FUTURE-END-DTE                            
                           ,:ML500-PV-DEPT-GP-CDE)                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -803                                          
                       MOVE 'ML500-0950-INSERT-PARTN'                           
                               TO ML500-PV-PARAGRAPH-NAME                       
                       MOVE ML500-PE-MSG-04                                     
                               TO ML500-PV-OPERATION-ATTEMPTED                  
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = 0                                             
                       MOVE 'Y' TO ML500-PS-BAL-ROW-INSERTED-SW                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE NOT = ZEROES                                    
                       MOVE 'N'       TO ML500-PS-BAL-ROW-INSERTED-SW           
                       MOVE 'N'       TO ML500-PS-PARTN-FOUND-SW                
                       MOVE 'ML500-0950-INSERT-PARTN'                           
                                      TO ML500-PV-PARAGRAPH-NAME                
                       MOVE ML500-PE-MSG-04                                     
                                      TO ML500-PV-OPERATION-ATTEMPTED           
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF ML500-PV-SQL-SUB > ML500-PC-MAX-RETRIES                           
               MOVE 'ML500-0950-INSERT-PARTN'                                   
                   TO ML500-PV-PARAGRAPH-NAME                                   
               MOVE ML500-PE-MSG-04 TO ML500-PV-OPERATION-ATTEMPTED             
           END-IF.                                                              
                                                                                
