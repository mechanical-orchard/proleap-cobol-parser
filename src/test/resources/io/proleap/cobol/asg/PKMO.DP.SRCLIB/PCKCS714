000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'TRAILER CONTENTS'.                                                
000300 PROGRAM-ID.    PCKCS714.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000600 DATE-WRITTEN.  MAY, 2001.                                                
000700 DATE-COMPILED.                                                           
000800******************************************************************        
000900*  THIS PROGRAM WILL RETURN A COUNT OF THE CARTONS ON A GIVEN             
001000*  TRAILER                                                                
001100******************************************************************        
001200 ENVIRONMENT DIVISION.                                                    
001300 CONFIGURATION SECTION.                                                   
001400 SOURCE-COMPUTER. IBM-3090.                                               
001500 OBJECT-COMPUTER. IBM-3090.                                               
001600                                                                          
001700 DATA DIVISION.                                                           
001800 WORKING-STORAGE SECTION.                                                 
001900                                                                          
002000                                                                          
002100 01  PROGRAM-VARIABLES.                                                   
002200     05  PV-CICS-MSG-AREA             PIC  X(80).                         
002300     05  PV-DISPLAY-TRIP              PIC  9(09) VALUE ZERO.              
002400     05  DK-OTBND-TRIP-ID             PIC S9(09) COMP.                    
002500     05  PV-USERID                    PIC  X(08) VALUE SPACES.            
002600     05  PV-CICS-RESP                 PIC S9(09) COMP                     
002700                                                 VALUE ZERO.              
002800                                                                          
002900 01  PV-LOG-MSG.                                                          
003000     05  FILLER                       PIC X(10) VALUE                     
003100         ' PCKCS714-'.                                                    
003200     05  PV-LOG-PARA                  PIC X(05) VALUE SPACES.             
003300     05  FILLER                       PIC X(05) VALUE                     
003400         '-BY: '.                                                         
003500     05  PV-LOG-CALLING-PGM           PIC X(08) VALUE SPACES.             
003600     05  FILLER                       PIC X(10) VALUE                     
003700         ' SQLCODE= '.                                                    
003800     05  PV-LOG-SQLCODE               PIC -999.                           
003900     05  FILLER                       PIC X(06) VALUE                     
004000         ' TASK='.                                                        
004100     05  PV-LOG-TASK                  PIC ZZZ9.                           
004200     05  FILLER                       PIC X(06) VALUE                     
004300         ' TRIP='.                                                        
004400     05  PV-LOG-TRIP                  PIC 9(09) VALUE ZERO.               
004500     05  FILLER                       PIC X(01) VALUE SPACES.             
004600     05  PV-LOG-TEXT                  PIC X(20) VALUE SPACES.             
004700                                                                          
004800     COPY PCWS047.                                                        
004900                                                                          
005000************************************************************************  
005100* PCWS049 - WORK FIELDS USED TO DETERMINE TRAILER CONTENTS             *  
005200************************************************************************  
005300     COPY PCWS049.                                                        
005400                                                                          
005500************************************************************************  
005600* SUT_OTBND_LOC - SHIPPING UNIT LOCATION TABLE                         *  
005700************************************************************************  
005800     EXEC SQL                                                             
005900          INCLUDE SUT00027                                                
006000     END-EXEC.                                                            
006100                                                                          
006200************************************************************************  
006300* TSUCDOT - CARTON DIVERT/OUTBOUND TRIP ASSOCIATION TABLE              *  
006400************************************************************************  
006500     EXEC SQL                                                             
006600          INCLUDE TSUCDOT                                                 
006700     END-EXEC.                                                            
006800                                                                          
006900************************************************************************  
007000* TSUOBTR - OUTBOUND TRAILER TRIP TABLE                                *  
007100************************************************************************  
007200     EXEC SQL                                                             
007300          INCLUDE TSUOBTR                                                 
007400     END-EXEC.                                                            
007500                                                                          
007600************************************************************************  
007700* TSUOBTR - OUTBOUND TRAILER TRIP STATUS TABLE                         *  
007800************************************************************************  
007900     EXEC SQL                                                             
008000          INCLUDE TSUOBTS                                                 
008100     END-EXEC.                                                            
008200                                                                          
008300************************************************************************  
008400* TSUOBSC - OUTBOUND TRAILER TRIP STATUS CODE TABLE                    *  
008500************************************************************************  
008600     EXEC SQL                                                             
008700          INCLUDE TSUOBSC                                                 
008800     END-EXEC.                                                            
008900                                                                          
009000************************************************************************  
009100* TSUCRDV - CARTON DIVERTS FROM CONVEYOR SYSTEM TABLE                  *  
009200************************************************************************  
009300     EXEC SQL                                                             
009400          INCLUDE TSUCRDV                                                 
009500     END-EXEC.                                                            
009600                                                                          
009700************************************************************************  
009800* TCNDCDR - DISTRIBUTION CENTER DOOR TABLE                             *  
009900************************************************************************  
010000     EXEC SQL                                                             
010100          INCLUDE TCNDCDR                                                 
010200     END-EXEC.                                                            
010300                                                                          
010400************************************************************************  
010500* TCNDRST - DISTRIBUTION CENTER DOOR TO STORE ASSOCIATION TABLE        *  
010600************************************************************************  
010700     EXEC SQL                                                             
010800          INCLUDE TCNDRST                                                 
010900     END-EXEC.                                                            
011000                                                                          
011070                                                                          
011080************************************************************************  
011090* SUT_OTBND_UPC                                                        *  
011091************************************************************************  
011092     EXEC SQL                                                             
011093          INCLUDE SUT00028                                                
011094     END-EXEC.                                                            
011095                                                                          
011096************************************************************************  
011097* SUT_OTBND_CART                                                       *  
011098************************************************************************  
011099     EXEC SQL                                                             
011100          INCLUDE SUT00024                                                
011101     END-EXEC.                                                            
011102                                                                          
011110************************************************************************  
011200* PCWS050 - CURSOR USED TO DETERMINE TRAILER CONTENTS                  *  
011300************************************************************************  
011400     COPY PCWS050.                                                        
011500                                                                          
011600                                                                          
011700     EXEC SQL                                                             
011800          INCLUDE SQLCA                                                   
011900     END-EXEC.                                                            
012000                                                                          
012100                                                                          
012200 LINKAGE SECTION.                                                         
012300 01  DFHCOMMAREA.                                                         
012400       05  FILLER                         OCCURS  1 TO 4072 TIMES         
012500                                          DEPENDING ON EIBCALEN.          
012600           10  FILLER                 PIC X.                              
012700/                                                                         
012800 PROCEDURE DIVISION.                                                      
012900                                                                          
013000 0000-PROGRAM-MAINLINE.                                                   
013100                                                                          
013200     PERFORM 1000-INITIALIZE.                                             
013300                                                                          
013400     EVALUATE TRUE                                                        
013500         WHEN PC047-SCAN-TOTAL                                            
013600             PERFORM 2000-SCAN-TOTAL                                      
013700             MOVE PC047-SCANNED-CTN-COUNT                                 
013800                                      TO PC047-TOTAL-CTN-COUNT            
013801*                                                                         
013802             MOVE PC047-OPER-UNT-ID TO  PC049-DK-OPER-UNT-ID              
013803             MOVE PC047-DOOR-ID     TO  PC049-DK-DOOR-ID                  
013804             PERFORM P3020-ALL-DOOR-STR-CSR                               
013810             PERFORM 3100-MISDIRECT-COUNT                                 
013820             PERFORM 3200-NO-DATA-COUNT                                   
013830                                                                          
013900         WHEN PC047-DIVERT-TOTAL                                          
014000             PERFORM 3000-DIVERT-TOTAL                                    
014100             MOVE PC047-DIVERTED-CTN-COUNT                                
014200                                      TO PC047-TOTAL-CTN-COUNT            
014201*                                                                         
014202             MOVE PC047-OPER-UNT-ID TO  PC049-DK-OPER-UNT-ID              
014203             MOVE PC047-DOOR-ID     TO  PC049-DK-DOOR-ID                  
014204             PERFORM P3020-ALL-DOOR-STR-CSR                               
014210             PERFORM 3100-MISDIRECT-COUNT                                 
014220             PERFORM 3200-NO-DATA-COUNT                                   
014300                                                                          
014400         WHEN OTHER                                                       
014500             PERFORM 2000-SCAN-TOTAL                                      
014600             PERFORM 3000-DIVERT-TOTAL                                    
014700             ADD PC047-DIVERTED-CTN-COUNT                                 
014800                 PC047-SCANNED-CTN-COUNT                                  
014900                 GIVING PC047-TOTAL-CTN-COUNT                             
014901*                                                                         
014902             MOVE PC047-OPER-UNT-ID TO  PC049-DK-OPER-UNT-ID              
014903             MOVE PC047-DOOR-ID     TO  PC049-DK-DOOR-ID                  
014904             PERFORM P3020-ALL-DOOR-STR-CSR                               
014910             PERFORM 3100-MISDIRECT-COUNT                                 
014920             PERFORM 3200-NO-DATA-COUNT                                   
014930                                                                          
015000     END-EVALUATE.                                                        
015100                                                                          
015200                                                                          
015300     PERFORM 9000-FINIS.                                                  
015400                                                                          
015500**********************************************************************    
015600*                                                                    *    
015700**********************************************************************    
015800 1000-INITIALIZE.                                                         
015900                                                                          
016000     EXEC CICS                                                            
016100         ASSIGN USERID(PV-USERID)                                         
016200     END-EXEC.                                                            
016300                                                                          
016400     MOVE DFHCOMMAREA                 TO PC047-COMMAREA                   
016500     MOVE PC047-OTBND-TRIP-ID         TO PC049-DK-OTBND-TRIP-ID           
016600                                         DK-OTBND-TRIP-ID                 
016700     SET PC047-SUCCESSFUL             TO TRUE.                            
016800                                                                          
016900**********************************************************************    
017000*                                                                    *    
017100**********************************************************************    
017200 2000-SCAN-TOTAL.                                                         
017300     INITIALIZE PC047-SCANNED-CTN-COUNT                                   
017400                PC047-TOTAL-CTN-COUNT                                     
017500     PERFORM P4000-CNT-SCAN-CRTN.                                         
017600                                                                          
017700     IF  PC049-ERROR                                                      
017800         MOVE  PC049-DK-OTBND-TRIP-ID  TO  PV-DISPLAY-TRIP                
017900         INITIALIZE                        PV-CICS-MSG-AREA               
018000         STRING  ' PCKCS714 - '            DELIMITED BY SIZE              
018100                 ' TRIP='                  DELIMITED BY SIZE              
018200                 PV-DISPLAY-TRIP           DELIMITED BY SIZE              
018300                '  P4000-CTN-SCAN-CRTN '   DELIMITED BY SIZE              
018400                '  SQLCODE-'               DELIMITED BY SIZE              
018500                PC049-DISPLAY-SQLCODE      DELIMITED BY SIZE              
018600                                      INTO PV-CICS-MSG-AREA               
018700         PERFORM 9997-CSSL                                                
018800                                                                          
018900         MOVE SQLCODE                   TO PC047-SQLCODE                  
019000         MOVE SQLCA                     TO PC047-SQLCA                    
019100         MOVE 'P4000-CTN-SCAN-CRTN'     TO PC047-PARAGRAPH                
019200         MOVE 'SUT00027'                TO PC047-DB2-TABLE-NAME(1)        
019300         SET PC047-SQL-ERROR            TO TRUE                           
019400         PERFORM 9999-WRITE-TO-CSSL                                       
019500     ELSE                                                                 
019600         MOVE  PC049-PV-SCAN-COUNT      TO PC047-SCANNED-CTN-COUNT        
019700     END-IF.                                                              
019800                                                                          
019900                                                                          
020000**********************************************************************    
020100*                                                                    *    
020200**********************************************************************    
020300 3000-DIVERT-TOTAL.                                                       
020400     INITIALIZE PC047-DIVERTED-CTN-COUNT                                  
020500                PC047-TOTAL-CTN-COUNT                                     
020600                                                                          
020700     PERFORM 7110-GET-DOOR-FOR-TRIP.                                      
020800                                                                          
020900     IF  PC047-SHIPPING                                                   
021000         MOVE  SUOBTS-STAT-OPER-UNT-ID  TO PC049-DK-OPER-UNT-ID           
021100         MOVE  SUOBTS-DOOR-ID           TO PC049-DK-DOOR-ID               
021200         MOVE  PC047-DEST-OPER-UNT-ID   TO PC049-DK-TRLR-DEST             
021300         PERFORM P3000-DOOR-STR-CSR                                       
021400     END-IF                                                               
021500                                                                          
021600     IF  PC049-NO-ERROR                                                   
021700         PERFORM P4000-STRT-END-DVRT                                      
021800     END-IF.                                                              
021810                                                                          
021811*DMH BEGIN - 8/25/01                                                      
021820     IF  SUOBTS-TRIP-STAT-CDE  = 'DO'                                     
021830         MOVE  PC049-PC-DEFAULT-TMST                                      
021840           TO  PC049-DK-END-TMST                                          
021850     END-IF.                                                              
021860*DMH END - 8/25/01                                                        
021900                                                                          
022000     IF  PC049-ERROR                                                      
022100         MOVE  PC049-DK-OTBND-TRIP-ID  TO  PV-DISPLAY-TRIP                
022200         INITIALIZE                        PV-CICS-MSG-AREA               
022300         STRING  ' PCKCS714 - '            DELIMITED BY SIZE              
022400                 ' TRIP='                  DELIMITED BY SIZE              
022500                 PV-DISPLAY-TRIP           DELIMITED BY SIZE              
022600                '  P4000-STRT-END-DVRT '   DELIMITED BY SIZE              
022700                '  SQLCODE-'               DELIMITED BY SIZE              
022800                PC049-DISPLAY-SQLCODE      DELIMITED BY SIZE              
022900                                      INTO PV-CICS-MSG-AREA               
023000         PERFORM 9997-CSSL                                                
023100                                                                          
023200         MOVE SQLCODE                   TO PC047-SQLCODE                  
023300         MOVE SQLCA                     TO PC047-SQLCA                    
023400         MOVE 'P4000-CTN-SCAN-CRTN'     TO PC047-PARAGRAPH                
023500         MOVE 'SUT00027'                TO PC047-DB2-TABLE-NAME(1)        
023600         SET PC047-SQL-ERROR            TO TRUE                           
023700         PERFORM 9999-WRITE-TO-CSSL                                       
023800     ELSE                                                                 
023810         IF PC049-DIVERT-CUTOFFS-YES                                      
023900             MOVE SUOBTS-STAT-OPER-UNT-ID TO PC049-DK-OPER-UNT-ID         
024000             MOVE PC047-OTBND-TRIP-ID   TO PC049-DK-OTBND-TRIP-ID         
024100             IF PC047-SHIPPING                                            
024200                 PERFORM P4110-CNT-DVRT-CRTN-SHIP                         
024300             ELSE                                                         
024400                 PERFORM P4100-CNT-DVRT-CRTN-SHTL                         
024500             END-IF                                                       
024600             MOVE PC049-PV-DVRT-COUNT TO PC047-DIVERTED-CTN-COUNT         
024610         ELSE                                                             
024630             MOVE ZERO                TO PC049-PV-DVRT-COUNT              
024640                                         PC047-DIVERTED-CTN-COUNT         
024700         END-IF                                                           
024710     END-IF.                                                              
024800                                                                          
024900                                                                          
024901**********************************************************************    
024902* COUNT THE NUMBER OF CARTONS MISDIRECT BY CONVEYOR                  *    
024903**********************************************************************    
024910 3100-MISDIRECT-COUNT.                                                    
024912                                                                          
024918     IF  PC047-SHIPPING                                                   
024919         PERFORM P4140-CNT-MISD-CRTN-SHIP                                 
024920         MOVE  PC049-PV-DVRT-COUNT  TO  PC047-MISDIRECT-COUNT             
024921     ELSE                                                                 
024922         MOVE  ZEROES               TO  PC047-MISDIRECT-COUNT             
024923     END-IF.                                                              
024924                                                                          
024925                                                                          
024926**********************************************************************    
024927* COUNT THE NUMBER OF CARTONS THAT WE KNOW NOTHING ABOUT             *    
024928*  MISSING SHUUPC INFORMATION FOR CARTON                             *    
024929**********************************************************************    
024930 3200-NO-DATA-COUNT.                                                      
024931                                                                          
024936     IF  PC047-SHIPPING                                                   
024937         PERFORM P4120-CNT-DVRT-NO-DATA-SHIP                              
024938         MOVE  PC049-PV-NO-DATA-CNT TO  PC047-NO-DATA-COUNT               
024939     ELSE                                                                 
024940         PERFORM P4130-CNT-DVRT-NO-DATA-SHTL                              
024941         MOVE  PC049-PV-NO-DATA-CNT TO  PC047-NO-DATA-COUNT               
024942     END-IF.                                                              
024943                                                                          
024950                                                                          
025000**********************************************************************    
025100*                                                                    *    
025200**********************************************************************    
025300 7110-GET-DOOR-FOR-TRIP.                                                  
025400     EXEC SQL                                                             
025500         SELECT DOOR_ID                                                   
025600         ,   STAT_OPER_UNT_ID                                             
025610         ,   TRIP_STAT_CDE                                                
025700     INTO                                                                 
025800             :SUOBTS-DOOR-ID                                              
025900         ,   :SUOBTS-STAT-OPER-UNT-ID                                     
025910         ,   :SUOBTS-TRIP-STAT-CDE                                        
026000     FROM TSUOBTS OBTS                                                    
026100     WHERE OBTS.OTBND_TRIP_ID = :DK-OTBND-TRIP-ID                         
026200     AND OBTS.OTBND_STAT_TMST =                                           
026300     (SELECT                                                              
026400         MAX(OBTS1.OTBND_STAT_TMST)                                       
026500         FROM TSUOBTS OBTS1                                               
026600         WHERE                                                            
026700           OBTS1.OTBND_TRIP_ID = :DK-OTBND-TRIP-ID)                       
026800     END-EXEC.                                                            
026900                                                                          
027000     EVALUATE TRUE                                                        
027100         WHEN SQLCODE = ZERO                                              
027200             CONTINUE                                                     
027300         WHEN SQLCODE = -904                                              
027400         WHEN SQLCODE = -913                                              
027500         WHEN SQLCODE = -911                                              
027600             MOVE SQLCODE             TO PC047-SQLCODE                    
027700             MOVE SQLCA               TO PC047-SQLCA                      
027800             MOVE '7110-GET-DOOR-FOR-TRIP'                                
027900                                      TO PC047-PARAGRAPH                  
028000             MOVE 'TSUOBTS'           TO PC047-DB2-TABLE-NAME(1)          
028100             SET PC047-SQL-ERROR      TO TRUE                             
028200             PERFORM 9999-WRITE-TO-CSSL                                   
028300         WHEN SQLWARN0 NOT = SPACES                                       
028400         WHEN SQLCODE NOT EQUAL ZERO                                      
028500             MOVE SQLCODE             TO PC047-SQLCODE                    
028600             MOVE SQLCA               TO PC047-SQLCA                      
028700             MOVE '7110-GET-DOOR-FOR-TRIP'                                
028800                                      TO PC047-PARAGRAPH                  
028900             MOVE 'TSUOBTS'           TO PC047-DB2-TABLE-NAME(1)          
029000             SET PC047-SQL-ERROR      TO TRUE                             
029100             PERFORM 9999-WRITE-TO-CSSL                                   
029200     END-EVALUATE.                                                        
029300                                                                          
029400                                                                          
029500 9000-FINIS.                                                              
029600     PERFORM 9998-RETURN.                                                 
029700                                                                          
029800                                                                          
029900**********************************************************************    
030000* DISPLAY MESSAGE IN CICS MESSAGE LOG                                *    
030100**********************************************************************    
030200 9997-CSSL.                                                               
030300                                                                          
030400                                                                          
030500     EXEC CICS                                                            
030600          WRITEQ TD                                                       
030700           QUEUE   ('CSSL')                                               
030800            FROM   (PV-CICS-MSG-AREA)                                     
030900          LENGTH   (80)                                                   
031000            RESP   (PV-CICS-RESP)                                         
031100     END-EXEC.                                                            
031200                                                                          
031300                                                                          
031400**********************************************************************    
031500*                                                                    *    
031600**********************************************************************    
031700 9998-RETURN.                                                             
031800                                                                          
031900*    MOVE 'ENDED'                     TO PV-LOG-PARA                      
032000*                                                                         
032100*    PERFORM 9999-WRITE-TO-CSSL                                           
032200     MOVE PC047-COMMAREA                TO DFHCOMMAREA.                   
032300                                                                          
032400     EXEC CICS                                                            
032500          RETURN                                                          
032600     END-EXEC.                                                            
032700                                                                          
032800                                                                          
032900**********************************************************************    
033000*                                                                    *    
033100**********************************************************************    
033200 9999-WRITE-TO-CSSL.                                                      
033300                                                                          
033400     MOVE EIBTASKN                    TO PV-LOG-TASK                      
033500     MOVE PC047-CALLING-PROGRAM       TO PV-LOG-CALLING-PGM               
033600     MOVE PC047-OTBND-TRIP-ID         TO PV-LOG-TRIP                      
033700     MOVE PV-USERID                   TO PV-LOG-TEXT                      
033800     MOVE PC047-SQLCODE               TO PV-LOG-SQLCODE                   
033900     IF PV-LOG-PARA =  'ENDED'                                            
034000         CONTINUE                                                         
034100     ELSE                                                                 
034200         MOVE PC047-PARAGRAPH         TO PV-LOG-PARA                      
034300     END-IF                                                               
034400                                                                          
034500     EXEC CICS                                                            
034600          WRITEQ TD                                                       
034700           QUEUE   ('CSSL')                                               
034800            FROM   (PV-LOG-MSG)                                           
034900          LENGTH   (80)                                                   
035000            RESP   (PV-CICS-RESP)                                         
035100     END-EXEC.                                                            
035200                                                                          
035300************************************************************************  
035400* PCPD049 - PROCEDURE DIVISION USED TO DETERMINE TRAILER CONTENTS      *  
035500************************************************************************  
035600     COPY PCPD049.                                                        
035700                                                                          
