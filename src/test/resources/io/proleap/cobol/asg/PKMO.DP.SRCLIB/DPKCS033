       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKCS033.                                                 
       AUTHOR.        C. MASTEL.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-10-96.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                *        
      *    MASS "NEW COPY" MNEMONIC ARCHITECTURE INFORMATION.          *        
      *                                                                *        
      *    BASED ON DPKCS032, THIS PROGRAM PERFORMS A MASS UPDATE OF   *        
      *    VSAM FILES FROM THE DB2 TABLES FOR ALL MNEMONICS WHOSE      *        
      *    UPD-VSAM-IND IS SET TO 'Y'.                                 *        
      *    A RECORD OF SUCCESSFUL VSAM FILE UPDATES IS WRITTEN TO THE  *        
      *    CICSARCH AUDIT LOG (A VSAM ESDS FILE WITH VARIABLE LRECL).  *        
      *    THE RECORD LAYOUT OF THIS DPCAUDIT FILE IS COPYBOOK DPRD520.*        
      *                                                                *        
      *                **********************************************  *        
      *                NOTE: THIS IS A NATIVE CICS PROGRAM; IT IS A    *        
      *                NON-ARCHITECTURE PROGRAM.                       *        
      *                **********************************************  *        
      *                NOTE: PARAGRAPH NUMBERING IN PROGRAMS DPKCS032  *        
      *                AND DPKCS033 VARY SOMEWHAT FROM STANDARDS, IN   *        
      *                ORDER TO ACCOMODATE THE PROCESSING SIMILARITES  *        
      *                BETWEEN THE TWO PROGRAMS.  THIS WAS DONE WITH   *        
      *                EASE OF FUTURE MAINTAINABILITY IN MIND.         *        
      *                ANY FUTURE MODIFICATIONS MADE TO THE "UPDATE"   *        
      *                PART OF 1100-PROCESS-REQUEST LOGIC, IN EITHER   *        
      *                ONE OF THESE PROGRAMS, WILL MOST LIKELY BE      *        
      *                NEEDED TO THE OTHER PROGRAM AS WELL.            *        
      *                **********************************************  *        
      *                NOTE: THIS PROGRAM HANDLES ABENDS DIFFERENTLY   *        
      *                THAN DPKCS032.  THIS PROGRAM CALLS THE ABEND    *        
      *                ROUTINE SPECIFYING LINK-RETURN: IT LOGS THE     *        
      *                ABEND AND KEEPS GOING.  A SWITCH IS ALSO SET:   *        
      *                PS-TRANSACTION-AWRY.  THIS SWITCH IS TESTED     *        
      *                BEFORE COMPLETING EACH TRANSACTION (IE: BEFORE  *        
      *                DELETING THE MNEMONIC -AND/OR- BEFORE UPDATING  *        
      *                THE MNEMONIC TO SET UPD_VSAM_IND TO "N"). IF    *        
      *                AN ABEND HAS OCCURRED, THE UPDATE/DELETE TO THE *        
      *                THE MNEMONIC DB2 TABLE WILL NOT TAKE PLACE;     *        
      *                THIS ALLOWS THAT TRANSACTION TO BE ATTEMPTED    *        
      *                AGAIN AT A LATER TIME.                          *        
      *                **********************************************  *        
      *                                                                *        
      ******************************************************************        
      *  09/08/98  JJR  ALLOW PARM TO BE ENTERED AT COMMAND LINE TO             
      *                 SPECIFY A THAT ONLY A PARTICULAR MNEMONIC               
      *                 SHOULD BE PROCESSED.  CORRECTED THE REBUILDING          
      *                 OF MENUITEMS AND HANDLING OF DELETES.                   
      ******************************************************************        
      *  02/25/99  JJR  CHANGED PC-DPCAUDIT-MENUITEM-LEN FROM +118              
      *                                                    TO +144.             
      *                 CHANGED PC-DPCAUDIT-MENUITEM-LEN FROM +908              
      *                                                    TO +934.             
      ******************************************************************        
      *  09/09/99  JJR  ADD ACCURATE LOCATION REQUIRED INDICATOR.               
      ******************************************************************        
224787*  05/28/19  JIM ARENDT                       CHG0224787                  
224787*            FOR THE DELOITTE REFACTOR PROJECT, REMOVED THE               
224787*            FOLLOWING 88 LEVEL VARIABLE:                                 
224787*        88  PS-NO-MNEMONIC-ERROR                    VALUE 'N'.           
224787*            THAT WAS UNDER 01 LEVEL PS-PROGRAM-SWITCHES.                 
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CHAR-MAX                    PIC S9(04)   VALUE +30            
                                                           COMP SYNC.           
           05  PC-MAX-RETRIES                 PIC 9(02)    VALUE  3.            
           05  PC-MNEMONIC-FILE               PIC X(8)     VALUE                
                                                           'DPMNEMIC'.          
           05  PC-MENUITEM-FILE               PIC X(8)     VALUE                
                                                           'DPMNUITM'.          
           05  PC-DPCAUDIT-FILE               PIC X(8)     VALUE                
                                                           'DPCAUDIT'.          
           05  PC-CURRENT-PROGRAM-NAME        PIC X(8)     VALUE                
                                                           'DPKCS033'.          
           05  PC-MENU-TRANID                 PIC X(4)     VALUE                
                                                           'Z007'.              
           05  PC-DPCAUDIT-MENUITEM-LEN       PIC S9(4)    VALUE +0144.         
           05  PC-DPCAUDIT-MNEMONIC-LEN       PIC S9(4)    VALUE +0934.         
           05  PC-VSAM-ACTION.                                                  
               10  PC-VSAM-ACTION-A-ADD       PIC X(1)     VALUE 'A'.           
               10  PC-VSAM-ACTION-U-UPDATE    PIC X(1)     VALUE 'U'.           
               10  PC-VSAM-ACTION-R-REPLACE   PIC X(1)     VALUE 'R'.           
               10  PC-VSAM-ACTION-D-DELETE    PIC X(1)     VALUE 'D'.           
           05  PC-VSAM-REC-TYPE.                                                
               10  PC-VSAM-REC-TYPE-MNEMONIC  PIC X(8)     VALUE                
                                                           'MNEMONIC'.          
               10  PC-VSAM-REC-TYPE-MENUITEM  PIC X(8)     VALUE                
                                                           'MENUITEM'.          
               10  PC-VSAM-REC-TYPE-MENUNAME  PIC X(8)     VALUE                
                                                           'MENUNAME'.          
           05  PC-UPD-VSAM-IND.                                                 
               10  PC-N-NOT-NEEDED            PIC X(1)     VALUE 'N'.           
               10  PC-Y-UPDATE-REQUESTED      PIC X(1)     VALUE 'Y'.           
               10  PC-D-DELETE-REQUESTED      PIC X(1)     VALUE 'D'.           
               10  PC-I-IMMEDIATE-UPDATE      PIC X(1)     VALUE 'I'.           
                                                                                
           05  PC-DB2-PKG-CDE.                                                  
               10  PC-P-PROD-TBLS-ONLY        PIC X(1)     VALUE 'P'.           
               10  PC-A-ARCHIVE-TBLS-IF-EXIST PIC X(1)     VALUE 'A'.           
                                                                                
           05  PC-N-NOT-AVAILABLE             PIC X(1)     VALUE 'N'.           
                                                                                
           05  PC-AT                          PIC X(04)    VALUE ' AT '.        
                                                                                
      *        *********************************************************        
      *        * INITIALIZATION TIMESTAMP DEFAULT EXISTS TO PREVENT             
      *        * SQLCODE -181 IF TIMESTAMP WERE INIT TO SPACES AND USED.        
      *        * (IE: IF NO UPDATE WITH CURRENT TIMESTAMP TO SET VALUE).        
      *        *********************************************************        
           05  PC-INIT-TMESTP-DEFAULT         PIC X(26)    VALUE                
                                           '1996-01-01-01.01.01.000001'.        
                                                                                
      *        *********************************************************        
      *        * REQUESTOR BATCHD2 = BATCH DELETE, 2ND PASS (HARD DLET)         
      *        * REQUESTOR BATCHUP = BATCH UPDATE                               
      *        *********************************************************        
           05  PC-REQUESTOR.                                                    
               10  PC-BATCHD2                 PIC X(7)     VALUE                
                                                           'BATCHD2'.           
               10  PC-BATCHUP                 PIC X(7)     VALUE                
                                                           'BATCHUP'.           
                                                                                
                                                                                
           05  PC-LOG-QUEUE-RESOURCE          PIC  X(04)   VALUE 'CSSL'.        
           05  PC-LOG-QUEUE                   PIC  X(04)   VALUE 'CSSL'.        
                                                                                
                                                                                
      ******************************************************************        
      *    LAYOUT USED TO GET THE SYSTEM DATE AND TIME                          
      ******************************************************************        
       01  DT-DATE-TIME-VARIABLES.                                              
           05  DT-ABSTIME                  PIC S9(15)  VALUE ZERO               
                                                       COMP-3.                  
           05  DT-YEAR-BIN                 PIC S9(09)  VALUE ZERO               
                                                       COMP SYNC.               
           05  DT-FORMATTED-TIME           PIC  X(08)  VALUE SPACE.             
           05  DT-FORMATTED-DATE           PIC  X(10)  VALUE SPACE.             
           05  FILLER                      REDEFINES DT-FORMATTED-DATE.         
               10  DT-FORMATTED-MMDDYY     PIC  X(08).                          
               10  FILLER                  PIC  X(02).                          
           05  FILLER                      REDEFINES DT-FORMATTED-DATE.         
               10  FILLER                  PIC  X(06).                          
               10  DT-FORMATTED-YYYY       PIC  9(04).                          
                                                                                
                                                                                
       01  PM-PROGRAM-MESSAGES.                                                 
           05  PM-PROGRAM-MESSAGE-SCOREBOARD.                                   
               10  FILLER                     PIC X(17)    VALUE                
                                              'DPKCS033 COMPLETE'.              
               10  PM-AT-OR-BLANK             PIC X(04).                        
               10  PM-LAST-TMESTP             PIC X(26)    VALUE SPACES.        
               10  PM-HEX15-LINEBREAK1        PIC X(1)     VALUE X'15'.         
               10  FILLER                     PIC X(15)    VALUE                
                                              'FINAL SCORE IS:'.                
               10  PM-HEX15-LINEBREAK2        PIC X(1)     VALUE X'15'.         
               10  PM-SOFT-SCORE.                                               
                   15  FILLER                 PIC X(17)    VALUE                
                                                  'SOFT DELETES:    '.          
                   15  PM-SOFT-SUCCESSES      PIC 9(6)     VALUE ZERO.          
                   15  FILLER                 PIC X(1)     VALUE '-'.           
                   15  PM-SOFT-AWRY           PIC 9(6)     VALUE ZERO.          
               10  PM-HEX15-LINEBREAK3        PIC X(1)     VALUE X'15'.         
               10  PM-DLET-SCORE.                                               
                   15  FILLER                 PIC X(17)    VALUE                
                                                  'AUDITED DELETES: '.          
                   15  PM-DLET-SUCCESSES      PIC 9(6)     VALUE ZERO.          
                   15  FILLER                 PIC X(1)     VALUE '-'.           
                   15  PM-DLET-AWRY           PIC 9(6)     VALUE ZERO.          
               10  PM-HEX15-LINEBREAK4        PIC X(1)     VALUE X'15'.         
               10  PM-UPDT-SCORE.                                               
                   15  FILLER                 PIC X(17)    VALUE                
                                                  'AUDITED UPDATES: '.          
                   15  PM-UPDT-SUCCESSES      PIC 9(6)     VALUE ZERO.          
                   15  FILLER                 PIC X(1)     VALUE '-'.           
                   15  PM-UPDT-AWRY           PIC 9(6)     VALUE ZERO.          
           05  PM-SCOREBOARD-LENGTH           PIC S9(4)    COMP                 
                                                           VALUE +0156.         
                                                                                
           05  PM-PARM-ERROR.                                                   
               10  FILLER                     PIC X(1)     VALUE X'15'.         
               10  FILLER                     PIC X(22)    VALUE                
                                              'DPKCS033 PARM ERROR!!!'.         
               10  FILLER                     PIC X(1)     VALUE X'15'.         
               10  FILLER                     PIC X(16)    VALUE                
                                           'USE FORMAT: Z033'.                  
               10  FILLER                     PIC X(1)     VALUE X'15'.         
               10  FILLER                     PIC X(25)    VALUE                
                                           '        OR: Z033 MNEMONIC'.         
               10  FILLER                     PIC X(1)     VALUE X'15'.         
           05  PM-PARM-MSG-LENGTH             PIC S9(4)    COMP                 
                                                           VALUE +0066.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-VSAM-MNEMONIC-FOUND-SW      PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MNEMONIC-FOUND                  VALUE 'Y'.           
               88  PS-VSAM-MNEMONIC-NOT-FOUND              VALUE 'N'.           
           05  PS-VSAM-MENUITEM-FOUND-SW      PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MENUITEM-FOUND                  VALUE 'Y'.           
               88  PS-VSAM-MENUITEM-NOT-FOUND              VALUE 'N'.           
           05  PS-VSAM-MNEMONIC-UPDT-SW       PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MNEMONIC-UPDT-OK                VALUE 'Y'.           
               88  PS-VSAM-MNEMONIC-UPDT-ERR               VALUE 'N'.           
           05  PS-VSAM-MNEMONIC-DLET-SW       PIC X(01)    VALUE 'N'.           
               88  PS-VSAM-MNEMONIC-DLET-OK                VALUE 'Y'.           
               88  PS-VSAM-MNEMONIC-DLET-ERR               VALUE 'N'.           
           05  PS-TRANSACTION-SW              PIC X(01)    VALUE 'Y'.           
               88  PS-TRANSACTION-ALLGOOD                  VALUE 'Y'.           
               88  PS-TRANSACTION-AWRY                     VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-TRANS-ID-CODE               PIC X(04)    VALUE SPACES.        
           05  PV-DESCRIPTION                 PIC X(30)    VALUE SPACES.        
           05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.        
           05  PV-MENUITEM-KEY                PIC X(12)    VALUE SPACES.        
           05  PV-RETRY-COUNTER               PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SQL-SUB                     PIC S9(3)    VALUE ZERO           
                                                           COMP-3.              
           05  PV-SUB                         PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-RESP                        PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-NULL-IND                    PIC S9(4)    VALUE ZERO           
                                                           COMP.                
           05  PV-AID-SEQUENCE-NMBRX.                                           
               10  PV-AID-SEQUENCE-NMBR       PIC S9(2).                        
                                                                                
           05  PV-TITLE-TABLE.                                                  
               10  PV-TITLE-CHAR              PIC X(1)                          
                                              OCCURS 30 TIMES.                  
           05  PV-CHAR-COUNT                  PIC S9(4)    COMP.                
           05  PV-STRING-POINTER              PIC S9(4)    COMP.                
           05  PV-DPCAUDIT-REC-LEN            PIC S9(4)    COMP.                
           05  PV-DPCAUDIT-RBA                PIC S9(9)    COMP SYNC.           
           05  PV-CURRENT-TMESTP-FOR-TRAN     PIC X(26).                        
           05  PV-ABEND-INFO.                                                   
               10  PV-ABEND-MNEMONIC          PIC X(8)     VALUE SPACE.         
               10  FILLER                     PIC X(2)     VALUE ', '.          
               10  PV-ABEND-TIMESTAMP         PIC X(26)    VALUE SPACE.         
           05  PV-MNEMONIC-JUST-UPDATED       PIC X(8)     VALUE SPACE.         
           05  PV-PROGRAM-SCOREBOARD.                                           
               10  PV-SOFT-SUCCESSES          PIC 9(6)     VALUE ZERO.          
               10  PV-SOFT-AWRY               PIC 9(6)     VALUE ZERO.          
               10  PV-DLET-SUCCESSES          PIC 9(6)     VALUE ZERO.          
               10  PV-DLET-AWRY               PIC 9(6)     VALUE ZERO.          
               10  PV-UPDT-SUCCESSES          PIC 9(6)     VALUE ZERO.          
               10  PV-UPDT-AWRY               PIC 9(6)     VALUE ZERO.          
                                                                                
                                                                                
           05  PV-LOG-LINE                    PIC  X(60)  VALUE SPACE.          
           05  PV-LOG-START-LINE.                                               
               10  FILLER                     PIC  X(22)  VALUE                 
                                              ' DPKCS033: STARTED AT '.         
               10  PV-LOG-START-TIME          PIC  X(08)  VALUE SPACE.          
               10  FILLER                     PIC  X(01)  VALUE SPACE.          
               10  PV-LOG-START-DATE          PIC  X(10)  VALUE SPACE.          
      *                                                                         
           05  PV-LOG-STATS-LINE.                                               
               10  FILLER                     PIC  X(11)  VALUE                 
                                              ' DPKCS033: '.                    
               10  PV-LOG-STATS-DESC          PIC  X(10)  VALUE  SPACE.         
               10  PV-LOG-STATS-SUCCESSES     PIC 9(6)     VALUE ZERO.          
               10  FILLER                     PIC X(1)     VALUE '-'.           
               10  PV-LOG-STATS-AWRY          PIC 9(6)     VALUE ZERO.          
      *                                                                         
           05  PV-LOG-END-LINE.                                                 
               10  FILLER                     PIC  X(20)  VALUE                 
                                              ' DPKCS033: ENDED AT '.           
               10  PV-LOG-END-TIME            PIC  X(08)  VALUE SPACE.          
               10  FILLER                     PIC  X(01)  VALUE SPACE.          
               10  PV-LOG-END-DATE            PIC  X(10)  VALUE SPACE.          
      *                                                                         
           05  PV-PARM-DATA.                                                    
               10  PV-PARM-TRANID             PIC  X(04)  VALUE SPACE.          
               10                             PIC  X(01)  VALUE SPACE.          
                   88  PV-VALID-FORMAT                    VALUE SPACE.          
               10  PV-PARM-MNEMONIC           PIC  X(08)  VALUE SPACE.          
           05  PV-PARM-LENGTH                 PIC S9(04)  COMP                  
                                                          VALUE +13.            
           05  PV-PARM-SW                     PIC  X(01)  VALUE 'N'.            
               88  PV-PARM-ENTERED                        VALUE 'Y'.            
               88  PV-PARM-ALL                            VALUE 'N'.            
      *                                                                         
                                                                                
      *****************************************************************         
      *    APPLICATION SPECIFIC WORKING STORAGE                                 
      *****************************************************************         
       01  DP033-WS.                                                            
               10  DP033-SPECIFIC-COMMAREA.                                     
                   15  DP033-APPL-CURSOR-POSITION PIC S9(04) COMP.              
                                                                                
                   15  DP033-MNEMONIC             PIC X(8).                     
                                                                                
                                                                                
      *****************************************************************         
      *    MNEMONIC VSAM RECORD LAYOUT.                                         
      *****************************************************************         
           COPY DPRD503.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    MENUITEM VSAM RECORD LAYOUT.                                         
      *****************************************************************         
           COPY DPRD504.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    DPCAUDIT - CICSARCH AUDIT LOG RECORD LAYOUT.                         
      *****************************************************************         
           COPY DPRD520.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    ABEND PROCESSING COPYBOOK.                                           
      *****************************************************************         
           COPY DPWS013.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 ROUTE TABLE.                                                     
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TCICSRTE                                                
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 MNEMONIC TABLE                                                   
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMNMNC                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 MENU ITEM TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TMNUITM                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATIONS AREA                                              
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    CICSROUTE CURSOR                                                     
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE ROUTE-CURSOR CURSOR FOR                                 
                SELECT  AID_SEQUENCE_NMBR                                       
                      , DEST_MNEMONIC_CODE                                      
                      , ACTION_CODE                                             
                      , ROUTE_STACK_INDCT                                       
                      , FUNC_KEY_LBL_NAME                                       
                      , IN_APLCTN_COMM_CDE                                      
                      , FUNC_KY_DISPN_IND                                       
                FROM    TCICSROUTE                                              
                WHERE   MNEMONIC_CODE   =  :DP033-MNEMONIC                      
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    CICSROUTE DESTINATION CURSOR                                         
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE ROUTE-DEST-CURSOR CURSOR FOR                            
                SELECT  MNEMONIC_CODE                                           
                      , AID_SEQUENCE_NMBR                                       
                FROM    TCICSROUTE                                              
                WHERE   DEST_MNEMONIC_CODE   =  :DP033-MNEMONIC                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    MENUITEM CURSOR FOR PROCESSING ALL THE MNEMONIC ITEMS LISTED         
      *    ON THE **MENU** ENTERED FOR THIS REQUEST.                            
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE MENUITEM-CURSOR CURSOR FOR                              
                SELECT  A.MENU_NAME                                             
                      , A.SEQUENCE_NMBR                                         
                      , A.ITEM_MNEMONIC_CODE                                    
                      , A.DESC                                                  
                      , A.IN_APLCTN_COMM_CDE                                    
                      , B.TRANS_ID_CODE                                         
                      , B.AVAIL_INDCT                                           
                      , B.ACRT_LOC_REQR_IND                                     
                      , B.CMAREA_FRMT_CODE                                      
                FROM    TMENUITEM A,                                            
                        TMNEMONIC B                                             
                WHERE   A.MENU_NAME     =  :TMNEMONIC-MNEMONIC-CODE             
                  AND   B.MNEMONIC_CODE =  A.ITEM_MNEMONIC_CODE                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    MENUNAME CURSOR FOR PROCESSING ALL THE MNEMONIC MENUS ON             
      *    WHICH THE **ITEM** ENTERED FOR THIS REQUEST IS LISTED.               
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE MENUNAME-CURSOR CURSOR FOR                              
                SELECT  MENU_NAME                                               
                FROM    TMENUITEM                                               
                WHERE   ITEM_MNEMONIC_CODE = :DP033-MNEMONIC                    
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    TMNEMONIC CURSOR FOR SELECTING ALL MNEMONICS MARKED FOR              
      *    DELETION WHEN NO PARM WAS ENTERED  OR  FOR A SPECIFIC                
      *    MNEMONIC WHEN A PARM WAS ENTERED. (MNEMONICS ARE MARKED FOR          
      *    DELETION BY PROGRAM DPKCS022).                                       
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE DELETION-CURSOR CURSOR FOR                              
                SELECT  MNEMONIC_CODE                                           
                     ,  TRANS_ID_CODE                                           
                     ,  AVAIL_INDCT                                             
                     ,  CMAREA_FRMT_CODE                                        
                     ,  SAVE_CMAREA_INDCT                                       
                     ,  CMAREA_LNGTH_MSRMT                                      
                     ,  PANEL_TITLE_NAME                                        
                     ,  LAST_UPDATE_TMESTP                                      
                     ,  GLBL_ACCS_IND                                           
                     ,  PGM_NAME                                                
                     ,  SCRN_TYP_CDE                                            
                     ,  UPD_VSAM_IND                                            
                     ,  DB2_PKG_CDE                                             
                     ,  ACRT_LOC_REQR_IND                                       
                FROM    TMNEMONIC                                               
                WHERE   UPD_VSAM_IND    =  :PC-D-DELETE-REQUESTED               
                  AND  (MNEMONIC_CODE   =  :PV-PARM-MNEMONIC                    
                   OR   :PV-PARM-SW     =  'N')                                 
                ORDER BY                                                        
                        MNEMONIC_CODE                                           
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    TMNEMONIC CURSOR FOR SELECTING ALL MNEMONICS MARKED FOR              
      *    UPDATING WHEN NO PARM WAS ENTERED  OR  FOR A SPECIFIC                
      *    MNEMONIC WHEN A PARM WAS ENTERED.                                    
      *****************************************************************         
           EXEC SQL                                                             
                DECLARE UPDATING-CURSOR CURSOR FOR                              
                SELECT  MNEMONIC_CODE                                           
                     ,  TRANS_ID_CODE                                           
                     ,  AVAIL_INDCT                                             
                     ,  CMAREA_FRMT_CODE                                        
                     ,  SAVE_CMAREA_INDCT                                       
                     ,  CMAREA_LNGTH_MSRMT                                      
                     ,  PANEL_TITLE_NAME                                        
                     ,  LAST_UPDATE_TMESTP                                      
                     ,  GLBL_ACCS_IND                                           
                     ,  PGM_NAME                                                
                     ,  SCRN_TYP_CDE                                            
                     ,  UPD_VSAM_IND                                            
                     ,  DB2_PKG_CDE                                             
                     ,  ACRT_LOC_REQR_IND                                       
                FROM    TMNEMONIC                                               
                WHERE  (UPD_VSAM_IND     = :PC-Y-UPDATE-REQUESTED               
                   OR   UPD_VSAM_IND     = :PC-D-DELETE-REQUESTED               
                  AND   :PV-PARM-SW      = 'N')                                 
                   OR  (MNEMONIC_CODE    = :PV-PARM-MNEMONIC                    
                  AND   :PV-PARM-SW      = 'Y')                                 
                   OR  (UPD_VSAM_IND     = :PC-I-IMMEDIATE-UPDATE)              
           END-EXEC.                                                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-CONTROL-PROCESSING.                                     
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
       1000-CONTROL-PROCESSING.                                                 
      ******************************************************************        
      *    CONTROL LOOP FOR PROCESSING.                                *        
      ******************************************************************        
                                                                                
      *    *************************************************************        
      *    * INITIALIZATIONS.                                          *        
      *    *************************************************************        
                                                                                
           EXEC CICS RECEIVE                                                    
               INTO   (PV-PARM-DATA)                                            
               LENGTH (PV-PARM-LENGTH)                                          
               RESP   (PV-RESP)                                                 
           END-EXEC.                                                            
           IF (PV-RESP = DFHRESP(NORMAL)                                        
           OR  PV-RESP = DFHRESP(EOC))                                          
           AND PV-VALID-FORMAT                                                  
               IF PV-PARM-MNEMONIC > SPACES                                     
                   SET PV-PARM-ENTERED TO TRUE                                  
               ELSE                                                             
                   SET PV-PARM-ALL     TO TRUE                                  
           ELSE                                                                 
               EXEC CICS SEND TEXT                                              
                   FROM   (PM-PARM-ERROR)                                       
                   LENGTH (PM-PARM-MSG-LENGTH)                                  
               END-EXEC                                                         
               GOBACK.                                                          
                                                                                
           PERFORM 9000-GET-CURR-DATE-TIME.                                     
           MOVE DT-FORMATTED-TIME   TO PV-LOG-START-TIME.                       
           MOVE DT-FORMATTED-DATE   TO PV-LOG-START-DATE.                       
           MOVE PV-LOG-START-LINE   TO PV-LOG-LINE.                             
           PERFORM 9800-ENQUEUE.                                                
           PERFORM 9810-WRITEQ-TD.                                              
                                                                                
           MOVE LENGTH OF DP503-PANEL-TITLE                                     
                                       TO  PC-CHAR-MAX.                         
           MOVE SPACES                 TO  DP503-MNEMONIC-RECORD.               
           MOVE PC-INIT-TMESTP-DEFAULT TO PV-CURRENT-TMESTP-FOR-TRAN.           
                                                                                
                                                                                
      *    *************************************************************        
      *    * PROCESS THE REQUEST.                                      *        
      *    *************************************************************        
           PERFORM 1100-PROCESS-REQUEST.                                        
                                                                                
                                                                                
      *    *************************************************************        
      *    * END-OF-RUN.                                               *        
      *    *************************************************************        
           MOVE 'SOFT DLET:'                TO PV-LOG-STATS-DESC.               
           MOVE PV-SOFT-SUCCESSES           TO PV-LOG-STATS-SUCCESSES.          
           MOVE PV-SOFT-AWRY                TO PV-LOG-STATS-AWRY.               
           MOVE PV-LOG-STATS-LINE   TO PV-LOG-LINE.                             
           PERFORM 9810-WRITEQ-TD.                                              
                                                                                
           MOVE 'AUDT DLET:'                TO PV-LOG-STATS-DESC.               
           MOVE PV-DLET-SUCCESSES           TO PV-LOG-STATS-SUCCESSES.          
           MOVE PV-DLET-AWRY                TO PV-LOG-STATS-AWRY.               
           MOVE PV-LOG-STATS-LINE   TO PV-LOG-LINE.                             
           PERFORM 9810-WRITEQ-TD.                                              
                                                                                
           MOVE 'AUDT UPDT:'                TO PV-LOG-STATS-DESC.               
           MOVE PV-UPDT-SUCCESSES           TO PV-LOG-STATS-SUCCESSES.          
           MOVE PV-UPDT-AWRY                TO PV-LOG-STATS-AWRY.               
           MOVE PV-LOG-STATS-LINE   TO PV-LOG-LINE.                             
           PERFORM 9810-WRITEQ-TD.                                              
                                                                                
           PERFORM 9000-GET-CURR-DATE-TIME.                                     
           MOVE DT-FORMATTED-TIME   TO PV-LOG-END-TIME.                         
           MOVE DT-FORMATTED-DATE   TO PV-LOG-END-DATE.                         
           MOVE PV-LOG-END-LINE     TO PV-LOG-LINE.                             
           PERFORM 9810-WRITEQ-TD.                                              
           PERFORM 9820-DEQUEUE.                                                
                                                                                
      *    *************************************************************        
      *    * DISPLAY SCOREBOARD.                                       *        
      *    *************************************************************        
           IF PV-CURRENT-TMESTP-FOR-TRAN  =    PC-INIT-TMESTP-DEFAULT           
               MOVE SPACES                  TO PM-AT-OR-BLANK                   
           ELSE                                                                 
               MOVE PC-AT                   TO PM-AT-OR-BLANK                   
               MOVE PV-CURRENT-TMESTP-FOR-TRAN                                  
                                            TO PM-LAST-TMESTP.                  
           MOVE PV-SOFT-SUCCESSES           TO PM-SOFT-SUCCESSES.               
           MOVE PV-SOFT-AWRY                TO PM-SOFT-AWRY.                    
           MOVE PV-DLET-SUCCESSES           TO PM-DLET-SUCCESSES.               
           MOVE PV-DLET-AWRY                TO PM-DLET-AWRY.                    
           MOVE PV-UPDT-SUCCESSES           TO PM-UPDT-SUCCESSES.               
           MOVE PV-UPDT-AWRY                TO PM-UPDT-AWRY.                    
           PERFORM 9900-SEND-TEXT.                                              
                                                                                
                                                                                
       1100-PROCESS-REQUEST.                                                    
      ******************************************************************        
      *    THERE ARE TWO PARTS TO THIS PROGRAM; TWO TYPES OF REQUESTS  *        
      *    ARE PROCESSED.                                              *        
      *    1) DELETION PROCESS.                                        *        
      *       THROUGHOUT THE DELETION PROCESS, ROWS MAY BE MARKED FOR  *        
      *       UPDATING.                                                *        
      *    2) UPDATING PROCESS.                                        *        
      *       THE UPDATING PROCESS TAKES CARE OF REQUESTS INITIATED    *        
      *       THROUGH OTHER PROGRAMS IN THE CICS ARCHITECTURE, AS      *        
      *       WELL AS THOSE GENERATED DURING THE DELETION PROCESS      *        
      *       (FIRST PART OF THIS PROGRAM).                            *        
      ******************************************************************        
                                                                                
                                                                                
      ******************************************************************        
      *    BEGIN THE DELETE PROCESS.                                   *        
      ******************************************************************        
           PERFORM 9010-OPEN-DELETION-CURSOR.                                   
           PERFORM 9011-FETCH-DELETION-CURSOR.                                  
                                                                                
           PERFORM                                                              
           UNTIL SQLCODE NOT EQUAL ZERO                                         
               SET  PS-TRANSACTION-ALLGOOD  TO  TRUE                            
               MOVE TMNEMONIC-MNEMONIC-CODE TO  DP033-MNEMONIC                  
               INITIALIZE PS-VSAM-MNEMONIC-DLET-SW                              
               PERFORM 2000-CONTROL-MNEMONIC-DELETE                             
               PERFORM 9011-FETCH-DELETION-CURSOR                               
           END-PERFORM.                                                         
           PERFORM 9012-CLOSE-DELETION-CURSOR.                                  
                                                                                
                                                                                
      ******************************************************************        
      *    BEGIN THE UPDATE PROCESS.                                   *        
      ******************************************************************        
           PERFORM 9020-OPEN-UPDATING-CURSOR.                                   
           PERFORM 9021-FETCH-UPDATING-CURSOR.                                  
                                                                                
           PERFORM                                                              
           UNTIL SQLCODE NOT EQUAL ZERO                                         
               SET  PS-TRANSACTION-ALLGOOD  TO  TRUE                            
               MOVE TMNEMONIC-MNEMONIC-CODE TO  DP033-MNEMONIC                  
               PERFORM 8110-POPULATE-AUDIT-FIELDS-1A                            
               PERFORM 9200-OPEN-ROUTE-CURSOR                                   
               PERFORM 5000-CONTROL-MNEMONIC-UPDATE                             
               IF  PS-VSAM-MNEMONIC-UPDT-OK                                     
                   IF  TMNEMONIC-TRANS-ID-CODE EQUAL PC-MENU-TRANID             
                       PERFORM 6000-CONTROL-MENUITEM-UPDATE                     
                   END-IF                                                       
                   PERFORM 7000-CONTROL-MENUNAME-UPDATE                         
               ELSE                                                             
                   SET PS-TRANSACTION-AWRY                                      
                                         TO  TRUE                               
                   INITIALIZE            DP013-MESSAGE-TABLE                    
                   SET DP013-CICS-ABEND  TO  TRUE                               
                   SET DP013-LINK-RETURN TO  TRUE                               
                   SET DP013-NO-ROLLBACK TO  TRUE                               
                   MOVE '1100-PROCESS-REQUEST'                                  
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'PS-VSAM-MNEMONIC-UPDT-ERR OCCURRED'                    
                                         TO  DP013-MESSAGE-TEXT (1)             
                   PERFORM DP014-0000-PROCESS-ABEND                             
               END-IF                                                           
                                                                                
      *        ***********************************************                  
      *        * ACCUMULATE COUNTS IN THE SCOREBOARD.        *                  
      *        ***********************************************                  
               IF  PS-TRANSACTION-ALLGOOD                                       
                   ADD 1 TO PV-UPDT-SUCCESSES                                   
               END-IF                                                           
               IF  PS-TRANSACTION-AWRY                                          
                   ADD 1 TO PV-UPDT-AWRY                                        
               END-IF                                                           
                                                                                
               PERFORM 9021-FETCH-UPDATING-CURSOR                               
           END-PERFORM.                                                         
           PERFORM 9022-CLOSE-UPDATING-CURSOR.                                  
                                                                                
                                                                                
       2000-CONTROL-MNEMONIC-DELETE.                                            
      ******************************************************************        
      *    THE MNEMONIC "DELETE" IS A TWO-PASS DELETE:                 *        
      *    1) FIRST PASS: SOFT DELETE.  UPDATE THE MNEMONIC TABLE TO   *        
      *       INDICATE: A) THAT THIS MNEMONIC IS UNAVAILABLE.          *        
      *                 B) AN UPDATE TO THE VSAM FILE IS NEEDED.       *        
      *                    NOTE THAT THIS REQUESTED UPDATING TO THE    *        
      *                    MNEMONIC VSAM WILL OCCUR IN THE SECOND PART *        
      *                    OF THIS PROGRAM (SEE PARA 1100-... ).       *        
      *                                                                *        
      *    2) SECOND PASS: HARD DELETE.  ALSO UPDATE / DELETE RELATED  *        
      *       ROWS ON OTHER DB2 TABLES AS APPROPRIATE:                 *        
      *                 ************************************************        
      *                 * PROCESSING RELATED TO TCICSROUTE DB2 TABLE: **        
      *                 ************************************************        
      *                 A) TCICSROUTE: DELETE ANY ROWS WHOSE DESTINATION        
      *                    IS THIS MNEMONIC.                           *        
      *                 B) TMNEMONIC:  UPDATE THOSE ROWS WHOSE DEST    *        
      *                    IS THIS MNEMONIC, TO INDICATE AN UPDATE TO  *        
      *                    THE VSAM FILE IS NEEDED.                    *        
      *                    NOTE THAT THIS REQUESTED UPDATING TO THE    *        
      *                    MNEMONIC VSAM WILL OCCUR IN THE SECOND PART *        
      *                    OF THIS PROGRAM (SEE PARA 1100-... ).       *        
      *                 ************************************************        
      *                 * PROCESSING RELATED TO TMENUITEM DB2 TABLE:  **        
      *                 ************************************************        
      *                 C) TMENUITEM:  DELETE THOSE ROWS WHOSE MNEMONIC*        
      *                    IS THIS MNEMONIC.                           *        
      *                 D) TMNEMONIC:                                  *        
      *                    D.1)  IF THIS MNEMONIC IS A MENU, NO UPDATE *        
      *                          NEEDED FOR THE ITEMS LISTED ON THIS   *        
      *                          MNEMONIC, AS THEY WILL ALL BE DELETED *        
      *                          (SEE STEP E. BELOW).                  *        
      *                    D.2)  IF THIS MNEMONIC IS AN ITEM, UPDATE   *        
      *                          THOSE ROWS WHICH ARE MENUS LISTING    *        
      *                          THIS MNEMONIC, TO INDICATE AN         *        
      *                          UPDATE TO THE VSAM FILE IS NEEDED.    *        
      *                    NOTE THAT THIS REQUESTED UPDATING TO THE    *        
      *                    MNEMONIC VSAM WILL OCCUR IN THE SECOND PART *        
      *                    OF THIS PROGRAM (SEE PARA 1100-... ).       *        
      *                 ************************************************        
      *                 * PROCESSING RELATED TO  MNEMONIC  VSAM FILE: **        
      *                 ************************************************        
      *                 E) MNEMONIC VSAM FILE: WRITE A COPY OF THE     *        
      *                    "BEFORE" IMAGE TO THE LOG, THEN DELETE THIS *        
      *                    RECORD FROM THE MNEMONIC VSAM FILE.         *        
      *                 ************************************************        
      *                 * PROCESSING RELATED TO TMNEMONIC  DB2 TABLE: **        
      *                 ************************************************        
      *                 F) TMNEMONIC:  DELETE THIS MNEMONIC.           *        
      *                    NOTE THAT THE TMENUITEM DB2 TABLE RECEIVES  *        
      *                    CASCADING DELETES WHEN THE MNEMONIC BEING   *        
      *                    DELETED MATCHES THE TMENUITEM'S             *        
      *                    ITEM_MNEMONIC_CODE -OR- MENU_NAME.          *        
      ******************************************************************        
           IF TMNEMONIC-AVAIL-INDCT = 'Y'                                       
               PERFORM 2200-DELETION-1ST-PASS                                   
           ELSE                                                                 
               PERFORM 2300-DELETION-2ND-PASS.                                  
                                                                                
                                                                                
       2200-DELETION-1ST-PASS.                                                  
      ******************************************************************        
      *    FIRST PASS: SOFT DELETE.                                    *        
      ******************************************************************        
           PERFORM 9121-UPDATE-MNEMONIC-ROW.                                    
                                                                                
      *    ***********************************************                      
      *    * ACCUMULATE COUNTS IN THE SCOREBOARD.        *                      
      *    ***********************************************                      
           IF  PS-TRANSACTION-ALLGOOD                                           
               ADD 1 TO PV-SOFT-SUCCESSES                                       
           END-IF.                                                              
           IF  PS-TRANSACTION-AWRY                                              
               ADD 1 TO PV-SOFT-AWRY                                            
           END-IF.                                                              
                                                                                
                                                                                
       2300-DELETION-2ND-PASS.                                                  
      ******************************************************************        
      *    SECOND PASS: HARD DELETE.                                   *        
      ******************************************************************        
                                                                                
      *    ********************************************************             
      *    * UPDATE TO GET A CURRENT TIMESTAMP FOR TRANSACTION.   *             
      *    ********************************************************             
           PERFORM 9121-UPDATE-MNEMONIC-ROW.                                    
                                                                                
      *    ********************************************************             
      *    * DELETION PROCESSING RELATED TO TCICSROUTE DB2 TABLE: *             
      *    * DELETE ANY ROW(S) WHERE ANOTHER MNEMONIC HAS THIS    *             
      *    * MNEMONIC AS ITS DESTINATION.                         *             
      *    ********************************************************             
           PERFORM 9220-OPEN-ROUTE-DEST-CURSOR.                                 
           PERFORM 9230-FETCH-ROUTE-DEST-CURSOR.                                
           PERFORM                                                              
             UNTIL SQLCODE NOT EQUAL ZERO                                       
               PERFORM 9240-DELETE-ROUTE-DEST-ROW                               
               PERFORM 9122-UPDATE-MNEMONIC-ITEM-ROW                            
               PERFORM 9230-FETCH-ROUTE-DEST-CURSOR                             
           END-PERFORM.                                                         
           PERFORM 9231-CLOSE-ROUTE-DEST-CURSOR.                                
                                                                                
                                                                                
      *    ********************************************************             
      *    * DELETION PROCESSING RELATED TO TCICSROUTE DB2 TABLE: *             
      *    * DELETE ALL ROW(S) FOR THIS MNEMONIC.                 *             
      *    ********************************************************             
           PERFORM 9245-DELETE-ROUTE-GENERIC.                                   
                                                                                
                                                                                
      *    ********************************************************             
      *    * DELETION PROCESSING RELATED TO TMENUITEM  DB2 TABLE: *             
      *    ********************************************************             
           PERFORM 9320-OPEN-MENUNAME-CURSOR.                                   
           PERFORM 9330-FETCH-MENUNAME-CURSOR.                                  
           PERFORM                                                              
             UNTIL SQLCODE NOT EQUAL ZERO                                       
               PERFORM 9340-DELETE-MENUNAME-ROW                                 
               PERFORM 9142-UPDATE-MNEMONIC-MENU-ROW                            
               PERFORM 9330-FETCH-MENUNAME-CURSOR                               
           END-PERFORM.                                                         
           PERFORM 9331-CLOSE-MENUNAME-CURSOR.                                  
                                                                                
                                                                                
      *    ********************************************************             
      *    * DELETION PROCESSING TO MNEMONIC VSAM FILE:           *             
      *    ********************************************************             
           PERFORM 8110-POPULATE-AUDIT-FIELDS-1D.                               
           PERFORM 8120-POPULATE-AUDIT-FIELDS-2D.                               
           PERFORM 8130-POPULATE-AUDIT-FIELDS-3.                                
           PERFORM 9600-WRITE-AUDIT-TRAIL-LOG.                                  
           PERFORM 9440-DELETE-MNEMONIC-VSAM.                                   
                                                                                
                                                                                
      *    ********************************************************             
      *    * DELETION PROCESSING TO TMNEMONIC DB2 TABLE:          *             
      *    ********************************************************             
           IF  PS-TRANSACTION-ALLGOOD                                           
               PERFORM 9140-DELETE-MNEMONIC-ROW.                                
                                                                                
                                                                                
      *    ***********************************************                      
      *    * ACCUMULATE COUNTS IN THE SCOREBOARD.        *                      
      *    ***********************************************                      
           IF  PS-TRANSACTION-ALLGOOD                                           
               ADD 1 TO PV-DLET-SUCCESSES                                       
           END-IF.                                                              
           IF  PS-TRANSACTION-AWRY                                              
               ADD 1 TO PV-DLET-AWRY                                            
           END-IF.                                                              
                                                                                
                                                                                
       5000-CONTROL-MNEMONIC-UPDATE.                                            
      ******************************************************************        
      *    1) READ THE MNEMONIC VSAM FILE FOR THE REQUESTED MNEMONIC.  *        
      *       - IF RECORD EXISTS, IT WILL BE UPDATED (REWRITE).        *        
      *       - IF RECORD DOES NOT EXIST, IT WILL BE ADDED (WRITE).    *        
      *    2) POPULATE APPROPRIATE FIELDS IN THE MNEMONIC VSAM RECORD  *        
      *       WITH VALUES FROM THE MNEMONIC DB2 TABLE ROWS ALREADY     *        
      *       SELECTED.                                                *        
      *    3) READ THE ROUTE CURSOR AND POPULATE FUNCTION KEY FIELDS   *        
      *       IN THE MNEMONIC VSAM RECORD WITH VALUES FROM THE ROUTE   *        
      *       CURSOR.                                                  *        
      ******************************************************************        
           PERFORM 9400-READ-MNEMONIC-VSAM.                                     
           PERFORM 8120-POPULATE-AUDIT-FIELDS-2A.                               
                                                                                
           INITIALIZE DP503-FUNCTION-KEY-ARRAY.                                 
           MOVE TMNEMONIC-TRANS-ID-CODE                                         
                                     TO  DP503-TRAN-ID.                         
           MOVE TMNEMONIC-GLBL-ACCS-IND                                         
                                     TO  DP503-GLOBAL-ACCESS-IND.               
           MOVE TMNEMONIC-AVAIL-INDCT                                           
                                     TO  DP503-TRAN-AVAILABLE-SW.               
           MOVE TMNEMONIC-CMAREA-FRMT-CODE                                      
                                     TO  DP503-COMMAREA-FORMAT.                 
           MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                                    
                                     TO  DP503-APPL-COMMAREA-LENGTH.            
           MOVE TMNEMONIC-SAVE-CMAREA-INDCT                                     
                                     TO  DP503-SAVE-COMMAREA-SW.                
           MOVE TMNEMONIC-DB2-PKG-CDE                                           
                                     TO  DP503-DB2-PKG-IND.                     
           MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                     
                                     TO  DP503-ACRT-LOC-REQR-IND.               
           MOVE TMNEMONIC-PANEL-TITLE-NAME                                      
                                     TO  PV-TITLE-TABLE.                        
           PERFORM                                                              
               VARYING PV-CHAR-COUNT                                            
               FROM PC-CHAR-MAX BY -1                                           
               UNTIL PV-TITLE-CHAR (PV-CHAR-COUNT) NOT EQUAL SPACE              
                 OR  PV-CHAR-COUNT EQUAL ZERO                                   
           END-PERFORM.                                                         
                                                                                
           COMPUTE PV-STRING-POINTER =                                          
               ((LENGTH OF DP503-PANEL-TITLE - PV-CHAR-COUNT) / 2) + 1.         
           MOVE SPACES TO DP503-PANEL-TITLE.                                    
           STRING TMNEMONIC-PANEL-TITLE-NAME DELIMITED BY SIZE                  
                  INTO    DP503-PANEL-TITLE                                     
                  POINTER PV-STRING-POINTER                                     
           END-STRING.                                                          
                                                                                
           PERFORM 9210-FETCH-ROUTE-CURSOR.                                     
           PERFORM 5100-POPULATE-VSAM-FKEY-TBL                                  
                   UNTIL SQLCODE NOT EQUAL ZERO.                                
           PERFORM 9211-CLOSE-ROUTE-CURSOR.                                     
                                                                                
           IF  PS-VSAM-MNEMONIC-FOUND                                           
               PERFORM 9410-REWRITE-MNEMONIC-VSAM                               
           ELSE                                                                 
               PERFORM 9420-WRITE-MNEMONIC-VSAM                                 
           END-IF.                                                              
           IF  PS-VSAM-MNEMONIC-UPDT-OK                                         
               IF TMNEMONIC-UPD-VSAM-IND NOT = 'D'                              
                   PERFORM 9120-UPDATE-MNEMONIC-ROW                             
               END-IF                                                           
               PERFORM 8130-POPULATE-AUDIT-FIELDS-3                             
               PERFORM 9600-WRITE-AUDIT-TRAIL-LOG.                              
                                                                                
                                                                                
       5100-POPULATE-VSAM-FKEY-TBL.                                             
      ******************************************************************        
      *    FOR EVERY ROW RETURNED FROM THE TCICSROUTE CURSOR, UPDATE   *        
      *    THE CORRESPONDING ENTRY IN THE VSAM FILE'S FUNCTION KEY TBL.*        
      *    WHEN ALL TCICSROUTE ROWS ARE PROCESSED, THE VSAM RECORD     *        
      *    IS FULLY POPULATED AND READY TO BE WRITTEN OUT.             *        
      ******************************************************************        
                                                                                
           MOVE TCICSROUTE-AID-SEQUENCE-NMBR                                    
                                     TO  PV-AID-SEQUENCE-NMBRX.                 
           SET DP503-FK-INDEX        TO  PV-AID-SEQUENCE-NMBR.                  
                                                                                
           MOVE TCICSROUTE-ACTION-CODE                                          
                                     TO  DP503-FK-ACTION                        
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-DEST-MNEMONIC-CODE                                   
                                     TO  DP503-FK-MNEMONIC                      
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-FUNC-KEY-LBL-NAME                                    
                                     TO  DP503-FK-LABEL                         
                                             (DP503-FK-INDEX).                  
           IF (TCICSROUTE-DEST-MNEMONIC-CODE NOT EQUAL SPACE                    
           AND TCICSROUTE-FUNC-KEY-LBL-NAME EQUAL SPACE)                        
               MOVE TCICSROUTE-DEST-MNEMONIC-CODE                               
                                     TO  DP503-FK-LABEL                         
                                             (DP503-FK-INDEX)                   
           END-IF.                                                              
                                                                                
           MOVE TCICSROUTE-ROUTE-STACK-INDCT                                    
                                     TO  DP503-FK-STACK-IND                     
                                             (DP503-FK-INDEX).                  
           MOVE TCICSROUTE-IN-APLCTN-COMM-CDE                                   
                                     TO  DP503-FK-INT-APPL-FORMAT-IND           
                                             (DP503-FK-INDEX).                  
                                                                                
           MOVE TCICSROUTE-FUNC-KY-DISPN-IND                                    
                                     TO  DP503-FK-DISPN-IND                     
                                             (DP503-FK-INDEX).                  
                                                                                
           IF  DP503-FK-MNEMONIC (DP503-FK-INDEX) EQUAL                         
               DP503-FK-LABEL (DP503-FK-INDEX)                                  
               SET DP503-FK-NOT-ALT-LABEL (DP503-FK-INDEX)                      
                                     TO  TRUE                                   
           ELSE                                                                 
               SET DP503-FK-ALT-LABEL (DP503-FK-INDEX)                          
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
      *    *************************************************************        
      *    * GET THE TRANID FROM THE TMNEMONIC DB2 TABLE,                       
      *    * WHICH CORRESPONDS TO THIS ROW FROM THE TCICSROUTE CURSOR.          
      *    * NOTE THAT IF THE TRANID IS NOT FOUND ON THE TMNEMONIC              
      *    * DB2 TABLE, SPACES ARE MOVED FOR THE TRANID.                        
      *    *************************************************************        
           INITIALIZE PV-TRANS-ID-CODE                                          
                      PV-DESCRIPTION.                                           
           PERFORM 9110-SELECT-TRANID-FROM-TMNEM.                               
           MOVE PV-TRANS-ID-CODE     TO  DP503-FK-TRAN-ID                       
                                             (DP503-FK-INDEX).                  
                                                                                
      *    *************************************************************        
      *    * GET NEXT                                                           
      *    *************************************************************        
           PERFORM 9210-FETCH-ROUTE-CURSOR.                                     
                                                                                
                                                                                
       6000-CONTROL-MENUITEM-UPDATE.                                            
      ******************************************************************        
      *    THE MNEMONIC ENTERED FOR THIS REQUEST IS A *MENU*.          *        
      *    FIND ALL THE MNEMONIC ITEMS LISTED ON THIS MENU AND         *        
      *    UPDATE THE APPROPRIATE INFORMATION ON THE MENU ITEM VSAM    *        
      *    DATASET.                                                    *        
      ******************************************************************        
           PERFORM 6100-LOOP-TO-READ-AND-LOG.                                   
                                                                                
           PERFORM 9510-DELETE-MENUITEM-VSAM-GNRC.                              
                                                                                
           PERFORM 9300-OPEN-MENUITEM-CURSOR.                                   
                                                                                
           PERFORM UNTIL SQLCODE NOT EQUAL ZERO                                 
                                                                                
               PERFORM 9310-FETCH-MENUITEM-CURSOR                               
                                                                                
               IF  SQLCODE EQUAL ZERO                                           
                   PERFORM 9520-WRITE-MENUITEM-VSAM                             
               END-IF                                                           
                                                                                
           END-PERFORM.                                                         
           PERFORM 9311-CLOSE-MENUITEM-CURSOR.                                  
                                                                                
                                                                                
       6100-LOOP-TO-READ-AND-LOG.                                               
      ******************************************************************        
      *    READ THE MENUITEM VSAM FILE.                                *        
      *    READ EACH RECORD FROM THE MENUITEM VSAM FILE FOR THIS       *        
      *    MNEMONIC.                                                   *        
      *    FOR EACH RECORD READ, WRITE A RECORD TO THE DPCAUDIT FILE   *        
      *    TO LOG THE IMAGE "BEFORE" COMMENCING WITH UPDATES.          *        
      ******************************************************************        
           MOVE DP033-MNEMONIC       TO  DP504-MENU-NAME.                       
           MOVE ZERO                 TO  DP504-MENU-ITEM-ORDER-NUMBER.          
                                                                                
           PERFORM                                                              
               UNTIL PV-RESP NOT EQUAL DFHRESP(NORMAL)                          
                OR   DP504-MENU-NAME NOT EQUAL DP033-MNEMONIC                   
                                                                                
               PERFORM 9500-READ-MENUITEM-VSAM                                  
                                                                                
               IF (PV-RESP EQUAL DFHRESP(NORMAL)                                
               AND DP504-MENU-NAME = DP033-MNEMONIC)                            
                   PERFORM 8110-POPULATE-AUDIT-FIELDS-1B                        
                   PERFORM 8120-POPULATE-AUDIT-FIELDS-2B                        
                   PERFORM 8130-POPULATE-AUDIT-FIELDS-3                         
                   PERFORM 9600-WRITE-AUDIT-TRAIL-LOG                           
               END-IF                                                           
                                                                                
               ADD 1  TO  DP504-MENU-ITEM-ORDER-NUMBER                          
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
       7000-CONTROL-MENUNAME-UPDATE.                                            
      ******************************************************************        
      *    THE MNEMONIC ENTERED FOR THIS REQUEST IS AN *ITEM*.         *        
      *    FIND ALL THE MENUS ON WHICH THIS MNEMONIC IS LISTED AND     *        
      *    UPDATE THE APPROPRIATE INFORMATION ON THE MENU ITEM VSAM    *        
      *    DATASET.                                                    *        
      ******************************************************************        
           PERFORM 9320-OPEN-MENUNAME-CURSOR.                                   
           PERFORM 7100-LOOP-FOR-MENUNAME-UPDATE                                
               UNTIL SQLCODE NOT EQUAL ZERO.                                    
           PERFORM 9331-CLOSE-MENUNAME-CURSOR.                                  
                                                                                
                                                                                
       7100-LOOP-FOR-MENUNAME-UPDATE.                                           
      ******************************************************************        
      *    CONTROL THE FETCH, VSAM READ AND UPDATE.                    *        
      ******************************************************************        
           PERFORM 9330-FETCH-MENUNAME-CURSOR.                                  
           IF  SQLCODE EQUAL ZERO                                               
               PERFORM 7200-LOOP2-FOR-MENUNAME-UPDATE                           
           END-IF.                                                              
                                                                                
                                                                                
       7200-LOOP2-FOR-MENUNAME-UPDATE.                                          
      ******************************************************************        
      *    UPDATE THE MENUITEM VSAM DATASET,                           *        
      *    FOR EACH MENU TO WHICH THIS MENUITEM BELONGS.               *        
      ******************************************************************        
           INITIALIZE DP504-MENU-ITEM-RECORD.                                   
           MOVE TMENUITEM-MENU-NAME  TO  DP504-MENU-NAME.                       
                                                                                
           PERFORM                                                              
               UNTIL PV-RESP NOT EQUAL DFHRESP(NORMAL)                          
                OR   DP504-MENU-NAME NOT EQUAL TMENUITEM-MENU-NAME              
                                                                                
               PERFORM 9501-READ-MENUITEM-VSAM-UPDT                             
                                                                                
               IF (PV-RESP EQUAL DFHRESP(NORMAL)                                
               AND DP504-MENU-ITEM-MNEMONIC = TMNEMONIC-MNEMONIC-CODE)          
                                                                                
                   PERFORM 8110-POPULATE-AUDIT-FIELDS-1B                        
                   PERFORM 8120-POPULATE-AUDIT-FIELDS-2C                        
                   PERFORM 8130-POPULATE-AUDIT-FIELDS-3                         
                   PERFORM 9600-WRITE-AUDIT-TRAIL-LOG                           
                                                                                
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                     TO  DP504-MENU-ITEM-TRAN-ID                
                   MOVE TMNEMONIC-AVAIL-INDCT                                   
                                     TO  DP504-MENU-ITEM-AVAIL-IND              
                   MOVE TMNEMONIC-ACRT-LOC-REQR-IND                             
                                     TO  DP504-ACRT-LOC-REQR-IND                
                   MOVE TMNEMONIC-CMAREA-FRMT-CODE                              
                                     TO  DP504-MENU-ITEM-COMMAREA-FRMT          
                   PERFORM 9530-UPDATE-MENUITEM-VSAM                            
                   MOVE SPACES       TO  DP504-MENU-NAME                        
               ELSE                                                             
                   PERFORM 9540-UNLOCK-MENUITEM-VSAM                            
               END-IF                                                           
                                                                                
               ADD 1                 TO  DP504-MENU-ITEM-ORDER-NUMBER           
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
       8110-POPULATE-AUDIT-FIELDS-1A.                                           
      ******************************************************************        
      *    BEGIN CONSTRUCTING A RECORD TO BE WRITTEN AS AN AUDIT TRAIL.*        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           INITIALIZE DP520-DPCAUDIT-RECORD.                                    
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP520-PGM.                          
           MOVE PC-BATCHUP               TO DP520-REQUESTOR.                    
           MOVE DP033-MNEMONIC           TO DP520-REQUEST.                      
           MOVE TMNEMONIC-LAST-UPDATE-TMESTP                                    
                                         TO DP520-MNEMONIC-PRIOR-TMESTP.        
                                                                                
                                                                                
       8110-POPULATE-AUDIT-FIELDS-1B.                                           
      ******************************************************************        
      *    BEGIN CONSTRUCTING A RECORD TO BE WRITTEN AS AN AUDIT TRAIL.*        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           INITIALIZE DP520-DPCAUDIT-RECORD.                                    
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP520-PGM.                          
           MOVE PC-BATCHUP               TO DP520-REQUESTOR.                    
           MOVE DP033-MNEMONIC           TO DP520-REQUEST.                      
           MOVE SPACES                   TO DP520-MNEMONIC-PRIOR-TMESTP.        
                                                                                
                                                                                
       8110-POPULATE-AUDIT-FIELDS-1D.                                           
      ******************************************************************        
      *    BEGIN CONSTRUCTING A RECORD TO BE WRITTEN AS AN AUDIT TRAIL.*        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           INITIALIZE DP520-DPCAUDIT-RECORD.                                    
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP520-PGM.                          
           MOVE PC-BATCHD2               TO DP520-REQUESTOR.                    
           MOVE DP033-MNEMONIC           TO DP520-REQUEST.                      
           MOVE TMNEMONIC-LAST-UPDATE-TMESTP                                    
                                         TO DP520-MNEMONIC-PRIOR-TMESTP.        
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2A.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP503-MNEMONIC-RECORD    TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MNEMONIC                                       
                                         TO DP520-VSAM-REC-TYPE.                
           IF  PS-VSAM-MNEMONIC-FOUND                                           
               MOVE PC-VSAM-ACTION-U-UPDATE                                     
                                         TO DP520-VSAM-ACTION                   
           ELSE                                                                 
               MOVE PC-VSAM-ACTION-A-ADD TO DP520-VSAM-ACTION                   
           END-IF.                                                              
           MOVE PC-DPCAUDIT-MNEMONIC-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2B.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP504-MENU-ITEM-RECORD   TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MENUITEM                                       
                                         TO DP520-VSAM-REC-TYPE.                
      *    ALL ITEM RECORDS BEING LOGGED NOW WILL SOON BE DELETED AND           
      *    REPLACED:                                                            
           MOVE PC-VSAM-ACTION-R-REPLACE TO DP520-VSAM-ACTION.                  
           MOVE PC-DPCAUDIT-MENUITEM-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2C.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP504-MENU-ITEM-RECORD   TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MENUNAME                                       
                                         TO DP520-VSAM-REC-TYPE.                
      *    THIS PARTICULAR MENU RECORD ON MENUITEM IS NOW BEING UPDATED:        
           MOVE PC-VSAM-ACTION-U-UPDATE  TO DP520-VSAM-ACTION.                  
           MOVE PC-DPCAUDIT-MENUITEM-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8120-POPULATE-AUDIT-FIELDS-2D.                                           
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE DP503-MNEMONIC-RECORD    TO DP520-VSAM-BEFORE-IMAGE.            
           MOVE PC-VSAM-REC-TYPE-MNEMONIC                                       
                                         TO DP520-VSAM-REC-TYPE.                
           MOVE PC-VSAM-ACTION-D-DELETE  TO DP520-VSAM-ACTION                   
           MOVE PC-DPCAUDIT-MNEMONIC-LEN TO PV-DPCAUDIT-REC-LEN.                
                                                                                
                                                                                
       8130-POPULATE-AUDIT-FIELDS-3.                                            
      ******************************************************************        
      *    ADD TO THE RECORD TO BE WRITTEN AS AN AUDIT TRAIL.          *        
      *    POPULATE FIELDS FOR WHICH VALUES ARE AVAILABLE AT THIS TIME.*        
      ******************************************************************        
           MOVE PV-CURRENT-TMESTP-FOR-TRAN                                      
                                         TO DP520-CURRENT-TMESTP.               
                                                                                
                                                                                
       9000-GET-CURR-DATE-TIME.                                                 
      ******************************************************************        
      *   GET THE CURRENT FORMATTED DATE AND TIME                               
      ******************************************************************        
           EXEC CICS                                                            
               ASKTIME                                                          
                   ABSTIME (DT-ABSTIME)                                         
           END-EXEC.                                                            
                                                                                
           EXEC CICS                                                            
               FORMATTIME                                                       
                   ABSTIME (DT-ABSTIME)                                         
                   MMDDYY  (DT-FORMATTED-MMDDYY)                                
                   DATESEP                                                      
                   TIME    (DT-FORMATTED-TIME)                                  
                   TIMESEP                                                      
           END-EXEC.                                                            
      *                                                                         
           EXEC CICS                                                            
               FORMATTIME                                                       
                   ABSTIME (DT-ABSTIME)                                         
                   YEAR    (DT-YEAR-BIN)                                        
           END-EXEC.                                                            
           MOVE DT-YEAR-BIN   TO DT-FORMATTED-YYYY.                             
                                                                                
                                                                                
      ******************************************************************        
      ******************************************************************        
      *    ALL FILE ACCESS PARAGRAPHS IN THIS PROGRAM BEGIN WITH                
      *    NINE-THOUSAND:                                                       
      *    0) DB2 TABLE ACCESS TMNEMONIC  - 90XX                                
      *       (CURSORS WHICH DRIVE THE                                          
      *       "DELETE" AND "UPDATE"                                             
      *       PROCESSING IN PGM DPKCS033)                                       
      *    1) DB2 TABLE ACCESS TMNEMONIC  - 91XX                                
      *    2) DB2 TABLE ACCESS TCICSROUTE - 92XX                                
      *    3) DB2 TABLE ACCESS TMENUITEM  - 93XX                                
      *    4) VSAM FILE ACCESS MNEMONIC   - 94XX                                
      *    5) VSAM FILE ACCESS MENUITEM   - 95XX                                
      *    6) VSAM FILE ACCESS DPCAUDIT   - 96XX                                
      ******************************************************************        
      ******************************************************************        
                                                                                
                                                                                
       9010-OPEN-DELETION-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE CURSOR OF ALL MNEMONICS MARKED FOR DELETION.       *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND                                  
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         DELETION-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ********************************************            
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ********************************************            
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ********************************************            
      *                 * ALL OTHER                                             
      *                 ********************************************            
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9010-OPEN-DELETION-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9010-OPEN-DELETION-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9011-FETCH-DELETION-CURSOR.                                              
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM THE CURSOR OF ALL MNEMONICS      *        
      *    MARKED FOR DELETION.                                        *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         DELETION-CURSOR                                        
                    INTO                                                        
                        :TMNEMONIC-MNEMONIC-CODE                                
                      , :TMNEMONIC-TRANS-ID-CODE                                
                      , :TMNEMONIC-AVAIL-INDCT                                  
                      , :TMNEMONIC-CMAREA-FRMT-CODE                             
                      , :TMNEMONIC-SAVE-CMAREA-INDCT                            
                      , :TMNEMONIC-CMAREA-LNGTH-MSRMT                           
                      , :TMNEMONIC-PANEL-TITLE-NAME                             
                      , :TMNEMONIC-LAST-UPDATE-TMESTP                           
                      , :TMNEMONIC-GLBL-ACCS-IND                                
                      , :TMNEMONIC-PGM-NAME                                     
                      , :TMNEMONIC-SCRN-TYP-CDE                                 
                      , :TMNEMONIC-UPD-VSAM-IND                                 
                      , :TMNEMONIC-DB2-PKG-CDE                                  
                      , :TMNEMONIC-ACRT-LOC-REQR-IND                            
                   END-EXEC                                                     
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9011-FETCH-DELETION-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9011-FETCH-DELETION-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9012-CLOSE-DELETION-CURSOR.                                              
      ******************************************************************        
      *    CLOSE CURSOR OF ALL MNEMONICS MARKED FOR DELETION.          *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         DELETION-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9012-CLOSE-DELETION-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9012-CLOSE-DELETION-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9020-OPEN-UPDATING-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE CURSOR OF ALL MNEMONICS MARKED FOR UPDATING.       *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN UPDATING-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9020-OPEN-UPDATING-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9020-OPEN-UPDATING-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9021-FETCH-UPDATING-CURSOR.                                              
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM THE CURSOR OF ALL MNEMONICS      *        
      *    MARKED FOR UPDATING.                                        *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         UPDATING-CURSOR                                        
                    INTO                                                        
                        :TMNEMONIC-MNEMONIC-CODE                                
                      , :TMNEMONIC-TRANS-ID-CODE                                
                      , :TMNEMONIC-AVAIL-INDCT                                  
                      , :TMNEMONIC-CMAREA-FRMT-CODE                             
                      , :TMNEMONIC-SAVE-CMAREA-INDCT                            
                      , :TMNEMONIC-CMAREA-LNGTH-MSRMT                           
                      , :TMNEMONIC-PANEL-TITLE-NAME                             
                      , :TMNEMONIC-LAST-UPDATE-TMESTP                           
                      , :TMNEMONIC-GLBL-ACCS-IND                                
                      , :TMNEMONIC-PGM-NAME                                     
                      , :TMNEMONIC-SCRN-TYP-CDE                                 
                      , :TMNEMONIC-UPD-VSAM-IND                                 
                      , :TMNEMONIC-DB2-PKG-CDE                                  
                      , :TMNEMONIC-ACRT-LOC-REQR-IND                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9021-FETCH-UPDATING-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9021-FETCH-UPDATING-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9022-CLOSE-UPDATING-CURSOR.                                              
      ******************************************************************        
      *    CLOSE CURSOR OF ALL MNEMONICS MARKED FOR UPDATING.          *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE UPDATING-CURSOR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9022-CLOSE-UPDATING-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9022-CLOSE-UPDATING-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9110-SELECT-TRANID-FROM-TMNEM.                                           
      ******************************************************************        
      *    READ THE MNEMONIC TABLE TO OBTAIN THE TRAN ID.              *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                         TRANS_ID_CODE                                          
                      ,  PANEL_TITLE_NAME                                       
                    INTO                                                        
                        :PV-TRANS-ID-CODE                                       
                      , :PV-DESCRIPTION                                         
                    FROM                                                        
                         TMNEMONIC                                              
                    WHERE                                                       
                         MNEMONIC_CODE = :TCICSROUTE-DEST-MNEMONIC-CODE         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9110-SELECT-TRANID-FROM-TMNEM'                    
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'SELECT FAILED FOR: '                              
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE TCICSROUTE-DEST-MNEMONIC-CODE                      
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9110-SELECT-TRANID-FROM-TMNEM'                             
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR TMNEMONIC SELECT'                 
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9120-UPDATE-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, FOR THE INFORMATION WHICH HAS   *        
      *    BEEN SUCCESSFULLY COPIED TO THE MNEMONIC VSAM FILE.         *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET  UPD_VSAM_IND       = :PC-N-NOT-NEEDED,                  
                        LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP                 
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMNEMONIC-MNEMONIC-CODE           
                        AND                                                     
                        LAST_UPDATE_TMESTP =                                    
                                          :TMNEMONIC-LAST-UPDATE-TMESTP)        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9120-UPDATE-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9120-UPDATE-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR UPDATE OF'                        
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE TMNEMONIC-MNEMONIC-CODE                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-MNEMONIC-JUST-UPDATED             
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET PS-TRANSACTION-AWRY                                        
                                       TO  TRUE                                 
                 INITIALIZE                DP013-MESSAGE-TABLE                  
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-LINK-RETURN TO  TRUE                                 
                 SET DP013-NO-ROLLBACK TO  TRUE                                 
                 MOVE '9120-UPDATE-MNEMONIC-ROW'                                
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE TMNEMONIC-LAST-UPDATE-TMESTP                              
                                       TO  PV-ABEND-TIMESTAMP                   
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP014-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9121-UPDATE-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, TO EFFECT A "SOFT" DELETE.      *        
      *    SET THE INDICATOR TO REFLECT THAT THIS MNEMONIC IS          *        
      *    UNAVAILABLE.                                                *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET  AVAIL_INDCT        = :PC-N-NOT-AVAILABLE,               
                        LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP                 
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMNEMONIC-MNEMONIC-CODE)          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9121-UPDATE-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9121-UPDATE-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC UPDATE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-MNEMONIC-JUST-UPDATED             
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET PS-TRANSACTION-AWRY                                        
                                       TO  TRUE                                 
                 INITIALIZE                DP013-MESSAGE-TABLE                  
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-LINK-RETURN TO  TRUE                                 
                 SET DP013-NO-ROLLBACK TO  TRUE                                 
                 MOVE '9121-UPDATE-MNEMONIC-ROW'                                
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TMNEMONIC-MNEMONIC-CODE                                   
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE TMNEMONIC-LAST-UPDATE-TMESTP                              
                                       TO  PV-ABEND-TIMESTAMP                   
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP014-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9122-UPDATE-MNEMONIC-ITEM-ROW.                                           
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, TO REQUEST AN UPDATE TO THE     *        
      *    MNEMONIC VSAM FILE FOR THIS ITEM;                           *        
      *    THIS ITEM WILL NO LONGER HAVE AS A DESTINATION THE MNEMONIC *        
      *    WHICH WE ARE NOW PERFORMING THE "HARD" DELETE ON.           *        
      *    (IE: CANNOT NAVIGATE FROM THIS ITEM, TO THIS DESTINATION    *        
      *    BECAUSE THIS DESTINATION NO LONGER EXISTS ON TCICSROUTE.)   *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                  SET  LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP,                 
                       UPD_VSAM_IND       = :PC-I-IMMEDIATE-UPDATE              
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TCICSROUTE-MNEMONIC-CODE)         
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9122-UPDATE-MNEMONIC-ITEM-ROW'                    
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9122-UPDATE-MNEMONIC-ITEM-ROW'                             
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC UPDATE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 MOVE TCICSROUTE-MNEMONIC-CODE                                  
                                       TO  PV-MNEMONIC-JUST-UPDATED             
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET PS-TRANSACTION-AWRY                                        
                                       TO  TRUE                                 
                 INITIALIZE                DP013-MESSAGE-TABLE                  
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-LINK-RETURN TO  TRUE                                 
                 SET DP013-NO-ROLLBACK TO  TRUE                                 
                 MOVE '9122-UPDATE-MNEMONIC-ITEM-ROW'                           
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TCICSROUTE-MNEMONIC-CODE                                  
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP014-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9142-UPDATE-MNEMONIC-MENU-ROW.                                           
      ******************************************************************        
      *    UPDATE THE TMNEMONIC TABLE, TO REQUEST AN UPDATE TO THE     *        
      *    MNEMONIC VSAM FILE FOR THIS MENU;                           *        
      *    THIS MENU WILL NO LONGER HAVE AS A LISTING THE MNEMONIC     *        
      *    WHICH WE ARE NOW PERFORMING THE "HARD" DELETE ON.           *        
      *    (IE: CANNOT OFFER THE OPTION OF THIS ITEM,                  *        
      *    BECAUSE THIS ITEM NO LONGER EXISTS.)                        *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
             IF PV-CURRENT-TMESTP-FOR-TRAN = PC-INIT-TMESTP-DEFAULT             
      *      ***********************************************************        
      *      * THIS IS THE FIRST UPDATE FOR THIS TRANSACTION;          *        
      *      * USE CURRENT TIMESTAMP                                   *        
      *      ***********************************************************        
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET LAST_UPDATE_TMESTP =  CURRENT TIMESTAMP,                 
                       UPD_VSAM_IND       = :PC-I-IMMEDIATE-UPDATE              
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMENUITEM-MENU-NAME)              
             END-EXEC                                                           
             ELSE                                                               
      *      ***********************************************************        
      *      * THIS IS *NOT* THE FIRST UPDATE FOR THIS TRANSACTION;    *        
      *      * USE TIMESTAMP FROM PREVIOUS UPDATE                      *        
      *      ***********************************************************        
             EXEC SQL                                                           
                 UPDATE TMNEMONIC                                               
                   SET LAST_UPDATE_TMESTP = :PV-CURRENT-TMESTP-FOR-TRAN,        
                       UPD_VSAM_IND       = :PC-I-IMMEDIATE-UPDATE              
                  WHERE                                                         
                       (MNEMONIC_CODE      = :TMENUITEM-MENU-NAME)              
             END-EXEC                                                           
             END-IF                                                             
                                                                                
             EVALUATE TRUE                                                      
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9142-UPDATE-MNEMONIC-MENU-ROW'                    
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'UPDATE FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        IF SQLCODE = -181                                       
                            MOVE PV-CURRENT-TMESTP-FOR-TRAN                     
                                     TO  DP013-MESSAGE-TEXT (3)                 
                        END-IF                                                  
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9142-UPDATE-MNEMONIC-MENU-ROW'                             
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC UPDATE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
             IF  SQLCODE = +0                                                   
                 MOVE TMENUITEM-MENU-NAME                                       
                                       TO  PV-MNEMONIC-JUST-UPDATED             
                 PERFORM 9130-SELECT-TMNEMONIC-TMESTP                           
             ELSE                                                               
                 SET PS-TRANSACTION-AWRY                                        
                                       TO  TRUE                                 
                 INITIALIZE                DP013-MESSAGE-TABLE                  
                 SET DP013-DB2-ABEND   TO  TRUE                                 
                 SET DP013-LINK-RETURN TO  TRUE                                 
                 SET DP013-NO-ROLLBACK TO  TRUE                                 
                 MOVE '9142-UPDATE-MNEMONIC-MENU-ROW'                           
                                       TO  DP013-PARAGRAPH                      
                 MOVE SQLCA            TO  DP013-SQLCA                          
                 MOVE 'TMNEMONIC         '                                      
                                       TO  DP013-DB2-TABLE-NAME (1)             
                 MOVE 'UPDATE FAILED FOR:'                                      
                                       TO  DP013-MESSAGE-TEXT (1)               
                 MOVE TMENUITEM-MENU-NAME                                       
                                       TO  PV-ABEND-MNEMONIC                    
                 MOVE PV-ABEND-INFO    TO  DP013-MESSAGE-TEXT (2)               
                 PERFORM DP014-0000-PROCESS-ABEND                               
             END-IF.                                                            
                                                                                
                                                                                
       9130-SELECT-TMNEMONIC-TMESTP.                                            
      ******************************************************************        
      *    SELECT THE UPDATED TMNEMONIC ROW, TO GET THE TMESTP.        *        
      *    THIS TIMESTAMP WILL BE USED AS A MEANS TO TIE TOGETHER:     *        
      *    1) OTHER RELATED DB2 TABLE UPDATES FOR THIS TRANSACTION.    *        
      *    2) THE RECORD WRITTEN TO THE VSAM DPCAUDIT LOG.             *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    SELECT LAST_UPDATE_TMESTP                                   
                    INTO   :TMNEMONIC-LAST-UPDATE-TMESTP                        
                    FROM   TMNEMONIC                                            
                    WHERE  MNEMONIC_CODE  =  :PV-MNEMONIC-JUST-UPDATED          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9130-SELECT-TMNEMONIC-TMESTP'                     
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'SELECT FAILED FOR MNEMONIC:'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = ZERO                                                   
               MOVE TMNEMONIC-LAST-UPDATE-TMESTP  TO                            
                                              PV-CURRENT-TMESTP-FOR-TRAN        
           END-IF.                                                              
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9130-SELECT-TMNEMONIC-TMESTP'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC SELECT'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9140-DELETE-MNEMONIC-ROW.                                                
      ******************************************************************        
      *    DELETE CURRENT ROW OF DELETION CURSOR, FROM TMNEMONIC TABLE.*        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    DELETE                                                      
                    FROM   TMNEMONIC                                            
      **            WHERE  CURRENT OF DELETION-CURSOR                           
      **            WHERE  MNEMONIC_CODE =  :TMNEMONIC-MNEMONIC-CODE            
                    WHERE  MNEMONIC_CODE =  :DP033-MNEMONIC                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9140-DELETE-MNEMONIC-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'DELETE FAILED'                                    
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9140-DELETE-MNEMONIC-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MNEMONIC DELETE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9200-OPEN-ROUTE-CURSOR.                                                  
      ******************************************************************        
      *    OPEN THE CICS ROUTE TABLE CURSOR.                           *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         ROUTE-CURSOR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9200-OPEN-ROUTE-CURSOR'                           
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9200-OPEN-ROUTE-CURSOR'                                    
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9210-FETCH-ROUTE-CURSOR.                                                 
      ******************************************************************        
      *    FETCH ONE ROW AT A TIME FROM THE ROUTE CURSOR               *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         ROUTE-CURSOR                                           
                    INTO                                                        
                        :TCICSROUTE-AID-SEQUENCE-NMBR                           
                      , :TCICSROUTE-DEST-MNEMONIC-CODE                          
                      , :TCICSROUTE-ACTION-CODE                                 
                      , :TCICSROUTE-ROUTE-STACK-INDCT                           
                      , :TCICSROUTE-FUNC-KEY-LBL-NAME                           
                      , :TCICSROUTE-IN-APLCTN-COMM-CDE                          
                      , :TCICSROUTE-FUNC-KY-DISPN-IND                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9210-FETCH-ROUTE-CURSOR'                          
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               MOVE '9210-FETCH-ROUTE-CURSOR'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9211-CLOSE-ROUTE-CURSOR.                                                 
      ******************************************************************        
      *    CLOSE CICS ROUTE TABLE CURSOR.                              *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         ROUTE-CURSOR                                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9211-CLOSE-ROUTE-CURSOR'                          
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9211-CLOSE-ROUTE-CURSOR'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9220-OPEN-ROUTE-DEST-CURSOR.                                             
      ******************************************************************        
      *    OPEN THE CICS ROUTE TABLE CURSOR, TO SELECT ROWS BY         *        
      *    DESTINATION.                                                *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         ROUTE-DEST-CURSOR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9220-OPEN-ROUTE-DEST-CURSOR'                      
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9220-OPEN-ROUTE-DEST-CURSOR'                               
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9230-FETCH-ROUTE-DEST-CURSOR.                                            
      ******************************************************************        
      *    FETCH ONE ROW AT A TIME FROM THE ROUTE DESTINATION CURSOR   *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         ROUTE-DEST-CURSOR                                      
                    INTO                                                        
                        :TCICSROUTE-MNEMONIC-CODE                               
                      , :TCICSROUTE-AID-SEQUENCE-NMBR                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9230-FETCH-ROUTE-DEST-CURSOR'                     
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9230-FETCH-ROUTE-DEST-CURSOR'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9231-CLOSE-ROUTE-DEST-CURSOR.                                            
      ******************************************************************        
      *    CLOSE CICS ROUTE TABLE CURSOR.  (ROWS SELECTED BY DEST)     *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         ROUTE-DEST-CURSOR                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9231-CLOSE-ROUTE-DEST-CURSOR'                     
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9231-CLOSE-ROUTE-DEST-CURSOR'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9240-DELETE-ROUTE-DEST-ROW.                                              
      ******************************************************************        
      *    DELETE CURRENT ROW OF ROUTE DEST CURSOR.                    *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    DELETE                                                      
                    FROM   TCICSROUTE                                           
                    WHERE  CURRENT OF ROUTE-DEST-CURSOR                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9240-DELETE-ROUTE-DEST-ROW'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'DELETE FAILED'                                    
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9240-DELETE-ROUTE-DEST-ROW'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR DELETE'                    
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9245-DELETE-ROUTE-GENERIC.                                               
      ******************************************************************        
      *    DELETE ALL ROWS FOR THIS MNEMONIC.                          *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    DELETE                                                      
                    FROM   TCICSROUTE                                           
                    WHERE  MNEMONIC_CODE   =  :DP033-MNEMONIC                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9245-DELETE-ROUTE-GENERIC'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'DELETE FAILED... FOR ALL ROWS FOR:'               
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE DP033-MNEMONIC                                     
                                     TO  DP013-MESSAGE-TEXT (2)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9245-DELETE-ROUTE-GENERIC'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR DELETE'                           
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9300-OPEN-MENUITEM-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE MENU ITEM CURSOR FOR PROCESSING.                   *        
      *    (CONTAINS COLUMNS FROM MENUITEM AND TMNEMONIC)              *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN  MENUITEM-CURSOR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9300-OPEN-MENUITEM-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9300-OPEN-MENUITEM-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9310-FETCH-MENUITEM-CURSOR.                                              
      ******************************************************************        
      *    READ THE MENUITEM/MNEMONIC TABLES TO OBTAIN THE COLUMNS     *        
      *    THAT MAY HAVE BEEN UPDATED.                                 *        
      ******************************************************************        
           INITIALIZE DP504-MENU-ITEM-RECORD.                                   
                                                                                
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH MENUITEM-CURSOR                                       
                    INTO                                                        
                         :DP504-MENU-NAME                                       
                       , :DP504-MENU-ITEM-ORDER-NUMBER                          
                       , :DP504-MENU-ITEM-MNEMONIC                              
                       , :DP504-MENU-ITEM-DESCRIPTION:PV-NULL-IND               
                       , :DP504-INT-APPL-FORMAT-IND                             
                       , :DP504-MENU-ITEM-TRAN-ID                               
                       , :DP504-MENU-ITEM-AVAIL-IND                             
                       , :DP504-ACRT-LOC-REQR-IND                               
                       , :DP504-MENU-ITEM-COMMAREA-FRMT                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9310-FETCH-MENUITEM-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9310-FETCH-MENUITEM-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR TMENUITEM SELECT'                 
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               MOVE DP504-MENU-ITEM-MNEMONIC                                    
                                     TO  TCICSROUTE-DEST-MNEMONIC-CODE          
               PERFORM 9110-SELECT-TRANID-FROM-TMNEM                            
               MOVE PV-TRANS-ID-CODE TO  DP504-MENU-ITEM-TRAN-ID                
                                                                                
               IF  DP504-MENU-ITEM-DESCRIPTION GREATER THAN SPACES              
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE PV-DESCRIPTION                                          
                                     TO  DP504-MENU-ITEM-DESCRIPTION            
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       9311-CLOSE-MENUITEM-CURSOR.                                              
      ******************************************************************        
      *    CLOSE MENU ITEM CURSOR FOR PROCESSING.                      *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE MENUITEM-CURSOR                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9311-CLOSE-MENUITEM-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9311-CLOSE-MENUITEM-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9320-OPEN-MENUNAME-CURSOR.                                               
      ******************************************************************        
      *    OPEN THE MENU NAME CURSOR.                                  *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    OPEN                                                        
                         MENUNAME-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9320-OPEN-MENUNAME-CURSOR'                        
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN FAILED'                                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9320-OPEN-MENUNAME-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR OPEN'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9330-FETCH-MENUNAME-CURSOR.                                              
      ******************************************************************        
      *    FETCH ONE ROW AT A TIME FROM THE MENUNAME CURSOR.           *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    FETCH                                                       
                         MENUNAME-CURSOR                                        
                    INTO                                                        
                        :TMENUITEM-MENU-NAME                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9330-FETCH-MENUNAME-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9330-FETCH-MENUNAME-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR FETCH'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9331-CLOSE-MENUNAME-CURSOR.                                              
      ******************************************************************        
      *    CLOSE MENU NAME CURSOR.                                     *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    CLOSE                                                       
                         MENUNAME-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9331-CLOSE-MENUNAME-CURSOR'                       
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'CLOSE FAILED'                                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9331-CLOSE-MENUNAME-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR CURSOR CLOSE'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9340-DELETE-MENUNAME-ROW.                                                
      ******************************************************************        
      *    DELETE CURRENT ROW OF MENUNAME CURSOR.                      *        
      ******************************************************************        
           MOVE SPACES TO DP013-ABEND-TYPE-IND.                                 
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE EQUAL ZERO                                         
                  OR SQLCODE EQUAL +100                                         
                  OR DP013-DB2-ABEND                                            
                                                                                
               EXEC SQL                                                         
                    DELETE                                                      
                    FROM   TMENUITEM                                            
                    WHERE  CURRENT OF MENUNAME-CURSOR                           
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
      *                 ************************************************        
      *                 *    0 SUCCESSFUL                                       
      *                 * +100 NOT FOUND                                        
      *                 * -904 UNAVAILABLE RESOURCE                             
      *                 * -913 DEADLOCK OR TIMEOUT                              
      *                 ************************************************        
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
      *                 ************************************************        
      *                 * ALL OTHER                                             
      *                 ************************************************        
                        SET PS-TRANSACTION-AWRY                                 
                                     TO  TRUE                                   
                        INITIALIZE       DP013-MESSAGE-TABLE                    
                        MOVE '9340-DELETE-MENUNAME-ROW'                         
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'DELETE FAILED'                                    
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-LINK-RETURN                                   
                                     TO  TRUE                                   
                        SET DP013-NO-ROLLBACK                                   
                                     TO  TRUE                                   
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP014-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               MOVE '9340-DELETE-MENUNAME-ROW'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR MENUNAME DELETE'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9400-READ-MNEMONIC-VSAM.                                                 
      ******************************************************************        
      *    READ THE MNEMONIC VSAM FILE.                                *        
      ******************************************************************        
           INITIALIZE PS-VSAM-MNEMONIC-FOUND-SW.                                
                                                                                
           MOVE DP033-MNEMONIC       TO  DP503-MNEMONIC.                        
                                                                                
           EXEC CICS READ                                                       
                INTO  (DP503-MNEMONIC-RECORD)                                   
                FILE  (PC-MNEMONIC-FILE)                                        
                RIDFLD(DP503-MNEMONIC)                                          
                RESP  (PV-RESP)                                                 
                UPDATE                                                          
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-FOUND TO TRUE                               
           ELSE                                                                 
               IF  PV-RESP EQUAL DFHRESP(NOTFND)                                
                   SET PS-VSAM-MNEMONIC-NOT-FOUND TO TRUE                       
               ELSE                                                             
                   SET PS-TRANSACTION-AWRY                                      
                                         TO  TRUE                               
                   INITIALIZE                DP013-MESSAGE-TABLE                
                   SET DP013-CICS-ABEND  TO  TRUE                               
                   SET DP013-LINK-RETURN TO  TRUE                               
                   SET DP013-NO-ROLLBACK TO  TRUE                               
                   MOVE '9400-READ-MNEMONIC-VSAM'                               
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM READ FOR:'          
                                         TO  DP013-MESSAGE-TEXT (1)             
                   MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)             
                   PERFORM DP014-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       9410-REWRITE-MNEMONIC-VSAM.                                              
      ******************************************************************        
      *    REWRITE THE UPDATED MNEMONIC RECORD.                        *        
      ******************************************************************        
           EXEC CICS REWRITE                                                    
                FROM(DP503-MNEMONIC-RECORD)                                     
                FILE(PC-MNEMONIC-FILE)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-UPDT-OK TO TRUE                             
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               SET PS-VSAM-MNEMONIC-UPDT-ERR                                    
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9410-REWRITE-MNEMONIC-VSAM'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM REWRITE FOR:'           
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9420-WRITE-MNEMONIC-VSAM.                                                
      ******************************************************************        
      *    WRITE THE NEW MNEMONIC RECORD.                              *        
      ******************************************************************        
           MOVE DP033-MNEMONIC       TO  DP503-MNEMONIC.                        
                                                                                
           EXEC CICS WRITE                                                      
                FROM(DP503-MNEMONIC-RECORD)                                     
                FILE(PC-MNEMONIC-FILE)                                          
                RIDFLD(DP503-MNEMONIC)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               SET PS-VSAM-MNEMONIC-UPDT-OK  TO TRUE                            
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               SET PS-VSAM-MNEMONIC-UPDT-ERR TO TRUE                            
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9420-WRITE-MNEMONIC-VSAM'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM WRITE FOR:'             
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9440-DELETE-MNEMONIC-VSAM.                                               
      ******************************************************************        
      *    DELETE THE MNEMONIC RECORD FROM THE VSAM FILE.              *        
      *    NOTE THAT IN ORDER TO AVOID DEADLOCKS, VSAM FILE RECORDS    *        
      *    ARE SUGGESTED TO BE DELETED IN ASCENDING ORDER: THEREFORE   *        
      *    THE "DELETION" CURSOR SELECTS ROWS FROM THE TMNEMONIC DB2   *        
      *    TABLE TO BE ORDERED BY THE FIELDS WHICH FORM THE KEY TO THE *        
      *    MNEMONIC VSAM FILE.                                         *        
      ******************************************************************        
           MOVE DP033-MNEMONIC       TO  DP503-MNEMONIC.                        
                                                                                
           EXEC CICS DELETE                                                     
                FILE  (PC-MNEMONIC-FILE)                                        
                RIDFLD(DP503-MNEMONIC)                                          
                RESP  (PV-RESP)                                                 
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
           OR PV-RESP EQUAL DFHRESP(NOTFND)                                     
               SET PS-VSAM-MNEMONIC-DLET-OK  TO TRUE                            
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               SET PS-VSAM-MNEMONIC-DLET-ERR                                    
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9440-DELETE-MNEMONIC-VSAM'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MNEMONIC VSAM WRITE FOR:'             
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9500-READ-MENUITEM-VSAM.                                                 
      ******************************************************************        
      *    PERFORM A FULLKEY READ ON THE VSAM MENUITEM FILE.           *        
      *    READ EACH MENUITEM RECORD, LISTED ON THE MENU               *        
      *    BEING PROCESSED.                                            *        
      ******************************************************************        
           EXEC CICS READ                                                       
                INTO     (DP504-MENU-ITEM-RECORD)                               
                FILE     (PC-MENUITEM-FILE)                                     
                GTEQ                                                            
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                RESP     (PV-RESP)                                              
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9500-READ-MENUITEM-VSAM'                                   
                                     TO  DP013-PARAGRAPH                        
               MOVE                                                             
               'BAD RETURN CODE ON READ OF MENUITEM VSAM FOR:'                  
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9501-READ-MENUITEM-VSAM-UPDT.                                            
      ******************************************************************        
      *    PERFORM A FULLKEY READ ON THE VSAM MENUITEM FILE.           *        
      *    READ A SPECIFIC MENU RECORD, TO WHICH THIS MENUITEM BELONGS.*        
      *    THIS RECORD IS HELD FOR EXCLUSIVE USE BY THE "READ... UPDATE"        
      *    AND AN "UNLOCK" COMMAND WILL BE ISSUED LATER.               *        
      ******************************************************************        
           EXEC CICS READ                                                       
                INTO     (DP504-MENU-ITEM-RECORD)                               
                FILE     (PC-MENUITEM-FILE)                                     
                GTEQ                                                            
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                RESP     (PV-RESP)                                              
                UPDATE                                                          
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9501-READ-MENUITEM-VSAM-UPDT'                              
                                     TO  DP013-PARAGRAPH                        
               MOVE                                                             
               'BAD RETURN CODE READING MENUITEM VSAM FOR:'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9510-DELETE-MENUITEM-VSAM-GNRC.                                          
      ******************************************************************        
      *    DELETE THE EXISTING MENU ITEM VSAM RECORDS.                 *        
      *    DELETE ALL RECORDS FROM THE MENUITEM VSAM FILE FOR THIS     *        
      *    MNEMONIC... PARTIAL KEY, THE 1ST 8 BYTES, ARE USED.         *        
      ******************************************************************        
           MOVE DP033-MNEMONIC       TO  DP504-MENU-NAME.                       
           MOVE ZERO                 TO  DP504-MENU-ITEM-ORDER-NUMBER.          
                                                                                
           EXEC CICS DELETE                                                     
                FILE     (PC-MENUITEM-FILE)                                     
                RIDFLD   (DP504-MENU-ITEM-KEY)                                  
                KEYLENGTH(8)                                                    
                GENERIC                                                         
                RESP     (PV-RESP)                                              
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP EQUAL DFHRESP(NORMAL)                                    
           OR  PV-RESP EQUAL DFHRESP(NOTFND))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9510-DELETE-MENUITEM-VSAM-GNRC'                            
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MENUITEM VSAM DELETE'                 
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9520-WRITE-MENUITEM-VSAM.                                                
      ******************************************************************        
      *    WRITE THE UPDATED MENUITEM VSAM RECORD.                     *        
      ******************************************************************        
           EXEC CICS WRITE                                                      
                FROM  (DP504-MENU-ITEM-RECORD)                                  
                RIDFLD(DP504-MENU-ITEM-KEY)                                     
                FILE  (PC-MENUITEM-FILE)                                        
                RESP  (PV-RESP)                                                 
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9520-WRITE-MENUITEM-VSAM'                                  
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM MENUITEM VSAM WRITE FOR:'             
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-KEY                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9530-UPDATE-MENUITEM-VSAM.                                               
      ******************************************************************        
      *    UPDATE THE MENUITEM VSAM DATASET.                           *        
      ******************************************************************        
           EXEC CICS REWRITE                                                    
                FROM(DP504-MENU-ITEM-RECORD)                                    
                FILE(PC-MENUITEM-FILE)                                          
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9530-UPDATE-MENUITEM-VSAM'                                 
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE REWRITING MENUITEM VSAM FOR:'              
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP504-MENU-ITEM-MNEMONIC                                    
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9540-UNLOCK-MENUITEM-VSAM.                                               
      ******************************************************************        
      *    UNLOCK MENUITEM VSAM FILE.                                  *        
      *    RELEASE THE RECORD HELD EXCLUSIVELY BY THE "READ... UPDATE".*        
      ******************************************************************        
           EXEC CICS UNLOCK                                                     
                FILE(PC-MENUITEM-FILE)                                          
           END-EXEC.                                                            
                                                                                
                                                                                
       9600-WRITE-AUDIT-TRAIL-LOG.                                              
      ******************************************************************        
      *    WRITE A RECORD TO THE CICSARCH AUDIT TRAIL LOG: DPCAUDIT.   *        
      *    THIS IS AN ESDS VSAM FILE WITH VARIABLE LENGTH RECORDS.     *        
      ******************************************************************        
           EXEC CICS WRITE                                                      
                FROM   (DP520-DPCAUDIT-RECORD)                                  
                FILE   (PC-DPCAUDIT-FILE)                                       
                LENGTH (PV-DPCAUDIT-REC-LEN)                                    
                RIDFLD (PV-DPCAUDIT-RBA)                                        
                RBA                                                             
                RESP   (PV-RESP)                                                
           END-EXEC.                                                            
                                                                                
           IF PV-RESP EQUAL DFHRESP(NORMAL)                                     
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRANSACTION-AWRY                                          
                                     TO  TRUE                                   
               INITIALIZE                DP013-MESSAGE-TABLE                    
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9600-WRITE-AUDIT-TRAIL-LOG'                                
                                     TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE WRITING CICSARCH AUDIT LOG FOR:'           
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DP033-MNEMONIC   TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9800-ENQUEUE.                                                            
      ******************************************************************        
      * ENQUEUE THE CICS LOG                                                    
      ******************************************************************        
           EXEC CICS                                                            
               ENQ                                                              
                   RESOURCE (PC-LOG-QUEUE-RESOURCE)                             
           END-EXEC.                                                            
                                                                                
                                                                                
       9810-WRITEQ-TD.                                                          
      ******************************************************************        
      * WRITE TO THE CICS LOG                                                   
      ******************************************************************        
           EXEC CICS                                                            
               WRITEQ TD                                                        
                   QUEUE (PC-LOG-QUEUE)                                         
                   FROM  (PV-LOG-LINE)                                          
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               INITIALIZE                         DP013-MESSAGE-TABLE           
               SET DP013-CICS-ABEND           TO  TRUE                          
               SET DP013-LINK-RETURN          TO  TRUE                          
               SET DP013-NO-ROLLBACK          TO  TRUE                          
               MOVE '9810-WRITEQ-TD'                                            
                                              TO  DP013-PARAGRAPH               
               MOVE 'BAD RETURN CODE FROM WRITE TO CSSL'                        
                                              TO  DP013-MESSAGE-TEXT (1)        
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       9820-DEQUEUE.                                                            
      ******************************************************************        
      * DEQUEUE THE CICS LOG                                                    
      ******************************************************************        
           EXEC CICS                                                            
               DEQ                                                              
                   RESOURCE (PC-LOG-QUEUE-RESOURCE)                             
           END-EXEC.                                                            
                                                                                
                                                                                
       9900-SEND-TEXT.                                                          
      ****************************************************************          
      * SEND TEXT TO TERMINAL WHERE TRANSACTION INITIATED            *          
      ****************************************************************          
           EXEC CICS                                                            
                SEND TEXT                                                       
                FROM   (PM-PROGRAM-MESSAGE-SCOREBOARD)                          
                LENGTH (PM-SCOREBOARD-LENGTH)                                   
                RESP   (PV-RESP)                                                
           END-EXEC.                                                            
           IF (PV-RESP EQUAL DFHRESP(NORMAL))                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-LINK-RETURN TO TRUE                                    
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '9900-SEND-TEXT' TO  DP013-PARAGRAPH                        
               MOVE 'BAD RETURN CODE FROM SEND TEXT FOR SCOREBOARD:'            
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE PM-PROGRAM-MESSAGE-SCOREBOARD                               
                                     TO  DP013-MESSAGE-TEXT (2)                 
               PERFORM DP014-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    ABEND PROCESSOR MODULE - FOR NON-ARCHITECTURE PROGRAMS      *        
      ******************************************************************        
           COPY DPPD014.                                                        
