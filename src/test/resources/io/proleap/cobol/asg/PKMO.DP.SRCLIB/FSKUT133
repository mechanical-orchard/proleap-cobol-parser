       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              FSKUT133.                                       
       AUTHOR.                  BOB FORD.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/01/04.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      ** IF ANY CHANGES ARE MADE TO FSRD122 MAKE THE SAME CHANGES TO  **        
      ** PS-CORP-DATA                                                 **        
      ******************************************************************        
      *             TSKST/TUPC/SKU-LOC DOMAIN MERGE TO TSKSTOO        *         
      *             STYLE SUMMARY EXTRACT                              *        
      *                                                                *        
      *   THIS PROGRAM READS A MERGED INPUT FILE OF TSKST ITEM/LOCS    *        
      *   THAT WERE MERGED WITH TUPC AND SKU-LOC DOMAIN DATA.  THE FILE*        
      *   IS MATCHED TO AN UNLOAD OF TSKSTOO DATA SORTED BY ITEM, LOC  *        
      *   OTB-PER-NBR.  FOR EACH MATCH A RECORD IS WRITTEN WITH THE    *        
      *   MIN OTB-PER-NBR AND THE TOTAL ON-ORDER FOR THE ITEM/LOC.     *        
      ******************************************************************        
      *  CHANGE LOG:                                                            
      *  03/03/05 - BOB FORD                                                    
      *             (WR #26357) NEW PROGRAM - CLONED PROGRAM FSKUT132           
RK0706*  07/20/06 - RICH KELLEN                                                 
RK0706*             (WR #30352) CHANGE PROGRAM TO SET TO 'N' THE FIELD          
RK0706*             FS123-FOUND-ON-TSKST-IND WHEN THE LOCATION EXISTS           
RK0706*             ON THE TSKSTOO-UNLD-FILE BUT NOT ON THE                     
RK0706*             TSKST-MERGED-FILE. ALSO, FIX PROBLEM WHERE AN OUTPUT        
RK0706*             RECORD WAS NOT BEING CREATED WHEN THE UNLOAD FILE           
RK0706*             REACHES EOF AND THE FOLLOWING CONDITION IS NOT TRUE         
RK0706*             (PS-124-LOC-GT-PREV AND PS-124-ITEM-GT-PREV AND             
RK0706*             (FS122-LOC-NBR NOT = PV-124-LOC-NBR).                       
RK0706*                                                                         
RK0906*  09/06/06 - RICH KELLEN                                                 
RK0906*             (WR #30352) CHANGE INPUT LENGTH TO 126 AND THE              
RK0906*             OUTPUT FILE TO 136 TO USE LESS SPACE WHEN SORTING.          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TSKST-MERGED-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT TSKSTOO-UNLD-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ITEM-SUMMARY-FILE-OUT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TSKST-MERGED-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
RK0906     RECORD CONTAINS 126 CHARACTERS.                                      
RK0906 01  TSKST-MERGED-REC                PIC  X(126).                         
                                                                                
       FD  TSKSTOO-UNLD-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS.                                       
       01  TSKST-UNLD-REC                  PIC  X(20).                          
                                                                                
       FD  ITEM-SUMMARY-FILE-OUT                                                
           RECORDING MODE IS F                                                  
RK0906     RECORD CONTAINS 136 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS ITEM-SUMMARY-REC.                                     
RK0906 01  ITEM-SUMMARY-REC               PIC X(136).                           
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-Y                    PIC X(1)        VALUE 'Y'.               
RK0706     05  PC-N                    PIC X(1)        VALUE 'N'.               
           05  PC-PROGRAM-NAME         PIC X(8)        VALUE 'FSKUT133'.        
           05  PC-20                   PIC X(2)        VALUE '20'.              
           05  PC-25                   PIC X(2)        VALUE '25'.              
           05  PC-30                   PIC X(2)        VALUE '30'.              
                                                                                
       01  PS-CORP-DATA.                                                        
           05  PS-ITEM-NMBR                PIC  S9(15)V COMP-3.         00005103
           05  PS-INTR-UPC-ID              PIC  S9(9) COMP.             00006001
           05  PS-LOC-NBR                  PIC  S9(4) COMP.             00007701
           05  PS-UPC-STAT-CDE             PIC   X(2).                  00007801
           05  PS-UNT-COST-AMT             PIC  S9(7)V9(3) COMP-3.      00007901
           05  PS-UNT-RTL-AMT              PIC  S9(7)V9(2) COMP-3.      00008001
           05  PS-MEITGP-NBR               PIC  S9(9) COMP.             00008101
           05  PS-STAT-CHG-DTE             PIC   X(10).                 00008201
           05  PS-SUB-CL-MDSEAR-ID         PIC  S9(6)V COMP-3.          00008301
           05  PS-VS-ID                    PIC  S9(9) COMP.             00008401
           05  PS-KOHLS-SIZE-CDE           PIC  S9(9) COMP.             00008601
           05  PS-CSTM-CLOR-DESC           PIC   X(10).                 00008701
           05  PS-GP-CDE                   PIC   X(3).                  00008801
           05  PS-RPLNMT-IND               PIC   X(1).                  00008901
           05  PS-KY-ITM-IND               PIC   X(1).                  00009001
           05  PS-SEA-SEQ-NBR              PIC  S9(4) COMP.             00009101
           05  PS-REGN-PRIC-IND            PIC   X(1).                  00009201
           05  PS-INTR-UPC-DESC            PIC   X(30).                 00010001
           05  PS-LAST-SALE-DATE           PIC  S9(7)V COMP-3.          00090001
           05  PS-LAST-RCVD-DATE           PIC  S9(7)V COMP-3.          00100001
           05  PS-ONHAND-QTY               PIC  S9(7)V COMP-3.          00110001
           05  PS-FIRST-RCVD-DATE          PIC  S9(7)V COMP-3.          00120001
           05  PS-FIRST-SALE-DATE          PIC  S9(7)V COMP-3.          00130001
           05  PS-YTD-SALES-UNIT-QTY       PIC  S9(7)V COMP-3.          00140001
           05  PS-FOUND-ON-TSKST-IND       PIC   X(1).                  00141002
           05  PS-STORE-NMBR               PIC   X(3).                  00150001
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
           05  PV-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.            
           05  PV-MIN-OTB              PIC X(06)       VALUE SPACES.            
           05  PV-MIN-OTB-FOR-LOC      PIC X(06)       VALUE SPACES.            
           05  PV-TOT-ONORD            PIC S9(9)       VALUE +0 COMP.           
           05  PV-ONORD-FOR-LOC        PIC S9(9)       VALUE +0 COMP.           
           05  PV-TOT-ONHAND           PIC S9(9)       VALUE +0 COMP.           
           05  PV-ONHAND-FOR-LOC       PIC S9(9)       VALUE +0 COMP.           
           05  PV-122-LOC-NBR          PIC S9(5)       VALUE +0 COMP.           
           05  PV-122-ITEM-NMBR        PIC S9(15)V     VALUE ZERO               
                                                       COMP-3.                  
           05  PV-124-LOC-NBR          PIC S9(5)       VALUE +0 COMP.           
           05  PV-124-ITEM-NMBR        PIC S9(15)V     VALUE ZERO               
                                                       COMP-3.                  
           05  PV-TSKST-READ-CNT       PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-TSKSTOO-READ-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
RK0706     05  PV-TSKSTOO-ONLY-CNT     PIC 9(9)       VALUE ZERO                
RK0706                                                 COMP-3.                  
           05  PV-RECS-WRITTEN-CNT     PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
           05  PV-OUTPUT-MAX           PIC S9(7)      VALUE +5000000            
                                                       COMP.                    
           05  PV-OUTPUT-CNT           PIC 9(9)       VALUE ZERO                
                                                       COMP-3.                  
                                                                                
       01  PS-SWITCHES.                                                         
           05  PS-REC-PROCESSED-SW        PIC X       VALUE 'N'.                
               88  PS-REC-PROCESSED                    VALUE 'Y'.               
               88  PS-REC-NOT-PROCESSED                VALUE 'N'.               
           05  PS-TSKST-UNLD-EOF-SW        PIC X       VALUE 'N'.               
               88  PS-TSKST-UNLD-EOF                   VALUE 'Y'.               
           05  PS-TSKSTOO-UNLD-EOF-SW      PIC X       VALUE 'N'.               
               88  PS-TSKSTOO-UNLD-EOF                 VALUE 'Y'.               
           05  PS-FIRST-LOC-SW             PIC X       VALUE 'N'.               
               88  PS-FIRST-LOC-FOR-ITEM               VALUE 'Y'.               
               88  PS-NOT-FIRST-LOC-FOR-ITEM           VALUE 'N'.               
                                                                                
      ** FOR THESE SWITCHES THE 122/124 SPECIFIES WHICH INPUT FILE,             
      ** THE ITEM/LOC SPECIFIES WHICH FIELD, THE EQ/GT/LT IS THE                
      ** RELATIONAL OP, AND THE 'PREV' MEANS CURRENT RECORD COMPARED TO         
      ** THE PREVIOUS RECORD ON THAT FILE.                                      
      ** AFTER EACH FILE READ THE SWITCHES ARE RESET.                           
           05  PS-122-ITEM-124-ITEM-SW     PIC X       VALUE '0'.               
               88  PS-122-ITEM-EQ-124-ITEM             VALUE '1'.               
               88  PS-122-ITEM-GT-124-ITEM             VALUE '2'.               
               88  PS-122-ITEM-LT-124-ITEM             VALUE '3'.               
           05  PS-122-LOC-124-LOC-SW       PIC X       VALUE '0'.               
               88  PS-122-LOC-EQ-124-LOC               VALUE '1'.               
               88  PS-122-LOC-GT-124-LOC               VALUE '2'.               
               88  PS-122-LOC-LT-124-LOC               VALUE '3'.               
           05  PS-122-ITEM-PREV-SW         PIC X       VALUE '0'.               
               88  PS-122-ITEM-EQ-PREV                 VALUE '1'.               
               88  PS-122-ITEM-GT-PREV                 VALUE '2'.               
           05  PS-122-LOC-PREV-SW          PIC X       VALUE '0'.               
               88  PS-122-LOC-EQ-PREV                  VALUE '1'.               
               88  PS-122-LOC-GT-PREV                  VALUE '2'.               
           05  PS-124-ITEM-PREV-SW         PIC X       VALUE '0'.               
               88  PS-124-ITEM-EQ-PREV                 VALUE '1'.               
               88  PS-124-ITEM-GT-PREV                 VALUE '2'.               
           05  PS-124-LOC-PREV-SW          PIC X       VALUE '0'.               
               88  PS-124-LOC-EQ-PREV                  VALUE '1'.               
               88  PS-124-LOC-GT-PREV                  VALUE '2'.               
                                                                                
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +4003          
                                                           COMP SYNC.           
      ******************************************************************        
      *  COPY MEMBER FOR THE TSKSTOO UNLOAD FILE                                
      ******************************************************************        
           COPY FSRD124.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE MERGED INPUT FILE                                  
      ******************************************************************        
           COPY FSRD122.                                                        
                                                                                
      ******************************************************************        
      *  COPY MEMBER FOR THE MERGED OUTPUT FILE                                 
      ******************************************************************        
           COPY FSRD123.                                                        
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF FSKUT133               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1000-HOUSEKEEPING.                                           
                                                                                
           PERFORM 2000-READ-TSKST-MERGED-FILE.                                 
           IF PS-TSKST-UNLD-EOF                                                 
              DISPLAY '***********************************************'         
              DISPLAY '*** NO REGIONAL CLEARANCE PRICED SKUS FOUND ***'         
              DISPLAY '***********************************************'         
           ELSE                                                                 
               PERFORM 2100-READ-TSKSTOO-UNLD-FILE                              
               PERFORM 2200-PROCESS-TSKST-MERGE                                 
                   UNTIL PS-TSKST-UNLD-EOF                                      
               IF PV-122-ITEM-NMBR = FS124-ITEM-NMBR AND                        
                  FS124-LOC-NBR > PV-122-LOC-NBR                                
                   PERFORM 3400-PROCESS-124                                     
      *        ELSE                                                             
      *            PERFORM 4500-ITEM-LOC-BREAK                                  
               END-IF                                                           
               PERFORM 4000-ITEM-BREAK                                          
           END-IF.                                                              
                                                                                
           PERFORM 1900-EOJ.                                                    
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* FSKUT133 PROGRAM STATISTICS:    '.                        
           DISPLAY '**********************************'.                        
RK0706     DISPLAY '* TOTAL TSKST/ITEM RECS READ: ' PV-TSKST-READ-CNT.          
RK0706     DISPLAY '* TOTAL ON-ORDER RECS READ: ' PV-TSKSTOO-READ-CNT.          
RK0706     DISPLAY '* TOTAL ON-ORDER NOT ON TSKST: '                            
RK0706                                   PV-TSKSTOO-ONLY-CNT.                   
           DISPLAY '* TOTAL OUTPUT RECORDS: '                                   
                                         PV-RECS-WRITTEN-CNT.                   
           DISPLAY '**********************************'.                        
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF FSKUT133                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
           STOP RUN.                                                            
      ******************************************************************        
      *    OPEN FILES & DETERMINE CURRENT DATE CCYY-MM-DD.                      
      ******************************************************************        
       1000-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  TSKST-MERGED-FILE                                        
                       TSKSTOO-UNLD-FILE                                        
                OUTPUT ITEM-SUMMARY-FILE-OUT.                                   
                                                                                
           INITIALIZE PV-MIN-OTB                                                
                      PV-MIN-OTB-FOR-LOC                                        
                      PV-122-ITEM-NMBR                                          
                      PS-ITEM-NMBR                                              
                      PV-124-ITEM-NMBR                                          
                      PV-122-LOC-NBR                                            
                      PV-124-LOC-NBR                                            
                      PV-ONORD-FOR-LOC                                          
                      PV-TOT-ONORD                                              
                      PV-ONHAND-FOR-LOC                                         
                      PV-TOT-ONHAND.                                            
           INITIALIZE FS122-FS-MERGED-RECORD                                    
                      FS123-FS-MERGED-RECORD                                    
                      FS124-TSKSTOO-RECORD.                                     
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       1900-EOJ.                                                                
                                                                                
           CLOSE TSKST-MERGED-FILE                                              
                 TSKSTOO-UNLD-FILE                                              
                 ITEM-SUMMARY-FILE-OUT.                                         
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE TSKST-MERGED-FILE.                          
      ******************************************************************        
       2000-READ-TSKST-MERGED-FILE.                                             
                                                                                
           MOVE FS122-ITEM-NMBR TO PV-122-ITEM-NMBR                             
           MOVE FS122-LOC-NBR   TO PV-122-LOC-NBR.                              
           READ TSKST-MERGED-FILE                                               
               INTO FS122-FS-MERGED-RECORD                                      
               AT END                                                           
                   SET PS-TSKST-UNLD-EOF TO TRUE                                
               NOT AT END                                                       
                   ADD +1 TO PV-TSKST-READ-CNT                                  
                   PERFORM 2150-SET-SWITCHES.                                   
                                                                                
      ******************************************************************        
      * READ THE NEXT RECORD ON THE TSKSTOO-UNLD-FILE.                          
      ******************************************************************        
       2100-READ-TSKSTOO-UNLD-FILE.                                             
                                                                                
           MOVE FS124-ITEM-NMBR TO PV-124-ITEM-NMBR                             
           MOVE FS124-LOC-NBR   TO PV-124-LOC-NBR.                              
           READ TSKSTOO-UNLD-FILE                                               
               INTO FS124-TSKSTOO-RECORD                                        
               AT END                                                           
                   SET PS-TSKSTOO-UNLD-EOF TO TRUE                              
               NOT AT END                                                       
                   PERFORM 2150-SET-SWITCHES                                    
                   ADD +1 TO PV-TSKSTOO-READ-CNT.                               
                                                                                
      ******************************************************************        
      *    SET SWITCHES                                                         
      ******************************************************************        
       2150-SET-SWITCHES.                                                       
                                                                                
           IF FS122-ITEM-NMBR = FS124-ITEM-NMBR                                 
               SET PS-122-ITEM-EQ-124-ITEM TO TRUE                              
           ELSE                                                                 
               IF FS122-ITEM-NMBR > FS124-ITEM-NMBR                             
                   SET PS-122-ITEM-GT-124-ITEM TO TRUE                          
               ELSE                                                             
                   SET PS-122-ITEM-LT-124-ITEM TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF FS122-LOC-NBR   = FS124-LOC-NBR                                   
               SET PS-122-LOC-EQ-124-LOC TO TRUE                                
           ELSE                                                                 
               IF FS122-LOC-NBR   > FS124-LOC-NBR                               
                   SET PS-122-LOC-GT-124-LOC TO TRUE                            
               ELSE                                                             
                   SET PS-122-LOC-LT-124-LOC TO TRUE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF FS122-ITEM-NMBR = PV-122-ITEM-NMBR                                
               SET PS-122-ITEM-EQ-PREV TO TRUE                                  
           ELSE                                                                 
               SET PS-122-ITEM-GT-PREV TO TRUE                                  
           END-IF.                                                              
                                                                                
           IF FS122-LOC-NBR   = PV-122-LOC-NBR                                  
               SET PS-122-LOC-EQ-PREV TO TRUE                                   
           ELSE                                                                 
               SET PS-122-LOC-GT-PREV TO TRUE                                   
           END-IF.                                                              
                                                                                
           IF FS124-ITEM-NMBR = PV-124-ITEM-NMBR                                
               SET PS-124-ITEM-EQ-PREV TO TRUE                                  
           ELSE                                                                 
               SET PS-124-ITEM-GT-PREV TO TRUE                                  
           END-IF.                                                              
                                                                                
           IF FS124-LOC-NBR   = PV-124-LOC-NBR                                  
               SET PS-124-LOC-EQ-PREV TO TRUE                                   
           ELSE                                                                 
               SET PS-124-LOC-GT-PREV TO TRUE                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PROCESS ALL TSKST ITEMS.  A REC-PROCESSED SWITCH ENSURES THAT        
      *    ONLY ONE OF THE FOLLOWING PATHS IS EXECUTED FOR EACH 122.            
      ********  SAME ITEM AND LOC                                               
      *    IF 122-ITEM = 124-ITEM AND 122-LOC = 124-LOC READ TO SEE IF          
      *        THERE ARE MORE 124 FOR THE ITEM/LOC.  WHEN THERE IS A            
      *        CHANGE IN LOC, WRITE THE LOC-RECORD.                             
      ********  SAME ITEM, 124 EXISTS WITHOUT 122                               
      *    IF 122-ITEM = 124-ITEM AND 122-LOC > 124-LOC READ TO SEE IF          
      *        THERE ARE MORE 124 FOR THE ITEM/LOC.  WHEN THERE IS A            
      *        CHANGE IN LOC, WRITE THE LOC-RECORD.                             
      ********  SAME ITEM, 122 EXISTS WITHOUT 124                               
      *    IF 122-ITEM = 124-ITEM AND 122-LOC < 124-LOC                         
      *        WRITE THE LOC-RECORD FOR THE 122.                                
      ********  ITEM NOT ON 124                                                 
      *    IF 122-ITEM < 124-ITEM                                               
      *        WRITE THE LOC-RECORD FOR THE 122.                                
      ********  ITEM NOT ON 122; BYPASS ITEM                                    
      *    IF 122-ITEM > 124-ITEM                                               
      *        READ 124 UNTIL EOF OR 122-ITEM NOT > 124-ITEM.                   
      *                                                                         
      *    AFTER THE ABOVE LOGIC THE 124 FILE SHOULD ALWAYS BE 'AHEAD'          
      *    OF THE 122 FILE, OR 124 WILL BE AT EOF.                              
      *    READ THE NEXT 122.                                                   
      ******************************************************************        
       2200-PROCESS-TSKST-MERGE.                                                
                                                                                
           SET PS-REC-NOT-PROCESSED TO TRUE.                                    
                                                                                
      ** PROCESS STORE ZERO FOR PREVIOUS ITEM                                   
           IF FS122-LOC-NBR = ZERO                                              
               IF PS-ITEM-NMBR > 0                                              
                   PERFORM 3350-PROCESS-124                                     
                     UNTIL PS-122-ITEM-LT-124-ITEM                              
                        OR (PS-122-ITEM-EQ-124-ITEM AND                         
                            PS-122-LOC-LT-124-LOC)                              
                        OR PS-TSKSTOO-UNLD-EOF                                  
                   PERFORM 4000-ITEM-BREAK                                      
               END-IF                                                           
               MOVE FS122-FS-MERGED-RECORD TO PS-CORP-DATA                      
               SET PS-REC-PROCESSED TO TRUE                                     
           END-IF.                                                              
                                                                                
      ** PROCESS STORE WHEN ITEM/LOC MATCH BETWEEN FILES                        
           IF PS-REC-NOT-PROCESSED                                              
               IF (PS-122-ITEM-EQ-124-ITEM AND                                  
                   PS-122-LOC-EQ-124-LOC)                                       
                       MOVE SPACES TO PV-MIN-OTB-FOR-LOC                        
                       SET PS-NOT-FIRST-LOC-FOR-ITEM TO TRUE                    
                       PERFORM 3300-PROCESS-124                                 
                         UNTIL PS-122-ITEM-LT-124-ITEM                          
                            OR (PS-122-ITEM-EQ-124-ITEM AND                     
                                PS-122-LOC-LT-124-LOC)                          
                            OR PS-TSKSTOO-UNLD-EOF                              
                       PERFORM 4500-ITEM-LOC-BREAK                              
                       SET PS-REC-PROCESSED TO TRUE                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ** PROCESS ITEM/LOC WHEN THERE ARE LOCS ON 124 FOR AN ITEM, BUT           
      ** THE LOC IS NOT ON 122.                                                 
           IF PS-REC-NOT-PROCESSED                                              
           AND (PS-122-ITEM-EQ-124-ITEM AND                                     
                PS-122-LOC-GT-124-LOC)                                          
                   MOVE SPACES TO PV-MIN-OTB-FOR-LOC                            
                   PERFORM 3350-PROCESS-124                                     
                     UNTIL PS-122-ITEM-LT-124-ITEM                              
                        OR (PS-122-ITEM-EQ-124-ITEM AND                         
                            PS-122-LOC-LT-124-LOC)                              
                        OR PS-TSKSTOO-UNLD-EOF                                  
                   PERFORM 4500-ITEM-LOC-BREAK                                  
                   SET PS-NOT-FIRST-LOC-FOR-ITEM TO TRUE                        
                   SET PS-REC-PROCESSED TO TRUE                                 
           END-IF.                                                              
                                                                                
      ** PROCESS ITEM/LOC WHEN THERE IS NO 124 FOR THE ITEM/LOC                 
           IF PS-REC-NOT-PROCESSED                                              
           AND (PS-122-ITEM-EQ-124-ITEM AND                                     
                PS-122-LOC-LT-124-LOC)                                          
                   MOVE SPACES TO PV-MIN-OTB-FOR-LOC                            
                   PERFORM 4400-ITEM-LOC-BREAK                                  
                   SET PS-REC-PROCESSED TO TRUE                                 
           END-IF.                                                              
                                                                                
      ** PROCESS ITEM/LOC WHEN THERE IS NO 124 FOR THE ITEM/LOC                 
           IF PS-REC-NOT-PROCESSED                                              
             AND (PS-122-ITEM-LT-124-ITEM OR                                    
                  PS-TSKSTOO-UNLD-EOF)                                          
                 MOVE ZERO TO PV-ONORD-FOR-LOC                                  
                 PERFORM 4500-ITEM-LOC-BREAK                                    
                 SET PS-REC-PROCESSED TO TRUE                                   
           END-IF.                                                              
                                                                                
      ** BYPASS ALL 124 FOR AN ITEM IF THE ITEM IS NOT ON 122                   
           IF PS-REC-NOT-PROCESSED                                              
             AND PS-122-ITEM-GT-124-ITEM                                        
                 PERFORM 2100-READ-TSKSTOO-UNLD-FILE                            
                   UNTIL PS-122-ITEM-EQ-124-ITEM                                
                      OR PS-122-ITEM-LT-124-ITEM                                
                      OR PS-TSKSTOO-UNLD-EOF                                    
                 SET PS-REC-PROCESSED TO TRUE                                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-TSKST-MERGED-FILE.                                 
                                                                                
      ******************************************************************        
      *    PROCESS ALL TSKSTOO FOR ITEM/LOC                                     
      ******************************************************************        
       3300-PROCESS-124.                                                        
           ADD FS124-ONORD-UNT-QTY TO PV-ONORD-FOR-LOC                          
           IF PV-MIN-OTB-FOR-LOC = SPACES OR                                    
              FS124-OTB-PER-NBR < PV-MIN-OTB-FOR-LOC                            
               MOVE FS124-OTB-PER-NBR TO PV-MIN-OTB-FOR-LOC                     
           END-IF                                                               
           IF PV-MIN-OTB-FOR-LOC < PV-MIN-OTB OR                                
              PV-MIN-OTB = SPACES                                               
               MOVE PV-MIN-OTB-FOR-LOC TO PV-MIN-OTB                            
           END-IF.                                                              
           IF (PS-122-ITEM-EQ-124-ITEM AND                                      
               PS-124-ITEM-EQ-PREV AND                                          
                (PS-122-LOC-GT-124-LOC AND                                      
                 PS-124-LOC-GT-PREV))                                           
           OR (PS-122-ITEM-GT-124-ITEM AND                                      
               PS-124-ITEM-EQ-PREV)                                             
                   PERFORM 4600-ITEM-LOC-BREAK-124                              
           END-IF.                                                              
                                                                                
           IF NOT PS-TSKSTOO-UNLD-EOF                                           
               PERFORM 2100-READ-TSKSTOO-UNLD-FILE                              
               IF PS-FIRST-LOC-FOR-ITEM                                         
               AND (PS-124-LOC-GT-PREV                                          
                OR  PS-124-ITEM-GT-PREV)                                        
                   PERFORM 4700-ITEM-LOC-BREAK-124                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PROCESS ALL TSKSTOO FOR ITEM/LOC                                     
      ******************************************************************        
       3350-PROCESS-124.                                                        
           ADD FS124-ONORD-UNT-QTY TO PV-ONORD-FOR-LOC                          
           IF PV-MIN-OTB-FOR-LOC = SPACES OR                                    
              FS124-OTB-PER-NBR < PV-MIN-OTB-FOR-LOC                            
               MOVE FS124-OTB-PER-NBR TO PV-MIN-OTB-FOR-LOC                     
           END-IF                                                               
           IF PV-MIN-OTB-FOR-LOC < PV-MIN-OTB OR                                
              PV-MIN-OTB = SPACES                                               
               MOVE PV-MIN-OTB-FOR-LOC TO PV-MIN-OTB                            
           END-IF.                                                              
                                                                                
           IF NOT PS-TSKSTOO-UNLD-EOF                                           
               PERFORM 2100-READ-TSKSTOO-UNLD-FILE                              
               IF (PS-124-LOC-GT-PREV                                           
                OR PS-124-ITEM-GT-PREV)                                         
                AND (FS122-LOC-NBR NOT = PV-124-LOC-NBR)                        
                   PERFORM 4700-ITEM-LOC-BREAK-124                              
RK0706         ELSE                                                             
RK0706           IF PS-TSKSTOO-UNLD-EOF                                         
RK0706             PERFORM 4700-ITEM-LOC-BREAK-124                              
RK0706           END-IF                                                         
RK0706         END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH PROCESSES THE REMAINING TSKSTOO AT EOJ.               
      *    IF  THE MERGE ITEM-NBR IS > THE TSKSTOO ITEM                         
      *    AND THE TSKSTOO ITEM-NBR IS = THE PREVIOUS MERGE ITEM-NBR            
      *    AND (THIS IS FIRST TIME THRU                                         
      *         OR THIS IS THE 2ND RECORD FOR THIS LOC)                         
      *        ACCUMULATE THE QTY, CHECK THE OTB.                               
      *      IF THIS IS THE FIRST RECORD FOR A NEW LOC WRITE THE PREV,          
      *        ACCUMULATE THE QTY, CHECK THE OTB.                               
      *                                                                         
      *    IF THE TSKSTOO ITEM DOESN'T = THE PREV MERGE ITEM NOTHING IS         
      *    WRITTEN.                                                             
      ******************************************************************        
       3400-PROCESS-124.                                                        
                                                                                
           IF (PS-122-ITEM-GT-124-ITEM AND                                      
               FS124-ITEM-NMBR = PV-122-ITEM-NMBR)                              
           OR (PV-122-ITEM-NMBR = FS124-ITEM-NMBR AND                           
               FS124-LOC-NBR > PV-122-LOC-NBR AND                               
               PS-TSKST-UNLD-EOF)                                               
               IF (PS-FIRST-LOC-FOR-ITEM)                                       
               OR (PS-124-LOC-EQ-PREV AND                                       
                   PS-124-ITEM-EQ-PREV)                                         
                   SET PS-NOT-FIRST-LOC-FOR-ITEM TO TRUE                        
                   ADD FS124-ONORD-UNT-QTY TO PV-ONORD-FOR-LOC                  
                   IF PV-MIN-OTB-FOR-LOC = SPACES OR                            
                      FS124-OTB-PER-NBR < PV-MIN-OTB-FOR-LOC                    
                       MOVE FS124-OTB-PER-NBR TO PV-MIN-OTB-FOR-LOC             
                   END-IF                                                       
                   IF PV-MIN-OTB-FOR-LOC < PV-MIN-OTB OR                        
                      PV-MIN-OTB = SPACES                                       
                       MOVE PV-MIN-OTB-FOR-LOC TO PV-MIN-OTB                    
                   END-IF                                                       
               ELSE                                                             
                   IF PS-124-ITEM-EQ-PREV                                       
                       ADD FS124-ONORD-UNT-QTY TO PV-ONORD-FOR-LOC              
                       IF PV-MIN-OTB-FOR-LOC = SPACES OR                        
                          FS124-OTB-PER-NBR < PV-MIN-OTB-FOR-LOC                
                           MOVE FS124-OTB-PER-NBR TO PV-MIN-OTB-FOR-LOC         
                       END-IF                                                   
                       IF PV-MIN-OTB-FOR-LOC < PV-MIN-OTB                       
                           MOVE PV-MIN-OTB-FOR-LOC TO PV-MIN-OTB                
                       END-IF                                                   
                       PERFORM 4600-ITEM-LOC-BREAK-124                          
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF PS-NOT-FIRST-LOC-FOR-ITEM                                     
                   ADD FS124-ONORD-UNT-QTY TO PV-ONORD-FOR-LOC                  
                   IF PV-MIN-OTB-FOR-LOC = SPACES OR                            
                      FS124-OTB-PER-NBR < PV-MIN-OTB-FOR-LOC                    
                       MOVE FS124-OTB-PER-NBR TO PV-MIN-OTB-FOR-LOC             
                   END-IF                                                       
                   IF PV-MIN-OTB-FOR-LOC < PV-MIN-OTB OR                        
                      PV-MIN-OTB = SPACES                                       
                       MOVE PV-MIN-OTB-FOR-LOC TO PV-MIN-OTB                    
                   END-IF                                                       
                   PERFORM 4600-ITEM-LOC-BREAK-124                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF NOT PS-TSKSTOO-UNLD-EOF                                           
               PERFORM 2100-READ-TSKSTOO-UNLD-FILE                              
           END-IF.                                                              
           IF PS-122-ITEM-LT-124-ITEM AND                                       
              PV-124-ITEM-NMBR = PV-122-ITEM-NMBR AND                           
              PV-124-LOC-NBR > PV-122-LOC-NBR AND                               
              FS124-ITEM-NMBR > PV-124-ITEM-NMBR                                
                PERFORM 4600-ITEM-LOC-BREAK-124.                                
                                                                                
      ******************************************************************        
      *    WRITE OUTPUT ON A CHANGE IN ITEM-NMBR.  THIS WILL WRITE THE          
      *    LOC = ZERO RECORD AFTER ALL PROCESSING IS DONE FOR AN ITEM.          
      ******************************************************************        
       4000-ITEM-BREAK.                                                         
           MOVE PS-CORP-DATA      TO FS123-FS-MERGED-RECORD                     
           MOVE PV-TOT-ONORD      TO FS123-ONORD-QTY                            
           MOVE PV-TOT-ONHAND     TO FS123-ONHAND-QTY                           
           MOVE PC-Y              TO FS123-FOUND-ON-TSKST-IND                   
           MOVE PV-MIN-OTB        TO FS123-MIN-OTB-PER-NBR                      
           PERFORM 5000-WRITE-OUTPUT-REC                                        
               SET PS-FIRST-LOC-FOR-ITEM TO TRUE                                
           INITIALIZE PV-MIN-OTB                                                
                      PV-MIN-OTB-FOR-LOC                                        
                      PV-TOT-ONORD                                              
                      PV-TOT-ONHAND                                             
                      PV-ONORD-FOR-LOC.                                         
                                                                                
      ******************************************************************        
      *    WRITE OUTPUT ON A CHANGE IN LOC-NBR                                  
      ******************************************************************        
       4400-ITEM-LOC-BREAK.                                                     
           IF FS122-LOC-NBR NOT = ZERO                                          
               MOVE FS122-FS-MERGED-RECORD                                      
                 TO FS123-FS-MERGED-RECORD                                      
               ADD FS123-ONHAND-QTY TO PV-TOT-ONHAND                            
               MOVE ZERO             TO FS123-ONORD-QTY                         
               MOVE PC-Y             TO FS123-FOUND-ON-TSKST-IND                
               MOVE PV-MIN-OTB-FOR-LOC TO FS123-MIN-OTB-PER-NBR                 
               PERFORM 5000-WRITE-OUTPUT-REC                                    
               INITIALIZE PV-MIN-OTB-FOR-LOC                                    
                          PV-ONORD-FOR-LOC                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE OUTPUT FROM HOLD ON A CHANGE IN LOC-NBR                        
      ******************************************************************        
       4500-ITEM-LOC-BREAK.                                                     
           IF PS-122-ITEM-GT-PREV                                               
               PERFORM 4000-ITEM-BREAK                                          
           END-IF.                                                              
           MOVE FS122-FS-MERGED-RECORD                                          
             TO FS123-FS-MERGED-RECORD.                                         
           MOVE FS122-ONHAND-QTY TO FS123-ONHAND-QTY.                           
           ADD FS123-ONHAND-QTY  TO PV-TOT-ONHAND.                              
           ADD PV-ONORD-FOR-LOC  TO PV-TOT-ONORD.                               
           MOVE PV-ONORD-FOR-LOC TO FS123-ONORD-QTY.                            
           MOVE PC-Y             TO FS123-FOUND-ON-TSKST-IND.                   
           MOVE PV-MIN-OTB-FOR-LOC TO FS123-MIN-OTB-PER-NBR.                    
           PERFORM 5000-WRITE-OUTPUT-REC.                                       
           IF PS-124-ITEM-GT-PREV                                               
               SET PS-FIRST-LOC-FOR-ITEM TO TRUE                                
           END-IF.                                                              
           INITIALIZE PV-MIN-OTB-FOR-LOC                                        
                      PV-ONORD-FOR-LOC.                                         
                                                                                
      ******************************************************************        
      *    WRITE OUTPUT ON A CHANGE IN LOC-NBR FOR TSKSTOO RECORDS              
      *    WHERE THE ITEM IS ON THE MERGE FILE BUT THE LOC IS NOT.              
      ******************************************************************        
       4600-ITEM-LOC-BREAK-124.                                                 
RK0706       ADD 1 TO PV-TSKSTOO-ONLY-CNT.                                      
             MOVE PS-CORP-DATA       TO FS123-FS-MERGED-RECORD.                 
             MOVE FS124-ITEM-NMBR    TO FS123-ITEM-NMBR.                        
             MOVE FS124-LOC-NBR      TO FS123-LOC-NBR.                          
             ADD  PV-ONORD-FOR-LOC   TO PV-TOT-ONORD.                           
             MOVE PV-ONORD-FOR-LOC   TO FS123-ONORD-QTY.                        
RK0706       MOVE PC-N               TO FS123-FOUND-ON-TSKST-IND.               
             MOVE PV-MIN-OTB-FOR-LOC TO FS123-MIN-OTB-PER-NBR.                  
             MOVE ZEROS TO FS123-LAST-SALE-DATE                                 
                           FS123-FIRST-SALE-DATE                                
                           FS123-LAST-RCVD-DATE                                 
                           FS123-FIRST-RCVD-DATE                                
                           FS123-ONHAND-QTY                                     
                           FS123-YTD-SALES-UNIT-QTY.                            
             IF FS123-UPC-STAT-CDE = PC-25                                      
                 MOVE PC-20 TO FS123-UPC-STAT-CDE                               
             END-IF.                                                            
             PERFORM 5000-WRITE-OUTPUT-REC.                                     
             INITIALIZE PV-MIN-OTB-FOR-LOC                                      
                        PV-ONORD-FOR-LOC.                                       
             IF PS-124-ITEM-GT-PREV                                             
                 SET PS-FIRST-LOC-FOR-ITEM TO TRUE                              
             ELSE                                                               
                 SET PS-NOT-FIRST-LOC-FOR-ITEM TO TRUE                          
             END-IF.                                                            
      ******************************************************************        
      *    WRITE OUTPUT ON A CHANGE IN LOC-NBR FOR TSKSTOO RECORDS              
      *    WHERE THE ITEM IS ON THE MERGE FILE BUT THE LOC IS NOT.              
      ******************************************************************        
       4700-ITEM-LOC-BREAK-124.                                                 
RK0706       ADD 1 TO PV-TSKSTOO-ONLY-CNT.                                      
             MOVE PS-CORP-DATA       TO FS123-FS-MERGED-RECORD.                 
             MOVE PV-124-LOC-NBR     TO FS123-LOC-NBR.                          
             ADD  PV-ONORD-FOR-LOC   TO PV-TOT-ONORD.                           
             MOVE PV-ONORD-FOR-LOC   TO FS123-ONORD-QTY.                        
RK0706       MOVE PC-N               TO FS123-FOUND-ON-TSKST-IND.               
             MOVE PV-MIN-OTB-FOR-LOC TO FS123-MIN-OTB-PER-NBR.                  
             MOVE ZEROS TO FS123-LAST-SALE-DATE                                 
                           FS123-FIRST-SALE-DATE                                
                           FS123-LAST-RCVD-DATE                                 
                           FS123-FIRST-RCVD-DATE                                
                           FS123-ONHAND-QTY                                     
                           FS123-YTD-SALES-UNIT-QTY.                            
             IF FS123-UPC-STAT-CDE = PC-25                                      
                 MOVE PC-20 TO FS123-UPC-STAT-CDE                               
             ELSE                                                               
                 IF FS123-UPC-STAT-CDE = PC-30                                  
                     MOVE PC-30 TO FS123-UPC-STAT-CDE                           
                 END-IF                                                         
             END-IF.                                                            
             PERFORM 5000-WRITE-OUTPUT-REC.                                     
             INITIALIZE PV-MIN-OTB-FOR-LOC                                      
                        PV-ONORD-FOR-LOC.                                       
             IF PS-124-ITEM-GT-PREV                                             
                 SET PS-FIRST-LOC-FOR-ITEM TO TRUE                              
             ELSE                                                               
                 SET PS-NOT-FIRST-LOC-FOR-ITEM TO TRUE                          
             END-IF.                                                            
      ******************************************************************        
      *    WRITE SUMMARY RECORDS                                                
      ******************************************************************        
       5000-WRITE-OUTPUT-REC.                                                   
                                                                                
           WRITE ITEM-SUMMARY-REC FROM FS123-FS-MERGED-RECORD.                  
           ADD +1 TO PV-OUTPUT-CNT                                              
                     PV-RECS-WRITTEN-CNT.                                       
      *    IF PV-OUTPUT-CNT > PV-OUTPUT-MAX                                     
      *        DISPLAY 'FS123-ITM = ' FS123-ITEM-NMBR                           
      *        DISPLAY 'OUTPUT RECS = ' PV-RECS-WRITTEN-CNT                     
      *        MOVE ZERO TO PV-OUTPUT-CNT.                                      
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPER-ATTEMPTED.                    
           DISPLAY '**********************************'.                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
