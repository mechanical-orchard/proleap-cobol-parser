000400 IDENTIFICATION DIVISION.                                                 
000500                                                                          
000600 PROGRAM-ID.             VLKUT035.                                        
000700 AUTHOR.                 PATRICK BURKE.                                   
000800 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000900 DATE-WRITTEN.           FEBRUARY 2005.                                   
001000 DATE-COMPILED.                                                           
001100                                                                          
001200* ****************************************************************        
001300*  PROGRAM NARRATIVE:                                                     
001400*  THIS PROGRAM WILL CREATE A HEADER FILE CONTAINING RECEIVER             
001500*  INFORMATION FOR COMPLIANCE NETWORK.                                    
001510* ****************************************************************        
001700*                                                                         
001800 ENVIRONMENT DIVISION.                                                    
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.                        IBM-3090.                        
002300 OBJECT-COMPUTER.                        IBM-3090.                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002810     SELECT VL-EXTRACT-FILE                                               
002820         ASSIGN TO INP01.                                                 
002830                                                                          
002900     SELECT VL-HEADER-FILE                                                
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003110     SELECT  VL-MARKER-FILE                                               
003120         ASSIGN TO OUT02.                                                 
003130                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700*                                                                         
003710 FD  VL-EXTRACT-FILE                                                      
003720     RECORDING MODE IS F                                                  
003730     LABEL RECORDS ARE STANDARD                                           
003740     BLOCK CONTAINS 0 RECORDS.                                            
003750 01  VL-EXTRACT-RECORD              PIC  X(50).                           
003760*                                                                         
003800 FD  VL-HEADER-FILE                                                       
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
0518   01  VL-HEADER-RECORD               PIC  X(387).                          
004300*                                                                         
004310 FD  VL-MARKER-FILE                                                       
004320     RECORDING MODE IS F                                                  
004330     LABEL RECORDS ARE STANDARD                                           
004340     BLOCK CONTAINS 0 RECORDS.                                            
004350 01  VL-MARKER-RECORD                   PIC  X(78).                       
004360*                                                                         
004400*                                                                         
004500 WORKING-STORAGE SECTION.                                                 
004510                                                                          
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
004600*****************************************************************         
004810 01  DK-DB2-KEYS.                                                         
004820     05  DK-PO-NBR                     PIC S9(09)  COMP.                  
004830     05  DK-REC-SEQ-NBR                PIC S9(09)  COMP.                  
004840     05  DK-OPER-UNT-ID                PIC S9(04) COMP.                   
004900                                                                          
004600*****************************************************************         
004810 01  NI-NULL-INDICATOR.                                                   
004820     05  NI-VEH-NBR                PIC S9(4) COMP VALUE 0.                
004830                                                                          
004910*****************************************************************         
004920*    PROGRAM CONSTANTS                                                    
004930*****************************************************************         
004940 01  PC-CONSTANTS.                                                        
005000     05  PC-CURRENT-PGM-VLKUT035      PIC  X(08) VALUE 'VLKUT035'.        
005100     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
005200                                                 COMP SYNC.               
005300                                                                          
005400*****************************************************************         
005500*    PROGRAM SWITCHES                                                     
005600*****************************************************************         
005700 01  PS-SWITCHES.                                                         
005710     05  PS-END-OF-INPUT-SW           PIC X(01)  VALUE 'N'.               
005720         88  PS-END-OF-INPUT                     VALUE 'Y'.               
005720         88  PS-NOT-END-OF-INPUT                 VALUE 'N'.               
005730                                                                          
006200*****************************************************************         
006300*    PROGRAM VARIABLES                                                    
006400*****************************************************************         
006500 01  PV-VARIABLES.                                                        
006600     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
006700                                                 COMP SYNC.               
006800     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
006900                                                 COMP SYNC.               
007000     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
007100     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
007200     05  PV-ABEND-TABLE-1             PIC  X(20) VALUE SPACES.            
007300     05  PV-ABEND-TABLE-2             PIC  X(20) VALUE SPACES.            
007400     05  PV-ERROR-MSG                 PIC  X(60) VALUE SPACES.            
007500     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
007520     05  PV-READ-COUNT                PIC  9(09) VALUE ZERO.              
007530     05  PV-RECEIVER.                                                     
007540         10  FILLER                   PIC  9(06) VALUE ZERO.              
007550         10  PV-REC                   PIC  9(03) VALUE ZERO.              
007600     05  PV-PO-RECEIVER.                                                  
007700         10  PV-PO-NBR                PIC X(08) VALUE SPACES.             
007710         10  PV-REC-SEQ-NBR           PIC X(03) VALUE SPACES.             
007711         10  FILLER                   PIC X(09) VALUE SPACES.             
007712     05  PV-RCVR-CTN-CNT-4.                                               
007713         10  PV-RCVR-CTN-CNT          PIC 9(09) VALUE ZERO.               
007715         10  FILLER                   PIC 9(04) VALUE ZERO.               
007716     05  PV-TOTAL-UNITS-4.                                                
007717         10  PV-TOTAL-UNITS           PIC 9(09) VALUE ZERO.               
007718         10  FILLER                   PIC 9(04) VALUE ZERO.               
007723     05  PV-AP-VERIFY-ITM-QTY         PIC S9(09)  VALUE +0                
007724                                                       COMP.              
007723     05  PV-DB2-COUNT-ACTL-SCN        PIC S9(04)  VALUE +0                
007724                                                       COMP.              
007725     05  PV-CCYYMMDD-DATE.                                                
007730         10  PV-CC                    PIC  X(02).                         
007731         10  PV-YY                    PIC  X(02).                         
007740         10  PV-MM                    PIC  X(02).                         
007750         10  PV-DD                    PIC  X(02).                         
007760     05  PV-DB2-DATE.                                                     
007770         10  PV-DB2-CC                  PIC X(02).                        
007780         10  PV-DB2-YY                  PIC X(02).                        
007790         10  FILLER                     PIC X(01).                        
007800         10  PV-DB2-MM                  PIC X(02).                        
007900         10  FILLER                     PIC X(01).                        
008000         10  PV-DB2-DD                  PIC X(02).                        
008100         10  FILLER                     PIC X(01).                        
008200         10  PV-DB2-HH                  PIC X(02).                        
008300         10  FILLER                     PIC X(01).                        
008400         10  PV-DB2-MN                  PIC X(02).                        
008500         10  FILLER                     PIC X(01).                        
008600         10  PV-DB2-SS                  PIC X(09).                        
009000                                                                          
009001 01  PV-ABEND-FIELDS.                                                     
009002     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'VLKUT035'.        
009003     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
009004     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
009005     05  PV-ABEND-STRUCTURE-1         PIC X(15)  VALUE SPACES.            
009006     05  PV-ABEND-STRUCTURE-2         PIC X(15)  VALUE SPACES.            
009007     05  PV-ABEND-STRUCTURE-3         PIC X(15)  VALUE SPACES.            
009008     05  PV-ABEND-PO                  PIC X(09)  VALUE SPACES.            
009009     05  PV-ABEND-RCVR                PIC X(09)  VALUE SPACES.            
009010     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
009011     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
009012     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
009013     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
009014     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
009015                                                                          
009023     05  PV-RUN-DATE-CN.                                                  
009024         10  PV-RUN-CCYY-CN           PIC  X(04) VALUE SPACES.            
009025         10  PV-RUN-MO-CN             PIC  X(02) VALUE SPACES.            
009026         10  PV-RUN-DD-CN             PIC  X(02) VALUE SPACES.            
009027     05  PV-RUN-TIME                  PIC  X(08) VALUE SPACES.            
009028     05  PV-RUN-TIME-R REDEFINES PV-RUN-TIME.                             
009029         10  PV-RUN-HH                PIC  X(02).                         
009031         10  PV-RUN-MM                PIC  X(02).                         
009033         10  PV-RUN-SS                PIC  X(02).                         
009032         10  FILLER                   PIC  X(02).                         
009034     05  PV-RUN-TIME-CN.                                                  
009035         10  PV-RUN-HH-CN             PIC  X(02) VALUE SPACES.            
009036         10  PV-RUN-MM-CN             PIC  X(02) VALUE SPACES.            
009037         10  PV-RUN-SS-CN             PIC  X(02) VALUE SPACES.            
009038***** FILE NAME CONSISTS OF RUN-DATE_RECEIVER.DAT                         
009039*****     EXAMPLE: 20050317_RECEIVER.DAT                                  
009040     05  PV-FILE-NAME.                                                    
009041         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
009042         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
009043         10  PV-RECEIVER-DAT          PIC  X(12) VALUE SPACES.            
009044                                                                          
009045***  MARKER FILE LAYOUT                                                   
009046     COPY VLRD029.                                                        
009047                                                                          
009048*---------------------------------------------------------------*         
009049*    ABEND PROCESSING COPYBOOK.                                 *         
009050*---------------------------------------------------------------*         
009051     COPY DPWS013.                                                        
009052/                                                                         
009047                                                                          
009048*---------------------------------------------------------------*         
009049*    CALENDOR ROUTINE COPYBOOK.                                 *         
009050*---------------------------------------------------------------*         
009051     COPY DPWS500.                                                        
009052/                                                                         
009053******************************************************************        
009054*  INPUT LAYOUT FOR EXTRACT FILE                                          
009055******************************************************************        
009056     COPY VLRD038.                                                        
009057/                                                                         
009070******************************************************************        
009100***  RECEIVER HEADER FILE LAYOUT                                          
009200     COPY VLRD035.                                                        
009600/                                                                         
009610*****************************************************************         
009620*    DB2 AREA FOR VEHICLE TRIP TABLE                            *         
009630*****************************************************************         
009640     EXEC SQL                                                             
009650          INCLUDE TVHTRIP                                                 
009660     END-EXEC.                                                            
009661/                                                                         
009670*****************************************************************         
009680*    DB2 AREA FOR RECEIVER TABLE                                *         
009690*****************************************************************         
009691     EXEC SQL                                                             
009692          INCLUDE TRECEIV                                                 
009693     END-EXEC.                                                            
009694/                                                                         
009695*****************************************************************         
009696*    DB2 AREA FOR SHIPMENT TABLE                                *         
009697*****************************************************************         
009698     EXEC SQL                                                             
009699          INCLUDE TSHIPMT                                                 
009700     END-EXEC.                                                            
009701/                                                                         
009702*****************************************************************         
009703*    DB2 AREA FOR SHIPMENT PO TABLE                             *         
009704*---------------------------------------------------------------*         
009705     EXEC SQL                                                             
009706          INCLUDE TSHPTPO                                                 
009707     END-EXEC.                                                            
009708/                                                                         
009709*---------------------------------------------------------------*         
009710*    DB2 AREA                                                             
009711*---------------------------------------------------------------*         
009712     EXEC SQL                                                             
009713          INCLUDE TRECDIS                                                 
009714     END-EXEC.                                                            
009715/                                                                         
009709*---------------------------------------------------------------*         
009710*    DB2 AREA                                                             
009711*---------------------------------------------------------------*         
009712     EXEC SQL                                                             
009713          INCLUDE TRECITM                                                 
009714     END-EXEC.                                                            
009715/                                                                         
009716*****************************************************************         
009717*    DB2 DCLGEN FOR TRIP STATUS TABLE                                     
009718*****************************************************************         
009719     EXEC SQL                                                             
009720          INCLUDE TTRIPST                                                 
009721     END-EXEC.                                                            
009730/                                                                         
009716*****************************************************************         
009717*    DB2 DCLGEN FOR PACKSLIP TABLE                                        
009718*****************************************************************         
009719     EXEC SQL                                                             
009720          INCLUDE TPKSLIP                                                 
009721     END-EXEC.                                                            
009730/                                                                         
009716*****************************************************************         
009717*    DB2 DCLGEN - INBOUND EDI PACKLIST/VENDOR SHPMNT UNIT                 
009717*                 ASSOCIATION                                             
009718*****************************************************************         
009719     EXEC SQL                                                             
009720          INCLUDE TINPKSU                                                 
009721     END-EXEC.                                                            
009730/                                                                         
009716*****************************************************************         
009717*    DB2 DCLGEN - SHIPMENT UNIT LOCATION                                  
009718*****************************************************************         
009719     EXEC SQL                                                             
009720          INCLUDE TSHUNLO                                                 
009721     END-EXEC.                                                            
009730/                                                                         
009745*****************************************************************         
009746***  DB2 ABEND COPYBOOK                                                   
009760                                                                          
009750     COPY DPWS004.                                                        
009760                                                                          
009910*****************************************************************         
010000     EXEC SQL                                                             
010100         INCLUDE SQLCA                                                    
010200     END-EXEC.                                                            
010300                                                                          
010310*****************************************************************         
010400     COPY SQLCA2.                                                         
010500                                                                          
013600 LINKAGE SECTION.                                                         
013700                                                                          
013800 PROCEDURE DIVISION.                                                      
013900*****************************************************************         
014000*  MAINLINE LOGIC FOR PROGRAM                                             
014100*****************************************************************         
014200 0000-MAINLINE.                                                           
014300     PERFORM 1000-INITIALIZE.                                             
014400                                                                          
014500     PERFORM 2000-PROCESS                                                 
014600         UNTIL PS-END-OF-INPUT.                                           
014700                                                                          
014800     PERFORM 9000-EOJ.                                                    
014900                                                                          
015000     STOP RUN.                                                            
015100                                                                          
015200*****************************************************************         
015300*  OPEN FILES AND DO INITIAL READ AND FETCH.                              
015400*****************************************************************         
015500 1000-INITIALIZE.                                                         
015501                                                                          
015510     OPEN INPUT  VL-EXTRACT-FILE.                                         
015600     OPEN OUTPUT VL-HEADER-FILE                                           
015610                 VL-MARKER-FILE.                                          
015700                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY '**********************************************'.            
000128     DISPLAY '*******  CONTROL CARD ====>  ' CC-CONTROL-CARD.             
000129                                                                          
000130     PERFORM 1050-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME.                      
000135                                                                          
000136     DISPLAY '*******  RUN TIME ====> '  PV-RUN-TIME.                     
000137                                                                          
000138     MOVE PV-RUN-HH          TO PV-RUN-HH-CN.                             
000139     MOVE PV-RUN-MM          TO PV-RUN-MM-CN.                             
000140     MOVE PV-RUN-SS          TO PV-RUN-SS-CN.                             
000141                                                                          
021002     PERFORM 1100-INITIALIZE-VARIABLES.                                   
015721     PERFORM 2100-READ-EXTRACT.                                           
015720     IF PS-NOT-END-OF-INPUT                                               
015730         PERFORM 4520-SELECT-HEADER                                       
015703     ELSE                                                                 
015703         DISPLAY '***********************************************'        
015703         DISPLAY '**** EMPTY INPUT FILE - NO RECORDS TO PROCESS'          
015703         DISPLAY '***********************************************'        
015720     END-IF.                                                              
016000                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1050-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT035 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1050-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
016010*****************************************************************         
016020*  INITIALIZATION PARAGRAPH                                               
016030*****************************************************************         
016040 1100-INITIALIZE-VARIABLES.                                               
016050                                                                          
016080     INITIALIZE DCLTVHTRIP                                                
016090                DCLTRECEIV                                                
016093                DCLTSHIPMT                                                
016094                DCLTSHPTPO                                                
016095                DCLTRECDIS                                                
016096                DCLTTRIPST.                                               
016098                                                                          
016100*****************************************************************         
016200* PROCESS DATA FROM TABLES AND WRITE OUTPUT RECORD                        
016300*****************************************************************         
016400 2000-PROCESS.                                                            
016600                                                                          
016700     MOVE 'A'                     TO VL035-ACD-INDICATOR.                 
016800     MOVE PV-PO-RECEIVER          TO VL035-RECEIVER-NBR.                  
016900     MOVE PV-PO-NBR               TO VL035-PO-NBR.                        
017000     MOVE VHTRIP-OWNER-SCAC-ID    TO VL035-SCAC.                          
017800     MOVE SHIPMT-PRO-NBR          TO VL035-PRO-NBR.                       
017900     MOVE VHTRIP-VEH-NBR          TO VL035-TRAILER-NBR.                   
018000     MOVE SPACES                  TO VL035-RCVR-CTN-CNT-SIGN.             
018100     IF RECEIV-ACTL-CART-CNT  > +0                                        
018100         MOVE RECEIV-ACTL-CART-CNT    TO PV-RCVR-CTN-CNT                  
018110         MOVE PV-RCVR-CTN-CNT-4       TO VL035-RCVR-CTN-CNT               
018110     ELSE                                                                 
018110         PERFORM 6000-SCANNED-CART-CNT                                    
018110         IF PV-DB2-COUNT-ACTL-SCN > +0                                    
018110             MOVE PV-DB2-COUNT-ACTL-SCN TO PV-RCVR-CTN-CNT                
018110             MOVE PV-RCVR-CTN-CNT-4     TO VL035-RCVR-CTN-CNT             
018110         ELSE                                                             
018110             MOVE RECEIV-EXPTED-CART-CNT TO PV-RCVR-CTN-CNT               
018110             MOVE PV-RCVR-CTN-CNT-4     TO VL035-RCVR-CTN-CNT             
018110         END-IF                                                           
018110     END-IF.                                                              
018200     MOVE SPACES                  TO VL035-XREF-PO                        
018300     MOVE SPACES                  TO VL035-XREF-RCVR.                     
018320     MOVE SPACES                  TO VL035-AREA.                          
018500     MOVE 'VE'                    TO VL035-RCVR-STATUS.                   
018600     MOVE RECEIV-OPER-UNT-ID      TO VL035-LOCATION.                      
018700     MOVE SPACES                  TO VL035-TOTAL-UNITS-SIGN.              
018710     PERFORM 5320-TOTAL-UNITS.                                            
018800     MOVE PV-AP-VERIFY-ITM-QTY    TO PV-TOTAL-UNITS.                      
018810     MOVE PV-TOTAL-UNITS-4        TO VL035-TOTAL-UNITS.                   
018900     MOVE SPACES                  TO VL035-RCVR-GOH-CNT-SIGN.             
019000     MOVE ZEROS                   TO VL035-RCVR-GOH-CNT.                  
019100     MOVE SHIPMT-BOL-NBR          TO VL035-BOL-NBR.                       
019200     MOVE RECEIV-CRE-TMST         TO PV-DB2-DATE.                         
019214     MOVE PV-DB2-CC               TO VL035-CREATE-CC.                     
019215     MOVE PV-DB2-YY               TO VL035-CREATE-YY.                     
019220     MOVE PV-DB2-MM               TO VL035-CREATE-MM.                     
019230     MOVE PV-DB2-DD               TO VL035-CREATE-DD.                     
019300     MOVE RECEIV-VERFYD-TMST      TO PV-DB2-DATE.                         
019310     MOVE PV-DB2-CC               TO VL035-VERFYD-CC.                     
019320     MOVE PV-DB2-YY               TO VL035-VERFYD-YY.                     
019330     MOVE PV-DB2-MM               TO VL035-VERFYD-MM.                     
019340     MOVE PV-DB2-DD               TO VL035-VERFYD-DD.                     
019400     MOVE SPACES                  TO VL035-RCVR-UNIQUE-ADDENDA.           
019500     MOVE SPACES                  TO VL035-PO-UNIQUE-ADDENDA.             
019510     IF SHIPMT-VEND-SHIP-DTE = '0001-01-01' OR                            
019510        SHIPMT-VEND-SHIP-DTE = SPACES                                     
019510         MOVE SPACES              TO VL035-PICKUP-DATE                    
019510     ELSE                                                                 
019510         MOVE SHIPMT-VEND-SHIP-DTE TO PV-DB2-DATE                         
019520         MOVE PV-DB2-CC           TO VL035-PICKUP-CC                      
019530         MOVE PV-DB2-YY           TO VL035-PICKUP-YY                      
019540         MOVE PV-DB2-MM           TO VL035-PICKUP-MM                      
019550         MOVE PV-DB2-DD           TO VL035-PICKUP-DD                      
019510     END-IF.                                                              
019700     MOVE RECEIV-REC-TMST         TO PV-DB2-DATE.                         
019710     MOVE PV-DB2-CC               TO VL035-ARRIVAL-CC.                    
019720     MOVE PV-DB2-YY               TO VL035-ARRIVAL-YY.                    
019730     MOVE PV-DB2-MM               TO VL035-ARRIVAL-MM.                    
019740     MOVE PV-DB2-DD               TO VL035-ARRIVAL-DD.                    
019750     PERFORM 7000-TRIP-STATUS-DTE.                                        
019510     IF TRIPST-STATUS-DTE = SPACES                                        
019510         MOVE SPACES              TO VL035-UNLOAD-DATE                    
019510     ELSE                                                                 
019510         MOVE TRIPST-STATUS-DTE       TO PV-DB2-DATE                      
019520         MOVE PV-DB2-CC               TO VL035-UNLOAD-CC                  
019530         MOVE PV-DB2-YY               TO VL035-UNLOAD-YY                  
019540         MOVE PV-DB2-MM               TO VL035-UNLOAD-MM                  
019550         MOVE PV-DB2-DD               TO VL035-UNLOAD-DD                  
019510     END-IF.                                                              
019900     MOVE RECEIV-REC-TYP-CDE      TO VL035-RCVR-TYPE.                     
020000     MOVE RECEIV-FRE-ASTRA-IND    TO VL035-FREE-ASTRAY.                   
0518       MOVE SHIPMT-CARR-AUTH-ID     TO VL035-CARR-AUTH-ID.                  
020800     MOVE 'X'                     TO VL035-ANCHOR-CONSTANT.               
020801                                                                          
021000     PERFORM 8000-WRITE-HEADER.                                           
021002     PERFORM 1100-INITIALIZE-VARIABLES.                                   
021004     PERFORM 2100-READ-EXTRACT.                                           
015720     IF PS-NOT-END-OF-INPUT                                               
015730         PERFORM 4520-SELECT-HEADER                                       
015720     END-IF.                                                              
021100                                                                          
021200******************************************************************        
021300*  READ NEXT INPUT FILE RECORD.                                           
021400******************************************************************        
021410 2100-READ-EXTRACT.                                                       
021420                                                                          
021430     READ VL-EXTRACT-FILE INTO VL038-RECEIVER-FEEDER-RECORD               
021440        AT END                                                            
021450           SET PS-END-OF-INPUT TO TRUE                                    
021460        NOT AT END                                                        
021470           ADD +1                     TO PV-READ-COUNT                    
021471           MOVE VL038-PO-NBR          TO PV-PO-NBR                        
021473                                         DK-PO-NBR                        
021474           MOVE VL038-REC-SEQ-NBR     TO PV-REC                           
021475                                         DK-REC-SEQ-NBR                   
021476           MOVE PV-REC                TO PV-REC-SEQ-NBR                   
021492     END-READ.                                                            
021493/                                                                         
023420*----------------------------------------------------------------*        
023500*    SELECT HEADER DATA                                          *        
023600*----------------------------------------------------------------*        
023700 4520-SELECT-HEADER.                                                      
023800                                                                          
023801     EXEC SQL                                                             
023810        SELECT A.ACTL_CART_CNT                                            
023810              ,A.EXPTED_CART_CNT                                          
023820              ,A.CRE_TMST                                                 
023830              ,A.REC_TMST                                                 
023840              ,A.REC_TYP_CDE                                              
023850              ,A.VERFYD_TMST                                              
023860              ,A.OPER_UNT_ID                                              
023870              ,A.FRE_ASTRA_IND                                            
023870              ,A.RCVG_CLERK_INIT_ID                                       
023880              ,C.PRO_NBR                                                  
023890              ,C.BOL_NBR                                                  
023890              ,C.VEND_SHIP_DTE                                            
0518                ,C.CARR_AUTH_ID                                             
023891              ,D.OWNER_SCAC_ID                                            
023892              ,D.VEH_NBR                                                  
023893              ,D.TRIP_ID                                                  
023894          INTO :RECEIV-ACTL-CART-CNT                                      
023895              ,:RECEIV-EXPTED-CART-CNT                                    
023895              ,:RECEIV-CRE-TMST                                           
023896              ,:RECEIV-REC-TMST                                           
023897              ,:RECEIV-REC-TYP-CDE                                        
023898              ,:RECEIV-VERFYD-TMST                                        
023899              ,:RECEIV-OPER-UNT-ID                                        
023900              ,:RECEIV-FRE-ASTRA-IND                                      
023900              ,:RECEIV-RCVG-CLERK-INIT-ID                                 
023901              ,:SHIPMT-PRO-NBR                                            
023902              ,:SHIPMT-BOL-NBR                                            
023902              ,:SHIPMT-VEND-SHIP-DTE                                      
0518                ,:SHIPMT-CARR-AUTH-ID                                       
023903              ,:VHTRIP-OWNER-SCAC-ID                                      
023904              ,:VHTRIP-VEH-NBR:NI-VEH-NBR                                 
023905              ,:VHTRIP-TRIP-ID                                            
023906         FROM  TRECEIV A                                                  
023907              ,TSHPTPO B                                                  
023908              ,TSHIPMT C                                                  
023909              ,TVHTRIP D                                                  
023910        WHERE A.ORD_NBR     = :DK-PO-NBR                                  
023911          AND A.REC_SEQ_NBR = :DK-REC-SEQ-NBR                             
023912          AND A.ORD_NBR     = B.PO_NBR                                    
023913          AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                               
023914          AND B.PRIM_SHIPT_IND = 'Y'                                      
023915          AND B.SHIPT_ID    = C.SHIPT_ID                                  
023916          AND C.TRIP_ID     = D.TRIP_ID                                   
023917                                                                          
024200     END-EXEC                                                             
024300                                                                          
024400     EVALUATE TRUE                                                        
024500         WHEN SQLCODE = +0                                                
024500              CONTINUE                                                    
024510         WHEN SQLCODE = +100                                              
024600              PERFORM 4521-SELECT-HEADER-TRECEIV                          
025200         WHEN SQLWARN0 NOT = SPACE                                        
025300         WHEN SQLCODE < +0                                                
025400         WHEN SQLCODE > +0                                                
026410             MOVE '4520-SELECT-HEADER '    TO PV-ABEND-PARAGRAPH          
026420             MOVE 'SELECT HEADER DATA '    TO PV-ABEND-OPERATION          
026430             MOVE 'HEADER EXTRACT    '     TO PV-ABEND-STRUCTURE-1        
026440             MOVE 'DB2 - TRECEIV     '     TO PV-ABEND-STRUCTURE-2        
026450             MOVE 'PO - RCVR NBR     '     TO PV-ABEND-STRUCTURE-3        
026460             MOVE PV-PO-NBR                TO PV-ABEND-PO                 
026470             MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR               
026500             PERFORM 9200-SQL-ERROR                                       
026600     END-EVALUATE.                                                        
026700                                                                          
026701*****************************************************************         
026702* RETRIVE THE DETAILS FROM THE TRECEIV TABLE.                   *         
026703*****************************************************************         
023700 4521-SELECT-HEADER-TRECEIV.                                              
023800                                                                          
023801     EXEC SQL                                                             
023810        SELECT ACTL_CART_CNT                                              
023810              ,EXPTED_CART_CNT                                            
023820              ,CRE_TMST                                                   
023830              ,REC_TMST                                                   
023840              ,REC_TYP_CDE                                                
023850              ,VERFYD_TMST                                                
023860              ,OPER_UNT_ID                                                
023870              ,FRE_ASTRA_IND                                              
023870              ,RCVG_CLERK_INIT_ID                                         
023894          INTO :RECEIV-ACTL-CART-CNT                                      
023895              ,:RECEIV-EXPTED-CART-CNT                                    
023895              ,:RECEIV-CRE-TMST                                           
023896              ,:RECEIV-REC-TMST                                           
023897              ,:RECEIV-REC-TYP-CDE                                        
023898              ,:RECEIV-VERFYD-TMST                                        
023899              ,:RECEIV-OPER-UNT-ID                                        
023900              ,:RECEIV-FRE-ASTRA-IND                                      
023900              ,:RECEIV-RCVG-CLERK-INIT-ID                                 
023906          FROM  TRECEIV                                                   
023910         WHERE  ORD_NBR     = :DK-PO-NBR                                  
023911           AND  REC_SEQ_NBR = :DK-REC-SEQ-NBR                             
024200     END-EXEC.                                                            
024300                                                                          
024400     EVALUATE TRUE                                                        
024500         WHEN SQLCODE = +0                                                
024510         WHEN SQLCODE = +100                                              
024600              CONTINUE                                                    
025200         WHEN SQLWARN0 NOT = SPACE                                        
025300         WHEN SQLCODE < +0                                                
025400         WHEN SQLCODE > +0                                                
026410             MOVE '4521-SELECT-HEADER-TRECEIV '                           
026410                                           TO PV-ABEND-PARAGRAPH          
026420             MOVE 'SELECT HEADER DATA '    TO PV-ABEND-OPERATION          
026430             MOVE 'HEADER EXTRACT    '     TO PV-ABEND-STRUCTURE-1        
026440             MOVE 'DB2 - TRECEIV     '     TO PV-ABEND-STRUCTURE-2        
026450             MOVE 'PO - RCVR NBR     '     TO PV-ABEND-STRUCTURE-3        
026460             MOVE PV-PO-NBR                TO PV-ABEND-PO                 
026470             MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR               
026500             PERFORM 9200-SQL-ERROR                                       
026600     END-EVALUATE.                                                        
026701*****************************************************************         
026702* GET TOTAL UNITS FOR THIS PO/RECEIVER                          *         
026703*****************************************************************         
026704 5320-TOTAL-UNITS.                                                        
026705                                                                          
026706     EXEC SQL                                                             
026708         SELECT SUM(B.AP_VERIFY_ITM_QTY)                                  
026710           INTO :PV-AP-VERIFY-ITM-QTY                                     
026711           FROM TRECITM A                                                 
026711              , TRECDIS B                                                 
026711              , TRECEIV C                                                 
026712          WHERE A.ORD_NBR     = :DK-PO-NBR                                
026713            AND A.REC_SEQ_NBR = :DK-REC-SEQ-NBR                           
012500            AND A.ORD_NBR     = B.ORD_NBR                                 
012600            AND A.REC_SEQ_NBR = B.REC_SEQ_NBR                             
012610            AND A.ORD_LNE_NBR = B.ORD_LNE_NBR                             
012700            AND A.VER_STATUS_CDE  = 'A'                                   
012710            AND A.ORD_NBR     = C.ORD_NBR                                 
012800            AND A.REC_SEQ_NBR = C.REC_SEQ_NBR                             
026715        WITH UR                                                           
026716     END-EXEC                                                             
026717                                                                          
026718                                                                          
026719     EVALUATE TRUE                                                        
026720         WHEN SQLCODE = ZERO                                              
026721         WHEN SQLCODE = -305                                              
026722         WHEN SQLCODE = +100                                              
026723              CONTINUE                                                    
026724         WHEN SQLWARN0 NOT = SPACE                                        
026725         WHEN SQLCODE NOT = ZERO                                          
026726             MOVE '5320-TOTAL-UNITS  '     TO PV-ABEND-PARAGRAPH          
026728             MOVE 'SUM AP VERIFIED QTY'    TO PV-ABEND-OPERATION          
026729             MOVE 'SUM AP VERIFIED QTY'    TO PV-ABEND-STRUCTURE-1        
026730             MOVE 'DB2 - TRECDIS     '     TO PV-ABEND-STRUCTURE-2        
026731             MOVE 'PO - RCVR NBR     '     TO PV-ABEND-STRUCTURE-3        
026732             MOVE PV-PO-NBR                TO PV-ABEND-PO                 
026733             MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR               
026734             PERFORM 9200-SQL-ERROR                                       
026747     END-EVALUATE.                                                        
026748                                                                          
026762                                                                          
026764*****************************************************************         
026765* GET SCANNED CARTON COUNT WHEN ACTUAL CARTON COUNT ON TRECEIV            
026765*       IS ZERO                                                           
026770*****************************************************************         
026800 6000-SCANNED-CART-CNT.                                                   
049500     EXEC SQL                                                             
049510         SELECT COUNT(*)                                                  
049520           INTO :PV-DB2-COUNT-ACTL-SCN                                    
049530           FROM TPKSLIP A                                                 
049540              , TINPKSU B                                                 
049550              , TSHUNLO C                                                 
049560          WHERE A.PO_NBR       = :DK-PO-NBR                               
049570            AND A.REC_SEQ_NBR  = :DK-REC-SEQ-NBR                          
049580            AND A.VER_STAT_CDE = 'A'                                      
049590            AND A.PO_NBR       = B.PO_NBR                                 
049600            AND A.SEQ_NBR      = B.SEQ_NBR                                
049610            AND B.SHIPT_UNT_ID = C.SHIPT_UNT_ID                           
049630         WITH UR                                                          
049640     END-EXEC.                                                            
026900                                                                          
026719     EVALUATE TRUE                                                        
026720         WHEN SQLCODE = ZERO                                              
026721         WHEN SQLCODE = -305                                              
026722         WHEN SQLCODE = +100                                              
026723              CONTINUE                                                    
026724         WHEN SQLWARN0 NOT = SPACE                                        
026725         WHEN SQLCODE NOT = ZERO                                          
026726             MOVE '6000-SCANNED-CART-CNT'  TO PV-ABEND-PARAGRAPH          
026728             MOVE 'TPKSLIP'                TO PV-ABEND-OPERATION          
026729             MOVE 'TINPKSU'                TO PV-ABEND-STRUCTURE-1        
026730             MOVE 'TSHUNLO'                TO PV-ABEND-STRUCTURE-2        
026731             MOVE 'PO - RCVR NBR     '     TO PV-ABEND-STRUCTURE-3        
026732             MOVE PV-PO-NBR                TO PV-ABEND-PO                 
026733             MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR               
026734             PERFORM 9200-SQL-ERROR                                       
026747     END-EVALUATE.                                                        
026748                                                                          
026764*****************************************************************         
026765* GET UNLOAD DATE USING TRIP ID FOR TRIP STATUS CDE OF 'UN'               
026770*****************************************************************         
026800 7000-TRIP-STATUS-DTE.                                                    
026900                                                                          
027000     EXEC SQL                                                             
027100         SELECT A.STATUS_DTE                                              
027400         INTO   :TRIPST-STATUS-DTE                                        
027500         FROM   TTRIPST A                                                 
027700         WHERE A.TRIP_ID           = :VHTRIP-TRIP-ID                      
027800           AND A.TRIP_STATUS_CDE   = 'UN'                                 
028800     END-EXEC.                                                            
028900                                                                          
029000                                                                          
029100     EVALUATE TRUE                                                        
029200         WHEN SQLCODE = ZERO                                              
029400             CONTINUE                                                     
029300         WHEN SQLCODE = +100                                              
029400             PERFORM 7100-TRIP-STAT-TRY2                                  
029900         WHEN SQLWARN0 NOT = SPACE                                        
030000         WHEN SQLCODE NOT = ZERO                                          
030100             MOVE '7000-TRIP-STATUS-DTE '  TO PV-ABEND-PARAGRAPH          
030200             MOVE 'SELECT STATUS DTE  '    TO PV-ABEND-OPERATION          
030300             MOVE 'SELECT STATUS DATE'     TO PV-ABEND-STRUCTURE-1        
030400             MOVE 'DB2 - TTRIPST     '     TO PV-ABEND-STRUCTURE-2        
030500             MOVE 'PO - RCVR NBR     '     TO PV-ABEND-STRUCTURE-3        
030600             MOVE PV-PO-NBR                TO PV-ABEND-PO                 
030700             MOVE PV-REC-SEQ-NBR           TO PV-ABEND-RCVR               
030800             PERFORM 9200-SQL-ERROR                                       
031500     END-EVALUATE.                                                        
031600/                                                                         
026764*****************************************************************         
026765* IF 'UN' STATUS NOT FOUND, THEN TAKE THE MAX DATE FOUND WHERE            
026765*     STATUS IS NOT EN, AR OR DE. (ENROUTE, ARRIVED OR DELETED)           
026770*****************************************************************         
026800 7100-TRIP-STAT-TRY2.                                                     
027000     EXEC SQL                                                             
027100         SELECT STATUS_DTE                                                
027400           INTO :TRIPST-STATUS-DTE                                        
027400           FROM TTRIPST A                                                 
027500          WHERE A.TRIP_ID     = :VHTRIP-TRIP-ID                           
027500            AND A.TRIP_STATUS_CDE NOT IN ('AR', 'EN', 'DE')               
028800            AND TIMESTAMP(A.STATUS_DTE, A.STATUS_TME) IN                  
028800                 (SELECT                                                  
028800                      MAX(TIMESTAMP(B.STATUS_DTE, B.STATUS_TME))          
028800                    FROM TTRIPST B                                        
028900                    WHERE  A.TRIP_ID   = B.TRIP_ID)                       
027000     END-EXEC.                                                            
029000                                                                          
029100     EVALUATE TRUE                                                        
029200         WHEN SQLCODE = ZERO                                              
029400             CONTINUE                                                     
029300         WHEN SQLCODE = +100                                              
029400             MOVE SPACES TO TRIPST-STATUS-DTE                             
029900         WHEN SQLWARN0 NOT = SPACE                                        
030000         WHEN SQLCODE NOT = ZERO                                          
030100             MOVE '7100-TRIP-STAT-TRY2 '  TO PV-ABEND-PARAGRAPH           
030200             MOVE 'SELECT STATUS DTE  '   TO PV-ABEND-OPERATION           
030300             MOVE 'SELECT STATUS DATE'    TO PV-ABEND-STRUCTURE-1         
030400             MOVE 'DB2 - TTRIPST     '    TO PV-ABEND-STRUCTURE-2         
030500             MOVE 'PO - RCVR NBR     '    TO PV-ABEND-STRUCTURE-3         
030600             MOVE PV-PO-NBR               TO PV-ABEND-PO                  
030700             MOVE PV-REC-SEQ-NBR          TO PV-ABEND-RCVR                
030800             PERFORM 9200-SQL-ERROR                                       
031500     END-EVALUATE.                                                        
031600/                                                                         
034300*****************************************************************         
034400*  WRITE HEADER FILE                                                      
034500*****************************************************************         
034600 8000-WRITE-HEADER.                                                       
034700     WRITE VL-HEADER-RECORD FROM VL035-RECEIVER-HEADER-RECORD.            
034800                                                                          
034900     ADD 1                TO PV-RECS-WRITTEN.                             
035000                                                                          
035010*****************************************************************         
035020*  CREATE MARKER FILE                                                     
035030*****************************************************************         
035040 8500-CREATE-MARKER.                                                      
035050     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
035060     MOVE 'RCVRHDR.DAT '  TO PV-RECEIVER-DAT.                             
035070     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
035080     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
035090     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
035091     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
035092     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
035093                                                                          
035094     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
035095                                                                          
035100*****************************************************************         
035200*  END OF JOB PROCESSING                                                  
035300*****************************************************************         
035400 9000-EOJ.                                                                
035600                                                                          
035700     DISPLAY 'HEADER RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.             
035800                                                                          
035900     PERFORM 8500-CREATE-MARKER.                                          
035901                                                                          
035910     CLOSE VL-EXTRACT-FILE.                                               
036000     CLOSE VL-HEADER-FILE                                                 
036010           VL-MARKER-FILE.                                                
036100                                                                          
036200*****************************************************************         
036300*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
036400*****************************************************************         
036500 9200-SQL-ERROR.                                                          
036600     CLOSE VL-EXTRACT-FILE                                                
036610     CLOSE VL-HEADER-FILE                                                 
036700                                                                          
036800     MOVE SQLCODE                     TO SQLCODE2.                        
036900     MOVE SQLERRD(3)                  TO SQLERRD3.                        
037000                                                                          
037100     DISPLAY '** ABEND **         ** = SQLERROR'.                         
037200     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT035.                
037300     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
037400     DISPLAY '** OPERATION ** = ' PV-ABEND-OPERATION.                     
037500     DISPLAY '**           ** = ' PV-ABEND-STRUCTURE-1                    
037600     DISPLAY '**           ** = ' PV-ABEND-STRUCTURE-2                    
037700     DISPLAY '**           ** = ' PV-ERROR-MSG                            
037800     DISPLAY '** PO        ** = ' PV-ABEND-PO                             
037810     DISPLAY '** RECEIVER  ** = ' PV-ABEND-RCVR                           
037900     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
038000     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
038100     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
038200     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
038300                                                                          
038400     COPY DPPD004.                                                        
038500                                                                          
038600     MOVE +4013                       TO PV-ABEND-CODE.                   
038700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
038800                                                                          
038900*************  END OF VLKUT035  ********************************          
