000100 IDENTIFICATION DIVISION.                                         00010072
000200 PROGRAM-ID.     APKRP046.                                        00020072
000300 AUTHOR.         PAM VOLK.                                        00030072
000400 DATE-WRITTEN.   02/14/1997.                                      00040072
000500 DATE-COMPILED.                                                   00050072
000600*************************************************************     00060072
000700*    COBOL 2                                                *     00070072
000800*                                                           *     00080072
000900*  --- PURPOSE IS TO:                                       *     00090072
001000*    REPORT ON THE UNJOURNALIZED AND NI RECEIPT             *     00100072
001100*    FILE BY FISCAL PERIOD.  COMPARE THE AMOUNTS OF THE     *     00110072
001101*    CURRENT SUMMARY FILE TO THE AMOUNTS ON THE PREVIOUS    *     00110072
001102*    SUMMARY FILE AND REPORT ON THE DIFFERENCES FOR THE MOST*     00110072
001103*    CURRENT 13 FISCAL PERIODS.                             *     00110072
001104*                                                           *     00110072
001400*  --- DESCRIPTION OF FLOW:                                 *     00140072
001500*    INITIALIZE AN ARRAY  WITH THE MOST CURRENT 13 FISCAL   *     00150072
001600*    PERIODS.  LOAD THE CURRENT SUMMARY FILE AND THE PREVIOUS             
001700*    SUMMARY FILE INTO THE PREDEFINED ARRAY PLACING THE DATA*             
001800*    IN ITS CORRESPONDING FISCAL PERIOD WITHIN THE ARRAY.   *             
001900*    THE LAST ENTRY IN THE ARRAY WILL BE THE BUCKET THAT    *             
004200*    IS USED TO ACCUMULATE THE OTHER FISCAL PERIODS.        *     00420072
004201*    FORMAT THE REPORT AND COMPUTE THE TOTALS.              *     00420072
004202*                                                           *     00420072
004300* INPUT:                                                    *     00430072
004400*  1. UNJOURNALIZED RECEIPTS SUMMED BY FISCAL PERIOD WHERE  *     00440072
004410*     THE COST, RETAIL AND QUANTITY HAS BEEN ACCUMULATED.   *     00440072
004500*     (PKMO.AP.APRD030.RCVSUM.FSCLPER(0))                   *     00450072
004501*  2. THE PREVIOUS GENERATION OF THE SAME FILE              *     00440072
004502*     (PKMO.AP.APRD030.RCVSUM.FSCLPER(-1))                  *     00450072
004700*                                                           *     00470072
004800* OUTPUT:                                                   *     00480072
004900*  1. REPORT TITLED 'MONTHLY SUMMARY OF UNJOURNALIZED       *     00490072
005000*     RECEIPTS'                       APKRP046              *     00500072
005100*                                                           *     00510072
005400* NOTE:  THERE IS NO LINE COUNT OR PAGING IN THIS REPORT    *     00540072
005401*        PROGRAM.  IT WAS DESIGNED TO REPORT ON THE MOST    *     00540072
005402*        RECENT 13 FISCAL PERIODS, THEREFORE SHOULD NEVER BE*     00540072
005403*        MORE THAN A PAGE.  TITLES PRINT ONCE.              *     00540072
005404*************************************************************     00550072
005600*--------------------------------------------------------------*          
005700* CHANGE LOG:                                                  *  00570072
005800*                                                              *  00580072
005900* 02/03/97  INITIAL VERSION                                    *  00590072
006000*           BY:  PDVOLK                       WR/PIT 3803      *  00600072
006100*                                                              *  00610072
008298* 06/03/97  MIKE BESKE                                         *  00860018
008299*           CHANGED THE LOGIC THAT DETERMINES WHAT THE         *          
008300*           CURRENT PERIOD IS TO USE THE SCHEDULER DATE REPROED*          
008301*           IN APCC085 RATHER THAN ACCEPTING THE SYSTEM DATE.  *          
008301*           THE DATE FORMAT IS CCYY-MM-DD RATHER THAN JULIAN AS*          
008301*           IT PREVIOUSLY WAS.                                 *          
005800*                                                              *  00580072
005900* 08/22/97  REPLACED THE HARD CODED DATE TABLE WITH COMMON     *  00590072
005800*           DATE ROUTINE, COPYBOOKS DPWS060 AND DPPD060.       *  00580072
006000*           BY:  JOYCE KNOKE                  WR/PIT P102433   *  00600072
      *                                                              *          
      * 11/10/97  CHANGE MADE:CHANGED CALL OF DATE MODULE FOR Y2K    *          
      *           MADE BY:ROBIN BERNOSKI           WR/IR #:5703      *          
      *                                                              *          
      * 06/08/98  CHANGE MADE: Y2K. ACCOUNT FOR ADD TO FISCAL YEAR   *          
      *           WHEN YEAR IS 99 AND SUBTRACT FROM FISCAL YEAR WHEN *          
      *           YEAR IS 00. REMOVE COPYBOOK DPPD060 AND ADDED NEW  *          
      *           CODE FOR DATE ROUTINE.                             *          
      *           MADE BY: CAROLYN GIFFORD         WR/IR #:6041      *          
      *                                                              *          
006200*--------------------------------------------------------------*  00620072
006300   EJECT                                                          00630072
006400 ENVIRONMENT DIVISION.                                            00640072
006500 CONFIGURATION SECTION.                                           00650072
006600 SOURCE-COMPUTER.    IBM-3090.                                    00660072
006700 OBJECT-COMPUTER.    IBM-3090.                                    00670072
006800                                                                  00680072
006900 INPUT-OUTPUT SECTION.                                            00690072
007000 FILE-CONTROL.                                                    00700072
007100     SELECT UNJRN-SUM-FILE       ASSIGN TO UT-S-INP01.            00710072
007101                                                                  00710072
007102     SELECT PREV-UNJRN-SUM       ASSIGN TO UT-S-INP02.            00710072
007103                                                                  00710072
007104     SELECT REPORT-FILE          ASSIGN TO UT-S-RPT01.            00710072
007200 EJECT                                                            00720072
007300 DATA DIVISION.                                                   00730072
007400 FILE SECTION.                                                    00740072
007500                                                                  00750072
007600 FD  UNJRN-SUM-FILE                                               00760072
007700     RECORDING MODE IS F                                          00770072
007800     LABEL RECORDS ARE STANDARD                                   00780072
007900     BLOCK CONTAINS 0 RECORDS                                     00790072
008000     DATA RECORD IS UNJRN-SUM-REC.                                00800072
008100 01  UNJRN-SUM-REC.                                               00810072
008200     05  FILLER                       PIC  X(70).                 00820072
008300                                                                  00830072
008301                                                                  00750072
008302 FD  PREV-UNJRN-SUM                                               00760072
008303     RECORDING MODE IS F                                          00770072
008304     LABEL RECORDS ARE STANDARD                                   00780072
008305     BLOCK CONTAINS 0 RECORDS                                     00790072
008306     DATA RECORD IS PREV-UNJRN-SUM-REC.                           00800072
008307 01  PREV-UNJRN-SUM-REC.                                          00810072
008308     05  FILLER                       PIC  X(70).                 00820072
008309                                                                  00830072
008310 FD  REPORT-FILE                                                     CL**1
008320     RECORDING MODE IS F                                             CL**1
008330     LABEL RECORDS ARE STANDARD                                      CL**1
008340     BLOCK CONTAINS 0 RECORDS                                        CL**1
008350     DATA RECORD IS REPORT-REC.                                      CL**1
008351 01  REPORT-REC                       PIC  X(132).                   CL**1
008352                                                                  00750072
008353                                                                  00830072
008400 EJECT                                                            00840072
008600                                                                  00860072
008700 WORKING-STORAGE SECTION.                                            CL**1
008800 01  PROGRAM-WORKAREA.                                               CL**1
008810     05  FILLER                      PIC  X(31) VALUE                CL**1
008900         '** BEGINNING OF APKRP046 W/S **'.                       00890072
008901     05  PW-PROGRAM-NAME           PIC  X(08)  VALUE 'APKRP046'.          
008902     05  PW-CURRENT-DATE.                                                 
008903         10  PW-CURRENT-YEAR       PIC  9(02)  VALUE ZEROS.               
008904         10  PW-CURRENT-MONTH      PIC  9(02)  VALUE ZEROS.               
008905         10  PW-CURRENT-DAY        PIC  9(02)  VALUE ZEROS.               
008906     05  PW-CURRENT-TIME.                                                 
008907         10  PW-CURRENT-HOUR       PIC  9(02)  VALUE ZEROS.               
008908         10  PW-CURRENT-MINUTE     PIC  9(02)  VALUE ZEROS.               
008909         10  FILLER                PIC  9(02)  VALUE ZEROS.               
018601     05  PW-NEXT-PERIOD            PIC  9(02)    VALUE ZEROS.     01860077
018602     05  PW-FISCAL-CCYYPP.                                        01860077
018603         10  PW-FISCAL-CENTURY     PIC  9(02)    VALUE ZEROS.     01860077
018604         10  PW-FISCAL-YEAR        PIC  9(02)    VALUE ZEROS.     01860077
018605         10  PW-FISCAL-PERIOD      PIC  9(02)    VALUE ZEROS.     01860077
025301                                                                     CL**1
025302 01  PC-PROGRAM-CONSTANTS.                                        01480072
025303     05  PC-PROGRAM-NAME            PIC  X(08)  VALUE 'APKRP046'. 01490072
025305     05  PC-MAX-FP-TO-REPORT        PIC  9(02)  VALUE  13.           CL**1
025306     05  PC-MAX-IND                 PIC  9(02)  VALUE  14.           CL**1
025307                                                                  01550072
017708 01  BATCH-PROCESS-DATE-AREA.                                        CL**1
017709     05  BATCH-PROCESS-DATE-YYYY      PIC X(04) VALUE SPACES.        CL**1
017710     05  FILLER                       PIC X(01) VALUE SPACES.        CL**1
017711     05  BATCH-PROCESS-DATE-MM        PIC X(02) VALUE SPACES.        CL**1
017712     05  FILLER                       PIC X(01) VALUE SPACES.        CL**1
017713     05  BATCH-PROCESS-DATE-DD        PIC X(02) VALUE SPACES.        CL**1
017720     05  FILLER                       PIC X(70) VALUE SPACES.        CL**1
017800*                                                                    CL**1
025308*----------------------------------------------------------------*   CL**4
025309*  THIS ARRAY WILL BE LOADED WITH NON-INVOICED RECEIPT TOTALS    *   CL**4
025310*  FOR THE MOST CURRENT 13 FISCAL MONTHS FROM THE CURRENT FILE   *   CL**4
025320*  AND THE PREVIOUS MONTHS FILE.                                 *   CL**4
025340*  THE LAST OCCURRENCE OF THIS ARRAY WILL BE FILLED WITH THE     *        
025350*  ACCUMULATED TOTALS FOR THE OTHER FISCAL PERIODS. (OTHER       *        
025360*  FISCAL PERIODS ARE FISCAL PERIODS THAT DO NOT FALL INTO       *        
025370*  THE FIRST 13 FISCAL PERIODS.)                                 *        
025380*----------------------------------------------------------------*   CL**4
025390 01  FA-FISCAL-ARRAY.                                                CL**2
025400     05  FA-FISCAL-ENTRIES OCCURS 14 TIMES                           CL**2
025500                           INDEXED BY FA-IND.                        CL**2
025710         10  FA-FISCAL-KEY.                                          CL*16
025720             15  FA-FISCAL-CENTURY  PIC  9(02).                           
025800             15  FA-FISCAL-YEAR     PIC  9(02).                          C
025900             15  FA-FISCAL-PERIOD   PIC  9(02).                          C
               10  FA-FISCAL-KEY2 REDEFINES FA-FISCAL-KEY.                 CL*16
                   15  FA-FISCAL-CCYY     PIC  9(04).                           
                   15  FA-FISCAL-PERIOD2  PIC  9(02).                          C
026000         10  FA-CURR-AMOUNTS.                                             
026100             15  FA-CR-UNJ-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026200             15  FA-CR-UNJ-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026300             15  FA-CR-NIR-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026400             15  FA-CR-NIR-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026500             15  FA-CR-TOT-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026600             15  FA-CR-TOT-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026700         10  FA-PREV-AMOUNTS.                                             
026800             15  FA-PR-UNJ-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
026900             15  FA-PR-UNJ-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027000             15  FA-PR-NIR-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027100             15  FA-PR-NIR-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027200             15  FA-PR-TOT-COST   PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027300             15  FA-PR-TOT-RETAIL PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027400         10  FA-DIFFERENCES.                                              
027500             15  FA-DIF-COST      PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027600             15  FA-DIF-RETAIL    PIC S9(11)V99 VALUE ZERO COMP-3.   CL*16
027601                                                                     CL**2
                                                                                
       01  PERIOD-TABLE.                                                        
          05   PERIOD-ENTRIES   OCCURS 12 TIMES INDEXED BY PA-IND.              
               10 PT-PERIOD-NBR            PIC 9(2).                            
               10 PT-PERIOD-NAME           PIC X(3).                            
                                                                                
027602 01  PS-PROGRAM-SWITCHES.                                         01560072
027603                                                                  01570072
027604     05  PS-OUT-OF-CURR-UNJ-SW       PIC X(01) VALUE 'M'.         01610072
027605         88  OUT-OF-CURR-UNJ                   VALUE 'O'.         01620072
027606         88  MORE-CURR-UNJ                     VALUE 'M'.         01630072
027607     05  PS-OUT-OF-PREV-UNJ-SW       PIC X(01) VALUE 'M'.         01610072
027608         88  OUT-OF-PREV-UNJ                   VALUE 'O'.         01620072
027609         88  MORE-PREV-UNJ                     VALUE 'M'.         01630072
027610     05  PS-FISCAL-PERIOD-SW         PIC X(01) VALUE 'N'.         01670072
027611         88  FP-FOUND                          VALUE 'Y'.         01680072
027612         88  FP-NOT-FOUND                      VALUE 'N'.         01690072
027613     05  PS-FISCAL-ARRAY-SW          PIC X(01) VALUE 'N'.         01670072
027614         88  FA-PERIOD-UNKNOWN                 VALUE 'N'.         01680072
027615         88  FA-PERIOD-FOUND                   VALUE 'Y'.         01680072
027616         88  FA-OTHER-PERIOD                   VALUE 'O'.         01690072
027617     05  PS-DATE-SW                  PIC X(01) VALUE 'N'.         01700072
027618         88  VALID-DATE                        VALUE 'Y'.         01710072
027619         88  NOT-VALID-DATE                    VALUE 'N'.         01720072
027620 EJECT                                                            01790072
027621 01  PA-RECORD-COUNTS.                                            01800072
027622     05  PA-IN-COUNT1-DISP         PIC Z9(09)    VALUE ZEROS.             
027623     05  PA-IN-COUNT2-DISP         PIC Z9(09)    VALUE ZEROS.     01810077
027624                                                                  01850072
027625     05  PA-PREV-UNJ-IN-COUNT      PIC S9(09)    VALUE ZEROS.     01860077
027626     05  PA-CURR-UNJ-IN-COUNT      PIC S9(09)    VALUE ZEROS.     01870077
027627                                                                          
027628 01  PA-ACCUM-TOTALS.                                             01800072
027629     05  PA-SUBT-UNJ-COST           PIC S9(11)V99 VALUE ZEROS.    01810077
027630     05  PA-SUBT-UNJ-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01820077
027631     05  PA-SUBT-NIR-COST           PIC S9(11)V99 VALUE ZEROS.    01860077
027632     05  PA-SUBT-NIR-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01810077
027633     05  PA-SUBT-TOT-COST           PIC S9(11)V99 VALUE ZEROS.    01820077
027634     05  PA-SUBT-TOT-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01860077
027635     05  PA-SUBT-DIF-COST           PIC S9(11)V99 VALUE ZEROS.            
027636     05  PA-SUBT-DIF-RETAIL         PIC S9(11)V99 VALUE ZEROS.            
027637                                                                          
027638     05  PA-GRDT-UNJ-COST           PIC S9(11)V99 VALUE ZEROS.    01810077
027639     05  PA-GRDT-UNJ-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01820077
027640     05  PA-GRDT-NIR-COST           PIC S9(11)V99 VALUE ZEROS.    01860077
027641     05  PA-GRDT-NIR-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01810077
027642     05  PA-GRDT-TOT-COST           PIC S9(11)V99 VALUE ZEROS.    01820077
027643     05  PA-GRDT-TOT-RETAIL         PIC S9(11)V99 VALUE ZEROS.    01860077
027644     05  PA-GRDT-DIF-COST           PIC S9(11)V99 VALUE ZEROS.            
027645     05  PA-GRDT-DIF-RETAIL         PIC S9(11)V99 VALUE ZEROS.            
027646                                                                          
027647 01  HDR-LINE-2.                                                     CL**1
027648     05  FILLER                       PIC  X(46)   VALUE SPACES.     CL**1
027649     05  FILLER                       PIC  X(41)  VALUE              CL**1
027650         'MONTHLY SUMMARY OF UNJOURNALIZED RECEIPTS'.                CL**1
027660     05  FILLER                       PIC  X(44)   VALUE SPACES.     CL**1
027670                                                                     CL**1
027680                                                                     CL**1
027690 01  PRINT-DETAIL-HEADER1.                                           CL**1
027700     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
027800     05  FILLER                       PIC  X(06)    VALUE            CL**1
027900                    '      '.                                             
028000     05  FILLER                       PIC  X(06)    VALUE SPACES.    CL**1
028100     05  FILLER                       PIC  X(22)    VALUE            CL**1
028200                    'UNJOURNALIZED RECEIPTS'.                        CL**1
028300     05  FILLER                       PIC  X(10)    VALUE SPACES.    CL**1
028400     05  FILLER                       PIC  X(21)    VALUE            CL**1
028500                    'NON INVOICED RECEIPTS'.                         CL**1
028600     05  FILLER                       PIC  X(14)    VALUE SPACES.    CL**1
028700     05  FILLER                       PIC  X(14)    VALUE            CL**1
028800                    'TOTAL RECEIPTS'.                                CL**1
028900     05  FILLER                       PIC  X(12)    VALUE SPACES.    CL**1
029000     05  FILLER                       PIC  X(24)    VALUE            CL**1
029100                    'COMPARISON TO PREV MONTH'.                      CL**1
029200     05  FILLER                       PIC  X(02)    VALUE SPACES.    CL**1
029300                                                                     CL**1
029400 01  PRINT-DETAIL-HEADER2.                                           CL**1
029500     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
029600     05  FILLER                       PIC  X(06)    VALUE            CL**1
029700                    'PERIOD'.                                             
029800     05  FILLER                       PIC  X(06)    VALUE SPACES.    CL**1
029900     05  FILLER                       PIC  X(22)    VALUE            CL**1
030000                    'COST           RETAIL '.                        CL**1
030100     05  FILLER                       PIC  X(10)    VALUE SPACES.    CL**1
030200     05  FILLER                       PIC  X(21)    VALUE            CL**1
030300                    'COST           RETAIL'.                         CL**1
030400     05  FILLER                       PIC  X(10)    VALUE SPACES.    CL**1
030500     05  FILLER                       PIC  X(22)    VALUE            CL**1
030600                    'COST            RETAIL'.                        CL**1
030700     05  FILLER                       PIC  X(12)    VALUE SPACES.    CL**1
030800     05  FILLER                       PIC  X(21)    VALUE            CL**1
030900                    'COST           RETAIL'.                         CL**1
031000     05  FILLER                       PIC  X(03)    VALUE SPACES.    CL**1
031100                                                                     CL**1
031200                                                                     CL**1
031300 01  PRINT-SUB-TOT.                                                  CL**1
031400     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
031500     05  FILLER                       PIC  X(28)    VALUE            CL**1
031600                    '* SUB TOTALS                '.                       
031700     05  FILLER                       PIC  X(103)   VALUE SPACES.    CL**1
031800                                                                     CL**1
031900 01  PRINT-OTHER-TOT.                                                CL**1
032000     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
032100     05  FILLER                       PIC  X(28)    VALUE            CL**1
032200                    '* OTHER FISCAL PERIOD TOTALS'.                       
032300     05  FILLER                       PIC  X(103)   VALUE SPACES.    CL**1
032400                                                                     CL**1
032500 01  PRINT-GRAND-TOT.                                                CL**1
032600     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
032700     05  FILLER                       PIC  X(28)    VALUE            CL**1
032800                    '* GRAND TOTALS              '.                       
032900     05  FILLER                       PIC  X(103)   VALUE SPACES.    CL**1
033000                                                                     CL**1
033100 01  PRINT-LINE.                                                     CL**1
033200     05  PL-FISCAL-PERIOD             PIC  X(03).                    CL**1
033300     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
033400     05  PL-FISCAL-YEAR               PIC  X(02).                    CL**1
033500     05  FILLER                       PIC  X(03)    VALUE SPACES.    CL**1
033600     05  PL-UNJ-COST                  PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
033700     05  FILLER                       PIC  X(02)    VALUE SPACES.    CL**1
033800     05  PL-UNJ-RETAIL                PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
033900     05  FILLER                       PIC  X(03)    VALUE SPACES.    CL**1
034000     05  PL-NIR-COST                  PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
034100     05  FILLER                       PIC  X(02)    VALUE SPACES.    CL**1
034200     05  PL-NIR-RETAIL                PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
034300     05  FILLER                       PIC  X(03)    VALUE SPACES.    CL**1
034400     05  PL-TOT-COST                  PIC  ZZ,ZZZ,ZZZ,ZZ9.           CL**1
034500     05  FILLER                       PIC  X(02)    VALUE SPACES.    CL**1
034600     05  PL-TOT-RETAIL                PIC  ZZ,ZZZ,ZZZ,ZZ9.           CL**1
034700     05  FILLER                       PIC  X(01)    VALUE SPACES.    CL**1
034800     05  PL-SIGN-CC1                  PIC  X(01)    VALUE SPACES.    CL**1
034900     05  PL-DIF-COST                  PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
035000     05  PL-SIGN-CC2                  PIC  X(01)    VALUE SPACES.    CL**1
035100     05  PL-SIGN-CR1                  PIC  X(01)    VALUE SPACES.    CL**1
035200     05  PL-DIF-RETAIL                PIC  Z,ZZZ,ZZZ,ZZ9.            CL**1
035300     05  PL-SIGN-CR2                  PIC  X(01)    VALUE SPACES.    CL**1
035400                                                                     CL**1
035500 01  EOR-END-OF-REPORT.                                              CL**1
035600     05  FILLER                  PIC  X(52)  VALUE SPACES.           CL**1
035700     05  FILLER                  PIC  X(80)  VALUE                   CL**1
035701         '*****  END OF REPORT *****'.                               CL**1
035702                                                                  01470072
035703 EJECT                                                            02430072
035704*---------------------------------------------------------------* 02440072
035705*   SUMMARY OF UNJOURNALIZED RECEIPTS BY FISCAL PERIOD          *         
035706*    (THE PREV    FILE WILL BE READ INTO THIS RECORD)           * 02450072
035707*---------------------------------------------------------------* 02460072
035708 01  AP030-PREV-RCPT-SUMMARY.                                             
035709     05  AP-PR-FISCAL-CENTURY         PIC  9(02).                         
035710     05  AP-PR-FISCAL-YEAR            PIC  9(02).                         
035711     05  AP-PR-FISCAL-PERIOD          PIC  9(02).                         
035712     05  AP-PR-RCV-RETAIL-TOTAL       PIC S9(11)V99 COMP-3.               
035713     05  AP-PR-RCV-COST-TOTAL         PIC S9(11)V99 COMP-3.               
035714     05  AP-PR-RCV-ITEM-QTY           PIC S9(07)    COMP-3.               
035715     05  AP-PR-NIR-RETAIL-TOTAL       PIC S9(11)V99 COMP-3.               
035716     05  AP-PR-NIR-COST-TOTAL         PIC S9(11)V99 COMP-3.               
035717     05  AP-PR-NIR-ITEM-QTY           PIC S9(07)    COMP-3.               
035719     05  AP-PR-TOTAL-RETAIL           PIC S9(11)V99 COMP-3.               
035720     05  AP-PR-TOTAL-COST             PIC S9(11)V99 COMP-3.               
035721     05  AP-PR-TOTAL-ITEM-QTY         PIC S9(07)    COMP-3.               
035730     05  FILLER                       PIC  X(10).                         
037000                                                                  03700072
037001*---------------------------------------------------------------  02440072
037002*   SUMMARY OF UNJOURNALIZED RECEIPTS BY FISCAL PERIOD          *         
037003*    (THE CURRENT FILE WILL BE READ INTO THIS RECORD)             02450072
037004*---------------------------------------------------------------  02460072
037005     COPY APRD030.                                                03690072
037006                                                                  03700072
037007*----------------------------------------------------------------*02490072
037008* WORKING STORAGE FOR DATE ROUTINE                                02500072
037009*----------------------------------------------------------------*02510072
037010     COPY DPWS500.                                                02520072
037011 EJECT                                                            02530072
000421*----------------------------------------------------------------*   CL**1
000422*  WORKING STORAGE AREA FOR DPPD060, LOAD DATE TABLE ROUTINE     *   CL**1
000423*----------------------------------------------------------------*   CL**1
000424*    COPY DPWS060.                                                   CL**1
000425 EJECT                                                               CL**1
037012*----------------------------------------------------------------*02490072
037013* WORKING STORAGE FOR STANDARD KOHLS REPORT HEADING               02500072
037014*----------------------------------------------------------------*02510072
037015     COPY DPWS132O.                                                  CL**1
037016                                                                     CL**1
037017*---------------------------------------------------------------  03370072
037018*  ABEND DATA AREAS                                               03380072
037019*---------------------------------------------------------------  03390072
037020 01  ABEND-CODE                  PIC S9(04)  VALUE ZEROS          03400072
037021                                             COMP SYNC.           03410072
037022     88  AC-DATE-ERROR                       VALUE +4019.         03430072
037023                                                                  03440072
037024 01  ABEND-AREAS.                                                 03450072
037025     05  AA-ABEND-LIT            PIC  X(40)  VALUE                03460072
037026             '*****       ABEND'.                                 03470072
037027     05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                03480072
037028             '*****   PROGRAM: APKRP046'.                         03490072
037029     05  AA-PARAGRAPH-LIT.                                        03500072
037030         10  FILLER              PIC  X(17)  VALUE                03510072
037031             '***** PARAGRAPH: '.                                 03520072
037032         10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.        03530072
037033     05  AA-MESSAGE-LINE.                                         03540072
037034         10  FILLER              PIC  X(06)  VALUE '*****'.       03550072
037035         10  AA-MESSAGE          PIC  X(127) VALUE SPACES.        03560072
037036                                                                  03680072
037037     COPY DPWS004.                                                03690072
037038                                                                  03700072
037100 01  FILLER                      PIC  X(25)  VALUE                03710072
037200     '** END OF APKRP046 W/S **'.                                 03720072
037300 EJECT                                                            03730072
037400 PROCEDURE DIVISION.                                              03740072
037500 A100-MAIN.                                                       03750072
037600     PERFORM B100-INITIALIZE.                                     03760072
037700                                                                  03770072
037800     PERFORM B200-PROCESS                                                 
037900         UNTIL OUT-OF-CURR-UNJ                                            
038000         OR    OUT-OF-PREV-UNJ.                                           
038010                                                                          
038100     PERFORM B300-END-PROGRAM.                                    03810072
038200                                                                  03820072
038300     GOBACK.                                                      03830072
038400 EJECT                                                            03840072
038500****************************************************************  03850072
038600*    INITIALIZATION PROCESSING                                    03860072
038700*    -INITIALIZE THE FISCAL ARRAY                                         
038800*    -GET THE CURRENT DATE FOR THE REPORT                                 
039100*    -INITIAL READ AND WRITE HEADINGS                             03910072
039110*    -IF EITHER FILE IS EMPTY END PROGRAM.                        03910072
039200****************************************************************  03920072
039300 B100-INITIALIZE.                                                 03930072
039400                                                                  03940072
039500     OPEN INPUT  UNJRN-SUM-FILE                                   03950072
039501                 PREV-UNJRN-SUM                                           
039502         OUTPUT  REPORT-FILE.                                     03950072
042200                                                                  04220072
042300     PERFORM B110-INIT-FISCAL-ARRAY.                                      
042400                                                                          
042500     ACCEPT PW-CURRENT-DATE FROM DATE.                                    
042600     MOVE PW-CURRENT-MONTH         TO DP132O-RUN-MONTH.                   
042700     MOVE PW-CURRENT-DAY           TO DP132O-RUN-DAY.                     
042800     MOVE PW-CURRENT-YEAR          TO DP132O-RUN-YEAR.                    
042900                                                                          
043000     ACCEPT PW-CURRENT-TIME FROM TIME.                                    
043100     MOVE PW-CURRENT-HOUR          TO DP132O-RUN-HOUR.                    
043200     MOVE PW-CURRENT-MINUTE        TO DP132O-RUN-MINUTE.                  
043300     MOVE PW-PROGRAM-NAME          TO DP132O-PROGRAM-NAME.                
043400     MOVE 1                        TO DP132O-REPORT-NUMBER                
043410                                      DP132O-PAGE-NUMBER.                 
043500                                                                          
001705**** PERFORM DP060-LOAD-DATE-TABLE.                                       
001706                                                                          
001707**   IF (NOT DP060-TABLE-LOADED)                                          
001710**       MOVE 'LOAD DATE TABLE ROUTINE ERROR'                             
001720**                               TO AA-MESSAGE                            
001740**       SET AC-DATE-ERROR       TO TRUE                                  
001750**       PERFORM Z999-ABEND                                               
001760**   END-IF.                                                              
001770                                                                          
044500* THE FIRST READ SHOULD ALWAYS RETURN A RECORD.  IT WOULD HAVE    04450072
044600* TO BE AN EXCEPTIONAL SITUATION IF THERE WERE NO UNJRNLZD        04460072
044700*---------------------------------------------------------------  04470072
044800                                                                  04480077
044900     PERFORM R100-READ-CURR-UNJRNLZD.                             04490077
044910     PERFORM R200-READ-PREV-UNJRNLZD.                             04490077
045000                                                                  04500077
045010     EVALUATE TRUE                                                        
045100     WHEN OUT-OF-CURR-UNJ                                         04510077
045200         DISPLAY '*****************************************'      04520077
045210         DISPLAY 'B100-INITIALIZE'                                04520077
045300         DISPLAY 'EMPTY CURRENT UNJOURNALIZED SUMMARY FILE'       04530077
045301         DISPLAY 'EMPTY CURRENT UNJOURNALIZED SUMMARY FILE'       04530077
045302         DISPLAY 'EMPTY CURRENT UNJOURNALIZED SUMMARY FILE'       04530077
045303     WHEN OUT-OF-PREV-UNJ                                         04510077
045304         DISPLAY '*****************************************'      04520077
045305         DISPLAY 'B100-INITIALIZE'                                04520077
045306         DISPLAY 'EMPTY PREVIOUS MONTHS UNJRNLZD SUMMARY FILE'    04530077
045307         DISPLAY 'EMPTY PREVIOUS MONTHS UNJRNLZD SUMMARY FILE'    04530077
045308     WHEN OTHER                                                           
045309         PERFORM W100-WRITE-HEADINGS                                      
046000     END-EVALUATE.                                                04600077
046100                                                                  04610077
046400 EJECT                                                            04640072
046500                                                                  04650072
046600                                                                  04650072
046700*****************************************************************    CL**6
046800*  DETERMINES THE FISCAL PERIODS AND YEARS TO BE CONTAININED IN *    CL**7
046900*  THE ARRAY BASED ON THE CURRENT DATE:  THE ARRAY WILL CONTAIN *    CL**7
047000*  THE MOST CURRENT 13 FISCAL PERIODS, WHETHER THERE IS DATA    *    CL**7
047100*  FOR THE PERIOD OR NOT.  IF THERE IS NO DATA FOR A PARTICULAR *    CL**7
047110*  PERIOD FILL WITH ZEROS.                                      *    CL**7
047200*****************************************************************    CL**6
047300 B110-INIT-FISCAL-ARRAY.                                             CL**6
047400                                                                     CL**6
047500     MOVE SPACES                    TO PS-DATE-SW.                   CL*13
047600     SET FA-IND, PA-IND             TO 1.                            CL**7
047700     INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.            CL**7
087600     ACCEPT BATCH-PROCESS-DATE-AREA.                                 CL**1
048000                                                                     CL**7
040603     MOVE '-'                    TO BATCH-PROCESS-DATE-AREA(5:1)          
040604                                    BATCH-PROCESS-DATE-AREA(8:1).         
040604                                                                          
048100     SET DPG51-FISCAL-454-CALENDAR                                   CL**6
048200         DPG51-DO-NOT-INCR-DECR-DATE                                 CL**6
048300         DPG52-LK-DTE-ISO-FMT       TO TRUE.                         CL**6
048400     MOVE BATCH-PROCESS-DATE-AREA   TO DPG52-LK-DATE-INPUT.          CL**6
048500                                                                     CL**6
048700                                                                     CL**6
      ******************************************************************   CL**1
      *    CALLS THE DATE ROUTINE MODULE.                              *   CL**1
      ******************************************************************   CL**1
                                                                           CL**1
           CALL DP500-KOHLS-CALENDAR-ROUTINE                               CL*13
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                  CL**1
                                                                           CL**1
                                                                                
           IF DPG54-NO-ERROR                                                    
               MOVE DPG56-FISCAL-YEAR-CCYY TO FA-FISCAL-CCYY(FA-IND)            
049203         MOVE DPG56-FISCAL-PERIOD-NBR                                CL**6
049204                                    TO FA-FISCAL-PERIOD (FA-IND)     CL**6
                                             PT-PERIOD-NBR (PA-IND)             
               MOVE DPG56-FISCAL-PERIOD-NAME-ABBR3 TO                           
                                       PT-PERIOD-NAME (PA-IND)                  
049500     ELSE                                                            CL**6
049600         DISPLAY 'BATCH PRC DATE'  BATCH-PROCESS-DATE-AREA           CL**6
049700         MOVE 'B110-INIT-FISCAL-ARRAY' TO  AA-PARAGRAPH-NAME         CL**6
049800         MOVE 'BAD DATE CONVERSION FOR CURRENT DATE'                 CL**6
049900                                    TO AA-MESSAGE                    CL**6
050000         SET AC-DATE-ERROR          TO TRUE                          CL**6
050100         PERFORM Z999-ABEND                                          CL**6
050200     END-IF.                                                         CL**6
050300                                                                     CL**7
050400     PERFORM WITH TEST AFTER                                         CL**7
050500         UNTIL FA-IND = PC-MAX-IND                                   CL**7
050600                                                                     CL**7
               INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56         CL**7
               MOVE FA-FISCAL-PERIOD (FA-IND) TO DPG52-FISCAL-CC-PRD-PP    CL**7
               MOVE FA-FISCAL-CCYY   (FA-IND) TO DPG52-FISCAL-CC-PRD-YR    CL**7
                                                                                
               SET DPG51-FISCAL-454-CALENDAR                               CL**6
                   DPG51-DECREMENT-DATE                                    CL**6
                   DPG52-LK-DTE-FISC-PRD-CC   TO TRUE                      CL**6
               MOVE 1 TO DPG51-INCR-DECR-FISCAL-PRD-9                           
                                                                                
                                                                           CL**7
               CALL DP500-KOHLS-CALENDAR-ROUTINE                           CL*13
                    USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56              CL**1
                                                                                
               IF NOT DPG54-NO-ERROR                                            
                  DISPLAY 'BATCH PRC DATE'  BATCH-PROCESS-DATE-AREA        CL**6
                  MOVE 'B110-INIT-FISCAL-ARRAY' TO  AA-PARAGRAPH-NAME      CL**6
                  MOVE 'BAD DATE CONVERSION FOR CURRENT DATE'              CL**6
                                              TO AA-MESSAGE                CL**6
                  SET AC-DATE-ERROR          TO TRUE                       CL**6
                  PERFORM Z999-ABEND                                       CL**6
               END-IF                                                      CL**6
                                                                           CL**7
051700         SET FA-IND UP BY 1                                          CL**7
               MOVE DPG56-FISCAL-YEAR-CCYY TO FA-FISCAL-CCYY (FA-IND)           
               MOVE DPG56-FISCAL-PERIOD-NBR TO FA-FISCAL-PERIOD (FA-IND)   CL**7
051901         MOVE ZERO                  TO FA-CR-UNJ-COST   (FA-IND)     CL**6
051902                                       FA-CR-UNJ-RETAIL (FA-IND)          
051903                                       FA-CR-NIR-COST   (FA-IND)          
051904                                       FA-CR-NIR-RETAIL (FA-IND)          
051905                                       FA-CR-TOT-COST   (FA-IND)          
051906                                       FA-CR-TOT-RETAIL (FA-IND)          
051907                                       FA-PR-UNJ-COST   (FA-IND)          
051908                                       FA-PR-UNJ-RETAIL (FA-IND)          
051909                                       FA-PR-NIR-COST   (FA-IND)          
051910                                       FA-PR-NIR-RETAIL (FA-IND)          
051911                                       FA-PR-TOT-COST   (FA-IND)          
051912                                       FA-PR-TOT-RETAIL (FA-IND)          
051913                                       FA-DIF-COST      (FA-IND)          
051914                                       FA-DIF-RETAIL    (FA-IND)          
               IF PA-IND < 12                                                   
                 SET PA-IND UP BY 1                                             
                 MOVE DPG56-FISCAL-PERIOD-NBR TO                                
                                            PT-PERIOD-NBR (PA-IND)              
                 MOVE DPG56-FISCAL-PERIOD-NAME-ABBR3 TO                         
                                            PT-PERIOD-NAME (PA-IND)             
               END-IF                                                           
052100     END-PERFORM.                                                    CL**7
052200                                                                     CL**7
052300                                                                     CL**7
052301*****************************************************************    CL**6
052302*  LOAD THE DATA FROM THE CURRENT SUMMARY FILE INTO THE ARRAY.  *    CL**7
052303*  THEN LOAD THE DATA FORM THE PREVIOUS SUMMARY FILE INTO THE   *    CL**7
052304*  ARRAY.  FORMAT THE THE LINES AND COMPARE THE TWO FILES.      *    CL**7
052305*  COMPUTE AND WRITE THE TOTAL LINES.                           *    CL**7
052306*****************************************************************    CL**6
052400 B200-PROCESS.                                                            
052500                                                                          
052600     SET FA-IND TO 1.                                                     
052700     PERFORM C100-LOAD-CURRENT-FILE                                       
052900         UNTIL OUT-OF-CURR-UNJ.                                   03790072
053000                                                                  03800072
053100     SET FA-IND             TO  1.                                        
053110     SET FA-PERIOD-UNKNOWN  TO  TRUE.                                     
053200     PERFORM C200-LOAD-PREVIOUS-FILE                                      
053400         UNTIL OUT-OF-PREV-UNJ.                                   03790072
053500                                                                  03800072
053600     PERFORM C300-FORMAT-REPORT                                           
053610         WITH TEST AFTER                                                  
053700         VARYING FA-IND FROM 1 BY 1                                       
053800         UNTIL FA-IND = PC-MAX-FP-TO-REPORT.                              
053900                                                                          
054000     PERFORM C400-COMPUTE-TOTALS.                                         
054001                                                                          
054002                                                                  04650072
055300****************************************************************  05530072
055400*    ENDING PROCESSING: DISPLAY COUNTS AND CLOSES FILES.          05540072
055500****************************************************************  05550072
055600*                                                                       05
055700 B300-END-PROGRAM.                                                       0
055800                                                                          
055900     CLOSE  UNJRN-SUM-FILE                                        05590072
056000            PREV-UNJRN-SUM                                                
056100            REPORT-FILE.                                                  
056200                                                                          
056400     MOVE PA-CURR-UNJ-IN-COUNT TO PA-IN-COUNT1-DISP.              05640077
056500     MOVE PA-PREV-UNJ-IN-COUNT TO PA-IN-COUNT2-DISP.              05640077
056800                                                                  05680077
056900     DISPLAY '*****   RECORD COUNTS FROM APKRP046    ******'.     05690072
057000     DISPLAY '---------------------------------------------'.     05700072
057300     DISPLAY 'CURR UNJRNLZD READ     = ' PA-IN-COUNT1-DISP.       05730072
057400     DISPLAY '                                             '.     05740072
057500     DISPLAY 'PREV UNJRNLZD READ     = ' PA-IN-COUNT2-DISP.       05730072
057900     DISPLAY '---------------------------------------------'.     05790072
058000                                                                  05800072
058100 EJECT                                                            05810072
058101****************************************************************  04660072
058102*   -MATCH THE FILES ON FISCAL PERIOD                          *  04680072
058103*    FILES ARE IN DESCENDING FISCAL PERIOD ORDER               *  04680072
058104*   -WRITE THE DATA TO THE CORRESPONDING FISCAL PERIOD         *  04700072
058105*   -ACCUM ALL OTHER FISCAL PERIODS AND REPORT IN OTHER        *  04690072
058106****************************************************************  04760072
076500                                                                          
076501 C100-LOAD-CURRENT-FILE.                                          04770072
076502                                                                          
076503     MOVE AP030-FISCAL-PERIOD  TO  PW-FISCAL-PERIOD.                      
076504     MOVE AP030-FISCAL-YEAR    TO  PW-FISCAL-YEAR.                        
076505     MOVE AP030-FISCAL-CENTURY TO  PW-FISCAL-CENTURY.                     
076506                                                                          
076507     SEARCH FA-FISCAL-ENTRIES                                        CL*13
076508         VARYING FA-IND                                                   
076509         AT END SET FA-OTHER-PERIOD  TO TRUE                         CL*22
076510                                                                     CL*10
076511         WHEN FA-FISCAL-KEY    (FA-IND)                              CL*10
076512              = PW-FISCAL-CCYYPP                                     CL*10
076513                                                                          
076514             MOVE AP030-RCV-COST-TOTAL   TO                               
076515                  FA-CR-UNJ-COST   (FA-IND)                               
076516             MOVE AP030-RCV-RETAIL-TOTAL TO                               
076517                  FA-CR-UNJ-RETAIL (FA-IND)                               
076518             MOVE AP030-NIR-COST-TOTAL   TO                               
076519                  FA-CR-NIR-COST   (FA-IND)                               
076520             MOVE AP030-NIR-RETAIL-TOTAL TO                               
076521                  FA-CR-NIR-RETAIL (FA-IND)                               
076522             MOVE AP030-TOTAL-COST       TO                               
076523                  FA-CR-TOT-COST   (FA-IND)                               
076524             MOVE AP030-TOTAL-RETAIL     TO                               
076525                  FA-CR-TOT-RETAIL (FA-IND)                               
076526     END-SEARCH.                                                          
076527                                                                          
076528     IF FA-OTHER-PERIOD                                                   
076529         SET  FA-IND TO PC-MAX-IND                                        
076530         ADD  AP030-RCV-RETAIL-TOTAL TO FA-CR-UNJ-RETAIL(FA-IND)      0072
076531         ADD  AP030-RCV-COST-TOTAL   TO FA-CR-UNJ-COST  (FA-IND)      0072
076532         ADD  AP030-NIR-RETAIL-TOTAL TO FA-CR-NIR-RETAIL(FA-IND)      0072
076533         ADD  AP030-NIR-COST-TOTAL   TO FA-CR-NIR-COST  (FA-IND)      0072
076534         ADD  AP030-TOTAL-RETAIL     TO FA-CR-TOT-RETAIL(FA-IND)      0072
076535         ADD  AP030-TOTAL-COST       TO FA-CR-TOT-COST  (FA-IND)      0072
076536     END-IF.                                                              
076537                                                                          
076538     PERFORM R100-READ-CURR-UNJRNLZD.                                     
076539                                                                          
076540                                                                          
076541****************************************************************  04660072
076542*   -MATCH THE FILES ON FISCAL PERIOD                          *  04680072
076543*    FILES ARE IN DESCENDING FISCAL PERIOD ORDER               *  04680072
076544*   -WRITE THE DATA TO THE CORRESPONDING FISCAL PERIOD         *  04700072
076545*   -ACCUM ALL OTHER FISCAL PERIODS AND REPORT IN OTHER        *  04690072
076546****************************************************************  04760072
076547 C200-LOAD-PREVIOUS-FILE.                                         04770072
076548                                                                          
076549     MOVE AP-PR-FISCAL-PERIOD  TO  PW-FISCAL-PERIOD.                      
076550     MOVE AP-PR-FISCAL-YEAR    TO  PW-FISCAL-YEAR.                        
076551     MOVE AP-PR-FISCAL-CENTURY TO  PW-FISCAL-CENTURY.                     
076552                                                                          
076553     SEARCH FA-FISCAL-ENTRIES                                        CL*13
076554         VARYING FA-IND                                                   
076555         AT END SET FA-OTHER-PERIOD  TO TRUE                         CL*22
076556                                                                     CL*10
076557         WHEN FA-FISCAL-KEY    (FA-IND)                              CL*10
076558              = PW-FISCAL-CCYYPP                                     CL*10
076559                                                                          
076560             MOVE AP-PR-RCV-COST-TOTAL   TO                               
076561                  FA-PR-UNJ-COST   (FA-IND)                               
076562             MOVE AP-PR-RCV-RETAIL-TOTAL TO                               
076563                  FA-PR-UNJ-RETAIL (FA-IND)                               
076564             MOVE AP-PR-NIR-COST-TOTAL   TO                               
076565                  FA-PR-NIR-COST   (FA-IND)                               
076566             MOVE AP-PR-NIR-RETAIL-TOTAL TO                               
076567                  FA-PR-NIR-RETAIL (FA-IND)                               
076568             MOVE AP-PR-TOTAL-COST       TO                               
076569                  FA-PR-TOT-COST   (FA-IND)                               
076570             MOVE AP-PR-TOTAL-RETAIL     TO                               
076571                  FA-PR-TOT-RETAIL (FA-IND)                               
076572     END-SEARCH.                                                          
076573                                                                          
076574     IF FA-OTHER-PERIOD                                                   
076575         SET  FA-IND TO PC-MAX-IND                                        
076576         ADD  AP-PR-RCV-RETAIL-TOTAL TO FA-PR-UNJ-RETAIL (FA-IND)  0072   
076577         ADD  AP-PR-RCV-COST-TOTAL   TO FA-PR-UNJ-COST   (FA-IND)  0072   
076578         ADD  AP-PR-NIR-RETAIL-TOTAL TO FA-PR-NIR-RETAIL (FA-IND)  0072   
076579         ADD  AP-PR-NIR-COST-TOTAL   TO FA-PR-NIR-COST   (FA-IND)  0072   
076580         ADD  AP-PR-TOTAL-RETAIL     TO FA-PR-TOT-RETAIL (FA-IND)  0072   
076581         ADD  AP-PR-TOTAL-COST       TO FA-PR-TOT-COST   (FA-IND)  0072   
076582     END-IF.                                                              
076583                                                                          
076584     PERFORM R200-READ-PREV-UNJRNLZD.                                     
076585                                                                          
076586                                                                          
076587****************************************************************  04660072
076588*   -COMPARE THE TWO FILES TOTALS TO ARRIVE AT THE DIFFERENCE  *  04680072
076589*   -CONVERT FISCAL PERIOD NUMBER TO ALPHA THRU INTERNAL TABLE *  04700072
076590*    TOTALS ARE ACCUM BY FISCAL PERIOD, BUT REPORTED ON THRU   *          
076600*    CALENDAR MONTHS.  SO FISCAL PERIOD 12-96 IS CALENDAR      *          
076601*    MONTH JAN 97.                                             *          
076602*   -MOVE IN THE FISCAL PERIOD, YEAR AND CENTURY               *  04680072
076603*   -ACCUM SUB TOTALS                                          *          
076604*   -FILL AND WRITE THE PRINT LINE.                            *          
076605****************************************************************  04760072
076606 C300-FORMAT-REPORT.                                                      
076607                                                                          
076608     COMPUTE    FA-DIF-COST   (FA-IND) =                                  
076609                    FA-CR-TOT-COST   (FA-IND) -                           
076610                    FA-PR-TOT-COST   (FA-IND).                            
076611                                                                          
076612     COMPUTE    FA-DIF-RETAIL (FA-IND) =                                  
076613                    FA-CR-TOT-RETAIL (FA-IND) -                           
076614                    FA-PR-TOT-RETAIL (FA-IND).                            
076615                                                                          
076616     IF FA-IND = PC-MAX-IND                                               
076617         MOVE SPACES            TO PL-FISCAL-YEAR                         
076618                                   PL-FISCAL-PERIOD                       
076619         WRITE REPORT-REC       FROM PRINT-OTHER-TOT AFTER 2              
076620     ELSE                                                                 
076621         SET FP-NOT-FOUND           TO TRUE                               
076623         PERFORM WITH TEST BEFORE                                         
001800**           VARYING DP060-IDX FROM 1 BY 1                                
001900**           UNTIL DP060-IDX > 12                                         
001800             VARYING PA-IND FROM 1 BY 1                                   
001900             UNTIL PA-IND > 12                                            
076625               OR    FP-FOUND                                             
076626**           IF DP060-FISCAL-MONTH-N (DP060-IDX) =                        
076626             IF PT-PERIOD-NBR (PA-IND) =                                  
076626                                     FA-FISCAL-PERIOD(FA-IND)             
076627**               MOVE DP060-MONTH-NAME-ABBR (DP060-IDX) TO                
076627                 MOVE PT-PERIOD-NAME (PA-IND) TO                          
076627                                     PL-FISCAL-PERIOD                     
076628                 SET FP-FOUND TO TRUE                                     
076629             END-IF                                                       
076631         END-PERFORM                                                      
076632                                                                          
076633         IF FP-NOT-FOUND                                                  
076634             MOVE 'INTERNAL TABLE LOOKUP ERROR'  TO                       
076635                                       AA-MESSAGE-LINE                    
076636             MOVE 'C300-FORMAT-REPORT'           TO                       
076637                                       AA-PARAGRAPH-NAME                  
076638             PERFORM Z999-ABEND                                           
076639         END-IF                                                           
076640                                                                          
076641         IF FA-FISCAL-PERIOD(FA-IND) = 12                                 
                   IF FA-FISCAL-YEAR(FA-IND) = 99                               
                      MOVE 00 TO FA-FISCAL-YEAR(FA-IND)                         
                      MOVE 20 TO FA-FISCAL-CENTURY(FA-IND)                      
                   ELSE                                                         
076642                ADD 1 TO FA-FISCAL-YEAR(FA-IND)                           
                   END-IF                                                       
076643         END-IF                                                           
076644                                                                          
076645         MOVE FA-FISCAL-YEAR   (FA-IND)  TO  PL-FISCAL-YEAR               
076646                                                                          
076647         ADD  FA-CR-UNJ-COST   (FA-IND)  TO  PA-SUBT-UNJ-COST             
076648         ADD  FA-CR-UNJ-RETAIL (FA-IND)  TO  PA-SUBT-UNJ-RETAIL           
076649         ADD  FA-CR-NIR-COST   (FA-IND)  TO  PA-SUBT-NIR-COST             
076650         ADD  FA-CR-NIR-RETAIL (FA-IND)  TO  PA-SUBT-NIR-RETAIL           
076651         ADD  FA-CR-TOT-COST   (FA-IND)  TO  PA-SUBT-TOT-COST             
076652         ADD  FA-CR-TOT-RETAIL (FA-IND)  TO  PA-SUBT-TOT-RETAIL           
076653         ADD  FA-DIF-COST      (FA-IND)  TO  PA-SUBT-DIF-COST             
076654         ADD  FA-DIF-RETAIL    (FA-IND)  TO  PA-SUBT-DIF-RETAIL           
076655     END-IF.                                                              
076656                                                                          
076657     IF FA-DIF-COST   (FA-IND)  < 0                                       
076658         MOVE '('   TO PL-SIGN-CC1                                        
076659         MOVE ')'   TO PL-SIGN-CC2                                        
076660     END-IF.                                                              
076661                                                                          
076662     IF FA-DIF-RETAIL (FA-IND)  < 0                                       
076663         MOVE '('   TO PL-SIGN-CR1                                        
076664         MOVE ')'   TO PL-SIGN-CR2                                        
076665     END-IF.                                                              
076666                                                                          
076667     MOVE FA-CR-UNJ-COST    (FA-IND)  TO  PL-UNJ-COST.                    
076668     MOVE FA-CR-UNJ-RETAIL  (FA-IND)  TO  PL-UNJ-RETAIL.                  
076669     MOVE FA-CR-NIR-COST    (FA-IND)  TO  PL-NIR-COST.                    
076670     MOVE FA-CR-NIR-RETAIL  (FA-IND)  TO  PL-NIR-RETAIL.                  
076671     MOVE FA-CR-TOT-COST    (FA-IND)  TO  PL-TOT-COST.                    
076672     MOVE FA-CR-TOT-RETAIL  (FA-IND)  TO  PL-TOT-RETAIL.                  
076673     MOVE FA-DIF-COST       (FA-IND)  TO  PL-DIF-COST.                    
076674     MOVE FA-DIF-RETAIL     (FA-IND)  TO  PL-DIF-RETAIL.                  
076675     WRITE REPORT-REC FROM PRINT-LINE.                                    
076676                                                                          
076677     MOVE SPACES TO   PL-SIGN-CC1                                         
076678                      PL-SIGN-CC2                                         
076679                      PL-SIGN-CR1                                         
076680                      PL-SIGN-CR2.                                        
076681                                                                          
076682****************************************************************  04660072
076683*   -WRITE OUT THE SUB TOTALS                                  *  04680072
076684*   -FORMAT THE REPORT TO WRITE OUT THE 'OTHER' TOTALS         *  04680072
076685*   -COMPUTE THE GRAND.                                        *  04700072
076686*   -ACCUM SUB TOTALS                                          *          
076687*   -FILL AND WRITE THE PRINT LINE.                            *          
076688****************************************************************  04760072
076689 C400-COMPUTE-TOTALS.                                                     
076690                                                                          
076691     PERFORM W300-WRITE-SUB-TOTALS.                                       
076692                                                                          
076693     SET FA-IND   TO PC-MAX-IND.                                          
076694     PERFORM C300-FORMAT-REPORT.                                          
076695                                                                          
076696     COMPUTE    PA-GRDT-UNJ-COST        =                                 
076697                PA-SUBT-UNJ-COST    +  FA-CR-UNJ-COST  (FA-IND)           
076698     COMPUTE    PA-GRDT-UNJ-RETAIL      =                                 
076699                PA-SUBT-UNJ-RETAIL  +  FA-CR-UNJ-RETAIL(FA-IND)           
076700     COMPUTE    PA-GRDT-NIR-COST        =                                 
076701                PA-SUBT-NIR-COST    +  FA-CR-NIR-COST  (FA-IND)           
076702     COMPUTE    PA-GRDT-NIR-RETAIL      =                                 
076703                PA-SUBT-NIR-RETAIL  +  FA-CR-NIR-RETAIL(FA-IND)           
076704     COMPUTE    PA-GRDT-TOT-COST        =                                 
076705                PA-SUBT-TOT-COST    +  FA-CR-TOT-COST  (FA-IND)           
076706     COMPUTE    PA-GRDT-TOT-RETAIL      =                                 
076707                PA-SUBT-TOT-RETAIL  +  FA-CR-TOT-RETAIL(FA-IND)           
076708     COMPUTE    PA-GRDT-DIF-COST       =                                  
076709                PA-SUBT-DIF-COST    +  FA-DIF-COST     (FA-IND)           
076710     COMPUTE    PA-GRDT-DIF-RETAIL     =                                  
076711                PA-SUBT-DIF-RETAIL  +  FA-DIF-RETAIL   (FA-IND)           
076712                                                                          
076713     PERFORM W400-WRITE-GRAND-TOTALS.                                     
076714                                                                          
076715                                                                  05520072
076730 EJECT                                                               CL**1
076740                                                                          
087300****************************************************************  08730072
003338*    LOAD DATE TABLE ROUTINE                                         CL**1
087300****************************************************************  08730072
003340*    COPY DPPD060.                                                   CL**1
076730 EJECT                                                               CL**1
003342                                                                     CL**5
087300****************************************************************  08730072
087400*  I/O  SECTION                                                   08740072
087500****************************************************************  08750072
087600                                                                  08720072
131102****************************************************************  13000072
131103*    READS THE CURRENT UNJRNLZD SUM FILE                          13010072
131104****************************************************************  13000072
131105 R100-READ-CURR-UNJRNLZD.                                         13030072
131106                                                                  13040072
131107     READ UNJRN-SUM-FILE                                          13050072
131108         INTO AP030-UNJ-NI-RCPT-SUMMARY                           13060072
131109         AT END SET OUT-OF-CURR-UNJ TO TRUE.                      13070072
131110                                                                  13080072
131111     IF MORE-CURR-UNJ                                             13090072
131112         ADD 1 TO PA-CURR-UNJ-IN-COUNT                            13100072
131113     END-IF.                                                      13110072
131114                                                                  08720072
131115****************************************************************  13000072
131116*    READS THE PREVIOUS UNJRNLZD SUM FILE                         13010072
131117****************************************************************  13000072
131118                                                                  13020072
131119 R200-READ-PREV-UNJRNLZD.                                         13030072
131120                                                                  13040072
131121     READ PREV-UNJRN-SUM                                          13050072
131122         INTO AP030-PREV-RCPT-SUMMARY                             13060072
131123         AT END SET OUT-OF-PREV-UNJ TO TRUE.                      13070072
131124                                                                  13080072
131125     IF MORE-PREV-UNJ                                             13090072
131126         ADD 1 TO PA-PREV-UNJ-IN-COUNT                            13100072
131127     END-IF.                                                      13110072
131200                                                                  13111078
131300                                                                  05820072
131400 W100-WRITE-HEADINGS.                                                     
131500     WRITE REPORT-REC FROM                                                
131600                      DP132O-STANDRD-HEADER-132-COLS.                     
131700     WRITE REPORT-REC FROM HDR-LINE-2 AFTER 2.                            
131800     WRITE REPORT-REC FROM PRINT-DETAIL-HEADER1 AFTER 2.                  
131900     WRITE REPORT-REC FROM PRINT-DETAIL-HEADER2 AFTER 1.                  
132000     MOVE SPACES TO REPORT-REC.                                           
132100     WRITE REPORT-REC AFTER 2.                                            
132200                                                                          
132300                                                                          
132301****************************************************************  13000072
132302*    WRITE OUT THE SUB TOTALS                                     13010072
132303****************************************************************  13000072
132400 W300-WRITE-SUB-TOTALS.                                                   
132500*                                                                         
132600     MOVE SPACES                     TO PL-FISCAL-PERIOD                  
132700                                        PL-FISCAL-YEAR.                   
132800     WRITE REPORT-REC  FROM PRINT-SUB-TOT AFTER 2.                        
132900                                                                          
133000     MOVE PA-SUBT-UNJ-COST           TO  PL-UNJ-COST.                     
133100     MOVE PA-SUBT-UNJ-RETAIL         TO  PL-UNJ-RETAIL.                   
133200     MOVE PA-SUBT-NIR-COST           TO  PL-NIR-COST.                     
133300     MOVE PA-SUBT-NIR-RETAIL         TO  PL-NIR-RETAIL.                   
133400     MOVE PA-SUBT-TOT-COST           TO  PL-TOT-COST.                     
133500     MOVE PA-SUBT-TOT-RETAIL         TO  PL-TOT-RETAIL.                   
133600     MOVE PA-SUBT-DIF-COST           TO  PL-DIF-COST.                     
133700     MOVE PA-SUBT-DIF-RETAIL         TO  PL-DIF-RETAIL.                   
133800                                                                          
133900     WRITE REPORT-REC FROM PRINT-LINE.                                    
134000                                                                          
134100                                                                          
134101****************************************************************  13000072
134102*    WRITE OUT THE GRAND TOTALS                                   13010072
134103****************************************************************  13000072
134200 W400-WRITE-GRAND-TOTALS.                                                 
134300                                                                          
134400     WRITE REPORT-REC  FROM PRINT-GRAND-TOT AFTER 2.                      
134500     MOVE PA-GRDT-UNJ-COST           TO PL-UNJ-COST.                      
134600     MOVE PA-GRDT-UNJ-RETAIL         TO PL-UNJ-RETAIL.                    
134700     MOVE PA-GRDT-NIR-COST           TO PL-NIR-COST.                      
134800     MOVE PA-GRDT-NIR-RETAIL         TO PL-NIR-RETAIL.                    
134900     MOVE PA-GRDT-TOT-COST           TO PL-TOT-COST.                      
135000     MOVE PA-GRDT-TOT-RETAIL         TO PL-TOT-RETAIL.                    
135100     MOVE PA-GRDT-DIF-COST           TO PL-DIF-COST.                      
135200     MOVE PA-GRDT-DIF-RETAIL         TO PL-DIF-RETAIL.                    
135300     WRITE REPORT-REC  FROM PRINT-LINE.                                   
135400                                                                          
135500     WRITE REPORT-REC  FROM EOR-END-OF-REPORT AFTER 2.                    
135600                                                                          
170100*---------------------------------------------------------------  16990072
170200*    ABEND ROUTINE                                                17000072
170300*---------------------------------------------------------------  17010072
170400 Z999-ABEND.                                                      17020072
170500                                                                  17030072
170600     CLOSE  UNJRN-SUM-FILE                                        17040072
170700            PREV-UNJRN-SUM                                                
170701            REPORT-FILE.                                                  
170702                                                                  17050072
170800     DISPLAY AA-ABEND-LIT.                                        17060072
170900     DISPLAY AA-PROGRAM-LIT.                                      17070072
171000     DISPLAY AA-PARAGRAPH-LIT.                                    17080072
171100     DISPLAY AA-MESSAGE-LINE.                                     17090072
171200     CALL 'ILBOABN0' USING ABEND-CODE.                            17100072
