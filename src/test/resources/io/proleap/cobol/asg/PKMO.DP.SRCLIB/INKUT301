       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     INKUT301.                                                
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   09/24/2006.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *                                                                         
      *PURPOSE: THIS PROCESS EXTRACTS TINENTY DATA AND REFORMATS AS A           
      *         TADJADD MASS ADD FORMAT AND INSERTS DATA. THE INPUT CC          
      *         LISTS THE LOC, SOURCE TABLE, DEST TABLE AND ADJUST TYPE         
      *         TADJADD MASS ADD FORMAT AND INSERTS DATA                        
      *             //*-5----0----5----0----5----0----5----0                    
      *             0196 TINENTY  TADJADD  MASSADD                              
      *             0197 TINENTY  TADJADD  MASSDEL                              
      *                                                                         
      * NOTE:  THIS PROCESS ASSUMES UNIT BOOKING HAS ALREADY OCCURRED           
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      * W28545   09-25-2006  CREATE A PROCESS TO PULL FROM SHEET DB AND         
      *                      INSERT RECORDS INTO TADJADD. FASHION SALES         
      *                      SALES ADJUST ALSO CREATED. ADDS OR DELETES         
      *                      BASED ON INPUT CONTROL CARD. INIT INSTALL          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATABASE-UNLOAD-CONTROL                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT FASHION-ADJUSTMENTS                                           
               ASSIGN TO OUT01.                                                 
                                                                                
      *    SELECT ADJUSTMENT-REPORT                                             
      *        ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DATABASE-UNLOAD-CONTROL                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATABASE-UNLOAD-REC  PIC X(80).                                      
                                                                                
       FD  FASHION-ADJUSTMENTS                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  FASHION-ADJUSTMENTS-RECORD    PIC X(200).                            
                                                                                
      *FD  ADJUSTMENT-REPORT                                                    
      *    LABEL RECORDS ARE STANDARD                                           
      *    RECORDING MODE IS F                                                  
      *    BLOCK CONTAINS 0 RECORDS.                                            
      *01  ADJUSTMENT-RPT-RECORD         PIC X(80).                             
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  DATABASE-LAYOUT-FORMAT.                                              
           05 TINENTY-DB-FORMAT.                                                
               10 TINENTY-STR-NBR                PIC  S9(4) COMP.               
               10 TINENTY-SHEET-TYP-CDE          PIC   X(1).                    
               10 TINENTY-SHEET-NBR              PIC  S9(9) COMP.               
               10 TINENTY-LNE-NBR                PIC  S9(4) COMP.               
               10 TINENTY-SKU-NBR                PIC  S9(8)V COMP-3.            
               10 TINENTY-UPC-NBR                PIC  S9(15)V COMP-3.           
               10 TINENTY-INV-QTY                PIC  S9(9) COMP.               
               10 TINENTY-UNT-RTL-AMT            PIC  S9(5)V9(2) COMP-3.        
               10 TINENTY-EXTD-RTL-AMT           PIC  S9(9)V9(2) COMP-3.        
               10 TINENTY-LAST-UPD-TMST          PIC   X(26).                   
               10 TINENTY-LAST-UPD-BY-USR-ID     PIC   X(8).                    
                                                                                
           05 TADJADD-DB-FORMAT.                                                
               10 TADJADD-LOC-NBR                PIC  S9(4) COMP.               
               10 TADJADD-AREA-NBR               PIC  S9(9) COMP.               
               10 TADJADD-SKU-NBR                PIC  S9(8)V COMP-3.            
               10 TADJADD-UPC-NBR                PIC  S9(15)V COMP-3.           
               10 TADJADD-ADJ-UNIT-PR-AMT        PIC  S9(5)V9(2) COMP-3.        
               10 TADJADD-ADJ-QTY                PIC  S9(9) COMP.               
               10 TADJADD-MISSED-MD-IND          PIC   X(1).                    
               10 TADJADD-DKEY-NBR               PIC  S9(9) COMP.               
               10 TADJADD-CHG-TMST               PIC   X(26).                   
               10 TADJADD-CHG-ID-NBR             PIC   X(8).                    
               10 TADJADD-ADJ-EXTD-AMT           PIC  S9(9)V9(2) COMP-3.        
               10 TADJADD-ADJ-RSN-TXT            PIC   X(20).                   
                                                                                
       01  PS-PROGRAM-CONSTANTS.                                                
           05  PC-11                    PIC  S9(04) COMP VALUE 11.              
           05  PC-MAX-RETRIES            PIC S9(03) VALUE +9                    
                                                    COMP SYNC.                  
           05  PC-ONE-BILLION       PIC S9(11) COMP-3 VALUE +1000000000.        
           05  PC-PRIME1            PIC S9(05) COMP-3 VALUE +38543.             
           05  PC-PRIME2            PIC S9(05) COMP-3 VALUE +58889.             
           05  PC-AREA-888888       PIC S9(9)  COMP   VALUE 888888.             
           05  PC-AREA-999999       PIC S9(9)  COMP   VALUE 999999.             
           05  PC-AREA-777777       PIC S9(9)  COMP   VALUE 777777.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DATA-SW               PIC X(01)   VALUE 'N'.                  
               88  PS-END-OF-DATA                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-DATA               VALUE 'N'.                  
           05  PS-CONTROL-SW            PIC X(01)   VALUE 'N'.                  
               88  PS-END-OF-CONTROL                VALUE 'Y'.                  
               88  PS-NOT-END-OF-CONTROL            VALUE 'N'.                  
           05  PS-INSERT-SW             PIC X(01)   VALUE 'N'.                  
               88  PS-INSERT-DONE                   VALUE 'Y'.                  
               88  PS-INSERT-NOT-DONE               VALUE 'N'.                  
           05  PS-VALID-LOC-SW         PIC X        VALUE 'N'.                  
               88  PS-VALID-LOCATION                VALUE 'Y'.                  
               88  PS-INVALID-LOCATION              VALUE 'N'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY           PIC 9(11)  VALUE ZERO.                      
           05  PV-ADD-NUM           PIC S9(04) COMP VALUE ZERO.                 
           05  PV-RANDOM-NUMBER     PIC S9(11) COMP-3 VALUE +0.                 
           05  PV-ADJ-QTY           PIC S9(9)  COMP VALUE ZERO.                 
           05  PV-WRITE-COUNT       PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-TADJADD-COUNT     PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-READ-COUNT        PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-WRITE-NEG-COUNT   PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-TADJADD-NEG-COUNT PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-READ-NEG-COUNT    PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-TRAILER-COUNT     PIC S9(9)  COMP-3 VALUE ZERO.               
           05  PV-RETRY-COUNT       PIC S9(04) COMP   VALUE +0.                 
           05  PV-PROGRAM-NAME      PIC X(08)  VALUE 'INKUT301'.                
           05  PV-PARAGRAPH-NAME    PIC X(30)  VALUE SPACE.                     
           05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)   VALUE SPACE.              
           05  PV-DB2-OPER-ATTEMPTED PIC X(30)  VALUE SPACE.                    
           05  PV-SKU-NUMBER        PIC  S9(8)V COMP-3.                         
           05  PV-SKU-NBR           PIC 9(8).                                   
           05  PV-SKU-NBR-ALPH REDEFINES                                        
               PV-SKU-NBR           PIC X(8).                                   
           05  PV-SEED              PIC S9(11) COMP-3 VALUE ZERO.               
           05  PV-WORK-FIELD        PIC S9(17) COMP-3 VALUE ZERO.               
           05  PV-WORK-FIELD2       PIC S9(17) COMP-3 VALUE ZERO.               
           05  PV-TODAYS-DATE       PIC 9(06)  VALUE ZERO.                      
           05  PV-TODAYS-TIME       PIC 9(08)  VALUE ZERO.                      
           05  FILLER REDEFINES PV-TODAYS-TIME.                                 
               10  PV-HH            PIC 9(02).                                  
               10  PV-MIN           PIC 9(02).                                  
               10  PV-SEC           PIC 9(02).                                  
               10  PV-HUND-SEC      PIC 9(02).                                  
           05  PV-INITIAL-SEED      PIC 9(08)    VALUE ZERO.                    
           05  FILLER REDEFINES PV-INITIAL-SEED.                                
               10  PV-SEED-BYTE-1-2 PIC 9(02).                                  
               10  PV-SEED-BYTE-3-4 PIC 9(02).                                  
               10  PV-SEED-BYTE-5-6 PIC 9(02).                                  
               10  PV-SEED-BYTE-7-8 PIC 9(02).                                  
                                                                                
           05  PV-LOC-NBR              PIC S9(4)    VALUE ZERO COMP.            
                                                                                
       01  ABEND-CODE               PIC S9(4)  COMP VALUE ZERO.                 
                                                                                
       01  CC-CONTROL-OPTION.                                                   
           05  CC-LOC-NBR                  PIC X(04)   VALUE SPACES.            
           05  CC-LOC-NUMBER REDEFINES                                          
               CC-LOC-NBR                  PIC 9(04).                           
           05  FILLER                      PIC X(01)   VALUE SPACES.            
           05  CC-DCLGEN-EXTRACT-LAYOUT    PIC X(08)   VALUE SPACES.            
           05  FILLER                      PIC X(01)   VALUE SPACES.            
           05  CC-DCLGEN-UPDATE-LAYOUT     PIC X(08)   VALUE SPACES.            
           05  FILLER                      PIC X(01)   VALUE SPACES.            
           05  CC-ADJUSTMENT-DESC          PIC X(07)   VALUE SPACES.            
               88  ADJ-MASS-ADD                        VALUE 'MASSADD'.         
               88  ADJ-MASS-DEL                        VALUE 'MASSDEL'.         
           05  FILLER                      PIC X(53)   VALUE SPACES.            
                                                                                
      ******************************************************************        
      *01  DP004-DB2-ERROR-FIELDS                                               
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TINENTY                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINENTY                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TADJADD                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TADJADD                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TSKXREF                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TINENTY CURSOR                                                          
      * THIS CURSOR WILL RETRIEVE ROWS FROM TINENTY                             
      *****************************************************************         
           EXEC SQL                                                             
               DECLARE TINENTY_CSR CURSOR FOR                                   
                SELECT   STR_NBR                                                
                        ,SHEET_TYP_CDE                                          
                        ,SHEET_NBR                                              
                        ,LNE_NBR                                                
                        ,SKU_NBR                                                
                        ,UPC_NBR                                                
                        ,INV_QTY                                                
                        ,UNT_RTL_AMT                                            
                        ,EXTD_RTL_AMT                                           
                        ,LAST_UPD_TMST                                          
                        ,LAST_UPD_BY_USR_ID                                     
                  FROM  TINENTY                                                 
                 WHERE  STR_NBR    =  :PV-LOC-NBR                               
            END-EXEC.                                                           
                                                                                
      ***  FASHION SALES LAYOUT                                                 
           COPY FSRD035.                                                        
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-DATA                                            
                   UNTIL PS-END-OF-DATA.                                        
                                                                                
           PERFORM 9000-EOJ-TASKS.                                              
                                                                                
       99999-EXIT-PROGRAM.                                                      
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * 1000-INITIALIZATION.                                                    
      *****************************************************************         
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  DATABASE-UNLOAD-CONTROL                                  
                OUTPUT FASHION-ADJUSTMENTS.                                     
      *                ADJUSTMENT-REPORT.                                       
                                                                                
           ACCEPT PV-TODAYS-DATE    FROM DATE.                                  
           ACCEPT PV-TODAYS-TIME    FROM TIME.                                  
                                                                                
           PERFORM 8200-READ-INPUT.                                             
           DISPLAY ' CONTROL CARD: '.                                           
           DISPLAY '   ----+----1----+----2----+----3----+----4----+'.          
           DISPLAY '   ' CC-CONTROL-OPTION.                                     
                                                                                
                                                                                
      /****************************************************************         
      * 2000-PROCESS-DATA.                                                      
      *    PROCESS INVENTORY ADJUSTMENTS.                                       
      *****************************************************************         
       2000-PROCESS-DATA.                                                       
                                                                                
           IF CC-DCLGEN-EXTRACT-LAYOUT = 'TINENTY'                              
               PERFORM 3000-PROCESS-TINENTY                                     
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 3000-PROCEE-TINENTY.                                                    
      *    PROCESS THE INVENTORY ADJUSTMENTS FLAT FILE.                         
      *****************************************************************         
       3000-PROCESS-TINENTY.                                                    
                                                                                
           MOVE CC-LOC-NUMBER      TO PV-LOC-NBR.                               
                                                                                
           PERFORM 3100-OPEN-TINENTY-CSR                                        
           PERFORM 3200-FETCH-TINENTY-CSR                                       
           PERFORM 3300-PROCESS-TINENTY-CSR                                     
               UNTIL PS-END-OF-DATA                                             
           PERFORM 3400-CLOSE-TINENTY-CSR.                                      
                                                                                
                                                                                
      ******************************************************************        
      *    OPEN THE TINENTY CURSOR                                              
      ******************************************************************        
       3100-OPEN-TINENTY-CSR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN TINENTY_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINENTY_CSR' TO PV-DB2-OPER-ATTEMPTED                 
               MOVE '3100-OPEN-TINENTY-CSR' TO PV-PARAGRAPH-NAME                
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    FETCH ROWS FROM TINENTY-CURSOR                                       
      ******************************************************************        
       3200-FETCH-TINENTY-CSR.                                                  
                                                                                
           INITIALIZE DCLTINENTY.                                               
                                                                                
           EXEC SQL                                                             
              FETCH TINENTY_CSR                                                 
               INTO   :INENTY-STR-NBR                                           
                     ,:INENTY-SHEET-TYP-CDE                                     
                     ,:INENTY-SHEET-NBR                                         
                     ,:INENTY-LNE-NBR                                           
                     ,:INENTY-SKU-NBR                                           
                     ,:INENTY-UPC-NBR                                           
                     ,:INENTY-INV-QTY                                           
                     ,:INENTY-UNT-RTL-AMT                                       
                     ,:INENTY-EXTD-RTL-AMT                                      
                     ,:INENTY-LAST-UPD-TMST                                     
                     ,:INENTY-LAST-UPD-BY-USR-ID                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
                 CONTINUE                                                       
             WHEN SQLCODE = +100                                                
                 SET PS-END-OF-DATA TO TRUE                                     
             WHEN OTHER                                                         
                 MOVE 'FETCH TINENTY CURSOR'                                    
                               TO  PV-DB2-OPER-ATTEMPTED                        
                 MOVE '3200-FETCH-TINENTY-CSR' TO PV-PARAGRAPH-NAME             
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    PROCESS ALL ROWS FROM TINENTY CURSOR                                 
      *                                                                         
      *   THIS PARAGRAPH WILL PROCESS EACH TINENTY CURSOR ROW.                  
      *   EACH ROW IS INSERTED TO TADJADD. SIGN IS BASED ON INPUT-CC.           
      *   MASSADD WILL MOVE AS IS. MASSDEL WILL BE NEGATED BEFORE MOVE          
      *                                                                         
      ******************************************************************        
       3300-PROCESS-TINENTY-CSR.                                                
                                                                                
           PERFORM 4000-MOVE-DATA-FIELDS.                                       
           PERFORM 4100-INSERT-TADJADD.                                         
           PERFORM 4200-CREATE-FASHION-ADJ.                                     
      *    PERFORM 4000-WRITE-INV-OUT-REC.                                      
                                                                                
           PERFORM 3200-FETCH-TINENTY-CSR.                                      
                                                                                
      *                                                                         
      ******************************************************************        
      *    CLOSE TINENTY CURSOR                                                 
      ******************************************************************        
       3400-CLOSE-TINENTY-CSR.                                                  
                                                                                
           EXEC SQL                                                             
              CLOSE TINENTY_CSR                                                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE TINENTY_CSR' TO PV-DB2-OPER-ATTEMPTED                
               MOVE '3400-CLOSE-TINENTY-CSR' TO PV-PARAGRAPH-NAME               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    MOVE TO TADJADD FIELD                                                
      ******************************************************************        
        4000-MOVE-DATA-FIELDS.                                                  
                                                                                
           INITIALIZE DCLTADJADD.                                               
                                                                                
           MOVE INENTY-STR-NBR        TO    ADJADD-LOC-NBR                      
           MOVE INENTY-SHEET-NBR      TO    ADJADD-AREA-NBR                     
           MOVE INENTY-SKU-NBR        TO    ADJADD-SKU-NBR                      
           MOVE INENTY-UPC-NBR        TO    ADJADD-UPC-NBR                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN ADJ-MASS-DEL                                                
                   COMPUTE ADJADD-ADJ-QTY  =   INENTY-INV-QTY * -1              
                   MOVE 'SHEET MASS DEL' TO    ADJADD-ADJ-RSN-TXT               
               WHEN ADJ-MASS-ADD                                                
                   COMPUTE ADJADD-ADJ-QTY  =   INENTY-INV-QTY                   
                   MOVE 'SHEET MASS ADD' TO    ADJADD-ADJ-RSN-TXT               
           END-EVALUATE.                                                        
                                                                                
           MOVE INENTY-UNT-RTL-AMT    TO    ADJADD-ADJ-UNIT-PR-AMT              
           COMPUTE ADJADD-ADJ-EXTD-AMT =   (ADJADD-ADJ-UNIT-PR-AMT *            
                                            ADJADD-ADJ-QTY)                     
           MOVE SPACES                TO    ADJADD-MISSED-MD-IND                
           MOVE 'INKUT301'            TO    ADJADD-CHG-ID-NBR                   
                                                                                
      *** GENERATE A RANDOM NUMBER FOR THE DKEY.....                            
           ADD PC-11                TO  PV-ADD-NUM.                             
           ACCEPT PV-TODAYS-TIME    FROM TIME.                                  
           COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-TIME                           
                                    + PV-ADD-NUM)                               
                                    * 11.                                       
           MOVE PV-HUND-SEC         TO PV-SEED-BYTE-1-2.                        
           MOVE PV-SEC              TO PV-SEED-BYTE-3-4.                        
           MOVE PV-MIN              TO PV-SEED-BYTE-5-6.                        
           MOVE PV-HH               TO PV-SEED-BYTE-7-8.                        
           MOVE PV-INITIAL-SEED     TO PV-SEED.                                 
           COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)                       
                                 * PC-PRIME2).                                  
           DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                              
               GIVING PV-WORK-FIELD2                                            
               REMAINDER PV-RANDOM-NUMBER.                                      
           MOVE PV-RANDOM-NUMBER    TO ADJADD-DKEY-NBR.                         
                                                                                
      ******************************************************************        
      *  INSERT TADJADD                                                         
      ******************************************************************        
        4100-INSERT-TADJADD.                                                    
                                                                                
      *** INSERT INTO TADJADD....                                               
           MOVE ZERO                TO PV-RETRY-COUNT.                          
           SET PS-INSERT-NOT-DONE   TO TRUE.                                    
                                                                                
           PERFORM UNTIL PS-INSERT-DONE                                         
                      OR PV-RETRY-COUNT > PC-MAX-RETRIES                        
               EXEC SQL                                                         
                    INSERT                                                      
                    INTO  TADJADD                                               
                         (LOC_NBR                                               
                        , AREA_NBR                                              
                        , SKU_NBR                                               
                        , UPC_NBR                                               
                        , ADJ_UNIT_PR_AMT                                       
                        , ADJ_QTY                                               
                        , MISSED_MD_IND                                         
                        , DKEY_NBR                                              
                        , CHG_TMST                                              
                        , CHG_ID_NBR                                            
                        , ADJ_RSN_TXT                                           
                        , ADJ_EXTD_AMT)                                         
               VALUES    (:ADJADD-LOC-NBR                                       
                        , :ADJADD-AREA-NBR                                      
                        , :ADJADD-SKU-NBR                                       
                        , :ADJADD-UPC-NBR                                       
                        , :ADJADD-ADJ-UNIT-PR-AMT                               
                        , :ADJADD-ADJ-QTY                                       
                        , :ADJADD-MISSED-MD-IND                                 
                        , :ADJADD-DKEY-NBR                                      
                        ,  CURRENT TIMESTAMP                                    
                        , :ADJADD-CHG-ID-NBR                                    
                        , :ADJADD-ADJ-RSN-TXT                                   
                        , :ADJADD-ADJ-EXTD-AMT)                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
      *                DISPLAY 'SKU : ' ADJADD-SKU-NBR                          
      *                DISPLAY 'SQLCODE 0 ' ADJADD-DKEY-NBR                     
                       SET PS-INSERT-DONE TO TRUE                               
                       ADD 1        TO PV-TADJADD-COUNT                         
                   WHEN SQLCODE = -803                                          
      *                DISPLAY 'SKU : ' ADJADD-SKU-NBR                          
      *                DISPLAY 'SQLCODE -803 ' ADJADD-DKEY-NBR                  
                       ADD 1 TO PV-RETRY-COUNT                                  
                       COMPUTE PV-RANDOM-NUMBER =                               
                                PV-RANDOM-NUMBER *                              
                               (11 * PV-ADD-NUM)                                
                       ADD 11    TO PV-ADD-NUM                                  
                       MOVE PV-RANDOM-NUMBER TO ADJADD-DKEY-NBR                 
                   WHEN SQLCODE = +100                                          
      *                DISPLAY 'SKU : ' ADJADD-SKU-NBR                          
      *                DISPLAY 'SQLCODE +100 ' ADJADD-DKEY-NBR                  
                        SET PS-INSERT-DONE TO TRUE                              
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
      *                DISPLAY 'SKU : ' ADJADD-SKU-NBR                          
      *                DISPLAY 'SQLCODE -904 ' ADJADD-DKEY-NBR                  
                        ADD 1 TO PV-RETRY-COUNT                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                      DISPLAY 'SQLCODE -000 ' ADJADD-DKEY-NBR                   
                      DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                         
                      DISPLAY 'UPC  :  ' ADJADD-UPC-NBR                         
                      MOVE '4100-INSERT-TADJADD'                                
                                         TO PV-PARAGRAPH-NAME                   
                      MOVE 'INSERT INTO TADJADD'                                
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                      PERFORM 9999-SQL-ABEND                                    
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
                DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                               
                DISPLAY 'UPC  :  ' ADJADD-UPC-NBR                               
                DISPLAY ' STORE: ' ADJADD-LOC-NBR                               
                        ' AREA: '  ADJADD-AREA-NBR                              
                        ' SKU: '   ADJADD-SKU-NBR                               
                        ' UPC: '   ADJADD-UPC-NBR                               
                        ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT                       
                        ' DKEY: '  ADJADD-DKEY-NBR                              
                MOVE PV-RANDOM-NUMBER TO PV-DISPLAY                             
                DISPLAY 'DKEY :  ' PV-DISPLAY                                   
                MOVE '4100-INSERT-MASSADD' TO PV-PARAGRAPH-NAME                 
                MOVE 'MAX RETRIES EXCEEDED ON TADJADD TABLE'                    
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                PERFORM 9999-SQL-ABEND                                          
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 4200-CREATE-FASHION-ADJ                                                 
      *****************************************************************         
       4200-CREATE-FASHION-ADJ.                                                 
                                                                                
            PERFORM 4300-GET-ITEM-NBR.                                          
            PERFORM 4400-WRITE-FASHION.                                         
                                                                                
      *****************************************************************         
      * 4300-GET-ITEM-NBR.                                                      
      *****************************************************************         
       4300-GET-ITEM-NBR.                                                       
                                                                                
           EXEC SQL                                                             
               SELECT ITM_NBR                                                   
                 INTO :SKXREF-ITM-NBR                                           
                 FROM TSKXREF                                                   
               WHERE SKU = :INENTY-SKU-NBR                                      
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                    MOVE ZEROS TO SKXREF-ITM-NBR                                
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                 DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                              
                  MOVE '4300-GET-ITM-NBR'                                       
                                     TO PV-PARAGRAPH-NAME                       
                  MOVE 'SELECT FROM SKXREF'                                     
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
      *****************************************************************         
      * 4400-WRITE-FASHION.                                                     
      *    HERE WE WRITE THE FASHION SALES ADJUSTMENT RECORD.                   
      *****************************************************************         
       4400-WRITE-FASHION.                                                      
                                                                                
                                                                                
           IF SKXREF-ITM-NBR > 0                                                
               MOVE SKXREF-ITM-NBR  TO FS035-ITM-NBR                            
           ELSE                                                                 
               MOVE ZEROES          TO FS035-ITM-NBR                            
           END-IF.                                                              
                                                                                
    **** BASED ON INPUT CONTROL CARD (DETERMINES SIGN +/-) FOR FS ADJUST        
           EVALUATE TRUE                                                        
               WHEN ADJ-MASS-DEL                                                
                   SET FS035-ACTN-NEG TO TRUE                                   
               WHEN ADJ-MASS-ADD                                                
                   SET FS035-ACTN-PLS TO TRUE                                   
           END-EVALUATE.                                                        
                                                                                
    **** PROCESS IS RUN WITH THE ASSUMPTION UNIT BOOKING HAS OCCURRED           
           MOVE 'Y' TO FS035-UNT-BKG-IND.                                       
                                                                                
           MOVE 'INKUT301'          TO FS035-ADJ-SRC-PGM-NM.                    
           MOVE ADJADD-LOC-NBR      TO FS035-LOC-NBR.                           
           MOVE '123'               TO FS035-ADJ-TYP.                           
                                                                                
           MOVE ADJADD-ADJ-QTY      TO FS035-ADJ-QTY.                           
           MOVE 'INOL'              TO FS035-BTCH-NBR.                          
                                                                                
           WRITE FASHION-ADJUSTMENTS-RECORD                                     
                                    FROM FS035-ADJUSTMENT-RECORD.               
                                                                                
                                                                                
      *****************************************************************         
      * 8200-READ-INPUT.                                                        
      *                                                                         
      *****************************************************************         
       8200-READ-INPUT.                                                         
                                                                                
           READ DATABASE-UNLOAD-CONTROL                                         
               INTO CC-CONTROL-OPTION                                           
           END-READ.                                                            
                                                                                
      /****************************************************************         
      * 9000-EOJ-TASKS.                                                         
      *****************************************************************         
       9000-EOJ-TASKS.                                                          
                                                                                
           CLOSE DATABASE-UNLOAD-CONTROL                                        
                 FASHION-ADJUSTMENTS.                                           
      *          ADJUSTMENT-REPORT.                                             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.                     
           DISPLAY '*INKUT300 SUMMARY REPORT:           *'.                     
           DISPLAY '*                                   *'.                     
           DISPLAY '*POSITIVE ADJUSTMENTS               *'.                     
           DISPLAY '*  INVENTORY READ * ', PV-READ-COUNT                        
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-COUNT                       
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-COUNT                     
           DISPLAY '*                                   *'.                     
           DISPLAY '*NEGATIVE ADJUSTMENTS               *'.                     
           DISPLAY '*  NEG INV READ   * ', PV-READ-NEG-COUNT                    
           DISPLAY '*  FASHION WRITE  * ', PV-WRITE-NEG-COUNT                   
           DISPLAY '*  TADJADD WRITTE * ', PV-TADJADD-NEG-COUNT                 
           DISPLAY '*                                   *'.                     
           DISPLAY '* * * * * * * * * * * * * * * * * * *'.                     
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE 4013      TO ABEND-CODE.                                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
