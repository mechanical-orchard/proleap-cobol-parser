      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.   PXKUT108.                                          00050032
       AUTHOR.       COGNIZANT.                                         00060032
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070000
       DATE-WRITTEN. APRIL 2011.                                        00080032
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ***************************************************************** 00110000
      * THIS PROGRAM LOOKS UP STORE INVENTORY THAT HAPPENS BETWEEN     *00130032
      * COMPLIANCE WINDOWS PERIOD FOR ADDITIONAL MARKS ONLY            *00140032
      ***************************************************************** 00170000
      ******************** HISTORY OF CHANGES **************************00180000
      * CHG ID/                                                        *00190000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00200000
      * -------  ----------  ----------------------------------------  *00210000
      *          04/01/2011  INITIAL IMPLEMENTATION                    *00220032
      ******************************************************************00230000
      ******************************************************************00240000
       ENVIRONMENT DIVISION.                                            00250000
      ******************************************************************00260000
      *                                                                 00270000
       CONFIGURATION SECTION.                                           00280000
       SOURCE-COMPUTER.   IBM-3090.                                     00290000
       OBJECT-COMPUTER.   IBM-3090.                                     00300000
      *                                                                 00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
           SELECT DATE-FILE                                             00340032
             ASSIGN                       TO INP01.                     00350072
                                                                        00360000
            SELECT IVN-STR-FILE                                         00370032
              ASSIGN                        TO OUT01.                   00380000
                                                                        00399004
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
      *                                                                 00430000
       FILE SECTION.                                                    00440000
       FD  DATE-FILE                                                    00450032
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           RECORD CONTAINS 080 CHARACTERS                               00480032
           LABEL RECORDS ARE STANDARD                                   00490000
           DATA RECORD IS INPUT-RECORD.                                 00500000
       01  INPUT-RECORD                           PIC X(080).           00510032
      *                                                                 00520000
       FD IVN-STR-FILE                                                  00530032
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS                                     00550000
           RECORD CONTAINS 012 CHARACTERS                               00551074
           LABEL RECORDS ARE STANDARD                                   00552000
           DATA RECORD IS IVN-STR-FILE-RECORD.                          00560032
      *                                                                 00570000
       01 IVN-STR-FILE-RECORD                    PIC X(012).            00580074
      *                                                                 00590000
      *                                                                 00601204
       WORKING-STORAGE SECTION.                                         00602004
                                                                        00610000
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO      00620000
                                                        COMP SYNC.      00630000
           88  ABEND-4013-DB2-ERROR                     VALUE +4013.    00640052
      *                                                                 00650000
                                                                        00700000
       01 PC-PROGRAM-CONSTANTS.                                         00930000
          05  PC-PXKUT108                 PIC X(10)    VALUE 'PXKUT108'.00940052
          05  PC-CURRENT-PROGRAM-NAME     PIC X(08)    VALUE 'PXKUT108'.00960052
          05  PC-PROGRAM-NAME             PIC X(08)    VALUE 'PXKUT108'.00980052
          05  PC-ONE                      PIC S9(01)   VALUE +1.        00991052
                                                                        00992038
       01 PV-PROGRAM-VARIABLES.                                         01000000
          05  PV-SQLCODE                  PIC +9(04).                   01010000
          05  PV-OPERATION-MSG            PIC X(60)    VALUE SPACES.    01020052
          05  PV-WRITE-CNT                PIC 9(7)     VALUE ZERO       01030052
                                                       COMP-3.          01040052
          05  PV-ABEND-CODE               PIC S9(04)   VALUE +0         01150052
                                                       COMP SYNC.       01160052
          05  PV-PARA-NAME                PIC X(35).                    01170052
          05  PV-PARAGRAPH-NAME           PIC X(30)    VALUE SPACE.     01180052
          05  PV-DB2-OPERATION-ATTEMPTED  PIC X(30)   VALUE SPACES.     01181072
      ******************************************************************01181201
      **DATE VARIABLES                                                  01182033
      ******************************************************************01183001
       01 PV-INPUT-DATE-RANGE.                                          01184033
          05 PV-INPUT-START-DATE.                                       01185043
             10  PV-INPUT-START-DATE-YEAR      PIC X(04) VALUE SPACES.  01186039
             10 FILLER                         PIC X(01) VALUE '-'.     01186133
             10  PV-INPUT-START-DATE-MONTH     PIC X(02) VALUE SPACES.  01187039
             10 FILLER                         PIC X(01) VALUE '-'.     01187133
             10  PV-INPUT-START-DATE-01        PIC X(02) VALUE SPACES.  01188039
          05 FILLER                            PIC X(03) VALUE SPACE.   01188153
          05 PV-INPUT-END-DATE.                                         01188246
             10  PV-INPUT-END-DATE-YEAR        PIC X(04) VALUE SPACES.  01188339
             10 FILLER                         PIC X(01) VALUE '-'.     01188433
             10  PV-INPUT-END-DATE-MONTH       PIC X(02) VALUE SPACES.  01188539
             10 FILLER                         PIC X(01) VALUE '-'.     01188633
             10  PV-INPUT-END-DATE-01          PIC X(02) VALUE SPACES.  01188739
          05 FILLER                            PIC X(57) VALUE SPACES.  01188853
       01 PV-OUTPUT-RECORD.                                             01188933
          05 PV-LOC-NBR                        PIC 9(04) VALUE ZERO.    01189056
          05 FILLER                            PIC X(08) VALUE SPACES.  01189174
       01 PV-INV-START-DATE                    PIC X(10) VALUE SPACES.  01189252
       01 PV-INV-END-DATE                      PIC X(10) VALUE SPACES.  01189352
                                                                        01190000
      *                                                                 01220000
       01 PS-PROGRAM-SWITCHES.                                          01230000
          05  PS-END-OF-INPUT-FILE-SW          PIC X(1) VALUE 'N'.      01240035
              88  PS-END-OF-INPUT-FILE         VALUE 'Y'.               01250035
          05  PS-END-OF-INV-STR-CSR-SW         PIC X(1) VALUE 'N'.      01260035
              88  PS-END-OF-INV-STR-CSR        VALUE 'Y'.               01270035
      *                                                                 01290000
      ******************************************************************01480000
      * DB2 ERROR MESSAGES FORMAT AREA.                                 01490000
      ******************************************************************01500000
           COPY DPWS004.                                                01510000
                                                                        01520000
      ******************************************************************01530052
      * KOHLS DATE ROUTINE COPY MEMBERS.                                01540052
      ******************************************************************01550052
           COPY DPWS500.                                                01560052
      *                                                                 01570000
      ******************************************************************01701052
      *  DB2 COMMUNICATION AREA.                                        01710000
      ******************************************************************01711052
           EXEC SQL                                                     01730000
                INCLUDE SQLCA                                           01740000
           END-EXEC.                                                    01750000
                                                                        01760000
           COPY SQLCA2.                                                 01770000
      ******************************************************************01780000
      *  DB2 TABLE TINVPAR                                              01790000
      ******************************************************************01800000
           EXEC SQL                                                     01810000
               INCLUDE TINVPAR                                          01820000
           END-EXEC.                                                    01830000
      ******************************************************************02440000
           EXEC SQL                                                     02442052
               DECLARE INV-STR-EXTR-CSR CURSOR WITH HOLD FOR            02443052
                  SELECT LOC_NBR                                        02444052
                  FROM TINVPAR                                          02449652
                  WHERE ACTL_UNIT_BK_DTE BETWEEN                        02450052
                  :PV-INV-START-DATE AND :PV-INV-END-DATE               02450152
                  ORDER BY LOC_NBR                                      02453452
                  WITH UR                                               02453552
           END-EXEC.                                                    02453652
                                                                        02453752
      ******************************************************************02453835
       PROCEDURE DIVISION.                                              02454000
      ******************************************************************02460000
       0000-MAINLINE-PROCESSING.                                        02490000
      ******************************************************************02500000
      * MAIN PROCESSING ROUTINE                                       * 02510000
      ******************************************************************02520000
                                                                        02530000
            PERFORM 1000-INITIALIZE.                                    02540035
                                                                        02541052
            PERFORM 2000-PROCESS-LKUP-INV-DATE                          02542023
              UNTIL PS-END-OF-INPUT-FILE.                               02560057
                                                                        02570000
            PERFORM 9000-END-OF-JOB.                                    02580000
                                                                        02590000
            GOBACK.                                                     02600000
                                                                        02610000
      ******************************************************************02620000
      * OPEN FILES AND READ FIRST INPUT RECORD.                        *02630000
      ******************************************************************02640000
      *                                                                 02650000
       1000-INITIALIZE.                                                 02660046
            MOVE '*1000-INITIALIZE.        *' TO PV-PARA-NAME.          02670035
            OPEN INPUT  DATE-FILE                                       02680034
                 OUTPUT IVN-STR-FILE.                                   02690034
      ******************************************************************02829000
      * PROCESS CURSOR RECORD.                                          02830035
      ******************************************************************02840000
       2000-PROCESS-LKUP-INV-DATE.                                      02850019
      ******************************************************************02860035
           PERFORM 3000-READ-INPUT-FILE.                                02860171
           IF NOT PS-END-OF-INPUT-FILE                                  02860265
               PERFORM 5000-OPEN-CURSOR                                 02860371
               PERFORM 6000-FETCH-INV-STR-CSR                           02860471
               PERFORM UNTIL PS-END-OF-INV-STR-CSR                      02860565
                  PERFORM 7000-WRITE-INT-STR                            02860671
                  PERFORM 6000-FETCH-INV-STR-CSR                        02860771
               END-PERFORM                                              02860865
               PERFORM 9000-CLOSE-CURSOR                                02860965
               MOVE 'N' TO PS-END-OF-INV-STR-CSR-SW                     02861069
               INITIALIZE PV-INPUT-DATE-RANGE                           02861165
           END-IF.                                                      02861366
      ******************************************************************02861471
      * READ INPUT FILE - COMPLIANCE WINDOW DATE RANGE                  02861571
      ******************************************************************02861671
      *                                                                 02861771
       3000-READ-INPUT-FILE.                                            02861871
           READ DATE-FILE                                               02861971
             INTO PV-INPUT-DATE-RANGE                                   02862071
               AT END                                                   02863071
                 SET PS-END-OF-INPUT-FILE TO TRUE                       02864071
           END-READ.                                                    02865071
                                                                        02866071
           IF PS-END-OF-INPUT-FILE THEN                                 02867071
                 DISPLAY ' '                                            02868071
                 DISPLAY 'END OF READ INPUT'                            02869071
           ELSE                                                         02869171
                 MOVE PV-INPUT-START-DATE TO PV-INV-START-DATE          02869271
                 MOVE PV-INPUT-END-DATE TO PV-INV-END-DATE              02869371
                 INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56         02869471
                 SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                 02869573
                 SET DPG52-LK-DTE-ISO-FMT TO TRUE                       02869671
                 MOVE PV-INV-START-DATE  TO DPG52-LK-DATE-INPUT         02869771
                 PERFORM 4000-VALIDATE-INPUT-DATE                       02869871
                 INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56         02869971
                 SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                 02870071
                 SET DPG52-LK-DTE-ISO-FMT TO TRUE                       02870171
                 MOVE PV-INV-END-DATE  TO DPG52-LK-DATE-INPUT           02870271
                 PERFORM 4000-VALIDATE-INPUT-DATE                       02870371
           END-IF.                                                      02870471
      ******************************************************************02871071
       4000-VALIDATE-INPUT-DATE.                                        02872071
      ******************************************************************02873071
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            02874071
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               02875071
                                                                        02876071
           IF DPG54-DATE-INVALID                                        02877071
               DISPLAY '** ABEND ** IN PXKUT108'                        02878071
               DISPLAY '** PARAGRAPH: 4000-VALIDATE-INPUT-DATE'         02879071
               DISPLAY '** INVALID PROCESS DATE ON INPUT FILE.'         02879171
               DISPLAY '** CALENDAR ROUTINE ERROR MESSAGE = '           02879271
                           DPG54-ERROR-MESSAGE                          02879371
               DISPLAY '** DPG51 = ' DPG51                              02879471
               DISPLAY '** DPG52 = ' DPG52                              02879571
               DISPLAY '** DPG53 = ' DPG53                              02879671
               DISPLAY '** DPG54 = ' DPG54                              02879771
               DISPLAY '** DPG55 = ' DPG55                              02879871
               DISPLAY '** DPG56 = ' DPG56                              02879971
               MOVE +4003 TO ABEND-CODE                                 02880071
               CALL 'ILBOABN0' USING ABEND-CODE                         02881071
           END-IF.                                                      02882071
      ******************************************************************02890035
       5000-OPEN-CURSOR.                                                02892071
      ******************************************************************02892138
                                                                        02893038
           MOVE '5000-OPEN-CURSOR'  TO PV-PARAGRAPH-NAME.               02894071
                                                                        02895038
           EXEC SQL                                                     02896038
                OPEN INV-STR-EXTR-CSR                                   02897038
           END-EXEC.                                                    02898038
                                                                        02899038
           EVALUATE TRUE                                                02899138
              WHEN SQLCODE = +0                                         02899238
                   CONTINUE                                             02899338
              WHEN OTHER                                                02899438
                   PERFORM 9999-SQL-ERROR                               02899738
           END-EVALUATE.                                                02899838
      ******************************************************************02899938
       6000-FETCH-INV-STR-CSR.                                          02900071
      ******************************************************************03966002
071800         EXEC SQL                                                 03966135
071900           FETCH INV-STR-EXTR-CSR                                 03966235
072000             INTO :INVPAR-LOC-NBR                                 03966335
073100         END-EXEC                                                 03967435
073200                                                                  03967535
074600     EVALUATE TRUE                                                03968935
074700       WHEN SQLCODE = +0                                          03969035
074800         CONTINUE                                                 03969235
074900       WHEN SQLCODE = +100                                        03969335
075000         SET PS-END-OF-INV-STR-CSR TO TRUE                        03969435
075100       WHEN OTHER                                                 03969535
075200         MOVE '6000-FETCH-INV-STR-CSR'                            03969671
075300           TO PV-PARAGRAPH-NAME                                   03969735
075400         MOVE 'FETCH INV-STR-EXTR-CSR'                            03969835
075500           TO PV-DB2-OPERATION-ATTEMPTED                          03969935
075600         PERFORM 9999-SQL-ERROR                                   03970047
075700     END-EVALUATE.                                                03970135
      ******************************************************************03970235
       7000-WRITE-INT-STR.                                              03970371
      ******************************************************************03970435
           MOVE INVPAR-LOC-NBR TO PV-LOC-NBR.                           03970537
           WRITE IVN-STR-FILE-RECORD FROM PV-OUTPUT-RECORD.             03970636
           ADD  PC-ONE TO PV-WRITE-CNT.                                 03970738
      ******************************************************************03970838
      **CLOSE CURSOR                                                    03970938
      ******************************************************************03971038
       9000-CLOSE-CURSOR.                                               03971138
                                                                        03971238
           MOVE '9000-CLOSE-CURSOR' TO PV-PARAGRAPH-NAME.               03971338
                                                                        03971438
           EXEC SQL                                                     03971538
                CLOSE  INV-STR-EXTR-CSR                                 03971638
           END-EXEC.                                                    03971738
                                                                        03971838
           EVALUATE TRUE                                                03971938
              WHEN SQLCODE = +0                                         03972038
                   CONTINUE                                             03972138
              WHEN OTHER                                                03972238
                   DISPLAY 'CLOSING INV-STR-EXTR-CSR'                   03972338
                   PERFORM 9999-SQL-ERROR                               03972438
           END-EVALUATE.                                                03972538
                                                                        03972638
      ******************************************************************03972735
      *  WRITE COUNTER FILES, CLOSE FILES                              *03973035
      ******************************************************************03980000
       9000-END-OF-JOB.                                                 03990000
           DISPLAY 'NUMBER OF INVENTORY STORE FOUND:'  PV-WRITE-CNT.    04000038
           CLOSE DATE-FILE                                              04060035
                 IVN-STR-FILE.                                          04071235
      ******************************************************************04091052
       9999-SQL-ERROR.                                                  04100047
      ******************************************************************04101052
                                                                        04120000
           MOVE SQLCODE    TO SQLCODE2.                                 04130000
           MOVE SQLERRD(3) TO SQLERRD3.                                 04140000
           DISPLAY '** PROGRAM        ** = PXKUT108'.                   04160034
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  04170000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  04180000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   04190000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  04200000
                                                                        04210000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04220000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04230000
                                                                        04240000
      *                                                                 04250000
