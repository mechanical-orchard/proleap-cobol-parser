000010*****************************************************************         
000020 IDENTIFICATION DIVISION.                                                 
000030*****************************************************************         
000040                                                                          
000050 PROGRAM-ID.             SUKUT011.                                        
000060 AUTHOR.                 MAAY SARDINA.                                    
000070 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000080 DATE-WRITTEN            SEPTEMBER, 1997.                                 
000090 DATE-COMPILED.                                                           
000091*****************************************************************         
000092*****************************************************************         
000093*                                                               *         
000094*  THIS PROGRAM WILL EXTRACT RECEIVER INFORMATION FOR AUDITED   *         
000095*  POS.                                                         *         
000097*****************************************************************         
000098*  CHANGED 10/05/09 BY SAURABH DUDDALWAR                        *         
000099*   WR# CHANGES DONE FOR HANDLING DATA CONTENTION ERROR.        *         
000100*                                                               *         
000110*****************************************************************         
000120*****************************************************************         
000130 ENVIRONMENT DIVISION.                                                    
000140*****************************************************************         
000150                                                                          
000160 CONFIGURATION SECTION.                                                   
000170                                                                          
000180 SOURCE-COMPUTER.        IBM-3090.                                        
000190 OBJECT-COMPUTER.        IBM-3090.                                        
000191                                                                          
000192 INPUT-OUTPUT SECTION.                                                    
000193                                                                          
000194 FILE-CONTROL.                                                            
000195*                                                                         
000196     SELECT CARDIN-AUDIT-NBR-FILE                                         
000197         ASSIGN TO CARDIN.                                                
000198                                                                          
000199     SELECT SU-RECEIV-EXTRACT-FILE                                        
000200         ASSIGN TO OUT01.                                                 
000210*                                                                         
000220*****************************************************************         
000230 DATA DIVISION.                                                           
000240*****************************************************************         
000250*                                                                         
000260 FILE SECTION.                                                            
000270*                                                                         
000280 FD  CARDIN-AUDIT-NBR-FILE                                                
000290     LABEL RECORDS ARE STANDARD                                           
000291     RECORDING MODE IS F                                                  
000292     BLOCK CONTAINS 0 RECORDS                                             
000293     RECORD CONTAINS 80 CHARACTERS.                                       
000294 01  CC-RECORD                       PIC X(80).                           
000295                                                                          
000296 FD  SU-RECEIV-EXTRACT-FILE                                               
000297     RECORDING MODE IS F                                                  
000298     BLOCK CONTAINS 0  RECORDS.                                           
000299 01  SU-RECEIV-EXTRACT-RECORD        PIC  X(200).                         
000300*                                                                         
000310                                                                          
000320 WORKING-STORAGE SECTION.                                                 
000321*                                                                         
000322 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
000323                                                   COMP SYNC.             
000331*                                                                         
000340 01  PROGRAM-SWITCHES.                                                    
000362     05  PS-END-OF-SUATRN-CURSOR-SW  PIC  X(01)    VALUE 'N'.             
000363         88  PS-END-OF-SUATRN-CURSOR               VALUE 'Y'.             
000364         88  PS-NOT-END-OF-SUATRN-CURSOR          VALUE 'N'.              
000370     05  PS-END-OF-TRIP-CURSOR-SW    PIC  X(01)    VALUE 'N'.             
000380         88  PS-END-OF-TRIP-CURSOR                 VALUE 'Y'.             
000390         88  PS-NOT-END-OF-TRIP-CURSOR             VALUE 'N'.             
000391     05  PS-END-OF-CARTON-CURSOR-SW PIC   X(01)    VALUE 'N'.             
000392         88  PS-END-OF-CARTON-CURSOR               VALUE 'Y'.             
000393         88  PS-NOT-END-OF-CARTON-CURSOR           VALUE 'N'.             
000394     05  PS-END-OF-PO-CURSOR-SW     PIC   X(01)    VALUE 'N'.             
000395         88  PS-END-OF-PO-CURSOR                   VALUE 'Y'.             
000396         88  PS-NOT-END-OF-PO-CURSOR               VALUE 'N'.             
000397     05  PS-END-OF-RECDIS-CURSOR-SW PIC   X(01)    VALUE 'N'.             
000398         88  PS-END-OF-RECDIS-CURSOR               VALUE 'Y'.             
000399         88  PS-NOT-END-OF-RECDIS-CURSOR           VALUE 'N'.             
000403     05  PS-END-OF-CARDIN-SW         PIC  X(01)    VALUE 'N'.             
000404         88  PS-END-OF-CARDIN-FILE                 VALUE 'Y'.             
000405         88  PS-NOT-END-OF-CARDIN-FILE             VALUE 'N'.             
000407*----------------------------------------------------------------*        
000408*  INCLUDED PROGRAM SWITCHES FOR XLKUT001       **   WR # 21361  *        
000409*----------------------------------------------------------------*        
000410     05  PS-CALL-API-SW               PIC  X    VALUE 'N'.                
000411         88  PS-CALL-API-NO                     VALUE 'N'.                
000412         88  PS-CALL-API-YES                    VALUE 'Y'.                
000413*----------------------------------------------------------------*        
000414* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
000415*----------------------------------------------------------------*        
000416*                                                                         
000417 01  PC-CONSTANTS.                                                        
000418     05  PC-1                        PIC S9(03)    VALUE +1               
000419                                                   COMP-3.                
000420     05  PC-A                        PIC  X(01)    VALUE 'A'.             
000421     05  PC-Y                        PIC  X(01)    VALUE 'Y'.             
000430     05  PC-RCV                      PIC  X(03)    VALUE 'RCV'.           
000431     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
000432         'SUKUT011'.                                                      
000441*                                                                         
000450 01  PV-MISC-FIELDS.                                                      
000460     05  PV-LINE-SUB                 PIC S9(03)    VALUE ZERO             
000470                                                   COMP-3.                
000480     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
000490                                                   COMP SYNC.             
000491     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
000492     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
000493     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.          
000494     05  PV-AUDIT-NBR-X              PIC  X(09).                          
000495     05  PV-AUDIT-NBR REDEFINES PV-AUDIT-NBR-X                            
000496                                     PIC  9(09).                          
000497     05  PV-SKU-COUNT                    PIC S9(09)  VALUE +0             
000498                                                     COMP-3.              
000499     05  PV-WRITE-COUNT              PIC S9(09)    COMP.                  
000500     05  PV-QTY-RECV                 PIC S9(09)    COMP.                  
000501     05  PV-HOLD-PO-NBR              PIC S9(09)    COMP.                  
000502     05  PV-HOLD-REC-SEQ-NBR         PIC S9(09)    COMP.                  
000503*----------------------------------------------------------------*        
000504* ADDED NEW VARIABLES                          ** WR # 21361     *        
000505*----------------------------------------------------------------*        
000509     05  PV-ABEND.                                                        
000510         10  PV-ABEND-MESSAGE-TEXT   PIC  X(40)    VALUE SPACES.          
000511         10  PV-SQLCODE              PIC  S9(9)    VALUE ZEROES.          
000512         10  PV-DB2-VIEW-NAME        PIC  X(10)    VALUE SPACES.          
000513*----------------------------------------------------------------*        
000514* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
000515*----------------------------------------------------------------*        
000516 EJECT                                                                    
000517*                                                                         
000518 01  DK-DB-KEYS.                                                          
000519    05  DK-SHIPT-UNT-ID             PIC S9(18) COMP-3.                    
000520    05  DK-AUD-CNTL-NBR             PIC S9(09) COMP.                      
000521                                                                          
000522 01  CC-CARDIN.                                                           
000523     05  CC-AUDIT-CNTL-NBR           PIC 9(09).                           
000524     05  FILLER                      PIC X(62).                           
000525                                                                          
000530*                                                                         
044800 01  SU004-COPYBOOK.                                                      
044801     COPY SURD004.                                                        
044802                                                                          
044803*----------------------------------------------------------------*        
044804*  COPYBOOK FOR LOGISTICS DOMAIN ITEM ACCESS MODULE  WR # 21361  *        
044805*----------------------------------------------------------------*        
044806     COPY XLWS001.                                                        
044807*----------------------------------------------------------------*        
044808* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
044809*----------------------------------------------------------------*        
044810*                                                                         
044811******************************************************************        
044812*  DB2 FORMATTED MESSAGE AREA                                             
044813******************************************************************        
044814     COPY DPWS004.                                                        
044815*                                                                         
044816*                                                                         
044817*  COPY BOOK FOR THE SHIPPING UNIT AUDIT TRANSACTION TABLE                
044818*                                                                         
044819     EXEC SQL                                                             
044820          INCLUDE TSUATRN                                                 
044821     END-EXEC.                                                            
044822                                                                          
044823*                                                                         
044824*  COPY BOOK FOR THE PURCHASE ORDER HEADER TABLE                          
044825*                                                                         
044826     EXEC SQL                                                             
044827          INCLUDE TPURCHS                                                 
044828     END-EXEC.                                                            
044829                                                                          
044830*                                                                         
044831*  COPY BOOK FOR THE RECEIVER TABLE                                       
044832*                                                                         
044833     EXEC SQL                                                             
044834          INCLUDE TRECEIV                                                 
044835     END-EXEC.                                                            
044836                                                                          
044837*                                                                         
044838*  COPY BOOK FOR THE PURCHASE ORDER LINE ITEM TABLE                       
044839*                                                                         
044840     EXEC SQL                                                             
044841          INCLUDE TPURITM                                                 
044842     END-EXEC.                                                            
044843*                                                                         
044844*  COPY BOOK FOR THE RECEIVER LINE ITEM TABLE                             
044845*                                                                         
044846     EXEC SQL                                                             
044847          INCLUDE TRECITM                                                 
044848     END-EXEC.                                                            
044849*                                                                         
044850*  COPY BOOK FOR THE RECEIVER DISTRIBUTION TABLE                          
044851*                                                                         
044852     EXEC SQL                                                             
044853          INCLUDE TRECDIS                                                 
044854     END-EXEC.                                                            
044855*                                                                         
044856*  COPY BOOK FOR THE SHIPPING UNIT  AUDIT TABLE                           
044857*                                                                         
044858     EXEC SQL                                                             
044859          INCLUDE TSUNAUD                                                 
044860     END-EXEC.                                                            
044861 EJECT                                                                    
044862                                                                          
044863 EJECT                                                                    
044864*  DB2 COMMUNICATION AREA                                                 
044865*                                                                         
044866     EXEC SQL                                                             
044867         INCLUDE SQLCA                                                    
044868     END-EXEC.                                                            
044869 EJECT                                                                    
044870*  DB2 COMMUNICATION AREA2                                                
044871*                                                                         
044872     COPY SQLCA2.                                                         
044873 EJECT                                                                    
044874                                                                          
044875     EXEC SQL                                                             
044876       DECLARE SUATRN-CSR CURSOR FOR                                      
044877         SELECT DISTINCT(TRN_NBR)                                         
044878                       ,(REC_SEQ_NBR)                                     
044879          FROM  TSUATRN                                                   
044880          WHERE AUD_CNTL_NBR        = :DK-AUD-CNTL-NBR                    
044881         GROUP BY TRN_NBR,REC_SEQ_NBR                                     
044882     END-EXEC.                                                            
044883                                                                          
044884*----------------------------------------------------------------*        
044885*  ADDED ITM-NBR IN THE CURSOR                       WR # 21361  *        
044886*----------------------------------------------------------------*        
044908                                                                          
044909     EXEC SQL                                                             
044910       DECLARE PO-CSR CURSOR FOR                                          
044911         SELECT A.PO_NBR                                                  
044912              , B.APP_PO_LNE_NBR                                          
044913              , A.DEPT_NBR                                                
044914              , B.SKU_SEQ_NBR                                             
044915              , B.CL_NBR                                                  
044916              , B.ITM_NBR                                                 
044917           FROM  TPURCHS A                                                
044918               , TPURITM B                                                
044919               , TRECITM C                                                
044920          WHERE  A.PO_NBR       = :SUATRN-TRN-NBR                         
044921            AND  A.PO_NBR       = B.PO_NBR                                
044922            AND  A.PO_NBR       = C.ORD_NBR                               
044923            AND  B.APP_PO_LNE_NBR = C.ORD_LNE_NBR                         
044924            AND  C.REC_SEQ_NBR    = :SUATRN-REC-SEQ-NBR                   
044925            AND  A.VER_STATUS_CDE = 'A'                                   
044926            AND  B.VER_STATUS_CDE = 'A'                                   
044927            AND  C.VER_STATUS_CDE = 'A'                                   
044928      ORDER BY B.APP_PO_LNE_NBR                                           
044929     END-EXEC.                                                            
044930*----------------------------------------------------------------*        
044931* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
044932*----------------------------------------------------------------*        
044933                                                                          
044934     EXEC SQL                                                             
044935       DECLARE RECDIS-CSR CURSOR FOR                                      
044936          SELECT                                                          
044937             EXPTED_ITM_QTY                                               
044938            ,STR_NBR                                                      
044939            ,OPER_UNT_ID                                                  
044940            ,STR_FCLTY_ID                                                 
044941            ,ORD_LNE_NBR                                                  
044942          FROM TRECDIS                                                    
044943          WHERE                                                           
044944                 ORD_NBR          =  :PURCHS-PO-NBR                       
044945          AND    ORD_LNE_NBR      =  :PURITM-APP-PO-LNE-NBR               
044946          AND    REC_SEQ_NBR      =  :SUATRN-REC-SEQ-NBR                  
044947        ORDER BY ORD_LNE_NBR                                              
044948     END-EXEC.                                                            
044949                                                                          
044950                                                                          
044951*                                                                         
044952 PROCEDURE DIVISION.                                                      
044953*                                                                         
044954 0000-MAINLINE-PROCESSING.                                                
044955******************************************************************        
044956**  THIS IS THE MAIN DRIVER FOR THE PROGRAM.                              
044957******************************************************************        
044958*                                                                         
044959     PERFORM 1000-INITIALIZATION.                                         
044960*                                                                         
044961     IF PS-NOT-END-OF-CARDIN-FILE                                         
044962         PERFORM 2000-PROCESS-SUATRN                                      
044963            UNTIL PS-END-OF-CARDIN-FILE                                   
044964     END-IF.                                                              
044965     PERFORM 9000-EOJ-ROUTINE.                                            
044966*                                                                         
044967     GOBACK.                                                              
044968*                                                                         
044969*                                                                         
044970 1000-INITIALIZATION.                                                     
044971******************************************************************        
044972**  PERFORM THE INITIAL PROCESSING FOR THE PROGRAM.                       
044973******************************************************************        
044974                                                                          
044975     OPEN INPUT  CARDIN-AUDIT-NBR-FILE                                    
044976          OUTPUT SU-RECEIV-EXTRACT-FILE.                                  
044977                                                                          
044978     PERFORM 7000-READ-CARDIN-FILE.                                       
044979                                                                          
044980     IF PS-END-OF-CARDIN-FILE                                             
044981         DISPLAY '** SUKUT011 ABEND **'                                   
044982         DISPLAY '** CARDIN (AUDIT NUMBER) FILE IS EMPTY **'              
044983         MOVE +4019                  TO PV-ABEND-CODE                     
044984         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
044985     END-IF.                                                              
044986                                                                          
044987     DISPLAY 'AUDIT NBR IS : ' CC-AUDIT-CNTL-NBR.                         
044988*                                                                         
044989 2000-PROCESS-SUATRN.                                                     
044990     PERFORM 6500-SELECT-TSUNAUD.                                         
044991     IF SQLCODE = +100                                                    
044992         PERFORM 7000-READ-CARDIN-FILE                                    
044994     ELSE                                                                 
044995        IF SUNAUD-AUD-TYP-CDE = '02'                                      
044996            PERFORM 7000-READ-CARDIN-FILE                                 
044997        ELSE                                                              
044998            SET PS-NOT-END-OF-SUATRN-CURSOR TO TRUE                       
044999            PERFORM 2100-OPEN-SUATRN-CURSOR                               
045000            PERFORM 2200-FETCH-SUATRN-CURSOR                              
045001            PERFORM 2300-PROCESS-SUATRN-CURSOR                            
045002                UNTIL PS-END-OF-SUATRN-CURSOR                             
045003            PERFORM 2400-CLOSE-SUATRN-CURSOR                              
045004            PERFORM 7000-READ-CARDIN-FILE                                 
045005        END-IF                                                            
045006     END-IF.                                                              
045007 2100-OPEN-SUATRN-CURSOR.                                                 
045008     MOVE '2100-OPEN-SUATRN-CURSOR   '                                    
045009                                 TO PV-ABEND-PARAGRAPH.                   
045010     EXEC SQL                                                             
045011         OPEN SUATRN-CSR                                                  
045012     END-EXEC.                                                            
045013     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
045014*        WHEN SQLCODE = -904                                              
045015*        WHEN SQLCODE = -911                                              
045016*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
045017         WHEN SQLWARN0 NOT = SPACES                                       
045018         WHEN SQLCODE < ZERO                                              
045019         WHEN SQLCODE > ZERO                                              
045020             MOVE '2100-OPEN-SUATRN-CURSOR         '                      
045021                             TO PV-PARAGRAPH-NAME                         
045022             MOVE 'OPEN THE SUATRN_CUROSR         '                       
045023                             TO PV-DB2-OPERATION-ATTEMPTED                
045024             PERFORM 9100-SQL-ABEND                                       
045025     END-EVALUATE.                                                        
045026                                                                          
045027 2200-FETCH-SUATRN-CURSOR.                                                
045028     MOVE '2200-FETCH-SUATRN-CURSOR   '                                   
045029                                 TO PV-ABEND-PARAGRAPH.                   
045030     EXEC SQL                                                             
045031          FETCH SUATRN-CSR                                                
045032          INTO  :SUATRN-TRN-NBR                                           
045033              , :SUATRN-REC-SEQ-NBR                                       
045034              , :SUATRN-DEST-OPER-UNT-ID                                  
045035     END-EXEC.                                                            
045036                                                                          
045037     EVALUATE TRUE                                                        
045038       WHEN SQLCODE = ZEROS                                               
045039         CONTINUE                                                         
045040       WHEN SQLCODE = +100                                                
045041           SET PS-END-OF-SUATRN-CURSOR TO TRUE                            
045042       WHEN SQLWARN0 NOT = SPACES                                         
045043       WHEN SQLCODE < ZERO                                                
045044       WHEN SQLCODE > ZERO                                                
045045           MOVE '2200-FETCH-SUATRN-CURSOR'                                
045046                               TO PV-PARAGRAPH-NAME                       
045047           MOVE 'FETCH SUATRN CURSOR '                                    
045048                               TO PV-DB2-OPERATION-ATTEMPTED              
045049           PERFORM 9100-SQL-ABEND                                         
045050     END-EVALUATE.                                                        
045051                                                                          
045052 2300-PROCESS-SUATRN-CURSOR.                                              
045053     IF SUATRN-TRN-NBR = PV-HOLD-PO-NBR AND                               
045054        SUATRN-REC-SEQ-NBR = PV-HOLD-REC-SEQ-NBR                          
045055         CONTINUE                                                         
045056     ELSE                                                                 
045057         MOVE SUATRN-TRN-NBR       TO PV-HOLD-PO-NBR                      
045058         MOVE SUATRN-REC-SEQ-NBR   TO PV-HOLD-REC-SEQ-NBR                 
045059         PERFORM 2310-GET-ORDER-TYPE                                      
045060     END-IF.                                                              
045061     PERFORM 3000-PROCESS-PO.                                             
045062     PERFORM 2200-FETCH-SUATRN-CURSOR.                                    
045063                                                                          
045064                                                                          
045065*****************************************************************         
045066*  2310-GET-ORDER-TYPE.                                         *         
045067* THIS PARAGRAPH WILL PERFORM A SELECT ON THE PO AGREEMENT TABLE*         
045068* TO DETERMINE IF THE PO IS A PACK BY STORE ORDER OR NOT        *         
045069*****************************************************************         
045070 2310-GET-ORDER-TYPE.                                                     
068051                                                                          
068052     EXEC SQL                                                             
068056       SELECT  PACK_BY_ST_IND                                             
068057         INTO  :RECEIV-PACK-BY-ST-IND                                     
068058         FROM  TRECEIV                                                    
068059        WHERE  ORD_NBR        = :SUATRN-TRN-NBR                           
068060          AND  REC_SEQ_NBR    = :SUATRN-REC-SEQ-NBR                       
068064     END-EXEC.                                                            
068065                                                                          
068066     EVALUATE TRUE                                                        
068067       WHEN SQLCODE = ZEROS                                               
068068            CONTINUE                                                      
068070       WHEN SQLCODE = +100                                                
068080            MOVE 'N'          TO RECEIV-PACK-BY-ST-IND                    
068090                                                                          
068091       WHEN OTHER                                                       21
068092             MOVE '2310-GET-ORDER-TYPE'                                   
068093                             TO PV-PARAGRAPH-NAME                         
068094             MOVE 'SELECT THE PBS INDICATOR FROM TPOAGMT'                 
068095                             TO PV-DB2-OPERATION-ATTEMPTED                
068096             PERFORM 9100-SQL-ABEND                                       
068097     END-EVALUATE.                                                RVKCS021
068098                                                                          
068099                                                                          
068100                                                                          
068101 2400-CLOSE-SUATRN-CURSOR.                                                
068102     MOVE '2400-CLOSE-SUATRN-CURSOR   '                                   
068103                                 TO PV-ABEND-PARAGRAPH.                   
068104     EXEC SQL                                                             
068105         CLOSE SUATRN-CSR                                                 
068106     END-EXEC.                                                            
068107     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068108*        WHEN SQLCODE = -904                                              
068109*        WHEN SQLCODE = -911                                              
068110*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
068111         WHEN SQLWARN0 NOT = SPACES                                       
068112         WHEN SQLCODE < ZERO                                              
068113         WHEN SQLCODE > ZERO                                              
068114             MOVE '2400-CLOSE-CURSOR '                                    
068115                             TO PV-PARAGRAPH-NAME                         
068116             MOVE 'CLOSE THE SUATRN_CURSOR        '                       
068117                             TO PV-DB2-OPERATION-ATTEMPTED                
068118             PERFORM 9100-SQL-ABEND                                       
068119          WHEN SQLCODE = ZERO                                             
068120              CONTINUE                                                    
068121     END-EVALUATE.                                                        
068122*                                                                         
068123 3000-PROCESS-PO.                                                         
068124     SET PS-NOT-END-OF-PO-CURSOR TO TRUE                                  
068125     PERFORM 3100-OPEN-PO-CURSOR.                                         
068126     PERFORM 3200-FETCH-PO-CURSOR.                                        
068127     PERFORM 3300-PROCESS-PO-CURSOR                                       
068128         UNTIL PS-END-OF-PO-CURSOR.                                       
068129     PERFORM 3400-CLOSE-PO-CURSOR.                                        
068130 3100-OPEN-PO-CURSOR.                                                     
068131     MOVE '3100-OPEN-PO-CURSOR   '                                        
068132                                 TO PV-ABEND-PARAGRAPH.                   
068133     EXEC SQL                                                             
068134         OPEN PO-CSR                                                      
068135     END-EXEC.                                                            
068136     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068137*        WHEN SQLCODE = -904                                              
068138*        WHEN SQLCODE = -911                                              
068139*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
068140         WHEN SQLWARN0 NOT = SPACES                                       
068141         WHEN SQLCODE < ZERO                                              
068142         WHEN SQLCODE > ZERO                                              
068143             MOVE '3100-OPEN-PO-CURSOR           '                        
068144                             TO PV-PARAGRAPH-NAME                         
068145             MOVE 'OPEN THE PO_CURSOR           '                         
068146                             TO PV-DB2-OPERATION-ATTEMPTED                
068147             DISPLAY 'PO NBR:  '   SUATRN-TRN-NBR                         
068148             PERFORM 9100-SQL-ABEND                                       
068149       WHEN SQLCODE = ZEROS                                               
068150         CONTINUE                                                         
068151     END-EVALUATE.                                                        
068152                                                                          
068153 3200-FETCH-PO-CURSOR.                                                    
068154     MOVE '3200-FETCH-PO-CURSOR   '                                       
068155                                 TO PV-ABEND-PARAGRAPH.                   
068156*----------------------------------------------------------------*        
068157*  FETCH STATEMENT MODIFIED TO ADD PURITM-ITM-NBR.               *        
068158*  SET THE FLAG PS-CALL-API-YES ON SUCCESSFUL FETCH  WR # 21361  *        
068159*----------------------------------------------------------------*        
068160     EXEC SQL                                                             
068161          FETCH PO-CSR                                                    
068162          INTO  :PURCHS-PO-NBR                                            
068163              , :PURITM-APP-PO-LNE-NBR                                    
068164              , :PURCHS-DEPT-NBR                                          
068165              , :PURITM-SKU-SEQ-NBR                                       
068166              , :PURITM-CL-NBR                                            
068167              , :PURITM-ITM-NBR                                           
068168     END-EXEC.                                                            
068172                                                                          
068173     EVALUATE TRUE                                                        
068174       WHEN SQLCODE = ZEROS                                               
068175         SET PS-CALL-API-YES       TO TRUE                                
068176       WHEN SQLCODE = +100                                                
068177           SET PS-END-OF-PO-CURSOR TO TRUE                                
068178       WHEN SQLWARN0 NOT = SPACES                                         
068179       WHEN SQLCODE < ZERO                                                
068180       WHEN SQLCODE > ZERO                                                
068181           MOVE '3200-FETCH-PO-CURSOR  '                                  
068182                               TO PV-PARAGRAPH-NAME                       
068183           MOVE 'FETCH PO CURSOR   '                                      
068184                               TO PV-DB2-OPERATION-ATTEMPTED              
068185           PERFORM 9100-SQL-ABEND                                         
068186     END-EVALUATE.                                                        
068187*----------------------------------------------------------------*        
068188* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
068189*----------------------------------------------------------------*        
068190                                                                          
068191 3300-PROCESS-PO-CURSOR.                                                  
068192*----------------------------------------------------------------*        
068193*                                               **   WR # 21361  *        
068195* ADDED CODE TO CHECK FOR THE DUMMY SKU AND CALL TO XLKUT001     *        
068196*----------------------------------------------------------------*        
068197         IF PURITM-ITM-NBR < 0                                            
068198            PERFORM 9100-SQL-ABEND                                        
068199         ELSE                                                             
068200            IF PURITM-ITM-NBR = 0                                         
068201              IF PURITM-SKU-SEQ-NBR = 000                                 
068202                 MOVE '00000000'    TO SU004-SKU-NMBR                     
068203              ELSE IF PURITM-SKU-SEQ-NBR = 999                            
068204                 MOVE '99999999' TO SU004-SKU-NMBR                        
068205            ELSE                                                          
068206              PERFORM 9100-SQL-ABEND.                                     
068207                                                                          
068212         IF PS-CALL-API-YES AND                                           
068213               PURITM-ITM-NBR > 0                                         
068214                PERFORM 3350-CALL-XLKUT-DOMAIN-PGM                        
068215         END-IF                                                           
068216                                                                          
068223*----------------------------------------------------------------*        
068224* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
068225*----------------------------------------------------------------*        
068226     PERFORM 4000-PROCESS-RECDIS.                                         
068227     PERFORM 3200-FETCH-PO-CURSOR.                                        
068228                                                                          
068229*----------------------------------------------------------------*        
068230*                                               **   WR # 21361  *        
068231* CODE FOR ACCESSING LOGISTICS DOMAIN ITEM ACCESS MODULE         *        
068232*----------------------------------------------------------------*        
068233 3350-CALL-XLKUT-DOMAIN-PGM.                                              
068234                                                                          
068235     INITIALIZE XL001-ITEM-COMMAREA                                       
068236     MOVE '7010-SELECT-SKU-BY-SKU'                                        
068237                               TO PV-ABEND-PARAGRAPH.                     
068238     MOVE PURITM-ITM-NBR       TO XL001-KEY-ITM-NBR.                      
068240                                                                          
068241     SET  XL001-SELECT-SKU                                                
068242          XL001-BY-ITEM TO TRUE.                                          
068243                                                                          
068244     CALL XL001-VSM-PGM USING  XL001-ITEM-COMMAREA.                       
068245                                                                          
068246     EVALUATE TRUE                                                        
068247         WHEN XL001-SKU-CONTENTION                                        
068248              MOVE '3350-CALL-XLKUT-DOMAIN-PGM'                           
068249                                     TO PV-ABEND-PARAGRAPH                
068250              MOVE 'XL001-SKU-CONTENTION'                                 
068251                                     TO PV-ABEND-MESSAGE-TEXT             
068252              MOVE XL001-SKU-SQLCODE TO PV-SQLCODE                        
068253              MOVE 'XLV_SKU'         TO PV-DB2-VIEW-NAME                  
068254              PERFORM 9200-PROCESS-ABEND                                  
068255         WHEN XL001-SKU-SQLCODE = +0                                      
068256              CONTINUE                                                    
068259         WHEN OTHER                                                       
068260              MOVE '3350-CALL-XLKUT-DOMAIN-PGM'                           
068261                                     TO PV-ABEND-PARAGRAPH                
068264              MOVE XL001-SKU-SQLCODE TO PV-SQLCODE                        
068265              MOVE 'XLV_SKU'         TO PV-DB2-VIEW-NAME                  
068266             PERFORM 9200-PROCESS-ABEND                                   
068267     END-EVALUATE.                                                        
068268*----------------------------------------------------------------*        
068269* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
068270*----------------------------------------------------------------*        
068271 3400-CLOSE-PO-CURSOR.                                                    
068272     MOVE '3400-CLOSE-PO-CURSOR   '                                       
068273                                 TO PV-ABEND-PARAGRAPH.                   
068274     EXEC SQL                                                             
068275         CLOSE PO-CSR                                                     
068276     END-EXEC.                                                            
068277     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068278*        WHEN SQLCODE = -904                                              
068279*        WHEN SQLCODE = -911                                              
068280*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
068281         WHEN SQLWARN0 NOT = SPACES                                       
068282         WHEN SQLCODE < ZERO                                              
068283         WHEN SQLCODE > ZERO                                              
068284             MOVE '3400-CLOSE-CURSOR '                                    
068285                             TO PV-PARAGRAPH-NAME                         
068286             MOVE 'CLOSE THE PO_CURSOR          '                         
068287                             TO PV-DB2-OPERATION-ATTEMPTED                
068288             PERFORM 9100-SQL-ABEND                                       
068289          WHEN SQLCODE = ZERO                                             
068290              CONTINUE                                                    
068291       WHEN SQLCODE = ZEROS                                               
068292         CONTINUE                                                         
068293     END-EVALUATE.                                                        
068294                                                                          
068295 4000-PROCESS-RECDIS.                                                     
068296     SET PS-NOT-END-OF-RECDIS-CURSOR TO TRUE                              
068297     PERFORM 4100-OPEN-RECDIS-CURSOR.                                     
068298     PERFORM 4200-FETCH-RECDIS-CURSOR.                                    
068299     PERFORM 4300-PROCESS-RECDIS-CURSOR                                   
068300         UNTIL PS-END-OF-RECDIS-CURSOR.                                   
068301     PERFORM 4400-CLOSE-RECDIS-CURSOR.                                    
068302 4100-OPEN-RECDIS-CURSOR.                                                 
068303     MOVE '4100-OPEN-RECDIS-CURSOR   '                                    
068304                                 TO PV-ABEND-PARAGRAPH.                   
068305     EXEC SQL                                                             
068306         OPEN RECDIS-CSR                                                  
068307     END-EXEC.                                                            
068308     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068309*        WHEN SQLCODE = -904                                              
068310*        WHEN SQLCODE = -911                                              
068311*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
068312         WHEN SQLWARN0 NOT = SPACES                                       
068313         WHEN SQLCODE < ZERO                                              
068314         WHEN SQLCODE > ZERO                                              
068315             MOVE '4100-OPEN-RECDIS-CURSOR           '                    
068316                             TO PV-PARAGRAPH-NAME                         
068317             MOVE 'OPEN THE RECDIS_CURSOR           '                     
068318                             TO PV-DB2-OPERATION-ATTEMPTED                
068319             DISPLAY 'PO NBR:      ' PURCHS-PO-NBR                        
068320             DISPLAY 'LINE NBR:    ' PURITM-APP-PO-LNE-NBR                
068321             DISPLAY 'REC SEQ NBR: ' SUATRN-REC-SEQ-NBR                   
068322             PERFORM 9100-SQL-ABEND                                       
068323       WHEN SQLCODE = ZEROS                                               
068324         CONTINUE                                                         
068325     END-EVALUATE.                                                        
068326                                                                          
068327 4200-FETCH-RECDIS-CURSOR.                                                
068328     MOVE '4200-FETCH-RECDIS-CURSOR   '                                   
068329                                 TO PV-ABEND-PARAGRAPH.                   
068330     EXEC SQL                                                             
068331          FETCH RECDIS-CSR                                                
068332          INTO  :RECDIS-EXPTED-ITM-QTY                                    
068333               ,:RECDIS-STR-NBR                                           
068334               ,:RECDIS-OPER-UNT-ID                                       
068335               ,:RECDIS-FCLTY-ID                                          
068336               ,:RECDIS-ORD-LNE-NBR                                       
068337     END-EXEC.                                                            
068338                                                                          
068339     EVALUATE TRUE                                                        
068340       WHEN SQLCODE = ZEROS                                               
068341         CONTINUE                                                         
068342       WHEN SQLCODE = +100                                                
068343           SET PS-END-OF-RECDIS-CURSOR TO TRUE                            
068344       WHEN SQLWARN0 NOT = SPACES                                         
068345       WHEN SQLCODE < ZERO                                                
068346       WHEN SQLCODE > ZERO                                                
068347           MOVE '4200-FETCH-RECDIS-CURSOR  '                              
068348                               TO PV-PARAGRAPH-NAME                       
068349           MOVE 'FETCH RECDIS CURSOR   '                                  
068350                               TO PV-DB2-OPERATION-ATTEMPTED              
068351           PERFORM 9100-SQL-ABEND                                         
068352     END-EVALUATE.                                                        
068353                                                                          
068354                                                                          
068355 4300-PROCESS-RECDIS-CURSOR.                                              
068356     INITIALIZE SU004-EXTRACT-RECORD.                                     
068357     MOVE DK-AUD-CNTL-NBR           TO SU004-AUD-CNTL-NBR.                
068358     MOVE SUATRN-TRN-NBR            TO SU004-PO-NBR.                      
068359     MOVE SUATRN-REC-SEQ-NBR        TO SU004-REC-SEQ-NBR.                 
068360     MOVE PURCHS-DEPT-NBR           TO SU004-SKU-DEPT-NBR.                
068361*----------------------------------------------------------------*        
068362*  REMOVED THE BELOW MOVE STATEMENT.                 WR # 21361  *        
068363*  MOVE PURITM-SKU-SEQ-NBR        TO SU004-SKU-SEQ-NBR           *        
068364*  ADDED THE BELOW MOVE STATEMENT.                               *        
068365*  MOVE XL001-SKU-NBR             TO SU004-SKU-NMBR              *        
068366*----------------------------------------------------------------*        
068367     MOVE PURITM-CL-NBR             TO SU004-SKU-CL-NBR.                  
068368     MOVE XL001-SKU-NBR             TO SU004-SKU-NMBR.                    
068369     MOVE +0                        TO SU004-PICK-ERR-CNT                 
068370                                       SU004-UNT-ERR-CNT-COMBO.           
068371     MOVE '9999-09-09-09.09.09.999999'                                    
068372                                    TO SU004-SCN-TMST.                    
068373     MOVE +1                        TO SU004-QTY-RECV.                    
068374                                                                          
068375     IF RECEIV-PACK-BY-ST-IND = 'Y'                                       
068380         SET SU004-PBS-ORDER        TO TRUE                               
068381         MOVE RECDIS-STR-NBR        TO SU004-DEST-OPER-UNT-ID             
068390     ELSE                                                                 
068400         SET SU004-CASEPACK-ORDER   TO TRUE                               
068410         MOVE RECDIS-OPER-UNT-ID    TO SU004-DEST-OPER-UNT-ID             
068500     END-IF                                                               
068501                                                                          
068503     MOVE RECDIS-EXPTED-ITM-QTY                                           
068504                                TO PV-QTY-RECV.                           
068508                                                                          
068509     PERFORM 8000-WRITE-RECEIV-EXTRACT-REC                                
068510        VARYING PV-WRITE-COUNT FROM 1 BY 1                                
068520        UNTIL   PV-WRITE-COUNT > PV-QTY-RECV.                             
068521                                                                          
068522     PERFORM 4200-FETCH-RECDIS-CURSOR.                                    
068523                                                                          
068524                                                                          
068525 4400-CLOSE-RECDIS-CURSOR.                                                
068526     MOVE '4400-CLOSE-RECDIS-CURSOR   '                                   
068527                                 TO PV-ABEND-PARAGRAPH.                   
068528     EXEC SQL                                                             
068529         CLOSE RECDIS-CSR                                                 
068530     END-EXEC.                                                            
068531     EVALUATE TRUE                                                        
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068532*        WHEN SQLCODE = -904                                              
068533*        WHEN SQLCODE = -911                                              
068534*            CONTINUE                                                     
      * SND 10/2009  DATA CONTENTION CHANGES END                                
068535         WHEN SQLWARN0 NOT = SPACES                                       
068536         WHEN SQLCODE < ZERO                                              
068537         WHEN SQLCODE > ZERO                                              
068538             MOVE '4400-CLOSE-CURSOR '                                    
068539                             TO PV-PARAGRAPH-NAME                         
068540             MOVE 'CLOSE THE RECDIS_CURSOR          '                     
068541                             TO PV-DB2-OPERATION-ATTEMPTED                
068542             PERFORM 9100-SQL-ABEND                                       
068543          WHEN SQLCODE = ZERO                                             
068544              CONTINUE                                                    
068545       WHEN SQLCODE = ZEROS                                               
068546         CONTINUE                                                         
068547     END-EVALUATE.                                                        
068548                                                                          
068549*                                                                         
068550 6500-SELECT-TSUNAUD.                                                     
068551******************************************************************        
068552**  SELECT INFORMATION FROM THE SHIPPING UNIT AUDIT TABLE FOR THE         
068553******************************************************************        
068554     INITIALIZE DCLTSUNAUD.                                               
068555                                                                          
068556     EXEC SQL                                                             
068557       SELECT  AUD_STRT_TMST                                              
068558              ,AUD_STP_TMST                                               
068559              ,AUD_PSPND_MNIT_QTY                                         
068560              ,AUD_CLO_MNIT_QTY                                           
068561              ,OPER_UNT_ID                                                
068562              ,FCLTY_ID                                                   
068563              ,PRC_FCLTY_AREA_ID                                          
068564              ,AUD_STAT_CDE                                               
068565              ,AUD_TYP_CDE                                                
068566         INTO  :SUNAUD-AUD-STRT-TMST                                      
068567              ,:SUNAUD-AUD-STP-TMST                                       
068568              ,:SUNAUD-AUD-PSPND-MNIT-QTY                                 
068569              ,:SUNAUD-AUD-CLO-MNIT-QTY                                   
068570              ,:SUNAUD-OPER-UNT-ID                                        
068571              ,:SUNAUD-FCLTY-ID                                           
068572              ,:SUNAUD-PRC-FCLTY-AREA-ID                                  
068573              ,:SUNAUD-AUD-STAT-CDE                                       
068574              ,:SUNAUD-AUD-TYP-CDE                                        
068575         FROM  TSUNAUD                                                    
068576        WHERE  AUD_CNTL_NBR = :DK-AUD-CNTL-NBR                            
068577     END-EXEC.                                                            
068578                                                                          
068579     EVALUATE TRUE                                                        
068580       WHEN SQLCODE = ZEROS                                               
068581       WHEN SQLCODE = +100                                                
068582         CONTINUE                                                         
068583       WHEN OTHER                                                         
068584         DISPLAY DK-AUD-CNTL-NBR                                          
068585         MOVE '6500-SELECT-TSUNAUD.'                                      
068586                         TO PV-PARAGRAPH-NAME                             
068587         MOVE 'SELECT TSUNAUD'                                            
068588                         TO PV-DB2-OPERATION-ATTEMPTED                    
068589         PERFORM 9100-SQL-ABEND                                           
068590     END-EVALUATE.                                                        
068591                                                                          
068592                                                                          
068593 7000-READ-CARDIN-FILE.                                                   
068594******************************************************************        
068595                                                                          
068596     READ CARDIN-AUDIT-NBR-FILE INTO CC-CARDIN                            
068597        AT END                                                            
068598           SET PS-END-OF-CARDIN-FILE TO TRUE.                             
068599                                                                          
068600     MOVE CC-AUDIT-CNTL-NBR   TO PV-AUDIT-NBR-X.                          
068601     MOVE PV-AUDIT-NBR        TO DK-AUD-CNTL-NBR.                         
068602                                                                          
068603                                                                          
068604*                                                                         
068605 8000-WRITE-RECEIV-EXTRACT-REC.                                           
068606     WRITE SU-RECEIV-EXTRACT-RECORD                                       
068607       FROM SU004-EXTRACT-RECORD.                                         
068608*                                                                         
068609 9000-EOJ-ROUTINE.                                                        
068610                                                                          
068611     CLOSE CARDIN-AUDIT-NBR-FILE                                          
068612           SU-RECEIV-EXTRACT-FILE.                                        
068613                                                                          
      * SND 10/2009  DATA CONTENTION CHANGES START                              
068614 9100-SQL-ABEND.                                                          
      *                                                                         
           IF SQLCODE                  = -904 OR -911 OR -913                   
              MOVE +4050               TO RETURN-CODE                           
              STOP RUN                                                          
           ELSE                                                                 
              PERFORM 9110-ABEND-PROGRAM                                        
           END-IF.                                                              
      *                                                                         
      *9100-EXIT.                                                               
      *                                                                         
       9110-ABEND-PROGRAM.                                                      
068615******************************************************************        
068616*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
068617*  ERROR OR WARNING CODE OCCURS.                                          
068618******************************************************************        
068619     DISPLAY '** ABEND     **'.                                           
068620     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
068621     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
068622     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
068623                                                                          
068624******************************************************************        
068625* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
068626* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
068627* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
068628* FORMAT.                                                                 
068629******************************************************************        
068630     COPY DPPD004.                                                        
068631                                                                          
068632     MOVE +4013                  TO ABEND-CODE.                           
068633     CALL 'ILBOABN0' USING ABEND-CODE.                                    
068634*----------------------------------------------------------------*        
068635*                                               ** WR # 21361    *        
068636* ADDED CODE TO DISPLAY MESSAGES,WHEN ABEND OCCURS FROM          *        
068637* LOGISTICS DOMAIN ITEM ACCESS MODULE.                           *        
068638*----------------------------------------------------------------*        
068639 9200-PROCESS-ABEND.                                                      
068640*                                                                         
           IF SQLCODE                  = -904 OR -911 OR -913                   
              MOVE +4050               TO RETURN-CODE                           
              STOP RUN                                                          
           ELSE                                                                 
              PERFORM 9210-ABEND-PROGRAM                                        
           END-IF.                                                              
      *                                                                         
      *9200-EXIT.                                                               
      *                                                                         
      * SND 10/2009  DATA CONTENTION CHANGES END                                
       9210-ABEND-PROGRAM.                                                      
068641     DISPLAY '** ABEND **       ** = SQLERROR'.                           
068650     DISPLAY '** PROGRAM        ** = SUKUT011'.                           
068660     DISPLAY '** ABEND PARAGRAPH **  ' PV-ABEND-PARAGRAPH.                
068670     DISPLAY '** ERROR MESSAGE   **  ' PV-ABEND-MESSAGE-TEXT.             
068680     DISPLAY '** SQLCODE         **  ' PV-SQLCODE.                        
068690     DISPLAY '** VIEW  NAME      **  ' PV-DB2-VIEW-NAME.                  
068700     MOVE +4013 TO PV-ABEND-CODE.                                         
068800     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
068900*----------------------------------------------------------------*        
069000* CHANGES MADE FOR WR # 21361 ENDS HERE.                         *        
069100*----------------------------------------------------------------*        
