********** THIS REPORT PROGRAM IS RECEIVING CODE TO DISPLAY THE                 
********** JOB NBR IN THE HEADER LINE. THIS IS PART OF THE                      
********** BV / VA SPLIT PROJECT.  IF YOU CARE CONSIDERING CHANGES TO           
********** THIS PROGRAM, PLEASE SEE THE BV / VA TEAM.                           
********** TKMA.ML.BV.PHASE2.TEST.SRCLIB                                        
********** SUE BARTELT                                                          
       ID DIVISION.                                                     04/25/94
00002  PROGRAM-ID.      MLKRP004                                        MLKRP004
00003  AUTHOR.          JEFF SNYDER - KOHLS DEPARTMENT STORES.             LV006
00004  DATE-WRITTEN     FEBRUARY 1989.                                  MLKRP004
00005  DATE-COMPILED.                                                   MLKRP004
W29908******************************************************************MLKRP004
  |   * THIS PROGRAM USES JULIAN DATE TO ASSIGN BATCH NUMBER (RCPTS)    MLKRP004
  |   *    JULIAN DATE SELECTED FOR SAT WK-END-DTE.                     MLKRP004
  |   *       ADD 1   DAY  FOR RECEIPTS  (SAT PLUS 1 DAY)               MLKRP004
  |   *       ADD 499 DAYS FOR PURCHASE ADJ                             MLKRP004
  |   *                                                                 MLKRP004
  |   *  ** IF AP RUNS INTO NEXT DAY, MAY NEED TO ADD 500 DAYS FOR RPT  MLKRP004
W29908*                                              ==========         MLKRP004
00006 ******************************************************************MLKRP004
00007 * THIS PROGRAM REPORTS ON THE PURCHASES, TRANSFERS, MARKDOWNS, ANDMLKRP004
00008 * MARKUPS WHICH WILL BE INPUT INTO THE MERCHANDISE LEDGER UPDATE. MLKRP004
00009 * THE REPORT WILL LIST TOTALS BY TYPE AND BATCH NUMBER FOR ALL    MLKRP004
00010 * TRANSACTIONS WHICH SURVIVED THE EDIT AND REFORMAT.              MLKRP004
00010 *                                                                 MLKRP004
00010 * WR#5830 -  SCOTT JACKSON   11/27/97                                     
00010 *    ADD NEW REPORT MLKRP004-2, FOR INVENTORY PURPOSES.                   
00010 *    PROGRAM WILL ACCEPT SYSIN CONTROL CARD, VERIFY SAT DATE AND  MLKRP004
00010 *    USE CALENDAR ROUTINE TO OBTAIN PREV SUNDAY DATE FOR REPORT.  MLKRP004
00010 *    REPORT 2 IS FOR PURCHASE TRANSACTIONS FOR CURRENT/PRIOR WEEK.MLKRP004
00010 *    RECEIPT AND ADJUSTMENT TOTALS ARE TRANS WITH AP CODES OF '01'MLKRP004
00010 *   -REPORT TOTALS AND BALANCES JOUNALIZED AND UNJOURNALIZED TOTALMLKRP004
00010 *    THAT ARE CREATED OUT OF FASION SALES (VERIFIED RECEIPTS)     MLKRP004
      *                                                                         
00010 * WR5701  -  AMY LAWSON      01/21/99                                     
00010 *    MADE CHANGES TO SECOND REPORT - MLKRP004-2.  CHANGED                 
00010 *    PROGRAM TO HANDLE RECEIPTS AND PURCHASE ADJUSTMENTS INSTEAD  MLKRP004
00010 *    OF JOURNALIZED AND UNJOURNALIZED TRANSACTIONS.  THE CONCEPT  MLKRP004
00010 *    OF JOURNALIZED AND UNJOURNALIZED TRANSACTIONS WAS ELIMINATED MLKRP004
00010 *    IN DECEMBER OF 1998.                                         MLKRP004
00011 *                                                                 MLKRP004
00010 *            AMY LAWSON      01/25/00                                     
00010 *    MADE CHANGES TO SECOND REPORT - MLKRP004-2.  CHANGED                 
00010 *    THE JULIAN-DAY-PLUS CALCULATION.  WE HAVE TO SUBTRACT ONE DAYMLKRP004
00010 *    FROM THE JULIAN-DAY-PLUS NUMBER.  THE REASON FOR THIS IS     MLKRP004
00010 *    BECAUSE THE PURCHASE ADJUSTMENT TRANSACTIONS ARE CREATED     MLKRP004
00010 *    ON SATURDAY.  IF THIS JOB(MPK110) RUNS AFTER MIDNIGHT THIS   MLKRP004
00010 *    LOGIC WILL NOT WORK.  IF SUE SAYS THE REPORT IS NOT PULLING  MLKRP004
00010 *    IN THE CORRECT TRANSACTIONS LOOK AT THE BATCH NUMBERS AND    MLKRP004
00010 *    DETERMINE WHAT THE JULIAN-DAY-PLUS NUMBER IS.  YOU MAY HAVE  MLKRP004
00010 *    TO REMOVE THE -1 LOGIC.                                      MLKRP004
00011 *                                                                 MLKRP004
00010 * 23511   -  CAROL WOJCIECHOWSKI  03/02                                   
00010 *    MODIFIED PROGRAM TO:                                                 
00011 *        EXPAND THE BATCH-RECORD AND HR-HOLD-RECORD               MLKRP004
00010 *        EXPAND FIELDS IN THE PA-PROGRAM-ACCUMULATORS             MLKRP004
00011 *                                                                 MLKRP004
00010 *W29908   -  SCOTT JACKSON       07/08/05                                 
00011 *        ALLOW INVENTORY ADJUSTMENTS TO BE DISPLAYED ON ML WEEKLY MLKRP004
00011 *        BATCH TOTALS RPT AND STORE-EXPECTED INVENTORY LEDGER RPT MLKRP004
00011 *        ALSO REMOVE THE KOHLS CALENDAR AND INSERT DB2 - TDTATTR. MLKRP004
00011 *        REPLACE SYSIN CARD TO USE A DATE CONTROL CARD FROM JOB.  MLKRP004
00010 *S29908   -  SCOTT JACKSON       01/28/06                                 
00011 *        FIX ISSUE WITH JULIAN/BATCH DAYS ON REPORT2              MLKRP004
C29908*C29908   -  SCOTT JACKSON       02/01/06                                 
C29908*        CUTOFF WEEK RPT2 FOR INV LEDGER NEEDS TO HANDLE BATCH    MLKRP004
C29908*        NBR CREATED PAST THE END OF THE FISCAL MONTH END DATE    MLKRP004
207210*CHG0207210  JIM HUNTER          08/24/18                                 
207210*        RENAME KDM2809 TO MLWS050                                MLKRP004
00011 ******************************************************************MLKRP004
00012 *                                                                 MLKRP004
00013  ENVIRONMENT DIVISION.                                            MLKRP004
00014 *                                                                 MLKRP004
00015  CONFIGURATION SECTION.                                           MLKRP004
00016 *                                                                 MLKRP004
00017  SOURCE-COMPUTER.        IBM-3090.                                MLKRP004
00018  OBJECT-COMPUTER.        IBM-3090.                                MLKRP004
00019 *                                                                 MLKRP004
00020  INPUT-OUTPUT SECTION.                                            MLKRP004
00021 *                                                                 MLKRP004
00022  FILE-CONTROL.                                                    MLKRP004
00023      SELECT  BATCH-FILE           ASSIGN INP01.                      CL**2
00024      SELECT  LEDGER-CONTROL-FILE  ASSIGN INP02.                      CL**6
W29908     SELECT  DATE-CONTROL-FILE    ASSIGN INP03.                      CL**6
00025      SELECT  REPORT-FILE          ASSIGN RPT01.                      CL**2
00025      SELECT  REPORT-FILE-2        ASSIGN RPT02.                      CL**2
00026 *                                                                 MLKRP004
00027  DATA DIVISION.                                                   MLKRP004
00028 *                                                                 MLKRP004
00029  FILE SECTION.                                                    MLKRP004
00030  FD  BATCH-FILE                                                   MLKRP004
00031      RECORDING MODE F                                             MLKRP004
00032      LABEL RECORDS STANDARD                                       MLKRP004
00033      BLOCK CONTAINS  0 RECORDS.                                   MLKRP004
23511  01  BATCH-RECORD           PIC X(125).                              CL**2
00035 *                                                                 MLKRP004
00036  FD  LEDGER-CONTROL-FILE                                             CL**6
00037      RECORDING MODE F                                                CL**2
00038      LABEL RECORDS STANDARD                                          CL**2
00039      BLOCK CONTAINS  0 RECORDS.                                      CL**2
00040  01  LEDGER-CONTROL-RECORD PIC X(80).                                CL**6
00041 *                                                                    CL**2
W29908 FD  DATE-CONTROL-FILE                                               CL**6
W29908     RECORDING MODE F                                                CL**2
W29908     LABEL RECORDS STANDARD                                          CL**2
W29908     BLOCK CONTAINS  0 RECORDS.                                      CL**2
W29908 01  DATE-CONTROL-RECORD PIC X(80).                                  CL**6
00041 *                                                                    CL**2
00042  FD  REPORT-FILE                                                  MLKRP004
00043      RECORDING MODE F                                             MLKRP004
00044      LABEL RECORDS OMITTED                                        MLKRP004
00045      BLOCK CONTAINS  0 RECORDS.                                   MLKRP004
00046  01  REPORT-RECORD.                                               MLKRP004
00047      05  REPORT-LINE        PIC X(132).                              CL**2
                                                                                
00042  FD  REPORT-FILE-2                                                MLKRP004
00043      RECORDING MODE F                                             MLKRP004
00044      LABEL RECORDS OMITTED                                        MLKRP004
00045      BLOCK CONTAINS  0 RECORDS.                                   MLKRP004
00046  01  REPORT-RECORD-2.                                             MLKRP004
00047      05  REPORT-LINE-2      PIC X(132).                              CL**2
00048  EJECT                                                            MLKRP004
00049  WORKING-STORAGE SECTION.                                         MLKRP004
00050 *                                                                 MLKRP004
207210     COPY MLWS050.                                                MLKRP004
00053      COPY DPWS132O.                                               MLKRP004
W34517******************************************************************        
W34517*  HEADER LINE                                                            
W34517******************************************************************        
W34517*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
W34517*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
W34517     COPY DPWS085.                                                        
WR5830*                                                                 MLKRP004
W29908*    COPY DPWS500.                                                MLKRP004
W29908******************************************************************        
W29908*  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
W29908******************************************************************        
W29908     COPY DPWS004.                                                        
W29908                                                                          
W29908                                                                          
W29908     EXEC SQL                                                             
W29908         INCLUDE SQLCA                                                    
W29908     END-EXEC.                                                            
W29908                                                                          
W29908     EXEC SQL                                                             
W29908         INCLUDE TDTATTR                                                  
W29908     END-EXEC.                                                            
W29908                                                                          
00054                                                                   MLKRP004
00055  01  LC-LEDGER-CONTROL-RECORD.                                       CL**6
00056      05  LC-LEDGER-IND         PIC X.                                CL**6
00057          88  LC-MERCH-LEDGER                 VALUE 'M'.              CL**6
00058          88  LC-INV-LEDGER                   VALUE 'I'.              CL**6
WR5830     05  FILLER                PIC X         VALUE SPACES.                
WR5830     05  LC-OVERRIDE-IND       PIC X         VALUE SPACES.                
WR5830     05  FILLER                PIC X         VALUE SPACES.                
WR5830     05  LC-OVERRIDE-BATCH-NO  PIC X(3)      VALUE SPACES.                
00059      05  FILLER                PIC X(73).                            CL**4
00060                                                                      CL**2
00061  01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              MLKRP004
23511      05  PA-RETAIL             PIC S9(11)V99  VALUE ZERO.         MLKRP004
23511      05  PA-COST               PIC S9(11)V99  VALUE ZERO.         MLKRP004
23511      05  PA-TOTAL-RETAIL       PIC S9(11)V99  VALUE ZERO.         MLKRP004
23511      05  PA-TOTAL-COST         PIC S9(11)V99  VALUE ZERO.         MLKRP004
23511      05  PA-GRAND-RETAIL       PIC S9(11)V99  VALUE ZERO.         MLKRP004
23511      05  PA-GRAND-COST         PIC S9(11)V99  VALUE ZERO.         MLKRP004
00068      05  PA-RECORD-COUNT       PIC S9(9)     VALUE ZERO.          MLKRP004
00069 *                                                                 MLKRP004
WR5830 01  PA2-PROGRAM-ACCUMULATORS                COMP-3.              MLKRP004
WR5830* PURCHASE ADJ TRANSACTIONS/ CODE 01=RECEIPT /CURR OR PRIOR WEEK  MLKRP004
WR5830     05  PA2-PRCH-ADJ-RECPT-CURR     PIC S9(10)V99  VALUE ZEROS.  MLKRP004
WR5830     05  PA2-PRCH-ADJ-RECPT-PRIOR    PIC S9(10)V99  VALUE ZEROS.  MLKRP004
WR5830* RECEIPT TRANSACTIONS/CODE 01=RECEIPT/CURR                       MLKRP004
WR5830     05  PA2-RECEIPTS-CURR           PIC S9(10)V99  VALUE ZEROS.  MLKRP004
WR5830* TOTAL ACCUMULATOR (PURCH ADJ/RECPTS / TOTAL RECPTS/ADJ CURRENT) MLKRP004
WR5830     05  PA2-TOTAL-PRCH-ADJ-VALUE    PIC S9(11)V99  VALUE ZEROS.  MLKRP004
WR5830     05  PA2-TOTAL-RECEIPTS-VALUE    PIC S9(11)V99  VALUE ZEROS.  MLKRP004
WR5830     05  PA2-TOTAL-CURR-RECEIPTS     PIC S9(11)V99  VALUE ZEROS.  MLKRP004
W29908* INVENTORY ADJUSTMENTS / CODE 80                                 MLKRP004
W29908     05  PA2-TOTAL-INV-ADJUSTMENTS   PIC S9(11)V99  VALUE ZEROS.  MLKRP004
00069 *                                                                 MLKRP004
00070  01  PC-PRINT-CONTROL                        COMP-3.              MLKRP004
00071       05  PC-LINE-COUNT        PIC S9(9)     VALUE +56.           MLKRP004
00072       05  PC-PAGE-COUNT        PIC S9(5)     VALUE ZERO.          MLKRP004
00073       05  PC-LINE-MAX          PIC S9(9)     VALUE +55.           MLKRP004
00074       05  PC-ADVANCE           PIC S9(3)     VALUE ZERO.          MLKRP004
00074       05  PC-SUB1              PIC S9(2)     VALUE ZERO.          MLKRP004
00074       05  PC-SUB2              PIC S9(2)     VALUE ZERO.          MLKRP004
00075                                                                      CL**5
C29908 01  PS-PROGRAM-SWITCHES.                                            CL**5
C29908      05  PS-CUTOFF-WEEK-SW         PIC X(1) VALUE 'N'.              CL**5
C29008          88  PS-CUTOFF-WEEK-DATE            VALUE 'Y'.              CL**4
C29008          88  PS-END-OF-WEEK-DATE            VALUE 'N'.              CL**4
00075                                                                      CL**5
00076  01  PC-PROGRAM-CONSTANTS.                                           CL**5
00077       05  PC-MERCH-LEDGER-LIT  PIC X(20)     VALUE                   CL**5
00078                              '(MERCHANDISE LEDGER)'.                 CL**4
00079       05  PC-INV-LEDGER-LIT    PIC X(20)     VALUE                   CL**5
00080                              ' (INVENTORY LEDGER) '.                 CL**4
00077       05  PC-TITLE-BATCH       PIC X(33)     VALUE                   CL**5
00078                              'WEEKLY BATCH TOTALS BEFORE UPDATE'.    CL**4
WR5830      05  PC-TITLE-RECEIPTS    PIC X(33)     VALUE                   CL**5
WR5830                             'MULTIPLE FLUSH REPORT            '.    CL**4
WR5830      05  PC-RECEIPTS-HEADING  PIC X(13)      VALUE                       
WR5830                             'RECEIPTS     '.                             
WR5830      05  PC-RCPTS-AND-ADJ     PIC X(31)      VALUE                       
WR5830                             'VERIFIED RECEIPTS & RECEIPT ADJ'.           
WR5830      05  PC-PRCH-ADJ-HEADING PIC X(13)     VALUE                         
WR5830                             'PURCHASE ADJ'.                              
WR5830      05  PC-PRCH-ADJ         PIC X(20)     VALUE                         
WR5830                             'PURCHASE ADJUSTMENTS'.                      
WR5830      05  PC-CURR-WEEK-HDR     PIC X(26)      VALUE                       
WR5830                             'VERIFIED FOR THE WEEK OF  '.                
WR5830                                                                          
WR5830 01  PC-TRAN-TYPES.                                                       
WR5830      05  BATCH-RECEIPTS       PIC 9(3)       VALUE ZEROES.               
WR5830      05  BATCH-PURCHASE-ADJ   PIC 9(3)       VALUE ZEROES.               
WR5830      05  PC-AP-CODES          PIC X(2).                                  
WR5830          88 RECEIPTS-AND-ADJ                 VALUE '01'.                 
WR5830* SYSIN CONTROL-CARD                                              MLKRP004
WR5830 01  CC-CONTROL-CARD.                                                     
WR5830*    05  CC-CALENDAR-TYPE      PIC X(02)     VALUE SPACES.                
WR5830     05  FILLER                PIC X(03)     VALUE SPACES.                
WR5830     05  SYSIN-DATE            PIC X(10)     VALUE ZERO.                  
W29908*                                                                 MLKRP004
W29908 01  CC-JULIAN-DATES.                                                     
W29908     05  CC-JULIAN-DATE-SIGN   PIC S9(07)    VALUE ZEROES.                
W29908     05  CC-JULIAN-DATE-9      PIC 9(07)     VALUE ZEROES.                
W29908     05  CC-JULIAN-DATE.                                                  
W29908         10  CC-JULIAN-YEAR    PIC 9(04)     VALUE ZEROES.                
W29908         10  CC-JULIAN-DAY     PIC 9(03)     VALUE ZEROES.                
W29908     05  CC-JULIAN-PLUS-500    PIC 9(03)     VALUE ZEROES.                
W29908     05  CC-JULIAN-PLUS-499    PIC 9(03)     VALUE ZEROES.                
W29908     05  CC-JULIAN-PLUS1       PIC 9(03)     VALUE ZEROES.                
00081 *                                                                 MLKRP004
00082  01  SA-SAVE-AREAS.                                               MLKRP004
00083      05  SA-BATCH              PIC X(4)      VALUE SPACES.        MLKRP004
00084      05  SA-BATCH-2 REDEFINES SA-BATCH.                           MLKRP004
00085          10  FILLER            PIC X.                             MLKRP004
00086          10  SA-SHORT-BATCH    PIC X(3).                          MLKRP004
WR5830     05  SA-BATCH-CONVERT      PIC X(4)      VALUE SPACES.        MLKRP004
WR5830     05  SA-BATCH-NUMERIC REDEFINES SA-BATCH-CONVERT.             MLKRP004
WR5830         10  FILLER            PIC X.                             MLKRP004
WR5830         10  SA-BATCH-NUM      PIC 9(3).                          MLKRP004
00087      05  SA-CODE               PIC 9(2)      VALUE ZERO.          MLKRP004
00088      05  SA-PURCH-BATCH.                                          MLKRP004
00089          10  SA-P-BATCH        PIC X(3)      VALUE ZERO.          MLKRP004
00090          10  FILLER            PIC X         VALUE SPACES.        MLKRP004
00091      05  SA-DATE.                                                 MLKRP004
00092          10  SA-YEAR           PIC X(2)      VALUE SPACES.        MLKRP004
00093          10  SA-MONTH          PIC X(2)      VALUE SPACES.        MLKRP004
00094          10  SA-DAY            PIC X(2)      VALUE SPACES.        MLKRP004
00095      05  SA-TIME.                                                 MLKRP004
00096          10  SA-HOUR           PIC X(2)      VALUE SPACES.        MLKRP004
00097          10  SA-MINUTE         PIC X(2)      VALUE SPACES.        MLKRP004
00098          10  FILLER            PIC X(2).                          MLKRP004
W29908     05  SA-DATE-DB2-N         PIC X(10).                                 
W29908     05  SA-DATE-DB2  REDEFINES SA-DATE-DB2-N.                            
W29908         10  SA-CC-DB2         PIC X(2).                          MLKRP004
W29908         10  SA-YY-DB2         PIC X(2).                          MLKRP004
W29908         10  FILLER            PIC X(1).                          MLKRP004
W29908         10  SA-MM-DB2         PIC X(2).                          MLKRP004
W29908         10  FILLER            PIC X(1).                          MLKRP004
W29908         10  SA-DD-DB2         PIC X(2).                          MLKRP004
WR5830     05  HOLD-JULIAN            PIC 9(5).                                 
WR5830     05  HOLD-JULIAN-RED REDEFINES HOLD-JULIAN.                           
WR5830         10  FILLER             PIC 99.                                   
WR5830         10  JULIAN-DAY-HOLD    PIC 999.                                  
WR5830     05  HOLD-JULIAN-PLUS.                                                
WR5830         10  JULIAN-DAY-PLUS    PIC 999.                                  
00099 *                                                                 MLKRP004
WR5830     05  SA-BATCH-NO-JRNL.                                                
WR5830         10  SA-BATCH-NO-JRNL-X PIC X(3)     VALUE SPACE.                 
WR5830         10  FILLER             PIC X(1)     VALUE SPACE.                 
WR5830     05  SA-BATCH-NO-UNJRNL.                                              
WR5830         10  SA-BATCH-NO-UNJRNL-X PIC X(3)   VALUE SPACE.                 
WR5830         10  FILLER             PIC X(1)     VALUE SPACE.                 
00099 *                                                                 MLKRP004
00100  01  PT-PURCHASE-TYPE          PIC X(2)      VALUE SPACE.         MLKRP004
00101      88  PT-REVERSAL                         VALUE '02' '03'      MLKRP004
00102                                                    '04' '05'      MLKRP004
00103                                                    '08' '11'      MLKRP004
00104                                                    '14'.          MLKRP004
00105  01  PV-PROGRAM-VARIABLES.                                        MLKRP004
W29908     05  PV-JULIAN-DATE-HOLD          PIC S9(9)    VALUE ZERO.    MLKRP004
W29908     05  PV-JULIAN-DATE-CNVRT         PIC  9(9)    VALUE ZERO.    MLKRP004
C29908     05  PV-PREV-FISCAL-WEEK-BEGIN    PIC X(10)    VALUE SPACE.   MLKRP004
00106      05  PV-TRANSFER-BATCH.                                       MLKRP004
00107          10  FILLER                   PIC 9         VALUE ZERO.   MLKRP004
00108          10  PV-TRANSFER-BATCH-POS2-4                             MLKRP004
00109                                       PIC X(3)      VALUE SPACES. MLKRP004
W29908     05  PV-PARAGRAPH-NAME            PIC  X(30)     VALUE SPACE.         
W29908     05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)     VALUE SPACE.         
W29908     05  PV-ABEND-CODE                PIC S9(04)     COMP                 
W29908                                                     VALUE ZERO.          
W34517     05  PV-JOB-INFO.                                                     
W34517         10  FILLER                  PIC X(01)       VALUE '('.           
W34517         10  PV-JOB-NAME             PIC X(08)       VALUE SPACES.        
W34517         10  FILLER                  PIC X(01)       VALUE ')'.           
00110                                                                   MLKRP004
00111  01  HR-HOLD-RECORD.                                              MLKRP004
00112      05  HR-CODE               PIC X(2)      VALUE SPACES.        MLKRP004
00113          88  HR-PURCHASE                     VALUE '10'.          MLKRP004
00114          88  HR-TRANSFER                     VALUE '30'           MLKRP004
00115                                                    '31'.          MLKRP004
00116          88  HR-MARKDOWN                     VALUE '40'.          MLKRP004
00117          88  HR-MARKON                       VALUE '50'.          MLKRP004
W29908         88  HR-IN-ADJUST                    VALUE '80'.          MLKRP004
00118      05  HR-BATCH              PIC 9(4)      VALUE ZERO.          MLKRP004
23511      05  FILLER                PIC X(119)    VALUE SPACES.        MLKRP004
00120                                                                   MLKRP004
00121  01  SW-SWITCHES.                                                 MLKRP004
00122      05  SW-EOF-BATCH          PIC X         VALUE 'N'.           MLKRP004
00123          88  SW-EOF-BATCH-FILE               VALUE 'Y'.           MLKRP004
00124      05  SW-FIRST-PASS-THRU    PIC X         VALUE 'Y'.           MLKRP004
00125          88  SW-FIRST-PASS                   VALUE 'Y'.           MLKRP004
00126                                                                   MLKRP004
00127  01  ABEND-CODE                PIC S9(04)    VALUE +0 COMP SYNC.     CL**2
00128                                                                   MLKRP004
00129  01  H2-HEADING2.                                                 MLKRP004
00130      05  FILLER                PIC X(39)     VALUE SPACES.        MLKRP004
00131      05  FILLER                PIC X(33)     VALUE                MLKRP004
00132      'WEEKLY BATCH TOTALS BEFORE UPDATE'.                         MLKRP004
00133      05  FILLER                PIC X(2)      VALUE SPACES.           CL**4
00134      05  H2-LEDGER-LIT         PIC X(20)     VALUE SPACES.           CL**4
00135      05  FILLER                PIC X(38)     VALUE SPACES.           CL**4
00136                                                                   MLKRP004
00137  01  H3-HEADING3.                                                 MLKRP004
00138      05  FILLER                PIC X(37)     VALUE SPACES.        MLKRP004
00139      05  FILLER                PIC X(60)     VALUE                MLKRP004
00140      'CODE  BATCH NO  RETAIL AMOUNT     COST AMOUNT'.             MLKRP004
00141      05  FILLER                PIC X(35)     VALUE SPACES.        MLKRP004
00142                                                                   MLKRP004
WR5830 01  H4-HEADING1.                                                 MLKRP004
WR5830     05  FILLER                PIC X(39)     VALUE SPACES.        MLKRP004
WR5830     05  H4-HEADING-TITLE      PIC X(33)     VALUE SPACES.        MLKRP004
WR5830     05  FILLER                PIC X(2)      VALUE SPACES.           CL**4
WR5830     05  H4-LEDGER-LIT         PIC X(20)     VALUE SPACES.           CL**4
WR5830     05  FILLER                PIC X(38)     VALUE SPACES.           CL**4
WR5830                                                                  MLKRP004
00143  01  D1-DETAIL-LINE.                                              MLKRP004
00144      05  FILLER                PIC X(38)     VALUE SPACE.         MLKRP004
00145      05  D1-CODE               PIC XX        VALUE SPACES.        MLKRP004
00146      05  FILLER                PIC X(5)      VALUE SPACES.        MLKRP004
00147      05  D1-BATCH              PIC 999       VALUE ZERO.          MLKRP004
00148      05  D1-RETAIL             PIC ZZ,ZZZ,ZZZ,ZZZ.99-.            MLKRP004
00149      05  D1-COST               PIC ZZ,ZZZ,ZZZ,ZZZ.99-.            MLKRP004
00150                                                                   MLKRP004
00151  01  T1-TOTAL-LINE.                                               MLKRP004
00152      05  FILLER                PIC X(35)     VALUE SPACE.         MLKRP004
00153      05  T1-HOLD               PIC X(13)     VALUE SPACES.        MLKRP004
00154      05  T1-RETAIL             PIC ZZ,ZZZ,ZZZ,ZZZ.99-             MLKRP004
00155                                              VALUE ZERO.          MLKRP004
00156      05  T1-COST               PIC ZZ,ZZZ,ZZZ,ZZZ.99-             MLKRP004
00157                                              VALUE ZERO.          MLKRP004
00158                                                                   MLKRP004
00159  01  RL-RECORD-LINE.                                              MLKRP004
00160      05  FILLER                PIC X         VALUE SPACE.         MLKRP004
00161      05  RL-RECORDS            PIC ZZ,ZZZ,ZZZ                     MLKRP004
00162                                              VALUE ZERO.          MLKRP004
00163      05  FILLER                PIC X(20)     VALUE                MLKRP004
00164      ' RECORDS TOTALED'.                                          MLKRP004
                                                                                
WR5830********** FOLLOWING ENTRIES ARE USED FOR REPORT-2***************         
WR5830 01  D2-JOURNAL-HEADER.                                                   
WR5830     05  FILLER                PIC X(20)      VALUE SPACES.               
WR5830     05  D2-ENTRY-TYPE         PIC X(13)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(01)      VALUE SPACES.               
WR5830     05  D2-TRANS-TYPE         PIC X(04)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(84)      VALUE SPACES.               
WR5830                                                                          
WR5830*D2-JOURNAL-LINE IS THE VERIFIED WEEK (PRIOR) MM/DD/YY HEADING            
WR5830 01  D2-JOURNAL-LINE.                                                     
WR5830     05  FILLER                PIC X(38)      VALUE SPACES.               
WR5830     05  D2-VERIFIED-WEEK      PIC X(26)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(01)      VALUE SPACES.               
WR5830     05  D2-WEEK-DATES.                                                   
WR5830         10 D2-WEEK-MM     PIC X(02).                                     
WR5830         10 FILLER         PIC X(01)      VALUE '/'.                      
WR5830         10 D2-DAY-DD      PIC X(02).                                     
WR5830         10 FILLER         PIC X(01)      VALUE '/'.                      
WR5830         10 D2-YEAR-YY     PIC X(02).                                     
WR5830     05  FILLER            PIC X(59)      VALUE SPACES.                   
WR5830                                                                          
WR5830 01  D3-JOURNAL-LINE.                                                     
WR5830     05  FILLER                PIC X(42)      VALUE SPACES.               
WR5830     05  D3-VERIFIED-TYPE      PIC X(31).                                 
WR5830     05  FILLER                PIC X(02)      VALUE SPACES.               
WR5830     05  D3-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
WR5830     05  FILLER                PIC X(24)      VALUE SPACES.               
WR5830                                                                          
WR5830 01  D4-TOTAL-LINE.                                                       
WR5830     05  FILLER                PIC X(39)      VALUE SPACES.               
WR5830     05  D4-BATCH-TITLE        PIC X(05)      VALUE 'BATCH'.              
WR5830     05  FILLER                PIC X(01)      VALUE SPACES.               
WR5830     05  D4-BATCH-TYPE         PIC X(04)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(01)      VALUE SPACES.               
WR5830     05  D4-RETAIL-VALUE       PIC X(19)      VALUE                       
WR5830                                        ' RETAIL VALUE      '.            
WR5830     05  D4-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
WR5830     05  FILLER                PIC X(30)      VALUE SPACES.               
WR5830*                                                                         
WR5830 01  T2-TOTAL-LINE.                                                       
WR5830     05  FILLER                PIC X(22)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(37)      VALUE                       
WR5830                       'TOTAL RECEIPT & ADJ VERIFIED WEEK OF '.           
WR5830     05  T2-CURR-DATE          PIC X(08)      VALUE SPACES.               
WR5830     05  FILLER                PIC X(08)      VALUE SPACES.               
WR5830     05  T2-VERIFIED-AMTS      PIC Z,ZZZ,ZZZ,ZZ9.99-.                     
WR5830     05  FILLER                PIC X(20)      VALUE SPACES.               
WR5830*                                                                         
W29908                                                                          
W29908 LINKAGE SECTION.                                                         
W29908                                                                          
00165   EJECT                                                           MLKRP004
00166  PROCEDURE DIVISION.                                              MLKRP004
00167                                                                   MLKRP004
00168 *************************************************************     MLKRP004
00169 *A000-MAINLINE  HIGHEST LEVEL CONTROL MODULE                *     MLKRP004
00170 *************************************************************     MLKRP004
00171  A000-MAINLINE.                                                   MLKRP004
WR5830      PERFORM A100-INITIALIZATION-ROUTINE.                                
00179       PERFORM B200-PROCESS-BATCHES                                MLKRP004
00180          UNTIL SW-EOF-BATCH-FILE.                                 MLKRP004
00181       PERFORM C100-WRITE-BATCH-LINE.                              MLKRP004
00182       PERFORM C200-WRITE-CODE-TOTAL.                              MLKRP004
00183       PERFORM B300-WRITE-FINAL-TOTAL.                             MLKRP004
WR5830      PERFORM B500-TALLY-RECEIPTS.                                        
WR5830      PERFORM C700-WRITE-RECEIPT-REPORT.                                  
00184       CLOSE BATCH-FILE                                            MLKRP004
00185             REPORT-FILE.                                          MLKRP004
00186       STOP RUN.                                                   MLKRP004
00187                                                                   MLKRP004
00377 ******************************************************************MLKRP004
00378 * OPEN BATCH/REPORT FILES. OBTAIN SATURDAY SYSIN DATE FOR PROCESS.MLKRP004
00377 * NEW MODULE   -  WAS PREVIOUSLY EXECUTED DIRECLTY IN A000-MAIN***MLKRP004
00377 ******************************************************************MLKRP004
00382  A100-INITIALIZATION-ROUTINE.                                     MLKRP004
00172       OPEN INPUT  BATCH-FILE                                      MLKRP004
W29908                  LEDGER-CONTROL-FILE                             MLKRP004
W29908                  DATE-CONTROL-FILE                               MLKRP004
00173            OUTPUT REPORT-FILE                                     MLKRP004
00173                   REPORT-FILE-2                                   MLKRP004
                                                                                
W34517*  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
W34517     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
W34517         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
W34517     MOVE DP085-JOB-NAME     TO PV-JOB-NAME.                              
W34517                                                                          
W34517     MOVE PV-JOB-INFO                                                     
W34517         TO DP132O-STANDRD-HEADER-132-COLS(20:10).                        
                                                                                
00174 *     PERFORM B100-FORMAT-DATE-TIME.                              MLKRP004
00175       PERFORM B150-PROCESS-CONTROL-CARD.                          MLKRP004
WR5830      INITIALIZE PA2-PROGRAM-ACCUMULATORS.                        MLKRP004
WR5830***************************************************************   MLKRP004
WR5830*  DETERMINE THE PURCHASE ADJ/RECEIPTS BATCH NUMBER FROM                  
WR5830*  THE CURRENT DATE OR FROM THE OVERRIDE CONTROL CARD NUMBER              
WR5830***************************************************************   MLKRP004
WR5830      IF LC-OVERRIDE-IND EQUAL 'O'                                        
WR5830          MOVE LC-OVERRIDE-BATCH-NO TO JULIAN-DAY-HOLD                    
W29908      ELSE                                                                
W29908          MOVE SYSIN-DATE TO DTATTR-KY-DTE                                
W29908          PERFORM E100-SELECT-TDTATTR                                     
S29908*         MOVE DTATTR-JLN-DTE         TO CC-JULIAN-DATE                   
S29908          MOVE DTATTR-JLN-DTE         TO CC-JULIAN-DATE-SIGN              
S29908          MOVE CC-JULIAN-DATE-SIGN    TO CC-JULIAN-DATE-9                 
S29908          MOVE CC-JULIAN-DATE-9       TO CC-JULIAN-DATE                   
W29908          MOVE DTATTR-FSCL-WK-BEG-DTE TO SA-DATE-DB2-N                    
W29908*****************************************************************         
W29908* JULIAN DATE IS USED ON THE BATCH TOTALS FOR PURCHASE TRANS.             
W29908* THE JULIAN DATE FROM THE JOB CARD WILL BE ATTACHED TO RECEIPTS          
W29908* THE JULIAN DATE (PLUS 500 DAYS) IS USED FOR PURCH-ADJUSTMENTS           
W29908* OCCASIONALLY AP RUNS AFTER MIDNIGHT WHICH WOULD HAVE A JULIAN           
W29908* DATE OF 500 - 1DAY (REASON FOR PLUS-499 VAR)                            
W29908*****************************************************************         
S29908          COMPUTE CC-JULIAN-PLUS1    = CC-JULIAN-DAY + 1                  
S29908          COMPUTE CC-JULIAN-PLUS-500 = CC-JULIAN-PLUS1 + 500              
S29908          COMPUTE CC-JULIAN-PLUS-499 = CC-JULIAN-PLUS1 + 499              
S29908** DATES                                                                  
S29908          DISPLAY 'CC-JULIAN-DAY: ',      CC-JULIAN-DAY                   
S29908          DISPLAY 'CC-JULIAN-PLUS1: ',    CC-JULIAN-PLUS1                 
S29908          DISPLAY 'CC-JULIAN-PLUS-500: ', CC-JULIAN-PLUS-500              
S29908          DISPLAY 'CC-JULIAN-PLUS-499: ', CC-JULIAN-PLUS-499              
W29908*         COMPUTE CC-JULIAN-PLUS-499 = CC-JULIAN-DAY + 499                
W29908*         COMPUTE CC-JULIAN-PLUS-500 = CC-JULIAN-DAY + 500                
W29908*         MOVE CC-JULIAN-DATE      TO SA-BATCH-NO-UNJRNL-X                
W29908          MOVE CC-JULIAN-PLUS1     TO SA-BATCH-NO-UNJRNL-X                
W29908          MOVE CC-JULIAN-PLUS-499  TO SA-BATCH-NO-JRNL-X                  
W29908      END-IF.                                                             
W29908                                                                          
W29908      PERFORM B100-FORMAT-DATE-TIME.                              MLKRP004
W29908*     PERFORM B150-PROCESS-CONTROL-CARD.                          MLKRP004
W29908                                                                          
00176       MOVE 'MLKRP004' TO DP132O-PROGRAM-NAME.                     MLKRP004
00177       MOVE '1' TO DP132O-REPORT-NUMBER.                           MLKRP004
                                                                                
00178       PERFORM Z100-READ-BATCH-FILE.                               MLKRP004
                                                                                
00188 ***************************************************************   MLKRP004
00189 * B100-FORMAT-DATE-TIME  THE CURRENT DATE AND TIME ARE MOVED  *   MLKRP004
00190 * TO THE REPORT HEADING LINES.                                *   MLKRP004
00191 ***************************************************************   MLKRP004
00192   B100-FORMAT-DATE-TIME.                                          MLKRP004
00193       ACCEPT SA-DATE FROM DATE.                                   MLKRP004
00194       ACCEPT SA-TIME FROM TIME.                                   MLKRP004
00195       MOVE SA-MONTH TO DP132O-RUN-MONTH.                          MLKRP004
00196       MOVE SA-DAY TO DP132O-RUN-DAY.                              MLKRP004
00197       MOVE SA-YEAR TO DP132O-RUN-YEAR.                            MLKRP004
00198       MOVE SA-HOUR TO DP132O-RUN-HOUR.                            MLKRP004
00199       MOVE SA-MINUTE TO DP132O-RUN-MINUTE.                        MLKRP004
00200 *                                                                 MLKRP004
WR5830******************************************************************MLKRP004
WR5830*  VALIDATE THE JOBTRAC DATE AND OBTAIN BEGINNING OF WEEK(SUNDAY)         
WR5830******************************************************************MLKRP004
WR5830                                                                  MLKRP004
WR5830*    ACCEPT CC-CONTROL-CARD  FROM SYSIN.                          MLKRP004
WR5830                                                                  04030000
W29908     IF  DTATTR-DAY-NM = 'SATURDAY '                              04040000
WR5830         NEXT SENTENCE                                            04050000
WR5830     ELSE                                                         04060000
C29908         EVALUATE TRUE                                               CL**6
C29908             WHEN LC-INV-LEDGER                                      CL**6
C29908                 SET PS-CUTOFF-WEEK-DATE TO TRUE                     CL**6
C29908             WHEN LC-MERCH-LEDGER                                    CL**6
C29908                 DISPLAY '** ABEND **'                            04070000
C29908                 DISPLAY '** PGM = MLKRP004'                      04080000
C29908                 DISPLAY '** INPUT DATE NOT A SATURDAY DATE.'     04090000
C29908                 MOVE +4003 TO ABEND-CODE                         04100000
C29908                 CALL 'ILBOABN0' USING ABEND-CODE                 04110000
C29908         END-EVALUATE                                                CL**6
WR5830     END-IF.                                                      04120000
WR5830                                                                  04130000
C29908     DISPLAY 'DTATTR-DAY-NM : ', DTATTR-DAY-NM.                           
041300                                                                          
00201 ******************************************************************MLKRP004
00202 * ACCEPT THE INPUT CONTROL CARD.                                  MLKRP004
00203 ******************************************************************MLKRP004
00204  B150-PROCESS-CONTROL-CARD.                                       MLKRP004
00205                                                                   MLKRP004
W29908*    OPEN INPUT LEDGER-CONTROL-FILE.                                 CL**6
00207                                                                      CL**2
00208      READ LEDGER-CONTROL-FILE INTO LC-LEDGER-CONTROL-RECORD          CL**6
00209          AT END DISPLAY '*** ABEND *** IN MLKRP004'                  CL**2
00210                 DISPLAY 'LEDGER CONTROL CARD EMPTY'                  CL**6
00211                 MOVE +4003 TO ABEND-CODE                             CL**2
00212                 CALL 'ILBOABN0' USING ABEND-CODE.                    CL**2
00213                                                                      CL**2
00214      DISPLAY 'MLKRP004 LEDGER CONTROL CARD:'                         CL**6
00215               LC-LEDGER-CONTROL-RECORD.                              CL**6
00216                                                                      CL**2
00217      EVALUATE TRUE                                                   CL**4
00218          WHEN LC-MERCH-LEDGER                                        CL**6
WR5830             MOVE PC-TITLE-BATCH      TO H4-HEADING-TITLE                 
00219              MOVE PC-MERCH-LEDGER-LIT TO H2-LEDGER-LIT               CL**4
WR5830                                         H4-LEDGER-LIT               CL**4
00220          WHEN LC-INV-LEDGER                                          CL**6
WR5830             MOVE PC-TITLE-BATCH      TO H4-HEADING-TITLE                 
00221              MOVE PC-INV-LEDGER-LIT   TO H2-LEDGER-LIT               CL**4
WR5830                                         H4-LEDGER-LIT               CL**4
00222          WHEN OTHER                                                  CL**4
00223              DISPLAY '*** ABEND *** IN MLKRP004'                     CL**4
00224              DISPLAY 'INVALID LEDGER INDICATOR ON CONTROL CARD.'     CL**4
00225              MOVE +4003 TO ABEND-CODE                                CL**4
00226              CALL 'ILBOABN0' USING ABEND-CODE                        CL**4
00227      END-EVALUATE.                                                   CL**4
00228                                                                      CL**4
           IF LC-OVERRIDE-IND EQUAL 'O'                                         
               IF LC-OVERRIDE-BATCH-NO NUMERIC                                  
                   CONTINUE                                                     
               ELSE                                                             
00223              DISPLAY '*** ABEND *** IN MLKRP004'                     CL**4
00224              DISPLAY 'INVALID BATCH NUMBER ON CONTROL CARD.'         CL**4
                   DISPLAY LC-LEDGER-CONTROL-RECORD                             
00225              MOVE +4003 TO ABEND-CODE                                CL**4
00226              CALL 'ILBOABN0' USING ABEND-CODE.                       CL**4
                                                                                
W29908     READ DATE-CONTROL-FILE INTO CC-CONTROL-CARD                     CL**6
00209          AT END DISPLAY '*** ABEND *** IN MLKRP004'                  CL**2
00210                 DISPLAY 'DATE CONTROL CARD EMPTY'                    CL**6
00211                 MOVE +4003 TO ABEND-CODE                             CL**2
00212                 CALL 'ILBOABN0' USING ABEND-CODE.                    CL**2
00213                                                                      CL**2
00229      CLOSE LEDGER-CONTROL-FILE                                       CL**6
00229            DATE-CONTROL-FILE.                                        CL**6
00230                                                                   MLKRP004
00231                                                                   MLKRP004
00232 ****************************************************************  MLKRP004
00233 * B200-PROCESS-BATCHES    CONTROLS THE PROCESSING OF TRANSACTION  MLKRP004
00234 * TYPES.  CHECKS FOR CHANGES IN BATCH OR TRANSACTION TYPES, AND   MLKRP004
00235 * CALCULATES THE COST AND RETAIL AMOUNTS FOR EACH BATCH.          MLKRP004
00236 ***************************************************************   MLKRP004
00237   B200-PROCESS-BATCHES.                                           MLKRP004
00238                                                                   MLKRP004
00239       IF SW-FIRST-PASS                                            MLKRP004
00240           MOVE HR-CODE TO SA-CODE                                 MLKRP004
00241           MOVE HR-BATCH TO SA-BATCH                               MLKRP004
00242           MOVE 'N' TO SW-FIRST-PASS-THRU                          MLKRP004
00243       END-IF.                                                     MLKRP004
00244                                                                   MLKRP004
00245       IF HR-CODE NOT = SA-CODE                                    MLKRP004
00246           PERFORM C100-WRITE-BATCH-LINE                           MLKRP004
00247           PERFORM C200-WRITE-CODE-TOTAL                           MLKRP004
00248       ELSE                                                        MLKRP004
00249           IF HR-BATCH NOT = SA-BATCH                              MLKRP004
00250               PERFORM C100-WRITE-BATCH-LINE                       MLKRP004
00251           END-IF                                                  MLKRP004
00252       END-IF.                                                     MLKRP004
00253                                                                   MLKRP004
00254       IF HR-PURCHASE                                              MLKRP004
00255           MOVE D115-TRANS-CD TO PT-PURCHASE-TYPE                  MLKRP004
00256           IF PT-REVERSAL                                          MLKRP004
00257               PERFORM C300-PROCESS-REVERSAL-AMT                   MLKRP004
00258           ELSE                                                    MLKRP004
00259               COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT     MLKRP004
00260               COMPUTE PA-COST = PA-COST + D115-NET-COST           MLKRP004
00261           END-IF                                                  MLKRP004
WR5830********* PROCESS FOR VERIFIED RECEIPTS/ADJ PURCHASE REPORT ******        
WR5830          PERFORM B210-PROCESS-RECEIPTS                                   
00262       END-IF.                                                     MLKRP004
00263       IF HR-TRANSFER                                              MLKRP004
00264           COMPUTE PA-RETAIL = PA-RETAIL +                         MLKRP004
00265               (D115-RETAIL-PER * D115-UNITS-TRANSFER)             MLKRP004
00266           MOVE ZERO TO PA-COST                                    MLKRP004
00267       END-IF.                                                     MLKRP004
00268       IF HR-MARKDOWN                                              MLKRP004
00269           COMPUTE PA-RETAIL = PA-RETAIL +                         MLKRP004
00270               ((D115-MKDWN-OPRICE - D115-MKDWN-NPRICE) *          MLKRP004
00271               D115-MKDWN-QTY)                                     MLKRP004
00272           MOVE ZERO TO PA-COST                                    MLKRP004
00273       END-IF.                                                     MLKRP004
00274       IF HR-MARKON                                                MLKRP004
00275           COMPUTE PA-RETAIL = PA-RETAIL +                         MLKRP004
00276               ((D115-MARKON-NPRICE - D115-MARKON-OPRICE) *        MLKRP004
00277               D115-MARKON-QTY)                                    MLKRP004
00278           MOVE ZERO TO PA-COST                                    MLKRP004
00279       END-IF.                                                     MLKRP004
W29908      IF HR-IN-ADJUST                                             MLKRP004
W29908          COMPUTE PA-RETAIL = PA-RETAIL + D115-AT-RETAIL          MLKRP004
W29908          MOVE ZERO TO PA-COST                                    MLKRP004
W29908          MOVE JULIAN-DAY-PLUS TO HR-BATCH                        MLKRP004
W29908      END-IF.                                                     MLKRP004
00280       ADD 1 TO PA-RECORD-COUNT.                                   MLKRP004
00281       MOVE HR-CODE TO SA-CODE.                                    MLKRP004
00282       MOVE HR-BATCH TO SA-BATCH                                   MLKRP004
00283       PERFORM Z100-READ-BATCH-FILE.                               MLKRP004
00284                                                                   MLKRP004
WR5830******************************************************************        
WR5830* B210-PROCESS  CONVERT SYSIN DATE TO CALENDAR FORMATS                    
WR5830* RECEIPTS AP CODE = '01'  ANY OTHER AP CODES LISTED AS 'OTHER'           
WR5830******************************************************************        
WR5830 B210-PROCESS-RECEIPTS.                                                   
WR5830      MOVE D115-AP-TRANS-CDE TO PC-AP-CODES.                              
WR5830*     MOVE DPG55-1ST-WKDY-GREG-CC-N TO SA-DATE-GREG-FMT.                  
WR5830      MOVE DTATTR-FSCL-WK-BEG-DTE   TO SA-DATE-DB2-N.                     
WR5830*     MOVE SA-MM-DB2  TO SA-MM.                                           
WR5830*     MOVE SA-DD-DB2  TO SA-DD.                                           
WR5830*     MOVE SA-YY-DB2  TO SA-YY.                                           
WR5830*     MOVE SA-CC-DB2  TO SA-CC.                                           
WR5830******************************************************************        
WR5830* FOLLOWING LOGIC DETERMINES WHICH PARAGRAPH/TOTALS TO CALCULATE          
WR5830* B230- PROCESSES RECEIPTS / 'OTHER' CODES                                
WR5830* B250- PROCESSES PURCHASE ADJ  / 'OTHER' CODES                           
WR5830******************************************************************        
WR5830*                                                                         
WR5830***** IF 'RECEIPTS' ENTRIES                                               
WR5830      IF D115-BATCH-NO EQUAL SA-BATCH-NO-UNJRNL                           
                ADD D115-RETAIL-AMT TO PA2-RECEIPTS-CURR                        
WR5830      ELSE                                                                
WR5830***** IF 'PURCHASE ADJ' ENTRIES                                           
WR5830      IF D115-BATCH-NO EQUAL SA-BATCH-NO-JRNL                             
                ADD D115-RETAIL-AMT TO PA2-PRCH-ADJ-RECPT-CURR                  
WR5830      END-IF.                                                             
WR5830                                                                          
00285 *********************************************************         MLKRP004
00286 * B300-WRITE-FINAL-TOTAL  FORMATS FINAL TOTAL LINE      *         MLKRP004
00287 *********************************************************         MLKRP004
00288   B300-WRITE-FINAL-TOTAL.                                         MLKRP004
00289       MOVE 'FINAL TOTALS' TO T1-HOLD.                             MLKRP004
00290       MOVE PA-GRAND-RETAIL TO T1-RETAIL.                          MLKRP004
00291       MOVE PA-GRAND-COST TO T1-COST.                              MLKRP004
00292       MOVE T1-TOTAL-LINE TO REPORT-RECORD.                        MLKRP004
00293       WRITE REPORT-RECORD                                         MLKRP004
00294           AFTER ADVANCING 2.                                      MLKRP004
00295       MOVE PA-RECORD-COUNT TO RL-RECORDS.                         MLKRP004
00296       MOVE RL-RECORD-LINE TO REPORT-RECORD.                       MLKRP004
00297       WRITE REPORT-RECORD                                         MLKRP004
00298           AFTER ADVANCING 2.                                      MLKRP004
00299                                                                   MLKRP004
                                                                                
WR5830  B500-TALLY-RECEIPTS.                                               CL**4
WR5830      MOVE PA2-RECEIPTS-CURR TO PA2-TOTAL-RECEIPTS-VALUE.                 
WR5830*                                                                         
WR5830      MOVE PA2-PRCH-ADJ-RECPT-CURR TO PA2-TOTAL-PRCH-ADJ-VALUE.           
WR5830*                                                                         
            ADD PA2-PRCH-ADJ-RECPT-CURR TO PA2-RECEIPTS-CURR                    
                GIVING PA2-TOTAL-CURR-RECEIPTS.                                 
00300 ************************************************************      MLKRP004
00301 * C100-WRITE-BATCH-LINE   FORMATS TOTAL FOR A BATCH NUMBER *      MLKRP004
00302 ************************************************************      MLKRP004
00303   C100-WRITE-BATCH-LINE.                                          MLKRP004
00304       IF PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                MLKRP004
00305            PERFORM D100-WRITE-HEADINGS.                           MLKRP004
00306       MOVE SA-CODE TO D1-CODE.                                    MLKRP004
00307       MOVE PA-RETAIL TO D1-RETAIL.                                MLKRP004
00308       ADD PA-RETAIL TO PA-TOTAL-RETAIL.                           MLKRP004
00309       MOVE ZERO TO PA-RETAIL.                                     MLKRP004
00310       IF SA-CODE = '10'                                           MLKRP004
00311           MOVE PA-COST TO D1-COST                                 MLKRP004
00312           ADD  PA-COST TO PA-TOTAL-COST                           MLKRP004
00313           MOVE ZERO TO PA-COST                                    MLKRP004
00314           MOVE SA-BATCH TO SA-PURCH-BATCH                         MLKRP004
00315           MOVE SA-P-BATCH TO D1-BATCH                             MLKRP004
00316       ELSE                                                        MLKRP004
00317           MOVE SA-BATCH  TO D1-BATCH                              MLKRP004
00318           MOVE ZERO TO D1-COST                                    MLKRP004
00319       END-IF.                                                     MLKRP004
00320       MOVE HR-BATCH TO SA-BATCH.                                  MLKRP004
00321       MOVE D1-DETAIL-LINE TO REPORT-LINE.                         MLKRP004
00322       WRITE REPORT-RECORD                                         MLKRP004
00323           AFTER ADVANCING PC-ADVANCE.                             MLKRP004
00324       MOVE 1 TO PC-ADVANCE.                                       MLKRP004
00325       ADD PC-ADVANCE TO PC-LINE-COUNT.                            MLKRP004
00326                                                                   MLKRP004
00327 ********************************************************          MLKRP004
00328 * C200-WRITE-CODE-TOTAL  FORMATS TOTAL FOR A TRAN TYPE *          MLKRP004
00329 ********************************************************          MLKRP004
00330  C200-WRITE-CODE-TOTAL.                                           MLKRP004
00331       IF PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                MLKRP004
00332            PERFORM D100-WRITE-HEADINGS.                           MLKRP004
00333       MOVE 'CODE TOTAL' TO T1-HOLD.                               MLKRP004
00334       MOVE PA-TOTAL-RETAIL TO T1-RETAIL.                          MLKRP004
00335       MOVE PA-TOTAL-COST TO T1-COST.                              MLKRP004
00336       MOVE T1-TOTAL-LINE TO REPORT-LINE.                          MLKRP004
00337       ADD PA-TOTAL-RETAIL TO PA-GRAND-RETAIL.                     MLKRP004
00338       ADD PA-TOTAL-COST TO PA-GRAND-COST.                         MLKRP004
00339       MOVE ZERO TO PA-TOTAL-RETAIL                                MLKRP004
00340                    PA-TOTAL-COST.                                 MLKRP004
00341       MOVE HR-CODE TO SA-CODE.                                    MLKRP004
00342       MOVE 2 TO PC-ADVANCE.                                       MLKRP004
00343       WRITE REPORT-RECORD                                         MLKRP004
00344           AFTER ADVANCING PC-ADVANCE.                             MLKRP004
00345       ADD PC-ADVANCE TO PC-LINE-COUNT.                            MLKRP004
00346                                                                   MLKRP004
00347 *******************************************************           MLKRP004
00348 * C300-PROCESS-REVERSAL-AMT   PURCHASE RECORDS WITH   *           MLKRP004
00349 * TRANSACTION CODES OF 02, 03, 04, 05, 08, 11 AND 14  *           MLKRP004
00350 * MUST HAVE THERE SIGNS REVERSED PRIOR TO BEING ADDED *           MLKRP004
00351 *******************************************************           MLKRP004
00352  C300-PROCESS-REVERSAL-AMT.                                       MLKRP004
00353       COMPUTE D115-NET-COST = D115-NET-COST * -1.                 MLKRP004
00354       COMPUTE D115-RETAIL-AMT = D115-RETAIL-AMT * -1.             MLKRP004
00355       COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT.            MLKRP004
00356       COMPUTE PA-COST = PA-COST + D115-NET-COST.                  MLKRP004
00357                                                                   MLKRP004
WR5830 C700-WRITE-RECEIPT-REPORT.                                               
WR5830     MOVE 0 TO PC-PAGE-COUNT.                                             
WR5830     PERFORM D200-WRITE-HEADINGS.                                         
WR5830     MOVE SPACES TO REPORT-RECORD-2.                                      
WR5830*    MOVE DPG55-1ST-WKDY-GREG-FMT    TO D2-WEEK-DATES.                    
C29908***  IF THE RUN IS EXECUTED DURING CUTOFF WEEK, MOVE THE PREVIOUS         
C29908***  FISCAL WEEK BEGIN DATE (LAST WEEK OF MONTH) TO THE RPT DATES         
C29908     IF PS-CUTOFF-WEEK-DATE                                               
C29908         MOVE PV-PREV-FISCAL-WEEK-BEGIN TO SA-DATE-DB2-N                  
C29908     END-IF.                                                              
C29908                                                                          
W29908     MOVE SA-MM-DB2 TO D2-WEEK-MM.                                        
W29908     MOVE SA-DD-DB2 TO D2-DAY-DD.                                         
W29908     MOVE SA-YY-DB2 TO D2-YEAR-YY.                                        
W29908*                                                                         
WR5830     PERFORM C750-PRINT-REPORT-2.                                         
                                                                                
WR5830* PRINT TOTAL OF ALL CURRENT RECEIPT TRANSACTIONS                         
WR5830*    MOVE DPG55-1ST-WKDY-GREG-FMT       TO T2-CURR-DATE.                  
W29908     MOVE D2-WEEK-DATES                 TO T2-CURR-DATE.                  
WR5830     MOVE PA2-TOTAL-CURR-RECEIPTS       TO T2-VERIFIED-AMTS.              
WR5830     WRITE REPORT-RECORD-2 FROM T2-TOTAL-LINE                             
WR5830         AFTER ADVANCING 3 LINES.                                         
WR5830*                                                                         
WR5830******************************************************************        
WR5830* START PROCESSING OF SECOND REPORT                                       
WR5830* PRINT RECEIPTS AND PURCHASE ADJUSTMENTS                                 
WR5830******************************************************************        
WR5830 C750-PRINT-REPORT-2.                                                     
           PERFORM C760-PROCESS-RECEIPTS-TRANS.                                 
           PERFORM C780-PROCESS-PRCH-ADJ-TRANS.                                 
WR5830*                                                                         
WR5830 C760-PROCESS-RECEIPTS-TRANS.                                             
WR5830     MOVE PC-RECEIPTS-HEADING        TO D2-ENTRY-TYPE.                    
WR5830*    MOVE JULIAN-DAY-HOLD            TO D2-TRANS-TYPE.                    
S29908     MOVE CC-JULIAN-PLUS1            TO D2-TRANS-TYPE.                    
WR5830     WRITE REPORT-RECORD-2 FROM D2-JOURNAL-HEADER                         
WR5830         AFTER ADVANCING 3 LINES.                                         
                                                                                
WR5830     PERFORM C770-PRINT-RECEIPTS.                                         
WR5830*                                                                         
WR5830 C770-PRINT-RECEIPTS.                                                     
           MOVE PC-CURR-WEEK-HDR             TO D2-VERIFIED-WEEK.               
               WRITE REPORT-RECORD-2 FROM D2-JOURNAL-LINE                       
                    AFTER ADVANCING 2 LINES.                                    
                                                                                
           MOVE PA2-RECEIPTS-CURR            TO D3-VERIFIED-AMTS.               
           MOVE PC-RCPTS-AND-ADJ             TO D3-VERIFIED-TYPE.               
               WRITE REPORT-RECORD-2 FROM D3-JOURNAL-LINE                       
                    AFTER ADVANCING 2 LINE.                                     
                                                                                
WR5830*                                                                         
WR5830 C780-PROCESS-PRCH-ADJ-TRANS.                                             
WR5830     MOVE PC-PRCH-ADJ-HEADING        TO D2-ENTRY-TYPE.                    
WR5830*    MOVE JULIAN-DAY-PLUS            TO D2-TRANS-TYPE.                    
S99080     MOVE CC-JULIAN-PLUS-499         TO D2-TRANS-TYPE.                    
WR5830     WRITE REPORT-RECORD-2 FROM D2-JOURNAL-HEADER                         
WR5830         AFTER ADVANCING 2 LINES.                                         
                                                                                
WR5830     PERFORM C790-PRINT-PURCHASE-ADJ.                                     
WR5830*                                                                         
WR5830 C790-PRINT-PURCHASE-ADJ.                                                 
           MOVE PC-CURR-WEEK-HDR       TO D2-VERIFIED-WEEK.                     
               WRITE REPORT-RECORD-2 FROM D2-JOURNAL-LINE                       
                   AFTER ADVANCING 2 LINES.                                     
                                                                                
           MOVE PA2-PRCH-ADJ-RECPT-CURR    TO D3-VERIFIED-AMTS.                 
           MOVE PC-PRCH-ADJ                TO D3-VERIFIED-TYPE.                 
               WRITE REPORT-RECORD-2 FROM D3-JOURNAL-LINE                       
                   AFTER ADVANCING 2 LINE.                                      
                                                                                
                                                                                
00358 *********************************************************         MLKRP004
00359 * D100-WRITE-HEADINGS   FORMATS HEADINGS FOR A NEW PAGE *         MLKRP004
00360 *********************************************************         MLKRP004
00361  D100-WRITE-HEADINGS.                                             MLKRP004
00362      ADD 1 TO PC-PAGE-COUNT.                                      MLKRP004
00363      MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    MLKRP004
00364      MOVE DP132O-STANDRD-HEADER-132-COLS TO REPORT-RECORD.        MLKRP004
00365      WRITE REPORT-RECORD                                          MLKRP004
00366          AFTER ADVANCING PAGE.                                    MLKRP004
00367      MOVE H2-HEADING2 TO REPORT-RECORD.                           MLKRP004
00368      WRITE REPORT-RECORD                                          MLKRP004
00369          AFTER ADVANCING 1.                                       MLKRP004
00370      MOVE H3-HEADING3 TO REPORT-RECORD.                           MLKRP004
00371      WRITE REPORT-RECORD                                          MLKRP004
00372          AFTER ADVANCING 2                                        MLKRP004
00373      MOVE ZERO TO PC-LINE-COUNT.                                  MLKRP004
00374      MOVE 6 TO PC-LINE-COUNT.                                     MLKRP004
00375      MOVE 2 TO PC-ADVANCE.                                        MLKRP004
00376                                                                   MLKRP004
WR5830*********************************************************         MLKRP004
WR5830* D200-WRITE-HEADINGS   FORMATS HEADINGS RECPT/ADJ PAGE *         MLKRP004
WR5830*********************************************************         MLKRP004
WR5830 D200-WRITE-HEADINGS.                                             MLKRP004
WR5830     ADD 1 TO PC-PAGE-COUNT.                                      MLKRP004
WR5830     MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    MLKRP004
WR5830     MOVE '2' TO DP132O-REPORT-NUMBER.                            MLKRP004
WR5830     MOVE DP132O-STANDRD-HEADER-132-COLS TO REPORT-RECORD-2.      MLKRP004
WR5830     WRITE REPORT-RECORD-2                                        MLKRP004
WR5830         AFTER ADVANCING PAGE.                                    MLKRP004
WR5830     MOVE PC-TITLE-RECEIPTS              TO H4-HEADING-TITLE.     MLKRP004
WR5830     MOVE H4-HEADING1                    TO REPORT-RECORD-2.      MLKRP004
WR5830     WRITE REPORT-RECORD-2                                        MLKRP004
WR5830         AFTER ADVANCING 1.                                       MLKRP004
WR5830     MOVE ZERO TO PC-LINE-COUNT.                                  MLKRP004
WR5830     MOVE 6 TO PC-LINE-COUNT.                                     MLKRP004
WR5830     MOVE 2 TO PC-ADVANCE.                                        MLKRP004
W29908                                                                          
W29908****************************************************************          
W29908*  DATE TABLE CURSOR                                                      
W29908****************************************************************          
W29908 E100-SELECT-TDTATTR.                                             MLKRP004
W29908                                                                          
W29908     EXEC SQL                                                             
W29908           SELECT JLN_DTE                                                 
W29908                 ,DAY_NM                                                  
W29908                 ,FSCL_WK_BEG_DTE                                         
W29908                 ,FSCL_WK_END_DTE                                         
W29908                 ,FSCL_MN_END_DTE                                         
C29908                 ,(PFW_END_DTE - 6 DAYS)                                  
W29908             INTO :DTATTR-JLN-DTE                                         
W29908                 ,:DTATTR-DAY-NM                                          
W29908                 ,:DTATTR-FSCL-WK-BEG-DTE                                 
W29908                 ,:DTATTR-FSCL-WK-END-DTE                                 
W29908                 ,:DTATTR-FSCL-MN-END-DTE                                 
C29908                 ,:PV-PREV-FISCAL-WEEK-BEGIN                              
W29908             FROM TDTATTR                                                 
W29908            WHERE KY_DTE = :DTATTR-KY-DTE                                 
W29908             WITH UR                                                      
W29908     END-EXEC.                                                            
W29908                                                                          
W29908     EVALUATE TRUE                                                        
W29908         WHEN SQLCODE = ZERO                                              
W29908             DISPLAY 'DTATTR-KEY-DATE:       ' DTATTR-KY-DTE              
W29908             DISPLAY 'DTATTR-DAY-NAME:       ' DTATTR-DAY-NM              
W29908             DISPLAY 'DTATTR-JULIAN-YYYYDDD: ' DTATTR-JLN-DTE             
W29908             DISPLAY 'DTATTR-FSCL-WK DATES : '                            
W29908                      DTATTR-FSCL-WK-BEG-DTE, ' THRU ',                   
W29908                      DTATTR-FSCL-WK-END-DTE                              
W29908             DISPLAY 'DTATTR-FSCL-MN-END-DT: '                            
W29908                                         DTATTR-FSCL-MN-END-DTE           
W29908         WHEN OTHER                                                       
W29908             DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
W29908             MOVE    'E100-SELECT-TDTATTR'                                
W29908                                          TO PV-PARAGRAPH-NAME            
W29908             MOVE    'SELECT JULIAN DATES'                                
W29908                                          TO PV-DB2-OPER-ATTEMPTED        
W29908             PERFORM 9998-SQL-ABEND                                       
W29908     END-EVALUATE.                                                        
W29908                                                                          
W29908                                                                  MLKRP004
00377 ******************************************************************MLKRP004
00378 * READ THE INPUT FILE. FOR TRANSFERS (CODES 30 AND 31), THE BATCH MLKRP004
00379 * NUMBER IS NOT IN THE SAME POSITION AS ON THE OTHER TRANSACTIONS,MLKRP004
00380 * SO MOVE IT INTO THE SAME POSITION BEFORE FURTHER PROCESSING.    MLKRP004
00381 ******************************************************************MLKRP004
00382  Z100-READ-BATCH-FILE.                                            MLKRP004
00383                                                                   MLKRP004
00384      READ BATCH-FILE INTO D115-ON-ORDER-REC                       MLKRP004
00385          AT END MOVE 'Y' TO SW-EOF-BATCH.                         MLKRP004
00386                                                                   MLKRP004
00387      MOVE D115-ON-ORDER-REC TO HR-HOLD-RECORD                     MLKRP004
00388                                D115-PURCH-JOURNAL-REC             MLKRP004
00389                                D115-TRANSFER-TO-REC               MLKRP004
00390                                D115-TRANSFER-FROM-REC             MLKRP004
00391                                D115-MKDWN-REC                     MLKRP004
00392                                D115-MARKON-REC                    MLKRP004
00393                                D115-ADJ-REC.                      MLKRP004
00394                                                                   MLKRP004
00395      IF NOT SW-EOF-BATCH-FILE                                     MLKRP004
00396          IF HR-TRANSFER                                           MLKRP004
00397              MOVE D115-TRANS-BATCH  TO PV-TRANSFER-BATCH-POS2-4   MLKRP004
00398              MOVE PV-TRANSFER-BATCH TO HR-BATCH                   MLKRP004
00399          END-IF                                                   MLKRP004
00400      END-IF.                                                      MLKRP004
W29908                                                                          
W29908 9998-SQL-ABEND.                                                          
W29908                                                                          
W29908     MOVE +4013 TO PV-ABEND-CODE.                                         
W29908     DISPLAY '** ABEND **         ** = SQLERROR'.                         
W29908     DISPLAY '** PROGRAM          ** = MLKRP004'.                         
W29908     DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
W29908     DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
W29908                                                                          
W29908*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
W29908*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
W29908     COPY DPPD004.                                                        
W29908                                                                          
W29908     PERFORM 9999-ABEND.                                                  
W29908                                                                          
W29908                                                                          
W29908 9999-ABEND.                                                              
W29908                                                                          
W29908     CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
