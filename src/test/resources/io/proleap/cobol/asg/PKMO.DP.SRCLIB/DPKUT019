       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKUT019.                                                 
                                                                                
       AUTHOR.        CAL BUSH.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01/09/97.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      **                                                             **         
      ** THIS PROGRAM FORMATS A MESSAGE INTO NOTES 'BATCH' FORMAT    **         
      ** FOR THE MAINFRAME E-MAIL SYSTEM.                            **         
      ** THIS PROGRAM WAS COPIED FROM DPKUT010.                      **         
      **----------------------------CHANGES--------------------------**         
      ** 11/13/97  BY CAL BUSH                                       **         
      **   CHANGED SUBJECT FROM 'UNIX' TO 'REMOTE SYSTEM'            **         
      ** 01/26/99  BY JEFF ROUBIK                                    **         
      **   CHANGED CHANGED FROM SYSM TO NOTES MESSAGE SYSTEM.        **         
      ** 10/09/00  BY JEFF ROUBIK                                    **         
      **   ALLOWED PARM TO BE PASSED TO MAKE FILE UNIQUE.            **         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT INPUT-EMAIL-FILE          ASSIGN TO INP01.                    
           SELECT OUTPUT-ADDR-FILE          ASSIGN TO OUT01.                    
           SELECT OUTPUT-EMAIL-FILE         ASSIGN TO OUT02.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-EMAIL-FILE                                                     
           RECORDING MODE IS F.                                                 
       01  INPUT-EMAIL-RECORD            PIC  X(80).                            
                                                                                
       FD  OUTPUT-ADDR-FILE                                                     
           RECORDING MODE IS F.                                                 
       01  OUTPUT-ADDR-RECORD            PIC  X(80).                            
                                                                                
       FD  OUTPUT-EMAIL-FILE                                                    
           RECORDING MODE IS F.                                                 
       01  OUTPUT-EMAIL-RECORD           PIC  X(80).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-ADDR-TO                PIC  X(12)  VALUE                      
                                                        'SMTPNOTE TO('.         
           05  PC-ADDR-AT                PIC  X(13)  VALUE                      
                                                       '@KOHLS.COM) -'.         
           05  PC-ADDR-SUBJECT           PIC  X(35)  VALUE                      
                                 'SUBJECT(MESSAGE FROM REMOTE SYSTEM '.         
           05  PC-ADDR-SUBJECT-END       PIC  X(03)  VALUE                      
                                                                 ') -'.         
           05  PC-ADDR-DATASET-1         PIC  X(26)  VALUE                      
                                'DA(''PKMT.SMTPNOTE.DPKUT019'.                  
           05  PC-ADDR-DATASET-2         PIC  X(08)  VALUE                      
                                ''') BATCH'.                                    
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-UNSTRING-COUNT         PIC S9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-UNSTRING-PTR           PIC S9(04)  VALUE ZERO                 
                                                     COMP.                      
           05  PV-FROM-USER              PIC  X(08)  VALUE SPACE.               
           05  PV-MAILLIST-ID            PIC  X(20)  VALUE SPACE.               
           05  PV-MESSAGE-TEXT           PIC  X(80)  VALUE SPACE.               
                                                                                
       01  SD-DATE.                                                             
           05  SD-YEAR                   PIC  9(02)  VALUE ZEROS.               
           05  SD-MONTH                  PIC  9(02)  VALUE ZEROS.               
           05  SD-DAY                    PIC  9(02)  VALUE ZEROS.               
                                                                                
       01  ST-TIME.                                                             
           05  ST-HOUR                   PIC  9(02)  VALUE ZEROS.               
           05  ST-MIN                    PIC  9(02)  VALUE ZEROS.               
           05  ST-SEC                    PIC  9(02)  VALUE ZEROS.               
                                                                                
       01  DTS-DATE-TIME-STAMP.                                                 
           05  DTS-MSG                   PIC  X(24)  VALUE                      
                                            '*** Remote message from '.         
           05  DTS-SENT.                                                        
               10                        PIC  X(13)  VALUE                      
                                                       ' was sent on '.         
               10  DTS-DATE.                                                    
                   15  DTS-MONTH         PIC  9(02).                            
                   15                    PIC  X(01)  VALUE '/'.                 
                   15  DTS-DAY           PIC  9(02).                            
                   15                    PIC  X(01)  VALUE '/'.                 
                   15  DTS-CENT          PIC  9(02).                            
                   15  DTS-YEAR          PIC  9(02).                            
               10                        PIC  X(04)  VALUE ' at '.              
               10  DTS-DATE.                                                    
                   15  DTS-HOUR          PIC  9(02).                            
                   15                    PIC  X(01)  VALUE ':'.                 
                   15  DTS-MIN           PIC  9(02).                            
                   15                    PIC  X(01)  VALUE ':'.                 
                   15  DTS-SEC           PIC  9(02).                            
               10                        PIC  X(05)  VALUE '. ***'.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SW                 PIC  X(01)  VALUE 'N'.                 
               88  PS-EOF                            VALUE 'Y'.                 
                                                                                
      ****************************************************************          
      ** THIS IS THE RECORD LAYOUT FOR THE INPUT RECORD USED BY THIS**          
      ** PROGRAM.                                                   **          
      ****************************************************************          
       01  IER-INPUT-EMAIL-RECORD.                                              
           05  IER-INPUT-MESSAGE-TXT     PIC  X(80)  VALUE SPACE.               
                                                                                
       01  ABEND-CODE                    PIC S9(04)  VALUE ZERO                 
                                                     COMP SYNC.                 
           88  AC-INVALID-PARAMETER                  VALUE +4003.               
           88  AC-INVALID-DATA                       VALUE +4008.               
                                                                                
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  LS-LINKAGE-SECTION-PARAMETERS.                                       
           05  LS-PARAMETER-LENGTH       PIC S9(4)                              
                                         COMP SYNC.                             
           05  LS-JOB-PARAMETER.                                                
               10  LS-JOB-NAME           PIC  X(08).                            
                                                                                
                                                                                
       PROCEDURE DIVISION USING LS-LINKAGE-SECTION-PARAMETERS.                  
      *****************************************************************         
      **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.  **         
      *****************************************************************         
       0000-MAIN-DRIVER.                                                        
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-PROCESS-INPUT                                           
               UNTIL PS-EOF.                                                    
           PERFORM 3000-TERMINATION.                                            
                                                                                
           GOBACK.                                                              
      ****************************************************************          
      ** OPEN FILES AND DO THE PRIMING READ.                        **          
      ****************************************************************          
       1000-INITIALIZATION.                                                     
           OPEN INPUT  INPUT-EMAIL-FILE                                         
                OUTPUT OUTPUT-ADDR-FILE                                         
                       OUTPUT-EMAIL-FILE.                                       
           MOVE SPACES TO OUTPUT-ADDR-RECORD.                                   
           MOVE SPACES TO OUTPUT-EMAIL-RECORD.                                  
                                                                                
           PERFORM 4100-READ-INPUT-MESSAGE.                                     
                                                                                
           IF  PS-EOF                                                           
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PROGRAM:  DPKUT019'                                  
               DISPLAY '** PARAGRAPH: 1000-INITIALIZATION'                      
               DISPLAY '** INPUT DATASET IS EMPTY'                              
               SET AC-INVALID-DATA TO TRUE                                      
               PERFORM 9000-ABEND                                               
           ELSE                                                                 
               PERFORM 1100-PARSE-MESSAGE-TEXT                                  
               PERFORM 1200-CREATE-MAIL-HEADERS                                 
               PERFORM 4100-READ-INPUT-MESSAGE                                  
           END-IF.                                                              
      ****************************************************************          
      ** READ THE MAILLIST ID FROM THE INPUT FILE.  ABEND IF THE    **          
      ** INPUT FILE IS EMPTY.                                       **          
      ****************************************************************          
       1100-PARSE-MESSAGE-TEXT.                                                 
           MOVE 1 TO PV-UNSTRING-PTR.                                           
           UNSTRING IER-INPUT-MESSAGE-TXT                                       
               DELIMITED BY SPACE                                               
               INTO PV-FROM-USER                                                
                    PV-MAILLIST-ID                                              
               COUNT IN PV-UNSTRING-COUNT                                       
               POINTER  PV-UNSTRING-PTR                                         
           END-UNSTRING.                                                        
                                                                                
           INSPECT PV-FROM-USER   REPLACING ALL ' ' BY '~'.                     
           INSPECT PV-MAILLIST-ID REPLACING ALL ' ' BY '~'.                     
                                                                                
           MOVE IER-INPUT-MESSAGE-TXT(PV-UNSTRING-PTR:)                         
                                      TO PV-MESSAGE-TEXT.                       
      ****************************************************************          
      ** CREATE THE NON-REPEATING LINES REQUIRED BY ALL MAIL        **          
      ** MESSAGES.                                                  **          
      ****************************************************************          
       1200-CREATE-MAIL-HEADERS.                                                
                                                                                
           STRING PC-ADDR-TO PV-MAILLIST-ID PC-ADDR-AT                          
                 DELIMITED BY '~'  INTO OUTPUT-ADDR-RECORD.                     
           PERFORM 4300-WRITE-OUTPUT-ADDR.                                      
                                                                                
           STRING PC-ADDR-SUBJECT PV-FROM-USER PC-ADDR-SUBJECT-END              
                 DELIMITED BY '~'  INTO OUTPUT-ADDR-RECORD.                     
           PERFORM 4300-WRITE-OUTPUT-ADDR.                                      
                                                                                
           IF LS-PARAMETER-LENGTH > 0                                           
               STRING PC-ADDR-DATASET-1                                         
                      '.'                                                       
                      LS-JOB-NAME(1:LS-PARAMETER-LENGTH)                        
                      PC-ADDR-DATASET-2                                         
                DELIMITED BY SIZE  INTO OUTPUT-ADDR-RECORD                      
           ELSE                                                                 
               STRING PC-ADDR-DATASET-1                                         
                      PC-ADDR-DATASET-2                                         
                DELIMITED BY SIZE  INTO OUTPUT-ADDR-RECORD.                     
           PERFORM 4300-WRITE-OUTPUT-ADDR.                                      
                                                                                
                                                                                
           ACCEPT SD-DATE FROM DATE.                                            
             MOVE SD-MONTH  TO DTS-MONTH.                                       
             MOVE SD-DAY    TO DTS-DAY.                                         
             MOVE SD-YEAR   TO DTS-YEAR.                                        
             IF SD-YEAR > 90                                                    
               MOVE 19      TO DTS-CENT                                         
             ELSE                                                               
               MOVE 20      TO DTS-CENT.                                        
                                                                                
           ACCEPT ST-TIME FROM TIME.                                            
             MOVE ST-HOUR   TO DTS-HOUR.                                        
             MOVE ST-MIN    TO DTS-MIN.                                         
             MOVE ST-SEC    TO DTS-SEC.                                         
                                                                                
           STRING DTS-MSG PV-FROM-USER DTS-SENT                                 
                 DELIMITED BY '~'  INTO OUTPUT-EMAIL-RECORD.                    
           PERFORM 4400-WRITE-OUTPUT-EMAIL.                                     
                                                                                
           MOVE SPACES               TO OUTPUT-EMAIL-RECORD.                    
           PERFORM 4400-WRITE-OUTPUT-EMAIL.                                     
                                                                                
           MOVE PV-MESSAGE-TEXT      TO OUTPUT-EMAIL-RECORD.                    
           PERFORM 4400-WRITE-OUTPUT-EMAIL.                                     
                                                                                
      ****************************************************************          
      **  FOR EACH LINE IN THE INPUT EMAIL TEXT FILE, GENERATE      **          
      **  AN OUTPUT TEXT RECORD.                                    **          
      ****************************************************************          
       2000-PROCESS-INPUT.                                                      
           MOVE IER-INPUT-MESSAGE-TXT TO OUTPUT-EMAIL-RECORD.                   
           PERFORM 4400-WRITE-OUTPUT-EMAIL.                                     
                                                                                
           PERFORM 4100-READ-INPUT-MESSAGE.                                     
                                                                                
      ****************************************************************          
      ** CLOSE FILES                                                **          
      ****************************************************************          
       3000-TERMINATION.                                                        
                                                                                
           CLOSE INPUT-EMAIL-FILE                                               
                 OUTPUT-ADDR-FILE                                               
                 OUTPUT-EMAIL-FILE.                                             
      ****************************************************************          
      ** READ A RECORD FROM THE INPUT FILE.                         **          
      ****************************************************************          
       4100-READ-INPUT-MESSAGE.                                                 
           READ INPUT-EMAIL-FILE INTO IER-INPUT-EMAIL-RECORD                    
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
      ****************************************************************          
      ** WRITE THE RECORD TO THE OUTPUT FILE.                       **          
      ****************************************************************          
       4300-WRITE-OUTPUT-ADDR.                                                  
           WRITE OUTPUT-ADDR-RECORD.                                            
           MOVE SPACES TO OUTPUT-ADDR-RECORD.                                   
      ****************************************************************          
      ** WRITE THE RECORD TO THE OUTPUT FILE.                       **          
      ****************************************************************          
       4400-WRITE-OUTPUT-EMAIL.                                                 
           WRITE OUTPUT-EMAIL-RECORD.                                           
      ****************************************************************          
      ** ABEND THE PROGRAM.                                         **          
      ****************************************************************          
       9000-ABEND.                                                              
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
