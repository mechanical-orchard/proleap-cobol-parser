000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'CREATE EXTRACT FOR IN-TRANSIT PURGE'.                             
000300                                                                          
000400 PROGRAM-ID.             PCKUT032.                                        
000500 AUTHOR.                 DENNIS STEFFEN.                                  
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            AUTUMN 2004.                                     
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200 SOURCE-COMPUTER.        IBM-3090.                                        
001300 OBJECT-COMPUTER.        IBM-3090.                                        
001400 INPUT-OUTPUT SECTION.                                                    
001500 FILE-CONTROL.                                                            
001600                                                                          
001700     SELECT CUTOFF-DATE-FILE                                              
001800         ASSIGN TO INP01.                                                 
001900                                                                          
002000     SELECT PURGE-UNLOAD-FILE                                             
002100         ASSIGN TO INP02.                                                 
002200                                                                          
002300     SELECT INTRANSIT-EXTRACT-FILE                                        
002400         ASSIGN TO OUT01.                                                 
002500                                                                          
002600******************************************************************        
002700 DATA DIVISION.                                                           
002800******************************************************************        
002900                                                                          
003000 FILE SECTION.                                                            
003100                                                                          
003200 FD  INTRANSIT-EXTRACT-FILE                                               
003300     RECORDING MODE IS F                                                  
003400     LABEL RECORDS ARE STANDARD                                           
003500     BLOCK CONTAINS 0 RECORDS.                                            
003600                                                                          
003700 01  PURGE-EXTRACT-REC                PIC X(22).                          
003800                                                                          
003900 FD  CUTOFF-DATE-FILE                                                     
004000     RECORDING MODE IS F                                                  
004100     LABEL RECORDS ARE STANDARD                                           
004200     BLOCK CONTAINS 0 RECORDS.                                            
004300 01  CUTOFF-DATE-REC                  PIC X(11).                          
004400                                                                          
004500 FD  PURGE-UNLOAD-FILE                                                    
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900                                                                          
005000 01  PURGE-UNLOAD-REC                 PIC X(22).                          
005100                                                                          
005200 WORKING-STORAGE SECTION.                                                 
005300                                                                          
005400 01  PROGRAM-SWITCHES.                                                    
005500     05  PS-END-UNLOAD-SW             PIC X(01)  VALUE 'N'.               
005600         88  PS-END-UNLOAD-YES                   VALUE 'Y'.               
005700                                                                          
005800 01  PV-CUTOFF-DATE-REC.                                                  
005900     05  PV-CUTOFF-DATE               PIC X(11).                          
006000                                                                          
006100 01  DK-KEYS.                                                             
006200     05  DK-CUTOFF-DTE                PIC X(10) VALUE SPACES.             
006300                                                                          
006400 01  PV-CRAPOLA.                                                          
006500     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
006600                                                   COMP SYNC.             
006700         88 ABEND-4013-DB2-ERROR                   VALUE +4013.           
006800     05  PV-DB2-OPERATION-ATTEMPTED                                       
006900                                      PIC X(60) VALUE SPACE.              
007000     05  PV-DB2-OPERATION-ATTEMPT-2                                       
007100                                      PIC X(60) VALUE SPACE.              
007200     05  PV-SQL-CODE                  PIC 9(04) VALUE ZERO.               
007300     05  PV-ABEND-PARAGRAPH           PIC X(30) VALUE SPACES.             
007400     05  PV-UNLOAD-COUNTER            PIC S9(9) COMP-3 VALUE ZERO.        
007500     05  PV-BYPASS-COUNTER            PIC S9(9) COMP-3 VALUE ZERO.        
007600     05  PV-WRITE-COUNTER             PIC S9(9) COMP-3 VALUE ZERO.        
007700*    05  PV-DETAIL-COUNT              PIC S9(9) COMP   VALUE ZERO.        
007800*    05  PV-TOT-DETAIL-COUNT          PIC S9(9) COMP   VALUE ZERO.        
007900     05  PV-DISPLAY-NUMBER            PIC Z(8)9.                          
007900     05  PV-TIMESTAMP                 PIC X(26).                          
008000                                                                          
008100 01  PC-CONSTANTS.                                                        
008200     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
008300                                                   'ILBOABN0'.            
008400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
008500                                                   'PCKUT032'.            
008600     COPY PCRD024.                                                        
008700/                                                                         
008800* INTRANSIT DETAILS                                                       
008900*    EXEC SQL                                                             
009000*        INCLUDE PCT00015                                                 
009100*    END-EXEC.                                                            
009200/                                                                         
009300*    EXEC SQL                                                             
009400*        INCLUDE TKRPRPM                                                  
009500*    END-EXEC.                                                            
009600/                                                                         
009700     EXEC SQL                                                             
009800         INCLUDE SQLCA                                                    
009900     END-EXEC.                                                            
010000     COPY SQLCA2.                                                         
010100/                                                                         
010200 PROCEDURE DIVISION.                                                      
010300                                                                          
010400     PERFORM 1000-INITIALIZATION.                                         
010500                                                                          
010600     PERFORM 2000-PROCESS-UNLOAD-FILE                                     
010700       UNTIL PS-END-UNLOAD-YES.                                           
010800                                                                          
010900     PERFORM 9000-EOJ.                                                    
011000                                                                          
011100     GOBACK.                                                              
011200                                                                          
011300 EJECT                                                                    
011400 1000-INITIALIZATION.                                                     
011500                                                                          
           EXEC SQL SET :PV-TIMESTAMP = CURRENT TIMESTAMP END-EXEC.             
                                                                                
011600     OPEN INPUT CUTOFF-DATE-FILE                                          
011700     READ CUTOFF-DATE-FILE                                                
011800         INTO                                                             
011900         PV-CUTOFF-DATE-REC                                               
012000     AT END                                                               
012100         DISPLAY ALL '*'                                                  
012200         DISPLAY ' CUTOFF DATE IS MISSING - GOTTA HAVE ONE'               
012300         MOVE +4003                  TO PV-ABEND-CODE                     
012400         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
012500     NOT AT END                                                           
012600         DISPLAY ' CUTOFF DATE IS ' PV-CUTOFF-DATE                        
012700     END-READ.                                                            
012800                                                                          
012900     CLOSE CUTOFF-DATE-FILE.                                              
013000                                                                          
013100     OPEN INPUT PURGE-UNLOAD-FILE                                         
013200     OPEN OUTPUT INTRANSIT-EXTRACT-FILE                                   
013300                                                                          
013400     PERFORM 8000-READ-UNLOAD.                                            
013500                                                                          
013600/                                                                         
013700 2000-PROCESS-UNLOAD-FILE.                                                
013800                                                                          
013900     IF PC024-LAST-UPD-DTE < PV-CUTOFF-DATE                               
014000             AND PC024-TRN-CLSN-QTY = ZERO                                
014100         PERFORM 8100-WRITE-EXTRACT                                       
014200*        PERFORM 7200-COUNT-DETAILS                                       
014300     ELSE                                                                 
014400*        DISPLAY PC024-PURGE-EXTRACT                                      
014500         ADD 1                        TO PV-BYPASS-COUNTER                
014600     END-IF.                                                              
014700                                                                          
014800     PERFORM 8000-READ-UNLOAD.                                            
014900/                                                                         
015000*7200-COUNT-DETAILS.                                                      
015100*    MOVE '7200-COUNT-DETAILS' TO PV-ABEND-PARAGRAPH.                     
015200*    MOVE ZERO                        TO PV-DETAIL-COUNT                  
015300*                                                                         
015400*    EXEC SQL                                                             
015500*        SELECT COUNT (*)                                                 
015600*        INTO                                                             
015700*            :PV-DETAIL-COUNT                                             
015800*        FROM                                                             
015900*            PCT_TRN_CLSN_DTL                                             
016000*        WHERE                                                            
016100*            INTR_UPC_ID              = :PC024-INTR-UPC-ID                
016200*        AND                                                              
016300*            LOC_NBR                  = :PC024-LOC-NBR                    
016400*        AND                                                              
016500*            TRN_CLSN_CDE             = :PC024-TRN-CLSN-CDE               
016600*    END-EXEC.                                                            
016700*                                                                         
016800*    EVALUATE TRUE                                                        
016900*        WHEN SQLCODE = ZERO                                              
017000*            ADD PV-DETAIL-COUNT      TO PV-TOT-DETAIL-COUNT              
017100*        WHEN SQLCODE = +100                                              
017200*            CONTINUE                                                     
017300*        WHEN OTHER                                                       
017400*            DISPLAY '**************************************'             
017500*            DISPLAY 'ERROR ISSUED ON COUNT OF CLSN_DTL      '            
017600*            DISPLAY '**************************************'             
017700*            PERFORM 9900-SQL-ERROR                                       
017800*    END-EVALUATE.                                                        
017900/                                                                         
018000 8000-READ-UNLOAD.                                                        
018100                                                                          
018200     READ PURGE-UNLOAD-FILE                                               
018300         INTO                                                             
018400             PC024-PURGE-EXTRACT                                          
018500         AT END                                                           
018600             SET PS-END-UNLOAD-YES    TO TRUE                             
018700         NOT AT END                                                       
018800             ADD 1                    TO PV-UNLOAD-COUNTER                
018900     END-READ.                                                            
019000                                                                          
019100 8100-WRITE-EXTRACT.                                                      
019200                                                                          
019300     WRITE PURGE-EXTRACT-REC                                              
019400         FROM PC024-PURGE-EXTRACT                                         
019500                                                                          
019600     ADD +1 TO PV-WRITE-COUNTER.                                          
019700/                                                                         
019800 9000-EOJ.                                                                
019900                                                                          
020000     CLOSE INTRANSIT-EXTRACT-FILE                                         
020100           PURGE-UNLOAD-FILE                                              
020200                                                                          
020300     MOVE PV-UNLOAD-COUNTER           TO PV-DISPLAY-NUMBER                
020400     DISPLAY SPACES.                                                      
020500     DISPLAY 'PURGE RECS READ  = ' PV-DISPLAY-NUMBER                      
020600                                                                          
020700     DISPLAY SPACES.                                                      
020800     MOVE PV-WRITE-COUNTER            TO PV-DISPLAY-NUMBER                
020900     DISPLAY 'EXTRACTS WRITTEN = ' PV-DISPLAY-NUMBER.                     
021000                                                                          
021100     DISPLAY SPACES.                                                      
021200     MOVE PV-BYPASS-COUNTER           TO PV-DISPLAY-NUMBER                
021300     DISPLAY 'UNLOADS BYPASSED = ' PV-DISPLAY-NUMBER.                     
021400                                                                          
021500*    MOVE PV-TOT-DETAIL-COUNT         TO PV-DISPLAY-NUMBER                
021600*    DISPLAY 'RELATED DETAILS  = ' PV-DISPLAY-NUMBER.                     
021700                                                                          
021800                                                                          
021900*    IF NO RECORDS WERE WRITTEN THEN ISSUE A RETURN CODE                  
022000*    TO CAUSE THE PROCESSING TO STOP AFTER THIS PROGRAM.                  
022100                                                                          
022200     IF PV-WRITE-COUNTER = ZERO                                           
022300        MOVE +1                       TO RETURN-CODE                      
022400        DISPLAY ' PCKUT032 - TRANSACTION FILE IS EMPTY '                  
022500        DISPLAY ' PCKUT032 - PCKUP010 WILL BE SKIPPED  '                  
022600     END-IF.                                                              
022700                                                                          
022800 9800-SQL-WARNING.                                                        
022900                                                                          
023000      MOVE SQLCODE               TO SQLCODE2.                             
023100      MOVE SQLERRD(3)            TO SQLERRD3.                             
023200      DISPLAY '** ABEND **       ** = SQLWARNING'.                        
023300      DISPLAY '** PROGRAM        ** = PCKUT032'.                          
023400      DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.               
023500      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
023600      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                         
023700      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                         
023800      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                         
023900      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                         
024000      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                         
024100      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                         
024200      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                         
024300      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                         
024400      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                         
024500                                                                          
024600      SET ABEND-4013-DB2-ERROR   TO TRUE                                  
024700      CALL 'ILBOABN0' USING PV-ABEND-CODE.                                
024800                                                                          
024900/                                                                         
025000 9900-SQL-ERROR.                                                          
025100                                                                          
025200      MOVE SQLCODE               TO SQLCODE2.                             
025300      MOVE SQLERRD(3)            TO SQLERRD3.                             
025400      DISPLAY '** ABEND **       ** = SQLERROR'.                          
025500      DISPLAY '** PROGRAM        ** = PCKUT032'.                          
025600      DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.               
025700      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
025800      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                         
025900      DISPLAY '** SQLERRM        ** = ' SQLERRM.                          
026000      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                         
026100                                                                          
026200      SET ABEND-4013-DB2-ERROR   TO TRUE                                  
026300      CALL 'ILBOABN0' USING PV-ABEND-CODE.                                
026400/                                                                         
