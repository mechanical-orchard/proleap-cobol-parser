       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUT063.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/28/2005.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************00080000
      * IMKUT063 - CREATE NON-CONTINUED CLEARANCE PERM PRICE CHANGES    00090002
      *            REPORT RECORDS                                       00090002
      ******************************************************************00100000
      * THIS PROGRAM WILL CREATE NON-CONTINUED CLEARANCE PERM PRICE     00110001
      * CHANGE REPORT RECORDS FROM THE XMT NON-CONTINUED CLEARANCE PERM 00111001
      * PRICE CHANGE TABLES EXTRACT FILE RECORDS.                       00120001
      *                                                                 00090002
      * FOR EACH EXTRACT FILE RECORD WITH A CORPORATE PRICING LEVEL     00120001
      * STORE GROUP, A STORE ZERO REPORT RECORD IS WRITTEN.  FOR EACH   00090002
      * EXTRACT FILE WITH NON-CORPORATE PRICING LEVEL STORE GROUPS,     00090002
      * A REPORT RECORD IS WRITTEN FOR EACH STORE THAT IS PART OF THAT  00090002
      * PRICING STORE GROUP.                                            00090002
      *                                                                 00090002
      * IF THE ALL REMAINING STORE INDICATOR IS SET TO 'Y', A CHECK IS  00090002
      * MADE TO DETERMINE IF THE STORE GROUP IS PART OF THE LAST TIER   00090002
      * OF PRICE CHANGES FOR THE PRICE CHANGE.  IF IT IS NOT A LAST TIER00090002
      * STORE GROUP THE ALL REMAINING STORE INDICATOR IS SET TO A 'N'   00090002
      * FOR EACH SKU CONTAINING THAT PRICE CHANGE STORE GROUP.          00090002
      *                                                                 00090002
      * WR/PITS#     DATE     DESCRIPTION                                       
      * --------  ----------  ------------------------------------              
      * WR#28566  03/28/2005  P. KEBER - INITIAL IMPLEMENTATION.                
      * WR#28566  04/06/2005  P. KEBER - REMOVE 4095 RETURN CODE LOGIC.         
      * WR#29434  09/28/2005  D. THEEL - ADDED RETRY LOGIC, ADDED               
      *                       SAS STORE CURSOR AND SAVE INTO WORKING            
      *                       STORAGE TABLE LOGIC.  ADDED LOGIC IN              
      *                       7000-WRITE TO SET THE IM044-PCRR-ALL-             
      *                       RMNG-STRS = N IF THIS IS AN RCP PRICE             
      *                       CHANGE, THE ALL REMAINING STORES INDICATOR        
      *                       IS SET, NO FUTURE TIER EXISTS, AND THE            
      *                       STORE IS A SAS STORE. ADD NEW PRICE               
      *                       CHANGE TYPE CODES OF MK2 AND FNL TO               
      *                       PV-PRICE-CHGS-EXTR-RECORD.                        
      ******************************************************************00140000
      * WR#29434  01/11/2006  P. KEBER - CHANGES FOR SAS B MARKDOWNS.           
      *                       ADDED NEW "SAS B" STORE GROUP NAME CHECK          
      *                       TO THE SAS_STR_CSR CURSOR.                        
      *****************************************************************         
      * WR#29434  12/26/2006  P. KEBER - REMOVE ALL LOGIC TO FIND SAS           
      *                       STORES.                                           
      ******************************************************************        
                                                                                
      ******************************************************************00290094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
      *                                                                 00300094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
      *                                                                 00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
      *                                                                 00370094
           SELECT PRICE-CHGS-EXTR-FILE                                  00400094
               ASSIGN TO INP01.                                         00410094
      *                                                                 00420094
           SELECT PRICE-CHGS-RFMT-FILE                                  00430094
               ASSIGN TO OUT01.                                         00440094
      *                                                                 00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
      *                                                                 00510094
       FILE SECTION.                                                    00520094
      *                                                                 00530094
       FD  PRICE-CHGS-EXTR-FILE                                         00590094
           RECORDING MODE IS F                                          00600094
           BLOCK CONTAINS 0 RECORDS.                                    00610094
       01  PRICE-CHGS-EXTR-RECORD  PIC X(35).                           00620094
      *                                                                 00630094
       FD  PRICE-CHGS-RFMT-FILE                                         00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  PRICE-CHGS-RFMT-RECORD  PIC X(30).                           00670094
      *                                                                 00680094
      *                                                                 00730094
       WORKING-STORAGE SECTION.                                         00740094
      *                                                                 01110094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-INPUT-READ-COUNT         PIC 9(09)    VALUE ZERO.     01190094
           05  PA-WRITE-COUNT              PIC 9(09)    VALUE ZERO.     01190094
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0         01390094
                                                        COMP SYNC.      01400094
      *                                                                 01410094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC X(08)    VALUE           01430094
               'IMKUT063'.                                              01440094
           05  PC-CORP-PRICING-LEVEL       PIC S9(04)   VALUE +1000     01550094
                                                        COMP.           01560094
           05  PC-STORE-PRICING-LEVEL      PIC S9(04)   VALUE +5000     01550094
                                                        COMP.           01560094
           05  PC-MAXIMUM-RETRY-COUNT      PIC S9(04)   VALUE +2                
                                                        COMP SYNC.              
      *                                                                 01570094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-CONTROL-CARDS-SW  PIC X(01)    VALUE 'N'.      01590094
               88  END-OF-CONTROL-CARDS                 VALUE 'Y'.      01600094
           05  PS-END-OF-INPUT-SW          PIC X(01)    VALUE 'N'.      01590094
               88  PS-END-OF-INPUT                      VALUE 'Y'.      01600094
      *                                                                 01760094
       01  PV-PROGRAM-VARIABLES.                                        01770094
           05  PV-PREV-PRCHG-ID            PIC S9(9)    VALUE +0 COMP.          
           05  PV-PREV-PRCHG-EFF-DTE       PIC X(10)      VALUE SPACES.         
           05  PV-PARA-NAME                PIC X(35)      VALUE SPACES. 02030094
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(35)      VALUE SPACES. 02030094
           05  PV-LAST-TIER-EXISTS-IND     PIC X(01)      VALUE SPACES. 02030094
           05  PV-SQLCODE-DISPLAY          PIC +(8)9      VALUE ZEROES. 00830000
           05  PV-RETRY-COUNT              PIC S9(04)     VALUE +0              
                                                           COMP.                
                                                                        01850094
                                                                        01930094
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00900094
                                                         COMP SYNC.     00910094
           88  AC-ABEND-4013                             VALUE +4013.           
      *        SQL ERROR                                                        
                                                                        02010094
      ****************************************************************  00830094
      * RECORD LAYOUT FOR PERM PRICE CHANGES EXTRACT FILE.              00840094
      ****************************************************************  00850094
       01  PV-PRICE-CHGS-EXTR-RECORD.                                           
           05  PV-PCER-PRCHG-EFF-DTE         PIC X(10)  VALUE SPACES.           
           05  PV-PCER-PRCHG-ID              PIC S9(09) VALUE +0 COMP.          
           05  PV-PCER-STR-GP-TYP-CDE        PIC S9(04) VALUE +0 COMP.          
           05  PV-PCER-STR-GP-ID             PIC S9(04) VALUE +0 COMP.          
           05  PV-PCER-PRCHG-TYP-CDE         PIC X(03)  VALUE SPACES.           
               88  PV-PCER-CLR-TO-ACTIVE                VALUE 'CTA'.            
               88  PV-PCER-MARKUP-ACTIVE                VALUE 'MUA'.            
               88  PV-PCER-MARKDOWN-ACTIVE              VALUE 'MDA'.            
               88  PV-PCER-CLEARNCE                     VALUE 'CLR'.            
               88  PV-PCER-REG-CLEARNCE                 VALUE 'RCP'.            
               88  PV-PCER-2ND-CLR-MRKDWN               VALUE 'MK2'.            
               88  PV-PCER-FINL-CLR-RMKDWN              VALUE 'FNL'.            
           05  PV-PCER-ALL-RMNG-STR-IND      PIC X(01)  VALUE SPACE.            
               88  PV-PCER-ALL-RMNG-STRS                VALUE 'Y'.              
           05  PV-PCER-ALL-RMNG-STR-IND-NI   PIC  X(01) VALUE SPACE.            
           05  PV-PCER-SKU                   PIC S9(08) VALUE +0                
                                                        COMP-3.                 
           05  PV-PCER-SKU-NI                PIC  X(01) VALUE SPACE.            
           05  PV-PCER-NEW-UNT-RTL-AMT       PIC S9(07)V99 VALUE +0             
                                                        COMP-3.                 
           05  PV-PCER-NEW-UNT-RTL-AMT-NI    PIC  X(01) VALUE SPACE.            
                                                                        00890094
      ******************************************************************00790094
      *  PERM PRICE CHANGES REFORMAT OUTPUT FILE RECORD LAYOUT          00800094
      ******************************************************************00810094
           COPY IMRD044.                                                00820094
                                                                                
           COPY SQLCA2.                                                 02110094
                                                                        02670099
      ***************************************************************** 02670199
      *  XMT_LOC DCLGEN                                                 02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00001                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02670199
      *  XMT_DC_LOC_ASGMT DCLGEN                                        02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00002                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02670199
      *  XMT_STR_GP_MBSHP DCLGEN                                        02672099
      ***************************************************************** 02673099
           EXEC SQL                                                     02674099
               INCLUDE XMT00003                                         02675099
           END-EXEC.                                                    02676099
                                                                        02677099
      ***************************************************************** 02677199
      *  XMT_STR_GP_PRCHG DCLGEN                                        02677299
      ***************************************************************** 02677399
           EXEC SQL                                                     02677499
               INCLUDE XMT00010                                         02677599
           END-EXEC.                                                    02677699
                                                                        02677799
      *                                                                 02680094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                                
      ***************************************************************** 02670199
      *  IMWS005 - STORE SELECTION WORKING STORAGE                      02672099
      ***************************************************************** 02673099
           COPY IMWS005.                                                02100094
                                                                                
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
      *                                                                 02790094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      *                                                                 02830094
       0000-IMKUT063.                                                   02850094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                        *02870094
      ******************************************************************02880094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
      *                                                                 02910094
           PERFORM 2000-PROCESS-PRICE-CHANGES                           02920094
               UNTIL PS-END-OF-INPUT.                                           
      *                                                                 02940094
           PERFORM 1500-END-OF-JOB.                                     02950094
                                                                                
           GOBACK.                                                      02960094
      *                                                                 02970094
                                                                        02980094
       1000-HOUSEKEEPING.                                               02990094
           MOVE '*1000-HOUSEKEEPING.              *' TO PV-PARA-NAME.   03010094
                                                                        03020094
           OPEN INPUT  PRICE-CHGS-EXTR-FILE                             03040094
                OUTPUT PRICE-CHGS-RFMT-FILE.                            03050094
                                                                        03070094
           PERFORM 6000-READ-PRICE-CHGS-EXTR-FILE.                      03240094
      *                                                                 03250094
                                                                        03680094
       1500-END-OF-JOB.                                                 03690094
                                                                        03700094
      ******************************************************************03710094
      *  CLOSE FILES AND DISPLAY COUNTS                                *03720094
      ******************************************************************03730094
                                                                        03740094
           CLOSE PRICE-CHGS-EXTR-FILE                                   03750094
                 PRICE-CHGS-RFMT-FILE.                                  03780094
                                                                        03790094
           DISPLAY '*******************************'.                           
           DISPLAY 'IMKUT063 - END OF PROGRAM STATS'.                           
           DISPLAY 'INPUT RECORDS READ: ' PA-INPUT-READ-COUNT.                  
           DISPLAY 'RECORDS WRITTEN   : ' PA-WRITE-COUNT.                       
           DISPLAY '*******************************'.                           
                                                                        03900094
      ******************************************************************07160094
      * PROCESS PRICE CHANGE CURSOR                                    *07170094
      ******************************************************************07180094
       2000-PROCESS-PRICE-CHANGES.                                      03910094
                                                                                
      *--  ONLY HAVE TO PERFORM CHECK ONCE PER PRICE CHANGE ID                  
           IF PV-PCER-PRCHG-ID = PV-PREV-PRCHG-ID                               
               PERFORM 2010-OUTPUT-CONTROL                                      
           ELSE                                                                 
               IF PV-PCER-REG-CLEARNCE                                          
                 AND PV-PCER-ALL-RMNG-STRS                                      
                   PERFORM 8600-CHECK-TIER                                      
               END-IF                                                           
               PERFORM 2010-OUTPUT-CONTROL                                      
               MOVE PV-PCER-PRCHG-ID TO PV-PREV-PRCHG-ID                        
           END-IF.                                                              
                                                                                
           PERFORM 6000-READ-PRICE-CHGS-EXTR-FILE.                      03240094
                                                                                
      ******************************************************************07160094
      * CONTROL WRITES OF OUTPUT FILE                                  *07170094
      ******************************************************************07180094
       2010-OUTPUT-CONTROL.                                                     
           MOVE '*2010-OUTPUT-CONTROL.       '    TO PV-PARA-NAME.      03920094
                                                                                
      *--  IF CORPORATE LEVEL, ONLY WRITE 1 STORE 0 RECORD                      
      *--  OTHERWISE WRITE ONE RECORD PER STORE                                 
           IF PV-PCER-STR-GP-TYP-CDE = PC-CORP-PRICING-LEVEL                    
               MOVE 0                   TO IM044-PCRR-STR-NBR                   
               PERFORM 7000-WRITE-PRICE-CHGS-RFMT                               
           ELSE                                                                 
               IF PV-PCER-PRCHG-EFF-DTE NOT = PV-PREV-PRCHG-EFF-DTE             
      **           -- SET UP INITIAL CALL TO IMPD005 FOR THE EFF DATE           
                   MOVE PC-PROGRAM-NAME TO IM005-PC-PROGRAM-NAME                
                   MOVE PV-PCER-PRCHG-EFF-DTE TO IM005-PV-EFF-DTE               
                   MOVE LOW-VALUES TO IM005-PROCESS-STORE-TABLE                 
                   PERFORM IM005-A000-GET-PROCESS-STORES                        
                   IF IM005-PV-ABEND-CODE = 0                                   
                       MOVE PV-PCER-PRCHG-EFF-DTE                               
                                              TO PV-PREV-PRCHG-EFF-DTE          
                   ELSE                                                         
                       DISPLAY '***********************************'            
                       DISPLAY 'ERROR RETURNED FROM IMPD005 '                   
                       MOVE IM005-PV-ABEND-PARAGRAPH TO PV-PARA-NAME            
                       MOVE IM005-PV-ABEND-CODE TO ABEND-CODE                   
                       IF AC-ABEND-4013                                         
                           MOVE IM005-PV-ABEND-MESSAGE                          
                               TO PV-DB2-OPERATION-ATTEMPTED                    
                           MOVE IM005-PV-ABEND-SQLCODE TO SQLCODE               
                           PERFORM 9980-SQL-ERROR                               
                       ELSE                                                     
                           DISPLAY IM005-PV-ABEND-MESSAGE                       
                           PERFORM 9999-ABEND                                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
      *--      IF STORE PRICING LEVEL, ASSUME THE STORE ID IS THE STORE         
               IF PV-PCER-STR-GP-TYP-CDE = PC-STORE-PRICING-LEVEL               
                   IF PV-PCER-STR-GP-ID <= IM005-PC-MAX-STORES                  
                       SET IM005-STORE-INDEX TO PV-PCER-STR-GP-ID               
                       IF IM005-PROCESS-STORE-IND (IM005-STORE-INDEX)           
                             = 'Y'                                              
                           MOVE PV-PCER-STR-GP-ID TO IM044-PCRR-STR-NBR         
                           PERFORM 7000-WRITE-PRICE-CHGS-RFMT                   
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   IF NOT(PV-PCER-STR-GP-TYP-CDE =                              
                              IM005-PV-STR-GP-TYP-CDE                           
                          AND PV-PCER-STR-GP-ID = IM005-PV-STR-GP-ID            
                          AND PV-PCER-PRCHG-EFF-DTE = IM005-PV-EFF-DTE)         
                       MOVE PV-PCER-STR-GP-TYP-CDE TO                           
                            IM005-PV-STR-GP-TYP-CDE                             
                       MOVE PV-PCER-STR-GP-ID      TO                           
                            IM005-PV-STR-GP-ID                                  
                       MOVE PV-PCER-PRCHG-EFF-DTE  TO                           
                            IM005-PV-EFF-DTE                                    
                       INITIALIZE IM005-GROUP-STORE-TABLE                       
                       SET IM005-GP-STR-IDX TO 1                                
                       MOVE 0               TO IM005-PV-ABEND-CODE              
                       MOVE SPACES          TO IM005-PV-ABEND-PARAGRAPH         
                       PERFORM IM005-B000-RCP-LOCATIONS                         
                       IF IM005-PV-ABEND-CODE NOT = 0                           
                           DISPLAY '***********************************'        
                           DISPLAY 'ERROR RETURNED FROM IMPD005 '               
                           MOVE IM005-PV-ABEND-PARAGRAPH TO PV-PARA-NAME        
                           MOVE IM005-PV-ABEND-CODE TO ABEND-CODE               
                           IF AC-ABEND-4013                                     
                               MOVE IM005-PV-ABEND-MESSAGE                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                               MOVE IM005-PV-ABEND-SQLCODE TO SQLCODE           
                               PERFORM 9980-SQL-ERROR                           
                           ELSE                                                 
                               DISPLAY IM005-PV-ABEND-MESSAGE                   
                               PERFORM 9999-ABEND                               
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM                                                      
                      VARYING IM005-GP-STR-IDX FROM 1 BY 1                      
                        UNTIL IM005-GP-STR-IDX >                                
                              IM005-PV-MAX-STR-GP-STR                           
                       IF IM005-STR-GRP-STORE (IM005-GP-STR-IDX) NOT = 0        
                           MOVE IM005-STR-GRP-STORE (IM005-GP-STR-IDX)          
                                 TO IM044-PCRR-STR-NBR                          
                           PERFORM 7000-WRITE-PRICE-CHGS-RFMT                   
                       END-IF                                                   
                   END-PERFORM                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *              IMPD005 - STORE EXTRACT PROCEDURE                          
      *   THIS PROCEDURE DIVISION WILL BE USED WITH XMWS005 TO EXTRACT          
      *   STORE INFORMATION.                                                    
      *   THE PROGRAMS WHICH USE THIS PROCEDURE MUST SUPPLY THE                 
      *   FOLLOWING:                                                            
      *     1.  MOVE EFFECTIVE DATE TO IM005-PV-EFF-DTE BEFORE                  
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     2.  MOVE PROGRAM NAME TO IM005-PC-PROGRAM-NAME BEFORE               
      *         PERFORMING IM005-A000-GET-PROCESS-STORES.                       
      *     3.  MOVE STORE GROUP TYPE CODE TO IM005-PV-STR-GP-TYP-CDE           
      *         BEFORE PERFORMING IM005-B000-RCP-LOCATIONS.                     
      *     4.  MOVE STORE GROUP ID TO IM005-PV-STR-GP-ID BEFORE                
      *         PERFORMING IM005-B000-RCP-LOCATIONS.                            
      *     5.  THE PROGRAM MUST INCLUDE THE DCLGENS FOR THE FOLLOWING          
      *         TABLES: XMT_LOC                                                 
      *                 XMT_DC_LOC_ASGMT                                        
      *                 XMT_STR_GP_MBSHP                                        
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *                                                                         
      *   IM005-A000-GET-PROCESS-STORES                                         
      *      SHOULD BE PERFORMED AS PART OF INITIALIZATION TO PROGRAM.          
      *      THIS PARAGRAPH LOADS ALL DS(DEPARTMENT STORE) LOCATIONS            
      *      TO BE PROCESSED INTO A TABLE.                                      
      *                                                                         
      *-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-        
      *   IM005-B000-RCP-LOCATIONS                                              
      *      THIS PARAGRAPH SHOULD BE PERFORMED FOR ALL RCP PRICE               
      *      CHANGES BEING PROCESSED.  FOR EACH LOCATION THAT SHOULD BE         
      *      PROCESSED FOR THE PRICE CHANGE, IM005-STR-GRP-STORE                
      *      (IM005-STORE-INDEX) WILL BE SET TO TRUE.                           
      *                                                                         
      ******************************************************************        
           COPY IMPD005.                                                04330094
                                                                                
      *                                                                 06760094
      ******************************************************************07160094
      * READ PRICE CHANGES EXTRACT FILE.                                07170094
      ******************************************************************07180094
       6000-READ-PRICE-CHGS-EXTR-FILE.                                  07140094
           MOVE '*6000-READ-PRICE-CHGS-EXTR-FILE. *' TO PV-PARA-NAME.   07150094
                                                                        07210094
           READ PRICE-CHGS-EXTR-FILE INTO PV-PRICE-CHGS-EXTR-RECORD     07190094
               AT END                                                   07200094
                   SET PS-END-OF-INPUT TO TRUE                          07200094
               NOT AT END                                               07200094
                   ADD 1 TO PA-INPUT-READ-COUNT.                        07210094
                                                                        07210094
      ******************************************************************07320094
      * WRITE PRICE CHANGE RECORDS                                     *07330094
      ******************************************************************07340094
       7000-WRITE-PRICE-CHGS-RFMT.                                              
                                                                                
           MOVE PV-PCER-PRCHG-EFF-DTE   TO IM044-PCRR-EFF-DTE.                  
           MOVE PV-PCER-PRCHG-ID        TO IM044-PCRR-PRCHG-ID.                 
           MOVE PV-PCER-SKU             TO IM044-PCRR-SKU.                      
           MOVE PV-PCER-NEW-UNT-RTL-AMT TO IM044-PCRR-NEW-UNT-RTL-AMT.          
           MOVE PV-PCER-PRCHG-TYP-CDE   TO IM044-PCRR-PRCHG-TYP-CDE.            
                                                                                
           IF PV-PCER-REG-CLEARNCE                                              
             AND PV-PCER-ALL-RMNG-STRS                                          
             AND PV-LAST-TIER-EXISTS-IND = 'Y'                                  
               MOVE 'N'  TO IM044-PCRR-ALL-RMNG-STR-IND                         
           ELSE                                                                 
               MOVE PV-PCER-ALL-RMNG-STR-IND TO                                 
                    IM044-PCRR-ALL-RMNG-STR-IND                                 
           END-IF.                                                              
                                                                                
           WRITE PRICE-CHGS-RFMT-RECORD FROM                            07370094
                 IM044-PRICE-CHGS-RFMT-RECORD.                                  
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
                                                                                
      *                                                                 07390094
      ******************************************************************07160094
      * CHECK IF FUTURE PRICE CHANGE RECORDS EXIST                      07170094
      ******************************************************************07180094
       8600-CHECK-TIER.                                                         
           MOVE '*8600-CHECK-TIER.           '    TO PV-PARA-NAME.      03920094
           MOVE 'N' TO PV-LAST-TIER-EXISTS-IND.                                 
                                                                                
      **--    IF FOUND - INDICATES THAT THIS IS NOT THE LAST TIER               
      **--    AND CHANGE RECORDS ARE TO BE WRITTEN.                             
                                                                                
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           PERFORM WITH TEST AFTER                                              
                   UNTIL ((SQLCODE = ZERO)                                      
                                OR                                              
                          (SQLCODE = +100)                                      
                                OR                                              
                          (PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT))            
                EXEC SQL                                                        
                    SELECT 'Y'                                                  
                      INTO :PV-LAST-TIER-EXISTS-IND                             
                      FROM  XMT_STR_GP_PRCHG                                    
                     WHERE PRCHG_ID     =  :PV-PCER-PRCHG-ID                    
                       AND PRCHG_EFF_DTE > :PV-PCER-PRCHG-EFF-DTE               
                     FETCH FIRST 1 ROWS ONLY                                    
                END-EXEC                                                        
                                                                                
                EVALUATE TRUE                                                   
                    WHEN SQLCODE = +000                                         
                    WHEN SQLCODE = +100                                         
                        CONTINUE                                                
                    WHEN SQLCODE = -911                                         
                    WHEN SQLCODE = -913                                         
                        ADD +1 TO PV-RETRY-COUNT                                
                    WHEN OTHER                                                  
                        MOVE '8600-CHECK-TIER.'                                 
                                      TO PV-PARA-NAME                           
                        MOVE 'SELECT OPERATION '                                
                               TO PV-DB2-OPERATION-ATTEMPTED                    
                        PERFORM 9980-SQL-ERROR                                  
                END-EVALUATE                                                    
           END-PERFORM.                                                         
                                                                                
           IF  SQLCODE = -911                                                   
            OR SQLCODE = -913                                                   
               MOVE '8600-CHECK-TIER.'                                          
                                     TO PV-PARA-NAME                            
               MOVE 'SELECT OPERATION'                                          
                                     TO PV-DB2-OPERATION-ATTEMPTED         CL**1
               PERFORM 9980-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
                                                                                
       9980-SQL-ERROR.                                                  07500094
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07520094
      ******************************************************************07530094
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07560094
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.        
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.                
           DISPLAY 'INPUT RECORDS READ   = ' PA-INPUT-READ-COUNT.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
      *                                                                 07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
             EXEC SQL                                                   07660094
                COMMIT                                                  07670094
             END-EXEC.                                                  07680094
      *                                                                 07690094
           SET AC-ABEND-4013 TO TRUE.                                   07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
       9999-ABEND.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              07780094
           DISPLAY 'INPUT RECORDS READ   = ' PA-INPUT-READ-COUNT.               
           DISPLAY 'RECORDS WRITTEN      = ' PA-WRITE-COUNT.                    
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
