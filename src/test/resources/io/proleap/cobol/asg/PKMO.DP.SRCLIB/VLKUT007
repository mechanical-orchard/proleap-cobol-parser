000001 IDENTIFICATION DIVISION.                                                 
000002 PROGRAM-ID.             VLKUT007.                                        
000003 AUTHOR.                 COGNIZANT.                                       
000004 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000005 DATE-WRITTEN            AUGUST 2014.                                     
000006 DATE-COMPILED.                                                           
000007                                                                          
000008******************************************************************        
000009*  THIS PROGRAM WILL WRITE THE RECORDS TO PO EDI RLSE DATE O/P   *        
000010*  FILE FROM PO EDI RLSE DATE UNIX FILE AND PO EDI PREV DATE RLSE*        
000011*  FILE.                                                         *        
000012*  AND ALSO THIS PROGRAM WILL COUNT UP THE RECORDS IN PO EDI PREV*        
000013*  DATE RLSE AND CREATE AN OUTPUT PO MARKER FILE CONTANING FILE  *        
000013*  NAME, RECORD COUNTS AND BUILD DATE AND TIME.                  *        
000014******************************************************************        
000015* CHANGE DESCRIPTION                                             *        
000015******************************************************************        
000015* DATE  08/08/2014                                               *        
000015*  CHANGE MADE: INITIAL VERSION.                                 *        
000015* MADE BY: VENKATSUBBU T (COGNIZANT)    TKID: TKMAGJ8            *        
000015* CR NO : CHG0089449                                             *        
000015******************************************************************        
000015                                                                          
000015                                                                          
000016 ENVIRONMENT DIVISION.                                                    
000017 INPUT-OUTPUT SECTION.                                                    
000018                                                                          
000019 FILE-CONTROL.                                                            
000020                                                                          
000021     SELECT PO-EDI-UNIX-FILE          ASSIGN TO INP01.                    
000022     SELECT PO-EDI-MF-FILE            ASSIGN TO INP02.                    
000023     SELECT PO-EDI-RLSE-FILE          ASSIGN TO OUT01.                    
000023     SELECT PO-MARKER-FILE            ASSIGN TO OUT02.                    
000024                                                                          
000025 DATA DIVISION.                                                           
000026                                                                          
000027 FILE SECTION.                                                            
000028                                                                          
000029 FD  PO-EDI-UNIX-FILE                                                     
000030     RECORDING MODE IS F                                                  
000031     LABEL RECORDS ARE STANDARD                                           
000032     BLOCK CONTAINS 0 RECORDS.                                            
000034                                                                          
000035 01  PO-EDI-UNIX-REC                PIC X(29).                            
000036                                                                          
000037 FD  PO-EDI-MF-FILE                                                       
000038     RECORDING MODE IS F                                                  
000039     LABEL RECORDS ARE STANDARD                                           
000040     BLOCK CONTAINS 0 RECORDS.                                            
000042                                                                          
000043 01  PO-EDI-MF-REC                  PIC X(29).                            
000044                                                                          
000029 FD  PO-EDI-RLSE-FILE                                                     
000030     RECORDING MODE IS F                                                  
000031     LABEL RECORDS ARE STANDARD                                           
000032     BLOCK CONTAINS 0 RECORDS.                                            
000034                                                                          
000035 01  PO-EDI-RLSE-REC                PIC X(29).                            
000052                                                                          
000045 FD  PO-MARKER-FILE                                                       
000046     RECORDING MODE IS F                                                  
000047     LABEL RECORDS ARE STANDARD                                           
000048     BLOCK CONTAINS 0 RECORDS.                                            
000050                                                                          
000051 01  PO-MARKER-REC                  PIC X(78).                            
000034                                                                          
000053 WORKING-STORAGE SECTION.                                                 
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
000060 01  PC-PROGRAM-CONSTANTS.                                                
000061     05  PC-CURRENT-PGM-NAME         PIC  X(08) VALUE 'VLKUT007'.         
000062                                                                          
000063 01  PS-SWITCHES.                                                         
000064     05  PS-EOF-UNX-SW               PIC  X(01)    VALUE 'N'.             
000065         88  PS-EOF-UNX                            VALUE 'Y'.             
000066         88  PS-NOT-EOF-UNX                        VALUE 'N'.             
000067     05  PS-EOF-MF-SW                PIC  X(01)    VALUE 'N'.             
000068         88  PS-EOF-MF                             VALUE 'Y'.             
000069         88  PS-NOT-EOF-MF                         VALUE 'N'.             
000070                                                                          
000071 01  PV-PROGRAM-VARIABLES.                                                
000072     05  PV-ABEND-CODE                PIC S9(04) VALUE +0 COMP.           
000072     05  PV-REC-CNT-UNX               PIC  9(09) VALUE ZEROS.             
000073     05  PV-REC-CNT-MF                PIC  9(09) VALUE ZEROS.             
000074     05  PV-REC-EDI-WRITTEN           PIC  9(09) VALUE ZEROS.             
000074     05  PV-REC-WRITTEN               PIC  9(05) VALUE ZEROS.             
000075     05  PV-SCHEDULE-DATE             PIC  9(08) VALUE ZEROS.             
000076     05  PV-RUN-DATE-CN.                                                  
000077         10  PV-RUN-CCYY-CN           PIC  X(04) VALUE SPACES.            
000078         10  PV-RUN-MO-CN             PIC  X(02) VALUE SPACES.            
000079         10  PV-RUN-DD-CN             PIC  X(02) VALUE SPACES.            
000080     05  PV-RUN-TIME                  PIC  X(08) VALUE SPACES.            
000081     05  PV-RUN-TIME-R REDEFINES PV-RUN-TIME.                             
000082         10  PV-RUN-HH                PIC  X(02).                         
000083         10  PV-RUN-MM                PIC  X(02).                         
000084         10  PV-RUN-SS                PIC  X(02).                         
000085         10  FILLER                   PIC  X(02).                         
000086     05  PV-RUN-TIME-CN.                                                  
000087         10  PV-RUN-HH-CN             PIC  X(02) VALUE SPACES.            
000088         10  PV-RUN-MM-CN             PIC  X(02) VALUE SPACES.            
000089         10  PV-RUN-SS-CN             PIC  X(02) VALUE SPACES.            
000090***** FILE NAME CONSISTS OF RUN-DATE-CN_20140714_EDIRLSEDTE.DAT           
000091*****     EXAMPLE: 20140714_EDIRLSEDTE.DAT                                
000093     05  PV-FILE-NAME.                                                    
000094         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
000095         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
000096         10  PV-DOT-DAT               PIC  X(14) VALUE SPACES.            
000097                                                                          
000097                                                                          
013420*** FILE LAYOUT FOR PO EDI UNIX FILE                             *        
013500 01  WS-PO-EDI-UNX.                                                       
013510     05  WS-ACD-IND-UNX               PIC X(01)  VALUE SPACES.            
013510     05  WS-PO-NBR-UNX                PIC X(20)  VALUE SPACES.            
013530     05  WS-RLSE-DTE-UNX              PIC X(08)  VALUE SPACES.            
007900                                                                          
013420*** FILE LAYOUT FOR PO EDI MF FILE                               *        
013500 01  WS-PO-EDI-MF.                                                        
013510     05  WS-ACD-IND-MF                PIC X(01)  VALUE SPACES.            
013510     05  WS-PO-NBR-MF                 PIC X(20)  VALUE SPACES.            
013530     05  WS-RLSE-DTE-MF               PIC X(08)  VALUE SPACES.            
000098                                                                          
013420*** FILE LAYOUT FOR PO EDI RLSE FILE                             *        
013500 01  WS-PO-EDI-RLS.                                                       
013510     05  WS-ACD-IND-RLS               PIC X(01)  VALUE SPACES.            
013510     05  WS-PO-NBR-RLS                PIC X(20)  VALUE SPACES.            
013530     05  WS-RLSE-DTE-RLS              PIC X(08)  VALUE SPACES.            
000098                                                                          
000099*** FILE LAYOUT FOR MARKER RECORD                                         
000100     COPY VLRD029.                                                        
000101                                                                          
000102*** FILE LAYOUT FOR CALENDAR ROUTINE - DPKUT500                           
000103     COPY DPWS500.                                                        
000104                                                                          
000105                                                                          
000106 PROCEDURE DIVISION.                                                      
000107                                                                          
000108 0000-MAINLINE-PROCESSING.                                                
000109     PERFORM 0100-INITIALIZATION.                                         
000110                                                                          
000111     PERFORM 0200-READ-EDI-UNIX                                           
000112          UNTIL PS-EOF-UNX.                                               
000113                                                                          
000114     PERFORM 0300-READ-EDI-MF                                             
000115          UNTIL PS-EOF-MF.                                                
000116                                                                          
000117     PERFORM 9000-EOJ.                                                    
000118                                                                          
000119     STOP RUN.                                                            
000120                                                                          
000121 0100-INITIALIZATION.                                                     
000122     OPEN  INPUT PO-EDI-UNIX-FILE                                         
000123           INPUT PO-EDI-MF-FILE                                           
000124          OUTPUT PO-EDI-RLSE-FILE                                         
000124          OUTPUT PO-MARKER-FILE.                                          
000125                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 0150-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME.                      
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME.                        
000137                                                                          
000138     MOVE PV-RUN-HH          TO PV-RUN-HH-CN.                             
000139     MOVE PV-RUN-MM          TO PV-RUN-MM-CN.                             
000140     MOVE PV-RUN-SS          TO PV-RUN-SS-CN.                             
000141                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 0150-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT007 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 0150-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
000170******************************************************************        
000171**  READ THE PO HEADER FILE AND COUNT THE RECORDS                         
000172******************************************************************        
000173 0200-READ-EDI-UNIX.                                                      
000174     READ  PO-EDI-UNIX-FILE INTO WS-PO-EDI-UNX                            
000175         AT END                                                           
000176             SET PS-EOF-UNX TO TRUE                                       
000177         NOT AT END                                                       
000178             ADD 1 TO PV-REC-CNT-UNX                                      
000178             PERFORM 0210-EDI-UNX-APPEND                                  
000179     END-READ.                                                            
000180                                                                          
000181******************************************************************        
000182**  READ THE PO DETAIL FILE AND COUNT THE RECORDS                         
000183******************************************************************        
000184 0300-READ-EDI-MF.                                                        
000185     READ  PO-EDI-MF-FILE INTO WS-PO-EDI-MF                               
000186         AT END                                                           
000187             SET PS-EOF-MF TO TRUE                                        
000188         NOT AT END                                                       
000189             ADD 1 TO PV-REC-CNT-MF                                       
000189             PERFORM 0310-EDI-UNX-APPEND                                  
000190     END-READ.                                                            
000191                                                                          
000192*****************************************************************         
000193*  APPEND EDI UNIX FILE                                                   
000194*****************************************************************         
000195 0210-EDI-UNX-APPEND.                                                     
000195                                                                          
000195     MOVE WS-ACD-IND-UNX   TO   WS-ACD-IND-RLS                            
000195     MOVE WS-PO-NBR-UNX    TO   WS-PO-NBR-RLS                             
000195     MOVE WS-RLSE-DTE-UNX  TO   WS-RLSE-DTE-RLS                           
000195     WRITE PO-EDI-RLSE-REC FROM WS-PO-EDI-RLS                             
000195     ADD 1                 TO   PV-REC-EDI-WRITTEN.                       
000195                                                                          
000192*****************************************************************         
000193*  APPEND EDI MF FILE                                                     
000194*****************************************************************         
000195 0310-EDI-UNX-APPEND.                                                     
000195                                                                          
000195     MOVE WS-ACD-IND-MF    TO   WS-ACD-IND-RLS                            
000195     MOVE WS-PO-NBR-MF     TO   WS-PO-NBR-RLS                             
000195     MOVE WS-RLSE-DTE-MF   TO   WS-RLSE-DTE-RLS                           
000195     WRITE PO-EDI-RLSE-REC FROM WS-PO-EDI-RLS                             
000195     ADD 1                 TO   PV-REC-EDI-WRITTEN.                       
000195                                                                          
000192*****************************************************************         
000193*  CREATE MARKER FILE                                                     
000194*****************************************************************         
000195 0400-CREATE-MARKER.                                                      
000196     MOVE PV-RUN-DATE-CN     TO PV-CREATE-DATE.                           
000197     MOVE 'EDIRLSEDTE.DAT'   TO PV-DOT-DAT.                               
000198     MOVE PV-FILE-NAME       TO VL029-FILE-NAME.                          
000199     MOVE PV-REC-EDI-WRITTEN TO VL029-RECORD-COUNT.                       
000200     MOVE PV-RUN-DATE-CN     TO VL029-BUILD-DATE.                         
000201     MOVE PV-RUN-TIME-CN     TO VL029-BUILD-TIME.                         
000202     MOVE 'X'                TO VL029-ANCHOR-CONSTANT.                    
000203                                                                          
000204     WRITE PO-MARKER-REC     FROM VL029-MARKER-RECORD.                    
000205     ADD 1                   TO PV-REC-WRITTEN.                           
000206                                                                          
000217*****************************************************************         
000218*  END OF JOB PROCESSING                                                  
000219*****************************************************************         
000220 9000-EOJ.                                                                
000221     DISPLAY 'PO EDI UNIX READ COUNT  => ' PV-REC-CNT-UNX.                
000222                                                                          
000223     DISPLAY 'PO EDI MF READ COUNT    => ' PV-REC-CNT-MF.                 
000224                                                                          
000225     PERFORM 0400-CREATE-MARKER.                                          
000226                                                                          
000227     DISPLAY 'PO EDI RLSDT WRITE COUNT=> ' PV-REC-EDI-WRITTEN.            
000228                                                                          
000227     DISPLAY 'PO MARKER WRITTEN       => ' PV-REC-WRITTEN.                
000228                                                                          
000229     CLOSE PO-EDI-UNIX-FILE                                               
000230           PO-EDI-MF-FILE                                                 
000230           PO-EDI-RLSE-FILE                                               
000231           PO-MARKER-FILE.                                                
000232                                                                          
000233*********** END OF VLKUT007 SOURCE CODE *************************         
