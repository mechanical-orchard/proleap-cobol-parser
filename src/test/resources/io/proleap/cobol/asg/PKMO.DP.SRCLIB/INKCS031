      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    INKCS031.                                                 
       AUTHOR.        SCOTT JACKSON.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10-08-99.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    I031 - SHEET INVENTORY UPDATE/DELETE PROGRAM               *         
      *                                                               *         
      *    THIS PROGRAM DISPLAYS THE SKU, QUANTITY, UNIT RETAIL AND   *         
      *    EXTENDED RETAIL FOR TOTAL SHEET AND EACH INDIVIDUAL LINE.  *         
      *    THIS SCREEN ALLOWS THE OPERATOR CHANGE SKU, QUANTITY OR    *         
      *    INDIVIDUAL SKU RETAIL FOR ANY OF THE SHEET LINES. USER CAN *         
      *    DELETE AN ENTIRE SHEET, OR ADD, CHANGE, AND DELETE         *         
      *    INDIVIDUAL LINES WITHIN A SHEET.                           *         
      *---------------------------------------------------------------*         
      ***RCP*** REGIONAL CLEARANCE PRICING                            *         
      *    DUE TO RCP, A PARTICULAR SKU MAY BE CLEARANCE IN SOME      *         
      *    MARKETS, WHILE STILL ACTIVE IN OTHERS. THIS IS DETERMINED  *         
      *    BASED ON FLAG SET ON 'TSK'. REGN_PRIC_IND IS SET TO 'Y'    *         
      *    IF THIS SKU HAS MULTIPLE STATUS CODES ACROSS LOCATIONS.    *         
      *    IF 'Y' IS FOUND, THEN UNIT RETAIL WILL BE ATTEMPTED TO BE  *         
      *    RETRIEVED FOR THE STORE SPECIFIC ROW (ON TUPCPLS). IF THE  *         
      *    INDICATOR IS SET TO 'N' (OR NO STORE SPECIFIC ROW FOUND),  *         
      *    THEN THE STORE SPECIFIC UNIT RETAIL ROW IS RETRIEVED.      *         
      *---------------------------------------------------------------*         
      * 03/04/2000  SCOTT JACKSON      CHG ID:SMJ                     *         
      *    ONLINE ABENDS (-913) OCCUR UPON ENTRY TO SHEET MAINTENANCE *         
      *    IF TINVPAR IS WAS BEING UPDATED BY BATCH PROCESSES. 'SOFT' *         
      *    ABEND TO USER WITH 'RESOURCE UNAVAILABLE' ON INITIAL READ .*         
      *---------------------------------------------------------------*         
IN2001* 09/06/2000  SCOTT JACKSON      CHG ID:IN2001                  *         
IN2001*  AN ITEM IN RCP STATUS MAY BE RETURNED TO NON-RCP STATUS      *         
IN2001*  PRIOR TO PROCESSING ALL OF THE STORE'S DATA. THEREFORE USING *         
IN2001*  TSK.REGN_PRICE_IND AS THE IND TO OBTAINING CURRENT PRICE MAY *         
IN2001*  BE INCORRECT AND UNRELIABLE BASED ON WHEN DATA IS PROCESSED. *         
IN2001*  TUPCPLS WILL BE USED SOLELY IN DETERMINING AN ITEM'S STATUS. *         
IN2001*  CHECK STORE 000 ROW 1ST (IF STATUS = '25' OR '30' CHECK FOR  *         
IN2001*  STORE-SPECIFIC ROW). USE STORE 000 PRICING IF NO STORE ROW.  *         
IN2001*  IF STATUS <> '25' THEN STORE-SPECIFIC ROW WILL NOT BE CHECKED*         
IN2001*---------------------------------------------------------------*         
23511C* 05/21/2002 CAROL WOJCIECHOWSKI CHG ID:23511C                  *         
23511C*  DISPLAY THE PRICE FOR A STATUE OF '25' OR '30' WHEN THE      *         
23511C*  UPC_STAT_CHG_DTE IS EQUAL TO THE UNT_RTL_CHG_DTE             *         
23511C*                                                               *         
W21732*---------------------------------------------------------------*         
W21732* 10/28/2002 IGSI                CHG ID:W21732                  *         
W21732* ACCEPT NON-INTELLIGENT SKU IN PLACE OF THE CURRENT DEPT/CLASS/*         
W21732* SEQ NUMBER.                                                   *         
C21732*----------------------------------------------------------------*        
C21732*    CHANGE BY S JACKSON  3/17/03                                *        
C21732*    REPLACE DPKUT040 MODULE FOR RENUMBER SKUS AND UPC-GENERATION         
C21732*    WITH DB2 USING TUPC AND TSKXREF. USE DPKUT041 FOR CHECK DIGIT        
W24521*    ALLOW MANUAL OVERIDE OF YELLOW-TICKET MERCHANDISE FOR DC/RTV*        
      *                                                                         
W26600*----------------------------------------------------------------*        
W26600*  WR26600  07/  /2004  PHYSICAL INVENTORY STORE EXPANSION                
W26600*                       ALSO DISPLAYED STORE NAME WITH THE                
W26600*                       LOCATION ID (STORE NUMBER)                        
W26600*                       M JOHNSON                                         
W28545*  W28545   08-22-2005  ROD JANSSEN THE NEW TRANSFER PROCESS              
W28545*----------------------------------------------------------------*        
W28545*  W28545   02-06-2006  CAPTURING AND REPORTING UPC FOR                   
W28545*                       INVENTORY.                                        
W28545*                       -JOHN SELWITSCHKA                                 
J28545*  W28545   05-30-2006  USE THE PRICING MODULE, INPD400, TO LOOK          
J28545*                       UP PRICE AND VALIDATE SKU OR UPC.                 
J28545*                       ALSO, TINVPAR/TINCNTL ARE USED TO GET             
J28545*                       THE PROPER INV-ID AND STORE.                      
J28545*                       -JAN BLAZICH                                      
W28582*  W28582   07-31-2007  AFTER LOOKING UP RETAIL WITH UPC, IF DUMMY        
W28582*                       UPC GET DATA FROM ATTRIBUTES.                     
W28545*                       -JOHN SELWITSCHKA                                 
      *  W33304   05-01-2008  HOLDING INVENTORY LEDGER OPEN ACROSS              
      *                       PERIODS INVENTORY SHORTAGE REPORTING              
      *                       -JILL LIN                                         
SR3403*  SR3403   03-27-2012  AMT FIELD EXPANSIONS FOR LARGE RETAIL             
SR3403*                       PROJECT                                           
SR3403*                       -JALAJ KUMAR                                      
      ******************************************************************        
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
      ******************************************************************        
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
      * CHANGED 02/14/2023    BY JEAN KURK       WR: CHG0281369                 
      * REMOVE COPYBOOK FOR THE UPC CHECK DIGIT ROUTINE                         
      *---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME     PIC  X(08) VALUE 'IN031A  '.             
           05  PC-CURRENT-MAPSET-NAME  PIC  X(08) VALUE 'INKM031 '.             
           05  PC-CURRENT-PROGRAM-NAME PIC  X(08) VALUE 'INKCS031'.             
           05  PC-CALENDAR-ROUTINE     PIC  X(08) VALUE 'DPKUT500'.             
           05  PC-LINES-PER-PANEL      PIC S9(04) VALUE +25   COMP SYNC.        
           05  PC-MDSELV-CDE           PIC  X(03) VALUE 'SCL'.                  
           05  PC-OPERCO-NBR           PIC  X(02) VALUE '03'.                   
           05  PC-APLSYS-CDE           PIC  X(02) VALUE 'IN'.                   
           05  PC-ITM-UNIT-PR-MAX-DIGITS                                        
                                       PIC S9(04) VALUE +5.                     
IN2001     05  PC-MIXED-STATUS         PIC  X(02) VALUE '25'.                   
           05  PC-CLEARANCE-SKU-STATUS PIC  X(02) VALUE '30'.                   
           05  PC-RENUMBERED-SKU-STAT98                                         
                                       PIC  X(02) VALUE '98'.                   
           05  PC-MAX-QTY              PIC  9(07) VALUE 9999.                   
           05  PC-MIN-PRICE            PIC  9(05)V99                            
                                                  VALUE .01.                    
SR3403     05  PC-MAX-PRICE            PIC  9(05)V99                            
SR3403                                            VALUE 99999.99.               
           05  PC-STORE-000            PIC S9(04) COMP  VALUE +0.               
W28545*    05  PC-UPC-NBR              PIC S9(15)V COMP-3 VALUE +0.             
           05  PC-KEYED-SHEET-ENTRY    PIC  X(01) VALUE 'K'.                    
W24521     05  PC-DC                   PIC  X(02) VALUE 'DC'.                   
W24521     05  PC-RTV                  PIC  X(02) VALUE 'RV'.                   
                                                                                
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00004         PIC 9(5)   VALUE 00004.                  
           05  PC-TSYMSG-00005         PIC 9(5)   VALUE 00005.                  
           05  PC-TSYMSG-00007         PIC 9(5)   VALUE 00007.                  
           05  PC-TSYMSG-00008         PIC 9(5)   VALUE 00008.                  
           05  PC-TSYMSG-00010         PIC 9(5)   VALUE 00010.                  
           05  PC-TSYMSG-00046         PIC 9(5)   VALUE 00046.                  
           05  PC-TSYMSG-00059         PIC 9(5)   VALUE 00059.                  
           05  PC-TSYMSG-00069         PIC 9(5)   VALUE 00069.                  
           05  PC-TSYMSG-00070         PIC 9(5)   VALUE 00070.                  
           05  PC-TSYMSG-00115         PIC 9(5)   VALUE 00115.                  
           05  PC-TSYMSG-00137         PIC 9(5)   VALUE 00137.                  
           05  PC-TSYMSG-00148         PIC 9(5)   VALUE 00148.                  
           05  PC-TSYMSG-00343         PIC 9(5)   VALUE 00343.                  
W28545     05  PC-TSYMSG-00453         PIC 9(5)   VALUE 00453.                  
           05  PC-TSYMSG-00474         PIC 9(5)   VALUE 00474.                  
           05  PC-TSYMSG-00547         PIC 9(5)   VALUE 00547.                  
           05  PC-TSYMSG-00551         PIC 9(5)   VALUE 00551.                  
           05  PC-TSYMSG-00560         PIC 9(5)   VALUE 00560.                  
           05  PC-TSYMSG-00614         PIC 9(5)   VALUE 00614.                  
SMJ        05  PC-TSYMSG-01063         PIC 9(5)   VALUE 01063.                  
           05  PC-TSYMSG-02144         PIC 9(5)   VALUE 02144.                  
           05  PC-TSYMSG-02145         PIC 9(5)   VALUE 02145.                  
J28545     05  PC-TSYMSG-02891         PIC 9(5)   VALUE 02891.                  
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                  PIC S9(04) VALUE +0    COMP SYNC.        
           05  PS-SUB2                 PIC S9(04) VALUE +0    COMP SYNC.        
           05  PS-ORIG-SUB             PIC S9(04) VALUE +0    COMP SYNC.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                 PIC X  VALUE 'Y'.                    
               88  PS-NO-ERROR                    VALUE 'Y'.                    
               88  PS-ERROR                       VALUE 'N'.                    
           05  PS-ERRORS-IN-LINE-SW        PIC X  VALUE 'N'.                    
               88  PS-ERRORS-IN-LINE              VALUE 'Y'.                    
               88  PS-NO-ERRORS-IN-LINE           VALUE 'N'.                    
           05  PS-INVALID-SKU-SW           PIC X  VALUE 'N'.                    
               88  PS-VALID-SKU                   VALUE 'Y'.                    
               88  PS-INVALID-SKU                 VALUE 'N'.                    
W28545     05  PS-INVALID-UPC-SW           PIC X  VALUE 'N'.                    
W28545         88  PS-VALID-UPC                   VALUE 'Y'.                    
W28545         88  PS-INVALID-UPC                 VALUE 'N'.                    
           05  PS-LIST-END-SW              PIC X  VALUE 'N'.                    
               88  PS-LIST-END                    VALUE 'Y'.                    
               88  PS-NO-LIST-END                 VALUE 'N'.                    
           05  PS-DATA-ENTERED-SW          PIC X  VALUE 'N'.                    
               88  PS-DATA-ENTERED                VALUE 'Y'.                    
               88  PS-NO-DATA-ENTERED             VALUE 'N'.                    
           05  PS-SHEET-FOUND-SW           PIC X  VALUE 'N'.                    
               88  PS-SHEET-FOUND                 VALUE 'Y'.                    
               88  PS-NO-SHEET-FOUND              VALUE 'N'.                    
           05  PS-UPDATES-REQUESTED-SW     PIC X  VALUE 'N'.                    
               88  PS-UPDATES-REQUESTED           VALUE 'Y'.                    
               88  PS-NO-UPDATES-REQUESTED        VALUE 'N'.                    
           05  PS-UPDATE-ERROR-FOUND-SW    PIC X  VALUE 'N'.                    
               88  PS-UPDATE-ERROR-FOUND          VALUE 'Y'.                    
               88  PS-NO-UPDATE-ERROR-FOUND       VALUE 'N'.                    
           05  PS-DONE-RENUMBERING-SW      PIC X  VALUE 'N'.                    
               88  PS-DONE-RENUMBERING            VALUE 'Y'.                    
               88  PS-NOT-DONE-RENUMBERING        VALUE 'N'.                    
           05  PS-GOT-LAST-PRICE-SW        PIC X.                               
               88  PS-GOT-LAST-PRICE-FROM-SCREEN                                
                                                  VALUE 'S'.                    
J28545         88  PS-GOT-LAST-PRICE-FROM-INPD400                               
                                                  VALUE 'T'.                    
               88  PS-DIDNT-GET-LAST-PRICE        VALUE 'N'.                    
SMJ        05  PS-INVPAR-SW                PIC X  VALUE 'N'.                    
SMJ            88  PS-UNAVAILABLE-RESOURCE        VALUE 'Y'.                    
SMJ            88  PS-INVPAR-PROCESSED            VALUE 'N'.                    
                                                                                
W28545 01  WS-EDIT-UPC-SKU-TABLE.                                               
W28545     05  WS-EDIT-ELEMENTS        OCCURS 15 TIMES.                         
W28545         10  WS-EDIT-BYTE        PIC X(01).                               
W28545                                                                          
       01  PV-PROGRAM-VARIABLES.                                                
W21732     05  PV-DUMMY-SKU            PIC X(08).                               
W28545     05  PV-BYTE-SUB             PIC S9(05) COMP VALUE ZEROS.             
W28545     05  PV-BYTE-COUNT           PIC S9(15) COMP VALUE ZEROS.             
W28545     05  PV-BYTE-ACCUM           PIC S9(15) COMP VALUE ZEROS.             
W28545     05  PV-EDIT-UPC-SKU         PIC X(15).                               
W28545     05  PV-EDIT-UPC-SKU-NUM REDEFINES                                    
W28545         PV-EDIT-UPC-SKU         PIC 9(15).                               
W28545     05  PV-SKU-POS              PIC S9(04) COMP VALUE ZEROS.             
W28545     05  PV-UPC-POS              PIC S9(04) COMP VALUE ZEROS.             
           05  PV-CICS-RESP            PIC S9(04) VALUE +0    COMP SYNC.        
           05  PV-ACTL-INV-DTE-YYYYMMDD.                                        
               10  PV-ACTL-INV-DTE-YYYY PIC  X(04).                             
               10  PV-ACTL-INV-DTE-MM   PIC  X(02).                             
               10  PV-ACTL-INV-DTE-DD   PIC  X(02).                             
                                                                                
           05  PV-STAT-CHNGD-DATE-YYYYMMDD                                      
                                       PIC  X(08).                              
W21732     05  PV-MAJ-CL-NBR.                                                   
W21732         10 PV-MAJ-CL-NBR-1      PIC  X     VALUE SPACES.                 
W21732         10 PV-MAJ-CL-NBR-2      PIC  X     VALUE SPACES.                 
           05  PV-SUB-CL-NBR.                                                   
               10 PV-SUB-CL-NBR-1      PIC  X     VALUE SPACES.                 
W21732         10 PV-SUB-CL-NBR-2-3    PIC  X(02) VALUE SPACES.                 
W21732     05  PV-DATE                 PIC  X(10).                              
W21732     05  PV-YYMMDD-DATE          PIC  X(6).                               
W21732     05  FILLER                  REDEFINES                                
W21732         PV-YYMMDD-DATE.                                                  
W21732         10  PV-YYMMDD-YY        PIC  X(02).                              
W21732         10  PV-YYMMDD-MMDD.                                              
W21732             15 PV-YYMMDD-MM     PIC  X(02).                              
W21732             15 PV-YYMMDD-DD     PIC  X(02).                              
W21732     05  PV-MMDDYY-DATE.                                                  
W21732         10  PV-MMDDYY-MMDD      PIC  X(04).                              
W21732         10  PV-MMDDYY-YY        PIC  X(02).                              
J28545     05  PV-IN400-ROW.                                                    
J28545         10  PV-IN400-UPC-STAT-CDE   PIC X(02)    VALUE SPACES.           
J28545         10  PV-IN400-UNT-RTL-AMT    PIC S9(7)V99 VALUE +0 COMP-3.        
J28545         10  PV-IN400-MITGPL-QTY     PIC S9(9)    VALUE +0 COMP-3.        
JKB        05  PV-INV-ID               PIC S9(9)    COMP.                       
                                                                                
       01  PV-DB2-KEY-AREA.                                                     
           05  PV-DB2-STORE-NBR        PIC S9(04)   COMP.                       
W28545     05  PV-DB2-TYPE-CDE         PIC X(01).                               
           05  PV-DB2-SHEET-NBR        PIC S9(09)   COMP.                       
           05  PV-DB2-LINE-NBR         PIC S9(04)             COMP SYNC.        
           05  PV-DB2-UPC-NBR          PIC S9(15)             COMP-3.           
           05  PV-DB2-ITEM-NMBR        PIC S9(15)             COMP-3.           
W21732     05  PV-DB2-SKU-SEQ-NBR      PIC  X(08) VALUE SPACES.                 
W21732     05  PV-DB2-SKU-SEQ-NBR-N REDEFINES                                   
W21732         PV-DB2-SKU-SEQ-NBR      PIC  9(08).                              
           05  PV-STR-NBR              PIC S9(04) COMP.                         
SR3403     05  PV-GROUP-UNIT-RTL       PIC  9(05)V99 VALUE ZEROES.              
SR3403     05  PV-ITEM-RETAIL          PIC  9(05)V99 VALUE ZEROES.              
           05  PV-EXTD-RTL-AMT         PIC  9(10)V99 VALUE ZEROES.              
                                                                                
       01  PV-INITIAL-TOTALS.                                                   
           05  PV-UNT-RTL-AMT-TOTAL    PIC  9(07)V99 VALUE ZEROES.              
           05  PV-INV-QTY-TOTAL        PIC  9(05)    VALUE ZEROES.              
           05  PV-EXTD-RTL-AMT-TOTAL   PIC  9(08)V99 VALUE ZEROES.              
                                                                                
       01  PV-PLND-INV-TMST            PIC  X(26) VALUE SPACES.                 
                                                                                
       01  PV-PLND-INV-TIMESTAMP.                                               
           10  PV-PLND-INV-DTE         PIC  X(10) VALUE SPACES.                 
           10  PV-DASH                 PIC  X(01) VALUE '-'.                    
           10  PV-PLND-DTE-HOUR        PIC  X(02) VALUE '23'.                   
           10  PV-SEPARATOR            PIC  X(01) VALUE '.'.                    
           10  PV-PLND-DTE-MINUTE      PIC  X(02) VALUE '59'.                   
           10  PV-SEPARATOR            PIC  X(01) VALUE '.'.                    
           10  PV-PLND-DTE-SECOND      PIC  X(02) VALUE '59'.                   
           10  PV-SEPARATOR            PIC  X(01) VALUE '.'.                    
           10  PV-PLND-DTE-MSEC        PIC  X(06) VALUE '999999'.               
                                                                                
W28545 01  PV-RENUMBERED-SKU.                                                   
W28545     10  PV-RSKU-NBR             PIC  X(08).                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    COPY BOOK FOR NUMERIC EDIT SUBROUTINE (DPKUT100) PARAMETER  *        
      *    LIST.                                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    COPY BOOK FOR CURRENT SYSTEM DATE/TIME PROCEDURE DIVISION   *        
      *    ROUTINE                                                              
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS017.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    COPY BOOK FOR CALENDAR SUBROUTINE (DPKUT500)                         
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS500.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
1122RM     COPY DPWS930.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      *    INTER-APPLICATION COMMAREA.                                *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY INWS030.                                                        
      *---------------------------------------------------------------*         
      *    SPECIFIC COMMMAREA FOR INKCS031.                           *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
W26600             15  ASC-KEY-STORE-NBR-X         PIC  X(04).                  
                   15  ASC-KEY-STORE-NBR REDEFINES                              
W26600                 ASC-KEY-STORE-NBR-X         PIC  9(04).                  
                   15  ASC-KEY-SHEET-NBR-X.                                     
                       20  ASC-KEY-SHEET-NBR       PIC  9(06).                  
W28545             15  ASC-KEY-TYPE-CDE           PIC X(01).                    
W28545                 88  ASC-TYPE-ALL-SHEETS    VALUE '*'.                    
W28545                 88  ASC-TYPE-TFR-SHEETS    VALUE 'T'.                    
W28545                 88  ASC-TYPE-MANI-SHEETS   VALUE 'M'.                    
W28545                 88  ASC-TYPE-ENTRD-SHEETS  VALUE 'K'.                    
W28545             15  ASC-TYPE-CDE-DESC              PIC X(25).                
W28545                 88  ASC-DESC-ALL-SHEETS                                  
W28545                     VALUE 'ALL SHEETS'.                                  
W28545                 88  ASC-DESC-TFR-SHEETS                                  
W28545                     VALUE 'TRANSFER STORE SHEETS'.                       
W28545                 88  ASC-DESC-MANI-SHEETS                                 
W28545                     VALUE 'MANIFEST TRAILER SHEETS'.                     
W28545                 88  ASC-DESC-ENTRD-SHEETS                                
W28545                     VALUE 'USER ENTERED SHEETS'.                         
                   15  ASC-STORE-NAME              PIC  X(25).                  
                   15  ASC-ACTL-INV-DTE.                                        
                       20  ASC-ACTL-INV-DTE-CCYY   PIC  X(04).                  
                       20  FILLER                  PIC  X(01).                  
                       20  ASC-ACTL-INV-DTE-MM     PIC  X(02).                  
                       20  FILLER                  PIC  X(01).                  
                       20  ASC-ACTL-INV-DTE-DD     PIC  X(02).                  
                   15  ASC-INT-APPL-FORMAT-IND     PIC  X.                      
                       88  ASC-CRIT-SPEC-FMT              VALUE '1' '2'.        
                       88  ASC-LIST-FMT                   VALUE '3' '4'.        
                       88  ASC-UPDATE-FMT                 VALUE '1' '3'.        
                       88  ASC-DELETE-FMT                 VALUE '2' '4'.        
                   15  ASC-DATA-EXISTS-SW          PIC X.                       
                       88  ASC-DATA-EXISTS                VALUE 'Y'.            
                       88  ASC-DATA-DOES-NOT-EXIST        VALUE 'N'.            
                                                                                
                   15  ASC-SEL-SUB                 PIC S9(04) COMP.             
                   15  ASC-SEL-ITEM                PIC S9(04) COMP.             
                   15  ASC-NUMBER-OF-TS-ITEMS      PIC S9(05) COMP-3.           
                   15  ASC-USE-SELECTION-QUEUE-IND PIC  X.                      
                       88  ASC-USE-SELECTION-QUEUE        VALUE 'Y'.            
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID         PIC  X(04).                  
                       20  ASC-SEL-TASK-NUMBER     PIC S9(05)                   
                                                              COMP-3.           
                       20  ASC-SEL-SEQ             PIC  9.                      
                   15  ASC-LAST-LINE-NBR           PIC S9(04)                   
                                                              COMP SYNC.        
                                                                                
                   15  ASC-ITEM-ARRAY.                                          
                       20  ASC-MAP-DETAIL-LINE     OCCURS 25 TIMES.             
                           25  ASC-LINE-CHANGED-SW PIC X.                       
                               88  ASC-LINE-CHANGED      VALUE 'Y'.             
                               88  ASC-LINE-DIDNT-CHANGE VALUE SPACE.           
                           25  ASC-LINE-NBR        PIC S9(04)                   
                                                              COMP SYNC.        
                           25  ASC-LINE-NBR-X      REDEFINES                    
                               ASC-LINE-NBR        PIC  X(02).                  
W21732                     25  ASC-SKU-NBR         PIC  X(08).                  
W21732                     25  ASC-SKU-NBR-N       REDEFINES                    
W21732                         ASC-SKU-NBR         PIC  9(08).                  
W28545                     25  ASC-UPC-NBR-X       PIC  X(15).                  
W28545                     25  ASC-UPC-NBR         REDEFINES                    
W28545                         ASC-UPC-NBR-X       PIC  9(15).                  
W28545                     25  FILLER              REDEFINES                    
W28545                         ASC-UPC-NBR-X.                                   
W28545                         30  FILLER          PIC  X(02).                  
W28545                         30  ASC-UPC-1-2     PIC  X(02).                  
W28545                             88  ASC-UPC-INTERNAL   VALUE '04'.           
W28545                         30  ASC-UPC-3-4     PIC  X(02).                  
W28545                         30  ASC-UPC-SKU     PIC  X(08).                  
W28545                         30  ASC-UPC-CHECK-DIGIT PIC  9.                  
                           25  ASC-INV-QTY-X       PIC  X(04).                  
                           25  ASC-INV-QTY         REDEFINES                    
                               ASC-INV-QTY-X       PIC  9(04).                  
SR3403                     25  ASC-ITM-UNIT-PR-AMT-X                            
SR3403                                             PIC  X(08).                  
SR3403                     25  ASC-ITM-UNIT-PR-AMT-X-FMT                        
SR3403                                             REDEFINES                    
SR3403                         ASC-ITM-UNIT-PR-AMT-X                            
SR3403                                             PIC  Z(5).99.                
SR3403                     25  ASC-ITM-UNIT-PR-AMT PIC  9(5)V99.                
W28545*                    25  ASC-ITM-EXTD-PR-AMT-X   PIC  X(13).              
W28545*                    25  ASC-ITM-EXTD-PR-AMT-X-FMT                        
W28545*                                            REDEFINES                    
W28545*                        ASC-ITM-EXTD-PR-AMT-X   PIC  Z(10).99.           
W28545*                    25  ASC-ITM-EXTD-PR-AMT     PIC  9(10)V99.           
W28545                     25  ASC-ITM-EXTD-PR-AMT-X   PIC  X(08).              
W28545                     25  ASC-ITM-EXTD-PR-AMT-X-FMT                        
W28545                                             REDEFINES                    
W28545                         ASC-ITM-EXTD-PR-AMT-X   PIC  Z(05).99.           
W28545                     25  ASC-ITM-EXTD-PR-AMT     PIC  9(05)V99.           
                           25  ASC-SKU-ENTERED-SW  PIC  X.                      
                               88 ASC-SKU-ENTERED         VALUE 'Y'.            
                               88 ASC-SKU-NOT-ENTERED     VALUE 'N'.            
W28545                     25  ASC-UPC-ENTERED-SW  PIC  X.                      
W28545                         88 ASC-UPC-ENTERED         VALUE 'Y'.            
W28545                         88 ASC-UPC-NOT-ENTERED     VALUE 'N'.            
                           25  ASC-GET-PRICE-SW    PIC X.                       
                               88  ASC-GET-PRICE-FROM-SCREEN                    
                                                          VALUE 'S'.            
J28545                         88  ASC-GET-PRICE-FROM-INPD400                   
                                                          VALUE 'T'.            
J28545                         88  ASC-GOT-INPD400-PRICE  VALUE 'O'.            
                               88  ASC-DONT-GET-PRICE     VALUE 'N'.            
                                                                                
                   15  ASC-ORIG-ITEM-ARRAY.                                     
                       20  ASC-ORIG-MAP-DETAIL-LINE                             
                                                   OCCURS 25 TIMES.             
                           25  ASC-ORIG-LINE-NBR   PIC S9(04)                   
                                                              COMP SYNC.        
W21732                     25  ASC-ORIG-SKU-NBR    PIC  X(08).                  
                           25  ASC-ORIG-INV-QTY-X  PIC  X(07).                  
                           25  ASC-ORIG-INV-QTY    REDEFINES                    
                               ASC-ORIG-INV-QTY-X  PIC  9(07).                  
                           25  ASC-ORIG-ITM-UNIT-PR-AMT-X                       
                                                   PIC X(07).                   
                           25  ASC-ORIG-ITM-UNIT-PR-AMT-X-FMT                   
                                                   REDEFINES                    
                               ASC-ORIG-ITM-UNIT-PR-AMT-X                       
                                                   PIC Z(4).99.                 
W28545*                    25  ASC-ORIG-ITM-EXTD-PR-AMT-X                       
W28545*                                            PIC  X(13).                  
W28545*                    25  ASC-ORIG-ITM-EXTD-PR-AMT-X-FMT                   
W28545*                                            REDEFINES                    
W28545*                        ASC-ORIG-ITM-EXTD-PR-AMT-X                       
W28545                     25  ASC-ORIG-ITM-EXTD-PR-AMT-X                       
W28545                                             PIC  X(08).                  
W28545                     25  ASC-ORIG-ITM-EXTD-PR-AMT-X-FMT                   
W28545                                             REDEFINES                    
W28545                         ASC-ORIG-ITM-EXTD-PR-AMT-X                       
                                                   PIC  Z(05).99.               
                   15  ASC-TOTAL-FIELDS.                                        
                       20  ASC-TOTAL-RTL-AMT-X         PIC  X(12).              
                       20  ASC-TOTAL-RTL-AMT-X-FMT     REDEFINES                
                           ASC-TOTAL-RTL-AMT-X         PIC  Z(09).99.           
                       20  ASC-TOTAL-RTL-AMT           PIC  9(09)V99.           
                                                                                
                       20  ASC-TOTAL-QTY-AMT-X         PIC  X(08).              
                       20  ASC-TOTAL-QTY-AMT-X-FMT     REDEFINES                
                           ASC-TOTAL-QTY-AMT-X         PIC  Z(07)9.             
                       20  ASC-TOTAL-QTY-AMT           PIC  9(08).              
                                                                                
                       20  ASC-TOTAL-EXTD-RTL-X        PIC  X(12).              
                       20  ASC-TOTAL-EXTD-RTL-X-FMT    REDEFINES                
                           ASC-TOTAL-EXTD-RTL-X        PIC  Z(09).99.           
                       20  ASC-TOTAL-EXTD-RTL          PIC  9(09)V99.           
                                                                                
SR3403         10  FILLER                          PIC  X(287).                 
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    SHEET SELECTION TSQ B QUEUE RECORD LAYOUT.                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY INWS031.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY INKM031.                                                        
                                                                                
       01  MR-OCCURS-LAYOUT          REDEFINES  IN031AO.                        
W28545     05  FILLER                PIC X(90).                                 
           05  MR-DETAIL-LINE OCCURS 25 TIMES.                                  
W21732         10  MR-SKUL           PIC S9(04) COMP.                           
W21732         10  MR-SKUA           PIC  X(01).                                
W21732         10  MR-SKUF           REDEFINES                                  
W21732             MR-SKUA           PIC  X(01).                                
W21732         10  MR-SKUC           PIC  X(01).                                
W21732         10  MR-SKUH           PIC  X(01).                                
W28545*        10  MR-SKU            PIC  X(08).                                
W28545         10  MR-SKU.                                                      
W28545             15                PIC  X(07).                                
W28545             15  MR-SKU-8      PIC  X(08).                                
W28545         10  MR-UPC            REDEFINES                                  
W28545             MR-SKU            PIC  X(15).                                
               10  MR-QTYL           PIC S9(04) COMP.                           
               10  MR-QTYA           PIC  X(01).                                
               10  MR-QTYF           REDEFINES                                  
                   MR-QTYA           PIC  X(01).                                
               10  MR-QTYC           PIC  X(01).                                
               10  MR-QTYH           PIC  X(01).                                
               10  MR-QTY-X.                                                    
SR3403            15  MR-QTY        PIC  Z(2)9.                                 
                   15  MR-QTY-RED REDEFINES                                     
SR3403                 MR-QTY        PIC  9(03).                                
               10  MR-PRCEL          PIC S9(04) COMP.                           
               10  MR-PRCEA          PIC  X(01).                                
               10  MR-PRCEF          REDEFINES                                  
                   MR-PRCEA          PIC  X(01).                                
               10  MR-PRCEC          PIC  X(01).                                
               10  MR-PRCEH          PIC  X(01).                                
               10  MR-PRCE-X.                                                   
SR3403             15  MR-PRCE       PIC  Z(05).99.                             
               10  MR-EXTDL          PIC S9(04) COMP.                           
               10  MR-EXTDA          PIC  X(01).                                
               10  MR-EXTDF          REDEFINES                                  
                   MR-EXTDA          PIC  X(01).                                
               10  MR-EXTDC          PIC  X(01).                                
               10  MR-EXTDH          PIC  X(01).                                
               10  MR-EXTD-X.                                                   
W28545*            15  MR-EXTD       PIC  ZZ,ZZZ,ZZZ.99.                        
W28545             15  MR-EXTD       PIC  ZZZZZ.99.                             
           05  MR-TOTAL-LINES.                                                  
               10  MR-TOTAL-RETAIL.                                             
                   15  MR-TRTLL          PIC S9(04) COMP.                       
                   15  MR-TRTLA          PIC  X(01).                            
                   15  MR-TRTLF          REDEFINES                              
                       MR-TRTLA          PIC  X(01).                            
                   15  MR-TRTLC          PIC  X(01).                            
                   15  MR-TRTLH          PIC  X(01).                            
                   15  MR-TRTL-X.                                               
                       20  MR-TRTL       PIC  Z,ZZZ,ZZZ,ZZZ.99.                 
               10  MR-TOTAL-QTY.                                                
                   15  MR-TQTYL           PIC S9(04) COMP.                      
                   15  MR-TQTYA           PIC  X(01).                           
                   15  MR-TQTYF           REDEFINES                             
                       MR-TQTYA           PIC  X(01).                           
                   15  MR-TQTYC           PIC  X(01).                           
                   15  MR-TQTYH           PIC  X(01).                           
                   15  MR-TQTY-X.                                               
                       20  MR-TQTY        PIC  ZZ,ZZZ,ZZ9.                      
               10  MR-TOTAL-EXTD-RTL.                                           
                   15  MR-TEXTDL         PIC S9(04) COMP.                       
                   15  MR-TEXTDA         PIC  X(01).                            
                   15  MR-TEXTDF         REDEFINES                              
                       MR-TEXTDA         PIC  X(01).                            
                   15  MR-TEXTDC         PIC  X(01).                            
                   15  MR-TEXTDH         PIC  X(01).                            
                   15  MR-TEXTD-X.                                              
                       20  MR-TEXTD      PIC  Z,ZZZ,ZZZ,ZZZ.99.                 
                                                                                
       EJECT                                                                    
J28545     COPY INWS400.                                                        
J28545 EJECT                                                                    
      *    DB2 AREA FOR TINVPAR (INVENTORY PARAMETERS TABLE)                    
                                                                                
           EXEC SQL                                                             
                INCLUDE TINVPAR                                                 
           END-EXEC.                                                            
                                                                                
W28545     EXEC SQL                                                             
W28545          INCLUDE TINCNTL                                                 
W28545     END-EXEC.                                                            
W28545                                                                          
      *    DB2 AREA FOR SHEET INVENTORY TABLE                                   
                                                                                
           EXEC SQL                                                             
                INCLUDE TINENTY                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    CURSOR TO OBTAIN ALL LINES FOR A SHEET INVENTORY SHEET     *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE LINE-NBR-CSR CURSOR FOR                                 
                SELECT LNE_NBR                                                  
W21732               , SKU_NBR                                                  
                     , UPC_NBR                                                  
                     , INV_QTY                                                  
                     , UNT_RTL_AMT                                              
                     , EXTD_RTL_AMT                                             
                FROM   TINENTY                                                  
W21732          WHERE  STR_NBR   = :INENTY-STR-NBR   AND                        
W21732                 SHEET_NBR = :INENTY-SHEET-NBR                            
W28545            AND  SHEET_TYP_CDE = :INENTY-SHEET-TYP-CDE                    
                ORDER BY                                                        
                       LNE_NBR                                                  
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
           MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
           MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
           SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
           MOVE LENGTH OF IN031AI       TO DP030-MAP-LENGTH (1).                
           MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING.                                     
                                                                                
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      *----------------------------------------------------------------*        
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
      *----------------------------------------------------------------*        
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                                       
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IN031AI.                                       
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK TO TRUE                                    
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
1122RM*             'T LINE'         TO DP013-MESSAGE-TEXT (2)                  
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                     TO DP013-MESSAGE-TEXT (3)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE DP020-INT-APPL-FORMAT-IND                               
                                          TO ASC-INT-APPL-FORMAT-IND            
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  DP020-NEXT-ACT-APPL-ERROR                                
                       CONTINUE                                                 
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                       PERFORM 3480-READ-TINVPAR                                
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
W26600             MOVE ASC-KEY-STORE-NBR TO PV-DB2-STORE-NBR                   
                   PERFORM 3480-READ-TINVPAR                                    
W28545******       PERFORM 1100-PROCESS-INTER-APPL-COMM                         
SMJ                IF  PS-INVPAR-PROCESSED                                      
                       PERFORM 2000-PROCESS-PANEL                               
SMJ                END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
W26600             MOVE ASC-KEY-STORE-NBR TO PV-DB2-STORE-NBR                   
                   PERFORM 3480-READ-TINVPAR                                    
SMJ                IF  PS-INVPAR-PROCESSED                                      
W28545******           PERFORM 1100-PROCESS-INTER-APPL-COMM                     
                       IF ASC-DATA-DOES-NOT-EXIST                               
                           PERFORM 4500-SET-ATTRIBUTES-TO-PROT                  
                       ELSE                                                     
                           IF ASC-UPDATE-FMT                                    
                               PERFORM 3000-EDIT-DATA-IN-COMMAREA               
                          ELSE                                                  
                               PERFORM 4500-SET-ATTRIBUTES-TO-PROT              
                               SET DP020-MSG-INFORMATIONAL TO TRUE              
      *                        ---- PRESS F2 TO CONFIRM REQUEST ----            
                               MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER         
                           END-IF                                               
                       END-IF                                                   
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
SMJ                END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
SMJ        IF  PS-UNAVAILABLE-RESOURCE                                          
SMJ           SET DP030-OVERRIDE-APPL-ERROR                                     
SMJ               DP020-MSG-WARNING    TO TRUE                                  
SMJ   *      ---- SYSTEM UNAVAILABLE, TRY AGAIN LATER ---                       
SMJ           MOVE PC-TSYMSG-01063  TO DP020-MSG-NUMBER                         
SMJ           SET DP030-SET-CURSOR-APPL-1    TO TRUE                            
SMJ        END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.               
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           IF IN030-SHEET-INV-COMPLETED                                         
               SET DP020-NEXT-ACT-APPL-ERROR                                    
                   DP020-MSG-FATAL           TO TRUE                            
      *        ---- STORE NOT AVAILABLE FOR SHEET ENTRY ----                    
               MOVE PC-TSYMSG-00560 TO DP020-MSG-NUMBER                         
           ELSE                                                                 
W28545         MOVE IN030-TYPE-CDE      TO ASC-KEY-TYPE-CDE                     
W28545         MOVE IN030-TYPE-CDE-DESC TO ASC-TYPE-CDE-DESC                    
               MOVE IN030-STORE-NAME  TO ASC-STORE-NAME                         
               MOVE IN030-STR-NBR     TO ASC-KEY-STORE-NBR                      
               MOVE IN030-DB2-ACTL-INV-DTE                                      
                                      TO ASC-ACTL-INV-DTE                       
               MOVE ASC-ACTL-INV-DTE-CCYY  TO PV-ACTL-INV-DTE-YYYY              
               MOVE ASC-ACTL-INV-DTE-MM    TO PV-ACTL-INV-DTE-MM                
               MOVE ASC-ACTL-INV-DTE-DD    TO PV-ACTL-INV-DTE-DD                
                                                                                
               IF ASC-CRIT-SPEC-FMT                                             
                   MOVE IN030-SHEET-NBR   TO ASC-KEY-SHEET-NBR                  
W28545             MOVE IN030-TYPE-CDE    TO ASC-KEY-TYPE-CDE                   
               ELSE                                                             
                   IF ASC-LIST-FMT                                              
                       SET ASC-USE-SELECTION-QUEUE TO TRUE                      
                       MOVE IN030-NUMBER-OF-TS-ITEMS                            
                                          TO ASC-NUMBER-OF-TS-ITEMS             
                       MOVE IN030-SEL-QUEUE-NAME                                
                                          TO ASC-SEL-QUEUE-NAME                 
                       PERFORM 1120-PREPARE-LIST-ENTRY                          
                   ELSE                                                         
      *            ---- DATA DOES NOT EXIST ----                                
                       MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                 
                       SET DP020-MSG-FATAL TO TRUE                              
                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
      *----------------------------------------------------------------*        
                                                                                
       1120-PREPARE-LIST-ENTRY.                                                 
                                                                                
           MOVE IN031-MAX-ITEMS TO ASC-SEL-SUB.                                 
           MOVE ZERO            TO ASC-SEL-ITEM                                 
           PERFORM 2140-GET-NEXT-SHEET.                                         
                                                                                
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON THE FUNCTION KEY THAT WAS SELECTED.                      *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   IF ASC-DATA-DOES-NOT-EXIST                                   
                       PERFORM 4500-SET-ATTRIBUTES-TO-PROT                      
                   ELSE                                                         
                       IF ASC-UPDATE-FMT                                        
                           PERFORM 3000-EDIT-DATA-IN-COMMAREA                   
                       ELSE                                                     
                           PERFORM 4500-SET-ATTRIBUTES-TO-PROT                  
                           SET DP020-MSG-INFORMATIONAL TO TRUE                  
      *                    ---- PRESS F2 TO CONFIRM REQUEST ----                
                           MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER             
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2140-GET-NEXT-SHEET                              
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                   ELSE                                                         
      *                ---- BOTTOM OF LIST ----                                 
                       MOVE PC-TSYMSG-00070                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
                   IF  ASC-USE-SELECTION-QUEUE                                  
                       PERFORM 2150-GET-PREV-SHEET                              
                       IF PS-LIST-END                                           
                           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
                           SET PS-NO-LIST-END TO TRUE                           
                       ELSE                                                     
                           PERFORM 4000-BUILD-INITIAL-PANEL                     
                       END-IF                                                   
                   ELSE                                                         
      *                ---- TOP OF LIST ----                                    
                       MOVE PC-TSYMSG-00069                                     
                                        TO DP020-MSG-NUMBER                     
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-CONFIRM (DP020-SRC-AID)                            
                   PERFORM 2170-DELETE-SHEET                                    
                                                                                
               WHEN OTHER                                                       
                   IF ASC-UPDATE-FMT AND ASC-DATA-EXISTS                        
                       PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                     
                           VARYING PS-SUB FROM 1 BY 1                           
                             UNTIL PS-SUB > PC-LINES-PER-PANEL                  
                       PERFORM 3000-EDIT-DATA-IN-COMMAREA                       
                       IF  PS-NO-ERROR                                          
                           PERFORM 2100-CHECK-FUNCTION-KEY                      
                       END-IF                                                   
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   ELSE                                                         
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * DP020-FK-LOCAL-FUNC-03  (UPDATE)                                        
      * DP020-FK-LOCAL-FUNC-04  (DELETE)                                        
      * DP020-FK-LOCAL-FUNC-07  (PAGE FORWARD)                                  
      * DP020-FK-LOCAL-FUNC-08  (PAGE BACK)                                     
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
               WHEN DP020-FK-LOCAL-FUNC-04 (DP020-SRC-AID)                      
               WHEN DP020-FK-LOCAL-FUNC-07 (DP020-SRC-AID)                      
               WHEN DP020-FK-LOCAL-FUNC-08 (DP020-SRC-AID)                      
                AND ASC-UPDATE-FMT                                              
                   IF ASC-DATA-EXISTS                                           
                       IF PS-NO-UPDATES-REQUESTED                               
      *                    ---- DATA NOT CHANGED; NO UPDATE PERFORMED --        
                           MOVE PC-TSYMSG-00046     TO DP020-MSG-NUMBER         
                           SET DP020-MSG-FATAL      TO TRUE                     
                       ELSE                                                     
                           PERFORM 2180-UPDATE-SHEET                            
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM 4500-SET-ATTRIBUTES-TO-PROT                      
      *                ---- DATA DOES NOT EXIST ----                            
                       MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                 
                       SET DP020-MSG-FATAL  TO TRUE                             
                   END-IF                                                       
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                       CONTINUE                                                 
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      *----------------------------------------------------------------*        
      *    GET THE NEXT SHEET IF A LIST OF SHEETS IS BEING PROCESSED.  *        
      *----------------------------------------------------------------*        
                                                                                
       2140-GET-NEXT-SHEET.                                                     
                                                                                
           ADD +1                       TO ASC-SEL-SUB.                         
           IF  ASC-SEL-SUB > IN031-MAX-ITEMS                                    
               ADD +1                   TO ASC-SEL-ITEM                         
               MOVE +1                  TO ASC-SEL-SUB                          
           END-IF.                                                              
           IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
               PERFORM 9100-READ-SEL-QUEUE                                      
               IF  IN031-ITEM (ASC-SEL-SUB) > ZERO                              
                   MOVE IN031-SHEET-NBR (ASC-SEL-SUB)                           
                                         TO ASC-KEY-SHEET-NBR                   
W28545             MOVE IN031-TYPE-CDE (ASC-SEL-SUB)                            
W28545                                   TO ASC-KEY-TYPE-CDE                    
                   SET IN031-INQ (ASC-SEL-SUB) TO TRUE                          
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               ELSE                                                             
                   SUBTRACT +1 FROM ASC-SEL-SUB                                 
      *            ---- BOTTOM OF LIST ----                                     
                   MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
                   SET PS-LIST-END             TO TRUE                          
               END-IF                                                           
           ELSE                                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IN031-MAX-ITEMS     TO ASC-SEL-SUB                          
      *        ---- BOTTOM OF LIST ----                                         
               MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    GET THE PREVIOUS SHEET IF A LIST OF SHEETS IS BEING         *        
      *    PROCESSED.                                                  *        
      *----------------------------------------------------------------*        
                                                                                
       2150-GET-PREV-SHEET.                                                     
                                                                                
           SUBTRACT +1 FROM ASC-SEL-SUB.                                        
           IF  ASC-SEL-SUB < +1                                                 
               SUBTRACT +1 FROM ASC-SEL-ITEM                                    
               MOVE IN031-MAX-ITEMS     TO ASC-SEL-SUB                          
           END-IF.                                                              
                                                                                
           IF  ASC-SEL-ITEM > ZERO                                              
               PERFORM 9100-READ-SEL-QUEUE                                      
               MOVE IN031-SHEET-NBR (ASC-SEL-SUB)                               
                                        TO ASC-KEY-SHEET-NBR                    
W28545         MOVE IN031-TYPE-CDE (ASC-SEL-SUB)                                
W28545                                  TO ASC-KEY-TYPE-CDE                     
           ELSE                                                                 
               MOVE +1                  TO ASC-SEL-ITEM                         
                                           ASC-SEL-SUB                          
      *        ---- TOP OF LIST ----                                            
               MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
               SET PS-LIST-END             TO TRUE                              
           END-IF.                                                              
                                                                                
                                                                                
       2170-DELETE-SHEET.                                                       
                                                                                
           MOVE ASC-KEY-STORE-NBR-X TO PV-DB2-STORE-NBR                         
                                       PV-STR-NBR.                              
           MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR.                        
W28545     MOVE ASC-KEY-TYPE-CDE    TO PV-DB2-TYPE-CDE.                         
           PERFORM 2175-DELETE-TINENTY-SHEET.                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
      *            ---- DELETE SUCCESSFUL ----                                  
                   MOVE PC-TSYMSG-00115         TO DP020-MSG-NUMBER             
                   MOVE ASC-KEY-SHEET-NBR-X     TO DP020-MSG-TEXT               
                   SET  DP020-MSG-INFORMATIONAL TO TRUE                         
                   IF ASC-USE-SELECTION-QUEUE                                   
                       PERFORM 9100-READ-SEL-QUEUE                              
                       SET IN031-DEL (ASC-SEL-SUB) TO TRUE                      
                       PERFORM 9200-REWRITE-SEL-QUEUE                           
                   ELSE                                                         
      *                *------------------------------------------------        
      *                * RETURN THE SPECIFICATION SCREEN WITH THE               
      *                * APPROPRIATE MESSAGE. THIS IS ACCOMPLISHED BY           
      *                * EMULATING THE SETTING OF PF3.                          
      *                *------------------------------------------------        
                       SET DP020-FK-RETURN (DP020-SRC-AID) TO TRUE              
                   END-IF                                                       
               WHEN SQLCODE = +100                                              
      *            ---- DATA DOES NOT EXIST ----                                
                   MOVE PC-TSYMSG-00148         TO DP020-MSG-NUMBER             
                   SET  DP020-MSG-FATAL         TO TRUE                         
                   IF ASC-USE-SELECTION-QUEUE                                   
                       CONTINUE                                                 
                   ELSE                                                         
                       SET DP020-FK-RETURN (DP020-SRC-AID) TO TRUE              
                   END-IF                                                       
               WHEN OTHER                                                       
                   SET DP020-MSG-WARNING TO TRUE                                
      *        ---- WAIT 15 SECONDS, THEN RETRY ----                            
                   MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER                    
           END-EVALUATE.                                                        
                                                                                
           SET ASC-DATA-DOES-NOT-EXIST TO TRUE.                                 
                                                                                
                                                                                
       2175-DELETE-TINENTY-SHEET.                                               
                                                                                
           EXEC SQL                                                             
               DELETE FROM TINENTY                                              
               WHERE  STR_NBR   = :PV-DB2-STORE-NBR AND                         
                      SHEET_NBR = :PV-DB2-SHEET-NBR                             
W28545           AND  SHEET_TYP_CDE = :PV-DB2-TYPE-CDE                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '2175-DELETE-TINENTY-SHEET'                             
                                     TO DP013-PARAGRAPH                         
                   MOVE 'DELETE ALL LINES FOR A SHEET FROM THE SHEET            
      -                 'ENTRY TABLE'      TO DP013-MESSAGE-TEXT(1)             
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TINENTY'    TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2180-UPDATE-SHEET.                                                       
                                                                                
           MOVE ASC-KEY-STORE-NBR-X TO PV-DB2-STORE-NBR                         
                                       PV-STR-NBR.                              
           MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR.                        
W28545     MOVE ASC-KEY-TYPE-CDE    TO PV-DB2-TYPE-CDE.                         
                                                                                
           PERFORM VARYING PS-SUB FROM PC-LINES-PER-PANEL BY -1                 
             UNTIL ASC-LINE-NBR (PS-SUB) NOT = ZERO                             
             OR    PS-SUB < 1                                                   
               CONTINUE                                                         
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING PS-ORIG-SUB FROM PC-LINES-PER-PANEL BY -1            
             UNTIL PS-ORIG-SUB < 1                                              
                OR ASC-ORIG-LINE-NBR (PS-ORIG-SUB) NOT = ZERO                   
               CONTINUE                                                         
           END-PERFORM.                                                         
                                                                                
           PERFORM UNTIL PS-SUB < 1 OR PS-UPDATE-ERROR-FOUND                    
               IF ASC-ORIG-LINE-NBR (PS-ORIG-SUB) =                             
                  ASC-LINE-NBR      (PS-SUB)                                    
                   IF ASC-LINE-CHANGED (PS-SUB)                                 
                       PERFORM 2187-CHANGE-LINE                                 
                   END-IF                                                       
                   SUBTRACT 1 FROM PS-SUB                                       
                                   PS-ORIG-SUB                                  
               ELSE                                                             
                   IF ASC-ORIG-LINE-NBR (PS-ORIG-SUB) >                         
                      ASC-LINE-NBR      (PS-SUB)                                
                       PERFORM 2193-DELETE-LINE                                 
                       SUBTRACT 1 FROM PS-ORIG-SUB                              
                   ELSE                                                         
                       PERFORM 2183-ADD-LINE                                    
                       SUBTRACT 1 FROM PS-SUB                                   
                  END-IF                                                        
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      *    *------------------------------------------------------------        
      *    * IF ANY UNMATCHED LINES REMAIN ON THE ORIGINAL SHEET, THEY          
      *    * ARE TO BE DELETED.                                                 
      *    *------------------------------------------------------------        
                                                                                
           PERFORM VARYING PS-ORIG-SUB FROM PS-ORIG-SUB BY -1                   
             UNTIL PS-ORIG-SUB < 1                                              
             OR    PS-UPDATE-ERROR-FOUND                                        
               PERFORM 2193-DELETE-LINE                                         
               SUBTRACT 1 FROM PS-ORIG-SUB                                      
           END-PERFORM.                                                         
                                                                                
           IF PS-NO-UPDATE-ERROR-FOUND                                          
               PERFORM VARYING PS-SUB FROM 1 BY 1                               
                     UNTIL PS-SUB > PC-LINES-PER-PANEL                          
                   MOVE ASC-LINE-NBR               (PS-SUB)                     
                     TO ASC-ORIG-LINE-NBR          (PS-SUB)                     
W21732             MOVE ASC-SKU-NBR                (PS-SUB)                     
W21732               TO ASC-ORIG-SKU-NBR           (PS-SUB)                     
                   MOVE ASC-INV-QTY-X              (PS-SUB)                     
                     TO ASC-ORIG-INV-QTY-X         (PS-SUB)                     
                   MOVE ASC-ITM-UNIT-PR-AMT-X      (PS-SUB)                     
                     TO ASC-ORIG-ITM-UNIT-PR-AMT-X (PS-SUB)                     
                   MOVE ASC-ITM-EXTD-PR-AMT-X      (PS-SUB)                     
                     TO ASC-ORIG-ITM-EXTD-PR-AMT-X (PS-SUB)                     
               END-PERFORM                                                      
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
      *        ---- UPDATES SUCCESSFUL ----                                     
               MOVE PC-TSYMSG-00059        TO DP020-MSG-NUMBER                  
               IF ASC-USE-SELECTION-QUEUE                                       
                   PERFORM 9100-READ-SEL-QUEUE                                  
                   SET IN031-UPD (ASC-SEL-SUB) TO TRUE                          
                   PERFORM 9200-REWRITE-SEL-QUEUE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       2183-ADD-LINE.                                                           
                                                                                
           MOVE PV-DB2-STORE-NBR              TO INENTY-STR-NBR.                
           MOVE PV-DB2-SHEET-NBR              TO INENTY-SHEET-NBR.              
W28545     MOVE PV-DB2-TYPE-CDE               TO INENTY-SHEET-TYP-CDE.          
           MOVE ASC-LINE-NBR        (PS-SUB)  TO INENTY-LNE-NBR.                
W21732     MOVE ASC-SKU-NBR         (PS-SUB)  TO INENTY-SKU-NBR.                
W28545*    MOVE ZEROES                        TO INENTY-UPC-NBR                 
W28545     IF ASC-UPC-ENTERED       (PS-SUB)                                    
W28545         MOVE ASC-UPC-NBR     (PS-SUB)                                    
W28545                                        TO INENTY-UPC-NBR                 
W28545     ELSE                                                                 
W28545         MOVE ZEROES                    TO INENTY-UPC-NBR                 
W28545     END-IF.                                                              
           MOVE ASC-INV-QTY         (PS-SUB)  TO INENTY-INV-QTY.                
           MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB)  TO INENTY-UNT-RTL-AMT.            
           COMPUTE INENTY-EXTD-RTL-AMT                                          
                 = INENTY-INV-QTY                                               
                 * INENTY-UNT-RTL-AMT.                                          
           MOVE DP020-USERID          TO INENTY-LAST-UPD-BY-USR-ID.             
                                                                                
      *    *-----------------------------------------------------------*        
      *    * INSERT THE ROW INTO THE SHEET INVENTORY TABLE             *        
      *    *-----------------------------------------------------------*        
                                                                                
W21732     MOVE PV-DB2-STORE-NBR      TO INENTY-STR-NBR.                        
W28545     MOVE PV-DB2-TYPE-CDE       TO INENTY-SHEET-TYP-CDE                   
W28545**** MOVE PC-KEYED-SHEET-ENTRY  TO INENTY-SHEET-TYP-CDE                   
W21732     MOVE PV-DB2-SHEET-NBR      TO INENTY-SHEET-NBR                       
W28545*    MOVE PC-UPC-NBR            TO INENTY-UPC-NBR.                        
           EXEC SQL                                                             
               INSERT INTO TINENTY                                              
                    (STR_NBR                                                    
                  ,  SHEET_TYP_CDE                                              
                  ,  SHEET_NBR                                                  
                  ,  LNE_NBR                                                    
W21732            ,  SKU_NBR                                                    
                  ,  UPC_NBR                                                    
                  ,  INV_QTY                                                    
                  ,  UNT_RTL_AMT                                                
                  ,  EXTD_RTL_AMT                                               
                  ,  LAST_UPD_TMST                                              
                  ,  LAST_UPD_BY_USR_ID)                                        
               VALUES                                                           
W21732             (:INENTY-STR-NBR                                             
W21732            , :INENTY-SHEET-TYP-CDE                                       
W21732            , :INENTY-SHEET-NBR                                           
                  , :INENTY-LNE-NBR                                             
W21732            , :INENTY-SKU-NBR                                             
W21732            , :INENTY-UPC-NBR                                             
                  , :INENTY-INV-QTY                                             
                  , :INENTY-UNT-RTL-AMT                                         
                  , :INENTY-EXTD-RTL-AMT                                        
                  , CURRENT TIMESTAMP                                           
                  , :INENTY-LAST-UPD-BY-USR-ID)                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   EXEC CICS                                                    
                       SYNCPOINT                                                
                           ROLLBACK                                             
                   END-EXEC                                                     
                   SET DP020-MSG-WARNING                                        
                       PS-UPDATE-ERROR-FOUND TO TRUE                            
      *            ---- WAIT 15 SECONDS, THEN RETRY ----                        
                   MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '2183-ADD-LINE'                                         
                                     TO DP013-PARAGRAPH                         
                   MOVE 'INSERT A ROW INTO THE SHEET ENTRY TABLE'               
                                     TO DP013-MESSAGE-TEXT(1)                   
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TINENTY'    TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2187-CHANGE-LINE.                                                        
                                                                                
           MOVE ASC-LINE-NBR        (PS-SUB)  TO PV-DB2-LINE-NBR.               
                                                                                
W21732     MOVE ASC-SKU-NBR         (PS-SUB)  TO INENTY-SKU-NBR.                
W28545*    MOVE ZERO                          TO INENTY-UPC-NBR.                
W28545     IF ASC-UPC-ENTERED       (PS-SUB)                                    
W28545         MOVE ASC-UPC-NBR     (PS-SUB)                                    
W28545                                        TO INENTY-UPC-NBR                 
W28545     ELSE                                                                 
W28545         MOVE ZEROES                    TO INENTY-UPC-NBR                 
W28545     END-IF.                                                              
           MOVE ASC-INV-QTY         (PS-SUB)  TO INENTY-INV-QTY.                
           MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB)  TO INENTY-UNT-RTL-AMT.            
           COMPUTE INENTY-EXTD-RTL-AMT                                          
                 = INENTY-INV-QTY                                               
                 * INENTY-UNT-RTL-AMT.                                          
           MOVE DP020-USERID           TO INENTY-LAST-UPD-BY-USR-ID.            
                                                                                
      *    *-----------------------------------------------------------*        
      *    * UPDATE THE ROW IN THE SHEET INVENTORY TABLE               *        
      *    *-----------------------------------------------------------*        
                                                                                
W28545*    MOVE PC-UPC-NBR         TO INENTY-UPC-NBR.                           
W21732     MOVE PV-DB2-STORE-NBR   TO INENTY-STR-NBR.                           
W21732     MOVE PV-DB2-SHEET-NBR   TO INENTY-SHEET-NBR.                         
W28545     MOVE PV-DB2-TYPE-CDE    TO INENTY-SHEET-TYP-CDE.                     
W21732     MOVE PV-DB2-LINE-NBR    TO INENTY-LNE-NBR.                           
           EXEC SQL                                                             
               UPDATE TINENTY                                                   
W21732             SET  SKU_NBR            = :INENTY-SKU-NBR                    
W21732                 ,UPC_NBR            = :INENTY-UPC-NBR                    
                       ,INV_QTY            = :INENTY-INV-QTY                    
                       ,UNT_RTL_AMT        = :INENTY-UNT-RTL-AMT                
                       ,EXTD_RTL_AMT       = :INENTY-EXTD-RTL-AMT               
                       ,LAST_UPD_TMST      = CURRENT TIMESTAMP                  
                       ,LAST_UPD_BY_USR_ID = :INENTY-LAST-UPD-BY-USR-ID         
W21732             WHERE  STR_NBR   = :INENTY-STR-NBR     AND                   
W28545                    SHEET_TYP_CDE = :INENTY-SHEET-TYP-CDE AND             
W21732                    SHEET_NBR = :INENTY-SHEET-NBR   AND                   
W21732                    LNE_NBR   = :INENTY-LNE-NBR                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   EXEC CICS                                                    
                       SYNCPOINT                                                
                           ROLLBACK                                             
                   END-EXEC                                                     
                   SET DP020-MSG-FATAL                                          
                       PS-UPDATE-ERROR-FOUND TO TRUE                            
      *            ---- DATA DOES NOT EXIST ----                                
                   MOVE PC-TSYMSG-00148  TO DP020-MSG-NUMBER                    
W21732             MOVE DP015-UNP-NUM-BRT-OFF  TO MR-SKUA  (PS-SUB)             
                                                  MR-QTYA  (PS-SUB)             
                                                  MR-PRCEA (PS-SUB)             
W21732             MOVE DP015-RED              TO MR-SKUC  (PS-SUB)             
                                                  MR-QTYC  (PS-SUB)             
                                                  MR-PRCEC (PS-SUB)             
W21732             MOVE DP015-REVERSE          TO MR-SKUH  (PS-SUB)             
                                                  MR-QTYH  (PS-SUB)             
                                                  MR-PRCEH (PS-SUB)             
W21732             MOVE -1                     TO MR-SKUL  (PS-SUB)             
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   EXEC CICS                                                    
                       SYNCPOINT                                                
                           ROLLBACK                                             
                   END-EXEC                                                     
                   SET DP020-MSG-WARNING                                        
                       PS-UPDATE-ERROR-FOUND TO TRUE                            
      *            ---- WAIT 15 SECONDS, THEN RETRY ----                        
                   MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '2187-CHANGE-LINE'                                      
                                     TO DP013-PARAGRAPH                         
                   MOVE 'UPDATE A ROW IN THE SHEET ENTRY'                       
                                     TO DP013-MESSAGE-TEXT(1)                   
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TINENTY'    TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2193-DELETE-LINE.                                                        
                                                                                
           MOVE ASC-ORIG-LINE-NBR (PS-ORIG-SUB)  TO PV-DB2-LINE-NBR.            
                                                                                
      *    *-----------------------------------------------------------*        
      *    * DELETE THE ROW FROM THE STOCKROOM WITHDRAWAL TABLE        *        
      *    *-----------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DELETE FROM TINENTY                                              
               WHERE  STR_NBR   = :PV-DB2-STORE-NBR AND                         
W28545                SHEET_TYP_CDE = :PV-DB2-TYPE-CDE AND                      
                      SHEET_NBR = :PV-DB2-SHEET-NBR AND                         
                      LNE_NBR   = :PV-DB2-LINE-NBR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   EXEC CICS                                                    
                       SYNCPOINT                                                
                           ROLLBACK                                             
                   END-EXEC                                                     
                   SET DP020-MSG-WARNING                                        
                       PS-UPDATE-ERROR-FOUND TO TRUE                            
      *            ---- WAIT 15 SECONDS, THEN RETRY ----                        
                   MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER                    
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '2193-DELETE-LINE'                                      
                                     TO DP013-PARAGRAPH                         
                   MOVE 'DELETE A ROW FROM THE STOCKROOM WITHDRAWAL TABL        
      -                 'E'          TO DP013-MESSAGE-TEXT(1)                   
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TINENTY'    TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           IF ASC-SKU-NOT-ENTERED (PS-SUB)                                      
J28545         MOVE SPACE           TO ASC-SKU-NBR         (PS-SUB)             
J28545         MOVE ZERO            TO ASC-INV-QTY         (PS-SUB)             
J28545                                 ASC-ITM-EXTD-PR-AMT (PS-SUB)             
W28545         IF ASC-UPC-NOT-ENTERED (PS-SUB)                                  
W28545             MOVE SPACE       TO ASC-SKU-NBR         (PS-SUB)             
W28545             MOVE ZERO        TO ASC-INV-QTY         (PS-SUB)             
W28545                                 ASC-ITM-EXTD-PR-AMT (PS-SUB)             
W28545         END-IF                                                           
W28545     END-IF.                                                              
                                                                                
                                                                                
W21732     IF  MR-SKUL (PS-SUB) > ZERO                                          
J28545         MOVE MR-SKU (PS-SUB)     TO ASC-SKU-NBR (PS-SUB)                 
J28545         IF ASC-SKU-NBR (PS-SUB) > SPACE                                  
J28545             SET ASC-SKU-ENTERED (PS-SUB) TO TRUE                         
J28545         ELSE                                                             
J28545             SET ASC-SKU-NOT-ENTERED (PS-SUB) TO TRUE                     
J28545         END-IF                                                           
W28545         MOVE 1               TO PV-BYTE-SUB                              
W28545         MOVE ZEROS           TO PV-BYTE-COUNT                            
W28545                                 PV-SKU-POS                               
W28545         MOVE SPACES          TO WS-EDIT-UPC-SKU-TABLE                    
W28545         MOVE MR-SKU (PS-SUB) TO WS-EDIT-UPC-SKU-TABLE                    
W28545         PERFORM 3600-EDIT-UPC-SKU                                        
W28545           VARYING PV-BYTE-SUB FROM 1 BY 1                                
W28545             UNTIL PV-BYTE-SUB IS GREATER THAN 15                         
W28545         INSPECT MR-SKU (PS-SUB) REPLACING LEADING SPACES BY ZEROS        
W28545         INSPECT MR-SKU (PS-SUB) TALLYING PV-SKU-POS FOR LEADING          
W28545             ZEROS                                                        
W28545         ADD 1 TO PV-SKU-POS                                              
W28545         IF PV-BYTE-COUNT IS GREATER THAN 8                               
W28545             MOVE MR-SKU (PS-SUB) (PV-SKU-POS:PV-BYTE-COUNT)              
W28545                                    TO ASC-UPC-NBR-X   (PS-SUB)           
W28545             MOVE ALL '0'           TO ASC-SKU-NBR     (PS-SUB)           
W28545             SET ASC-UPC-ENTERED     (PS-SUB) TO TRUE                     
W28545             SET ASC-SKU-NOT-ENTERED (PS-SUB) TO TRUE                     
W28545         ELSE                                                             
W28545             MOVE MR-SKU (PS-SUB) (PV-SKU-POS:PV-BYTE-COUNT)              
W28545                                    TO ASC-SKU-NBR     (PS-SUB)           
W28545             MOVE ALL '0'           TO ASC-UPC-NBR-X   (PS-SUB)           
W28545             SET ASC-SKU-ENTERED     (PS-SUB) TO TRUE                     
W28545             SET ASC-UPC-NOT-ENTERED (PS-SUB) TO TRUE                     
W28545         END-IF                                                           
W21732     ELSE                                                                 
W21732         IF  MR-SKUF (PS-SUB) = DP015-ERASE-EOF                           
W21732             MOVE SPACE           TO ASC-SKU-NBR (PS-SUB)                 
W28545                                     ASC-UPC-NBR-X   (PS-SUB)             
W21732             SET ASC-SKU-NOT-ENTERED (PS-SUB) TO TRUE                     
W28545             SET ASC-UPC-NOT-ENTERED (PS-SUB) TO TRUE                     
W21732         END-IF                                                           
W21732     END-IF.                                                              
                                                                                
           IF  MR-QTYL (PS-SUB) > ZERO                                          
               MOVE MR-QTY-X (PS-SUB)   TO ASC-INV-QTY-X (PS-SUB)               
           ELSE                                                                 
               IF  MR-QTYF (PS-SUB) = DP015-ERASE-EOF                           
                   MOVE SPACE           TO ASC-INV-QTY-X (PS-SUB)               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  MR-PRCEL (PS-SUB) > ZERO                                         
               MOVE MR-PRCE-X (PS-SUB)                                          
                               TO ASC-ITM-UNIT-PR-AMT-X (PS-SUB)                
           ELSE                                                                 
               IF  MR-PRCEF (PS-SUB) = DP015-ERASE-EOF                          
                   MOVE SPACE  TO ASC-ITM-UNIT-PR-AMT-X (PS-SUB)                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  MR-EXTDL (PS-SUB) > ZERO                                         
               MOVE MR-EXTD-X (PS-SUB)                                          
                               TO ASC-ITM-EXTD-PR-AMT-X (PS-SUB)                
           ELSE                                                                 
               IF  MR-EXTDF (PS-SUB) = DP015-ERASE-EOF                          
                   MOVE SPACE  TO ASC-ITM-EXTD-PR-AMT-X (PS-SUB)                
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
                                                                                
           INITIALIZE PV-INITIAL-TOTALS                                         
                      ASC-TOTAL-FIELDS.                                         
                                                                                
           PERFORM 3220-REMOVE-DELETED-LINES.                                   
                                                                                
           MOVE ASC-KEY-STORE-NBR-X TO PV-DB2-STORE-NBR                         
                                       PV-STR-NBR.                              
           MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR.                        
W28545     MOVE ASC-KEY-TYPE-CDE    TO PV-DB2-TYPE-CDE.                         
                                                                                
           PERFORM VARYING PS-SUB FROM PC-LINES-PER-PANEL BY -1                 
             UNTIL PS-SUB < 1                                                   
                OR ASC-LINE-NBR (PS-SUB) NOT = ZERO                             
               PERFORM 4525-SET-ATTRIBUTES-TO-UNPROT                            
           END-PERFORM.                                                         
                                                                                
           IF PS-SUB < 1                                                        
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL         TO TRUE                              
      *        ---- SHEET MUST CONTAIN AT LEAST ONE LINE ----                   
               MOVE PC-TSYMSG-00614        TO DP020-MSG-NUMBER                  
           ELSE                                                                 
               PERFORM VARYING PS-ORIG-SUB FROM PC-LINES-PER-PANEL BY -1        
                 UNTIL PS-ORIG-SUB < 1                                          
                    OR ASC-ORIG-LINE-NBR (PS-ORIG-SUB) NOT = ZERO               
                   CONTINUE                                                     
               END-PERFORM                                                      
               PERFORM 3230-EDIT-LINES                                          
               IF PS-NO-ERROR                                                   
W21732             MOVE -1 TO MR-SKUL (1)                                       
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * IF THE DEPARTMENT PORTION OF THE SKU ARE EMPTY,                *        
      * REMOVE THE ENTIRE LINE AND MOVE THE SUBSEQUENT LINES UP TO     *        
      * CLOSE THE GAP. ALSO, ASSIGN LINE NUMBERS TO LINES THAT ARE     *        
      * BEING ADDED.                                                   *        
      *----------------------------------------------------------------*        
                                                                                
       3220-REMOVE-DELETED-LINES.                                               
                                                                                
           MOVE +1 TO PS-SUB2.                                                  
           PERFORM VARYING PS-SUB FROM 1 BY 1                                   
                     UNTIL PS-SUB > PC-LINES-PER-PANEL                          
               IF ASC-SKU-ENTERED (PS-SUB)                                      
W28545         OR ASC-UPC-ENTERED (PS-SUB)                                      
                     MOVE ASC-MAP-DETAIL-LINE (PS-SUB)                          
                       TO ASC-MAP-DETAIL-LINE (PS-SUB2)                         
                   IF ASC-LINE-NBR (PS-SUB2) = ZERO                             
                       ADD +1 TO ASC-LAST-LINE-NBR                              
                       MOVE ASC-LAST-LINE-NBR TO ASC-LINE-NBR (PS-SUB2)         
                   END-IF                                                       
                                                                                
                   ADD +1 TO PS-SUB2                                            
               ELSE                                                             
W21732             MOVE SPACE TO ASC-SKU-NBR         (PS-SUB2)                  
W28545                           ASC-UPC-NBR-X       (PS-SUB2)                  
               END-IF                                                           
                                                                                
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING PS-SUB2 FROM PS-SUB2 BY 1                            
             UNTIL PS-SUB2 > PC-LINES-PER-PANEL                                 
               INITIALIZE ASC-MAP-DETAIL-LINE (PS-SUB2)                         
           END-PERFORM.                                                         
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  MATCH THE LINES ON THE ORIGINAL SHEET TO THE LINES ON THE              
      *  UPDATED SHEET TO DETERMINE WHICH LINES ARE BEING ADDED,                
      *  SUBTRACTED, OR CHANGED.  EDIT THE FIELDS ON THE LINES BEING            
      *  ADDED OR CHANGED. SET A SWITCH INDICATING WHETHER OR NOT               
      *  ANY UPDATES WERE MADE TO THE SHEET.                                    
      *-----------------------------------------------------------------        
                                                                                
       3230-EDIT-LINES.                                                         
                                                                                
           PERFORM UNTIL PS-SUB < 1                                             
               PERFORM 4525-SET-ATTRIBUTES-TO-UNPROT                            
               IF ASC-ORIG-LINE-NBR (PS-ORIG-SUB) =                             
                  ASC-LINE-NBR      (PS-SUB)                                    
                   PERFORM 3250-EDIT-MAP-DETAIL-FIELDS                          
W21732             IF  ASC-SKU-NBR                (PS-SUB) =                    
W21732                 ASC-ORIG-SKU-NBR           (PS-ORIG-SUB)                 
                   AND ASC-INV-QTY-X              (PS-SUB) =                    
                       ASC-ORIG-INV-QTY-X         (PS-ORIG-SUB)                 
*                  AND ASC-ITM-UNIT-PR-AMT-X      (PS-SUB) =                    
*                      ASC-ORIG-ITM-UNIT-PR-AMT-X (PS-ORIG-SUB)                 
                       IF ASC-GET-PRICE-FROM-SCREEN      (PS-SUB)               
                           IF ASC-ITM-UNIT-PR-AMT-X      (PS-SUB) =             
                              ASC-ORIG-ITM-UNIT-PR-AMT-X (PS-ORIG-SUB)          
                               SET ASC-LINE-DIDNT-CHANGE (PS-SUB)               
                                                              TO TRUE           
                           ELSE                                                 
                               SET ASC-LINE-CHANGED (PS-SUB)                    
                                   PS-UPDATES-REQUESTED     TO TRUE             
                           END-IF                                               
                       ELSE                                                     
                           SET ASC-LINE-DIDNT-CHANGE (PS-SUB) TO TRUE           
                       END-IF                                                   
                   ELSE                                                         
                       SET ASC-LINE-CHANGED (PS-SUB)                            
                           PS-UPDATES-REQUESTED      TO TRUE                    
                   END-IF                                                       
                   SUBTRACT 1 FROM PS-SUB                                       
                                   PS-ORIG-SUB                                  
               ELSE                                                             
                   IF ASC-ORIG-LINE-NBR (PS-ORIG-SUB) >                         
                      ASC-LINE-NBR      (PS-SUB)                                
                       SET PS-UPDATES-REQUESTED TO TRUE                         
                       SUBTRACT 1 FROM PS-ORIG-SUB                              
                   ELSE                                                         
                       PERFORM 3250-EDIT-MAP-DETAIL-FIELDS                      
                       SET PS-UPDATES-REQUESTED TO TRUE                         
                       SUBTRACT 1 FROM PS-SUB                                   
                  END-IF                                                        
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      *    *------------------------------------------------------------        
      *    * IF ANY UNMATCHED LINES REMAIN ON THE ORIGINAL SHEET, THEY          
      *    * ARE TO BE DELETED. SET THE SWITCH THAT INDICATES THAT              
      *    * UPDATES WERE REQUESTED.                                            
      *    *------------------------------------------------------------        
                                                                                
           IF PS-ORIG-SUB < 1                                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-UPDATES-REQUESTED TO TRUE                                 
           END-IF.                                                              
                                                                                
                                                                                
       3250-EDIT-MAP-DETAIL-FIELDS.                                             
                                                                                
      *    *------------------------------------------------------------        
      *    * EDIT THE QUANTITY                                                  
      *    *------------------------------------------------------------        
                                                                                
           SET PS-NO-ERRORS-IN-LINE                                             
W28545         PS-VALID-UPC                                                     
               PS-VALID-SKU         TO TRUE.                                    
IN2001                                                                          
J28545     INITIALIZE PV-IN400-UPC-STAT-CDE                                     
J28545                PV-IN400-UNT-RTL-AMT                                      
J28545                PV-IN400-MITGPL-QTY.                                      
                                                                                
W28545*    IF ASC-SKU-ENTERED (PS-SUB)   AND                                    
W28545     IF (ASC-SKU-ENTERED (PS-SUB)                                         
W28545     OR  ASC-UPC-ENTERED (PS-SUB)) AND                                    
              ASC-INV-QTY-X (PS-SUB) = SPACE                                    
               SET PS-ERROR                                                     
                   PS-ERRORS-IN-LINE                                            
                   DP020-MSG-FATAL         TO TRUE                              
      *        ---- REQUIRED FIELD ----                                         
               MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER                  
               MOVE DP015-UNP-NUM-BRT-OFF  TO MR-QTYA (PS-SUB)                  
               MOVE DP015-RED              TO MR-QTYC (PS-SUB)                  
               MOVE DP015-REVERSE          TO MR-QTYH (PS-SUB)                  
               MOVE -1                     TO MR-QTYL (PS-SUB)                  
               SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
           ELSE                                                                 
W28545*    IF ASC-SKU-ENTERED (PS-SUB)   AND                                    
W28545     IF (ASC-SKU-ENTERED (PS-SUB)                                         
W28545     OR  ASC-UPC-ENTERED (PS-SUB)) AND                                    
              ASC-INV-QTY-X (PS-SUB) NOT EQUAL SPACE                            
               MOVE ASC-INV-QTY-X (PS-SUB) TO DP010I-UNEDITED-FIELD             
               MOVE LENGTH OF ASC-INV-QTY (PS-SUB)                              
                                           TO DP010I-MAXIMUM-DIGITS             
               MOVE ZERO                   TO DP010I-MAXIMUM-DECIMALS           
               SET  DP010I-NEGATIVE-NOT-ALLOWED                                 
                                           TO TRUE                              
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF DP010I-ERROR-DETECTED                                         
                   SET  PS-ERROR                                                
                        PS-ERRORS-IN-LINE                                       
                        DP020-MSG-FATAL        TO TRUE                          
      *            ---- MUST BE NUMERIC ----                                    
                   MOVE PC-TSYMSG-00008        TO DP020-MSG-NUMBER              
                   MOVE DP015-UNP-NUM-BRT-OFF  TO MR-QTYA (PS-SUB)              
                   MOVE DP015-RED              TO MR-QTYC (PS-SUB)              
                   MOVE DP015-REVERSE          TO MR-QTYH (PS-SUB)              
                   MOVE -1                     TO MR-QTYL (PS-SUB)              
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD   TO ASC-INV-QTY (PS-SUB)          
                   IF ASC-INV-QTY (PS-SUB) = ZERO                               
                       SET  PS-ERROR                                            
                            PS-ERRORS-IN-LINE                                   
                            DP020-MSG-FATAL    TO TRUE                          
      *                ---- CANNOT BE ZERO ----                                 
                       MOVE PC-TSYMSG-00010    TO DP020-MSG-NUMBER              
                       MOVE DP015-UNP-NUM-BRT-OFF                               
                                               TO MR-QTYA (PS-SUB)              
                       MOVE DP015-RED          TO MR-QTYC (PS-SUB)              
                       MOVE DP015-REVERSE      TO MR-QTYH (PS-SUB)              
                       MOVE -1                 TO MR-QTYL (PS-SUB)              
                       SET DP030-SET-CURSOR-APPL-1                              
                                               TO TRUE                          
                   ELSE                                                         
                       IF ASC-INV-QTY (PS-SUB) > PC-MAX-QTY                     
                           SET  PS-ERROR                                        
                                PS-ERRORS-IN-LINE                               
                                DP020-MSG-FATAL    TO TRUE                      
      *                    -- CANNOT EXCEED 9999 UNITS PER LINE --              
                           MOVE PC-TSYMSG-02144    TO DP020-MSG-NUMBER          
                           MOVE DP015-UNP-NUM-BRT-OFF                           
                                                   TO MR-QTYA (PS-SUB)          
                           MOVE DP015-RED          TO MR-QTYC (PS-SUB)          
                           MOVE DP015-REVERSE      TO MR-QTYH (PS-SUB)          
                           MOVE -1                 TO MR-QTYL (PS-SUB)          
                           SET DP030-SET-CURSOR-APPL-1                          
                                                   TO TRUE                      
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
SJ           END-IF                                                             
           END-IF.                                                              
                                                                                
      *    *------------------------------------------------------------        
      *    * EDIT THE SKU OR UPC                                                
      *    *------------------------------------------------------------        
                                                                                
           MOVE ASC-GET-PRICE-SW  (PS-SUB) TO PS-GOT-LAST-PRICE-SW.             
           SET ASC-DONT-GET-PRICE (PS-SUB) TO TRUE.                             
                                                                                
           IF ASC-SKU-ENTERED (PS-SUB)                                          
J28545         PERFORM 3300-EDIT-SKU                                            
               IF PS-VALID-SKU                                                  
J28545             PERFORM 3325-VALIDATE-SKU                                    
               END-IF                                                           
W28545     ELSE                                                                 
J28545         PERFORM 3400-EDIT-UPC                                            
J28545         IF PS-VALID-UPC                                                  
J28545             PERFORM 3425-VALIDATE-UPC                                    
J28545         END-IF                                                           
J28545     END-IF.                                                              
                                                                                
J28545     IF ASC-GET-PRICE-FROM-SCREEN (PS-SUB)                                
J28545         PERFORM 3275-PROCESS-CLEARANCE-ITEM                              
J28545     ELSE                                                                 
J28545         IF ASC-GOT-INPD400-PRICE (PS-SUB)                                
J28545             CONTINUE                                                     
J28545         ELSE                                                             
J28545             IF ASC-GET-PRICE-FROM-INPD400 (PS-SUB)                       
J28545                 IF IN400-PRC-STATUS-OUT =                                
J28545                             PC-CLEARANCE-SKU-STATUS                      
J28545                     PERFORM 3275-PROCESS-CLEARANCE-ITEM                  
J28545                 END-IF                                                   
J28545             END-IF                                                       
J28545         END-IF                                                           
J28545     END-IF.                                                              
J28545     PERFORM 3500-CALC-EXTD-RTL.                                          
J28545                                                                          
      *                                                                         
23511C*------------------------------------------------------------             
J28545* THIS PARAGRAPH WAS MOVED FROM WITHIN 3250-EDIT-MAP-DETAIL-FIELDS        
23511C* TO CHECK THE DATES TO DECIDE IF THE PRICE SHOULD BE DISPLAYED           
23511C* OR THE USER SHOULD ENTER THE PRICE                                      
W24521* DC AND RTV LOCATION ARE GIVEN THE ABILITY TO UPDATE THE RETAIL          
W24521* AMOUNT FOR 'YELLOW-TICKET' (INITIAL MARKDOWN) CLEARANCE ITEMS           
W24521*------------------------------------------------------------             
      *                                                                         
J28545 3265-PROCESS-CLEARANCE-DATES.                                            
J28545      IF IN400-PRC-UPC-CHG-DTE-OUT = IN400-PRC-STAT-CHG-DTE-OUT           
W24521*       FOR DC OR RTV LOCATION TYPES (INITIAL MARKDOWNS)                  
W24521        IF IN030-STORE-TYPE-CODE = (PC-DC OR PC-RTV)                      
W24521            SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB) TO TRUE                
W24521            IF ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB) NOT EQUAL SPACES        
W24521               IF ASC-ITM-UNIT-PR-AMT (PS-SUB) NOT EQUAL                  
J28545                                          IN400-PRC-UNIT-RTL-OUT          
J28545                  MOVE IN400-PRC-STATUS-OUT TO                            
J28545                                            PV-IN400-UPC-STAT-CDE         
W24521                  MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB) TO                    
J28545                                            PV-IN400-UNT-RTL-AMT          
J28545                             ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)           
W24521                                            PV-ITEM-RETAIL                
J28545                  MOVE IN400-PRC-MITGPL-QTY TO                            
J28545                        PV-IN400-MITGPL-QTY                               
W24521               END-IF                                                     
W24521            ELSE                                                          
J28545                MOVE IN400-PRC-STATUS-OUT TO                              
J28545                        PV-IN400-UPC-STAT-CDE                             
J28545                MOVE IN400-PRC-UNIT-RTL-OUT                               
J28545                    TO PV-IN400-UNT-RTL-AMT                               
W24521                       PV-ITEM-RETAIL                                     
W24521                       ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)                 
W24521                       ASC-ITM-UNIT-PR-AMT       (PS-SUB)                 
J28545                MOVE IN400-PRC-MITGPL-QTY TO PV-IN400-MITGPL-QTY          
W24521            END-IF                                                        
W24521*           ALLOW 'YELLOW-TICKET' CLEARANCE RETAIL TO BE UPDATED          
W24521            MOVE DP015-UNP-NUM-NOR-OFF    TO MR-PRCEA (PS-SUB)            
W24521            MOVE DP015-GREEN              TO MR-PRCEC (PS-SUB)            
W24521            MOVE DP015-UNDERLINE          TO MR-PRCEH (PS-SUB)            
W24521        ELSE                                                              
J28545*         IN030-STORE-TYPE-CODE <> (PC-DC OR PC-RTV)                      
J28545          IF PS-GOT-LAST-PRICE-FROM-SCREEN                                
J28545              SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB) TO TRUE              
J28545          ELSE                                                            
J28545              MOVE IN400-PRC-STATUS-OUT TO PV-IN400-UPC-STAT-CDE          
J28545              MOVE IN400-PRC-UNIT-RTL-OUT                                 
J28545                  TO PV-IN400-UNT-RTL-AMT                                 
23511C                     PV-ITEM-RETAIL                                       
23511C                     ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)                   
23511C                     ASC-ITM-UNIT-PR-AMT   (PS-SUB)                       
J28545              MOVE IN400-PRC-MITGPL-QTY TO PV-IN400-MITGPL-QTY            
J28545          END-IF                                                          
W24521        END-IF                                                            
23511C      ELSE                                                                
J28545*         RED TICKET ITEM - SCREEN ENTRY OF PRICE REQUIRED                
23511C          SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB) TO TRUE                  
23511C      END-IF.                                                             
23511C                                                                          
IN2001*------------------------------------------------------------             
J28545* THIS PARAGRAPH WAS MOVED FROM WITHIN 3250-EDIT-MAP-DETAIL-FIELDS        
IN2001* TO ACCOUNT FOR RE-ANALYSIS OF CLEARANCE ITEMS (IF STR000 ROW WAS        
IN2001* FOUND TO BE A MIXED ('25') STATUS).                                     
IN2001* THIS WILL BE EXECUTED AT LEAST ONCE FROM THE INITIAL REVIEW OF          
IN2001* SWITCH THAT WAS SET FROM THE STR000 ROW. IF THE STORE-SPECIFIC          
IN2001* ROW IS CALLED AND FOUND TO BE A CLEARANCE STATUS, THIS MUST BE          
IN2001* RE-EVALUATED.                                                           
IN2001*------------------------------------------------------------             
J28545 3275-PROCESS-CLEARANCE-ITEM.                                             
                                                                                
           IF ASC-ITM-UNIT-PR-AMT-X (PS-SUB) = SPACE                            
               IF PS-NO-ERRORS-IN-LINE                                          
                   SET DP020-MSG-FATAL         TO TRUE                          
      *            ---- REQUIRED FIELD ----                                     
                   MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER              
                   MOVE -1                     TO MR-PRCEL (PS-SUB)             
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               END-IF                                                           
               SET PS-ERROR                    TO TRUE                          
               MOVE DP015-UNP-NUM-BRT-OFF      TO MR-PRCEA (PS-SUB)             
               MOVE DP015-RED                  TO MR-PRCEC (PS-SUB)             
               MOVE DP015-REVERSE              TO MR-PRCEH (PS-SUB)             
      *    EDIT THE CLEARANCE PRICE ENTERED ON THE SCREEN                       
           ELSE                                                                 
               MOVE ASC-ITM-UNIT-PR-AMT-X (PS-SUB)                              
                                          TO DP010I-UNEDITED-FIELD              
               MOVE PC-ITM-UNIT-PR-MAX-DIGITS                                   
                                          TO DP010I-MAXIMUM-DIGITS              
               MOVE 2                     TO DP010I-MAXIMUM-DECIMALS            
               SET  DP010I-NEGATIVE-NOT-ALLOWED                                 
                                          TO TRUE                               
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF DP010I-ERROR-DETECTED                                         
                   IF PS-NO-ERRORS-IN-LINE                                      
                       SET DP020-MSG-FATAL      TO TRUE                         
J28545*                ---- MUST BE NUMERIC, NON-NEGATIVE, ----                 
      *                ---- AND WITHIN SIZE CONSTRAINT      ----                
                       MOVE PC-TSYMSG-00343     TO DP020-MSG-NUMBER             
                       MOVE -1                  TO MR-PRCEL (PS-SUB)            
                       SET DP030-SET-CURSOR-APPL-1                              
                                                TO TRUE                         
                   END-IF                                                       
                   SET  PS-ERROR                TO TRUE                         
                   MOVE DP015-UNP-NUM-BRT-OFF   TO MR-PRCEA (PS-SUB)            
                   MOVE DP015-RED               TO MR-PRCEC (PS-SUB)            
                   MOVE DP015-REVERSE           TO MR-PRCEH (PS-SUB)            
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD                                    
                               TO ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)            
                                  ASC-ITM-UNIT-PR-AMT       (PS-SUB)            
                   IF DP010I-NUMERIC-FIELD  = ZERO                              
                       IF PS-NO-ERRORS-IN-LINE                                  
                           SET  DP020-MSG-FATAL TO TRUE                         
      **                   ---- CANNOT BE ZERO ----                             
                           MOVE PC-TSYMSG-00010 TO DP020-MSG-NUMBER             
                           MOVE -1              TO MR-PRCEL (PS-SUB)            
                           SET DP030-SET-CURSOR-APPL-1                          
                                                TO TRUE                         
                       END-IF                                                   
                       SET  PS-ERROR            TO TRUE                         
                       MOVE DP015-UNP-NUM-BRT-OFF                               
                                                TO MR-PRCEA (PS-SUB)            
                       MOVE DP015-RED           TO MR-PRCEC (PS-SUB)            
                       MOVE DP015-REVERSE       TO MR-PRCEH (PS-SUB)            
                   ELSE                                                         
                       IF ASC-ITM-UNIT-PR-AMT (PS-SUB)                          
                                                      < PC-MIN-PRICE            
                       OR ASC-ITM-UNIT-PR-AMT (PS-SUB)                          
                                                      > PC-MAX-PRICE            
                           IF PS-NO-ERRORS-IN-LINE                              
                               SET  DP020-MSG-FATAL TO TRUE                     
      *                        -- UNIT PRICE MUST BE BETWEEN .01 --             
      *                        -- AND 99999.99                   --             
                               MOVE PC-TSYMSG-02145                             
                                                TO DP020-MSG-NUMBER             
                               MOVE -1          TO MR-PRCEL (PS-SUB)            
                               SET DP030-SET-CURSOR-APPL-1                      
                                                TO TRUE                         
                           END-IF                                               
                           SET  PS-ERROR            TO TRUE                     
                           MOVE DP015-UNP-NUM-BRT-OFF                           
                                                TO MR-PRCEA (PS-SUB)            
                           MOVE DP015-RED       TO MR-PRCEC (PS-SUB)            
                           MOVE DP015-REVERSE   TO MR-PRCEH (PS-SUB)            
                       ELSE                                                     
                           MOVE DP015-UNP-NUM-NOR-OFF                           
                                                TO MR-PRCEA (PS-SUB)            
                           MOVE DP015-GREEN     TO MR-PRCEC (PS-SUB)            
                           MOVE DP015-UNDERLINE TO MR-PRCEH (PS-SUB)            
J28545                     MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB) TO                 
J28545                          PV-ITEM-RETAIL                                  
J28545                          ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)              
J28545                          ASC-ITM-UNIT-PR-AMT      (PS-SUB)               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
J28545 3300-EDIT-SKU.                                                           
J28545                                                                          
J28545*    *------------------------------------------------------------        
J28545*    * EDIT THE SKU NUMBER.                                               
J28545*    *------------------------------------------------------------        
J28545                                                                          
J28545     IF ASC-SKU-NBR (PS-SUB) > SPACE                                      
J28545         MOVE ASC-SKU-NBR (PS-SUB) TO DP010I-UNEDITED-FIELD               
J28545         MOVE LENGTH OF ASC-SKU-NBR (PS-SUB)                              
J28545                                   TO DP010I-MAXIMUM-DIGITS               
J28545         MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
J28545         SET  DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                         
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
J28545                                                                          
J28545         IF DP010I-ERROR-DETECTED                                         
J28545                 SET  PS-ERROR                                            
J28545                      PS-INVALID-SKU                                      
J28545                      DP020-MSG-FATAL        TO TRUE                      
J28545*                ---- MUST BE NUMERIC ----                                
J28545                 MOVE PC-TSYMSG-00008        TO DP020-MSG-NUMBER          
J28545                 MOVE DP015-UNP-NUM-BRT-OFF  TO MR-SKUA  (PS-SUB)         
J28545                 MOVE DP015-RED              TO MR-SKUC  (PS-SUB)         
J28545                 MOVE DP015-REVERSE          TO MR-SKUH  (PS-SUB)         
J28545                 MOVE -1                     TO MR-SKUL  (PS-SUB)         
J28545                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
J28545         ELSE                                                             
J28545             MOVE DP010I-NUM-FIELD-11-DIG-6-DEC                           
J28545                                     TO ASC-SKU-NBR-N (PS-SUB)            
J28545         END-IF                                                           
J28545     END-IF.                                                              
      *-----------------------------------------------------------------        
      * IF THE SKU HAS A SEQUENCE NUMBER OF '000' OR '999', VALIDATE   *        
      * THE DEPARTMENT AND CLASS AGAINST TMDSEAR.  IF NOT, LOOK UP     *        
W21732* SKU ON TSKXREF. IF IT IS NOT FOUND, IT IS INVALID. IF THE SKU  *        
      * WAS RENUMBERED ON OR BEFORE THE ACTUAL INVENTORY DATE, THE     *        
      * RENUMBERED SKU IS OBTAINED SO THAT ITS ITEM NUMBER CAN BE USED *        
      * LATER TO RETRIEVE THE CUTOFF PRICE.                            *        
      *----------------------------------------------------------------*        
                                                                                
J28545 3325-VALIDATE-SKU.                                                       
J28545                                                                          
J28545     MOVE ASC-SKU-NBR (PS-SUB)      TO IN400-PV-SKU-IN                    
J28545                                       PV-DUMMY-SKU.                      
J28545     MOVE INVPAR-PLND-INV-DTE       TO IN400-PV-EFF-DTE-IN                
J28545     MOVE INVPAR-LOC-NBR            TO IN400-PV-LOC-IN-NUM                
J28545     SET IN400-PV-SKU-LOOKUP        TO TRUE                               
J28545     PERFORM IN400-LOOKUP-RETAIL.                                         
J28545                                                                          
J28545     IF IN400-PRC-SKU-OUT > 0                                             
J28545         MOVE IN400-PRC-SKU-OUT     TO ASC-SKU-NBR(PS-SUB)                
J28545     END-IF.                                                              
J28545                                                                          
J28545     MOVE IN400-PRC-SKU-OUT         TO PV-DUMMY-SKU                       
J28545     IF IN400-PV-DUMMY-SKU                                                
J28545         IF PV-DUMMY-SKU (6:3) = '000' OR '999'                           
J28545             EVALUATE TRUE                                                
J28545               WHEN IN400-PV-FOUND                                        
J28545                   SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB) TO TRUE         
J28545**                 MOVE ZEROS              TO PV-ITEM-RETAIL              
J28545               WHEN IN400-PV-NOT-FOUND                                    
J28545                                                                          
J28545                   SET PS-ERROR                                           
J28545                       PS-ERRORS-IN-LINE                                  
J28545                       DP020-MSG-FATAL     TO TRUE                        
J28545*   * ---- INVALID ENTRY ----                                             
J28545                   MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER            
J28545                   MOVE DP015-UNP-NUM-BRT-OFF TO MR-SKUA (PS-SUB)         
J28545                   MOVE DP015-RED          TO MR-SKUC  (PS-SUB)           
J28545                   MOVE DP015-REVERSE      TO MR-SKUH  (PS-SUB)           
J28545                   MOVE -1                 TO MR-SKUL  (PS-SUB)           
J28545                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                    
                     WHEN OTHER                                                 
                         SET PS-ERROR                                           
                            DP020-MSG-WARNING TO TRUE                           
      *     ---- WAIT 15 SECONDS, THEN RETRY ----                               
                         MOVE PC-TSYMSG-00551 TO DP020-MSG-NUMBER               
                   END-EVALUATE                                                 
J28545         END-IF                                                           
J28545     END-IF.                                                              
J28545                                                                          
J28545     IF NOT IN400-PV-DUMMY-SKU                                            
J28545         EVALUATE TRUE                                                    
J28545                                                                          
J28545             WHEN IN400-PV-FOUND                                          
J28545                 IF IN400-PRC-STATUS-OUT = PC-CLEARANCE-SKU-STATUS        
J28545                     PERFORM 3265-PROCESS-CLEARANCE-DATES                 
J28545                 ELSE                                                     
J28545                     SET ASC-GET-PRICE-FROM-INPD400 (PS-SUB)              
J28545                                  TO TRUE                                 
J28545                     MOVE IN400-PRC-SKU-OUT TO                            
J28545                                  ASC-SKU-NBR(PS-SUB)                     
J28545                                  PV-DB2-SKU-SEQ-NBR                      
J28545                     MOVE IN400-PRC-STATUS-OUT TO                         
J28545                            PV-IN400-UPC-STAT-CDE                         
J28545                     MOVE IN400-PRC-UNIT-RTL-OUT  TO                      
J28545                            PV-IN400-UNT-RTL-AMT                          
J28545                            PV-ITEM-RETAIL                                
J28545                            ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)            
J28545                            ASC-ITM-UNIT-PR-AMT   (PS-SUB)                
J28545                     MOVE IN400-PRC-MITGPL-QTY TO                         
J28545                            PV-IN400-MITGPL-QTY                           
J28545                     PERFORM 3359-CHECK-GROUP-PRICING                     
J28545                 END-IF                                                   
---->                  IF ASC-ITM-UNIT-PR-AMT-X (PS-SUB) = SPACES               
IN2001                     CONTINUE                                             
                       ELSE                                                     
      *IF ORIG AND ASC RETAIL NOT SAME (USE SCREEN OVERRIDE)                    
                           IF (ASC-ITM-UNIT-PR-AMT-X (PS-SUB)                   
                                 EQUAL                                          
                               ASC-ORIG-ITM-UNIT-PR-AMT-X (PS-SUB))             
                               SET ASC-DONT-GET-PRICE (PS-SUB)   TO TRUE        
                           ELSE                                                 
                               SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB)           
                                                                 TO TRUE        
                           END-IF                                               
                       END-IF                                                   
J28545                                                                          
J28545             WHEN IN400-PV-NOT-FOUND                                      
J28545*   *    --- SKU NOT FOUND                                                
                       SET PS-INVALID-SKU TO TRUE                               
                       SET PS-ERROR                                             
                           PS-ERRORS-IN-LINE                                    
                           DP020-MSG-FATAL         TO TRUE                      
J28545*   *    ---- INVALID ENTRY ----                                          
                       MOVE PC-TSYMSG-00005        TO DP020-MSG-NUMBER          
W21732                 MOVE DP015-UNP-NUM-BRT-OFF  TO MR-SKUA  (PS-SUB)         
W21732                 MOVE DP015-RED              TO MR-SKUC  (PS-SUB)         
W21732                 MOVE DP015-REVERSE          TO MR-SKUH  (PS-SUB)         
W21732                 MOVE -1                     TO MR-SKUL  (PS-SUB)         
                       SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
                   WHEN OTHER                                                   
                         SET  PS-ERROR                                          
                              DP020-MSG-WARNING TO TRUE                         
      *                ---- WAIT 15 SECONDS, THEN RETRY ----                    
                       MOVE PC-TSYMSG-00551 TO DP020-MSG-NUMBER                 
J28545                                                                          
               END-EVALUATE                                                     
           END-IF.                                                              
J28545                                                                          
           IF PS-INVALID-SKU                                                    
               SET PS-ERROR                                                     
                   PS-ERRORS-IN-LINE                                            
                   DP020-MSG-FATAL         TO TRUE                              
      *        ---- INVALID ENTRY ----                                          
               MOVE PC-TSYMSG-00005        TO DP020-MSG-NUMBER                  
W21732         MOVE DP015-UNP-NUM-BRT-OFF  TO MR-SKUA  (PS-SUB)                 
W21732         MOVE DP015-RED              TO MR-SKUC  (PS-SUB)                 
W21732         MOVE DP015-REVERSE          TO MR-SKUH  (PS-SUB)                 
W21732         MOVE -1                     TO MR-SKUL  (PS-SUB)                 
               SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
           END-IF.                                                              
                                                                                
       3359-CHECK-GROUP-PRICING.                                                
J28545*                                                                         
J28545     IF IN400-PRC-MITGPL-QTY NOT EQUAL 0                                  
J28545     AND IN400-PRC-GP-AMT > ZERO                                          
J28545         COMPUTE ASC-ITM-UNIT-PR-AMT (PS-SUB) = IN400-PRC-GP-AMT          
J28545                               / IN400-PRC-MITGPL-QTY                     
J28545         MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB)                                
                   TO ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)                        
J28545                PV-ITEM-RETAIL                                            
           END-IF.                                                              
                                                                                
       3380-PROCESS-RENUMBERED-SKU.                                             
                                                                                
           PERFORM 3385-FMT-STCHNG-DTE-FOR-CMPARE.                              
                                                                                
           IF PV-ACTL-INV-DTE-YYYYMMDD < PV-STAT-CHNGD-DATE-YYYYMMDD            
J28545         SET ASC-GET-PRICE-FROM-INPD400 (PS-SUB) TO TRUE                  
J28545         MOVE IN400-PRC-ITM-NBR-OUT TO  PV-DB2-ITEM-NMBR                  
           END-IF.                                                              
                                                                                
                                                                                
       3385-FMT-STCHNG-DTE-FOR-CMPARE.                                          
                                                                                
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
      *    MOVE DP017-CURRENT-DATE-YMD      TO DPG51-SYSTEM-DATE.               
           SET  DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                            
           SET  DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                            
           MOVE ZERO                      TO DPG51-INCR-DECR-DAYS-9             
                                             DPG51-INCR-DECR-BUS-DAYS-9.        
           SET  DPG52-LK-DTE-GREG           TO TRUE.                            
J28545     MOVE IN400-PRC-UPC-CHG-DTE-OUT TO PV-DATE.                           
W21732     MOVE PV-DATE(3:2)            TO PV-YYMMDD-YY.                        
W21732     MOVE PV-DATE(6:2)            TO PV-YYMMDD-MM.                        
W21732     MOVE PV-DATE(9:2)            TO PV-YYMMDD-DD.                        
           MOVE PV-YYMMDD-MMDD          TO PV-MMDDYY-MMDD.                      
           MOVE PV-YYMMDD-YY            TO PV-MMDDYY-YY.                        
           MOVE PV-MMDDYY-DATE          TO DPG52-LK-DATE-INPUT.                 
                                                                                
           CALL PC-CALENDAR-ROUTINE USING DPG51                                 
                                          DPG52                                 
                                          DPG53                                 
                                          DPG54                                 
                                          DPG55                                 
                                          DPG56.                                
           EVALUATE TRUE                                                        
           WHEN DPG54-SEVERE-ERROR                                              
           WHEN DPG54-DATE-INVALID                                              
               MOVE '3385-FMT-STCHNG-DTE-FOR-CMPARE'                            
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ERROR DETECTED BY CALENDAR ROUTINE:'                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE DPG54-ERROR-MESSAGE                                         
                                     TO  DP013-MESSAGE-TEXT (2)                 
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG55-DB2-ISO-DATE TO PV-STAT-CHNGD-DATE-YYYYMMDD.              
                                                                                
J28545 3400-EDIT-UPC.                                                           
J28545*        ---- UPC ENTERED ----                                            
J28545     MOVE ASC-UPC-NBR-X (PS-SUB)                                          
J28545                                TO DP010I-UNEDITED-FIELD                  
J28545     MOVE LENGTH OF ASC-UPC-NBR-X (PS-SUB)                                
J28545                                TO DP010I-MAXIMUM-DIGITS                  
J28545     MOVE ZERO                      TO DP010I-MAXIMUM-DECIMALS            
J28545     SET      DP010I-NEGATIVE-NOT-ALLOWED                                 
J28545                                TO TRUE                                   
263126     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263126          USING DP010I-NUMERIC-EDIT-AREA                                  
J28545                                                                          
J28545     IF DP010I-ERROR-DETECTED                                             
J28545         SET      PS-ERROR                                                
J28545              PS-INVALID-UPC                                              
J28545              DP020-MSG-FATAL            TO TRUE                          
J28545*        ---- MUST BE NUMERIC ----                                        
J28545         MOVE PC-TSYMSG-00008            TO DP020-MSG-NUMBER              
J28545         MOVE DP015-UNP-NUM-BRT-OFF      TO MR-SKUA (PS-SUB)              
J28545         MOVE DP015-RED                  TO MR-SKUC (PS-SUB)              
J28545         MOVE DP015-REVERSE              TO MR-SKUH (PS-SUB)              
J28545         MOVE -1                         TO MR-SKUL (PS-SUB)              
J28545         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
J28545     ELSE                                                                 
J28545         MOVE DP010I-NUM-FIELD-15-DIG-2-DEC                               
J28545                                   TO ASC-UPC-NBR (PS-SUB)                
J28545         IF ASC-UPC-NBR (PS-SUB) = ZERO                                   
J28545             SET      PS-ERROR                                            
J28545                  DP020-MSG-FATAL        TO TRUE                          
J28545*            ---- CANNOT BE ZERO ----                                     
J28545             MOVE PC-TSYMSG-00010        TO DP020-MSG-NUMBER              
J28545             MOVE DP015-UNP-NUM-BRT-OFF                                   
J28545                                     TO MR-SKUA (PS-SUB)                  
J28545             MOVE DP015-RED              TO MR-SKUC (PS-SUB)              
J28545             MOVE DP015-REVERSE          TO MR-SKUH (PS-SUB)              
J28545             MOVE -1                     TO MR-SKUL (PS-SUB)              
J28545             SET DP030-SET-CURSOR-APPL-1                                  
J28545                                     TO TRUE                              
J28545         ELSE                                                             
J28545             PERFORM 3425-VALIDATE-UPC                                    
J28545         END-IF                                                           
J28545     END-IF.                                                              
J28545*                                                                         
J28545*----------------------------------------------------------------*        
J28545* FOR ALL UPC'S, NO LONGER VALIDATE THE CHECK DIGIT.                      
J28545* IF THE UPC IS AN INTERNAL UPC WITH A SEQUENCE NUMBER OF '000'  *        
J28545* OR '999' IN THE EMBEDDED SKU, VALIDATE THE DEPARTMENT AND               
J28545* CLASS AGAINST TMDSEAR. IF NOT, CONVERT THE UPC TO A SKU.                
J28545*----------------------------------------------------------------*        
J28545                                                                          
J28545                                                                          
J28545                                                                          
J28545 3425-VALIDATE-UPC.                                                       
J28545                                                                          
J28545     MOVE ASC-UPC-NBR-X (PS-SUB)    TO IN400-PV-UPC-NBR-IN                
J28545     MOVE INVPAR-PLND-INV-DTE       TO IN400-PV-EFF-DTE-IN                
J28545     MOVE INVPAR-LOC-NBR            TO IN400-PV-LOC-IN-NUM                
J28545     SET IN400-PV-UPC-NBR-LOOKUP        TO TRUE                           
J28545     PERFORM IN400-LOOKUP-RETAIL.                                         
J28545     MOVE IN400-PRC-SKU-OUT         TO PV-DUMMY-SKU                       
J28545                                                                          
J28545     IF ASC-UPC-INTERNAL    (PS-SUB) AND                                  
W28582*        PV-DUMMY-SKU (6:3) = '000' OR '999'                              
W28582         IN400-PV-DUMMY-UPC                                               
J28545         EVALUATE TRUE                                                    
J28545             WHEN IN400-PV-FOUND                                          
J28545                 SET ASC-GET-PRICE-FROM-SCREEN (PS-SUB)                   
J28545                                                           TO TRUE        
W28582                 MOVE IN400-ATT-SKU-OUT TO                                
W28582                                  ASC-SKU-NBR (PS-SUB)                    
W28582                 MOVE IN400-ATT-ITM-NBR-OUT TO PV-DB2-ITEM-NMBR           
J28545             WHEN IN400-PV-NOT-FOUND                                      
J28545                 SET PS-ERROR                                             
J28545                     PS-ERRORS-IN-LINE                                    
J28545                     DP020-MSG-FATAL                                      
J28545                     DP030-SET-CURSOR-APPL-1 TO TRUE                      
J28545*                     ---- INVALID ENTRY ----                             
J28545                 MOVE PC-TSYMSG-00005                                     
J28545                                             TO DP020-MSG-NUMBER          
J28545                 MOVE DP015-UNP-NUM-BRT-OFF                               
J28545                                             TO MR-SKUA (PS-SUB)          
J28545                 MOVE DP015-RED                                           
J28545                                             TO MR-SKUC (PS-SUB)          
J28545                 MOVE DP015-REVERSE                                       
J28545                                             TO MR-SKUH (PS-SUB)          
J28545                 MOVE -1                     TO MR-SKUL (PS-SUB)          
J28545             WHEN OTHER                                                   
J28545                 SET  PS-ERROR                                            
J28545                      DP020-MSG-WARNING TO TRUE                           
J28545*                     ---- WAIT 15 SECONDS, THEN RETRY ----               
J28545                 MOVE PC-TSYMSG-00551                                     
J28545                                          TO DP020-MSG-NUMBER             
J28545         END-EVALUATE                                                     
J28545     ELSE                                                                 
J28545         MOVE ASC-UPC-NBR (PS-SUB) TO PV-DB2-UPC-NBR                      
J28545         EVALUATE TRUE                                                    
J28545             WHEN IN400-PV-FOUND                                          
J28545                 IF IN400-PRC-STATUS-OUT = PC-CLEARANCE-SKU-STATUS        
J28545                     PERFORM 3265-PROCESS-CLEARANCE-DATES                 
J28545                 ELSE                                                     
J28545                     SET ASC-GET-PRICE-FROM-INPD400 (PS-SUB)              
J28545                            TO TRUE                                       
J28545                     MOVE IN400-PRC-STATUS-OUT TO                         
J28545                            PV-IN400-UPC-STAT-CDE                         
J28545                     MOVE IN400-PRC-UNIT-RTL-OUT TO                       
J28545                            PV-IN400-UNT-RTL-AMT                          
J28545                            PV-ITEM-RETAIL                                
J28545                            ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)            
J28545                            ASC-ITM-UNIT-PR-AMT (PS-SUB)                  
J28545                     MOVE IN400-PRC-MITGPL-QTY                            
J28545                            TO  PV-IN400-MITGPL-QTY                       
J28545                     PERFORM 3359-CHECK-GROUP-PRICING                     
J28545                 END-IF                                                   
J28545                                                                          
J28545                 MOVE IN400-PRC-SKU-OUT TO                                
J28545                                  ASC-SKU-NBR (PS-SUB)                    
J28545                 MOVE IN400-PRC-ITM-NBR-OUT TO PV-DB2-ITEM-NMBR           
J28545               WHEN IN400-PV-NOT-FOUND                                    
J28545                   SET   PS-ERROR                                         
J28545                        PS-ERRORS-IN-LINE                                 
J28545                        DP020-MSG-FATAL         TO TRUE                   
J28545*                  ---- INVALID ENTRY ----                                
J28545                   MOVE PC-TSYMSG-00005                                   
J28545                                      TO DP020-MSG-NUMBER                 
J28545                   MOVE DP015-UNP-NUM-BRT-OFF                             
J28545                                      TO MR-SKUA (PS-SUB)                 
J28545                   MOVE DP015-RED   TO MR-SKUC (PS-SUB)                   
J28545                   MOVE DP015-REVERSE                                     
J28545                                      TO MR-SKUH (PS-SUB)                 
J28545                   MOVE -1          TO MR-SKUL (PS-SUB)                   
J28545                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                    
J28545               WHEN OTHER                                                 
J28545                   SET   PS-ERROR                                         
J28545                   DP020-MSG-WARNING TO TRUE                              
J28545*                  ---- WAIT 15 SECONDS, THEN RETRY ----                  
J28545                   MOVE PC-TSYMSG-00551                                   
J28545                                       TO DP020-MSG-NUMBER                
J28545         END-EVALUATE                                                     
J28545       END-IF.                                                            
J28545                                                                          
J28545                                                                          
       3480-READ-TINVPAR.                                                       
           MOVE IN030-INV-ID         TO PV-INV-ID.                              
                                                                                
           EXEC SQL                                                             
W28545       SELECT A.PLND_INV_DTE                                              
W28545             ,A.LOC_NBR                                                   
             INTO  :INVPAR-PLND-INV-DTE                                         
W26600            ,:INVPAR-LOC-NBR                                              
W28545       FROM   TINVPAR A                                                   
W28545             ,TINCNTL B                                                   
W28545     WHERE A.INV_ID  = B.INV_ID                                           
W28545       AND A.INV_ID  = :PV-INV-ID                                         
W28545       AND B.ACTV_IND = 'Y'                                               
W28545       AND A.LOC_NBR = :PV-DB2-STORE-NBR                                  
W28545       AND A.ACTL_FIN_BK_DTE = '9999-09-09'                               
W33304       AND A.LOC_INV_STAT_CDE = 'IN'                                      
           END-EXEC.                                                            
W28545                                                                          
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
W26600             MOVE INVPAR-LOC-NBR        TO PV-STR-NBR                     
W26600*            MOVE INVPAR-STORE-NBR      TO PV-STR-NBR                     
                   MOVE INVPAR-PLND-INV-DTE   TO PV-PLND-INV-DTE                
                   MOVE PV-PLND-INV-TIMESTAMP TO PV-PLND-INV-TMST               
SMJ            WHEN SQLCODE = -913                                              
SMJ            WHEN SQLCODE = -904                                              
SMJ            WHEN SQLCODE = -911                                              
SMJ                SET  PS-ERROR                                                
SMJ                     PS-UNAVAILABLE-RESOURCE  TO TRUE                        
               WHEN SQLCODE = +100                                              
               WHEN SQLWARN NOT = SPACE                                         
               WHEN SQLCODE NOT = ZERO                                          
                   MOVE '3480-READ-TINVPAR'                                     
                                     TO DP013-PARAGRAPH                         
                   MOVE 'RETRIEVE STORE PLANNED INV DATE'                       
                                     TO DP013-MESSAGE-TEXT(1)                   
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TINVPAR'    TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * CALCULATE TOTALS FOR RETAIL, QUANTITY AND EXTENDED RETAIL               
      ******************************************************************        
      *                                                                         
       3500-CALC-EXTD-RTL.                                                      
      *                                                                         
           IF ASC-INV-QTY-X (PS-SUB) = SPACES                                   
               MOVE ZEROES TO ASC-INV-QTY (PS-SUB)                              
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-EXTD-RTL-AMT EQUAL                                        
               (ASC-INV-QTY (PS-SUB) * PV-ITEM-RETAIL).                         
                                                                                
J28545     MOVE PV-EXTD-RTL-AMT TO                                              
J28545                          ASC-ITM-EXTD-PR-AMT           (PS-SUB)          
J28545                          ASC-ITM-EXTD-PR-AMT-X-FMT (PS-SUB)              
      * ADD TO TOTAL FIELDS                                                     
           ADD PV-ITEM-RETAIL           TO  ASC-TOTAL-RTL-AMT.                  
           ADD ASC-INV-QTY (PS-SUB)     TO  ASC-TOTAL-QTY-AMT.                  
           ADD PV-EXTD-RTL-AMT          TO  ASC-TOTAL-EXTD-RTL.                 
           MOVE ASC-TOTAL-RTL-AMT       TO  ASC-TOTAL-RTL-AMT-X-FMT.            
           MOVE ASC-TOTAL-QTY-AMT       TO  ASC-TOTAL-QTY-AMT-X-FMT.            
           MOVE ASC-TOTAL-EXTD-RTL      TO  ASC-TOTAL-EXTD-RTL-X-FMT.           
                                                                                
                                                                                
W28545*----------------------------------------------------------------*        
W28545* 3600-EDIT THE UPC OR SKU.                                               
W28545*    THE ROUTINE COUNTS THE NUMBER OF BYTES THE USER ENTERED. THE         
W28545* RESULT IS USED TO DETERMINE IF THE USER HAD ENTERED A SKU OR AN         
W28545* UPC.     (DEFINITIIONS - SKU 8 BYTES, UPC 9 TO 15 BYTES)                
W28545*----------------------------------------------------------------*        
W28545 3600-EDIT-UPC-SKU.                                                       
W28545                                                                          
W28545     IF WS-EDIT-BYTE (PV-BYTE-SUB) IS EQUAL TO SPACE                      
W28545         CONTINUE                                                         
W28545     ELSE                                                                 
W28545         IF (WS-EDIT-BYTE (PV-BYTE-SUB) IS EQUAL TO ZERO AND              
W28545             PV-BYTE-COUNT IS EQUAL TO ZEROS)                             
W28545             CONTINUE                                                     
W28545         ELSE                                                             
W28545             ADD 1           TO PV-BYTE-COUNT                             
W28545         END-IF                                                           
W28545     END-IF.                                                              
W28545                                                                          
W28545                                                                          
      *----------------------------------------------------------------*        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE ASC-ITEM-ARRAY                                            
                      ASC-ORIG-ITEM-ARRAY.                                      
                                                                                
           MOVE ASC-KEY-STORE-NBR-X TO PV-DB2-STORE-NBR                         
                                       PV-STR-NBR.                              
           MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR.                        
W28545     MOVE ASC-KEY-TYPE-CDE    TO PV-DB2-TYPE-CDE.                         
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                            
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB.                                     
                                                                                
           PERFORM 4020-CLOSE-CURSOR.                                           
                                                                                
           IF PS-NO-SHEET-FOUND                                                 
               SET DP020-MSG-FATAL                                              
                   ASC-DATA-DOES-NOT-EXIST TO TRUE                              
      *        ---- DATA DOES NOT EXIST ----                                    
               MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                         
               IF  ASC-CRIT-SPEC-FMT                                            
               AND NOT DP020-FK-REFRESH (DP020-SRC-AID)                         
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
               ELSE                                                             
                   PERFORM 4500-SET-ATTRIBUTES-TO-PROT                          
                   IF ASC-LIST-FMT                                              
                       PERFORM 9100-READ-SEL-QUEUE                              
                       SET IN031-DEL (ASC-SEL-SUB) TO TRUE                      
                       PERFORM 9200-REWRITE-SEL-QUEUE                           
                   END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
               END-IF                                                           
           ELSE                                                                 
               SET ASC-DATA-EXISTS TO TRUE                                      
               IF ASC-DELETE-FMT                                                
                   PERFORM 4500-SET-ATTRIBUTES-TO-PROT                          
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
      *            ---- PRESS F2 TO CONFIRM REQUEST ----                        
                   MOVE PC-TSYMSG-00004 TO DP020-MSG-NUMBER                     
               ELSE                                                             
                   PERFORM 4525-SET-ATTRIBUTES-TO-UNPROT                        
                       VARYING PS-SUB FROM 1 BY 1                               
                       UNTIL PS-SUB > PC-LINES-PER-PANEL                        
W21732             MOVE -1 TO MR-SKUL (1)                                       
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               END-IF                                                           
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * OPEN THE CURSOR.                                               *        
      *----------------------------------------------------------------*        
                                                                                
       4010-OPEN-CURSOR.                                                        
W21732     MOVE PV-DB2-STORE-NBR TO INENTY-STR-NBR.                             
W21732     MOVE PV-DB2-SHEET-NBR TO INENTY-SHEET-NBR.                           
W28545     MOVE PV-DB2-TYPE-CDE  TO INENTY-SHEET-TYP-CDE.                       
                                                                                
           EXEC SQL                                                             
                OPEN LINE-NBR-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4010-OPEN-CURSOR'                                     
                                 TO  DP013-PARAGRAPH                            
                    MOVE 'OPEN STOCKROOM WITHDRAWAL LINE-NBR CURSOR'            
                                 TO  DP013-MESSAGE-TEXT (1)                     
                    MOVE SQLCA   TO  DP013-SQLCA                                
                    SET DP013-DB2-ABEND                                         
                                 TO  TRUE                                       
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * CLOSE THE CURSOR.                                              *        
      *----------------------------------------------------------------*        
                                                                                
       4020-CLOSE-CURSOR.                                                       
                                                                                
           EXEC SQL                                                             
               CLOSE LINE-NBR-CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
                    MOVE '4020-CLOSE-CURSOR'                                    
                                 TO  DP013-PARAGRAPH                            
                    MOVE 'CLOSE STOCKROOM WITHDRAWAL LINE-NBR CURSOR'           
                                 TO  DP013-MESSAGE-TEXT (1)                     
                    MOVE SQLCA   TO  DP013-SQLCA                                
                    SET DP013-DB2-ABEND                                         
                                 TO  TRUE                                       
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE ARRAY.         
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
                                                                                
           INITIALIZE PV-INITIAL-TOTALS                                         
                      ASC-TOTAL-FIELDS.                                         
                                                                                
           PERFORM VARYING PS-SUB FROM 1 BY 1                                   
             UNTIL PS-SUB > PC-LINES-PER-PANEL                                  
             OR    SQLCODE = +100                                               
                                                                                
               PERFORM 4310-FETCH                                               
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       SET PS-SHEET-FOUND TO TRUE                               
                       MOVE INENTY-LNE-NBR                                      
                                    TO ASC-LINE-NBR             (PS-SUB)        
                                       ASC-ORIG-LINE-NBR        (PS-SUB)        
                                       ASC-LAST-LINE-NBR                        
W21732                 MOVE INENTY-SKU-NBR                                      
W21732                              TO ASC-SKU-NBR              (PS-SUB)        
W21732                                 ASC-ORIG-SKU-NBR         (PS-SUB)        
W28545                 MOVE INENTY-UPC-NBR                                      
W28545                              TO ASC-UPC-NBR              (PS-SUB)        
                       MOVE INENTY-INV-QTY                                      
                                    TO ASC-INV-QTY              (PS-SUB)        
                                       ASC-ORIG-INV-QTY         (PS-SUB)        
                       MOVE INENTY-UNT-RTL-AMT                                  
                              TO ASC-ITM-UNIT-PR-AMT            (PS-SUB)        
                                 ASC-ITM-UNIT-PR-AMT-X-FMT      (PS-SUB)        
                                 ASC-ORIG-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)        
                       MOVE INENTY-EXTD-RTL-AMT                                 
                              TO ASC-ITM-EXTD-PR-AMT            (PS-SUB)        
                                 ASC-ITM-EXTD-PR-AMT-X-FMT      (PS-SUB)        
                                 ASC-ORIG-ITM-EXTD-PR-AMT-X-FMT (PS-SUB)        
      *    TOTALS FOR SHEET                                                     
                       MOVE INENTY-INV-QTY                                      
                              TO ASC-ORIG-INV-QTY         (PS-SUB)              
                       MOVE INENTY-UNT-RTL-AMT                                  
                              TO ASC-ITM-UNIT-PR-AMT-X-FMT      (PS-SUB)        
                                 ASC-ORIG-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)        
                       MOVE INENTY-EXTD-RTL-AMT                                 
                              TO ASC-ITM-EXTD-PR-AMT-X-FMT      (PS-SUB)        
                                 ASC-ORIG-ITM-EXTD-PR-AMT-X-FMT (PS-SUB)        
      *                                                                         
                       ADD  INENTY-INV-QTY                                      
                              TO ASC-TOTAL-QTY-AMT                              
                       ADD  INENTY-UNT-RTL-AMT                                  
                              TO ASC-TOTAL-RTL-AMT                              
                       ADD  INENTY-EXTD-RTL-AMT                                 
                              TO ASC-TOTAL-EXTD-RTL                             
      *                                                                         
W28545*                SET ASC-SKU-ENTERED     (PS-SUB) TO TRUE                 
W28545                 IF INENTY-UPC-NBR > ZERO                                 
W28545                     SET ASC-UPC-ENTERED (PS-SUB) TO TRUE                 
W28545                 ELSE                                                     
W28545                     SET ASC-SKU-ENTERED (PS-SUB) TO TRUE                 
W28545                 END-IF                                                   
                   WHEN OTHER                                                   
                       CONTINUE                                                 
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           MOVE ASC-TOTAL-RTL-AMT        TO ASC-TOTAL-RTL-AMT-X-FMT.            
           MOVE ASC-TOTAL-QTY-AMT        TO ASC-TOTAL-QTY-AMT-X-FMT.            
           MOVE ASC-TOTAL-EXTD-RTL       TO ASC-TOTAL-EXTD-RTL-X-FMT.           
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE LINE-NBR CURSOR.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4310-FETCH.                                                              
                                                                                
           EXEC SQL                                                             
                FETCH LINE-NBR-CSR                                              
                INTO  :INENTY-LNE-NBR                                           
W21732              , :INENTY-SKU-NBR                                           
                    , :INENTY-UPC-NBR                                           
                    , :INENTY-INV-QTY                                           
                    , :INENTY-UNT-RTL-AMT                                       
                    , :INENTY-EXTD-RTL-AMT                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE  NOT = ZERO                                         
                   MOVE '4310-FETCH-CURSOR'                                     
                                TO  DP013-PARAGRAPH                             
                   MOVE 'FETCH STOCKROOM WITHDRAWAL LINE-NBR CURSOR'            
                                TO  DP013-MESSAGE-TEXT (1)                      
                   MOVE SQLCA   TO  DP013-SQLCA                                 
                   SET DP013-DB2-ABEND                                          
                                TO  TRUE                                        
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
W28545     IF ASC-TYPE-TFR-SHEETS                                               
W28545         SET ASC-DESC-TFR-SHEETS  TO TRUE                                 
W28545         MOVE ASC-TYPE-CDE-DESC   TO ATYPDSCO                             
W28545     ELSE                                                                 
W28545     IF ASC-TYPE-MANI-SHEETS                                              
W28545         SET ASC-DESC-MANI-SHEETS TO TRUE                                 
W28545         MOVE ASC-TYPE-CDE-DESC   TO ATYPDSCO                             
W28545     ELSE                                                                 
W28545     IF ASC-TYPE-ENTRD-SHEETS                                             
W28545         SET ASC-DESC-ENTRD-SHEETS TO TRUE                                
W28545         MOVE ASC-TYPE-CDE-DESC    TO ATYPDSCO                            
W28545     ELSE                                                                 
W28545         MOVE ASC-KEY-TYPE-CDE     TO ATYPDSCO                            
W28545     END-IF.                                                              
W28545                                                                          
W26600*    MOVE ASC-STORE-NAME      TO ASTNAMEO.                                
W26600     STRING                                                               
W26600       ASC-KEY-STORE-NBR-X SPACE '-' SPACE                                
W26600       ASC-STORE-NAME                                                     
W26600       DELIMITED BY SIZE      INTO ASTNAMEO.                              
           MOVE ASC-KEY-SHEET-NBR-X TO ASHEETO.                                 
                                                                                
           PERFORM VARYING PS-SUB FROM 1 BY 1                                   
               UNTIL PS-SUB > PC-LINES-PER-PANEL                                
               IF ASC-SKU-ENTERED (PS-SUB)                                      
W21732             MOVE ASC-SKU-NBR         (PS-SUB) TO MR-SKU  (PS-SUB)        
               ELSE                                                             
W28545*            MOVE SPACE                        TO MR-SKU  (PS-SUB)        
W28545             IF ASC-UPC-ENTERED (PS-SUB)                                  
W28545                 MOVE ZERO                     TO PV-UPC-POS              
W28545                 INSPECT ASC-UPC-NBR-X (PS-SUB) TALLYING                  
W28545                     PV-UPC-POS FOR LEADING ZEROS                         
W28545                 ADD 1                         TO PV-UPC-POS              
W28545                 MOVE ASC-UPC-NBR-X    (PS-SUB) (PV-UPC-POS:)             
W28545                                               TO MR-UPC  (PS-SUB)        
W28545             ELSE                                                         
W28545                 MOVE SPACE                    TO MR-SKU  (PS-SUB)        
W28545             END-IF                                                       
               END-IF                                                           
                                                                                
               IF ASC-INV-QTY (PS-SUB) IS NUMERIC                               
                   MOVE ASC-INV-QTY   (PS-SUB) TO MR-QTY   (PS-SUB)             
               ELSE                                                             
                   MOVE ASC-INV-QTY-X (PS-SUB) TO MR-QTY-X (PS-SUB)             
               END-IF                                                           
                                                                                
               MOVE ASC-ITM-UNIT-PR-AMT-X-FMT (PS-SUB)                          
                                               TO MR-PRCE-X (PS-SUB)            
                                                                                
               IF ASC-ITM-EXTD-PR-AMT (PS-SUB) > 0                              
                   MOVE ASC-ITM-EXTD-PR-AMT (PS-SUB)                            
                                               TO MR-EXTD   (PS-SUB)            
               ELSE                                                             
                   MOVE ASC-ITM-EXTD-PR-AMT-X (PS-SUB)                          
                                               TO MR-EXTD-X (PS-SUB)            
               END-IF                                                           
                                                                                
           END-PERFORM.                                                         
                                                                                
               MOVE ASC-TOTAL-RTL-AMT-X-FMT    TO MR-TRTL.                      
               MOVE ASC-TOTAL-QTY-AMT-X-FMT    TO MR-TQTY.                      
               MOVE ASC-TOTAL-EXTD-RTL-X-FMT   TO MR-TEXTD.                     
                                                                                
                                                                                
       4500-SET-ATTRIBUTES-TO-PROT.                                             
                                                                                
           PERFORM VARYING PS-SUB FROM 1 BY 1                                   
             UNTIL PS-SUB > PC-LINES-PER-PANEL                                  
W21732         MOVE DP015-PRO-NOR-OFF        TO MR-SKUA  (PS-SUB)               
                                                MR-QTYA  (PS-SUB)               
                                                MR-PRCEA (PS-SUB)               
W21732         MOVE DP015-BLUE               TO MR-SKUC  (PS-SUB)               
                                                MR-QTYC  (PS-SUB)               
                                                MR-PRCEC (PS-SUB)               
W21732         MOVE DP015-HL-OFF             TO MR-SKUH  (PS-SUB)               
                                                MR-QTYH  (PS-SUB)               
                                                MR-PRCEH (PS-SUB)               
           END-PERFORM.                                                         
                                                                                
                                                                                
       4525-SET-ATTRIBUTES-TO-UNPROT.                                           
                                                                                
W21732     MOVE DP015-UNP-NUM-NOR-OFF    TO MR-SKUA  (PS-SUB)                   
                                            MR-QTYA  (PS-SUB)                   
                                            MR-PRCEA (PS-SUB).                  
W21732     MOVE DP015-GREEN              TO MR-SKUC  (PS-SUB)                   
                                            MR-QTYC  (PS-SUB)                   
                                            MR-PRCEC (PS-SUB).                  
W21732     MOVE DP015-UNDERLINE          TO MR-SKUH  (PS-SUB)                   
                                            MR-QTYH  (PS-SUB)                   
                                            MR-PRCEH (PS-SUB).                  
                                                                                
           MOVE DP015-PRO-NOR-OFF        TO MR-EXTDA (PS-SUB).                  
           MOVE DP015-BLUE               TO MR-EXTDC (PS-SUB).                  
           MOVE DP015-HL-OFF             TO MR-EXTDH (PS-SUB).                  
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    CURRENT SYSTEM DATE/TIME MODULE                                      
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
                                                                                
       9100-READ-SEL-QUEUE.                                                     
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   INTO  (IN031-SHEET-SEL-REC)                                  
                   ITEM  (ASC-SEL-ITEM)                                         
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9100-READ-SEL-QUEUE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
      *----------------------------------------------------------------*        
       9200-REWRITE-SEL-QUEUE.                                                  
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE  (ASC-SEL-QUEUE-NAME)                                 
                    FROM   (IN031-SHEET-SEL-REC)                                
                    ITEM   (ASC-SEL-ITEM)                                       
                    RESP   (PV-CICS-RESP)                                       
                    REWRITE                                                     
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
               SET DP013-CICS-ABEND                                             
                   DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '9200-REWRITE-SEL-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
J28545     COPY INPD400.                                                        
