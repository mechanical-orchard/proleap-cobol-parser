00001  IDENTIFICATION DIVISION.                                         12/18/95
00002  PROGRAM-ID.    MAKUT030.                                         MAKUT030
00003  AUTHOR.        CHRISTINE FOLEY.                                     LV006
00004  DATE-WRITTEN.  SEPTEMBER 1996.                                      CL**2
00005 *****************************************************************    CL**2
00006 *          MA - INVENTORY ROLLUP PROGRAM                        *    CL**2
00007 *****************************************************************    CL**2
00008 *    THE PURPOSE OF THIS PROGRAM IS TO APPLY THE VENDOR NAME TO *    CL**2
00009 *    THE RECORDS VST AND VND RECORDS.                           *    CL**2
00012 *****************************************************************    CL**2
00013 *                                                                    CL**2
00014  ENVIRONMENT DIVISION.                                               CL**2
00015  INPUT-OUTPUT SECTION.                                               CL**2
00016  FILE-CONTROL.                                                       CL**2
00017      SELECT SKU-STORE-INV-EXTRACT-FILE                               CL**2
00018          ASSIGN TO INP01.                                            CL**2
00017      SELECT ROLLUP-VNDR-EXTRACT-FILE                                 CL**2
00018          ASSIGN TO OUT01.                                            CL**2
00019 *                                                                    CL**2
00020 *                                                                    CL**2
00021  DATA DIVISION.                                                      CL**2
00022  FILE SECTION.                                                       CL**2
00023  FD  SKU-STORE-INV-EXTRACT-FILE                                      CL**2
00024      RECORDING MODE IS F                                             CL**2
00025      BLOCK CONTAINS 0 RECORDS.                                       CL**2
00026  01  SKU-STORE-INV-EXTRACT-RECORD    PIC  X(109).                    CL**5
00027 *                                                                    CL**2
00023  FD  ROLLUP-VNDR-EXTRACT-FILE                                        CL**2
00024      RECORDING MODE IS F                                             CL**2
00025      BLOCK CONTAINS 0 RECORDS.                                       CL**2
00026  01  ROLLUP-EXTRACT-RECORD           PIC  X(109).                    CL**5
00027 *                                                                    CL**2
00028 *                                                                    CL**2
00029  WORKING-STORAGE SECTION.                                            CL**2
00030                                                                      CL**2
00031  01  ABEND-CODE                      PIC S9(04)    VALUE ZERO        CL**2
00032                                                    COMP SYNC.        CL**2
00033      88  ABEND-4001                                VALUE +4001.      CL**2
00034      88  ABEND-4002                                VALUE +4002.      CL**2
00035                                                                      CL**2
00036  01  PV-PROGRAM-VARIABLES.                                           CL**2
00038      05  PV-SQL-SUB                  PIC S9(04)    COMP.             CL**2
00039      05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACE.      CL**2
00040      05  PV-DB2-OPER-ATTEMPTED       PIC X(30)     VALUE SPACE.      CL**2
           05  PV-NAME-DESC                PIC X(30)     VALUE SPACE.           
           04  PV-PREV-ENT-ID              PIC 9(09)     COMP.                  
                                                                                
00041 *                                                                    CL**2
00042 *                                                                    CL**2
00043  01  PC-PROGRAM-CONSTANTS.                                           CL**2
00044      05  PC-CLEARANCE-STATUS         PIC S9(03)    VALUE +30         CL**2
00045                                                           COMP-3.    CL**2
00046      05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3 COMP.    CL**2
00047      05  PC-VND                      PIC X(03)     VALUE 'VND'.      CL**2
           05  PC-1                        PIC 9(01)     VALUE 1.               
           05  PC-500                      PIC 9(03)     VALUE 500.             
           05  PC-5000                     PIC 9(04)     VALUE 5000.            
00048                                                                      CL**2
00049  01  PS-PROGRAM-SWITCHES.                                            CL**2
00050      05  PS-EOF-SKUINV-SW            PIC  X(01)    VALUE 'N'.        CL**2
00051          88  PS-EOF-SKUINV                         VALUE 'Y'.        CL**2
00052 *                                                                    CL**2
       01  PT-VND-NAME-TBL.                                                     
           05  PT-VND-NAME-TABLE           OCCURS 80000 TIMES                   
                                           INDEXED BY NAME-IDX.                 
               10 PT-NAME-ENT-ID           PIC  9(09) COMP.                     
               10 PT-NAME-DESC             PIC  X(30).                          
00053 *                                                                    CL**2
00054 ******************************************************************   CL**2
00055 *  RECORD LAYOUT FOR THE SKU STORE INVENTORY EXTRACT FILE.           CL**2
00056 ******************************************************************   CL**2
00057      COPY MARD021.                                                   CL**2
00058 *                                                                    CL**2
00060 ******************************************************************   CL**2
00061 *    WORK AREA FOR THE ABEND ROUTINE.                                CL**2
00062 ******************************************************************   CL**2
00063      COPY DPWS004.                                                   CL**2
00064                                                                      CL**2
00072 *----------------------------------------------------------------*   CL**2
00073 *    ENTERPRISE NAME TABLE                                       *   CL**2
00074 *----------------------------------------------------------------*   CL**2
00075      EXEC SQL                                                        CL**2
00076          INCLUDE TENTNME                                             CL**2
00077      END-EXEC.                                                       CL**2
00078                                                                      CL**2
00079 *----------------------------------------------------------------*   CL**2
00080 *    SQL COMMUNICATIONS AREA                                     *   CL**2
00081 *----------------------------------------------------------------*   CL**2
00082      EXEC SQL                                                        CL**2
00083           INCLUDE SQLCA                                              CL**2
00084      END-EXEC.                                                       CL**2
00085                                                                      CL**2
00086 *----------------------------------------------------------------*   CL**2
00087 *    ENT NAME CURSOR                                             *   CL**2
00088 *----------------------------------------------------------------*   CL**2
00089      EXEC SQL                                                        CL**2
00090          DECLARE NAME-CSR CURSOR FOR                                 CL**2
00091             SELECT  ENT_ID                                           CL**2
00092                    ,ENT_NAME_DESC                                    CL**2
00096              FROM   TENTNME                                          CL**2
00098              WHERE TYPE_CDE         = '01'                           CL**2
00102      END-EXEC.                                                       CL**2
00103                                                                      CL**2
00104                                                                      CL**2
00105  LINKAGE SECTION.                                                    CL**2
00106                                                                      CL**2
00107  PROCEDURE DIVISION.                                                 CL**2
00108                                                                      CL**2
00109  0000-MAINLINE.                                                      CL**2
00110 ******************************************************************   CL**2
00111 *  CONTROLS THE ENTIRE FLOW OF THE PROGRAM.                          CL**2
00112 *    -- OPEN FILES                                                   CL**2
00113 *    -- PERFORM MAIN PROCESSING LOOP                                 CL**2
00114 *    -- CLOSE FILES                                                  CL**2
00115 *    -- DISPLAY COUNT                                                CL**2
00116 *    -- RETURN CONTROL TO SYSTEM                                     CL**2
00117 ******************************************************************   CL**2
00118      OPEN  INPUT SKU-STORE-INV-EXTRACT-FILE                          CL**2
00118           OUTPUT ROLLUP-VNDR-EXTRACT-FILE.                           CL**2
00119                                                                      CL**2
           PERFORM 0100-INITIALIZATION.                                         
                                                                                
00120      PERFORM 1000-PROCESS-SKUINV-FILE                                CL**2
00121          UNTIL PS-EOF-SKUINV.                                        CL**2
00122                                                                      CL**2
00123      CLOSE SKU-STORE-INV-EXTRACT-FILE                                CL**2
00123            ROLLUP-VNDR-EXTRACT-FILE.                                 CL**2
00124                                                                      CL**2
00128      GOBACK.                                                         CL**2
00129                                                                      CL**2
00130 /                                                                    CL**2
00131  0100-INITIALIZATION.                                                CL**2
00132 *----------------------------------------------------------------*   CL**2
00133 *                                                                *   CL**2
00134 *    THIS PROCESS LOADS AN INTERNAL TABLE OF VENDOR NAMES        *   CL**2
00137 *                                                                *   CL**2
00138 *----------------------------------------------------------------*   CL**2
00139                                                                      CL**2
           PERFORM                                                              
               VARYING NAME-IDX                                                 
               FROM 1 BY 1                                                      
               UNTIL NAME-IDX > 80000                                           
                                                                                
               MOVE ZEROS TO PT-NAME-ENT-ID(NAME-IDX)                           
               MOVE SPACES TO PT-NAME-DESC(NAME-IDX)                            
                                                                                
           END-PERFORM.                                                         
                                                                                
           PERFORM 2000-OPEN-ENTNME.                                            
                                                                                
00140      PERFORM                                                         CL**2
00140          VARYING NAME-IDX                                            CL**2
00140          FROM 1 BY 1                                                 CL**2
00140          UNTIL NAME-IDX > 80000                                      CL**2
               OR SQLCODE = 100                                                 
00144                                                                      CL**2
               PERFORM 2100-FETCH-ENTNME                                        
                                                                                
               IF SQLCODE = ZEROES                                              
                  MOVE ENTNME-ENT-NAME-DESC  TO PT-NAME-DESC(NAME-IDX)          
                  MOVE ENTNME-ENT-ID         TO PT-NAME-ENT-ID(NAME-IDX)        
               END-IF                                                           
                                                                                
00140      END-PERFORM.                                                    CL**2
                                                                                
           PERFORM 2200-CLOSE-ENTNME.                                           
                                                                                
00194      PERFORM 1210-READ-SKUINV-RECORD.                                CL**2
                                                                                
00145 /                                                                    CL**2
00131  1000-PROCESS-SKUINV-FILE.                                           CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC TO PROCESS EACH SKU INVENTORY ROW READ.   *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00193                                                                      CL**2
00195                                                                      CL**2
           IF MA021-ENT-ID = PV-PREV-ENT-ID                                     
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2300-SEARCH-ENT-NAME-DESC                                
               MOVE MA021-ENT-ID TO PV-PREV-ENT-ID                              
           END-IF.                                                              
                                                                                
           IF MA021-HIER-CDE = PC-VND                                           
               MOVE PV-NAME-DESC(1:10) TO MA021-ITEM-NAME(1:10)                 
           ELSE                                                                 
               MOVE PV-NAME-DESC(1:10) TO MA021-OWNER-VENDOR-NAME               
           END-IF.                                                              
                                                                                
           PERFORM 3000-WRITE-RECORD.                                           
                                                                                
00194      PERFORM 1210-READ-SKUINV-RECORD.                                CL**2
                                                                                
00210  1210-READ-SKUINV-RECORD.                                            CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    READ THE SKU INVENTORY FILE .                               *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00211                                                                      CL**2
00023          READ SKU-STORE-INV-EXTRACT-FILE                             CL**2
                    INTO MA021-SKU-STORE-INVENTORY                              
                    AT END                                                      
                       SET PS-EOF-SKUINV TO TRUE.                               
00286 *                                                                    CL**2
00187  2000-OPEN-ENTNME.                                                   CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC WILL RETRIEV ENT-NAME-DESC FROM TENTNME   *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00286                                                                      CL**2
00153      PERFORM                                                         CL**2
00154          WITH TEST AFTER                                             CL**2
00155          VARYING PV-SQL-SUB                                          CL**2
00156          FROM 1 BY 1                                                 CL**2
00157          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**2
00158                    OR SQLCODE = 0)                                   CL**2
00212          EXEC SQL                                                    CL**2
00203              OPEN NAME-CSR                                           CL**2
00219          END-EXEC                                                    CL**2
00220                                                                      CL**2
00221          EVALUATE TRUE                                               CL**2
00222              WHEN SQLCODE = 0                                        CL**2
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -904                                          
                        CONTINUE                                                
00233              WHEN SQLWARN NOT = SPACES                               CL**2
00234              WHEN SQLCODE NOT = ZERO                                 CL**2
00235                   MOVE '2000-OPEN-TENTNME'                           CL**2
00236                                     TO PV-PARAGRAPH-NAME             CL**2
00237                   MOVE 'OPEN OPERATION'                              CL**2
00238                                     TO PV-DB2-OPER-ATTEMPTED         CL**2
00239                   PERFORM 9900-SQL-ABEND                             CL**2
00240          END-EVALUATE                                                CL**2
00177      END-PERFORM.                                                    CL**2
00178                                                                      CL**2
00179      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**2
00180          MOVE '2000-OPEN-ENTNME'                                     CL**2
00181                                 TO  PV-PARAGRAPH-NAME                CL**2
00182          MOVE 'OPEN CURSOR RETRIES EXCEEDED'                         CL**2
00183                                 TO  PV-DB2-OPER-ATTEMPTED            CL**2
00184          PERFORM 9900-SQL-ABEND                                      CL**2
00185      END-IF.                                                         CL**2
      *                                                                         
00187  2100-FETCH-ENTNME.                                                  CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC WILL RETRIEV ENT-NAME-DESC FROM TENTNME   *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
      *                                                                         
00212      EXEC SQL                                                        CL**2
00213          FETCH NAME-CSR                                              CL**2
00214          INTO  :ENTNME-ENT-ID                                        CL**2
00215              , :ENTNME-ENT-NAME-DESC                                 CL**2
00219      END-EXEC.                                                       CL**2
00220                                                                      CL**2
00221      EVALUATE TRUE                                                   CL**2
00222          WHEN SQLCODE = 0                                            CL**2
00224          WHEN SQLCODE = +100                                         CL**2
                    CONTINUE                                                    
00233          WHEN SQLWARN NOT = SPACES                                   CL**2
00234          WHEN SQLCODE NOT = ZERO                                     CL**2
00235               MOVE '2100-FETCH-ENTNME'                               CL**2
00236                                 TO  PV-PARAGRAPH-NAME                CL**2
00237               MOVE 'FETCH VENDOR NAME CURSOR'                        CL**2
00238                                 TO  PV-DB2-OPER-ATTEMPTED            CL**2
00239               PERFORM 9900-SQL-ABEND                                 CL**2
00240      END-EVALUATE.                                                   CL**2
00241                                                                      CL**2
00187  2200-CLOSE-ENTNME.                                                  CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC WILL RETRIEV ENT-NAME-DESC FROM TENTNME   *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
      *                                                                         
00249      PERFORM                                                         CL**2
00250          WITH TEST AFTER                                             CL**2
00251          VARYING PV-SQL-SUB                                          CL**2
00252          FROM 1 BY 1                                                 CL**2
00253          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**2
00254                    OR SQLCODE = 0                                    CL**2
00255                    OR SQLCODE = +100)                                CL**2
00256                                                                      CL**2
00257          EXEC SQL                                                    CL**2
00258             CLOSE NAME-CSR                                           CL**2
00259          END-EXEC                                                    CL**2
00260                                                                      CL**2
00261          EVALUATE TRUE                                               CL**2
00262              WHEN SQLCODE = 0                                        CL**2
00263              WHEN SQLCODE = +100                                     CL**2
00264              WHEN SQLCODE = -904                                     CL**2
00265              WHEN SQLCODE = -911                                     CL**2
00266                   CONTINUE                                           CL**2
00267              WHEN SQLWARN NOT = SPACES                               CL**2
00268              WHEN SQLCODE NOT = ZERO                                 CL**2
00269                   MOVE '2200-CLOSE-ENTNME'                           CL**2
00270                                 TO  PV-PARAGRAPH-NAME                CL**2
00271                   MOVE 'CLOSE VENDOR NAME CURSOR'                    CL**2
00272                                 TO  PV-DB2-OPER-ATTEMPTED            CL**2
00273                   PERFORM 9900-SQL-ABEND                             CL**2
00274          END-EVALUATE                                                CL**2
00275      END-PERFORM.                                                    CL**2
00276                                                                      CL**2
00277      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**2
00278          MOVE '2200-CLOSE-ENTNME'                                    CL**2
00279                                 TO  PV-PARAGRAPH-NAME                CL**2
00280          MOVE 'CLOSE CURSOR RETRIES EXCEEDED'                        CL**2
00281                                 TO  PV-DB2-OPER-ATTEMPTED            CL**2
00282          PERFORM 9900-SQL-ABEND                                      CL**2
00283      END-IF.                                                         CL**2
00187  2300-SEARCH-ENT-NAME-DESC.                                          CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC TO WRITE THE ROLLUP RECORDS               *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00286 *                                                                    CL**2
           SET NAME-IDX TO 1.                                                   
           SEARCH PT-VND-NAME-TABLE                                             
               AT END                                                           
                  DISPLAY 'VENDOR NAME NOT FOUND FOR ENT ID: '                  
                            MA021-ENT-ID                                        
               WHEN MA021-ENT-ID = PT-NAME-ENT-ID(NAME-IDX)                     
                  MOVE PT-NAME-DESC(NAME-IDX) TO PV-NAME-DESC                   
           END-SEARCH.                                                          
                                                                                
00187  3000-WRITE-RECORD.                                                  CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    THIS IS THE LOGIC TO WRITE THE ROLLUP RECORDS               *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00286 *                                                                    CL**2
00203          WRITE ROLLUP-EXTRACT-RECORD                                 CL**2
00204                FROM MA021-SKU-STORE-INVENTORY.                       CL**2
      *                                                                         
00287  9900-SQL-ABEND.                                                     CL**2
00188 *----------------------------------------------------------------*   CL**2
00189 *                                                                *   CL**2
00190 *    COMMON DB2 ABEND ROUTINE                                    *   CL**2
00191 *                                                                *   CL**2
00192 *----------------------------------------------------------------*   CL**2
00286 *                                                                    CL**2
00288                                                                      CL**2
00289      SET ABEND-4001             TO TRUE.                             CL**2
00290      DISPLAY '**  ABEND     **'.                                     CL**2
00291      DISPLAY '**  PROGRAM   **  =  MAKUT030'.                        CL**2
00292      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.               CL**2
00293      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.           CL**2
00294                                                                      CL**2
00295 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**2
00296 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**2
00297                                                                      CL**2
00298      COPY DPPD004.                                                   CL**2
00299                                                                      CL**2
00300      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**2
