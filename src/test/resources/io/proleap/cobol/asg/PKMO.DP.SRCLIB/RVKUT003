000100******************************************************************01/29/96
000200 IDENTIFICATION DIVISION.                                         RVKUT003
000300******************************************************************   LV002
000400                                                                  RVKUT003
000500 PROGRAM-ID.    RVKUT003.                                         RVKUT003
000600 AUTHOR.        MICHAEL WALTERS.                                  RVKUT003
000700 INSTALLATION.  KOHLS.                                            RVKUT003
000800 DATE-WRITTEN   AUGUST, 1995.                                     RVKUT003
000900 DATE-COMPILED.                                                   RVKUT003
001000                                                                  RVKUT003
001100******************************************************************RVKUT003
001200*  THIS PROGRAM DELETES ROWS FROM THE DEFECTIVE ORDER TABLE (TDFCTRVKUT003
001300*  AND THE DEFECTIVE ITEM TABLE (TDFMEIT) FOR ALL THE ORDERS THAT RVKUT003
001400*  HAVE A STATUS DATE OLDER THAN THE 18 MONTHS                    RVKUT003
001500******************************************************************RVKUT003
001600                                                                  RVKUT003
005700*   CHANGES:                                                     *        
005800*                                                                *        
042613* DATE  06/01/2022                                               *        
042614*     CHANGE MADE: CHANGE COMMIT COUNT FROM 1000 TO 50 TO REDUCE *        
042615*     THE AMOUNT OF ROWS THAT ARE BEING HELD BY THE PROGRAM,     *        
042615*     PER THE REQUEST OF THE DB2 DBA                             *        
042618*     MADE BY: JEAN KURK               WR/IR #: CHG0270407       *        
042619*                                                                *        
042613* DATE  01/26/2023                                               *        
042614*     CHANGE MADE: CHANGE COMMIT COUNT FROM 500 TO 250 TO REDUCE *        
042615*     THE AMOUNT OF ROWS THAT ARE BEING HELD BY THE PROGRAM,     *        
042615*     PER THE REQUEST OF THE DB2 DBA                             *        
042618*     MADE BY: BOB MCASEY              WR/IR #: CHG0270407       *        
042619*                                                                *        
001700******************************************************************RVKUT003
001800 ENVIRONMENT DIVISION.                                            RVKUT003
001900******************************************************************RVKUT003
002000                                                                  RVKUT003
002100 CONFIGURATION SECTION.                                           RVKUT003
002200                                                                  RVKUT003
002300 SOURCE-COMPUTER.        IBM-3090.                                RVKUT003
002400 OBJECT-COMPUTER.        IBM-3090.                                RVKUT003
002500                                                                  RVKUT003
002600 INPUT-OUTPUT SECTION.                                            RVKUT003
002700                                                                  RVKUT003
002800 FILE-CONTROL.                                                    RVKUT003
002900                                                                  RVKUT002
003000     SELECT RTV-UNLOAD-HEADER-FILE                                RVKUT002
003100         ASSIGN TO IN01.                                          RVKUT002
003200                                                                  RVKUT002
003300     SELECT RTV-UNLOAD-DETAIL-FILE                                RVKUT002
003400         ASSIGN TO IN02.                                          RVKUT002
003500                                                                  RVKUT002
003600     SELECT RTV-HEADER-ARCHIVE                                    RVKUT002
003700         ASSIGN TO OUT01.                                         RVKUT002
003800                                                                  RVKUT002
003900     SELECT RTV-DETAIL-ARCHIVE                                    RVKUT002
004000         ASSIGN TO OUT02.                                         RVKUT002
004100                                                                  RVKUT002
004200     SELECT RTV-RELOAD-DETAIL-FILE                                RVKUT002
004300         ASSIGN TO OUT03.                                         RVKUT002
004400                                                                  RVKUT002
004500                                                                  RVKUT003
004600******************************************************************RVKUT003
004700 DATA DIVISION.                                                   RVKUT003
004800******************************************************************RVKUT003
004900                                                                  RVKUT003
005000 FILE SECTION.                                                    RVKUT003
005100                                                                  RVKUT002
005200 FD  RTV-UNLOAD-HEADER-FILE                                       RVKUT002
005300     RECORDING MODE IS F                                          RVKUT002
005400     LABEL RECORDS ARE STANDARD                                   RVKUT002
005500     BLOCK CONTAINS 0 RECORDS.                                    RVKUT002
005600                                                                  RVKUT002
005700*PRB44956 START                                                           
005800 01  RTV-UNLOAD-HEADER-RECORD        PIC  X(620).                 RVKUT002
005900*PRB44956 END                                                     RVKUT002
006000                                                                  RVKUT002
006100 FD  RTV-UNLOAD-DETAIL-FILE                                       RVKUT002
006200     RECORDING MODE IS F                                          RVKUT002
006300     LABEL RECORDS ARE STANDARD                                   RVKUT002
006400     BLOCK CONTAINS 0 RECORDS.                                    RVKUT002
006500                                                                  RVKUT002
006600*PRB44956 START                                                           
006700 01  RTV-UNLOAD-DETAIL-RECORD        PIC  X(52).                  RVKUT002
006800*PRB44956 END                                                     RVKUT002
006900                                                                  RVKUT002
007000 FD  RTV-HEADER-ARCHIVE                                           RVKUT002
007100     RECORDING MODE IS F                                          RVKUT002
007200     LABEL RECORDS ARE STANDARD                                   RVKUT002
007300     BLOCK CONTAINS 0 RECORDS.                                    RVKUT002
007400                                                                  RVKUT002
007500*PRB44956 START                                                           
007600 01  RTV-HEADER-ARCHIVE-RECORD       PIC  X(620).                 RVKUT002
007700*PRB44956 END                                                     RVKUT002
007800                                                                  RVKUT002
007900 FD  RTV-DETAIL-ARCHIVE                                           RVKUT002
008000     RECORDING MODE IS F                                          RVKUT002
008100     LABEL RECORDS ARE STANDARD                                   RVKUT002
008200     BLOCK CONTAINS 0 RECORDS.                                    RVKUT002
008300                                                                  RVKUT002
008400*PRB44956 START                                                           
008500 01  RTV-DETAIL-ARCHIVE-RECORD       PIC  X(52).                  RVKUT002
008600*PRB44956 END                                                     RVKUT003
008700                                                                  RVKUT002
008800 FD  RTV-RELOAD-DETAIL-FILE                                       RVKUT002
008900     RECORDING MODE IS F                                          RVKUT002
009000     LABEL RECORDS ARE STANDARD                                   RVKUT002
009100     BLOCK CONTAINS 0 RECORDS.                                    RVKUT002
009200                                                                  RVKUT002
009300*PRB44956 START                                                           
009400 01  RTV-RELOAD-DETAIL-RECORD        PIC  X(52).                  RVKUT002
009500*PRB44956 START                                                   RVKUT003
009600                                                                          
009700******************************************************************RVKUT003
009800 WORKING-STORAGE SECTION.                                         RVKUT003
009900******************************************************************RVKUT003
010000*                                                                 RVKUT003
010100*                                                                 RVKUT003
010200******************************************************************RVKUT003
010300*  DB2 FORMATTED MESSAGE AREA                                     RVKUT003
010400******************************************************************RVKUT003
010500     COPY DPWS004.                                                RVKUT003
010600*                                                                 RVKUT003
010700*                                                                 RVKUT003
010800******************************************************************RVKUT003
010900*  COMMUNICATIONS AREA FOR DB2                                    RVKUT003
011000******************************************************************RVKUT003
011100     EXEC SQL                                                     RVKUT003
011200          INCLUDE SQLCA                                           RVKUT003
011300     END-EXEC.                                                    RVKUT003
011400 EJECT                                                            RVKUT003
011500*                                                                 RVKUT003
011600******************************************************************RVKUT003
011700*  DB2 TABLE DECLARATION - DEFECTIVE ITEM HEADER TABLE (TDFCTOR)  RVKUT003
011800******************************************************************RVKUT003
011900     EXEC SQL                                                     RVKUT003
012000          INCLUDE TDFCTOR                                         RVKUT003
012100     END-EXEC.                                                    RVKUT003
012200 EJECT                                                            RVKUT003
012300******************************************************************RVKUT003
012400*  DB2 TABLE DECLARATION - DEFECTIVE ITEM DETAIL TABLE (TDFMEIT)  RVKUT003
012500******************************************************************RVKUT003
012600     EXEC SQL                                                     RVKUT003
012700          INCLUDE TDFMEIT                                         RVKUT003
012800     END-EXEC.                                                    RVKUT003
012900 EJECT                                                            RVKUT003
013000*                                                                 RVKUT003
013100 01  ABEND-CODE                      PIC S9(04)    VALUE +0       RVKUT003
013200                                                   COMP SYNC.     RVKUT003
013300*                                                                 RVKUT003
013400*                                                                         
013500 01  PC-PROGRAM-CONSTANTS.                                        RVKUT003
014000*PRB44956 START                                                           
014100*    05  PC-COMMIT-HOLD-CNT            PIC  S9(04) VALUE  +1000.          
0123RM*    05  PC-COMMIT-HOLD-CNT            PIC  S9(04) VALUE  +500.           
0123RM     05  PC-COMMIT-HOLD-CNT            PIC  S9(04) VALUE  +250.           
014200*PRB44956 END                                                             
014300*                                                                 RVKUT003
014400 01  PS-PROGRAM-SWITCHES.                                         RVKUT003
014500     05  PS-EOF-HEADER-UNLOAD-SW     PIC  X(01)    VALUE 'N'.     RVKUT003
014600         88  PS-EOF-HEADER-UNLOAD                  VALUE 'Y'.     RVKUT003
014700         88  PS-NOT-EOF-HEADER-UNLOAD              VALUE 'N'.     RVKUT003
014800     05  PS-EOF-DETAIL-UNLOAD-SW     PIC  X(01)    VALUE 'N'.     RVKUT003
014900         88  PS-EOF-DETAIL-UNLOAD                  VALUE 'Y'.     RVKUT003
015000         88  PS-NOT-EOF-DETAIL-UNLOAD              VALUE 'N'.     RVKUT003
015100 01  PA-PROGRAM-ACCUMULATORS.                                     RVKUT003
015200     05  PA-DELETED-HEADER-COUNT         PIC  9(10)  VALUE  0.    RVKUT003
015300     05  PA-DELETED-DETAIL-COUNT         PIC  9(10)  VALUE  0.    RVKUT003
015400     05  PA-SKIP-HEADER-COUNT            PIC  9(10)  VALUE  0.    RVKUT003
015500     05  PA-RELOAD-DETAIL-COUNT          PIC  9(10)  VALUE  0.    RVKUT003
015600     05  PA-DETAIL-UNLOAD-READ           PIC  9(10)  VALUE  0.    RVKUT003
015700     05  PA-HEADER-UNLOAD-READ           PIC  9(10)  VALUE  0.    RVKUT003
015800     05  PA-COMMIT-COUNT                 PIC  9(10)  VALUE  0.    RVKUT003
015900*PRB44956 START                                                           
016000     05  PA-DELETE-CNT                   PIC  9(04)  VALUE  0.            
016100*PRB44956 END                                                             
016200 EJECT                                                            RVKUT003
016300                                                                  RVKUT003
016400******************************************************************RVKUT003
016500 LINKAGE SECTION.                                                 RVKUT003
016600******************************************************************RVKUT003
016700                                                                  RVKUT003
016800******************************************************************RVKUT003
016900 PROCEDURE DIVISION.                                              RVKUT003
017000******************************************************************RVKUT003
017100                                                                  RVKUT003
017200******************************************************************RVKUT003
017300*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM    RVKUT003
017400******************************************************************RVKUT003
017500 0000-PROGRAM-MAINLINE.                                           RVKUT003
017600                                                                  RVKUT003
017700     PERFORM 1000-INITIALIZE.                                     RVKUT003
017800                                                                  RVKUT003
017900     PERFORM 2000-PURGE-DATA.                                     RVKUT003
018000                                                                  RVKUT003
018100     PERFORM 9000-EOJ-ROUTINE.                                    RVKUT003
018200     STOP RUN.                                                    RVKUT003
018300*0000-EXIT.                                                       RVKUT003
018400                                                                  RVKUT003
018500******************************************************************RVKUT003
018600*  1000-INITIALIZE -  INITIALIZE PROGRAM TOTALS, OPEN ALL THE     RVKUT003
018700*  INPUT AND OUTPUT FILES, AND PERFORM FIRST READS OF THE HEADER  RVKUT003
018800*  AND DETAIL INPUT FILES.                                        RVKUT003
018900******************************************************************RVKUT003
019000 1000-INITIALIZE.                                                 RVKUT003
019100                                                                  RVKUT003
019200     OPEN INPUT  RTV-UNLOAD-HEADER-FILE                           RVKUT002
019300                 RTV-UNLOAD-DETAIL-FILE                           RVKUT002
019400          OUTPUT RTV-HEADER-ARCHIVE                               RVKUT002
019500                 RTV-DETAIL-ARCHIVE                               RVKUT002
019600                 RTV-RELOAD-DETAIL-FILE.                          RVKUT002
019700                                                                          
019710*PRB44956 START                                                           
019800     INITIALIZE PA-PROGRAM-ACCUMULATORS                           RVKUT003
020000                DCLTDFMEIT                                                
020100                DCLTDFCTOR.                                               
020200*PRB44956 END                                                             
020300                                                                          
020400     SET  PS-NOT-EOF-HEADER-UNLOAD                                RVKUT003
020500          PS-NOT-EOF-DETAIL-UNLOAD   TO TRUE.                     RVKUT003
020600                                                                  RVKUT003
020700     PERFORM 8400-READ-HEADER-FILE.                               RVKUT003
020800                                                                  RVKUT003
020900     PERFORM 8000-READ-UNLOAD-FILE.                                       
021000                                                                  RVKUT003
021100*1000-EXIT.                                                       RVKUT003
021200*                                                                 RVKUT003
021300******************************************************************RVKUT003
021400* 2000-PURGE-DATA.                                                RVKUT003
021500* PROCESS:  THIS PARAGRAPH WILL DELETE THE HEADER AND DETAIL      RVKUT003
021600* ROWS FOR EACH DEFECTIVE ORDER                                   RVKUT003
021700******************************************************************RVKUT003
021800 2000-PURGE-DATA.                                                 RVKUT003
021900                                                                  RVKUT003
022000                                                                  RVKUT003
022100     PERFORM UNTIL PS-EOF-HEADER-UNLOAD                           RVKUT003
022200                                                                  RVKUT003
022300* IF THE HEADER IS A VALID TYPE (SHIPPED RTV, D & D, APPROVED             
022400* MARKOUTS, OR OLD INFO -SPACES-) THEN DELETE/ARCHIVE THE RECORD.         
022500* OTHERWISE DISPLAY THE DEFECT LISTING NUMBER AND FETCH THE NEXT          
022600* HEADER RECORD                                                           
022700                                                                          
022800         IF  DFCTOR-DFCT-ORD-ID-CDE   = ' '                               
022900         OR (DFCTOR-DFCT-ORD-ID-CDE   = 'R'                               
023000         AND DFCTOR-DFCT-ORD-STAT-CDE = 'A')                              
023100         OR (DFCTOR-DFCT-ORD-ID-CDE   = 'D'                               
023200         AND DFCTOR-DFCT-ORD-STAT-CDE = 'A')                              
023300         OR (DFCTOR-DFCT-ORD-ID-CDE   = 'M'                               
023400         AND DFCTOR-DFCT-ORD-STAT-CDE = 'A')                              
023500             PERFORM 5000-DELETE-HEADER-INFO                          RVKU
023600                                                                      RVKU
023700             PERFORM 5100-BUMP-DETAIL-FILE                            RVKU
023800                                                                      RVKU
023900*PRB44956 START                                                           
024000             IF PA-DELETE-CNT = PC-COMMIT-HOLD-CNT                    RVKU
024100                 PERFORM 2300-COMMIT                                  RVKU
024200             END-IF                                                   RVKU
024300*PRB44956 END                                                             
024400                                                                          
024500         ELSE                                                             
024600             DISPLAY '*** LISTING NUMBER '                                
024700                     DFCTOR-DFCT-ORD-ID                                   
024800                     ' WAS NOT DELETED. '                                 
024900                     ' TYPE CODE = '                                      
025000                     DFCTOR-DFCT-ORD-ID-CDE                               
025100                     ' - STATUS CODE = '                                  
025200                     DFCTOR-DFCT-ORD-STAT-CDE                             
025300             ADD +1 TO PA-SKIP-HEADER-COUNT                               
025400         END-IF                                                           
025500                                                                        03
025600         PERFORM 8400-READ-HEADER-FILE                                  03
025700                                                                        03
025800     END-PERFORM.                                                       03
025900                                                                          
026000* WRITE THE REST TO THE RELOAD FILE                                       
026100                                                                          
026200     PERFORM UNTIL PS-EOF-DETAIL-UNLOAD                                   
026300          PERFORM 8100-WRITE-RELOAD-FILE                                  
026400          PERFORM 8000-READ-UNLOAD-FILE                                   
026500     END-PERFORM.                                                         
026600                                                                          
026700*                                                                 RVKUT003
026800******************************************************************RVKUT003
026900* 2300-COMMIT                                                     RVKUT003
027000* PROCESS:  THIS PARAGRAPH WILL GET THE CURRENT TIMESTAMP NEEDED  RVKUT003
027100* TO DETERMINE IF A COMMIT IS NEEDED.                             RVKUT003
027200******************************************************************RVKUT003
027300 2300-COMMIT.                                                     RVKUT003
027400                                                                  RVKUT003
027500     EXEC SQL                                                     RVKUT003
027600         COMMIT                                                   RVKUT003
027700     END-EXEC.                                                    RVKUT003
027800                                                                  RVKUT003
028200*PRB44956 START                                                           
028210     EVALUATE TRUE                                                        
028220         WHEN SQLCODE = ZEROS                                             
028230             ADD  1 TO PA-COMMIT-COUNT                                    
028240             MOVE 0 TO PA-DELETE-CNT                                      
028241             DISPLAY ' LAST COMMIT ISSUED  DFCT_ORD_ID: '                 
028242                                            DFCTOR-DFCT-ORD-ID            
028250         WHEN OTHER                                                       
028260             DISPLAY '   ABEND   '                                        
028270             DISPLAY '   PROGRAM = RVKUT040   '                           
028280             DISPLAY '   PARAGRAPH = 2300-COMMIT'                         
028281                                                                          
028290               PERFORM 9100-SQL-ABEND                                     
028291     END-EVALUATE.                                                        
028292*PRB44956 END                                                             
028300                                                                  RVKUT003
028400*                                                                 RVKUT003
028500******************************************************************RVKUT003
028600* 5000-DELETE-HEADER-INFO.                                       *RVKUT003
028700******************************************************************RVKUT003
028800 5000-DELETE-HEADER-INFO.                                         RVKUT003
028900                                                                  RVKUT003
029000     PERFORM 8300-WRITE-HEADER-ARCHIVE.                           RVKUT002
029100                                                                          
029200     EXEC SQL                                                     RVKUT003
029300          DELETE                                                  RVKUT003
029400          FROM                                                    RVKUT003
029500                 TDFCTOR                                          RVKUT003
029600          WHERE DFCT_ORD_ID = :DFCTOR-DFCT-ORD-ID                 RVKUT003
029700     END-EXEC.                                                    RVKUT003
029800                                                                  RVKUT003
029840                                                                          
029900     EVALUATE TRUE                                                RVKUT003
030000         WHEN SQLCODE EQUAL +0                                    RVKUT003
030100             ADD SQLERRD(3)     TO PA-DELETED-HEADER-COUNT        RVKUT003
030200*PRB44956 START                                                           
030300             ADD SQLERRD(3)     TO PA-DELETE-CNT                          
030400*PRB44956 END                                                             
030500         WHEN SQLCODE = +100                                      RVKUT003
030600             CONTINUE                                             RVKUT003
030700         WHEN SQLWARN0 NOT EQUAL SPACE                            RVKUT003
030800         WHEN SQLCODE NOT EQUAL ZERO                              RVKUT003
030900             DISPLAY '   ABEND   '                                RVKUT003
031000             DISPLAY '   PROGRAM = RVKUT003   '                   RVKUT003
031100             DISPLAY '   PARAGRAPH = 5000-DELETE-HEADER-INFO   '  RVKUT003
031200             PERFORM 9100-SQL-ABEND                               RVKUT003
031300     END-EVALUATE.                                                RVKUT003
031400                                                                  RVKUT003
031500                                                                  RVKUT002
031600*                                                                 RVKUT003
031700*                                                                 RVKUT003
031800******************************************************************RVKUT003
031900* 5100-BUMP-DETAIL-FILE.                                         *RVKUT003
032000*      DFCTOR-DFCT-ORD-ID IS THE ORDER TO DELETE...READ THE FILE          
032100*      EITHER WRITING TO THE ARCHIVE (DELETE RECORD) OR TO THE            
032200*      RE-LOAD FILE (GOOD RECORD) UNTIL THE DETAIL RECORD IS PAST         
032300*      THE CURRENT HEADER RECORD.                                         
032400******************************************************************RVKUT003
032500 5100-BUMP-DETAIL-FILE.                                           RVKUT003
032600                                                                          
032700     PERFORM UNTIL DFMEIT-DFCT-ORD-ID > DFCTOR-DFCT-ORD-ID                
032800                OR PS-EOF-DETAIL-UNLOAD                                   
032900                                                                          
033000          IF DFMEIT-DFCT-ORD-ID = DFCTOR-DFCT-ORD-ID                      
033100              PERFORM 8200-WRITE-DETAIL-ARCHIVE                           
033200          ELSE                                                            
033300              PERFORM 8100-WRITE-RELOAD-FILE                              
033400          END-IF                                                          
033500                                                                          
033600         PERFORM 8000-READ-UNLOAD-FILE                                    
033700                                                                          
033800     END-PERFORM.                                                         
033900                                                                  RVKUT003
034000******************************************************************RVKUT003
034100* 8000-READ-UNLOAD-FILE.                                          RVKUT003
034200******************************************************************RVKUT003
034300 8000-READ-UNLOAD-FILE.                                           RVKUT003
034400     READ RTV-UNLOAD-DETAIL-FILE                                        KU
034500         INTO DCLTDFMEIT                                             RV KU
034600     AT END                                                               
034700         SET PS-EOF-DETAIL-UNLOAD TO TRUE.                                
034800                                                                          
034900     IF PS-NOT-EOF-DETAIL-UNLOAD                                          
035000         ADD +1 TO PA-DETAIL-UNLOAD-READ.                                 
035100                                                                          
035200******************************************************************RVKUT003
035300* 8100-WRITE-RELOAD-FILE.                                         RVKUT003
035400******************************************************************RVKUT003
035500 8100-WRITE-RELOAD-FILE.                                          RVKUT003
035600     WRITE RTV-RELOAD-DETAIL-RECORD                                     KU
035700         FROM DCLTDFMEIT.                                            RV KU
035800                                                                          
035900     ADD +1 TO PA-RELOAD-DETAIL-COUNT.                                    
036000                                                                          
036100******************************************************************RVKUT003
036200* 8200-WRITE-DETAIL-ARCHIVE.                                      RVKUT003
036300******************************************************************RVKUT003
036400 8200-WRITE-DETAIL-ARCHIVE.                                       RVKUT003
036500     WRITE RTV-DETAIL-ARCHIVE-RECORD                              RV    KU
036600      FROM DCLTDFMEIT.                                            RV    KU
036700                                                                  RVKUT003
036800     ADD +1 TO PA-DELETED-DETAIL-COUNT.                                   
036900                                                                          
037000******************************************************************RVKUT003
037100* 8300-WRITE-HEADER-ARCHIVE.                                      RVKUT003
037200******************************************************************RVKUT003
037300 8300-WRITE-HEADER-ARCHIVE.                                       RVKUT003
037400     WRITE RTV-HEADER-ARCHIVE-RECORD                              RVKUT002
037500       FROM DCLTDFCTOR.                                           RVKUT002
037600                                                                  RVKUT003
037700                                                                  RVKUT003
037800******************************************************************RVKUT003
037900* 8400-READ-HEADER-FILE.                                          RVKUT003
038000******************************************************************RVKUT003
038100 8400-READ-HEADER-FILE.                                           RVKUT003
038200     READ RTV-UNLOAD-HEADER-FILE                                        KU
038300         INTO DCLTDFCTOR                                             RV KU
038400     AT END                                                               
038500         SET PS-EOF-HEADER-UNLOAD TO TRUE.                                
038600                                                                          
038700     IF PS-NOT-EOF-HEADER-UNLOAD                                          
038800         ADD +1 TO PA-HEADER-UNLOAD-READ.                                 
038900                                                                  RVKUT003
039000******************************************************************RVKUT003
039100* 9000-EOJ-ROUTINE.                                               RVKUT003
039200******************************************************************RVKUT003
039300 9000-EOJ-ROUTINE.                                                RVKUT003
039400                                                                  RVKUT003
039500     CLOSE       RTV-UNLOAD-DETAIL-FILE                           RVKUT002
039600                 RTV-UNLOAD-HEADER-FILE                           RVKUT002
039700                 RTV-HEADER-ARCHIVE                               RVKUT002
039800                 RTV-DETAIL-ARCHIVE                               RVKUT002
039900                 RTV-RELOAD-DETAIL-FILE.                          RVKUT002
040000                                                                          
040100*PRB44956 START                                                           
040200       IF PA-DELETE-CNT > 0                                               
040300          PERFORM 2300-COMMIT                                             
040400       END-IF                                                             
040500*PRB44956 END                                                             
040600                                                                          
040700     DISPLAY '*************************************************'. RVKUT003
040800     DISPLAY '** RVKUT003 TOTALS:'                                RVKUT003
040900     DISPLAY '*************************************************'. RVKUT003
041000     DISPLAY '**        HEADER RECORDS READ: '                    RVKUT003
041100         PA-HEADER-UNLOAD-READ.                                         03
041200     DISPLAY '**        DETAIL RECORDS READ: '                    RVKUT003
041300         PA-DETAIL-UNLOAD-READ.                                         03
041400     DISPLAY '*************************************************'. RVKUT003
041500     DISPLAY '**     HEADER RECORDS DELETED: '                    RVKUT003
041600         PA-DELETED-HEADER-COUNT.                                 RVKUT003
041700     DISPLAY '**     HEADER RECORDS SKIPPED: '                    RVKUT003
041800         PA-SKIP-HEADER-COUNT.                                    RVKUT003
041900     DISPLAY '**'.                                                RVKUT003
042000     DISPLAY '**     DETAIL RECORDS DELETED: '                    RVKUT003
042100         PA-DELETED-DETAIL-COUNT.                                 RVKUT003
042200     DISPLAY '**    DETAIL RECORDS RELOADED: '                    RVKUT003
042300         PA-RELOAD-DETAIL-COUNT.                                        03
042400     DISPLAY '*************************************************'. RVKUT003
042500     DISPLAY '**              COMMITS TAKEN: '                    RVKUT003
042600         PA-COMMIT-COUNT.                                         RVKUT003
042700     DISPLAY '*************************************************'. RVKUT003
042800                                                                  RVKUT003
042900*9000-EXIT.                                                       RVKUT003
043000******************************************************************RVKUT003
043100* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE      RVKUT003
043200* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                 RVKUT003
043300******************************************************************RVKUT003
043400 9100-SQL-ABEND.                                                  RVKUT003
043500******************************************************************RVKUT003
043600* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    RVKUT003
043700* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         RVKUT003
043800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     RVKUT003
043900* FORMAT.                                                         RVKUT003
044000******************************************************************RVKUT003
044100*    COPY DPPD004.                                                RVKUT003
044200     COPY DPPD004.                                                RVKUT003
044300                                                                  RVKUT003
044400     MOVE +4013 TO ABEND-CODE.                                    RVKUT003
044500     CALL 'ILBOABN0' USING ABEND-CODE.                            RVKUT003
044600*9100-EXIT.                                                       RVKUT003
