       IDENTIFICATION DIVISION.                                                 
      **************************************************************            
      *  DPKCS502 - WRAPPER FOR DPKUT500 DATE ROUTINE FOR ACCESS   *            
      *             FROM JAVA PROGRAMS USING MQSERIES.             *            
      **************************************************************            
       PROGRAM-ID.         DPKCS502.                                            
       AUTHOR.             JEFF ROUBIK.                                         
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       OCTOBER 2000.                                        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM.                                                 
       OBJECT-COMPUTER.    IBM.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
       01  PC-BEGIN-STORAGE                PIC X(36)  VALUE                     
                                 'DPKCS502 WORKING-STORAGE BEGINS HERE'.        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC X(08) VALUE 'DPKCS502'.              
           05  PC-QMGR-NAME            PIC X(48) VALUE 'QKSP'.                  
               88  PC-QMGR-PROD                  VALUE 'QKSP'.                  
               88  PC-QMGR-QA                    VALUE 'QKSQ'.                  
               88  PC-QMGR-TEST                  VALUE 'QKST'.                  
           05  PC-DATA-COLLECT-QNAME   PIC X(48) VALUE                          
                                               'IS.CALENDAR.REQUEST.Q'.         
           05  PC-FIFTY-STARS          PIC X(50) VALUE ALL '*'.                 
           05  PC-ABEND-4020           PIC S9(4) COMP SYNC VALUE +4020. 00880099
                                                                                
      * DATE ROUTINE COPYBOOK                                                   
           COPY DPWS500.                                                        
                                                                                
      * MQSERIES COMPLETION CODES                                               
           COPY ISWS005.                                                        
                                                                                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-QM-NAME              PIC X(48).                               
           05  PV-DATA-COLLECT-QNAME   PIC X(48).                               
           05  PV-HCONN                PIC S9(9) BINARY VALUE 0.                
           05  PV-OPEN-OPTIONS         PIC S9(9) BINARY.                        
           05  PV-HANDLE               PIC S9(9) BINARY VALUE 0.                
           05  PV-COMPLETION-CODE      PIC S9(9) BINARY.                        
           05  PV-REASON               PIC S9(9) BINARY.                        
           05  PV-CON-REASON           PIC S9(9) BINARY.                        
           05  PV-MSGBUFFER.                                                    
               10  PV-MSGBUFFER-ARRAY  PIC X(1) OCCURS 1000 TIMES.              
           05  PV-MSGLENGTH            PIC S9(9) BINARY VALUE 1000.             
           05  PV-DATA-LENGTH          PIC S9(9) BINARY.                        
           05  PV-ACTION               PIC X(60)        VALUE SPACES.           
           05  PV-SYSID                PIC X(4).                        00870099
           05  PV-REQUESTS             PIC S9(9) COMP-3 VALUE ZERO.     00870099
           05  PV-JAVA-SIGN-4.                                                  
               10  PV-POSITIVE-4       PIC  9(4).                       00870099
           05  FILLER  REDEFINES  PV-JAVA-SIGN-4.                               
               10  PV-NEGATIVE-4       PIC -9(3).                       00870099
           05  PV-JAVA-SIGN-5.                                                  
               10  PV-POSITIVE-5       PIC  9(5).                       00870099
           05  FILLER  REDEFINES  PV-JAVA-SIGN-5.                               
               10  PV-NEGATIVE-5       PIC -9(4).                       00870099
           05  PV-DISPLAY-REQUESTS     PIC ZZZ,ZZZ,ZZ9.                 00870099
           05  PV-ABEND-CODE           PIC S9(4) COMP SYNC VALUE 4020.  00870099
                                                                                
       01  WS-MQCONN                   PIC X(8)  VALUE 'CSQCCONN'.              
       01  WS-MQOPEN                   PIC X(8)  VALUE 'CSQCOPEN'.              
       01  WS-MQINQ                    PIC X(8)  VALUE 'CSQCINQ'.               
       01  WS-MQGET                    PIC X(8)  VALUE 'CSQCGET'.               
       01  WS-MQPUT                    PIC X(8)  VALUE 'CSQCPUT'.               
       01  WS-MQPUT1                   PIC X(8)  VALUE 'CSQCPUT1'.              
       01  WS-MQCLOSE                  PIC X(8)  VALUE 'CSQCCLOS'.              
       01  WS-MQDISC                   PIC X(8)  VALUE 'CSQCDISC'.              
                                                                                
      * API CONTROL BLOCKS                                                      
       01  MQM-OBJECT-DESCRIPTOR.                                               
           COPY CMQODV.                                                         
       01  MQM-MESSAGE-DESCRIPTOR.                                              
           COPY CMQMDV.                                                         
       01  MQM-PUT-MESSAGE-OPTIONS.                                             
           COPY CMQPMOV.                                                        
       01  MQM-GET-MESSAGE-OPTIONS.                                             
           COPY CMQGMOV.                                                        
                                                                                
      * MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)              
      * AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)                     
       01  MQM-CONSTANTS.                                                       
           COPY CMQV.                                                           
                                                                                
       01  PC-END-STORAGE.                                                      
           05  FILLER                         PIC X(34)    VALUE                
                                   'DPKCS502 WORKING-STORAGE ENDS HERE'.        
                                                                                
                                                                                
      **************************************************************            
      *         P R O C E D U R E   D I V I S I O N                *            
      **************************************************************            
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-SETUP.                                                  
           PERFORM 2000-PROCESS-DATE-REQUESTS.                                  
           PERFORM 3000-CLEANUP.                                                
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
       1000-SETUP.                                                              
                                                                                
           EXEC CICS ASSIGN                                                     
               SYSID (PV-SYSID)                                                 
           END-EXEC.                                                            
           IF PV-SYSID (3:1) = 'P'                                              
               SET PC-QMGR-PROD TO TRUE                                         
           ELSE                                                                 
           IF PV-SYSID(3:1) = 'Q'                                               
               SET PC-QMGR-QA TO TRUE                                           
           ELSE                                                                 
               SET PC-QMGR-TEST TO TRUE.                                        
                                                                                
           PERFORM 1100-MQSERIES-CONNECT.                                       
           PERFORM 1200-MQSERIES-OPEN.                                          
                                                                                
                                                                                
       1100-MQSERIES-CONNECT.                                                   
                                                                                
           MOVE PC-QMGR-NAME TO PV-QM-NAME                                      
           CALL WS-MQCONN USING PV-QM-NAME                                      
                                PV-HCONN                                        
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
      *    AND EXIT                                                             
                                                                                
           MOVE PV-REASON TO PV-CON-REASON.                                     
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                                
               STRING 'CONNECT - ' PV-QM-NAME                                   
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
           MOVE SPACES TO PV-ACTION.                                            
           STRING 'MQCONN SUCCESSFUL TO ' PV-QM-NAME                            
             DELIMITED BY SIZE INTO PV-ACTION.                                  
           PERFORM 4000-LOG-MESSAGE.                                            
                                                                                
                                                                                
       1200-MQSERIES-OPEN.                                                      
                                                                                
           COMPUTE PV-OPEN-OPTIONS        = MQOO-INPUT-SHARED                   
                                          + MQOO-FAIL-IF-QUIESCING.             
           MOVE MQOT-Q                   TO MQOD-OBJECTTYPE.                    
           MOVE PV-QM-NAME               TO MQOD-OBJECTQMGRNAME.                
           MOVE PC-DATA-COLLECT-QNAME TO MQOD-OBJECTNAME.                       
                                                                                
           CALL WS-MQOPEN USING PV-HCONN                                        
                                MQM-OBJECT-DESCRIPTOR                           
                                PV-OPEN-OPTIONS                                 
                                PV-HANDLE                                       
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      *    IF CONNECTION FAILED THEN DISPLAY ERROR MESSAGE                      
      *    AND EXIT                                                             
                                                                                
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                                
               STRING 'OPEN - ' PC-DATA-COLLECT-QNAME                           
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
           MOVE SPACES TO PV-ACTION.                                            
           STRING 'MQOPEN SUCCESSFUL FOR '                                      
                  PC-DATA-COLLECT-QNAME                                         
             DELIMITED BY SIZE INTO PV-ACTION.                                  
           PERFORM 4000-LOG-MESSAGE.                                            
                                                                                
                                                                                
       2000-PROCESS-DATE-REQUESTS.                                              
                                                                                
           PERFORM 2100-GET-DATE-REQUESTS.                                      
           PERFORM  UNTIL PV-COMPLETION-CODE NOT = MQCC-OK                      
               PERFORM 2200-CALL-DATE-ROUTINE                                   
               PERFORM 2300-PUT-DATE-REPLY                                      
               PERFORM 2100-GET-DATE-REQUESTS                                   
           END-PERFORM.                                                         
                                                                                
                                                                                
       2100-GET-DATE-REQUESTS.                                                  
                                                                                
           MOVE MQMI-NONE       TO MQMD-MSGID.                                  
           MOVE MQCI-NONE       TO MQMD-CORRELID.                               
      * WAIT FOR 30 MINUTES IN PRODUCTION (1,800,000 MILLISECONDS)              
      * AND 2 MINUTES IN OTHER REGIONS (120,000 MILLISECONDS).                  
      * THIS WILL MAKE IT EASIER TO DO A NEW COPY IN A DEVELOPMENT              
      * REGION QUICKLY.  YOU WONT HAVE TO WAIT TOO LONG FOR THE                 
      * TRANSACTION TO FINISH.                                                  
           IF PV-SYSID (3:1) = 'P'                                              
               MOVE 1800000     TO MQGMO-WAITINTERVAL                           
           ELSE                                                                 
               MOVE 120000      TO MQGMO-WAITINTERVAL                           
           MOVE 1000            TO PV-MSGLENGTH.                                
           COMPUTE MQGMO-OPTIONS = MQGMO-NO-SYNCPOINT                           
                                 + MQGMO-WAIT                                   
                                 + MQGMO-CONVERT                                
                                 + MQGMO-FAIL-IF-QUIESCING.                     
                                                                                
           CALL WS-MQGET USING PV-HCONN                                         
                               PV-HANDLE                                        
                               MQM-MESSAGE-DESCRIPTOR                           
                               MQM-GET-MESSAGE-OPTIONS                          
                               PV-MSGLENGTH                                     
                               PV-MSGBUFFER                                     
                               PV-DATA-LENGTH                                   
                               PV-COMPLETION-CODE                               
                               PV-REASON.                                       
                                                                                
      * IF GET FAILED THEN DISPLAY ERROR MESSAGE                                
           IF (PV-COMPLETION-CODE = MQCC-OK)                                    
           OR (PV-REASON          = MQRC-NO-MSG-AVAILABLE)                      
               CONTINUE                                                         
           ELSE                                                                 
               STRING 'GET - ' PC-DATA-COLLECT-QNAME                            
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
                                                                                
       2200-CALL-DATE-ROUTINE.                                                  
                                                                                
           MOVE PV-MSGBUFFER (01:39) TO DPG51.                                  
           MOVE PV-MSGBUFFER (40:20) TO DPG52.                                  
           MOVE PV-MSGBUFFER (60:20) TO DPG53.                                  
                                                                                
           INITIALIZE DPG54, DPG55, DPG56.                                      
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
      * REMOVE THE SIGN FROM THESE FIELDS SINCE JAVA CAN'T HANDLE THEM.         
           IF DPG55-DAYS-DIFFERENCE < 0                                         
             MOVE DPG55-DAYS-DIFFERENCE TO PV-NEGATIVE-5                        
           ELSE                                                                 
             MOVE DPG55-DAYS-DIFFERENCE TO PV-POSITIVE-5.                       
           MOVE PV-JAVA-SIGN-5 TO DPG55-DAYS-DIFFERENCE-X.                      
                                                                                
           IF DPG55-BUS-DAYS-DIFFERENCE < 0                                     
             MOVE DPG55-BUS-DAYS-DIFFERENCE TO PV-NEGATIVE-5                    
           ELSE                                                                 
             MOVE DPG55-BUS-DAYS-DIFFERENCE TO PV-POSITIVE-5.                   
           MOVE PV-JAVA-SIGN-5 TO DPG55-BUS-DAYS-DIFFERENCE-X.                  
                                                                                
           IF DPG56-WEEKS-DIFFERENCE < 0                                        
             MOVE DPG56-WEEKS-DIFFERENCE TO PV-NEGATIVE-4                       
           ELSE                                                                 
             MOVE DPG56-WEEKS-DIFFERENCE TO PV-POSITIVE-4.                      
           MOVE PV-JAVA-SIGN-4 TO DPG56-WEEKS-DIFFERENCE-X.                     
                                                                                
           IF DPG56-PERIODS-DIFFERENCE < 0                                      
             MOVE DPG56-PERIODS-DIFFERENCE TO PV-NEGATIVE-4                     
           ELSE                                                                 
             MOVE DPG56-PERIODS-DIFFERENCE TO PV-POSITIVE-4.                    
           MOVE PV-JAVA-SIGN-4 TO DPG56-PERIODS-DIFFERENCE-X.                   
                                                                                
                                                                                
       2300-PUT-DATE-REPLY.                                                     
                                                                                
           MOVE MQMD-REPLYTOQMGR     TO MQOD-OBJECTQMGRNAME.                    
           MOVE MQMD-REPLYTOQ        TO MQOD-OBJECTNAME.                        
           MOVE MQMD-MSGID           TO MQMD-CORRELID.                          
           MOVE MQMI-NONE            TO MQMD-MSGID.                             
           MOVE MQMT-REPLY           TO MQMD-MSGTYPE.                           
           MOVE MQFMT-STRING         TO MQMD-FORMAT.                            
           MOVE 5000                 TO MQMD-EXPIRY.                            
           MOVE 671                  TO PV-MSGLENGTH.                           
           STRING DPG54 DPG55 DPG56                                             
                 DELIMITED BY SIZE INTO PV-MSGBUFFER.                           
           MOVE MQPER-PERSISTENT     TO MQMD-PERSISTENCE.                       
           COMPUTE MQPMO-OPTIONS      = MQPMO-NO-SYNCPOINT                      
                                      + MQPMO-FAIL-IF-QUIESCING.                
                                                                                
           CALL WS-MQPUT1 USING PV-HCONN                                        
                                MQM-OBJECT-DESCRIPTOR                           
                                MQM-MESSAGE-DESCRIPTOR                          
                                MQM-PUT-MESSAGE-OPTIONS                         
                                PV-MSGLENGTH                                    
                                PV-MSGBUFFER(1:671)                             
                                PV-COMPLETION-CODE                              
                                PV-REASON.                                      
                                                                                
      * IF PUT1 FAILED THEN DISPLAY ERROR MESSAGE                               
                                                                                
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                                
               INSPECT MQOD-OBJECTQMGRNAME                                      
                 REPLACING ALL SPACE BY LOW-VALUE                               
               INSPECT MQOD-OBJECTNAME                                          
                 REPLACING ALL SPACE BY LOW-VALUE                               
               STRING 'PUT1 - ' MQMD-REPLYTOQMGR                                
                                        ' / ' MQMD-REPLYTOQ                     
                 DELIMITED BY LOW-VALUE INTO PV-ACTION                          
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
           ADD 1 TO PV-REQUESTS.                                                
                                                                                
                                                                                
       3000-CLEANUP.                                                            
                                                                                
           PERFORM 3100-MQSERIES-CLOSE.                                         
           PERFORM 3200-MQSERIES-DISCONNECT.                                    
                                                                                
           MOVE SPACES TO PV-ACTION.                                            
           MOVE PV-REQUESTS TO PV-DISPLAY-REQUESTS.                             
           STRING PV-DISPLAY-REQUESTS                                           
                ' CALENDAR REQUESTS PROCESSED.'                                 
             DELIMITED BY SIZE INTO PV-ACTION.                                  
           PERFORM 4000-LOG-MESSAGE.                                            
                                                                                
                                                                                
       3100-MQSERIES-CLOSE.                                                     
                                                                                
           CALL WS-MQCLOSE USING PV-HCONN                                       
                                 PV-HANDLE                                      
                                 MQCO-NONE                                      
                                 PV-COMPLETION-CODE                             
                                 PV-REASON.                                     
           IF (PV-COMPLETION-CODE NOT = MQCC-OK)                                
               STRING 'CLOSE - ' PC-DATA-COLLECT-QNAME                          
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 9001-MQSERIES-ERROR.                                     
                                                                                
           MOVE SPACES TO PV-ACTION.                                            
           STRING 'MQCLOSE SUCCESSFUL FOR '                                     
                  PC-DATA-COLLECT-QNAME                                         
             DELIMITED BY SIZE INTO PV-ACTION.                                  
           PERFORM 4000-LOG-MESSAGE.                                            
                                                                                
                                                                                
       3200-MQSERIES-DISCONNECT.                                                
                                                                                
           IF PV-CON-REASON NOT = MQRC-ALREADY-CONNECTED                        
               CALL WS-MQDISC USING PV-HCONN                                    
                                    PV-COMPLETION-CODE                          
                                    PV-REASON                                   
               IF (PV-COMPLETION-CODE NOT = MQCC-OK)                            
                   STRING 'DISCONNECT - ' PC-QMGR-NAME                          
                     DELIMITED BY SIZE INTO PV-ACTION                           
                   PERFORM 9001-MQSERIES-ERROR                                  
               END-IF                                                           
               MOVE SPACES TO PV-ACTION                                         
               STRING 'MQDISC SUCCESSFUL FROM '                                 
                      PV-QM-NAME                                                
                 DELIMITED BY SIZE INTO PV-ACTION                               
               PERFORM 4000-LOG-MESSAGE.                                        
                                                                                
                                                                                
      ************************************************************              
      * TAKE ADVANTAGE OF LOGGING ROUTINES IN ISWS005.                          
      ************************************************************              
       4000-LOG-MESSAGE.                                                        
                                                                                
           EXEC CICS ASKTIME                                                    
               ABSTIME (IS-ABSTIME)                                             
           END-EXEC.                                                            
           EXEC CICS FORMATTIME                                                 
               ABSTIME  (IS-ABSTIME)                                            
               MMDDYYYY (IS-LOG-DATE)                                           
               DATESEP                                                          
               TIME     (IS-LOG-TIME)                                           
               TIMESEP                                                          
           END-EXEC.                                                            
                                                                                
           STRING ' ' PC-PGM-NAME                                               
                  ' ' IS-LOG-DATE                                               
                  ' ' IS-LOG-TIME                                               
                  ' ' PV-ACTION                                                 
             DELIMITED BY SIZE INTO IS-LOG-LINE.                                
                                                                                
           PERFORM 9002-ENQUEUE.                                                
           PERFORM 9003-WRITEQ-TD.                                              
           PERFORM 9004-DEQUEUE.                                                
                                                                                
                                                                                
           COPY ISPD005.                                                        
