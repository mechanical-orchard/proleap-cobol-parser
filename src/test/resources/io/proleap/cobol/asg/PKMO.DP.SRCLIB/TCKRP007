      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKRP007.                                00050000
       AUTHOR.                 BARB SCHULTZ.                            00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            OCTOBER 2002.                            00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *                                                               * 00120000
      *  THIS PROGRAM REMOVES OLD RECORDS FROM THE UNMATCHED MERCHANT * 00130000
      *  ACTIVITY FILE AND THE UNMATCHED COSA FILE.  IT THEN WRITES   * 00140000
      *  OUT NEW UNMATCHED FILES WITHOUT THE OLD RECORDS.  A REPORT   * 00150000
      *  FILE IS ALSO CREATED FROM THE DELETED RECORDS.               * 00160000
      *   INPUT: INP01 - UNMATCHED MERCHANT ACTIVITY FILE             * 00170000
      *          INP02 - UNMATCHED COSA FILE                          * 00180000
      *          INP03 - CONTROL DATE                                 * 00190000
      *  OUTPUT: OUT01 - NEW UNMATCHED MERCHANT ACTIVITY FILE         * 00200000
      *          OUT02 - NEW UNMATCHED COSA FILE                      * 00210000
      *          RPT01 - REPORT SHOWING ALL DELETED RECORDS           * 00220000
      *                                                               * 00230000
      ***************************************************************** 00240000
      ***************************************************************** 00250000
       ENVIRONMENT DIVISION.                                            00260000
      ***************************************************************** 00270000
                                                                        00280000
       CONFIGURATION SECTION.                                           00290000
                                                                        00300000
       SOURCE-COMPUTER.        IBM-3090.                                00310000
                                                                        00320000
       OBJECT-COMPUTER.        IBM-3090.                                00330000
                                                                        00340000
       INPUT-OUTPUT SECTION.                                            00350000
                                                                        00360000
       FILE-CONTROL.                                                    00370000
                                                                        00380000
           SELECT UNMATCH-MAF-IN-FILE                                   00390000
               ASSIGN TO INP01.                                         00400000
                                                                        00410000
           SELECT UNMATCH-COSA-IN-FILE                                  00420000
               ASSIGN TO INP02.                                         00430000
                                                                        00440000
           SELECT CONTROL-CARD-FILE                                     00450000
               ASSIGN TO INP03.                                         00460000
                                                                        00470000
           SELECT UNMATCH-MAF-OUT-FILE                                  00480000
               ASSIGN TO OUT01.                                         00490000
                                                                        00500000
           SELECT UNMATCH-COSA-OUT-FILE                                 00510000
               ASSIGN TO OUT02.                                         00520000
                                                                        00530000
           SELECT DELETED-MAF-FILE                                      00540000
               ASSIGN TO OUT03.                                         00550000
                                                                        00560000
           SELECT DELETED-COSA-FILE                                     00570000
               ASSIGN TO OUT04.                                         00580000
                                                                        00590000
           SELECT DELETED-REPORT-FILE                                   00600000
               ASSIGN TO RPT01.                                         00610000
                                                                        00620000
      ******************************************************************00630000
       DATA DIVISION.                                                   00640000
      ******************************************************************00650000
                                                                        00660000
       FILE SECTION.                                                    00670000
                                                                        00680000
       FD  UNMATCH-MAF-IN-FILE                                          00690000
           RECORDING MODE IS F                                          00700000
           BLOCK CONTAINS 0 RECORDS                                     00710000
           LABEL RECORDS ARE STANDARD.                                  00720000
                                                                        00730000
       01  UNMATCH-MAF-IN-RECORD   PIC X(80).                           00740000
                                                                        00750000
       FD  UNMATCH-COSA-IN-FILE                                         00760000
           RECORDING MODE IS F                                          00770000
           BLOCK CONTAINS 0  RECORDS.                                   00780000
                                                                        00790000
       01  UNMATCH-COSA-IN-RECORD  PIC  X(69).                          00800000
                                                                        00810000
       FD  CONTROL-CARD-FILE                                            00820000
           RECORDING MODE IS F                                          00830000
           BLOCK CONTAINS 0 RECORDS.                                    00840000
                                                                        00850000
       01  CONTROL-CARD-RECORD     PIC X(80).                           00860000
                                                                        00870000
       FD  UNMATCH-MAF-OUT-FILE                                         00880000
           RECORDING MODE IS F                                          00890000
           BLOCK CONTAINS 0 RECORDS.                                    00900000
                                                                        00910000
       01  UNMATCH-MAF-OUT-RECORD  PIC X(80).                           00920000
                                                                        00930000
       FD  UNMATCH-COSA-OUT-FILE                                        00940000
           RECORDING MODE IS F                                          00950000
           BLOCK CONTAINS 0  RECORDS.                                   00960000
                                                                        00970000
       01  UNMATCH-COSA-OUT-RECORD PIC  X(69).                          00980000
                                                                        00990000
       FD  DELETED-MAF-FILE                                             01000000
           RECORDING MODE IS F                                          01010000
           BLOCK CONTAINS 0 RECORDS                                     01020000
           LABEL RECORDS ARE STANDARD.                                  01030000
                                                                        01040000
       01  DELETED-MAF-RECORD      PIC X(80).                           01050000
                                                                        01060000
       FD  DELETED-COSA-FILE                                            01070000
           RECORDING MODE IS F                                          01080000
           BLOCK CONTAINS 0  RECORDS.                                   01090000
                                                                        01100000
       01  DELETED-COSA-RECORD     PIC  X(69).                          01110000
                                                                        01120000
       FD  DELETED-REPORT-FILE                                          01130000
           RECORDING MODE IS F                                          01140000
           BLOCK CONTAINS 0  RECORDS.                                   01150000
                                                                        01160000
       01  DELETED-REPORT-RECORD   PIC  X(132).                         01170000
                                                                        01180000
      ******************************************************************01190000
       WORKING-STORAGE SECTION.                                         01200000
      ******************************************************************01210000
       01  FILLER                            PIC X(50)  VALUE           01220000
           ' *** WORKING STORAGE STARTS HERE FOR TCKRP007 *** '.        01230000
                                                                        01240000
      ******************************************************************01250000
      *    PRINT LINE WORK AREAS / STANDARD HEADER                     *01260000
      ******************************************************************01270000
           COPY DPWS132O.                                               01280000
                                                                        01290000
      ******************************************************************01300000
      * RECORD LAYOUT FOR INPUT CARD - HOW MANY DAYS OLD CAN A         *01310000
      * TRANSACTION BE BEFORE WE TAKE IT OFF OF THE UNMATCHED FILES.   *01320000
      * EXAMPLE - 040  ANYTHING OLDER THAN 40 DAYS GETS REMOVED FROM   *01330000
      * THE UNMATCHED FILES AND PUT ON THE DELETED FILES AND ON TO THE *01340000
      * REPORT.                                                        *01350000
      ******************************************************************01360000
       01  CC-CONTROL-CARD.                                             01370000
           05  FILLER                   PIC X(24)    VALUE              01380000
              'TRANSACTIONS OLDER THAN '.                               01390000
           05  CC-DAYS                  PIC 9(03)    VALUE ZERO.        01400000
           05  FILLER                   PIC X(17)    VALUE              01410000
              ' DAYS ARE DELETED'.                                      01420000
           05  FILLER                   PIC X(28)    VALUE SPACE.       01430000
                                                                        01440000
      ******************************************************************01450000
      *   ABEND CODE - AND NUMBER FOR EACH POSSIBLE TYPE OF ABEND.     *01460000
      ******************************************************************01470000
       01  AC-ABEND-CODE                    PIC S9(04) VALUE ZERO       01480000
                                                       COMP SYNC.       01490000
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.     01500000
           88  AC-CALENDAR-ROUTINE-ERROR               VALUE +4019.     01510000
           88  AC-TABLE-OVERFLOW                       VALUE +4004.     01520000
                                                                        01530000
                                                                        01540000
       01  SUBSCRIPTS.                                                  01550000
           05  SUB                          PIC S9(04) VALUE ZERO       01560000
                                                       COMP.            01570000
      ******************************************************************01580000
      *   PROGRAM SWITCHES                                             *01590000
      ******************************************************************01600000
       01  PS-PROGRAM-SWITCHES.                                         01610000
           05  PS-END-OF-COSA-CSR-SW         PIC X(01)  VALUE 'N'.      01620000
               88  PS-END-OF-COSA-FILE                  VALUE 'Y'.      01630000
           05  PS-END-OF-MAF-FILE-SW         PIC X(01)  VALUE 'N'.      01640000
               88  PS-END-OF-MAF-FILE                   VALUE 'Y'.      01650000
           05  PS-END-OF-CONTROL-FILE-SW     PIC  X(01) VALUE 'N'.      01660000
               88  PS-END-OF-CONTROL-FILE               VALUE 'Y'.      01670000
                                                                        01680000
      ******************************************************************01690000
      *   PROGRAM CONSTANTS                                            *01700000
      ******************************************************************01710000
       01  PC-CONSTANTS.                                                01720000
           05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.     01730000
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01740000
                                                         'TCKRP007'.    01750000
           05  PC-REPORT-NBR-1             PIC 9(01)     VALUE 1.       01760000
                                                                        01770000
      ******************************************************************01780000
      *   PROGRAM ACCUMULATERS.                                        *01790000
      ******************************************************************01800000
       01  PA-ACCUMULATORS                 COMP-3.                      01810000
           05  PA-PAGE-CNT-1               PIC S9(05)    VALUE ZEROES.  01820000
           05  PA-LINE-CNT-1               PIC S9(03)    VALUE ZEROES.  01830000
           05  PA-COSA-FILE-RECS-READ      PIC S9(07)    VALUE ZEROES.  01840000
           05  PA-MAF-FILE-RECS-READ       PIC S9(07)    VALUE ZEROES.  01850000
           05  PA-UNMATCH-MAF-OUT          PIC S9(07)    VALUE ZEROES.  01860000
           05  PA-UNMATCH-COSA-OUT         PIC S9(07)    VALUE ZEROES.  01870000
           05  PA-DELETE-MAF               PIC S9(07)    VALUE ZEROES.  01880000
           05  PA-DELETE-COSA              PIC S9(07)    VALUE ZEROES.  01890000
                                                                        01900000
      ******************************************************************01910000
      *   PROGRAM VARIABLES.                                           *01920000
      ******************************************************************01930000
       01  PV-PROGRAM-VARIABLES.                                        01940000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.  01950000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACES.  01960000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACES.  01970000
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.  01980000
           05  PV-DAYS                PIC S9(03)    VALUE ZERO COMP-3.  01990000
                                                                        02000000
      ******************************************************************02010000
      *   DATE AND TIME FIELDS.                                        *02020000
      ******************************************************************02030000
       01  PV-DATE-AND-TIME-FIELDS.                                     02040000
           05  PV-DELETE-DATE              PIC X(10)     VALUE SPACES.  02050000
           05  PV-CURR-DATE-CCYYMMDD.                                   02060000
               10  PV-CURR-DATE-CCYY       PIC 9(04)     VALUE ZEROES.  02070000
               10  PV-CURR-DATE-MM         PIC 9(02)     VALUE ZEROES.  02080000
               10  PV-CURR-DATE-DD         PIC 9(02)     VALUE ZEROES.  02090000
           05  PV-CURR-TIME.                                            02100000
               10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZEROES.  02110000
               10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZEROES.  02120000
               10  PV-CURR-TIME-SS         PIC  9(02)    VALUE ZEROES.  02130000
           05  PV-SPLIT-TIME.                                           02140000
               10  PV-SPLIT-TIME-HH        PIC  9(02)    VALUE ZEROES.  02150000
               10  PV-SPLIT-TIME-MM        PIC  9(02)    VALUE ZEROES.  02160000
               10  PV-SPLIT-TIME-SS        PIC  9(02)    VALUE ZEROES.  02170000
           05  PV-SPLIT-TIME-S.                                         02180000
               10  PV-TIME-HH-S            PIC  9(02)    VALUE ZEROES.  02190000
               10  FILLER                  PIC  X(01)    VALUE ':'.     02200000
               10  PV-TIME-MM-S            PIC  9(02)    VALUE ZEROES.  02210000
               10  FILLER                  PIC  X(01)    VALUE ':'.     02220000
               10  PV-TIME-SS-S            PIC  9(02)    VALUE ZEROES.  02230000
           05  PV-SPLIT-DATE.                                           02240000
               10  PV-CC-S                 PIC  9(02)    VALUE 20.      02250000
               10  PV-YY-S                 PIC  9(02)    VALUE ZEROES.  02260000
               10  PV-S1                   PIC  X(01)    VALUE '-'.     02270000
               10  PV-MM-S                 PIC  9(02)    VALUE ZEROES.  02280000
               10  PV-S2                   PIC  X(01)    VALUE '-'.     02290000
               10  PV-DD-S                 PIC  9(02)    VALUE ZEROES.  02300000
           05  PV-DB2-DATE REDEFINES PV-SPLIT-DATE PIC X(10).           02310000
           05  PV-SPLIT-DATE-CCYYMMDD.                                  02320000
               10  PV-SPLIT-DATE-CCYY.                                  02330000
                   15  PV-SPLIT-DATE-CC    PIC  9(02)    VALUE ZEROES.  02340000
                   15  PV-SPLIT-DATE-YY    PIC  9(02)    VALUE ZEROES.  02350000
               10  PV-SPLIT-DATE-MM        PIC  9(02)    VALUE ZEROES.  02360000
               10  PV-SPLIT-DATE-DD        PIC  9(02)    VALUE ZEROES.  02370000
           05  PV-COMP-DATE REDEFINES PV-SPLIT-DATE-CCYYMMDD PIC X(08). 02380000
           05  PV-HOLD-DATE                PIC  X(08)    VALUE ZEROES.  02390000
           05  PV-SPLIT-CCYY.                                           02400000
               10  PV-CC                   PIC  9(02)    VALUE ZEROES.  02410000
               10  PV-YY                   PIC  9(02)    VALUE ZEROES.  02420000
           05  PV-SPLIT-DATE-1.                                         02430000
               10  PV-SPLIT-MM             PIC  9(02)    VALUE ZEROES.  02440000
               10  PV-SLASH-1              PIC  X(01)    VALUE '/'.     02450000
               10  PV-SPLIT-DD             PIC  9(02)    VALUE ZEROES.  02460000
               10  PV-SLASH-2              PIC  X(01)    VALUE '/'.     02470000
               10  PV-SPLIT-CC             PIC  9(02)    VALUE 20.      02480000
               10  PV-SPLIT-YY             PIC  9(02)    VALUE ZEROES.  02490000
           05  PV-TRAN-DATE.                                            02500000
               10  PV-TRAN-MM              PIC  9(02)    VALUE ZEROES.  02510000
               10  PV-SLASH-1              PIC  X(01)    VALUE '/'.     02520000
               10  PV-TRAN-DD              PIC  9(02)    VALUE ZEROES.  02530000
               10  PV-SLASH-2              PIC  X(01)    VALUE '/'.     02540000
               10  PV-TRAN-CCYY            PIC  9(02)    VALUE ZEROES.  02550000
                                                                        02560000
           05  PV-CARD-NUMBER.                                          02570000
               10  PV-SPACE-CARD-NUMBER      PIC  X(12)                 02580000
                                                  VALUE 'XXXXXXXXXXXX'. 02590000
               10  PV-CARD-NBR-1             PIC  X(03) VALUE SPACE.    02591000
               10  PV-CARD-NBR-2             PIC  X(01) VALUE SPACE.    02592000
               10  PV-CARD-NBR-3             PIC  X(03) VALUE SPACE.    02593000
                                                                        02594000
                                                                        02595000
      ******************************************************************02596000
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 132 CHARACTER REPORT. *02597000
      ******************************************************************02598000
           COPY DPWS132.                                                02599000
                                                                        02600000
       01  SPACE-LINE.                                                  02610000
           05  FILLER                      PIC  X(132)   VALUE SPACES.  02620000
                                                                        02630000
      ******************************************************************02640000
      *    STANDARD DB2 ERROR HANDLING AREA                            *02650000
      ******************************************************************02660000
           COPY DPWS004.                                                02670000
                                                                        02680000
      ******************************************************************02690000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)            *02700000
      ******************************************************************02710000
           COPY DPWS500.                                                02720000
                                                                        02730000
      ******************************************************************02740000
      *    STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA             *02750000
      ******************************************************************02760000
           EXEC SQL                                                     02770000
                INCLUDE SQLCA                                           02780000
           END-EXEC.                                                    02790000
                                                                        02800000
           COPY SQLCA2.                                                 02810000
                                                                        02820000
      ******************************************************************02830000
      *  HEADING LINES DEBIT EXCEPTION REPORT                          *02840000
      ******************************************************************02850000
       01  T1-TITLE-LINE-1.                                             02860000
           05  FILLER                      PIC  X(45)    VALUE SPACES.  02870000
           05  T1-TITLE                    PIC  X(48)    VALUE          02880000
               'DELETED DEBIT UNMATCHED TRANSACTIONS REPORT'.           02890000
           05  FILLER                      PIC  X(44)    VALUE SPACES.  02900000
                                                                        02910000
       01  H1-COLUMN-HEADING-1.                                         02920000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  02930000
           05  FILLER                      PIC  X(04)    VALUE          02940000
               'DATE'.                                                  02950000
           05  FILLER                      PIC  X(08)    VALUE SPACES.  02960000
           05  FILLER                      PIC  X(04)    VALUE          02970000
               'TIME'.                                                  02980000
           05  FILLER                      PIC  X(05)    VALUE SPACES.  02990000
           05  FILLER                      PIC  X(05)    VALUE          03000000
               'STORE'.                                                 03010000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03020000
           05  FILLER                      PIC  X(03)    VALUE          03030000
               'REG'.                                                   03040000
           05  FILLER                      PIC  X(05)    VALUE SPACES.  03050000
           05  FILLER                      PIC  X(04)    VALUE          03060000
               'TRAN'.                                                  03070000
           05  FILLER                      PIC  X(07)    VALUE SPACES.  03080000
           05  FILLER                      PIC  X(11)    VALUE          03090000
               'CARD NUMBER'.                                           03100000
           05  FILLER                      PIC  X(12)    VALUE SPACES.  03110000
           05  FILLER                      PIC  X(05)    VALUE          03120000
               'SALES'.                                                 03130000
           05  FILLER                      PIC  X(05)    VALUE SPACES.  03140000
           05  FILLER                      PIC  X(09)    VALUE          03150000
               'SALE TYPE'.                                             03160000
           05  FILLER                      PIC  X(05)    VALUE SPACES.  03170000
           05  FILLER                      PIC  X(14)    VALUE          03180000
               'TYPE OF RECORD'.                                        03190000
           05  FILLER                      PIC  X(20)    VALUE SPACES.  03200000
                                                                        03210000
       01  NO-MAF-DELETE-LINE.                                          03220000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03230000
           05  FILLER                      PIC  X(29)    VALUE          03240000
               'NO MAF DELETIONS FOR THIS RUN'.                         03250000
           05  FILLER                      PIC  X(99)    VALUE SPACES.  03260000
                                                                        03270000
       01  NO-COSA-DELETE-LINE.                                         03280000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03290000
           05  FILLER                      PIC  X(30)    VALUE          03300000
               'NO COSA DELETIONS FOR THIS RUN'.                        03310000
           05  FILLER                      PIC  X(98)    VALUE SPACES.  03320000
                                                                        03321000
      ******************************************************************03322000
      *  DETAIL LINE.                                                   03323000
      ******************************************************************03324000
       01  D1-DETAIL-LINE-1.                                            03325000
           05  FILLER                      PIC  X(01)    VALUE SPACES.  03326000
           05  D1-DATE                     PIC  X(10).                  03327000
           05  FILLER                      PIC  X(03)    VALUE SPACES.  03328000
           05  D1-TIME                     PIC  X(08).                  03329000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03330000
           05  D1-STORE                    PIC  X(04).                  03340000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03350000
           05  D1-REG                      PIC  X(04).                  03360000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03370000
           05  D1-TRAN                     PIC  X(04).                  03380000
           05  FILLER                      PIC  X(05)    VALUE SPACES.  03390000
           05  D1-CARD                     PIC  X(19).                  03400000
           05  FILLER                      PIC  X(02)    VALUE SPACES.  03410000
           05  D1-SALES-AMT                PIC  ZZZZZZ.99-.             03420000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03430000
           05  D1-SALE-TYPE                PIC  X(10).                  03440000
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03450000
           05  D1-RECORD-TYP               PIC  X(29).                  03460000
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03470000
                                                                        03480000
      ******************************************************************03490000
      *  COSA FILE LAYOUT BUILT IN TCKUT002                            *03500000
      ******************************************************************03510000
           COPY TCWS002.                                                03520000
                                                                        03530000
                                                                        03540000
      ******************************************************************03550000
      *  MERCHANT ACTIVITY LAYOUT BUILT IN TCKUT001                    *03560000
      ******************************************************************03570000
           COPY TCWS003.                                                03580000
                                                                        03590000
      ***************************************************************** 03600000
      * PROCEDURE DIVISION.                                           * 03610000
      ***************************************************************** 03620000
       PROCEDURE DIVISION.                                              03630000
                                                                        03640000
      ******************************************************************03650000
      * MAINLINE PARAGRAPH                                             *03660000
      ******************************************************************03670000
       0000-MAINLINE-PARAGRAPH.                                         03680000
                                                                        03690000
           PERFORM 1000-INITIALIZE-PROGRAM.                             03700000
                                                                        03710000
           PERFORM 2000-PROCESS-MAF-DATA                                03720000
             UNTIL PS-END-OF-MAF-FILE.                                  03730000
                                                                        03740000
           IF PA-DELETE-MAF > 0                                         03750000
              PERFORM 4700-WRITE-BLANK-LINE                             03760000
           END-IF.                                                      03770000
                                                                        03780000
           PERFORM 3000-PROCESS-COSA-DATA                               03790000
             UNTIL PS-END-OF-COSA-FILE.                                 03800000
                                                                        03810000
           PERFORM 8000-END-PROGRAM.                                    03820000
                                                                        03830000
           GOBACK.                                                      03840000
                                                                        03850000
      ******************************************************************03860000
      *  -  OPEN FILES                                                  03870000
      *  -  READ CONTROL CARD                                           03880000
      *  -  FORMAT REPORT HEADINGS                                      03890000
      *  -  READ 1ST RECORD ON MAF FILE                                 03900000
      *  -  READ 1ST RECORD ON COSA FILE                                03910000
      ******************************************************************03920000
       1000-INITIALIZE-PROGRAM.                                         03930000
                                                                        03940000
           OPEN INPUT  UNMATCH-MAF-IN-FILE                              03950000
                       UNMATCH-COSA-IN-FILE                             03960000
                       CONTROL-CARD-FILE                                03970000
                OUTPUT UNMATCH-MAF-OUT-FILE                             03980000
                       UNMATCH-COSA-OUT-FILE                            03990000
                       DELETED-MAF-FILE                                 04000000
                       DELETED-COSA-FILE                                04010000
                       DELETED-REPORT-FILE.                             04020000
                                                                        04030000
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  04040000
              AT END                                                    04050000
                 SET PS-END-OF-CONTROL-FILE TO TRUE.                    04060000
                                                                        04070000
           IF PS-END-OF-CONTROL-FILE                                    04080000
              DISPLAY 'CONTROL CARD MISSING '                           04090000
              MOVE '1000-INITIALIZE-PROGRAM ' TO PV-PARAGRAPH-NAME      04100000
              SET AC-CONTROL-CARD-ERROR TO TRUE                         04110000
              PERFORM 9999-ABEND                                        04120000
           ELSE                                                         04130000
              IF CC-DAYS < +1                                           04140000
                 DISPLAY 'DAYS ON CONTROL CARD INVALID ' CC-DAYS        04150000
                 MOVE '1000-INITIALIZE-PROGRAM ' TO PV-PARAGRAPH-NAME   04160000
                 SET AC-CONTROL-CARD-ERROR TO TRUE                      04170000
                 PERFORM 9999-ABEND                                     04180000
              ELSE                                                      04190000
                 DISPLAY PC-PROGRAM-NAME ' ' CC-CONTROL-CARD            04200000
                 PERFORM 1100-GET-DELETION-DATE                         04210000
              END-IF                                                    04220000
           END-IF.                                                      04230000
                                                                        04240000
           PERFORM 1400-FORMAT-HEADINGS.                                04250000
                                                                        04260000
           PERFORM 5000-READ-MAF-FILE.                                  04270000
                                                                        04280000
           PERFORM 5100-READ-COSA-FILE.                                 04290000
                                                                        04300000
      ***************************************************************** 04310000
      *     GET THE DELETION DATE. ANY TRANSACTION WITH A TRANSACTION   04320000
      *     DATE LESS THAN THIS DATE WILL BE DELETED.                   04330000
      ***************************************************************** 04340000
       1100-GET-DELETION-DATE.                                          04350000
                                                                        04360000
           MOVE CC-DAYS TO PV-DAYS.                                     04370000
                                                                        04380000
           EXEC SQL                                                     04390000
              SELECT CURRENT DATE - :PV-DAYS DAYS                       04400000
              INTO :PV-DELETE-DATE                                      04410000
              FROM SYSIBM.SYSDUMMY1                                     04420000
           END-EXEC.                                                    04430000
                                                                        04440000
           EVALUATE TRUE                                                04450000
               WHEN SQLCODE = +0                                        04460000
                   MOVE PV-DELETE-DATE TO PV-SPLIT-DATE                 04470000
                   MOVE PV-CC-S        TO PV-SPLIT-DATE-CC              04480000
                   MOVE PV-YY-S        TO PV-SPLIT-DATE-YY              04490000
                   MOVE PV-MM-S        TO PV-SPLIT-DATE-MM              04500000
                   MOVE PV-DD-S        TO PV-SPLIT-DATE-DD              04510000
                   MOVE PV-COMP-DATE   TO PV-HOLD-DATE                  04520000
               WHEN OTHER                                               04530000
                   MOVE '1100-GET-DELETION-DATE '                       04540000
                                     TO PV-PARAGRAPH-NAME               04550000
                   MOVE 'PROBLEM GETTING DELETE DATE  '                 04560000
                                     TO PV-DB2-OPERATION-ATTEMPTED      04570000
                   PERFORM 9900-SQL-ABEND                               04580000
           END-EVALUATE.                                                04590000
                                                                        04600000
      ******************************************************************04610000
      *     GET CURRENT DATE AND TIME FROM SYSTEM.                      04620000
      *     FORMAT REPORT FIELDS IN THE TOP PAGE HEADING LINE.          04630000
      ******************************************************************04640000
       1400-FORMAT-HEADINGS.                                            04650000
                                                                        04660000
           MOVE FUNCTION CURRENT-DATE (1:8) TO PV-CURR-DATE-CCYYMMDD.   04670000
           ACCEPT PV-CURR-TIME FROM TIME.                               04680000
                                                                        04690000
           MOVE PV-CURR-DATE-MM             TO DP132-RUN-MONTH.         04700000
           MOVE PV-CURR-DATE-DD             TO DP132-RUN-DAY.           04710000
           MOVE PV-CURR-DATE-CCYY           TO DP132-RUN-YEAR.          04720000
                                                                        04730000
           MOVE PV-CURR-TIME-HH             TO DP132-RUN-HOUR.          04740000
           MOVE PV-CURR-TIME-MM             TO DP132-RUN-MINUTE.        04750000
           MOVE PC-PROGRAM-NAME             TO DP132-PROGRAM-NAME.      04760000
           MOVE PC-REPORT-NBR-1             TO DP132-REPORT-NUMBER.     04770000
                                                                        04780000
           PERFORM 4600-WRITE-PAGE-HEADINGS-1.                          04790000
                                                                        04800000
      ******************************************************************04810000
      * PROCESS THE MAF RECORDS                                        *04820000
      * IF THE MAF TRANSACTION IS OLDER THAN THE DETERMINED DATE       *04830000
      *    WRITE A LINE ON THE REPORT AND ON TO THE DELETED FILE.      *04840000
      * IF THE MAF TRANSACTION IS NOT OLDER THAN THE DETERMINED DATE   *04850000
      *    WRITE THE RECORD BACK OUT ON TO THE UNMATCHED MAF FILE.     *04860000
      ******************************************************************04870000
       2000-PROCESS-MAF-DATA.                                           04880000
                                                                        04890000
           IF TC003-SLS-DTE > PV-HOLD-DATE                              04900000
              MOVE UNMATCH-MAF-IN-RECORD TO UNMATCH-MAF-OUT-RECORD      04910000
              PERFORM 4300-WRITE-UNMATCH-MAF-RECORD                     04920000
           ELSE                                                         04930000
              MOVE UNMATCH-MAF-IN-RECORD TO DELETED-MAF-RECORD          04940000
              PERFORM 4000-WRITE-MAF-DETAIL-LINE-1                      04950000
              PERFORM 4500-WRITE-DELETED-MAF-RECORD                     04960000
           END-IF.                                                      04970000
                                                                        04980000
           IF PS-END-OF-MAF-FILE                                        04990000
              CONTINUE                                                  05000000
           ELSE                                                         05010000
              PERFORM 5000-READ-MAF-FILE                                05020000
           END-IF.                                                      05030000
                                                                        05040000
      ***************************************************************** 05050000
      * PROCESS THE COSA RECORDS                                      * 05060000
      * IF THE COSA TRANSACTION IS OLDER THAN THE DETERMINED DATE     * 05070000
      *    WRITE A LINE ON THE REPORT AND ON TO THE DELETED FILE      * 05080000
      * IF THE COSA TRANSACTION IS OLDER THAN THE DETERMINED DATE     * 05090000
      *    WRITE THE RECORD BACK OUT ON TO THE UNMATCHED COSA FILE    * 05100000
      ***************************************************************** 05110000
       3000-PROCESS-COSA-DATA.                                          05120000
                                                                        05130000
           IF TC002-SLS-DTE > PV-HOLD-DATE                              05140000
              MOVE UNMATCH-COSA-IN-RECORD TO UNMATCH-COSA-OUT-RECORD    05150000
              PERFORM 4200-WRITE-UNMATCH-COSA-RECORD                    05160000
           ELSE                                                         05170000
              MOVE UNMATCH-COSA-IN-RECORD TO DELETED-COSA-RECORD        05180000
              PERFORM 4100-WRITE-COSA-DETAIL-LINE-1                     05190000
              PERFORM 4400-WRITE-DELETED-COSA-RECORD                    05200000
           END-IF.                                                      05210000
                                                                        05220000
           IF PS-END-OF-COSA-FILE                                       05230000
              CONTINUE                                                  05240000
           ELSE                                                         05250000
              PERFORM 5100-READ-COSA-FILE                               05260000
           END-IF.                                                      05270000
                                                                        05280000
      ******************************************************************05290000
      * WRITE MAF DETAIL LINE                                          *05300000
      ******************************************************************05310000
       4000-WRITE-MAF-DETAIL-LINE-1.                                    05320000
                                                                        05330000
           IF PA-LINE-CNT-1 > PC-LINE-LIMIT                             05340000
              PERFORM 4600-WRITE-PAGE-HEADINGS-1                        05350000
           END-IF.                                                      05360000
                                                                        05370000
           MOVE TC003-SLS-DTE      TO PV-SPLIT-DATE-CCYYMMDD.           05380000
           MOVE PV-SPLIT-DATE-MM   TO PV-SPLIT-MM.                      05390000
           MOVE PV-SPLIT-DATE-DD   TO PV-SPLIT-DD.                      05400000
           MOVE PV-SPLIT-DATE-CCYY TO PV-SPLIT-CCYY.                    05410000
           MOVE PV-CC              TO PV-SPLIT-CC.                      05420000
           MOVE PV-YY              TO PV-SPLIT-YY.                      05430000
           MOVE PV-SPLIT-DATE-1    TO D1-DATE.                          05440000
           MOVE TC003-STRT-TM      TO PV-SPLIT-TIME.                    05450000
           MOVE PV-SPLIT-TIME-HH   TO PV-TIME-HH-S.                     05460000
           MOVE PV-SPLIT-TIME-MM   TO PV-TIME-MM-S.                     05470000
           MOVE PV-SPLIT-TIME-SS   TO PV-TIME-SS-S.                     05480000
           MOVE PV-SPLIT-TIME-S    TO D1-TIME.                          05490000
           MOVE TC003-STORE-NBR    TO D1-STORE.                         05500000
           MOVE TC003-REG-NBR      TO D1-REG.                           05510000
           MOVE TC003-TRAN-NBR     TO D1-TRAN.                          05520000
           MOVE TC003-CARD-NBR     TO PV-CARD-NUMBER.                   05530000
           IF PV-CARD-NBR-3 NUMERIC                                     05540000
              MOVE 'XXXXXXXXXXXX'  TO PV-SPACE-CARD-NUMBER              05550000
              MOVE 'XXXX'          TO PV-CARD-NBR-1                     05560000
           ELSE                                                         05570000
              MOVE 'XXXXXXXXXXXX'  TO PV-SPACE-CARD-NUMBER              05580000
           END-IF.                                                      05590000
           MOVE PV-CARD-NUMBER     TO D1-CARD.                          05600000
           MOVE TC003-TRAN-AMT     TO D1-SALES-AMT.                     05610000
           IF TC003-MAF-REC                                             05620000
              MOVE 'MAF      '     TO D1-SALE-TYPE                      05630000
           ELSE                                                         05640000
              MOVE 'ADJUST   '     TO D1-SALE-TYPE                      05650000
           END-IF.                                                      05660000
           MOVE 'MERCHANT ACTIVITY FILE RECORD' TO D1-RECORD-TYP.       05670000
                                                                        05680000
           WRITE DELETED-REPORT-RECORD                                  05690000
             FROM D1-DETAIL-LINE-1                                      05700000
             AFTER ADVANCING 1 LINE.                                    05710000
                                                                        05720000
           ADD +1 TO PA-LINE-CNT-1.                                     05730000
                                                                        05740000
      ******************************************************************05750000
      * WRITE COSA DETAIL LINE FOR EXCEPTION REPORT                    *05760000
      ******************************************************************05770000
       4100-WRITE-COSA-DETAIL-LINE-1.                                   05780000
                                                                        05790000
           IF PA-LINE-CNT-1 > PC-LINE-LIMIT                             05800000
              PERFORM 4600-WRITE-PAGE-HEADINGS-1                        05810000
           END-IF.                                                      05820000
                                                                        05830000
           MOVE TC002-SLS-DTE      TO PV-SPLIT-DATE-CCYYMMDD.           05840000
           MOVE PV-SPLIT-DATE-MM   TO PV-SPLIT-MM.                      05850000
           MOVE PV-SPLIT-DATE-DD   TO PV-SPLIT-DD.                      05860000
           MOVE PV-SPLIT-DATE-CCYY TO PV-SPLIT-CCYY.                    05870000
           MOVE PV-CC              TO PV-SPLIT-CC.                      05880000
           MOVE PV-YY              TO PV-SPLIT-YY.                      05890000
           MOVE PV-SPLIT-DATE-1    TO D1-DATE.                          05900000
           MOVE TC002-STRT-TIME    TO PV-SPLIT-TIME                     05910000
           MOVE PV-SPLIT-TIME-HH   TO PV-TIME-HH-S.                     05920000
           MOVE PV-SPLIT-TIME-MM   TO PV-TIME-MM-S.                     05930000
           MOVE PV-SPLIT-TIME-SS   TO PV-TIME-SS-S.                     05940000
           MOVE PV-SPLIT-TIME-S    TO D1-TIME.                          05950000
           MOVE TC002-STR-NBR      TO D1-STORE.                         05960000
           MOVE TC002-RGST-ID      TO D1-REG.                           05970000
           MOVE TC002-TRN-NBR      TO D1-TRAN.                          05980000
           MOVE TC002-CARD-NBR     TO PV-CARD-NUMBER.                   05990000
           IF PV-CARD-NBR-3 NUMERIC                                     06000000
              MOVE 'XXXXXXXXXXXX'     TO PV-SPACE-CARD-NUMBER           06010000
              MOVE 'XXX'              TO PV-CARD-NBR-1                  06020000
           ELSE                                                         06030000
              MOVE 'XXXXXXXXXXXX'     TO PV-SPACE-CARD-NUMBER           06040000
           END-IF.                                                      06050000
           MOVE PV-CARD-NUMBER     TO D1-CARD.                          06060000
           MOVE TC002-TNDR-AMT     TO D1-SALES-AMT.                     06070000
           EVALUATE TC002-SALE-TYPE                                     06080000
               WHEN 'C'                                                 06090000
                  MOVE 'CORRECTION'   TO D1-SALE-TYPE                   06100000
               WHEN 'L'                                                 06110000
                  MOVE 'LATE      '   TO D1-SALE-TYPE                   06120000
               WHEN 'W'                                                 06130000
                  MOVE 'LATE VOID '   TO D1-SALE-TYPE                   06140000
               WHEN 'R'                                                 06150000
                  MOVE 'REVERSAL  '   TO D1-SALE-TYPE                   06160000
               WHEN 'X'                                                 06170000
                  MOVE 'RETURN    '   TO D1-SALE-TYPE                   06180000
               WHEN 'S'                                                 06190000
                  MOVE 'SALE      '   TO D1-SALE-TYPE                   06200000
               WHEN 'V'                                                 06210000
                  MOVE 'VOID      '   TO D1-SALE-TYPE                   06220000
           END-EVALUATE.                                                06230000
           MOVE 'COSA RECORD                  ' TO D1-RECORD-TYP.       06240000
                                                                        06250000
           WRITE DELETED-REPORT-RECORD                                  06260000
             FROM D1-DETAIL-LINE-1                                      06270000
             AFTER ADVANCING 1 LINE.                                    06280000
                                                                        06290000
           ADD +1 TO PA-LINE-CNT-1.                                     06300000
                                                                        06310000
      ******************************************************************06320000
      * WRITE COSA RECORD TO OUTPUT FILE                               *06330000
      ******************************************************************06340000
       4200-WRITE-UNMATCH-COSA-RECORD.                                  06350000
                                                                        06360000
           WRITE UNMATCH-COSA-OUT-RECORD FROM UNMATCH-COSA-IN-RECORD.   06370000
           ADD +1 TO PA-UNMATCH-COSA-OUT.                               06380000
                                                                        06390000
      ******************************************************************06400000
      * WRITE UNMATCHED MAF RECORD TO OUTPUT FILE                      *06410000
      ******************************************************************06420000
       4300-WRITE-UNMATCH-MAF-RECORD.                                   06430000
                                                                        06440000
           WRITE UNMATCH-MAF-OUT-RECORD FROM UNMATCH-MAF-IN-RECORD.     06450000
           ADD +1 TO PA-UNMATCH-MAF-OUT.                                06460000
                                                                        06470000
      ******************************************************************06480000
      * WRITE DELETED COSA RECORD TO OUTPUT FILE                        06490000
      ******************************************************************06500000
       4400-WRITE-DELETED-COSA-RECORD.                                  06510000
                                                                        06520000
           WRITE DELETED-COSA-RECORD FROM UNMATCH-COSA-IN-RECORD.       06530000
           ADD +1 TO PA-DELETE-COSA.                                    06540000
                                                                        06550000
      ******************************************************************06560000
      * WRITE DELETED UNMATCHED MAF RECORD TO OUTPUT FILE               06570000
      ******************************************************************06580000
       4500-WRITE-DELETED-MAF-RECORD.                                   06590000
                                                                        06600000
           WRITE DELETED-MAF-RECORD FROM UNMATCH-MAF-IN-RECORD.         06610000
           ADD +1 TO PA-DELETE-MAF.                                     06620000
                                                                        06630000
      ******************************************************************06640000
      * WRITE PAGE HEADINGS FOR EXCEPTION REPORT                       *06650000
      ******************************************************************06660000
       4600-WRITE-PAGE-HEADINGS-1.                                      06670000
                                                                        06680000
           MOVE +5 TO PA-LINE-CNT-1.                                    06690000
           ADD  +1 TO PA-PAGE-CNT-1.                                    06700000
                                                                        06710000
           MOVE PA-PAGE-CNT-1 TO DP132-PAGE-NUMBER.                     06720000
                                                                        06730000
           WRITE DELETED-REPORT-RECORD                                  06740000
            FROM DP132-STANDRD-HEADER-132-COLS                          06750000
            AFTER ADVANCING PAGE.                                       06760000
                                                                        06770000
           WRITE DELETED-REPORT-RECORD                                  06780000
            FROM T1-TITLE-LINE-1                                        06790000
            AFTER ADVANCING 1 LINE.                                     06800000
                                                                        06810000
           WRITE DELETED-REPORT-RECORD                                  06820000
            FROM H1-COLUMN-HEADING-1                                    06830000
            AFTER ADVANCING 2 LINES.                                    06840000
                                                                        06850000
      ******************************************************************06860000
      * WRITE BLANK LINE                                               *06870000
      ******************************************************************06880000
       4700-WRITE-BLANK-LINE.                                           06890000
                                                                        06900000
           ADD  +1 TO PA-LINE-CNT-1.                                    06910000
                                                                        06920000
           WRITE DELETED-REPORT-RECORD                                  06930000
               FROM SPACE-LINE                                          06940000
               AFTER ADVANCING 1 LINE.                                  06950000
                                                                        06960000
      ******************************************************************06970000
      *    READ SORTED MAF-DATA-FILE                                   *06980000
      ******************************************************************06990000
       5000-READ-MAF-FILE.                                              07000000
                                                                        07010000
           READ UNMATCH-MAF-IN-FILE INTO TC003-MAF-DATA                 07020000
              AT END                                                    07030000
                 SET PS-END-OF-MAF-FILE TO TRUE                         07040000
           END-READ.                                                    07050000
                                                                        07060000
           IF PS-END-OF-MAF-FILE                                        07070000
              CONTINUE                                                  07080000
           ELSE                                                         07090000
              ADD +1 TO PA-MAF-FILE-RECS-READ                           07100000
           END-IF.                                                      07110000
                                                                        07120000
      ***************************************************************** 07130000
      *     READ SORTED COSA FILE                                       07140000
      ***************************************************************** 07150000
       5100-READ-COSA-FILE.                                             07160000
                                                                        07170000
           READ UNMATCH-COSA-IN-FILE INTO TC002-DATA                    07180000
              AT END                                                    07190000
                 SET PS-END-OF-COSA-FILE TO TRUE                        07200000
           END-READ.                                                    07210000
                                                                        07220000
           IF PS-END-OF-COSA-FILE                                       07230000
              CONTINUE                                                  07240000
           ELSE                                                         07250000
              ADD +1 TO PA-COSA-FILE-RECS-READ                          07260000
           END-IF.                                                      07270000
                                                                        07280000
      ******************************************************************07290000
      *     CLOSE FILES AND CURSORS                                    *07300000
      ******************************************************************07310000
       8000-END-PROGRAM.                                                07320000
                                                                        07330000
           IF PA-DELETE-MAF  = 0                                        07340000
               WRITE DELETED-REPORT-RECORD                              07350000
                   FROM SPACE-LINE                                      07360000
                   AFTER ADVANCING 1 LINE                               07370000
               WRITE DELETED-REPORT-RECORD FROM NO-MAF-DELETE-LINE      07380000
                  AFTER ADVANCING 1 LINE                                07390000
               WRITE DELETED-REPORT-RECORD                              07400000
                   FROM SPACE-LINE                                      07410000
                   AFTER ADVANCING 1 LINE                               07420000
           END-IF.                                                      07430000
                                                                        07440000
           IF PA-DELETE-COSA  = 0                                       07450000
               WRITE DELETED-REPORT-RECORD                              07460000
                   FROM SPACE-LINE                                      07470000
                   AFTER ADVANCING 1 LINE                               07480000
               WRITE DELETED-REPORT-RECORD FROM NO-COSA-DELETE-LINE     07490000
                  AFTER ADVANCING 1 LINE                                07500000
               WRITE DELETED-REPORT-RECORD                              07510000
                   FROM SPACE-LINE                                      07520000
                   AFTER ADVANCING 1 LINE                               07530000
           END-IF.                                                      07540000
                                                                        07550000
           DISPLAY 'UNMATCH MAF FILE READ ' PA-MAF-FILE-RECS-READ.      07560000
           DISPLAY 'UNMATCH COSA FILE READ ' PA-COSA-FILE-RECS-READ.    07570000
           DISPLAY 'UNMATCH MAF RECORDS WRITTEN ' PA-UNMATCH-MAF-OUT.   07580000
           DISPLAY 'UNMATCH COSA RECORDS WRITTEN ' PA-UNMATCH-COSA-OUT. 07590000
           DISPLAY 'DELETED MAF RECORDS ' PA-DELETE-MAF.                07600000
           DISPLAY 'DELETED COSA RECORDS ' PA-DELETE-COSA.              07610000
                                                                        07620000
           CLOSE UNMATCH-MAF-IN-FILE                                    07630000
                 UNMATCH-COSA-IN-FILE                                   07640000
                 CONTROL-CARD-FILE                                      07650000
                 UNMATCH-MAF-OUT-FILE                                   07660000
                 UNMATCH-COSA-OUT-FILE                                  07670000
                 DELETED-MAF-FILE                                       07680000
                 DELETED-COSA-FILE                                      07690000
                 DELETED-REPORT-FILE.                                   07700000
                                                                        07710000
      ******************************************************************07720000
      * ==> PROCESSES:                                                 *07730000
      *     -PERFORMS ABEND PROCESSING FOR SQL ERRORS.                 *07740000
      * ==> PERFORMED FROM:  B200-PROCESS-EXCEPTIONS                   *07750000
      *                      D000-FETCH-TKMCACT-CURSOR                 *07760000
      *                      G100-GET-STORE-NAME                       *07760100
      ******************************************************************07760200
       9900-SQL-ABEND.                                                  07760300
                                                                        07760400
           DISPLAY '9900-SQL-ABEND'.                                    07760500
           DISPLAY '**  ABEND     **'.                                  07760600
           DISPLAY '**  PROGRAM   ** = ' PC-PROGRAM-NAME.               07760700
           DISPLAY '**  PARAGRAPH ** = ' PV-PARAGRAPH-NAME.             07760800
           DISPLAY '**  OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.    07760900
                                                                        07761000
      ******************************************************************07762000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *07763000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *07764000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *07765000
      * FORMAT.                                                        *07766000
      ******************************************************************07767000
                                                                        07768000
           COPY DPPD004.                                                07769000
                                                                        07769100
           MOVE +4013 TO AC-ABEND-CODE.                                 07769200
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07769302
                                                                        07769400
      ***************************************************************** 07769500
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07769600
      ***************************************************************** 07769700
       9999-ABEND.                                                      07769800
                                                                        07769900
           DISPLAY '** ABEND           **'.                             07770000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          07780000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07790000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07800000
                                                                        07810000
