       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    IMKUT076.                                         00020000
       AUTHOR.        PAUL KEBER.                                       00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  06-28-06.                                         00050001
       DATE-COMPILED.                                                   00060000
                                                                        00061000
      ******************************************************************00062000
      *     IMKUT076 - PERM PRICING TUPCPLS CHANGES FILE SPLIT          00063000
      ******************************************************************00064000
      *  THIS PROGRAM WILL INPUT NON-CONTINUED CLEARANCE PERM PRICE     00065001
      *  CHANGES FILE RECORDS THAT ARE SORTED IN UPC ID SEQUENCE AND    00066001
      *  SPLIT THE FILE INTO EITHER ONE FILE OR SIX EQUAL FILES BASED   00067011
      *  ON THE NUMBER OF INPUT FILE RECORDS AND A SPLIT START POINT    00067101
      *  COUNT INPUT CONTROL CARD FILE COUNT.  IF THE TOTAL INPUT FILE  00067201
      *  RECORD COUNT IS LESS THAN THE SPLIT START POINT COUNT FILE     00067301
      *  COUNT, ALL INPUT FILE RECORDS WILL BE WRITTEN TO THE FIRST     00067401
      *  OUTPUT FILE.  OTHERWISE, THIS PROGRAM WILL SPLIT THE INPUT     00067508
      *  FILE INTO SIX EQUAL FILES BASED ON THE NUMBER OF INPUT FILE    00067711
      *  RECORDS.                                                       00067808
      *                                                                 00067901
      *  AN INPUT CONTROL CARD FILE WILL CONTAIN TUPCPLS PARTITIONS BY  00068000
      *  UPC_ID RECORDS.  THIS FILE WILL BE USED TO PREVENT THE START   00069000
      *  OF A NEW OUTPUT FILE IN THE MIDDLE OF A TUPCPLS PARTITION.     00070000
      ******************************************************************00080000
      *  WR29434   06/28/06 P. KEBER    INITIAL CREATION.              *00081001
      ******************************************************************00083001
PK0208*  WR34307   02/15/08 P. KEBER    CHANGE TO SPLIT FROM 4 TO 6    *00084009
PK0208*                                 EQUAL FILES.                   *00084109
      ******************************************************************00085009
      *                                                                 00090000
       ENVIRONMENT DIVISION.                                            00091000
      *                                                                 00092000
       CONFIGURATION SECTION.                                           00093000
      *                                                                 00094000
       SOURCE-COMPUTER.        IBM-3090.                                00095000
       OBJECT-COMPUTER.        IBM-3090.                                00095100
      *                                                                 00095200
       INPUT-OUTPUT SECTION.                                            00095300
       FILE-CONTROL.                                                    00095400
                                                                        00095500
           SELECT PRICE-UPDATES-FILE                                    00095600
               ASSIGN TO INP01.                                         00095700
                                                                        00095800
           SELECT PRICE-UPDATES-COUNT-FILE                              00095900
               ASSIGN TO INP02.                                         00096000
                                                                        00096100
           SELECT PARTITIONS-CONTROL-FILE                               00096501
               ASSIGN TO INP03.                                         00096601
                                                                        00096701
           SELECT SPLIT-START-POINT-CC-FILE                             00096801
               ASSIGN TO INP04.                                         00096901
                                                                        00097001
           SELECT SPLIT-UPDATES-FILE-1                                  00097101
               ASSIGN TO OUT01.                                         00097201
                                                                        00097301
           SELECT SPLIT-UPDATES-FILE-2                                  00097401
               ASSIGN TO OUT02.                                         00097501
                                                                        00097601
           SELECT SPLIT-UPDATES-FILE-3                                  00097701
               ASSIGN TO OUT03.                                         00097801
                                                                        00097901
           SELECT SPLIT-UPDATES-FILE-4                                  00098001
               ASSIGN TO OUT04.                                         00098101
                                                                        00098201
PK0208     SELECT SPLIT-UPDATES-FILE-5                                  00098310
PK0208         ASSIGN TO OUT05.                                         00098410
                                                                        00098510
PK0208     SELECT SPLIT-UPDATES-FILE-6                                  00098610
PK0208         ASSIGN TO OUT06.                                         00098710
                                                                        00098810
      ******************************************************************00098910
       DATA DIVISION.                                                   00099010
      ******************************************************************00099110
                                                                        00099210
       FILE SECTION.                                                    00099310
                                                                        00099410
       FD  PRICE-UPDATES-FILE                                           00099510
           RECORDING MODE IS F                                          00099610
           LABEL RECORDS ARE STANDARD                                   00100000
           BLOCK CONTAINS 0 RECORDS.                                    00110000
                                                                        00120000
       01  PRICE-UPDATES-IN-RECORD     PIC  X(56).                      00130003
                                                                        00140000
       FD  PRICE-UPDATES-COUNT-FILE                                     00150000
           RECORDING MODE IS F                                          00160000
           LABEL RECORDS ARE STANDARD                                   00170000
           BLOCK CONTAINS 0 RECORDS.                                    00180000
                                                                        00181000
       01  PRICE-UPDATES-COUNT-REC     PIC  X(9).                       00182000
                                                                        00183000
       FD  PARTITIONS-CONTROL-FILE                                      00184000
           RECORDING MODE IS F                                          00185000
           LABEL RECORDS ARE STANDARD                                   00186000
           BLOCK CONTAINS 0 RECORDS.                                    00187000
                                                                        00188000
       01  PARTITIONS-CONTROL-RECORD   PIC X(80).                       00189000
                                                                        00190000
       FD  SPLIT-START-POINT-CC-FILE                                    00191001
           RECORDING MODE IS F                                          00192001
           LABEL RECORDS ARE STANDARD                                   00193001
           BLOCK CONTAINS 0 RECORDS.                                    00194001
                                                                        00195001
       01  SPLIT-START-POINT-CC-RECORD PIC X(80).                       00196001
                                                                        00197001
       FD  SPLIT-UPDATES-FILE-1                                         00200000
           RECORDING MODE IS F                                          00210000
           LABEL RECORDS ARE STANDARD                                   00220000
           BLOCK CONTAINS 0 RECORDS.                                    00230000
                                                                        00240000
       01  SPLIT-UPDATES-FILE-1-REC    PIC  X(56).                      00250003
                                                                        00260000
       FD  SPLIT-UPDATES-FILE-2                                         00261000
           RECORDING MODE IS F                                          00262000
           LABEL RECORDS ARE STANDARD                                   00263000
           BLOCK CONTAINS 0 RECORDS.                                    00264000
                                                                        00265000
       01  SPLIT-UPDATES-FILE-2-REC    PIC  X(56).                      00266003
                                                                        00266100
       FD  SPLIT-UPDATES-FILE-3                                         00266200
           RECORDING MODE IS F                                          00266300
           LABEL RECORDS ARE STANDARD                                   00266400
           BLOCK CONTAINS 0 RECORDS.                                    00266500
                                                                        00266600
       01  SPLIT-UPDATES-FILE-3-REC    PIC  X(56).                      00266703
                                                                        00266800
       FD  SPLIT-UPDATES-FILE-4                                         00266900
           RECORDING MODE IS F                                          00267000
           LABEL RECORDS ARE STANDARD                                   00267100
           BLOCK CONTAINS 0 RECORDS.                                    00267200
                                                                        00267300
       01  SPLIT-UPDATES-FILE-4-REC    PIC  X(56).                      00267403
                                                                        00267500
PK0208 FD  SPLIT-UPDATES-FILE-5                                         00267610
PK0208     RECORDING MODE IS F                                          00267710
PK0208     LABEL RECORDS ARE STANDARD                                   00267810
PK0208     BLOCK CONTAINS 0 RECORDS.                                    00267910
PK0208                                                                  00268010
PK0208 01  SPLIT-UPDATES-FILE-5-REC    PIC  X(56).                      00268110
PK0208                                                                  00268210
PK0208 FD  SPLIT-UPDATES-FILE-6                                         00268310
PK0208     RECORDING MODE IS F                                          00268410
PK0208     LABEL RECORDS ARE STANDARD                                   00268510
PK0208     BLOCK CONTAINS 0 RECORDS.                                    00268610
PK0208                                                                  00268710
PK0208 01  SPLIT-UPDATES-FILE-6-REC    PIC  X(56).                      00268810
                                                                        00268910
      *                                                                 00269000
      ******************************************************************00269100
       WORKING-STORAGE SECTION.                                         00269200
      ******************************************************************00269300
                                                                        00269400
       01  PC-PROGRAM-CONSTANTS.                                        00269500
           05  PC-MAX-PARTITIONS-CNT   PIC S9(04)     VALUE +500 COMP.  00269600
                                                                        00269700
       01  PV-PROGRAM-VARIABLES.                                        00269800
           05  PV-PARTITIONS-UPC-ID-10 PIC S9(10)     VALUE +0  COMP.   00269900
           05  PV-PRICE-UPDTS-READ-CNT PIC S9(09)     VALUE +0  COMP.   00270000
           05  PV-SPLIT-FILE-RECS-CNT  PIC S9(09)     VALUE +0  COMP.   00280000
           05  PV-SPLIT-FILE-COUNT     PIC S9(09)     VALUE +0  COMP.   00290000
           05  PV-OUTPUT-1-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00300000
           05  PV-OUTPUT-2-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00310000
           05  PV-OUTPUT-3-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00320000
           05  PV-OUTPUT-4-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00330000
PK0208     05  PV-OUTPUT-5-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00340010
PK0208     05  PV-OUTPUT-6-WRITTEN-CNT PIC S9(09)     VALUE +0  COMP.   00350010
           05  PV-PARTITIONS-CNT       PIC S9(04)     VALUE +0  COMP.   00360000
           05  PV-PARTITIONS-SUB       PIC S9(04)     VALUE +0  COMP.   00370000
           05  PV-SWITCH-PARTITION-SUB PIC S9(09)     VALUE +0  COMP.   00380000
           05  PV-ABEND-CODE           PIC S9(04)     VALUE +0  COMP.   00381000
               88  PV-ABEND-4001                      VALUE 4001.       00382000
      *            EMPTY PRICE UPDATES COUNT FILE                       00383000
               88  PV-ABEND-4002                      VALUE 4002.       00384000
      *            NON-NUMERIC PRICE UPDATES COUNT                      00385000
               88  PV-ABEND-4003                      VALUE 4003.       00386000
      *            MORE THAN 500 PARTITIONS ON PARTITIONS CONTROL FILE  00387000
               88  PV-ABEND-4004                      VALUE 4004.       00388000
      *            NON-NUMERIC PARTITIONS CONTROL RECORD UPC ID         00389000
               88  PV-ABEND-4005                      VALUE 4005.       00390000
      *            EMPTY PARTITIONS CONTROL FILE                        00400000
               88  PV-ABEND-4006                      VALUE 4006.       00410000
      *            PARTITIONS CONTROL RECORD OUT OF SEQUENCE            00420000
               88  PV-ABEND-4007                      VALUE 4007.       00430000
      *            PARTITIONS SUB OVERFLOW                              00431000
               88  PV-ABEND-4008                      VALUE 4008.       00431101
      *            EMPTY SPLIT START POINT COUNT CONTROL CARD           00431201
                                                                        00432000
       01  PS-PROGRAM-SWITCHES.                                         00433000
           05  PS-END-OF-PRICE-UPDATES-SW  PIC  X(01) VALUE 'N'.        00434000
               88  PS-END-OF-PRICE-UPDATES            VALUE 'Y'.        00435000
           05  PS-END-OF-PARTITIONS-SW     PIC  X(01) VALUE 'N'.        00436000
               88  PS-END-OF-PARTITIONS               VALUE 'Y'.        00437000
           05  PS-PARTITION-SWITCH-SW      PIC  X(01) VALUE 'N'.        00438000
               88  PS-PARTITION-SWITCH                VALUE 'Y'.        00439000
               88  PS-NO-PARTITION-SWITCH             VALUE 'N'.        00440000
           05  PS-SPLIT-FILE-REACHED-SW    PIC  X(01) VALUE 'N'.        00450000
               88  PS-SPLIT-FILE-REACHED              VALUE 'Y'.        00460000
               88  PS-SPLIT-FILE-NOT-REACHED          VALUE 'N'.        00470000
           05  PS-PRICE-UPDATES-OUT-SW     PIC  9(01) VALUE 1.          00480000
                                                                        00490000
      *    INTERNAL PARTITIONS TABLE                                    00491000
       01  PT-PARTITIONS-TABLE.                                         00492000
           05  PT-PARTITIONS-UPC-ID OCCURS 500 TIMES  PIC S9(9) COMP.   00493000
                                                                        00494000
      ******************************************************************00494100
      *  RECORD LAYOUT FOR THE NON-CONTINUED CLEARANCE PERM SKU PRICE   00494201
      *  UPDATES INPUT FILE                                             00494301
      ******************************************************************00494401
           COPY IMRD048.                                                00494501
                                                                        00494601
      ***************************************************************** 00494701
      *  RECORD LAYOUT FOR THE PRICE UPDATES COUNT FILE                 00494801
      ***************************************************************** 00494901
       01  WS-PRICE-UPDATES-COUNT-REC.                                  00495001
           05  WS-PRICE-UPDATES-COUNT       PIC 9(09) VALUE ZERO.       00495101
                                                                        00495201
      ***************************************************************** 00495301
      *  RECORD LAYOUT FOR THE PARTITIONS CONTROL FILE                  00495401
      ***************************************************************** 00495501
       01  WS-PARTITIONS-CTRL-RECORD.                                   00495601
           05  FILLER                       PIC X(07) VALUE SPACES.     00495701
           05  WS-PARTITIONS-UPC-ID         PIC 9(10) VALUE ZERO.       00495801
           05  FILLER                       PIC X(63) VALUE SPACES.     00495901
                                                                        00496001
      ***************************************************************** 00496101
      *  RECORD LAYOUT FOR THE SPLIT START POINT COUNT CONTROL CARD     00496201
      ***************************************************************** 00496301
       01  WS-SPLIT-START-POINT-REC.                                    00496401
           05  WS-SPLIT-START-POINT-COUNT   PIC 9(09) VALUE ZERO.       00496501
                                                                        00496601
       EJECT                                                            00496700
                                                                        00497000
      ******************************************************************00498000
       PROCEDURE DIVISION.                                              00499000
      ******************************************************************00500000
                                                                        00510000
       0100-MAINLINE-IMKUT076.                                          00520000
      *                                                                 00530000
           PERFORM 1000-INITIALIZATION.                                 00540000
                                                                        00550000
           IF PS-END-OF-PRICE-UPDATES                                   00560001
               DISPLAY '*** IMKUT076 - EMPTY PRICE UPDATES INPUT FILE'  00570000
               MOVE 4095 TO RETURN-CODE                                 00580000
           ELSE                                                         00581001
               IF WS-PRICE-UPDATES-COUNT < WS-SPLIT-START-POINT-COUNT   00582001
                   DISPLAY '*** IMKUT076 - ALL RECORDS WRITTEN TO SPLIT'00583001
                           ' FILE 1'                                    00583101
                   DISPLAY '********************************************00583206
      -            '**********'                                         00583307
                   MOVE 4094 TO RETURN-CODE                             00584001
               ELSE                                                     00585001
                   MOVE 0 TO RETURN-CODE                                00586001
               END-IF                                                   00590001
           END-IF.                                                      00591001
                                                                        00600000
           IF RETURN-CODE = 0                                           00601001
               PERFORM 2000-PROCESS-PRICE-UPDTS-SPLIT                   00610001
                   UNTIL PS-END-OF-PRICE-UPDATES                        00620001
           ELSE                                                         00620101
               PERFORM 3000-WRITE-PRICE-UPDTS-SPLIT1                    00620201
                   UNTIL PS-END-OF-PRICE-UPDATES                        00620301
           END-IF.                                                      00621001
                                                                        00630000
           PERFORM 8000-EOJ.                                            00640000
                                                                        00650000
           GOBACK.                                                      00660000
                                                                        00670000
       EJECT                                                            00680000
                                                                        00690000
      ******************************************************************00700000
      *  OPEN FILES AND READ THE FIRST RECORD OF THE PRICE UPDATES      00710000
      *  INPUT FILE.  ALSO READ THE PRICE UPDATES COUNT FILE AND LOAD   00720000
      *  THE PARTITIONS CONTROL FILE RECORDS INTO THE PARTITIONS TABLE. 00730000
      ******************************************************************00740000
       1000-INITIALIZATION.                                             00750000
      *                                                                 00760000
           OPEN INPUT  PRICE-UPDATES-FILE                               00770000
                       PRICE-UPDATES-COUNT-FILE                         00780000
                       PARTITIONS-CONTROL-FILE                          00790001
                       SPLIT-START-POINT-CC-FILE.                       00791001
           OPEN OUTPUT SPLIT-UPDATES-FILE-1                             00800000
                       SPLIT-UPDATES-FILE-2                             00810000
                       SPLIT-UPDATES-FILE-3                             00820000
                       SPLIT-UPDATES-FILE-4                             00830010
PK0208                 SPLIT-UPDATES-FILE-5                             00831010
PK0208                 SPLIT-UPDATES-FILE-6.                            00832010
                                                                        00833000
           DISPLAY '****************************************************00834000
      -            '**'.                                                00835000
                                                                        00836000
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          00837000
                                                                        00838000
           PERFORM 4100-READ-PRICE-UPDATES-COUNT.                       00839000
                                                                        00840000
           IF WS-PRICE-UPDATES-COUNT NOT NUMERIC                        00840100
               DISPLAY '*** IMKUT076 - NON-NUMERIC PRICE UPDATES COUNT' 00840200
               SET PV-ABEND-4002 TO TRUE                                00840300
               PERFORM 9999-ABEND                                       00840400
           ELSE                                                         00840500
PK0208         DIVIDE WS-PRICE-UPDATES-COUNT BY 6 GIVING                00840610
                     PV-SPLIT-FILE-COUNT ROUNDED                        00840700
               DISPLAY '*** IMKUT076 - PRICE UPDATES FILE COUNT: '      00840800
                     WS-PRICE-UPDATES-COUNT                             00840900
               DISPLAY '*** IMKUT076 - SPLIT FILE COUNT: '              00841000
                     PV-SPLIT-FILE-COUNT                                00841100
           END-IF.                                                      00841200
                                                                        00841300
           MOVE LOW-VALUES TO PT-PARTITIONS-TABLE.                      00841400
                                                                        00841500
           PERFORM 4200-READ-PARTITIONS-CNTL                            00841600
               UNTIL PS-END-OF-PARTITIONS.                              00841700
                                                                        00841800
           IF PV-PARTITIONS-CNT = ZERO                                  00841900
               DISPLAY '*** EMPTY PARTITIONS CONTROL FILE'              00842000
               SET PV-ABEND-4005 TO TRUE                                00843000
               PERFORM 9999-ABEND                                       00844000
           END-IF.                                                      00845000
                                                                        00846000
           MOVE +1 TO PV-PARTITIONS-SUB.                                00847000
                                                                        00848000
           PERFORM 4300-SPLIT-START-POINT-COUNT.                        00848101
                                                                        00848201
           DISPLAY '****************************************************00849000
      -            '**'.                                                00850000
                                                                        00860000
       EJECT                                                            00870000
                                                                        00880000
      ******************************************************************00890000
      *  WRITE PRICE UPDATES RECORDS TO THE PROPER SPLIT FILE           00900000
      ******************************************************************00910000
       2000-PROCESS-PRICE-UPDTS-SPLIT.                                  00920001
      *                                                                 00930000
           ADD +1 TO PV-SPLIT-FILE-RECS-CNT.                            00940000
                                                                        00950000
           IF IM048-UIR-INTR-UPC-ID >                                   00960002
                 PT-PARTITIONS-UPC-ID (PV-PARTITIONS-SUB)               00970000
               SET PS-NO-PARTITION-SWITCH TO TRUE                       00980000
               PERFORM 2010-PARTITIONS-SUB-CHG                          00980101
                   UNTIL PS-PARTITION-SWITCH                            00980200
           END-IF.                                                      00980300
                                                                        00980400
           IF PV-SPLIT-FILE-RECS-CNT >= PV-SPLIT-FILE-COUNT             00980500
               IF PS-SPLIT-FILE-NOT-REACHED                             00980600
                   MOVE PV-PARTITIONS-SUB TO PV-SWITCH-PARTITION-SUB    00980700
                   SET PS-SPLIT-FILE-REACHED TO TRUE                    00980800
               ELSE                                                     00980900
                   IF PV-PARTITIONS-SUB > PV-SWITCH-PARTITION-SUB       00981000
                       ADD 1 TO PS-PRICE-UPDATES-OUT-SW                 00981100
                       MOVE +1 TO PV-SPLIT-FILE-RECS-CNT                00981212
                       SET PS-SPLIT-FILE-NOT-REACHED TO TRUE            00981300
                   END-IF                                               00981400
               END-IF                                                   00981500
           END-IF.                                                      00981600
                                                                        00981700
           EVALUATE PS-PRICE-UPDATES-OUT-SW                             00981800
               WHEN 1                                                   00981900
                   PERFORM 5000-CREATE-PRICE-UPDTS-1-REC                00982000
               WHEN 2                                                   00983000
                   PERFORM 5010-CREATE-PRICE-UPDTS-2-REC                00984000
               WHEN 3                                                   00985000
                   PERFORM 5020-CREATE-PRICE-UPDTS-3-REC                00986000
               WHEN 4                                                   00987000
                   PERFORM 5030-CREATE-PRICE-UPDTS-4-REC                00988000
PK0208         WHEN 5                                                   00989010
PK0208             PERFORM 5040-CREATE-PRICE-UPDTS-5-REC                00990010
PK0208         WHEN 6                                                   01000010
PK0208             PERFORM 5050-CREATE-PRICE-UPDTS-6-REC                01010010
           END-EVALUATE.                                                01020000
                                                                        01030000
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          01040000
                                                                        01050000
      ******************************************************************01060000
      *  FIND THE NEXT PARTITIONS CONTROL UPC ID RECORD                 01070000
      ******************************************************************01080000
       2010-PARTITIONS-SUB-CHG.                                         01090001
      *                                                                 01100000
           ADD +1 TO PV-PARTITIONS-SUB.                                 01110000
                                                                        01120000
           IF PV-PARTITIONS-SUB > PV-PARTITIONS-CNT                     01130000
               DISPLAY '*** IMKUT076 - PARTITIONS SUB OVERFLOW FOR UPC' 01140000
                       ' ID: ' IM048-UIR-INTR-UPC-ID                    01150002
               SET PV-ABEND-4007 TO TRUE                                01160000
               PERFORM 9999-ABEND                                       01170000
           END-IF.                                                      01180000
                                                                        01181000
           IF IM048-UIR-INTR-UPC-ID <=                                  01182002
                 PT-PARTITIONS-UPC-ID (PV-PARTITIONS-SUB)               01183000
               SET PS-PARTITION-SWITCH TO TRUE                          01184000
           END-IF.                                                      01185000
                                                                        01186000
      ******************************************************************01186101
      *  WRITE PRICE UPDATES RECORDS TO THE FIRST SPLIT FILE            01186201
      ******************************************************************01186301
       3000-WRITE-PRICE-UPDTS-SPLIT1.                                   01186401
      *                                                                 01186501
           PERFORM 5000-CREATE-PRICE-UPDTS-1-REC.                       01186701
           PERFORM 4000-READ-PRICE-UPDATES-IN.                          01186901
                                                                        01187001
      ***************************************************************** 01187100
      *  READ A PRICE UPDATES INPUT FILE RECORD.                        01188000
      ***************************************************************** 01189000
       4000-READ-PRICE-UPDATES-IN.                                      01190000
      *                                                                 01200000
           READ PRICE-UPDATES-FILE INTO IM048-UPC-ID-PRICE-UPDATE-REC   01210003
             AT END                                                     01220000
                 SET PS-END-OF-PRICE-UPDATES TO TRUE                    01230000
             NOT AT END                                                 01240000
                 ADD +1 TO PV-PRICE-UPDTS-READ-CNT                      01250000
           END-READ.                                                    01260000
                                                                        01270000
      ***************************************************************** 01280000
      *  READ THE PRICE UPDATES COUNT FILE RECORD.                      01290000
      ***************************************************************** 01300000
       4100-READ-PRICE-UPDATES-COUNT.                                   01310000
      *                                                                 01320000
           READ PRICE-UPDATES-COUNT-FILE INTO WS-PRICE-UPDATES-COUNT-REC01321000
             AT END                                                     01322000
                 DISPLAY '*** IMKUT076 - EMPTY PRICE UPDATES COUNT FILE'01323000
                 SET PV-ABEND-4001 TO TRUE                              01324000
                 PERFORM 9999-ABEND                                     01325000
           END-READ.                                                    01326000
                                                                        01327000
      ***************************************************************** 01328000
      *  READ A PARTITIONS CONTROL FILE RECORD.                         01329000
      ***************************************************************** 01330000
       4200-READ-PARTITIONS-CNTL.                                       01340000
                                                                        01350000
           READ PARTITIONS-CONTROL-FILE INTO WS-PARTITIONS-CTRL-RECORD  01360000
             AT END                                                     01370000
                 SET PS-END-OF-PARTITIONS TO TRUE                       01380000
                 DISPLAY '*** IMKUT076 - PARTITIONS CONTROL FILE '      01390000
                         'COUNT: ' PV-PARTITIONS-CNT                    01400000
             NOT AT END                                                 01410000
                 ADD +1 TO PV-PARTITIONS-CNT                            01420000
                 IF PV-PARTITIONS-CNT > PC-MAX-PARTITIONS-CNT           01421000
                     DISPLAY '*** MORE THAN ' PC-MAX-PARTITIONS-CNT     01422000
                             ' PARTITIONS ON PARTITIONS CONTROL FILE'   01422100
                     SET PV-ABEND-4003 TO TRUE                          01422200
                     PERFORM 9999-ABEND                                 01422300
                 END-IF                                                 01422400
                 IF WS-PARTITIONS-UPC-ID NUMERIC                        01422500
                     MOVE WS-PARTITIONS-UPC-ID TO                       01422600
                           PV-PARTITIONS-UPC-ID-10                      01422700
                     MOVE PV-PARTITIONS-UPC-ID-10 TO                    01422800
                           PT-PARTITIONS-UPC-ID (PV-PARTITIONS-CNT)     01422900
                     IF PV-PARTITIONS-CNT > +1                          01423000
                         IF PT-PARTITIONS-UPC-ID (PV-PARTITIONS-CNT) <= 01423100
                            PT-PARTITIONS-UPC-ID (PV-PARTITIONS-CNT - 1)01423200
                             DISPLAY '*** PARTITIONS CONTROL RECORD OUT'01423300
                                     ' OF SEQUENCE FOR RECORD NUMBER: ' 01423400
                                     PV-PARTITIONS-CNT                  01423500
                             DISPLAY '*** PARTITIONS CONTROL RECORD: '  01423600
                                     WS-PARTITIONS-CTRL-RECORD          01423700
                             SET PV-ABEND-4006 TO TRUE                  01423800
                             PERFORM 9999-ABEND                         01423900
                         END-IF                                         01424000
                     END-IF                                             01424100
                 ELSE                                                   01424200
                     DISPLAY '*** NON-NUMERIC PARTITIONS CONTROL RECORD'01424300
                             ' UPC ID FOR RECORD NUMBER: '              01424400
                             PV-PARTITIONS-CNT                          01424500
                     DISPLAY '*** PARTITIONS CONTROL RECORD: '          01424600
                             WS-PARTITIONS-CTRL-RECORD                  01424700
                     SET PV-ABEND-4004 TO TRUE                          01424800
                     PERFORM 9999-ABEND                                 01424900
                 END-IF                                                 01425000
           END-READ.                                                    01426000
                                                                        01427000
      ***************************************************************** 01427101
      *  READ THE SPLIT START POINT COUNT CONTROL CARD RECORD.          01427201
      ***************************************************************** 01427301
       4300-SPLIT-START-POINT-COUNT.                                    01427401
      *                                                                 01427501
           READ SPLIT-START-POINT-CC-FILE INTO WS-SPLIT-START-POINT-REC 01427601
             AT END                                                     01427801
                 DISPLAY '*** IMKUT076 - EMPTY SPLIT START POINT COUNT '01427901
                         'CONTROL CARD'                                 01428001
                 SET PV-ABEND-4008 TO TRUE                              01428101
                 PERFORM 9999-ABEND                                     01428201
             NOT AT END                                                 01428305
               DISPLAY '*** IMKUT076 - SPLIT START POINT COUNT: '       01428405
                     WS-SPLIT-START-POINT-COUNT                         01428505
           END-READ.                                                    01428601
                                                                        01428701
       EJECT                                                            01428801
                                                                        01429000
      ******************************************************************01430000
      *  CREATE A PRICE UPDATES FILE 1 OUTPUT RECORD                    01440000
      ******************************************************************01450000
       5000-CREATE-PRICE-UPDTS-1-REC.                                   01460000
      *                                                                 01470000
           WRITE SPLIT-UPDATES-FILE-1-REC                               01480000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01490004
                                                                        01500000
           ADD 1 TO PV-OUTPUT-1-WRITTEN-CNT.                            01510000
                                                                        01520000
      ******************************************************************01530000
      *  CREATE A PRICE UPDATES FILE 2 OUTPUT RECORD                    01540000
      ******************************************************************01550000
       5010-CREATE-PRICE-UPDTS-2-REC.                                   01560000
      *                                                                 01570000
           WRITE SPLIT-UPDATES-FILE-2-REC                               01580000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01590004
                                                                        01600000
           ADD 1 TO PV-OUTPUT-2-WRITTEN-CNT.                            01610000
                                                                        01620000
      ******************************************************************01630000
      *  CREATE A PRICE UPDATES FILE 3 OUTPUT RECORD                    01640000
      ******************************************************************01650000
       5020-CREATE-PRICE-UPDTS-3-REC.                                   01660000
      *                                                                 01670000
           WRITE SPLIT-UPDATES-FILE-3-REC                               01680000
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01681004
                                                                        01682000
           ADD 1 TO PV-OUTPUT-3-WRITTEN-CNT.                            01683000
                                                                        01684000
      ******************************************************************01685000
      *  CREATE A PRICE UPDATES FILE 4 OUTPUT RECORD                    01686000
      ******************************************************************01687000
       5030-CREATE-PRICE-UPDTS-4-REC.                                   01688000
      *                                                                 01689000
           WRITE SPLIT-UPDATES-FILE-4-REC                               01689100
               FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01689204
                                                                        01689400
           ADD 1 TO PV-OUTPUT-4-WRITTEN-CNT.                            01689500
                                                                        01689600
PK0208******************************************************************01689710
PK0208*  CREATE A PRICE UPDATES FILE 5 OUTPUT RECORD                    01689810
PK0208******************************************************************01689910
PK0208 5040-CREATE-PRICE-UPDTS-5-REC.                                   01690010
PK0208*                                                                 01691010
PK0208     WRITE SPLIT-UPDATES-FILE-5-REC                               01691110
PK0208         FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01691210
PK0208                                                                  01691310
PK0208     ADD 1 TO PV-OUTPUT-5-WRITTEN-CNT.                            01691410
PK0208                                                                  01691510
PK0208******************************************************************01691610
PK0208*  CREATE A PRICE UPDATES FILE 6 OUTPUT RECORD                    01691710
PK0208******************************************************************01691810
PK0208 5050-CREATE-PRICE-UPDTS-6-REC.                                   01691910
PK0208*                                                                 01692010
PK0208     WRITE SPLIT-UPDATES-FILE-6-REC                               01692110
PK0208         FROM IM048-UPC-ID-PRICE-UPDATE-REC.                      01692210
PK0208                                                                  01692310
PK0208     ADD 1 TO PV-OUTPUT-6-WRITTEN-CNT.                            01692410
PK0208                                                                  01692510
      ******************************************************************01692610
      *  FINAL PROCESSING FOR THE PROGRAM.                              01692710
      ******************************************************************01692810
       8000-EOJ.                                                        01692910
      *                                                                 01693010
           DISPLAY '*** RECORD COUNT SUMMARY'.                          01693110
           DISPLAY 'PRICE UPDATES RECORDS READ = '                      01694000
                 PV-PRICE-UPDTS-READ-CNT.                               01695000
           DISPLAY 'PARTITION CNTRL RECS READ  = '                      01696000
                 PV-PARTITIONS-CNT                                      01697000
           DISPLAY 'OUTPUT FILE 1 RECS WRITTEN = '                      01698000
                 PV-OUTPUT-1-WRITTEN-CNT.                               01699000
           DISPLAY 'OUTPUT FILE 2 RECS WRITTEN = '                      01700000
                 PV-OUTPUT-2-WRITTEN-CNT.                               01710000
           DISPLAY 'OUTPUT FILE 3 RECS WRITTEN = '                      01720000
                 PV-OUTPUT-3-WRITTEN-CNT.                               01730000
           DISPLAY 'OUTPUT FILE 4 RECS WRITTEN = '                      01740000
                 PV-OUTPUT-4-WRITTEN-CNT.                               01750000
PK0208     DISPLAY 'OUTPUT FILE 5 RECS WRITTEN = '                      01760010
PK0208           PV-OUTPUT-5-WRITTEN-CNT.                               01770010
PK0208     DISPLAY 'OUTPUT FILE 6 RECS WRITTEN = '                      01780010
PK0208           PV-OUTPUT-6-WRITTEN-CNT.                               01790010
           DISPLAY '****************************************************01800000
      -            '**'.                                                01801000
                                                                        01802000
           CLOSE PRICE-UPDATES-FILE                                     01803000
                 PRICE-UPDATES-COUNT-FILE                               01804000
                 PARTITIONS-CONTROL-FILE                                01805000
                 SPLIT-START-POINT-CC-FILE                              01805101
                 SPLIT-UPDATES-FILE-1                                   01806000
                 SPLIT-UPDATES-FILE-2                                   01807000
                 SPLIT-UPDATES-FILE-3                                   01808000
                 SPLIT-UPDATES-FILE-4                                   01809010
PK0208           SPLIT-UPDATES-FILE-5                                   01809110
PK0208           SPLIT-UPDATES-FILE-6.                                  01809210
                                                                        01840000
       9999-ABEND.                                                      01850000
      *                                                                 01860000
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                        01870000
