       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT310.                                                 
       AUTHOR.        MARK A FRAMNESS.                                          
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  JULY 2011.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT310 - PHYSICAL INVENTORY VARIANCE                                  
      * CREATE DATA FOR LPT_PIP_PHYINV_WRK                                      
      *                                                                         
      * THIS PROGRAM WILL CREATE A FILE TO BE LOADED TO A WORK TABLE            
      * LPT_PIP_PHYINV_WRK VIA INFORMATICA.                                     
      *                                                                         
      * CHANGE LOG:  MARIBETH ZAMORA    WR33304 (HILO)                          
      *              NEW                                                        
      *                                                                         
      *              MARK A FRAMNESS    3033D (MDSE EDW)                        
      *              COPY FROM MWKUT693 - REFRESH COMMENTS AND                  
      *              CLONE ANY MW COPYBOOKS TO IN.                              
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  100 CHARACTERS.                                     
       01  INPUT-RECORD                PIC X(100).                              
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS  129 CHARACTERS.                                     
       01  OUTPUT-RECORD               PIC X(129).                              
                                                                                
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUT310'.          
       01  ABEND-CODE                  PIC S9(04)    VALUE ZEROES               
                                                     COMP SYNC.                 
       01  PV-COUNTS.                                                           
           05 PV-IN-COUNT              PIC  9(09)    VALUE ZEROES.              
           05 PV-OUT-COUNT             PIC  9(09)    VALUE ZEROES.              
                                                                                
       01  PV-INPUT-SW                 PIC  X(01)    VALUE 'N'.                 
           88  NOT-END-INPUT                         VALUE 'N'.                 
           88  END-INPUT                             VALUE 'Y'.                 
                                                                                
       01  PV-TEMP.                                                             
           05  PV-ITM-NBR              PIC S9(15)    VALUE 0.                   
           05  PV-PHY-SITE-ID          PIC S9(05)    VALUE 0.                   
           05  PV-INV-CYC-ID           PIC S9(09)    VALUE 0.                   
           05  PV-ENT-ID               PIC S9(09)    VALUE 0.                   
           05  PV-CTOFF-QTY            PIC S9(09)    VALUE 0.                   
           05  PV-SHTG-QTY             PIC S9(09)    VALUE 0.                   
           05  PV-OVRG-QTY             PIC S9(09)    VALUE 0.                   
           05  PV-ACTL-CTOFF-QTY       PIC S9(09)    VALUE 0.                   
           05  PV-ACTL-SHTG-QTY        PIC S9(09)    VALUE 0.                   
           05  PV-ACTL-OVRG-QTY        PIC S9(09)    VALUE 0.                   
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR INPUT FILE (PI)                                          
      ****************************************************************          
           COPY INRD138.                                                        
                                                                                
      ****************************************************************          
      *  COPY BOOK FOR OUTPUT FILE                                              
      ****************************************************************          
3033D      COPY INRD310.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-SECTION.                                                   
      ******************************************************************        
                                                                                
           PERFORM 1000-BEG-OF-JOB.                                             
                                                                                
           PERFORM 3000-MAIN-PROCESS                                            
             UNTIL END-INPUT.                                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       1000-BEG-OF-JOB.                                                         
      ******************************************************************        
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '** PROGRAM :       ' PC-PROGRAM-NAME                        
           DISPLAY '************************************************'.          
                                                                                
           OPEN INPUT  INPUT-FILE                                               
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESS.                                                       
      *****************************************************************         
                                                                                
           PERFORM 6000-FORMAT-OUTPUT-RECORD.                                   
           PERFORM 6100-WRITE-OUTPUT-RECORD.                                    
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
       4000-READ-INPUT-FILE.                                                    
      *****************************************************************         
                                                                                
           READ INPUT-FILE                                                      
               INTO  IN138-VARIANCE-SUMM-RECORD                                 
               AT END                                                           
                   SET END-INPUT     TO TRUE                                    
           END-READ.                                                            
                                                                                
           IF NOT-END-INPUT                                                     
              ADD +1 TO PV-IN-COUNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
       6000-FORMAT-OUTPUT-RECORD.                                               
      *****************************************************************         
                                                                                
           INITIALIZE IN310-VARIANCE-RECORD.                                    
                                                                                
           MOVE '+'                    TO IN310-PRD-DIM-KY-ID1                  
           MOVE IN138-UPC-ID           TO IN310-PRD-DIM-KY-ID2                  
           MOVE IN138-ITM-NBR          TO PV-ITM-NBR                            
           MOVE PV-ITM-NBR             TO IN310-PRD-DIM-KY-ID3                  
                                                                                
           MOVE IN138-BEG-WK-DTE       TO IN310-TM-DIM-KY-DTE                   
                                                                                
           MOVE IN138-STR-NBR          TO PV-PHY-SITE-ID                        
           MOVE PV-PHY-SITE-ID         TO IN310-PHY-SITE-ID                     
                                                                                
           MOVE IN138-STAT-CDE         TO IN310-STAT-CDE                        
                                                                                
           MOVE IN138-INV-ID           TO PV-INV-CYC-ID                         
           MOVE PV-INV-CYC-ID          TO IN310-INV-CYC-ID                      
                                                                                
           MOVE IN138-INV-SRVC-ENT-ID  TO PV-ENT-ID                             
           MOVE PV-ENT-ID              TO IN310-ENT-ID                          
                                                                                
           MOVE IN138-UNT-CTOFF        TO PV-CTOFF-QTY                          
           MOVE PV-CTOFF-QTY           TO IN310-CTOFF-QTY                       
                                                                                
           MOVE IN138-UNTS-DECR        TO PV-SHTG-QTY                           
           MOVE PV-SHTG-QTY            TO IN310-SHTG-QTY                        
                                                                                
           MOVE IN138-UNTS-INCR        TO PV-OVRG-QTY                           
           MOVE PV-OVRG-QTY            TO IN310-OVRG-QTY                        
                                                                                
           MOVE IN138-TRUE-UNTS-DECR   TO PV-ACTL-SHTG-QTY.                     
           MOVE PV-ACTL-SHTG-QTY       TO IN310-ACTL-SHTG-QTY.                  
                                                                                
           MOVE IN138-TRUE-UNTS-INCR   TO PV-ACTL-OVRG-QTY.                     
           MOVE PV-ACTL-OVRG-QTY       TO IN310-ACTL-OVRG-QTY.                  
                                                                                
           MOVE IN138-TRUE-CUTOFF-QTY  TO PV-ACTL-CTOFF-QTY.                    
           MOVE PV-ACTL-CTOFF-QTY      TO IN310-ACTL-CTOFF-QTY.                 
                                                                                
                                                                                
      *****************************************************************         
       6100-WRITE-OUTPUT-RECORD.                                                
      *****************************************************************         
                                                                                
           WRITE OUTPUT-RECORD         FROM IN310-VARIANCE-RECORD.              
           ADD +1 TO PV-OUT-COUNT.                                              
                                                                                
                                                                                
      *****************************************************************         
       9000-END-OF-JOB.                                                         
      *****************************************************************         
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '========== INKUT310 PROGRAM SUMMARY =========='.            
           DISPLAY '  INPUT  RECS READ        : ' PV-IN-COUNT                   
           DISPLAY '  OUTPUT RECS WRITTEN     : ' PV-OUT-COUNT                  
           DISPLAY '************************************************'.          
                                                                                
      ******************************************************************        
      ******************************************************************        
