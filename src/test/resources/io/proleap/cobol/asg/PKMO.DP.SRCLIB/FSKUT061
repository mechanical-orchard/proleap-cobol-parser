       IDENTIFICATION DIVISION.                                                 
      *                                                                         
       PROGRAM-ID.         FSKUT061.                                            
       AUTHOR.             KRISTIN PETERSON.                                    
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       12-26-2002.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM PREPARES THE RECEIPT TRANSACTION FILE TO BE      *        
      *  PROCESSED BY FSKED003 UNDER THE DB2 CHECKPOINT RESTART        *        
      *  SYSTEM.                                                       *        
      *                                                                *        
      *  THE PROGRAM READS A TRANSACTION FILE, REFORMATS THE RECORDS   *        
      *  AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED ON HOW    *        
      *  CHECKPOINTS WILL BE TAKEN BY THE EDIT PROGRAM.  AFTER A       *        
      *  RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE TRANSACTION   *        
      *  PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE PROCESSING *        
      *  ON THE RECORDS AND WRITES THE REFORMATTED RECORDS OUT TO A    *        
      *  SEQUENTIAL FILE.                                              *        
      *                                                                *        
      *  THE JOB NAME AND PLAN NAME USED TO IDENTIFY THE CHECKPOINT    *        
      *  CONTROL RECORD TO BE USED ARE SUPPLIED THROUGH A CONTROL      *        
      *  CARD.                                                         *        
      *                                                                *        
      *  THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION     *        
      *  MODULE USING FUNCTION CODES (OPEN, WRITE & CLOSE).            *        
      *                                                                *        
      *  DB2 TABLES:                                                   *        
      *    - N/A                                                       *        
      *                                                                *        
      *  INPUT FILES:                                                  *        
      *    - RECEIPT TRANSACTION FILE                DDNAME = INP01    *        
      *                                                                *        
      *  OUTPUT FILES:                                                 *        
      *    - N/A                                                       *        
      ******************************************************************        
      *  CHANGES:                                                      *        
      *    12/26/02   KRISTIN PETERSON         WR# 23946               *        
      *               NEW PROGRAM - TRANSACTION PREP FOR FSKED003      *        
      *               DAILY ON-HAND RECEIPTS EDIT.                     *        
      ******************************************************************        
260107*    08/19/21   JIM HUNTER               CHG0260107                       
260107*               ADDED COPYBOOK DPWS990 TO MAKE THE CALL TO                
260107*               DPKUT900 DYNAMIC.                                         
      ******************************************************************        
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RCPT-TRANSACTION-FILE     ASSIGN TO INP01.                    
                                                                                
       EJECT                                                                    
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD  RCPT-TRANSACTION-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS RCPT-TRANSACTION-RECORD.                              
       01  RCPT-TRANSACTION-RECORD     PIC  X(200).                             
        EJECT                                                                   
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)     VALUE +0  COMP.           
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME         PIC X(08)      VALUE 'FSKUT061'.         
           05  PC-TRAN-PREP-FUNC-CODES.                                         
               10  PC-TRAN-PREP-FUNC-OPEN                                       
                                       PIC X(05)      VALUE 'OPEN '.            
               10  PC-TRAN-PREP-FUNC-WRITE                                      
                                       PIC X(05)      VALUE 'WRITE'.            
               10  PC-TRAN-PREP-FUNC-CLOSE                                      
                                       PIC X(05)      VALUE 'CLOSE'.            
           05  PC-TRANS-RECORD-LENGTH  PIC S9(04)     VALUE +200 COMP.          
       EJECT                                                                    
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                  PIC X(09)      VALUE SPACES.             
           05  CC-JOB-NAME             PIC X(08)      VALUE SPACES.             
           05  FILLER                  PIC X(11)      VALUE SPACES.             
           05  CC-PLAN-NAME            PIC X(08)      VALUE SPACES.             
           05  FILLER                  PIC X(44)      VALUE SPACES.             
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-PARAGRAPH-NAME       PIC X(30)      VALUE SPACES.             
           05  PV-ERROR-MESSAGE        PIC X(40)      VALUE SPACES.             
           05  PV-CHECKPOINT-TYPE      PIC X(01)      VALUE SPACE.              
               88  CONTROL-BREAK                      VALUE 'C'.                
               88  LOGICAL-TRANSACTION                VALUE 'L'.                
               88  TIME-INTERVAL                      VALUE 'T'.                
               88  REQUESTED-BY-PROGRAM               VALUE 'R'.                
           05  PV-HOLD-ITM-NUMBER      PIC S9(15)V    VALUE ZERO COMP-3.        
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-RCPT-TRANS-EOF-SW    PIC X(01)      VALUE 'N'.                
               88  RCPT-TRANS-EOF                     VALUE 'Y'.                
           05  PS-FIRST-TRANS-READ-SW  PIC X(01)      VALUE 'Y'.                
               88  FIRST-TRANS-READ                   VALUE 'Y'.                
       EJECT                                                                    
      *                                                                         
      *     RECEIPT TRANSACTION RECORD LAYOUT.                                  
      *                                                                         
           COPY FSRD036.                                                        
       EJECT                                                                    
      *                                                                         
      *     TRANSACTION PREPARATION MODULE LINKAGE SECTION.                     
      *                                                                         
260107     COPY DPWS990.                                                        
                                                                                
           COPY DPLS000.                                                        
       EJECT                                                                    
      *                                                                         
      *     SQL COMMUNICATIONS WORK AREA.                                       
      *                                                                         
           COPY SQLCA2.                                                         
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      * A100-MAINLINE-PROCESSING                                       *        
      *      CONTROLS FLOW OF THE PROGRAM.                             *        
      ******************************************************************        
                                                                                
       A100-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM B100-ISSUE-OPEN-REQUEST.                                     
                                                                                
           OPEN INPUT RCPT-TRANSACTION-FILE.                                    
                                                                                
           MOVE 'Y' TO PS-FIRST-TRANS-READ-SW.                                  
                                                                                
           PERFORM B200-READ-RCPT-TRANS-FILE                                    
               UNTIL RCPT-TRANS-EOF.                                            
                                                                                
           CLOSE RCPT-TRANSACTION-FILE.                                         
                                                                                
           PERFORM B300-ISSUE-CLOSE-REQUEST.                                    
                                                                                
           GOBACK.                                                              
       EJECT                                                                    
      ******************************************************************        
      * B100-ISSUE-OPEN-REQUEST.                                       *        
      *      ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION    *        
      *      MODULE.                                                   *        
      *                                                                *        
      *      PERFORMED BY A100-MAINLINE-PROCESSING.                    *        
      ******************************************************************        
                                                                                
       B100-ISSUE-OPEN-REQUEST.                                                 
                                                                                
           ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
                                                                                
           DISPLAY '** FSKUT061 - CONTROL CARD = ' CC-CONTROL-CARD.             
                                                                                
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.                      
           MOVE CC-JOB-NAME TO DP000-JOB-NAME.                                  
           MOVE CC-PLAN-NAME TO DP000-PLAN-NAME.                                
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
                                                                                
           MOVE DP000-CKPT-TYPE-CODE TO PV-CHECKPOINT-TYPE.                     
       EJECT                                                                    
      ******************************************************************        
      * B200-READ-RCPT-TRANS-FILE.                                     *        
      *      READ A RECEIPT TRANSACTION.  IF END-OF-FILE STOP          *        
      *      PROCESSING.  IF A RECORD IS READ, GATHER WHATEVER OTHER   *        
      *      DATA IS NECESSARY TO TAKE CHECKPOINTS BASED ON THE TYPE   *        
      *      OF CHECKPOINTS BEING TAKEN BY THE EDIT PROGRAM AND ISSUE  *        
      *      A WRITE REQUEST TO THE TRANSACTION PREPARATION MODULE.    *        
      *                                                                *        
      *      PERFORMED BY A100-MAINLINE-PROCESSING.                    *        
      ******************************************************************        
                                                                                
       B200-READ-RCPT-TRANS-FILE.                                               
                                                                                
           READ RCPT-TRANSACTION-FILE INTO FS036-RECEIPT-RECORD                 
               AT END                                                           
                 MOVE 'Y' TO PS-RCPT-TRANS-EOF-SW.                              
                                                                                
           IF RCPT-TRANS-EOF                                                    
               CONTINUE                                                         
           ELSE                                                                 
               IF FIRST-TRANS-READ                                              
                   MOVE FS036-ITM-NBR TO PV-HOLD-ITM-NUMBER                     
                   MOVE 'N' TO PS-FIRST-TRANS-READ-SW                           
               ELSE                                                             
                   CONTINUE                                                     
               END-IF                                                           
               EVALUATE TRUE                                                    
                   WHEN LOGICAL-TRANSACTION                                     
                       PERFORM C100-CKPT-BY-LOGICAL-TRANS                       
                   WHEN CONTROL-BREAK                                           
      ************     CONTROL BREAK BY DEPT/CLASS IS NOT A VALID               
      ************     CKPT TYPE FOR THE RECEIPT AUDIT TABLE UPDATE             
                       MOVE 'B200-READ-RCPT-TRANS-FILE'                         
                         TO PV-PARAGRAPH-NAME                                   
                       MOVE 'CONTROL BREAK NOT VALID'                           
                         TO PV-ERROR-MESSAGE                                    
                       PERFORM 9990-USER-ABEND                                  
                   WHEN OTHER                                                   
                       PERFORM C200-CKPT-BY-TIME-OR-REQUEST                     
               END-EVALUATE                                                     
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * B300-ISSUE-CLOSE-REQUEST.                                      *        
      *      ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION    *        
      *      MODULE.                                                   *        
      *                                                                *        
      *      PERFORMED BY A100-MAINLINE-PROCESSING.                    *        
      ******************************************************************        
                                                                                
       B300-ISSUE-CLOSE-REQUEST.                                                
                                                                                
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.                     
           MOVE CC-JOB-NAME TO DP000-JOB-NAME.                                  
           MOVE CC-PLAN-NAME TO DP000-PLAN-NAME.                                
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
       EJECT                                                                    
      ******************************************************************        
      * C100-CKPT-BY-LOGICAL-TRANS.                                    *        
      *      THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE         *        
      *      TRANSACTION RECORD "ON" IF THERE IS A CHANGE IN ITEM      *        
      *      NUMBER.  AFTER THE TRANSACTION IS FORMATTED A WRITE       *        
      *      REQUEST IS ISSUED TO THE TRANSACTION PREPARATION MODULE.  *        
      *                                                                *        
      *      PERFORMED BY B200-READ-RCPT-TRANS-FILE                    *        
      ******************************************************************        
                                                                                
       C100-CKPT-BY-LOGICAL-TRANS.                                              
                                                                                
           IF FS036-ITM-NBR =  PV-HOLD-ITM-NUMBER                               
               MOVE 'N' TO DP000-CHECKPOINT-IND                                 
           ELSE                                                                 
               MOVE 'Y' TO DP000-CHECKPOINT-IND                                 
               MOVE FS036-ITM-NBR TO PV-HOLD-ITM-NUMBER.                        
                                                                                
           MOVE PC-TRANS-RECORD-LENGTH TO DP000-TRANS-REC-LENGTH.               
                                                                                
           MOVE FS036-RECEIPT-RECORD    TO DP000-TRANS-REC.                     
                                                                                
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                     
           MOVE CC-JOB-NAME TO DP000-JOB-NAME.                                  
           MOVE CC-PLAN-NAME TO DP000-PLAN-NAME.                                
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
       EJECT                                                                    
      ******************************************************************        
      * C200-CKPT-BY-TIME-OR-REQUEST.                                  *        
      *      THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A   *        
      *      WRITE REQUEST TO THE TRANSACTION PREPARATION MODULE IF    *        
      *      CHECKPOINTS ARE TAKEN BY TIME INTERVAL (SECONDS) OR RE-   *        
      *      QUESTED BY THE MAINTENANCE PROGRAM.                       *        
      *                                                                *        
      *      PERFORMED BY B200-READ-RCPT-TRANS-FILE                    *        
      ******************************************************************        
                                                                                
       C200-CKPT-BY-TIME-OR-REQUEST.                                            
                                                                                
           MOVE 'N' TO DP000-CHECKPOINT-IND.                                    
                                                                                
           MOVE PC-TRANS-RECORD-LENGTH TO DP000-TRANS-REC-LENGTH.               
                                                                                
           MOVE FS036-RECEIPT-RECORD    TO DP000-TRANS-REC.                     
                                                                                
           MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.                     
           MOVE CC-JOB-NAME TO DP000-JOB-NAME.                                  
           MOVE CC-PLAN-NAME TO DP000-PLAN-NAME.                                
                                                                                
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                                 
       EJECT                                                                    
      *                                                                         
      *     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.             
      *                                                                         
       EJECT                                                                    
           COPY DPPD000.                                                        
                                                                                
      *****************************************************************         
      * USER ABEND                                                              
      *****************************************************************         
       9990-USER-ABEND.                                                         
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '** ABEND **'.                                               
           DISPLAY '** PGM = ' PC-PROGRAM-NAME.                                 
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
           DISPLAY '** ERROR MESSAGE = ' PV-ERROR-MESSAGE.                      
           DISPLAY '******************************************'.                
           MOVE +4003 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
