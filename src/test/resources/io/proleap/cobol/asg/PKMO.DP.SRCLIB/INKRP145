       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKRP145.                                        
       AUTHOR.                 J SEIDLER.                                       
       INSTALLATION.           KOHLS.                                           
       DATE-WRITTEN.           AUGUST 1998.                                     
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                               *         
      *    THIS PROGRAM PRODUCES THE STORE SHORTAGE REPORT            *         
      *                                                               *         
      *****************************************************************         
      *   DATE: 02/01/2000     CHANGE ID:            WHO: B.MCASEY    *         
      *   REASON FOR CHANGE : PHYSICAL INV RETAIL FIELD WAS BEING     *         
      *   TRUNCATED. EXPANDED W/S AND REPORT FIELDS AND MODIFIED      *         
      *   PRINT LINE.                                                 *         
      *****************************************************************         
      *   DATE: 07/01/2000     CHANGE ID: ADJ00      WHO: S.JACKSON   *         
      *   REASON FOR CHANGE : INCLUDE TINVPAR. PGM IS EXECUTED FROM   *         
      *   INK900 (GENERATES RPTS FOR ALL LOCATIONS) AND INK910 (SOME  *         
      *   LOCATIONS WILL BE BYPASSED FROM PRINTING (IE. CLOSED STRS)  *         
      *   ADDED INP02 TO DETERMINE FROM WHICH JOB EXECUTED THE PGM.   *         
      *****************************************************************         
      *   DATE: 10/08/2001     CHANGE ID: PCT02      WHO: S.JACKSON   *         
      *   REASON FOR CHANGE : THE SHRINK PERCENTAGE NEEDS TO CARRY THE*         
      *   SAME SIGN AS THE RETAIL SHORTAGE. IF THE SHORTAGE RETAIL IS *         
      *   POSITIVE, THE SHRINK % IS POSITIVE. BUT IF THE SHORTAGE     *         
      *   RETAIL IS NEGATIVE, THE SHRINK % SHOULD ALSO BE NEGATIVE.   *         
      *****************************************************************         
      *   DATE: 02/07/2002     CHANGE ID: PCT02A     WHO: S.JACKSON   *         
      *   REASON FOR CHANGE : INCREASE SHRINK % PRECISION TO 999.99 %.*         
      *****************************************************************         
      *   DATE: 12/03/2002     CHANGE ID: DATECC     WHO: S.JACKSON   *         
      *   REASON FOR CHANGE : ADD INP03 TO CONTROL WHETHER TO USE THE *         
      *   DEFAULT TMLCNTL DATE TABLE OR AN INPUT CONTROL CARD TO DRIVE*         
      *   SELECTION OF INVENTORY STORES. IF PROCESS RUNS OUTSIDE OF   *         
      *   CURRENT FISCAL MONTH, THE CONTROL CARD MUST CONTAIN THE     *         
      *   FISCAL MONTH END DATE OF THE INVENTORYING MONTH.            *         
      *****************************************************************         
RB1202*   DATE: 12/16/2002     CHANGE ID: RB1202     WHO: RONNA BUTZ            
RB1202*                                                                         
RB1202* SUPRESS PRINTING OF COMPANY TOTALS DATA                                 
RB1202*                                                                         
RB1202* ON STORE BREAK, PRINT THE BLANK PAGE WITH HEADERS AND A LINE            
RB1202* STATING THAT THE PAGE IS INTENTIONALLY LEFT BLANK.                      
RB1202*                                                                         
RB1202*****************************************************************         
RB1202*  DATE: 04/  /2004     CHANGE ID: W26600     WHO: M JOHNSON              
W26600*  INV LEDGER STORE EXPANSION                                             
W27710* W27710  01-24-2005 ROD JANSSEN ISSUE NOT GENERATING THE ENTIRE          
W27710*                                REPORT WHEN A CLOSED STORE IS            
W27710*                                ENCOUTNERED.                             
W26600*****************************************************************         
W30692* W30692  05-18-2006 R. MCDONALD ADD A CONTROL CARD TO INDICATE           
W30692*                                WHETHER OR NOT TO PRINT THE              
W30692*                                ***  TOTAL STORE XXXXX  ***              
W30692*                                LINE ALONG WITH THE TOTALS               
W30692*****************************************************************         
W28545* W28545  08-10-2006 B. GRAHAM DEPARTMENT INVENTORY PROJECT     *         
W28545*                              ADD JOIN OF TINVPAR/TINCNTL      *         
W28545*****************************************************************         
972188* P972188 01-22-2007 JILL LIN  ADD A CONTROL FIELD IN INP03     *         
972188*****************************************************************         
W33304* W33304  12-16-2008 JIMMY/INFOSYS                              *         
W33304*                    HILO CHANGES. PGM RUNS IN BOTH INK900 AND  *         
W33304*                    INK910. IN 910, SUPRESS GRAND TOTAL BY     *         
W33304*                    PASSING 'G' THROUGH THE JOB TO INCC248.    *         
W33304*****************************************************************         
W34259* W34259  11-01-2013 COGNIZANT                                            
W34259*                    MODIFIED PROGRAM TO INCORPORATE THE BELOW            
W34259*                    CHANGES TO THE REPORT.                               
W34259*                   -ADDED GMM, DMM AND BUYER DETAILS TO SUBTITLE,        
W34259*                   -ADDED CLASS DESCRIPTION COLUMN TO THE REPORT,        
W34259*                   -REMOVED SHORTAGE COST COLUMN,                        
W34259*                   -CHANGED THE HEADING OF SHORTAGE RETAIL AND           
W34259*                    SHRINK % TO TY SHORTAGE RETAIL & TY SHRINK %,        
W34259*                   -ROUNDED OFF EACH CLASS DOLLAR VALUES BEFORE          
W34259*                    DISPLAY.                                             
W34259*                   -SHRINK % WAS CALCULATED BASED ON DECIMAL             
W34259*                    VALUES AND ROUNDED BEFORE DISPLAYING                 
W34259*                    AS REQUESTED BY BUSINESS.                            
W34259*                   -DEPARTMENT, STORE AND COMPANY TOTALS ARE             
W34259*                    CALCULATED BASED ON DECIMAL VALUES AND               
W34259*                    ROUNDED BEFORE DISPLAYING AS REQUESTED               
W34259*                    BY BUSINESS.                                         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT STORE-SHORT-FILE           ASSIGN TO INP01.                   
           SELECT CONTROL-FILE               ASSIGN TO INP02.                   
           SELECT DATE-CONTROL               ASSIGN TO INP03.                   
           SELECT PRINT-FILE                 ASSIGN TO RPT01.                   
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  STORE-SHORT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W26600*    RECORD CONTAINS 94 CHARACTERS                                        
           DATA RECORD IS SHORT-REC.                                            
W34259 01  SHORT-REC                   PIC X(155).                              
                                                                                
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS CONTROL-REC.                                          
       01  CONTROL-REC                 PIC X(80).                               
                                                                                
       FD  DATE-CONTROL                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS DATE-REC.                                             
       01  DATE-REC                    PIC X(80).                               
                                                                                
       FD  PRINT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS PRINT-REC.                                            
       01  PRINT-REC                   PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
ADJ00  01  CONTROL-CARD.                                                        
ADJ00      05  CC-CONTROL-IND         PIC  X(16).                               
ADJ00          88  CC-LOSS-PREV-REPORT      VALUE ' LOSS PREVENTION'.           
ADJ00          88  CC-STORE-DIST-REPORT     VALUE 'STR DISTRIBUTION'.           
                                                                                
DATECC 01  DATE-CONTROL-CC.                                                     
W30692     05  CC-PRINT-TOTAL-STR-LN  PIC  X(01)   VALUE SPACES.                
W30692     05  CC-FILLER              PIC  X(01)   VALUE SPACES.                
DATECC     05  CC-CONTROL-DATE        PIC  X(10)   VALUE SPACES.                
972188     05  CC-FILLER              PIC  X(01)   VALUE SPACES.                
972188     05  CC-DATE-TYPE-CODE      PIC  X(01)   VALUE SPACES.                
972188         88  CC-DATE-TYPE-MONTHLY     VALUE 'M' ' '.                      
972188         88  CC-DATE-TYPE-WEEKLY      VALUE 'W'.                          
972188         88  CC-DATE-TYPE-MTD         VALUE 'D'.                          
972188         88  CC-DATE-TYPE-WTD         VALUE 'O'.                          
972188     05  CC-FILLER              PIC  X(66)   VALUE SPACES.                
                                                                                
ADJ00  01  PV-PROGRAM-VARIABLE.                                                 
W26600     05  PV-STORE-NBR           PIC  9(05)   VALUE ZEROES.                
ADJ00      05  PV-STORE-NBRX  REDEFINES                                         
W26600         PV-STORE-NBR           PIC  X(05).                               
PCT02A     05  PV-SHRINK-PERCENT      PIC  S9(3)V99 COMP-3 VALUE +0.            
RB1202     05  PV-HALF-PAGES          PIC 9(10)            VALUE ZEROES.        
RB1202     05  PV-REMAINDER-PAGES     PIC 9(10)            VALUE ZEROES.        
                                                                                
ADJ00  01  PS-PROGRAM-SWITCHES.                                                 
ADJ00      05  PS-PHYS-CNT-STORE-SW   PIC  X(03)           VALUE 'YES'.         
ADJ00          88  PS-PROCESS-STORE                        VALUE 'YES'.         
ADJ00          88  PS-BYPASS-STORE                         VALUE 'NO '.         
972188     05  PS-PHYS-CNT-LOC-SW     PIC  X               VALUE 'Y'.           
972188         88  PHYS-CNT-LOC                            VALUE 'Y'.           
972188         88  NOT-PHYS-CNT-LOC                        VALUE 'N'.           
W33304     05  PS-CODE-BREAK-SW     PIC  X                 VALUE 'N'.           
W33304         88  PS-CODE-BREAK-YES                       VALUE 'Y'.           
W33304         88  PS-CODE-BREAK-NO                        VALUE 'N'.           
                                                                                
RB1202 01  PC-PROGRAM-CONSTANTS.                                                
RB1202     05  PC-TWO                 PIC 9(02)            VALUE 2.             
                                                                                
       01  MV-MISC-VARIABLES.                                                   
           05  MV-ABEND-CODE          PIC  9(02)    COMP   VALUE 0.             
           05  MV-PRINT-REC-SAVE      PIC  X(132)          VALUE ' '.           
           05  MV-STRIPPED-STORE.                                               
               10  MV-JUST-STORE      PIC  X(03).                               
               10  FILLER             PIC  X(01).                               
               10  FILLER             PIC  X(01).                               
           05  MV-NUMB-LINES          PIC  9(02)           VALUE 0.             
           05  MV-LINE-COUNT          PIC  9(02)    COMP-3 VALUE 61.            
           05  MV-PAGE-COUNT          PIC  9(04)    COMP-3 VALUE 0.             
W33304     05  MV-PREV-FSCL-MN-DATE   PIC  X(10)           VALUE SPACES.        
W33304     05  MV-PREV-CYCLE-NBR      PIC S9(9)     COMP   VALUE ZERO.          
           05  MV-PREV-DEPARTMENT     PIC  X(03)           VALUE ' '.           
           05  MV-PREV-STORE          PIC  X(05)           VALUE ' '.           
W26600     05  MV-DPT-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-DPT-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-DPT-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-DPT-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-DPT-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-DPT-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-DPT-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-CYC-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
W33304     05  MV-MTH-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-STR-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-STR-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-STR-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-STR-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-STR-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-STR-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-STR-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-TOT-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-TOT-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-TOT-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
           05  MV-TOT-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-TOT-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.            
0200RM     05  MV-TOT-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.            
W26600     05  MV-TOT-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTDPT-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTSTR-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTCYC-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTMTH-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-TOTRND-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.            
W34259     05  MV-RND-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.               
W34259     05  MV-RND-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.               
       01  CL-CLASS-LEVEL-PRINT.                                                
W34259     05  FILLER                  PIC X(01)            VALUE ' '.          
W34259     05  CL-CLASS-NUMBER         PIC X(02).                               
W34259     05  FILLER                  PIC X(03)            VALUE ' '.          
W34259     05  CL-CLASS-DESCRIPTION    PIC X(25).                               
W34259     05  CL-DEPT-BOOK-INV        PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  CL-DEPT-SHRINK-ALLOW    PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  CL-DEPT-TOTAL-RETAIL    PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  CL-DEPT-TOTAL-SALES     PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  CL-DEPT-PHY-INV-RETAIL  PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  CL-DEPT-TY-SHORT-RETAIL PIC ZZ,ZZZ,ZZZ,ZZZ-.                     
W34259     05  FILLER                  PIC X(03)            VALUE ' '.          
W34259     05  CL-SHRINK-TY-PERCENT    PIC ZZZ.99-.                             
W33304 01  SH-SECOND-HEADING-LINE.                                              
W33304     05  FILLER                 PIC X(134)           VALUE ' '.           
W33304 01  SH-SECOND-HEADING-LINE1.                                             
W33304     05  FILLER                 PIC X(49)            VALUE ' '.           
W33304     05  FILLER                 PIC X(45)            VALUE                
W33304           'TOTAL PHYSICAL INVENTORY SHORTAGE REPORT '.                   
W33304     05  FILLER                 PIC X(40)            VALUE ' '.           
W33304 01  SH-SECOND-HEADING-LINE2.                                             
W33304     05  FILLER                 PIC X(43)            VALUE ' '.           
W33304     05  SH-STORE.                                                        
W33304         10  FILLER             PIC X(06)            VALUE                
W33304           'STORE '.                                                      
W33304         10  SH-STORE-NUMBER    PIC X(06)            VALUE ' '.           
W33304     05  FILLER                 PIC X(39)            VALUE                
W33304           'PHYSICAL INVENTORY SHORTAGE REPORT '.                         
W33304     05  FILLER                 PIC X(45)            VALUE ' '.           
W33304 01  SH-CYCLE-MONTH-HEADER.                                               
W33304     05  FILLER                 PIC X(134)           VALUE ' '.           
W33304 01  SH-CYCLE-MONTH-HEADER1.                                              
W33304     05  FILLER                 PIC X(07)            VALUE                
W33304                                                     ' MONTH '.           
W33304     05  SH-REPORT-MON          PIC X(03)            VALUE ' '.           
W33304     05  FILLER                 PIC X(50)            VALUE ' '.           
W33304     05  SH-REPORT-MONTH        PIC X(09)            VALUE ' '.           
W33304     05  FILLER                 PIC X(55)            VALUE ' '.           
W33304 01  SH-CYCLE-MONTH-HEADER2.                                              
W33304     05  FILLER                 PIC X(07)            VALUE                
W33304                                                     ' CYCLE '.           
W33304     05  SH-CYCLE-NO            PIC 9(05)            VALUE 0.             
W33304     05  FILLER                 PIC X(43)            VALUE ' '.           
W33304     05  SH-CYCLE-NAME          PIC X(20)            VALUE ' '.           
W33304     05  FILLER                 PIC X(64)            VALUE ' '.           
W33304 01  SH-CYCLE-MONTH-HEADER3.                                              
W33304     05  FILLER                 PIC X(07)            VALUE                
W33304                                                     ' CYCLE '.           
W33304     05  SH-CYCLE-NUM           PIC 9(05)            VALUE 0.             
W33304     05  FILLER                 PIC X(43)            VALUE ' '.           
W33304     05  SH-CYCLE-NAM           PIC X(20)            VALUE ' '.           
W33304     05  FILLER                 PIC X(64)            VALUE ' '.           
       01  TH-THIRD-HEADING-LINE.                                               
W34259     05  FILLER                 PIC X(09)            VALUE ' '.           
W34259     05  FILLER                 PIC X(52)            VALUE                
W34249         'CLASS                      BOOK           SHRINKAGE '.          
W34259     05  FILLER                 PIC X(45)            VALUE                
W34259           '        TOTAL            TOTAL     PHY. INV. '.               
W34259     05  FILLER                 PIC X(26)            VALUE                
W34249           '   TY SHORTAGE  TY SHRINK'.                                   
       01  FH-FOURTH-HEADING-LINE.                                              
W34259     05  FILLER                 PIC X(51)            VALUE                
W34249           ' CLASS   DESCRIPTION              INVENTORY'.                 
W34259     05  FILLER                 PIC X(54)            VALUE                
W34249         'ALLOWANCE         RETAIL           SALES      RETAIL'.          
W34249     05  FILLER                 PIC X(24)            VALUE                
W34249           '       RETAIL         %'.                                     
       01  FH-FIFTH-HEADING-LINE.                                               
           05  FILLER                 PIC X(06)            VALUE ' '.           
           05  FILLER                 PIC X(07)            VALUE                
                 'STORE: '.                                                     
W26600     05  FH-STORE-NUMBER        PIC X(05).                                
           05  FILLER                 PIC X(02)            VALUE ' '.           
           05  FH-STORE-NAME          PIC X(10).                                
W34259     05  FILLER                 PIC X(07)            VALUE                
W34259           '  GMM: '.                                                     
W34259     05  FH-GMM-NUMBER          PIC 9(03).                                
W34259     05  FILLER                 PIC X(07)            VALUE                
W34259           '  DMM: '.                                                     
W34259     05  FH-DMM-NUMBER          PIC 9(03).                                
W34259     05  FILLER                 PIC X(09)            VALUE                
W34259           '  BUYER: '.                                                   
W34259     05  FH-BUYER-NUMBER        PIC 9(03).                                
           05  FILLER                 PIC X(14)            VALUE                
                 '  DEPARTMENT: '.                                              
           05  FH-DEPARTMENT-NUMBER   PIC X(03).                                
W34259     05  FILLER                 PIC X(19)            VALUE                
W34259           '  DEPARTMENT NAME: '.                                         
           05  FH-DEPARTMENT-NAME     PIC X(20).                                
W34259     05  FILLER                 PIC X(17)            VALUE ' '.           
       01  DT-DEPARTMENT-TOTAL-LINE.                                            
           05  FILLER                 PIC X(18)            VALUE                
                 '  ***  TOTAL DEPT '.                                          
           05  DT-DEPARTMENT-NUMBER   PIC X(03).                                
           05  FILLER                 PIC X(05)            VALUE                
                 '  ***'.                                                       
           05  FILLER                 PIC X(106)           VALUE ' '.           
       01  ST-STORE-TOTAL-LINE.                                                 
           05  FILLER                 PIC X(19)            VALUE                
                 '  ***  TOTAL STORE '.                                         
W26600     05  ST-STORE-NUMBER        PIC X(05).                                
           05  FILLER                 PIC X(05)            VALUE                
                 '  ***'.                                                       
           05  FILLER                 PIC X(105)           VALUE ' '.           
W33304 01  ST-CYCLE-TOTAL-LINE.                                                 
W33304     05  FILLER                 PIC X(19)            VALUE                
W33304           '  ***  TOTAL CYCLE '.                                         
W33304     05  ST-CYCLE-NUMBER        PIC 9(05)            VALUE ZEROES.        
W33304     05  FILLER                 PIC X(05)            VALUE                
W33304           '  ***'.                                                       
W33304     05  FILLER                 PIC X(105)           VALUE ' '.           
W33304 01  ST-MONTH-TOTAL-LINE.                                                 
W33304     05  FILLER                 PIC X(24)            VALUE                
W33304           '  ***  TOTAL MONTH   ***'.                                    
W33304     05  FILLER                 PIC X(108)           VALUE ' '.           
       01  TR-TOTAL-RETAIL-LINE.                                                
           05  FILLER                 PIC X(24)            VALUE                
W33304           '  ***  GRAND TOTAL   ***'.                                    
           05  FILLER                 PIC X(108)           VALUE ' '.           
RB1202 01  BP-BLANK-PAGE-DETAIL.                                                
RB1202     05  FILLER                 PIC X(46)            VALUE SPACES.        
RB1202     05  FILLER                 PIC X(40)            VALUE                
RB1202           '  THIS PAGE IS INTENTIALLY LEFT BLANK   '.                    
RB1202     05  FILLER                 PIC X(46)            VALUE SPACES.        
       01  WS-LEVEL-88.                                                         
           05  WS-RECORD-INDICATOR            PIC X(03)  VALUE 'YES'.           
               88  MORE-RECORDS                          VALUE 'YES'.           
               88  NO-MORE-RECORDS                       VALUE 'NO'.            
           05  DEPARTMENT-SWITCH              PIC X(03)  VALUE 'YES'.           
               88  NEW-DEPARTMENT                        VALUE 'YES'.           
               88  OLD-DEPARTMENT                        VALUE 'NO'.            
       01  SD-SYSTEM-DATE.                                                      
           05  SD-YY              PIC 9(02).                                    
           05  SD-MM              PIC 9(02).                                    
           05  SD-DD              PIC 9(02).                                    
       01  ST-SYSTEM-TIME.                                                      
           05  ST-HH              PIC 9(02).                                    
           05  ST-MM              PIC 9(02).                                    
           05  ST-SS              PIC 9(02).                                    
      *                                                                         
0200RM 01  ABEND-AREAS.                                                         
0200RM     05  ABEND-CODE              PIC S9(04)  VALUE ZERO                   
0200RM                                             COMP SYNC.                   
0200RM     05  AA-ABEND-LIT            PIC X(40)   VALUE                        
0200RM           '*****       ABEND'.                                           
0200RM     05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
0200RM             '*****   PROGRAM: INKRP145'.                                 
0200RM     05  AA-PARAGRAPH-LIT.                                                
0200RM         10  FILLER              PIC X(17)   VALUE                        
0200RM             '***** PARAGRAPH: '.                                         
0200RM         10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
0200RM     05  AA-MESSAGE-LINE.                                                 
0200RM         10  FILLER              PIC X(06)   VALUE '*****'.               
0200RM         10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
0200RM     05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
0200RM             '*****    DB2 ERROR'.                                        
0200RM     05  AA-DB2-OPERATION-LIT.                                            
0200RM         10  FILLER              PIC X(17)   VALUE                        
0200RM             '***** OPERATION: '.                                         
0200RM         10  AA-DB2-OPERATION.                                            
0200RM             15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
0200RM             15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
0200RM     05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
0200RM     05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
0200RM     05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
0200RM     05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
      *                                                                         
W34259     COPY INRD010.                                                        
           COPY DPWS132O.                                                       
       EJECT                                                                    
                                                                                
0200RM******************************************************************        
0200RM*  DB2 ERROR MESSAGES FORMAT AREA.                                        
0200RM******************************************************************        
0200RM     COPY DPWS004.                                                        
0200RM                                                                          
0200RM******************************************************************        
0200RM*  DB2 ERRORS                                                             
0200RM******************************************************************        
0200RM     COPY SQLCA2.                                                         
0200RM                                                                          
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
ADJ00 ******************************************************************        
ADJ00 *  DB2 INVPAR TABLE LAYOUT                                                
ADJ00 ******************************************************************        
ADJ00      EXEC SQL                                                             
ADJ00           INCLUDE TINVPAR                                                 
ADJ00      END-EXEC.                                                            
ADJ00                                                                           
0200RM******************************************************************        
0200RM*  DB2 DTATTR TABLE LAYOUT                                                
0200RM******************************************************************        
0200RM     EXEC SQL                                                             
0200RM          INCLUDE TDTATTR                                                 
0200RM     END-EXEC.                                                            
0200RM                                                                          
0200RM******************************************************************        
0200RM*  DB2 COMMUNICATION AREA                                                 
0200RM******************************************************************        
0200RM     EXEC SQL                                                             
0200RM          INCLUDE SQLCA                                                   
0200RM     END-EXEC.                                                            
0200RM                                                                          
0200RM     EJECT                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH DOES THE INITIAL READS AND CONTROLS THE               
      *    PROCESSING OF THE PROGRAM.                                           
      ******************************************************************        
       A000-MAIN-DRIVER.                                                        
                                                                                
           PERFORM Z200-CONTROL-RECORD-PROCESS.                                 
                                                                                
           OPEN INPUT  STORE-SHORT-FILE                                         
ADJ00                  CONTROL-FILE                                             
DATECC                 DATE-CONTROL.                                            
           OPEN OUTPUT PRINT-FILE.                                              
                                                                                
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC                  
W33304         AT END                                                           
W33304             GO TO A010-END-JOB.                                          
W33304                                                                          
DATECC     PERFORM Z500-READ-DATE-CONTROL.                                      
DATECC                                                                          
W34259*    USE IN010-OPN-FSCL-MN-DTE UNLESS DATE OVERRIDE IS USED               
W30692     IF CC-CONTROL-DATE = SPACES                                          
W33304         PERFORM Z300-SELECT-TDTATTR                                      
DATECC     ELSE                                                                 
DATECC         PERFORM Z600-DATE-OVERRIDE                                       
DATECC     END-IF.                                                              
                                                                                
ADJ00      PERFORM Z400-READ-CONTROL-FILE.                                      
                                                                                
W34259     MOVE IN010-STORE-NUMB    TO PV-STORE-NBR.                            
ADJ00 *                                                                         
ADJ00 *   LOSS PREVENTION RECEIVES REPORTS ON ALL LOCATIONS                     
ADJ00      IF CC-LOSS-PREV-REPORT                                               
ADJ00          CONTINUE                                                         
ADJ00      ELSE                                                                 
ADJ00 *   DISTRIBUTION TO STORE LOCATIONS NEEDS TO BE DETERMINED                
ADJ00          IF CC-STORE-DIST-REPORT                                          
ADJ00              SET PS-BYPASS-STORE TO TRUE                                  
W26600             MOVE PV-STORE-NBR   TO INVPAR-LOC-NBR                        
ADJ00              PERFORM H150-GET-PHYS-CNT-STORE                              
ADJ00                  UNTIL PS-PROCESS-STORE                                   
ADJ00                     OR NO-MORE-RECORDS                                    
ADJ00          END-IF                                                           
ADJ00      END-IF.                                                              
ADJ00 *                                                                         
ADJ00      IF MORE-RECORDS                                                      
W34259         MOVE IN010-DEPARTMENT-NUMB TO  MV-PREV-DEPARTMENT                
                                              FH-DEPARTMENT-NUMBER              
                                              DT-DEPARTMENT-NUMBER              
W34259         MOVE IN010-STORE-NUMB TO  MV-PREV-STORE                          
                                         FH-STORE-NUMBER                        
                                         ST-STORE-NUMBER                        
                                         SH-STORE-NUMBER                        
ADJ00                                    PV-STORE-NBR                           
W34259         MOVE IN010-OPN-FSCL-MN-DTE TO MV-PREV-FSCL-MN-DATE               
W34259         MOVE IN010-INV-ID TO MV-PREV-CYCLE-NBR                           
                                                                                
W34259         MOVE IN010-DEPARTMENT-NAME TO FH-DEPARTMENT-NAME                 
W34259         MOVE IN010-GMM-NUMBER      TO FH-GMM-NUMBER                      
W34259         MOVE IN010-DMM-NUMBER      TO FH-DMM-NUMBER                      
W34259         MOVE IN010-BUYER-NUMB      TO FH-BUYER-NUMBER                    
W34259         MOVE IN010-STORE-NAME TO FH-STORE-NAME                           
W33304         PERFORM Z300-SELECT-TDTATTR                                      
W33304         PERFORM Z310-SELECT-CYCLE-NAME                                   
               ACCEPT ST-SYSTEM-TIME FROM TIME                                  
               MOVE ST-HH TO DP132O-RUN-HOUR                                    
               MOVE ST-MM TO DP132O-RUN-MINUTE                                  
               MOVE 'INKRP145' TO DP132O-PROGRAM-NAME                           
               MOVE 1 TO DP132O-REPORT-NUMBER                                   
               PERFORM B000-PROCESS-SHORTAGE-RECORDS                            
                   UNTIL NO-MORE-RECORDS                                        
W33304         PERFORM C000-DEPARTMENT-BREAK                                    
W33304         PERFORM D000-STORE-BREAK                                         
W33304*        PERFORM D100-CYCLE-BREAK                                         
W33304*        PERFORM D200-MONTH-BREAK                                         
W33304         IF CC-PRINT-TOTAL-STR-LN = SPACE OR 'S'                          
W33304            PERFORM E000-FINAL-TOTALS                                     
W33304         END-IF                                                           
ADJ00      END-IF.                                                              
ADJ00                                                                           
           CLOSE STORE-SHORT-FILE                                               
                 CONTROL-FILE                                                   
                 PRINT-FILE.                                                    
           STOP RUN.                                                            
ADJ00                                                                           
       A010-END-JOB.                                                            
           CLOSE STORE-SHORT-FILE                                               
                 CONTROL-FILE                                                   
                 PRINT-FILE.                                                    
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH DOES ALL THE BREAK PROCESSING AND ACCUMUMATES         
W33304*    MONTH, CYCLE, DEPARTMENT, STORE, AND COMPANY TOTALS.  SHRINK         
W33304*    PERCENT IS CALCULATED AND DETAIL RECORDS ARE WRITTEN.                
      ******************************************************************        
       B000-PROCESS-SHORTAGE-RECORDS.                                           
                                                                                
W33304     MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.              
W33304     MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.               
W34259*    IF MV-PREV-FSCL-MN-DATE NOT = IN010-OPN-FSCL-MN-DTE                  
W33304*       PERFORM C000-DEPARTMENT-BREAK                                     
W33304*       PERFORM D000-STORE-BREAK                                          
W33304*       PERFORM D100-CYCLE-BREAK                                          
W33304*       PERFORM D200-MONTH-BREAK                                          
W33304*       SET PS-CODE-BREAK-YES TO TRUE                                     
W33304*       PERFORM Z300-SELECT-TDTATTR                                       
W33304*       PERFORM Z310-SELECT-CYCLE-NAME                                    
W33304*    END-IF.                                                              
W33304                                                                          
W34259     IF MV-PREV-CYCLE-NBR NOT = IN010-INV-ID                              
W33304*       AND PS-CODE-BREAK-NO )                                            
W33304*       PERFORM C000-DEPARTMENT-BREAK                                     
W33304*       PERFORM D000-STORE-BREAK                                          
W33304*       PERFORM D100-CYCLE-BREAK                                          
W33304*       SET PS-CODE-BREAK-YES TO TRUE                                     
W33304        PERFORM Z310-SELECT-CYCLE-NAME                                    
W33304     END-IF.                                                              
W33304                                                                          
W34259     IF MV-PREV-DEPARTMENT NOT = IN010-DEPARTMENT-NUMB                    
W33304        AND PS-CODE-BREAK-NO                                              
               PERFORM C000-DEPARTMENT-BREAK.                                   
W33304     MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.              
W33304     MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.               
W34259     IF MV-PREV-STORE NOT = IN010-STORE-NUMB                              
W33304        AND PS-CODE-BREAK-NO                                              
               PERFORM D000-STORE-BREAK.                                        
           IF NEW-DEPARTMENT                                                    
               MOVE 3 TO MV-NUMB-LINES                                          
               MOVE FH-FIFTH-HEADING-LINE TO PRINT-REC                          
               MOVE 'NO' TO DEPARTMENT-SWITCH                                   
               PERFORM F100-PRINT-HEADING5.                                     
W33304     SET PS-CODE-BREAK-NO TO TRUE.                                        
W34259     ADD IN010-BOOK-INV          TO MV-DPT-BOOK-INV                       
                                          MV-STR-BOOK-INV                       
W33304                                    MV-CYC-BOOK-INV                       
W33304                                    MV-MTH-BOOK-INV                       
                                          MV-TOT-BOOK-INV.                      
W34259     ADD IN010-SHRINK-ALLOWANCE  TO MV-DPT-SHRINK-ALLOW                   
                                          MV-STR-SHRINK-ALLOW                   
W33304                                    MV-CYC-SHRINK-ALLOW                   
W33304                                    MV-MTH-SHRINK-ALLOW                   
                                          MV-TOT-SHRINK-ALLOW.                  
W34259     ADD IN010-TOTAL-RETAIL      TO MV-DPT-TOTAL-RETAIL                   
                                          MV-STR-TOTAL-RETAIL                   
W33304                                    MV-CYC-TOTAL-RETAIL                   
W33304                                    MV-MTH-TOTAL-RETAIL                   
                                          MV-TOT-TOTAL-RETAIL.                  
W34259     ADD IN010-TOTAL-COST        TO MV-DPT-TOTAL-COST                     
                                          MV-STR-TOTAL-COST                     
W33304                                    MV-CYC-TOTAL-COST                     
W33304                                    MV-MTH-TOTAL-COST                     
                                          MV-TOT-TOTAL-COST.                    
W34259     ADD IN010-PHY-INV-RETAIL    TO MV-DPT-PHY-INV-RETAIL                 
                                          MV-STR-PHY-INV-RETAIL                 
W33304                                    MV-CYC-PHY-INV-RETAIL                 
W33304                                    MV-MTH-PHY-INV-RETAIL                 
                                          MV-TOT-PHY-INV-RETAIL.                
W34259     ADD IN010-SHORT-RETAIL      TO MV-DPT-SHORT-RETAIL                   
                                          MV-STR-SHORT-RETAIL                   
W33304                                    MV-CYC-SHORT-RETAIL                   
W33304                                    MV-MTH-SHORT-RETAIL                   
                                          MV-TOT-SHORT-RETAIL.                  
W34259     ADD IN010-TOTAL-SALES       TO MV-DPT-TOTAL-SALES                    
                                          MV-STR-TOTAL-SALES                    
W33304                                    MV-CYC-TOTAL-SALES                    
W33304                                    MV-MTH-TOTAL-SALES                    
                                          MV-TOT-TOTAL-SALES.                   
W34259     COMPUTE MV-RND-BOOK-INV  ROUNDED                                     
W34259                           = IN010-BOOK-INV * 1.                          
W34259     COMPUTE MV-RND-SHRINK-ALLOW  ROUNDED                                 
W34259                           = IN010-SHRINK-ALLOWANCE * 1.                  
W34259     COMPUTE MV-RND-TOTAL-RETAIL ROUNDED                                  
W34259                           = IN010-TOTAL-RETAIL * 1.                      
W34259     COMPUTE MV-RND-PHY-INV-RETAIL ROUNDED                                
W34259                           = IN010-PHY-INV-RETAIL * 1.                    
W34259     COMPUTE MV-RND-TY-SHORT-RETAIL ROUNDED                               
W34259                           = IN010-SHORT-RETAIL * 1.                      
W34259     COMPUTE MV-RND-TOTAL-SALES  ROUNDED                                  
W34259                           = IN010-TOTAL-SALES * 1.                       
W34259                                                                          
W34259     MOVE MV-RND-BOOK-INV         TO CL-DEPT-BOOK-INV.                    
W34259     MOVE MV-RND-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.                
W34259     MOVE MV-RND-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.                
W34259     MOVE MV-RND-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.                 
W34259     MOVE MV-RND-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.              
W34259     MOVE MV-RND-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.             
PCT02                                                                           
W34259     IF IN010-TOTAL-SALES = ZERO                                          
PCT02A         MOVE 999.99 TO PV-SHRINK-PERCENT                                 
           ELSE                                                                 
PCT02          COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
W34259          (IN010-SHORT-RETAIL * 100 / IN010-TOTAL-SALES)                  
                       ON SIZE ERROR                                            
PCT02A                     MOVE 999.99 TO PV-SHRINK-PERCENT.                    
PCT02                                                                           
PCT02 *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
W34259     IF IN010-SHORT-RETAIL > 0                                            
PCT02          IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT                                  
W34259                          = (PV-SHRINK-PERCENT * (-1))                    
PCT02          ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
PCT02          END-IF                                                           
PCT02      ELSE                                                                 
PCT02 *        IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE        
W34259         IF IN010-SHORT-RETAIL < 0                                        
PCT02              IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
PCT02                                       (PV-SHRINK-PERCENT * (-1))          
PCT02              ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
PCT02              END-IF                                                       
PCT02          ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
PCT02          END-IF                                                           
PCT02      END-IF.                                                              
PCT02                                                                           
W34259     MOVE IN010-CLASS-NUMB TO CL-CLASS-NUMBER.                            
W34259     MOVE IN010-CLASS-DESCRIPTION TO CL-CLASS-DESCRIPTION.                
           MOVE 1 TO MV-NUMB-LINES.                                             
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                              
           PERFORM F000-PRINT-A-LINE.                                           
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC                  
               AT END                                                           
                   MOVE 'NO' TO WS-RECORD-INDICATOR.                            
ADJ00 *   LOSS PREVENTION RECEIVES REPORTS ON ALL LOCATIONS                     
ADJ00      IF CC-LOSS-PREV-REPORT                                               
ADJ00          CONTINUE                                                         
ADJ00      ELSE                                                                 
ADJ00 *   DISTRIBUTION TO STORE LOCATIONS NEEDS TO BE DETERMINED                
ADJ00          IF ((CC-STORE-DIST-REPORT) AND                                   
W34259             (IN010-STORE-NUMB   NOT EQUAL  PV-STORE-NBR))                
ADJ00 *                                                                         
W34259             MOVE IN010-STORE-NUMB     TO PV-STORE-NBR                    
ADJ00              SET PS-BYPASS-STORE TO TRUE                                  
W26600             MOVE PV-STORE-NBR   TO INVPAR-LOC-NBR                        
ADJ00              PERFORM H150-GET-PHYS-CNT-STORE                              
ADJ00                  UNTIL PS-PROCESS-STORE                                   
ADJ00                     OR NO-MORE-RECORDS                                    
ADJ00          END-IF                                                           
ADJ00      END-IF.                                                              
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH MOVES ALL THE DEPARTMENT INFO TO THE DETAIL           
      *    LINE AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK          
      *    PERCENT AT THE DEPARTMENT LEVEL.                                     
      ******************************************************************        
       C000-DEPARTMENT-BREAK.                                                   
W34259     COMPUTE MV-TOTDPT-BOOK-INV  ROUNDED                                  
W34259                           = MV-DPT-BOOK-INV * 1.                         
W34259     COMPUTE MV-TOTDPT-SHRINK-ALLOW ROUNDED                               
W34259                           = MV-DPT-SHRINK-ALLOW * 1.                     
W34259     COMPUTE MV-TOTDPT-TOTAL-RETAIL ROUNDED                               
W34259                           = MV-DPT-TOTAL-RETAIL * 1.                     
W34259     COMPUTE MV-TOTDPT-PHY-INV-RETAIL ROUNDED                             
W34259                          = MV-DPT-PHY-INV-RETAIL * 1.                    
W34259     COMPUTE MV-TOTDPT-TY-SHORT-RETAIL ROUNDED                            
W34259                           = MV-DPT-SHORT-RETAIL * 1.                     
W34259     COMPUTE MV-TOTDPT-TOTAL-SALES ROUNDED                                
W34259                           = MV-DPT-TOTAL-SALES * 1.                      
W34259     MOVE MV-TOTDPT-BOOK-INV         TO CL-DEPT-BOOK-INV.                 
W34259     MOVE MV-TOTDPT-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.             
W34259     MOVE MV-TOTDPT-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.             
W34259     MOVE MV-TOTDPT-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.           
W34259     MOVE MV-TOTDPT-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.          
W34259     MOVE MV-TOTDPT-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.              
PCT02                                                                           
           IF MV-DPT-TOTAL-SALES = ZERO                                         
W34259         MOVE 999.99 TO CL-SHRINK-TY-PERCENT                              
           ELSE                                                                 
PCT02          COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
                   (MV-DPT-SHORT-RETAIL * 100 / MV-DPT-TOTAL-SALES)             
                       ON SIZE ERROR                                            
PCT02A                     MOVE 999.99 TO PV-SHRINK-PERCENT.                    
PCT02                                                                           
PCT02 *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
PCT02      IF MV-DPT-SHORT-RETAIL > 0                                           
PCT02          IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT                                  
W34259                         = (PV-SHRINK-PERCENT * (-1))                     
PCT02          ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
PCT02          END-IF                                                           
PCT02      ELSE                                                                 
PCT02 *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE            
PCT02          IF MV-DPT-SHORT-RETAIL < 0                                       
PCT02              IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
PCT02                                       (PV-SHRINK-PERCENT * (-1))          
PCT02              ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
PCT02              END-IF                                                       
PCT02          ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
PCT02          END-IF                                                           
PCT02      END-IF.                                                              
PCT02                                                                           
           IF MV-LINE-COUNT > 56                                                
                MOVE 61 TO MV-LINE-COUNT.                                       
           MOVE 2 TO MV-NUMB-LINES.                                             
           MOVE DT-DEPARTMENT-TOTAL-LINE TO PRINT-REC.                          
           PERFORM F000-PRINT-A-LINE.                                           
           MOVE SPACES TO CL-CLASS-NUMBER.                                      
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                                 
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                              
           PERFORM F000-PRINT-A-LINE.                                           
W34259     MOVE IN010-DEPARTMENT-NUMB  TO MV-PREV-DEPARTMENT                    
                                          FH-DEPARTMENT-NUMBER                  
                                          DT-DEPARTMENT-NUMBER.                 
W34259     MOVE IN010-DEPARTMENT-NAME TO FH-DEPARTMENT-NAME.                    
W34259     MOVE IN010-GMM-NUMBER      TO FH-GMM-NUMBER.                         
W34259     MOVE IN010-DMM-NUMBER      TO FH-DMM-NUMBER.                         
W34259     MOVE IN010-BUYER-NUMB      TO FH-BUYER-NUMBER.                       
W34259     MOVE IN010-STORE-NAME      TO FH-STORE-NAME.                         
           MOVE ZERO TO MV-DPT-BOOK-INV                                         
                        MV-DPT-SHRINK-ALLOW                                     
                        MV-DPT-TOTAL-RETAIL                                     
                        MV-DPT-PHY-INV-RETAIL                                   
                        MV-DPT-SHORT-RETAIL                                     
                        MV-DPT-TOTAL-SALES.                                     
           MOVE 'YES' TO DEPARTMENT-SWITCH.                                     
                                                                                
      ******************************************************************        
      *    THIS PARAGRAPH MOVES ALL THE STORE INFO TO THE DETAIL LINE           
      *    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK               
      *    PERCENT AT THE STORE LEVEL.                                          
      ******************************************************************        
       D000-STORE-BREAK.                                                        
W34259     COMPUTE MV-TOTSTR-BOOK-INV  ROUNDED                                  
W34259                           = MV-STR-BOOK-INV * 1.                         
W34259     COMPUTE MV-TOTSTR-SHRINK-ALLOW ROUNDED                               
W34259                           = MV-STR-SHRINK-ALLOW * 1.                     
W34259     COMPUTE MV-TOTSTR-TOTAL-RETAIL ROUNDED                               
W34259                           = MV-STR-TOTAL-RETAIL * 1.                     
W34259     COMPUTE MV-TOTSTR-PHY-INV-RETAIL ROUNDED                             
W34259                           = MV-STR-PHY-INV-RETAIL * 1.                   
W34259     COMPUTE MV-TOTSTR-TY-SHORT-RETAIL ROUNDED                            
W34259                           = MV-STR-SHORT-RETAIL * 1.                     
W34259     COMPUTE MV-TOTSTR-TOTAL-SALES ROUNDED                                
W34259                           = MV-STR-TOTAL-SALES * 1.                      
W34259     MOVE MV-TOTSTR-BOOK-INV         TO CL-DEPT-BOOK-INV.                 
W34259     MOVE MV-TOTSTR-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.             
W34259     MOVE MV-TOTSTR-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.             
W34259     MOVE MV-TOTSTR-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.           
W34259     MOVE MV-TOTSTR-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.          
W34259     MOVE MV-TOTSTR-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.              
PCT02                                                                           
           IF MV-STR-TOTAL-SALES = ZERO                                         
W34259         MOVE 999.99 TO CL-SHRINK-TY-PERCENT                              
           ELSE                                                                 
PCT02          COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
                   (MV-STR-SHORT-RETAIL * 100 / MV-STR-TOTAL-SALES)             
                       ON SIZE ERROR                                            
PCT02A                     MOVE 999.99 TO PV-SHRINK-PERCENT.                    
PCT02                                                                           
PCT02 *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
PCT02      IF MV-STR-SHORT-RETAIL > 0                                           
PCT02          IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT =                                
W34259                           (PV-SHRINK-PERCENT * (-1))                     
PCT02          ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
PCT02          END-IF                                                           
PCT02      ELSE                                                                 
PCT02 *        IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE        
PCT02          IF MV-STR-SHORT-RETAIL < 0                                       
PCT02              IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
PCT02                                       (PV-SHRINK-PERCENT * (-1))          
PCT02              ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
PCT02              END-IF                                                       
PCT02          ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
PCT02          END-IF                                                           
PCT02      END-IF.                                                              
PCT02                                                                           
W30692*    IF CC-PRINT-TOTAL-STR-LN = SPACE                                     
W33304     IF CC-PRINT-TOTAL-STR-LN = SPACE OR 'G'                              
              MOVE 3 TO MV-NUMB-LINES                                           
              MOVE ST-STORE-TOTAL-LINE TO PRINT-REC                             
              PERFORM F000-PRINT-A-LINE                                         
              MOVE 2 TO MV-NUMB-LINES                                           
              MOVE SPACES TO CL-CLASS-NUMBER                                    
W34259        MOVE SPACES TO CL-CLASS-DESCRIPTION                               
              MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC                            
              PERFORM F000-PRINT-A-LINE                                         
W30692     END-IF.                                                              
W30692                                                                          
           PERFORM Z100-WRITE-STORE-BREAK.                                      
W34259     MOVE IN010-STORE-NUMB  TO MV-PREV-STORE                              
                                     FH-STORE-NUMBER                            
                                     ST-STORE-NUMBER                            
                                     SH-STORE-NUMBER.                           
W34259     MOVE IN010-STORE-NAME TO FH-STORE-NAME.                              
W33304     MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.              
W33304     MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.               
           MOVE ZERO TO MV-STR-BOOK-INV                                         
                       MV-STR-SHRINK-ALLOW                                      
                       MV-STR-TOTAL-RETAIL                                      
                       MV-STR-PHY-INV-RETAIL                                    
                       MV-STR-SHORT-RETAIL                                      
                       MV-STR-TOTAL-SALES.                                      
           MOVE 61 TO MV-LINE-COUNT.                                            
                                                                                
W33304******************************************************************        
W33304*    THIS PARAGRAPH MOVES THE PARTICULAR CYCLE INFO TO THE DETAIL         
W33304*    LINE AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK          
W33304*    PERCENT AT THE COMPANY LEVEL.                                        
W33304******************************************************************        
W33304 D100-CYCLE-BREAK.                                                        
W34259     COMPUTE MV-TOTCYC-BOOK-INV  ROUNDED                                  
W34259                           = MV-CYC-BOOK-INV * 1.                         
W34259     COMPUTE MV-TOTCYC-SHRINK-ALLOW ROUNDED                               
W34259                           = MV-CYC-SHRINK-ALLOW * 1.                     
W34259     COMPUTE MV-TOTCYC-TOTAL-RETAIL ROUNDED                               
W34259                           = MV-CYC-TOTAL-RETAIL * 1.                     
W34259     COMPUTE MV-TOTCYC-PHY-INV-RETAIL ROUNDED                             
W34259                           = MV-CYC-PHY-INV-RETAIL * 1.                   
W34259     COMPUTE MV-TOTCYC-TY-SHORT-RETAIL ROUNDED                            
W34259                           = MV-CYC-SHORT-RETAIL * 1.                     
W34259     COMPUTE MV-TOTCYC-TOTAL-SALES ROUNDED                                
W34259                           = MV-CYC-TOTAL-SALES * 1.                      
W34259     MOVE MV-TOTCYC-BOOK-INV         TO CL-DEPT-BOOK-INV.                 
W34259     MOVE MV-TOTCYC-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.             
W34259     MOVE MV-TOTCYC-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.             
W34259     MOVE MV-TOTCYC-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.           
W34259     MOVE MV-TOTCYC-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.          
W34259     MOVE MV-TOTCYC-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.              
W33304                                                                          
W33304     IF MV-CYC-TOTAL-SALES = ZERO                                         
W34259         MOVE 999.99 TO CL-SHRINK-TY-PERCENT                              
W33304     ELSE                                                                 
W33304         COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
W33304             (MV-CYC-SHORT-RETAIL * 100 / MV-CYC-TOTAL-SALES)             
W33304                 ON SIZE ERROR                                            
W33304                     MOVE 999.99 TO PV-SHRINK-PERCENT                     
W33304     END-IF.                                                              
W33304                                                                          
W33304*    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
W33304     IF MV-CYC-SHORT-RETAIL > 0                                           
W33304         IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT                                  
W34259                          = (PV-SHRINK-PERCENT * (-1))                    
W33304         ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
W33304         END-IF                                                           
W33304     ELSE                                                                 
W33304*    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE            
W33304         IF MV-CYC-SHORT-RETAIL < 0                                       
W33304             IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
W33304                                      (PV-SHRINK-PERCENT * (-1))          
W33304             ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
W33304             END-IF                                                       
W33304         ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
W33304         END-IF                                                           
W33304     END-IF.                                                              
W33304                                                                          
W33304     MOVE 4 TO MV-NUMB-LINES.                                             
W33304     MOVE ST-CYCLE-TOTAL-LINE TO PRINT-REC.                               
W33304     MOVE SH-SECOND-HEADING-LINE1 TO SH-SECOND-HEADING-LINE.              
W33304     MOVE SH-CYCLE-MONTH-HEADER3  TO SH-CYCLE-MONTH-HEADER.               
W33304     PERFORM F000-PRINT-A-LINE.                                           
W33304     MOVE 2 TO MV-NUMB-LINES.                                             
W33304     MOVE SPACES TO CL-CLASS-NUMBER.                                      
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                                 
W33304     MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                              
W33304     PERFORM F000-PRINT-A-LINE.                                           
W33304                                                                          
W33304     PERFORM Z100-WRITE-STORE-BREAK.                                      
W34259     MOVE IN010-INV-ID TO MV-PREV-CYCLE-NBR.                              
W33304     MOVE ZERO TO MV-CYC-BOOK-INV                                         
W33304                 MV-CYC-SHRINK-ALLOW                                      
W33304                 MV-CYC-TOTAL-RETAIL                                      
W33304                 MV-CYC-PHY-INV-RETAIL                                    
W33304                 MV-CYC-SHORT-RETAIL                                      
W33304                 MV-CYC-TOTAL-SALES.                                      
W33304     MOVE 61 TO MV-LINE-COUNT.                                            
W33304                                                                          
W33304******************************************************************        
W33304*    THIS PARAGRAPH MOVES ALL THE MONTH INFO TO THE DETAIL LINE           
W33304*    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK               
W33304*    PERCENT AT THE COMPANY LEVEL.                                        
W33304******************************************************************        
W33304 D200-MONTH-BREAK.                                                        
W34259     COMPUTE MV-TOTMTH-BOOK-INV  ROUNDED                                  
W34259                           = MV-MTH-BOOK-INV * 1.                         
W34259     COMPUTE MV-TOTMTH-SHRINK-ALLOW ROUNDED                               
W34259                           = MV-MTH-SHRINK-ALLOW * 1.                     
W34259     COMPUTE MV-TOTMTH-TOTAL-RETAIL ROUNDED                               
W34259                           = MV-MTH-TOTAL-RETAIL * 1.                     
W34259     COMPUTE MV-TOTMTH-PHY-INV-RETAIL ROUNDED                             
W34259                           = MV-MTH-PHY-INV-RETAIL * 1.                   
W34259     COMPUTE MV-TOTMTH-TY-SHORT-RETAIL ROUNDED                            
W34259                           = MV-MTH-SHORT-RETAIL * 1.                     
W34259     COMPUTE MV-TOTMTH-TOTAL-SALES ROUNDED                                
W34259                           = MV-MTH-TOTAL-SALES * 1.                      
W34259     MOVE MV-TOTMTH-BOOK-INV         TO CL-DEPT-BOOK-INV.                 
W34259     MOVE MV-TOTMTH-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.             
W34259     MOVE MV-TOTMTH-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.             
W34259     MOVE MV-TOTMTH-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.           
W34259     MOVE MV-TOTMTH-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.          
W34259     MOVE MV-TOTMTH-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.              
W33304                                                                          
W33304     IF MV-MTH-TOTAL-SALES = ZERO                                         
W34259         MOVE 999.99 TO CL-SHRINK-TY-PERCENT                              
W33304     ELSE                                                                 
W33304         COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
W33304             (MV-MTH-SHORT-RETAIL * 100 / MV-MTH-TOTAL-SALES)             
W33304                 ON SIZE ERROR                                            
W33304                     MOVE 999.99 TO PV-SHRINK-PERCENT                     
W33304     END-IF.                                                              
W33304                                                                          
W33304*    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
W33304     IF MV-MTH-SHORT-RETAIL > 0                                           
W33304         IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT                                  
W34259                           = (PV-SHRINK-PERCENT * (-1))                   
W33304         ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
W33304         END-IF                                                           
W33304     ELSE                                                                 
W33304*    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE            
W33304         IF MV-MTH-SHORT-RETAIL < 0                                       
W33304             IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
W33304                                      (PV-SHRINK-PERCENT * (-1))          
W33304             ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
W33304             END-IF                                                       
W33304         ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
W33304         END-IF                                                           
W33304     END-IF.                                                              
W33304                                                                          
W33304     ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                            
W33304     MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
W33304     MOVE 6 TO MV-LINE-COUNT.                                             
W33304     WRITE PRINT-REC FROM                                                 
W33304        DP132O-STANDRD-HEADER-132-COLS                                    
W33304           AFTER ADVANCING PAGE.                                          
W33304     WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE1                         
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER1                          
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                           
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                          
W33304        AFTER ADVANCING 1 LINES.                                          
W33304     MOVE ST-MONTH-TOTAL-LINE TO PRINT-REC.                               
W33304     PERFORM F000-PRINT-A-LINE.                                           
W33304     MOVE 2 TO MV-NUMB-LINES.                                             
W33304     MOVE SPACES TO CL-CLASS-NUMBER.                                      
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                                 
W33304     MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                              
W33304     PERFORM F000-PRINT-A-LINE.                                           
W33304                                                                          
W34259     MOVE IN010-OPN-FSCL-MN-DTE TO MV-PREV-FSCL-MN-DATE.                  
W33304     MOVE ZERO TO MV-MTH-BOOK-INV                                         
W33304                 MV-MTH-SHRINK-ALLOW                                      
W33304                 MV-MTH-TOTAL-RETAIL                                      
W33304                 MV-MTH-PHY-INV-RETAIL                                    
W33304                 MV-MTH-SHORT-RETAIL                                      
W33304                 MV-MTH-TOTAL-SALES.                                      
W33304     MOVE 61 TO MV-LINE-COUNT.                                            
W33304                                                                          
      ******************************************************************        
      *    THIS PARAGRAPH MOVES ALL THE COMPANY INFO TO THE DETAIL LINE         
      *    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK               
      *    PERCENT AT THE COMPANY LEVEL.                                        
      ******************************************************************        
       E000-FINAL-TOTALS.                                                       
W34259     COMPUTE MV-TOTRND-BOOK-INV  ROUNDED                                  
W34259                           = MV-TOT-BOOK-INV * 1.                         
W34259     COMPUTE MV-TOTRND-SHRINK-ALLOW ROUNDED                               
W34259                           = MV-TOT-SHRINK-ALLOW * 1.                     
W34259     COMPUTE MV-TOTRND-TOTAL-RETAIL ROUNDED                               
W34259                           = MV-TOT-TOTAL-RETAIL * 1.                     
W34259     COMPUTE MV-TOTRND-PHY-INV-RETAIL ROUNDED                             
W34259                           = MV-TOT-PHY-INV-RETAIL * 1.                   
W34259     COMPUTE MV-TOTRND-TY-SHORT-RETAIL ROUNDED                            
W34259                           = MV-TOT-SHORT-RETAIL * 1.                     
W34259     COMPUTE MV-TOTRND-TOTAL-SALES ROUNDED                                
W34259                           = MV-TOT-TOTAL-SALES * 1.                      
W34259     MOVE MV-TOTRND-BOOK-INV         TO CL-DEPT-BOOK-INV.                 
W34259     MOVE MV-TOTRND-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.             
W34259     MOVE MV-TOTRND-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.             
W34259     MOVE MV-TOTRND-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.           
W34259     MOVE MV-TOTRND-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.          
W34259     MOVE MV-TOTRND-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.              
PCT02                                                                           
           IF MV-TOT-TOTAL-SALES = ZERO                                         
W34259         MOVE 999.99 TO CL-SHRINK-TY-PERCENT                              
           ELSE                                                                 
PCT02          COMPUTE PV-SHRINK-PERCENT ROUNDED =                              
                   (MV-TOT-SHORT-RETAIL * 100 / MV-TOT-TOTAL-SALES)             
                       ON SIZE ERROR                                            
PCT02A                     MOVE 999.99 TO PV-SHRINK-PERCENT.                    
PCT02                                                                           
PCT02 *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE            
PCT02      IF MV-TOT-SHORT-RETAIL > 0                                           
PCT02          IF PV-SHRINK-PERCENT < 0                                         
W34259            COMPUTE CL-SHRINK-TY-PERCENT                                  
W34259                          = (PV-SHRINK-PERCENT * (-1))                    
PCT02          ELSE                                                             
W34259            MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT                
PCT02          END-IF                                                           
PCT02      ELSE                                                                 
PCT02 *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE            
PCT02          IF MV-TOT-SHORT-RETAIL < 0                                       
PCT02              IF PV-SHRINK-PERCENT > 0                                     
W34259                 COMPUTE CL-SHRINK-TY-PERCENT =                           
PCT02                                       (PV-SHRINK-PERCENT * (-1))          
PCT02              ELSE                                                         
W34259                MOVE PV-SHRINK-PERCENT TO CL-SHRINK-TY-PERCENT            
PCT02              END-IF                                                       
PCT02          ELSE                                                             
W34259             MOVE ZERO TO CL-SHRINK-TY-PERCENT                            
PCT02          END-IF                                                           
PCT02      END-IF.                                                              
PCT02                                                                           
W33304     ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                            
W33304     MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
W33304     MOVE 6 TO MV-LINE-COUNT.                                             
W33304     WRITE PRINT-REC FROM                                                 
W33304        DP132O-STANDRD-HEADER-132-COLS                                    
W33304           AFTER ADVANCING PAGE.                                          
W33304     MOVE 99999 TO SH-CYCLE-NUM.                                          
W33304     MOVE 'ALL INVENTORY CYCLES' TO SH-CYCLE-NAM.                         
W33304     WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE1                         
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER3                          
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                           
W33304        AFTER ADVANCING 2 LINES.                                          
W33304     WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                          
W33304        AFTER ADVANCING 1 LINES.                                          
           MOVE TR-TOTAL-RETAIL-LINE TO PRINT-REC.                              
           PERFORM F000-PRINT-A-LINE.                                           
           MOVE 2 TO MV-NUMB-LINES.                                             
           MOVE SPACES TO CL-CLASS-NUMBER.                                      
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                                 
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                              
           PERFORM F000-PRINT-A-LINE.                                           
                                                                                
       F000-PRINT-A-LINE.                                                       
           IF MV-LINE-COUNT > 59                                                
              MOVE PRINT-REC TO MV-PRINT-REC-SAVE                               
              PERFORM G000-PRINT-HEADINGS                                       
              MOVE MV-PRINT-REC-SAVE TO PRINT-REC                               
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                                
              WRITE PRINT-REC                                                   
                 AFTER ADVANCING MV-NUMB-LINES                                  
           ELSE                                                                 
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                                
              WRITE PRINT-REC                                                   
                 AFTER ADVANCING MV-NUMB-LINES.                                 
                                                                                
       F100-PRINT-HEADING5.                                                     
           IF MV-LINE-COUNT > 56                                                
              MOVE PRINT-REC TO MV-PRINT-REC-SAVE                               
              PERFORM G000-PRINT-HEADINGS                                       
              MOVE MV-PRINT-REC-SAVE TO PRINT-REC                               
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                                
              WRITE PRINT-REC                                                   
                 AFTER ADVANCING MV-NUMB-LINES                                  
           ELSE                                                                 
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                                
              WRITE PRINT-REC                                                   
                 AFTER ADVANCING MV-NUMB-LINES.                                 
                                                                                
       G000-PRINT-HEADINGS.                                                     
           ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                            
           MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           MOVE 6 TO MV-LINE-COUNT.                                             
           WRITE PRINT-REC FROM                                                 
              DP132O-STANDRD-HEADER-132-COLS                                    
                 AFTER ADVANCING PAGE.                                          
           WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE                          
              AFTER ADVANCING 2 LINES.                                          
           WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER                           
              AFTER ADVANCING 2 LINES.                                          
           WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                           
              AFTER ADVANCING 2 LINES.                                          
           WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                          
              AFTER ADVANCING 1 LINES.                                          
*                                                                               
ADJ00 ******************************************************************        
ADJ00 *   H100-SELECT-TINVPAR.                                         *        
ADJ00 *      OBTAIN THE SCHEDULED PHYSICAL COUNT & SCAN INV INDICATORS.*        
ADJ00 *      THIS WILL BE USED TO DETERMINE WHICH STORE LOC TO PRINT.  *        
ADJ00 ******************************************************************        
ADJ00  H100-SELECT-TINVPAR.                                                     
ADJ00                                                                           
ADJ00      EXEC SQL                                                             
W33304         SELECT SCHD_PHY_CNT_CDE                                          
ADJ00                ,SCAN_INV_IND                                              
W33304         INTO  :INVPAR-SCHD-PHY-CNT-CDE                                   
ADJ00               ,:INVPAR-SCAN-INV-IND                                       
28545          FROM   TINVPAR A                                                 
28545                ,TINCNTL B                                                 
W26600         WHERE LOC_NBR = :INVPAR-LOC-NBR                                  
W33304           AND A.LOC_INV_STAT_CDE = 'IN'                                  
W33304           AND A.ACTL_FIN_BK_DTE = '9999-09-09'                           
ADJ00            AND (ACTL_INV_DTE >= :DTATTR-FSCL-MN-BEG-DTE   AND             
ADJ00                 ACTL_INV_DTE <= :DTATTR-FSCL-MN-END-DTE)                  
28545            AND A.INV_ID = B.INV_ID                                        
28545            AND B.ACTV_IND = 'Y'                                           
ADJ00      END-EXEC.                                                            
ADJ00                                                                           
ADJ00      EVALUATE TRUE                                                        
ADJ00          WHEN SQLCODE = ZERO                                              
972188             SET PHYS-CNT-LOC TO TRUE                                     
972188         WHEN SQLCODE = +100                                              
972188             SET NOT-PHYS-CNT-LOC TO TRUE                                 
ADJ00          WHEN OTHER                                                       
                   DISPLAY INVPAR-LOC-NBR                                       
ADJ00              MOVE 'H100-SELECT-TINVPAR'                                   
ADJ00                                  TO AA-PARAGRAPH-NAME                     
ADJ00              MOVE 'TINVPAR ROW NOT FOUND'                                 
ADJ00                                  TO AA-MESSAGE                            
ADJ00              MOVE 'SELECT'       TO AA-DB2-OPERATION                      
ADJ00              PERFORM Z999-SQL-ERROR                                       
ADJ00      END-EVALUATE.                                                        
ADJ00                                                                           
ADJ00  EJECT                                                                    
ADJ00 *                                                                         
ADJ00 ******************************************************************        
ADJ00 *   H150-GET-PHYS-CNT-STORE. - DETERMINES WHICH LOC NOT TO PRINT.*        
ADJ00 *    CLOSED STR = SCHD-PHY-CNT-IND = 'C'                         *        
ADJ00 ******************************************************************        
ADJ00  H150-GET-PHYS-CNT-STORE.                                                 
ADJ00 *                                                                         
ADJ00      PERFORM H100-SELECT-TINVPAR.                                         
ADJ00 *                                                                         
W33304     IF ( INVPAR-SCHD-PHY-CNT-CDE = 'CL' )                                
972188         OR ( PS-PHYS-CNT-LOC-SW = 'N' )                                  
ADJ00          SET PS-BYPASS-STORE TO TRUE                                      
ADJ00          PERFORM H200-BYPASS-CLOSED-STORE                                 
W34259            UNTIL IN010-STORE-NUMB NOT EQUAL PV-STORE-NBR                 
ADJ00                OR NO-MORE-RECORDS                                         
W34259            MOVE IN010-STORE-NUMB TO PV-STORE-NBR                         
W27710            MOVE PV-STORE-NBR      TO INVPAR-LOC-NBR                      
ADJ00      ELSE                                                                 
ADJ00          SET PS-PROCESS-STORE TO TRUE                                     
ADJ00      END-IF.                                                              
ADJ00 *                                                                         
ADJ00 *                                                                         
ADJ00 ******************************************************************        
ADJ00 *   H200-BYPASS-CLOSED-STORE.                                    *        
ADJ00 *      OBTAIN THE SCHEDULED PHYSICAL COUNT INDICATOR FOR THE     *        
ADJ00 *      STORE ROW WITHIN THE CURRENTL COUNT INDICATOR             *        
ADJ00 ******************************************************************        
ADJ00  H200-BYPASS-CLOSED-STORE.                                                
      *                                                                         
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC                  
               AT END                                                           
                   MOVE 'NO' TO WS-RECORD-INDICATOR.                            
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * INSERT A DIVIDER PAGE BETWEEN STORE BREAKS                              
      ******************************************************************        
       Z100-WRITE-STORE-BREAK.                                                  
RB1202*    DETERMINE IF LAST PAGE NUMBER WAS ODD                                
RB1202     DIVIDE MV-PAGE-COUNT                                                 
RB1202         BY PC-TWO                                                        
RB1202            GIVING PV-HALF-PAGES                                          
RB1202                REMAINDER PV-REMAINDER-PAGES.                             
RB1202     IF PV-REMAINDER-PAGES NOT = ZERO                                     
RB1202         PERFORM G000-PRINT-HEADINGS                                      
RB1202*        MOVE SPACES TO PRINT-REC.                                        
RB1202         MOVE BP-BLANK-PAGE-DETAIL TO PRINT-REC                           
               WRITE PRINT-REC                                                  
RB1202*            AFTER ADVANCING PAGE.                                        
RB1202             AFTER ADVANCING 2 LINES                                      
RB1202     END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *    READ THE CONTROL RECORD AND OBTAIN THE MONTH NAME AND                
      *    NUMBER FOR PROCESSING.                                               
      ******************************************************************        
       Z200-CONTROL-RECORD-PROCESS.                                             
                                                                                
           ACCEPT SD-SYSTEM-DATE   FROM  DATE.                                  
           MOVE SD-MM                TO  DP132O-RUN-MONTH.                      
           MOVE SD-DD                TO  DP132O-RUN-DAY.                        
           MOVE SD-YY                TO  DP132O-RUN-YEAR.                       
      *                                                                         
      *    GET THE MONTH NAME OF WHEN THE INVENTORY WAS TAKEN                   
      *                                                                         
                                                                                
W33304 Z300-SELECT-TDTATTR.                                                     
0200RM******************************************************************        
W33304*   Z300-SELECT-TDTATTR.                                         *        
0200RM*      OBTAIN THE OPEN FISCAL MONTH DATE                         *        
0200RM******************************************************************        
0200RM                                                                          
0200RM     EXEC SQL                                                             
W33304         SELECT FSCL_MN_BEG_DTE,                                          
ADJ00                 FSCL_MN_END_DTE,                                          
0200RM                FSCL_MN_NM                                                
W33304         INTO  :DTATTR-FSCL-MN-BEG-DTE                                    
ADJ00             ,  :DTATTR-FSCL-MN-END-DTE                                    
0200RM            ,  :DTATTR-FSCL-MN-NM                                         
W33304         FROM   TDTATTR                                                   
W34259         WHERE KY_DTE = :IN010-OPN-FSCL-MN-DTE                            
0200RM     END-EXEC.                                                            
0200RM                                                                          
0200RM     EVALUATE TRUE                                                        
0200RM         WHEN SQLCODE = ZERO                                              
0200RM             MOVE DTATTR-FSCL-MN-NM TO SH-REPORT-MONTH                    
W33304                                       SH-REPORT-MON                      
0200RM         WHEN OTHER                                                       
W33304             MOVE 'Z300-SELECT-TDTATTR'                                   
0200RM                                 TO AA-PARAGRAPH-NAME                     
W33304             MOVE 'TDTATTR ROW NOT FOUND'                                 
0200RM                                 TO AA-MESSAGE                            
0200RM             MOVE 'SELECT'       TO AA-DB2-OPERATION                      
0200RM             PERFORM Z999-SQL-ERROR                                       
0200RM     END-EVALUATE.                                                        
0200RM                                                                          
0200RM EJECT                                                                    
ADJ00 *                                                                         
W33304******************************************************************        
W33304* Z310-SELECT-CYCLE-NAME - FIND CYCLE NAME FOR THE INV ID       *         
W33304******************************************************************        
W33304                                                                          
W33304 Z310-SELECT-CYCLE-NAME.                                                  
W34259     MOVE IN010-INV-ID  TO SH-CYCLE-NO                                    
W33304                           SH-CYCLE-NUM                                   
W33304                           ST-CYCLE-NUMBER.                               
W33304     INITIALIZE INCNTL-INV-NM-TEXT.                                       
W33304                                                                          
W33304     EXEC SQL                                                             
W33304          SELECT  INV_NM                                                  
W33304          INTO   :INCNTL-INV-NM                                           
W33304          FROM    TINCNTL                                                 
W34259          WHERE INV_ID = :IN010-INV-ID                                    
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE TRUE                                                        
W33304         WHEN SQLCODE        = +000                                       
W33304             MOVE INCNTL-INV-NM-TEXT TO SH-CYCLE-NAME                     
W33304                                        SH-CYCLE-NAM                      
W33304         WHEN OTHER                                                       
W33304             DISPLAY 'INV-ID:  ' SH-CYCLE-NO                              
W33304             MOVE    'Z310-SELECT-CYCLE-NAME'                             
W33304                                      TO AA-PARAGRAPH-NAME                
W33304             MOVE 'TINCNTL ROW NOT FOUND'                                 
W33304                                 TO AA-MESSAGE                            
W33304             MOVE 'SELECT'       TO AA-DB2-OPERATION                      
W33304             PERFORM Z999-SQL-ERROR                                       
W33304     END-EVALUATE.                                                        
W33304*                                                                         
ADJ00 ******************************************************************        
ADJ00 *    READ THE CONTROL FILE TO DETERMINE WHICH JOB EXECUTED                
ADJ00 *    THIS PROGRAM.                                                        
ADJ00 ******************************************************************        
ADJ00  Z400-READ-CONTROL-FILE.                                                  
ADJ00                                                                           
ADJ00      READ CONTROL-FILE INTO CONTROL-CARD.                                 
DATECC*                                                                         
DATECC******************************************************************        
DATECC*    READ THE DATE CONTROL FILE TO DETERMINE WHICH JOB EXECUTED           
DATECC*    THIS PROGRAM.                                                        
DATECC******************************************************************        
DATECC Z500-READ-DATE-CONTROL.                                                  
DATECC                                                                          
DATECC     READ DATE-CONTROL INTO DATE-CONTROL-CC.                              
972188     IF CC-CONTROL-DATE = SPACES                                          
972188         DISPLAY 'NO OVERRIDE DATE PROCESS'                               
972188     ELSE                                                                 
972188         DISPLAY 'PROCESS OVERRIDE DATE: ' CC-CONTROL-DATE                
972188         EVALUATE  CC-DATE-TYPE-CODE                                      
972188             WHEN 'M'                                                     
972188             WHEN ' '                                                     
972188                 DISPLAY 'PROCESS METHOD: FISCAL MONTH'                   
972188             WHEN 'W'                                                     
972188                 DISPLAY 'PROCESS METHOD: FISCAL WEEK'                    
972188             WHEN 'D'                                                     
972188                 DISPLAY 'PROCESS METHOD: FISCAL MONTH TO DATE'           
972188             WHEN 'O'                                                     
972188                 DISPLAY 'PROCESS METHOD: FISCAL WEEK TO DATE'            
972188         END-EVALUATE                                                     
972188     END-IF.                                                              
DATECC*                                                                         
DATECC                                                                          
DATECC Z600-DATE-OVERRIDE.                                                      
DATECC******************************************************************        
DATECC*   Z600-DATE-OVERRIDE.                                          *        
DATECC*      OBTAIN THE FISCAL MONTH DATE ASSOCIATED WITH CONTROL CARD *        
DATECC******************************************************************        
972188     EVALUATE CC-DATE-TYPE-CODE                                           
972188         WHEN 'M'                                                         
972188         WHEN ' '                                                         
972188             EXEC SQL                                                     
972188                 SELECT FSCL_MN_BEG_DTE,                                  
972188                        FSCL_MN_END_DTE,                                  
972188                        FSCL_MN_NM                                        
972188                  INTO  :DTATTR-FSCL-MN-BEG-DTE                           
972188                     ,  :DTATTR-FSCL-MN-END-DTE                           
972188                     ,  :DTATTR-FSCL-MN-NM                                
972188                   FROM TDTATTR                                           
972188                  WHERE KY_DTE = :CC-CONTROL-DATE                         
972188             END-EXEC                                                     
972188         WHEN 'W'                                                         
972188             EXEC SQL                                                     
972188                 SELECT FSCL_WK_BEG_DTE,                                  
972188                        FSCL_WK_END_DTE,                                  
972188                        FSCL_MN_NM                                        
972188                  INTO  :DTATTR-FSCL-MN-BEG-DTE                           
972188                     ,  :DTATTR-FSCL-MN-END-DTE                           
972188                     ,  :DTATTR-FSCL-MN-NM                                
972188                   FROM TDTATTR                                           
972188                  WHERE KY_DTE = :CC-CONTROL-DATE                         
972188             END-EXEC                                                     
972188         WHEN 'D'                                                         
972188             EXEC SQL                                                     
972188                 SELECT FSCL_MN_BEG_DTE,                                  
972188                        FSCL_MN_NM                                        
972188                  INTO  :DTATTR-FSCL-MN-BEG-DTE                           
972188                     ,  :DTATTR-FSCL-MN-NM                                
972188                   FROM TDTATTR                                           
972188                  WHERE KY_DTE = :CC-CONTROL-DATE                         
972188             END-EXEC                                                     
972188             MOVE CC-CONTROL-DATE TO DTATTR-FSCL-MN-END-DTE               
972188         WHEN 'O'                                                         
972188             EXEC SQL                                                     
972188                 SELECT FSCL_WK_BEG_DTE,                                  
972188                        FSCL_MN_NM                                        
972188                  INTO  :DTATTR-FSCL-MN-BEG-DTE                           
972188                     ,  :DTATTR-FSCL-MN-NM                                
972188                   FROM TDTATTR                                           
972188                  WHERE KY_DTE = :CC-CONTROL-DATE                         
972188             END-EXEC                                                     
972188             MOVE CC-CONTROL-DATE TO DTATTR-FSCL-MN-END-DTE               
972188     END-EVALUATE.                                                        
972188                                                                          
DATECC     EVALUATE TRUE                                                        
DATECC         WHEN SQLCODE = ZERO                                              
DATECC             MOVE DTATTR-FSCL-MN-NM TO SH-REPORT-MONTH                    
DATECC         WHEN OTHER                                                       
DATECC             MOVE 'Z600-DATE-OVERRIDE'                                    
DATECC                                 TO AA-PARAGRAPH-NAME                     
DATECC             MOVE 'TDTATTR ROW NOT FOUND'                                 
DATECC                                 TO AA-MESSAGE                            
DATECC             MOVE 'SELECT'       TO AA-DB2-OPERATION                      
DATECC             PERFORM Z999-SQL-ERROR                                       
DATECC     END-EVALUATE.                                                        
DATECC                                                                          
0200RM Z999-SQL-ERROR.                                                          
0200RM******************************************************************        
0200RM*   Z999-SQL-ERROR.                                              *        
0200RM*      SQL ERROR ABEND ROUTINE.                                  *        
0200RM******************************************************************        
0200RM                                                                          
0200RM     DISPLAY AA-ABEND-LIT.                                                
0200RM     DISPLAY AA-DB2-ERROR-LIT.                                            
0200RM     DISPLAY AA-PROGRAM-LIT.                                              
0200RM     DISPLAY AA-PARAGRAPH-LIT.                                            
0200RM     DISPLAY AA-MESSAGE-LINE.                                             
0200RM     DISPLAY AA-DB2-OPERATION-LIT.                                        
0200RM     MOVE +4013                  TO ABEND-CODE.                           
0200RM                                                                          
0200RM     COPY DPPD004.                                                        
0200RM     CALL 'ILBOABN0' USING ABEND-CODE.                                    
