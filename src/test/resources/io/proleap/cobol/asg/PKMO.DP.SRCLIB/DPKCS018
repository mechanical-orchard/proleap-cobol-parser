      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    DPKCS018.                                                 
       AUTHOR.        JEFF ROUBIK.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11-10-98.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    Z018 - MENU ITEM LIST PROGRAM.                             *         
      *                                                               *         
      *  THIS TRANSACTION CONTROLS THE DEVELOPMENT OF MENUS WITHIN     *        
      *  CICS. ITS PURPOSE IS TO DISPLAY ALL MNEMONICS CURRENTLY ASSO- *        
      *  CIATED WITH A SELECTED MENU. ENTRY TO THIS TRANSACTION IS     *        
      *  EITHER THROUGH SELECTING AN ELIGIBLE MNEMONIC ON THE MNEMONIC *        
      *  LIST (Z018) TRANSACTION AND PRESSING THE FUNCTION KEY ASSO-   *        
      *  CIATED WITH THIS TRANSACTION OR THROUGH THE SELECTION OF THIS *        
      *  TRANSACTION FROM A MENU, OR THROUGH THE ENTRY OF THE MNEMONIC *        
      *  ASSOCIATED WITH THIS TRANSACTION ON THE COMMAND LINE OF       *        
      *  ANOTHER SCREEN. AN ELIGIBLE MNEMONIC IS DEFINED AS A MNEMONIC *        
      *  ASSOCIATED WITH THE MENU TRANSACTION (Z007).                  *        
      *                                                                *        
      *  UPON INITIAL ENTRY, THIS TRANSACTION WILL BUILD A LIST OF ALL *        
      *  ROWS OF THE MENU ITEM TABLE. THE USER THEN EITHER:            *        
      *                                                                *        
      *          A. SELECTS ONE AND ONLY ONE ITEM AND PRESSES A        *        
      *             FUNCTION KEY TO PERFORM MAINTENANCE ON IT,  OR     *        
      *          B. PRESSES A FUNCTION KEY (PF13) WITHOUT MAKING A     *        
      *             SELECTION TO ADD A TRANSACTION,  OR                *        
      *          C. PRESSES A FUNCTION KEY TO MANIPULATE THE CURRENT   *        
      *             VIEW OF THE LIST (BROWSE, REFRESH, ETC.),  OR      *        
      *          D. PRESSES A FUNCTION KEY TO END THE TRANSACTION      *        
      *             (PF3).                                             *        
      *                                                                *        
      *  WHEN RETURNING TO THIS TRANSACTION (VIA THE RETURN (PF3)      *        
      *  CAPABILITY) FROM ONE OF THE DETAIL PROCESSING TRANSACTIONS,   *        
      *  THE LIST WILL BE UPDATED WITH THE PROCESSING RESULTS.         *        
      *                                                                *        
      *  DURING THE LIFE OF THE LIST THAT WAS CREATED ON INITIAL ENTRY *        
      *  TO THIS TRANSACTION, ALL SELECTIONS AND SELECTION RESULTS ARE *        
      *  POSTED TO THE LIST. ONCE SELECTED, AN ITEM REMAINS SELECTED   *        
      *  AND THIS FACT IS RECORDED IN THE LIST. ONCE SELECTED, AN ITEM *        
      *  REMAINS IN THE INTER APPLICATIONS COMMAREA (DP031) UNTIL      *        
      *  ANOTHER ITEM IS SELECTED.                                     *        
      *                                                                *        
      *  PF KEY PROCESSING:                                            *        
      *                                                                *        
      *         ENTER - EDIT SCREEN                                    *        
      *         PA2   - TOGGLE FUNCTION KEY LINES                      *        
      *         PF5   - REFRESH LIST FROM DATABASE                     *        
      *         PF6   - DISPLAY FIRST SCREEN OF LIST                   *        
      *         PF7   - DISPLAY PREVIOUS SCREEN OF LIST                *        
      *         PF8   - DISPLAY NEXT SCREEN OF LIST                    *        
      *         PF9   - DESELECT                                       *        
      *         CLEAR - RE-DISPLAY CURRENT SCREEN OF LIST              *        
      *                                                                *        
      *                                                                *        
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023       *        
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE   *        
262023*  MAKING THE CALL DYNAMIC VS STATIC                             *        
      *----------------------------------------------------------------*        
       EJECT                                                                    
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           15  DK-MENU                        PIC  X(08)  VALUE SPACE.          
           15  DK-SEQ                         PIC S9(04)  COMP-3                
                                                          VALUE ZERO.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
                                                          'DP018A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
                                                          'DPKM018 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
                                                          'DPKCS018'.           
           05  PC-TRAN-Z007                   PIC  X(04)  VALUE 'Z007'.         
           05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
           05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
           05  PC-INT-APPL-FORMAT             PIC  X(01)  VALUE '1'.            
           05  PC-TSQ-B-FORMAT                PIC  X(01)  VALUE '2'.            
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +15             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MAX-GROUPS                  PIC S9(09)  VALUE +1              
                                                          COMP SYNC.            
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-MAX-ITEMS                   PIC S9(09)  VALUE +99999          
                                                          COMP-3.               
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3               
                                                          COMP-3.               
           05  PC-INQUIRED             PIC X(14)  VALUE 'INQUIRED     '.        
           05  PC-CREATED              PIC X(14)  VALUE 'CREATED      '.        
           05  PC-UPDATED              PIC X(14)  VALUE 'UPDATED      '.        
           05  PC-DELETED              PIC X(14)  VALUE 'DELETED      '.        
           05  PC-SELECTED             PIC X(14)  VALUE 'SELECTED     '.        
           05  PC-ERROR-OCCURED        PIC X(14)  VALUE 'ERROR OCCURED'.        
           05  PC-NOT-FOUND            PIC X(14)  VALUE 'NOT FOUND    '.        
           05  PC-UNKNOWN-ERROR        PIC X(14)  VALUE 'UNKNOWN ERROR'.        
      *                                                                         
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
               10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
               10  PC-TSYMSG-00045            PIC  9(05)  VALUE 00045.          
               10  PC-TSYMSG-00047            PIC  9(05)  VALUE 00047.          
               10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
               10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
               10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
               10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
               10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
               10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
               10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
               10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
               10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
               10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
               10  PC-TSYMSG-00266            PIC  9(05)  VALUE 00266.          
               10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
               10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
               88  PS-NO-ERROR                            VALUE 'Y'.            
               88  PS-ERROR                               VALUE 'N'.            
           05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
               88  PS-COMMAREA-VALID                      VALUE 'Y'.            
               88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB                         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-TEMP-NUM                    PIC S9(09)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-GROUPS-TO-READ              PIC S9(09)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-MAX-ITEMS                   PIC S9(09)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-DISP-RESP                   PIC  9(04)  VALUE  0.             
           05  PV-ITEM                        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-DESC-NULL-IND               PIC S9(4)   VALUE ZEROS           
                                                          COMP.                 
      ******************************************************************        
      **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **        
      **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **        
      **  APPLICATION-SPECIFIC COMMAREA.                              **        
      ******************************************************************        
       01  LQW-LIST-QUEUE-WORK-AREA.                                            
           05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.          
           05  LQW-ITEM-ENTRIES               OCCURS 15 TIMES                   
                                              INDEXED BY LQW-IDX.               
               10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
               10  LQW-TEMP-SELECTED-SW       PIC  X(01).                       
               10  LQW-SELECTED-SW            PIC  X(01).                       
                   88  LQW-SELECT                         VALUE 'S'.            
                   88  LQW-DESELECT                       VALUE ' '.            
               10  LQW-MENU-NAME              PIC  X(08).                       
               10  LQW-MNEMONIC               PIC  X(08).                       
               10  LQW-SEQ                    PIC  X(04).                       
               10  LQW-FMT                    PIC  X(01).                       
               10  LQW-TITLE                  PIC  X(30).                       
               10  LQW-PROGRAM                PIC  X(08).                       
               10  LQW-RESULT                 PIC  X(14).                       
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *  RECORD LAYOUT FOR THE TSQ "B" RECORD.                        *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS002.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPKM018.                                                        
      *                                                                         
       01  MR-OCCURS-LAYOUT                   REDEFINES  DP018AO.               
           05  FILLER                         PIC X(90).                        
           05  MR-SCROLL-AREA                 OCCURS 15 TIMES.                  
               10  MR-SELECTL                 PIC S9(04) COMP.                  
               10  MR-SELECTA                 PIC  X(01).                       
               10  MR-SELECTC                 PIC  X(01).                       
               10  MR-SELECTH                 PIC  X(01).                       
               10  MR-SELECTI                 PIC  X(01).                       
                                                                                
               10  MR-MNEMONICL               PIC S9(04) COMP.                  
               10  MR-MNEMONICA               PIC  X(01).                       
               10  MR-MNEMONICC               PIC  X(01).                       
               10  MR-MNEMONICH               PIC  X(01).                       
               10  MR-MNEMONIC                PIC  X(08).                       
                                                                                
               10  MR-SEQL                    PIC S9(04) COMP.                  
               10  MR-SEQA                    PIC  X(01).                       
               10  MR-SEQC                    PIC  X(01).                       
               10  MR-SEQH                    PIC  X(01).                       
               10  MR-SEQ                     PIC  X(04).                       
                                                                                
               10  MR-FMTL                    PIC S9(04) COMP.                  
               10  MR-FMTA                    PIC  X(01).                       
               10  MR-FMTC                    PIC  X(01).                       
               10  MR-FMTH                    PIC  X(01).                       
               10  MR-FMT                     PIC  X(01).                       
                                                                                
               10  MR-TITLEL                  PIC S9(04) COMP.                  
               10  MR-TITLEA                  PIC  X(01).                       
               10  MR-TITLEC                  PIC  X(01).                       
               10  MR-TITLEH                  PIC  X(01).                       
               10  MR-TITLE                   PIC  X(30).                       
                                                                                
               10  MR-PROGRAML                PIC S9(04) COMP.                  
               10  MR-PROGRAMA                PIC  X(01).                       
               10  MR-PROGRAMC                PIC  X(01).                       
               10  MR-PROGRAMH                PIC  X(01).                       
               10  MR-PROGRAM                 PIC  X(08).                       
                                                                                
               10  MR-RESULTL                 PIC S9(04) COMP.                  
               10  MR-RESULTA                 PIC  X(01).                       
               10  MR-RESULTC                 PIC  X(01).                       
               10  MR-RESULTH                 PIC  X(01).                       
               10  MR-RESULT                  PIC  X(14).                       
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      * INTER-APPLICATION COMMAREA.                                   *         
      *   DPWS054 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    *         
      *             SPEC SCREEN.                                      *         
      *   DPWS056 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
      *             THIS LIST PROGRAMMING FOR A LOCAL-GO FUNCTION.    *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY DPWS055.                                                        
      *---------------------------------------------------------------*         
      *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
      *---------------------------------------------------------------*         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-LIST-KEYS.                                           
                       20  ASC-KEY-MENU                    PIC  X(08).          
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
                       20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).          
                           88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.           
                           88  ASC-BLOCK-MODIFIED          VALUE 'Y'.           
                                                                                
                       20  ASC-ITEM-ARRAY.                                      
                           25  ASC-ITEM-ENTRIES OCCURS 15 TIMES.                
                               30  ASC-LIST-ITEM-NUMBER                         
                                                           PIC S9(09)           
                                                           COMP-3.              
                               30  ASC-TEMP-SELECTED-SW    PIC  X(01).          
                                   88  ASC-VALID-SELECT    VALUE ' '            
                                                                 'S'            
                                                                 'B'            
                                                                 'E'.           
                                   88  ASC-TEMP-NO-SELECT  VALUE ' '.           
                                   88  ASC-TEMP-SELECT     VALUE 'S'.           
                                   88  ASC-TEMP-BEGIN      VALUE 'B'.           
                                   88  ASC-TEMP-END        VALUE 'E'.           
                               30  ASC-SELECTED-SW         PIC  X(01).          
                                   88  ASC-NO-SELECT       VALUE ' '.           
                                   88  ASC-SELECT          VALUE 'S'.           
                                   88  ASC-BEGIN           VALUE 'B'.           
                                   88  ASC-END             VALUE 'E'.           
                               30  ASC-MENU-NAME           PIC  X(08).          
                               30  ASC-MNEMONIC            PIC  X(08).          
                               30  ASC-SEQ                 PIC  X(04).          
                               30  ASC-FMT                 PIC  X(01).          
                               30  ASC-TITLE               PIC  X(30).          
                               30  ASC-PROGRAM             PIC  X(08).          
                               30  ASC-RESULT              PIC  X(14).          
                                                                                
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-LAST-SEQ-READ                   PIC S9(05)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                                COMP            
                                                                SYNC.           
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
                                                                COMP-3.         
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.           
                       88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.           
                   15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).          
                       88  ASC-UPDATE-TSQ                  VALUE 'Y'.           
                       88  ASC-NO-TSQ-UPDATE               VALUE ' '.           
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-LIST-SEQ                    PIC  9(01).          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                                                                                
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
                   15  ASC-USE-TSQB-SW                     PIC X.               
                       88  ASC-USE-TSQB                    VALUE 'Y'.           
                       88  ASC-NO-TSQB                     VALUE 'N'.           
                   15  ASC-SCREEN-TITLE                    PIC  X(30).          
                   15  ASC-MN-ITEM                         PIC S9(4)            
                                                                COMP.           
                   15  ASC-MN-SUB                          PIC S9(4)            
                                                                COMP.           
                   15  ASC-NUMBER-PROCESSED-ITEMS          PIC S9(9)            
                                                                COMP-3.         
                   15  ASC-RETURN-MODE-SW                  PIC X.               
                       88  ASC-RETURN-1                    VALUE '1'.           
                       88  ASC-RETURN-2                    VALUE '2'.           
                   15  FILLER                              PIC X(348).          
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------*                                            
      * DCLGEN FOR TABLE TMNEMONIC *                                            
      *----------------------------*                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------*                                            
      * DCLGEN FOR TABLE TMENUITEM *                                            
      *----------------------------*                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNUITM                                                  
           END-EXEC.                                                            
                                                                                
      *------------------------------------*                                    
      * DECLARE CURSOR FOR TABLE TMENUITEM *                                    
      *------------------------------------*                                    
                                                                                
           EXEC SQL                                                             
               DECLARE MENU-CURSOR CURSOR FOR                                   
                   SELECT  MENU_NAME,                                           
                           ITEM_MNEMONIC_CODE,                                  
                           DESC,                                                
                           SEQUENCE_NMBR,                                       
                           IN_APLCTN_COMM_CDE                                   
                     FROM  TMENUITEM                                            
                    WHERE  MENU_NAME     = :DK-MENU                             
                      AND  SEQUENCE_NMBR > :DK-SEQ                              
                 ORDER BY  MENU_NAME,                                           
                           SEQUENCE_NMBR                                        
           END-EXEC.                                                            
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **        
      **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **        
      **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF DP018AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
      *                                                                         
           PERFORM 1000-CONTROL-PROCESSING                                      
      *                                                                         
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **        
      **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **        
      ******************************************************************        
       0001-CALL-CICS-ARCH-API.                                                 
           CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                    DFHCOMMAREA                                 
                                    DP030-CICS-API-FIELDS                       
                                    DP020-STANDARD-COMMAREA                     
                                    DP018AI.                                    
      *                                                                         
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE '0001-CALL-CICS-ARCH-API'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
                                            ASC-SEL-TERM-ID                     
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                         TO ASC-LIST-TASK-NUMBER                
                                            ASC-SEL-TASK-NUMBER                 
                   MOVE PC-LIST-QUEUE-SEQ                                       
                                         TO ASC-LIST-SEQ                        
                   MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
      ***-----$TEMPLATE NOTE$-------------------------------------------        
      *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
      *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
      *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
      ***---------------------------------------------------------------        
                   IF  PS-COMMAREA-NOT-VALID                                    
      ***              MOVE PC-TSYMSG-99901                                     
      ***                                TO DP020-MSG-NUMBER                    
      ***              SET DP020-MSG-FATAL                                      
      ***                                TO TRUE                                
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                         TO TRUE                                
                   ELSE                                                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                         TO PV-TOP-ITEM                         
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   IF  (ASC-USE-TSQB)                                           
                   AND (DP055-CREATED                                           
                   OR   DP055-UPDATED                                           
                   OR   DP055-DELETED)                                          
                       PERFORM 1110-READ-TSQB-QUEUE                             
                       SET DP002-UPD (ASC-MN-SUB) TO TRUE                       
                       PERFORM 1140-REWRITE-TSQB                                
                   END-IF                                                       
                   PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                         TO DP013-MESSAGE-TEXT(1)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
      * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
      *----------------------------------------------------------------*        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID           TO TRUE.                             
           MOVE SPACE                      TO ASC-START-ITEM-REQUEST.           
           IF DP020-INT-APPL-FORMAT-IND = PC-INT-APPL-FORMAT                    
               SET  ASC-NO-TSQB            TO TRUE                              
               MOVE DP055-MNEMONIC         TO ASC-KEY-MENU                      
           ELSE                                                                 
               IF DP020-INT-APPL-FORMAT-IND = PC-TSQ-B-FORMAT                   
                   SET  ASC-USE-TSQB       TO TRUE                              
                   MOVE 1                  TO ASC-MN-ITEM                       
                                              ASC-MN-SUB                        
                                              ASC-NUMBER-PROCESSED-ITEMS        
                   PERFORM 1110-READ-TSQB-QUEUE                                 
                   MOVE DP002-MNEMONIC (1) TO ASC-KEY-MENU                      
                   SET DP002-INQ (1)       TO TRUE                              
                   PERFORM 1140-REWRITE-TSQB                                    
               ELSE                                                             
                   SET  ASC-NO-TSQB        TO TRUE                              
                   IF  DP020-NAV-KEY-DATA > SPACE                               
                       MOVE DP020-NAV-KEY-DATA TO ASC-KEY-MENU                  
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ASC-KEY-MENU TO TMENUITEM-ITEM-MNEMONIC-CODE.                   
           PERFORM 4350-READ-TMNEMONIC-ROW.                                     
           MOVE TMNEMONIC-PANEL-TITLE-NAME TO ASC-SCREEN-TITLE.                 
           PERFORM 1199-MAKE-SURE-MENU.                                         
                                                                                
                                                                                
       1110-READ-TSQB-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE  (DP055-TSQB-QUEUE-NAME)                                   
               INTO   (DP002-ARCH-MAINT-TSQB-REC)                               
               LENGTH (DP055-TSQB-QUEUE-LENGTH)                                 
               ITEM   (ASC-MN-ITEM)                                             
               RESP   (PV-CICS-RESP)                                            
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '1110-READ-TSQB-QUEUE'                                      
                                         TO  DP013-PARAGRAPH                    
               MOVE PV-CICS-RESP         TO PV-DISP-RESP                        
               STRING 'LOGIC ERROR TRYING TO READ TSQ "B" QUEUE '               
                      '- RESP = ' PV-DISP-RESP                                  
                     DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       1120-GET-PREV-MNEMONIC.                                                  
                                                                                
           IF ASC-MN-SUB > 1                                                    
               SUBTRACT 1          FROM ASC-MN-SUB                              
           ELSE                                                                 
               IF ASC-MN-ITEM > 1                                               
                   SUBTRACT 1          FROM ASC-MN-ITEM                         
                   MOVE DP002-MAX-ITEMS  TO ASC-MN-SUB                          
               ELSE                                                             
                   MOVE PC-TSYMSG-00069  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO MR-SELECTL (1)                      
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO  TRUE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 1110-READ-TSQB-QUEUE.                                        
           IF DP002-MNEMONIC (ASC-MN-SUB) NOT = ASC-KEY-MENU                    
               MOVE DP002-MNEMONIC (ASC-MN-SUB) TO ASC-KEY-MENU                 
               IF NOT DP002-UPD (ASC-MN-SUB)                                    
                   SET  DP002-INQ (ASC-MN-SUB)  TO TRUE                         
               END-IF                                                           
               PERFORM 1140-REWRITE-TSQB                                        
                                                                                
               MOVE ASC-KEY-MENU TO TMENUITEM-ITEM-MNEMONIC-CODE                
               PERFORM 4350-READ-TMNEMONIC-ROW                                  
               MOVE TMNEMONIC-PANEL-TITLE-NAME TO ASC-SCREEN-TITLE              
               PERFORM 1199-MAKE-SURE-MENU                                      
           END-IF.                                                              
                                                                                
                                                                                
       1130-GET-NEXT-MNEMONIC.                                                  
                                                                                
           ADD 1                     TO ASC-MN-SUB.                             
           IF ASC-MN-SUB > DP002-MAX-ITEMS                                      
               ADD 1                 TO ASC-MN-ITEM                             
               MOVE 1                TO ASC-MN-SUB                              
           END-IF.                                                              
                                                                                
           PERFORM 1110-READ-TSQB-QUEUE.                                        
                                                                                
           IF DP002-MNEMONIC (ASC-MN-SUB) > SPACES                              
               MOVE DP002-MNEMONIC (ASC-MN-SUB)                                 
                                     TO ASC-KEY-MENU                            
               IF NOT DP002-UPD (ASC-MN-SUB)                                    
                   SET  DP002-INQ (ASC-MN-SUB)                                  
                                     TO TRUE                                    
               END-IF                                                           
               PERFORM 1140-REWRITE-TSQB                                        
                                                                                
               MOVE ASC-KEY-MENU TO TMENUITEM-ITEM-MNEMONIC-CODE                
               PERFORM 4350-READ-TMNEMONIC-ROW                                  
               MOVE TMNEMONIC-PANEL-TITLE-NAME TO ASC-SCREEN-TITLE              
               PERFORM 1199-MAKE-SURE-MENU                                      
           ELSE                                                                 
               SUBTRACT 1          FROM ASC-MN-SUB                              
               MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                        
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP020-MSG-INFORMATIONAL                                      
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
                                                                                
       1140-REWRITE-TSQB.                                                       
                                                                                
           COMPUTE DP055-NUMBER-PROCESSED-ITEMS =                               
                 ((ASC-MN-ITEM - 1) * DP002-MAX-ITEMS) +                        
                   ASC-MN-SUB.                                                  
           IF DP055-NUMBER-PROCESSED-ITEMS < ASC-NUMBER-PROCESSED-ITEMS         
               MOVE   ASC-NUMBER-PROCESSED-ITEMS                                
                 TO DP055-NUMBER-PROCESSED-ITEMS                                
           ELSE                                                                 
               MOVE DP055-NUMBER-PROCESSED-ITEMS                                
                 TO   ASC-NUMBER-PROCESSED-ITEMS                                
           END-IF.                                                              
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                QUEUE(DP055-TSQB-QUEUE-NAME)                                    
                FROM(DP002-ARCH-MAINT-TSQB-REC)                                 
                LENGTH(DP055-TSQB-QUEUE-LENGTH)                                 
                ITEM(ASC-MN-ITEM)                                               
                RESP(PV-CICS-RESP)                                              
                REWRITE                                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '1140-REWRITE-TSQB'  TO DP013-PARAGRAPH                     
               MOVE PV-CICS-RESP         TO PV-DISP-RESP                        
               STRING 'LOGIC ERROR TRYING TO REWRITE TSQ "B" QUEUE '            
                      '- RESP = ' PV-DISP-RESP                                  
                     DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       1199-MAKE-SURE-MENU.                                                     
                                                                                
           IF TMNEMONIC-TRANS-ID-CODE NOT = PC-TRAN-Z007                        
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL           TO TRUE                            
               MOVE PC-TSYMSG-00266          TO DP020-MSG-NUMBER                
               IF ASC-USE-TSQB                                                  
                   PERFORM 1110-READ-TSQB-QUEUE                                 
                   SET DP002-ERR (ASC-MN-SUB) TO TRUE                           
                   PERFORM 1140-REWRITE-TSQB                                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
      * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
      * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
      * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
      * FUNCTION KEYS CAN GET THIS FAR.                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-PANEL.                                                      
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   MOVE ASC-KEY-MENU      TO DK-MENU                            
                   MOVE ASC-LAST-SEQ-READ TO DK-SEQ                             
                   PERFORM 2050-DELETE-LIST-QUEUE                               
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 2140-DESELECT-ALL-ITEMS                              
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
                   PERFORM 3200-RESET-ATTRIBUTES                                
                   PERFORM 2150-SELECT-ALL-ITEMS                                
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       SET DP030-CONTINUE-GO                                    
                                       TO TRUE                                  
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                       SET DP030-OVERRIDE-GO                                    
                                       TO TRUE                                  
                   END-IF                                                       
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
      *----------------------------------------------------------------*        
       2050-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
                                                                                
               MOVE '2050-DELETE-LIST-QUEUE'                                    
                                         TO DP013-PARAGRAPH                     
               MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
                                         TO DP013-MESSAGE-TEXT (1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
      * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
      * CICS ARCHITECTURE API.                                         *        
      *----------------------------------------------------------------*        
       2100-CHECK-FUNCTION-KEY.                                                 
           SET ASC-RETURN-2 TO TRUE.                                            
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
                   MOVE ASC-KEY-MENU           TO DP055-MENU-NAME               
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
                   IF ASC-NUMBER-OF-SELECTED-ITEMS = 1                          
                       PERFORM 5000-PREPARE-SEL-QUEUE                           
                       MOVE DP002-MNEMONIC (1)  TO DP055-MNEMONIC               
                       MOVE ASC-KEY-MENU        TO DP055-MENU-NAME              
                       SET  DP055-SELECTED      TO TRUE                         
                       SET ASC-RETURN-1         TO TRUE                         
                   ELSE                                                         
                       IF ASC-NUMBER-OF-SELECTED-ITEMS > 1                      
                           MOVE PC-TSYMSG-00047 TO DP020-MSG-NUMBER             
                       ELSE                                                     
                           MOVE PC-TSYMSG-00057 TO DP020-MSG-NUMBER             
                       END-IF                                                   
                       SET  DP030-OVERRIDE-GO   TO TRUE                         
                       SET  DP020-MSG-FATAL     TO TRUE                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-05(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-06(DP020-SRC-AID)                       
                   IF  (ASC-NUMBER-OF-SELECTED-ITEMS  = 1                       
                   AND  DP020-FK-INT-APPL-FORMAT-IND (DP020-SRC-AID)            
                                                  = PC-INT-APPL-FORMAT)         
                   OR  (ASC-NUMBER-OF-SELECTED-ITEMS >= 1                       
                   AND  DP020-FK-INT-APPL-FORMAT-IND (DP020-SRC-AID)            
                                                  = PC-TSQ-B-FORMAT)            
                       PERFORM 5000-PREPARE-SEL-QUEUE                           
                       MOVE DP002-MNEMONIC (1)  TO DP055-MNEMONIC               
                       MOVE ASC-KEY-MENU        TO DP055-MENU-NAME              
                       SET  DP055-SELECTED      TO TRUE                         
                   ELSE                                                         
                       IF ASC-NUMBER-OF-SELECTED-ITEMS > 1                      
                           MOVE PC-TSYMSG-00047 TO DP020-MSG-NUMBER             
                       ELSE                                                     
                           MOVE PC-TSYMSG-00057 TO DP020-MSG-NUMBER             
                       END-IF                                                   
                       SET  DP030-OVERRIDE-GO   TO TRUE                         
                       SET  DP020-MSG-FATAL     TO TRUE                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-PREV-SELECTION(DP020-SRC-AID)                      
                   IF ASC-USE-TSQB                                              
                       PERFORM 1120-GET-PREV-MNEMONIC                           
                       PERFORM 4411-DELETE-TEMP-STORAGE                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   ELSE                                                         
                       SET  DP030-OVERRIDE-GO TO TRUE                           
                       MOVE PC-TSYMSG-00045   TO DP020-MSG-NUMBER               
                       SET  DP020-MSG-FATAL   TO TRUE                           
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
                                                                                
               WHEN DP020-FK-NEXT-SELECTION(DP020-SRC-AID)                      
                   IF ASC-USE-TSQB                                              
                       PERFORM 1130-GET-NEXT-MNEMONIC                           
                       PERFORM 4411-DELETE-TEMP-STORAGE                         
                       PERFORM 4000-BUILD-INITIAL-PANEL                         
                   ELSE                                                         
                       SET  DP030-OVERRIDE-GO TO TRUE                           
                       MOVE PC-TSYMSG-00045   TO DP020-MSG-NUMBER               
                       SET  DP020-MSG-FATAL   TO TRUE                           
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                   MOVE +1                    TO PV-TOP-ITEM                    
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                   PERFORM 2110-BROWSE-BACKWARD                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                   PERFORM 2120-BROWSE-FORWARD                                  
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
      *            IF  PS-ERROR                                                 
      *                CONTINUE                                                 
      *            ELSE                                                         
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
      *            END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      *                                                                         
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
      *    MESSAGE.                                                    *        
      *----------------------------------------------------------------*        
                                                                                
       2110-BROWSE-BACKWARD.                                                    
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
               GIVING PV-TOP-ITEM.                                              
      *                                                                         
           IF  ((PV-TOP-ITEM < +1)                                              
             OR (PV-TOP-ITEM = +1))                                             
               MOVE +1                   TO PV-TOP-ITEM                         
      *        -- TOP OF LIST --                                                
               MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
      *----------------------------------------------------------------*        
                                                                                
       2120-BROWSE-FORWARD.                                                     
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
               GIVING PV-TOP-ITEM.                                              
           COMPUTE PV-BOTTOM-ITEM =                                             
               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1.                            
      *                                                                         
           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                        
             AND ASC-ITEMS-LEFT-ON-DB                                           
               ADD PC-ITEMS-PER-PANEL  1                                        
                   GIVING ASC-ITEM-SUB                                          
               ADD PC-MAX-BLOCKS-IN-ARRAY 1                                     
                   GIVING ASC-BLK-SUB                                           
               MOVE PC-MAX-GROUPS      TO PV-GROUPS-TO-READ                     
               PERFORM 4005-BUILD-ADDITIONAL-PANELS                             
           END-IF.                                                              
                                                                                
      *                                                                         
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
      *        -- BOTTOM OF LIST --                                             
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           ELSE                                                                 
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
      *            -- BOTTOM OF LIST --                                         
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *----------------------------------------------------------------*        
                                                                                
       2130-CHECK-FOR-JUMP-BROWSE.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           IF (ASC-START-ITEM-REQUEST = SPACE)                                  
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO  ASC-START-ITEM-REQUEST-N           
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       PERFORM 2131-GET-MORE-PANELS                             
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *                        -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *                --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-MDT                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-MDT                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * SEE IF THERE IS A NEED TO READ ADDITIONAL PANELS OF INFO FROM  *        
      * DATABASE BASED ON THE JUMP-BROWSE REQUEST.  IF THERE IS        *        
      * CALCULATE THE NUMBER TO READ AND READ THEM.                    *        
      *----------------------------------------------------------------*        
       2131-GET-MORE-PANELS.                                                    
           IF  ASC-START-ITEM-REQUEST-N >                                       
                                    ASC-NUMBER-OF-ITEMS-READ                    
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                   ADD PC-ITEMS-PER-PANEL  1                                    
                       GIVING ASC-ITEM-SUB                                      
                   COMPUTE PV-GROUPS-TO-READ ROUNDED =                          
                       (((ASC-START-ITEM-REQUEST-N                              
                       - ASC-NUMBER-OF-ITEMS-READ)                              
                       / (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-PANEL))         
                       + 1)                                                     
                   PERFORM 4005-BUILD-ADDITIONAL-PANELS                         
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
      *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
      *----------------------------------------------------------------*        
       2140-DESELECT-ALL-ITEMS.                                                 
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
                                                                                
           PERFORM 2141-CLEAR-SELECTED-INDS                                     
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
              (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB FROM 1 BY 1                         
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2141-CLEAR-SELECTED-INDS                             
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE +0                       TO ASC-NUMBER-OF-SELECTED-ITEMS        
                                            ASC-START-OF-SELECTED-RANGE         
                                            ASC-END-OF-SELECTED-RANGE.          
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
      *----------------------------------------------------------------*        
                                                                                
       2141-CLEAR-SELECTED-INDS.                                                
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  (ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                 AND ASC-TEMP-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB))              
                   CONTINUE                                                     
               ELSE                                                             
                   SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
      *  FOR ALL ITEMS.                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2150-SELECT-ALL-ITEMS.                                                   
                                                                                
                                                                                
      *  SELECT ALL ITEMS IN THE CURRENT 'FIRST' BLOCK.                         
           MOVE +1                       TO ASC-BLK-SUB.                        
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
      *  SELECT ALL ITEMS IN THE CURRENT 'SECOND' BLOCK.                        
           ADD +1                        TO ASC-BLK-SUB.                        
           PERFORM 2151-SET-SELECTED-INDS                                       
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO PV-LIST-ITEM-NUMBER.                
                                                                                
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF ((PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                            
              (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY) OR         
              (ASC-ITEMS-LEFT-ON-DB))                                           
               MOVE +1                   TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                    TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               MOVE +1                   TO ASC-BLK-SUB                         
                                                                                
      *  SELECT EVERYTHING 'BEFORE' THE CURRENT BLOCKS                          
               MOVE +1                   TO PV-BLOCK-NUMBER                     
                                                                                
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-BLOCK-NUMBER                     
                                                                                
               END-PERFORM                                                      
                                                                                
      * IF NOT ALL DATA HAS BEEN READ, READ EVERYTHING, AND PUT IT              
      * INTO TEMP STORAGE.                                                      
           IF  ASC-ITEMS-LEFT-ON-DB                                             
               COMPUTE PV-GROUPS-TO-READ =                                      
                   (((PC-MAX-ITEMS - ASC-NUMBER-OF-ITEMS-READ)                  
                   / (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-PANEL)) + 1)        
               PERFORM 4005-BUILD-ADDITIONAL-PANELS                             
           END-IF                                                               
                                                                                
      * SELECT EVERYTHING AFTER THE CURRENT BLOCKS                              
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
                                                                                
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         ASC-HIGHEST-BLOCK-WRITTEN                              
                                                                                
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                                                                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
                                                                                
                   PERFORM 2151-SET-SELECTED-INDS                               
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                                                                                
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                                                                                
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
                                                                                
               END-PERFORM                                                      
                                                                                
      *  REPOSITION TO CURRENT BLOCKS                                           
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
                                                                                
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
                             ASC-NUMBER-OF-SELECTED-ITEMS.                      
                                                                                
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *----------------------------------------------------------------*        
                                                                                
       2151-SET-SELECTED-INDS.                                                  
                                                                                
           IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
                   NOT EQUAL ZERO                                               
               IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                   SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
                       ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
                       ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **        
      ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **        
      ******************************************************************        
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
                                                                                
           MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
           MOVE +1                       TO PV-SUB.                             
      *                                                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           IF  ASTRTITL > ZERO                                                  
               MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST                  
           ELSE                                                                 
               IF  ASTRTITF = DP015-ERASE-EOF                                   
                   INITIALIZE ASC-START-ITEM-REQUEST                            
               END-IF                                                           
           END-IF.                                                              
           PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
      ******************************************************************        
      ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**        
      ******************************************************************        
       2210-MOVE-LINES-TO-COMMAREA.                                             
           IF  MR-SELECTL (PV-SUB) > ZERO                                       
               SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                     
               MOVE MR-SELECTI (PV-SUB)                                         
                                         TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
           ELSE                                                                 
               IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
                       SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             
                       MOVE SPACE        TO ASC-TEMP-SELECTED-SW                
                                       (ASC-BLK-SUB, ASC-ITEM-SUB)              
               END-IF                                                           
           END-IF.                                                              
           ADD +1                        TO ASC-ITEM-SUB.                       
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               MOVE +1                   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      ******************************************************************        
      ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **        
      ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **        
      ******************************************************************        
       3000-EDIT-DATA-IN-COMMAREA.                                              
           SET PS-NO-ERROR               TO TRUE.                               
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
      **                                                                        
      * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
      * EDITTING IS DONE FROM THE BOTTOM UP.                                    
      **                                                                        
           COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
               ASC-ITEMS-DISPLAYED - 1.                                         
           PERFORM 3100-SET-SUBSCRIPTS.                                         
           PERFORM 3300-EDIT-SELECTIONS                                         
               VARYING PV-SUB                                                   
               FROM PV-SUB BY -1                                                
               UNTIL PV-SUB < +1                                                
      *                                                                         
           IF  PS-NO-ERROR                                                      
               PERFORM 3400-CHECK-RANGE-SEQUENCE                                
           END-IF.                                                              
      *                                                                         
           IF  ((PS-NO-ERROR)                                                   
             AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
             AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
                   PERFORM 3500-MARK-RANGE-AS-SELECTED                          
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *----------------------------------------------------------------*        
                                                                                
       3100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
                                                                                
           IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB                       
           ELSE                                                                 
               MOVE +1                     TO ASC-BLK-SUB                       
           END-IF.                                                              
      ******************************************************************        
      ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **        
      ******************************************************************        
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
      *                                                                         
           PERFORM                                                              
               VARYING PV-SUB                                                   
               FROM 1 BY 1                                                      
               UNTIL PV-SUB > PC-ITEMS-PER-PANEL                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO MR-SELECTA (PV-SUB)                 
               MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)                 
               MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)                 
           END-PERFORM.                                                         
      ******************************************************************        
      ** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **        
      ** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **        
      ** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **        
      ** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **        
      ******************************************************************        
       3300-EDIT-SELECTIONS.                                                    
           EVALUATE TRUE                                                        
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                   = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
                       CONTINUE                                                 
               WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                   IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
                       ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS           
                       MOVE ASC-TEMP-SELECTED-SW                                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                                      TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-IF                                                       
               WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
                       = SPACE                                                  
                   EVALUATE TRUE                                                
                       WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
                           SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
                           MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE            
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                       WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
                           MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE              
                           MOVE SPACE TO ASC-SELECTED-SW                        
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   END-EVALUATE                                                 
               WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
                   PERFORM 3310-EDIT-RANGE                                      
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER                   
                   SET DP020-MSG-FATAL    TO TRUE                               
                   MOVE -1                TO MR-SELECTL (PV-SUB)                
                   SET DP030-SET-CURSOR-APPL-1                                  
                                          TO TRUE                               
                   MOVE DP015-RED         TO MR-SELECTC (PV-SUB)                
                   MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)                
                   SET PS-ERROR TO TRUE                                         
                   IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
                       SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
                       MOVE SPACE         TO ASC-SELECTED-SW                    
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           SUBTRACT +1             FROM PV-ITEM-IN-USE.                         
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < 1                                                 
               SUBTRACT +1 FROM ASC-BLK-SUB                                     
               MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
           END-IF.                                                              
      ******************************************************************        
      ** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **        
      ** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **        
      ** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**        
      ** THE ENDING.                                                  **        
      ******************************************************************        
       3310-EDIT-RANGE.                                                         
           IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
               IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
                   MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
                                         TO ASC-SELECTED-SW                     
                                              (ASC-BLK-SUB ASC-ITEM-SUB)        
                   MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
               ELSE                                                             
                   IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
                       MOVE PC-TSYMSG-00054                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
                       MOVE DP015-UNDERLINE                                     
                                         TO MR-SELECTH (PV-SUB)                 
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                         TO ASC-SELECTED-SW                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
                   MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
               ELSE                                                             
                   IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- ONLY ONE END-OF-RANGE ALLOWED --                      
                       MOVE PC-TSYMSG-00055                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-FATAL                                      
                                         TO TRUE                                
                       MOVE -1           TO MR-SELECTL (PV-SUB)                 
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      ** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **        
      ** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **        
      ******************************************************************        
       3400-CHECK-RANGE-SEQUENCE.                                               
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
             AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
             AND  ASC-START-OF-SELECTED-RANGE >                                 
                                               ASC-END-OF-SELECTED-RANGE        
      *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
               MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-FATAL TO TRUE                                      
               SET PS-ERROR TO TRUE                                             
               PERFORM 3410-POSITION-AT-ERROR                                   
           END-IF.                                                              
      ******************************************************************        
      ** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **        
      ** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **        
      ** SHOULD BE POSITIONED ON A RANGE ERROR.                       **        
      ******************************************************************        
       3410-POSITION-AT-ERROR.                                                  
           SET DP030-SET-CURSOR-APPL-1                                          
                                         TO TRUE.                               
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
      *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
      *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
      *----------------------------------------------------------------*        
                                                                                
       3500-MARK-RANGE-AS-SELECTED.                                             
           MOVE +1                       TO  ASC-BLK-SUB.                       
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           ADD +1                        TO ASC-BLK-SUB.                        
           PERFORM 3510-MARK-SELECTED-INDS                                      
               VARYING ASC-ITEM-SUB                                             
               FROM 1 BY 1                                                      
               UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
                                         TO  PV-LIST-ITEM-NUMBER.               
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-START-OF-SELECTED-RANGE                                     
                                         TO PV-START-OF-SELECTED-RANGE.         
           COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
               1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
                     PC-ITEMS-PER-PANEL).                                       
                                                                                
           MOVE ASC-END-OF-SELECTED-RANGE                                       
                                         TO  PV-END-OF-SELECTED-RANGE.          
           COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
               1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
                     PC-ITEMS-PER-PANEL).                                       
           COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
               PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
                                                                                
           IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
              (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
               MOVE +1                   TO ASC-BLK-SUB                         
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
                         PV-PREV-BLOCK-IN-ARRAY                                 
                   MOVE PV-FIRST-BLOCK-IN-RANGE                                 
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         PV-LAST-BLOCK-IN-RANGE                                 
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                         TO PV-BLOCK-NUMBER                     
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   PERFORM 3510-MARK-SELECTED-INDS                              
                       VARYING ASC-ITEM-SUB                                     
                       FROM 1 BY 1                                              
                       UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                         TO PV-BLOCK-NUMBER                     
               PERFORM 4410-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
           MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
                                            ASC-END-OF-SELECTED-RANGE.          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
      *----------------------------------------------------------------*        
                                                                                
       3510-MARK-SELECTED-INDS.                                                 
                                                                                
           IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) >                           
               ASC-START-OF-SELECTED-RANGE)                                     
           OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                                 
                                      ASC-ITEM-SUB) =                           
               ASC-START-OF-SELECTED-RANGE)                                     
                                                                                
               IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) <                       
                   ASC-END-OF-SELECTED-RANGE)                                   
               OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                             
                                          ASC-ITEM-SUB) =                       
                   ASC-END-OF-SELECTED-RANGE)                                   
                                                                                
                   IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
                       SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
                           ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
                                     TO TRUE                                    
                       SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
                                     TO TRUE                                    
                       ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                 ASC-BLOCK-ENTRIES (1)                     
                                      ASC-BLOCK-ENTRIES (2).                    
           MOVE +1                 TO ASC-BLK-SUB.                              
           SET ASC-ITEMS-LEFT-ON-DB                                             
                                   TO TRUE.                                     
           MOVE PC-MAX-GROUPS      TO PV-GROUPS-TO-READ.                        
                                                                                
           MOVE +0                 TO ASC-ITEM-SUB                              
                                      ASC-NUMBER-OF-ITEMS-READ                  
                                      ASC-NUMBER-OF-SELECTED-ITEMS              
                                      ASC-HIGHEST-BLOCK-WRITTEN                 
                                      ASC-START-OF-SELECTED-RANGE               
                                      ASC-END-OF-SELECTED-RANGE                 
                                      ASC-ITEM-AT-TOP-OF-PANEL                  
                                      ASC-LAST-SEQ-READ.                        
                                                                                
           PERFORM 4005-BUILD-ADDITIONAL-PANELS.                                
                                                                                
           MOVE +1                   TO  PV-TOP-ITEM.                           
           IF ASC-NUMBER-OF-ITEMS-READ = ZERO                                   
               MOVE DP015-ASK-DRK-OFF TO ASTRTITA                               
               MOVE DP015-HL-OFF      TO ASTRTITH                               
               PERFORM VARYING PV-SUB FROM 1 BY 1                               
                         UNTIL PV-SUB > PC-ITEMS-PER-PANEL                      
                   MOVE DP015-ASK-DRK-OFF TO MR-SELECTA  (PV-SUB)               
                   MOVE DP015-HL-OFF      TO MR-SELECTH  (PV-SUB)               
                   MOVE SPACE             TO MR-SELECTI  (PV-SUB)               
                                             MR-MNEMONIC (PV-SUB)               
                                             MR-SEQ      (PV-SUB)               
                                             MR-FMT      (PV-SUB)               
                                             MR-TITLE    (PV-SUB)               
                                             MR-PROGRAM  (PV-SUB)               
                                             MR-RESULT   (PV-SUB)               
               END-PERFORM                                                      
           ELSE                                                                 
               MOVE 1                TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
               PERFORM 4410-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2            TO ASC-BLK-SUB                             
                                        PV-BLOCK-NUMBER                         
                   PERFORM 4410-READ-TEMP-STORAGE                               
               END-IF                                                           
               MOVE -1               TO MR-SELECTL (1)                          
               SET DP030-SET-CURSOR-APPL-1                                      
                                     TO TRUE                                    
               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    THE LOGIC IN THIS PARAGRAPH IS VERY SIMILAR TO THAT IN THE  *        
      *    'BUILD INITIAL PANEL' PARAGRAPH, EXCEPT THAT THE VARIABLES  *        
      *    ARE NOT INITIALIZED.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4005-BUILD-ADDITIONAL-PANELS.                                            
                                                                                
      *    MOVE +1                 TO ASC-BLK-SUB.                              
                                                                                
           PERFORM 4100-PREPARE-CURSOR                                          
                                                                                
           PERFORM 4010-OPEN-CURSOR.                                            
                                                                                
           PERFORM 4300-READ-ITEMS-FROM-DB                                      
               VARYING PV-TEMP-NUM                                              
               FROM 1 BY 1                                                      
               UNTIL PV-TEMP-NUM > PV-GROUPS-TO-READ                            
                  OR ASC-NO-ITEMS-LEFT-ON-DB.                                   
                                                                                
           PERFORM 4020-CLOSE-CURSOR.                                           
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * OPEN THE TMENUITEM CURSOR.                                     *        
      *----------------------------------------------------------------*        
                                                                                
       4010-OPEN-CURSOR.                                                        
           PERFORM  WITH TEST AFTER                                             
                 VARYING PV-RETRY-COUNTER FROM 1 BY 1                           
                   UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                   
                      OR  (SQLCODE          = ZERO))                            
               EXEC SQL                                                         
                   OPEN MENU-CURSOR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '4010-OPEN-CURSOR'                                  
                                         TO DP013-PARAGRAPH                     
                       MOVE 'OPEN MENU CURSOR - '                               
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TMENUITEM'  TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                                 
               MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'           
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TMENUITEM'          TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * CLOSE THE TMENUITEM CURSOR.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       4020-CLOSE-CURSOR.                                                       
           EXEC SQL                                                             
               CLOSE MENU-CURSOR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '4020-CLOSE-CURSOR'                                     
                                     TO DP013-PARAGRAPH                         
                   MOVE 'CLOSE MENU CURSOR '                                    
                                     TO DP013-MESSAGE-TEXT (1)                  
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TMENUITEM'  TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *----------------------------------------------------------------*        
                                                                                
       4100-PREPARE-CURSOR.                                                     
                                                                                
           MOVE ASC-KEY-MENU       TO DK-MENU.                                  
           MOVE ASC-LAST-SEQ-READ  TO DK-SEQ.                                   
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *----------------------------------------------------------------*        
                                                                                
       4300-READ-ITEMS-FROM-DB.                                                 
           COMPUTE PV-MAX-ITEMS =                                               
                   ((PC-MAX-GROUPS * PC-ITEMS-PER-PANEL)                        
                   * PC-MAX-BLOCKS-IN-ARRAY).                                   
                                                                                
           PERFORM                                                              
               VARYING PV-READ-COUNT                                            
               FROM 1 BY 1                                                      
               UNTIL ((PV-READ-COUNT > PV-MAX-ITEMS)                            
                  OR  (ASC-NO-ITEMS-LEFT-ON-DB))                                
                                                                                
               PERFORM 4310-FETCH                                               
                                                                                
               IF  ASC-ITEMS-LEFT-ON-DB                                         
                   IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
                       ADD +1            TO ASC-ITEM-SUB                        
                   ELSE                                                         
                       ADD +1            TO ASC-BLK-SUB                         
                       MOVE +1           TO ASC-ITEM-SUB                        
                                                                                
                       IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
                           MOVE +1       TO ASC-BLK-SUB                         
                           SET ASC-BLOCK-MODIFIED (1)                           
                                         TO TRUE                                
                           PERFORM 5100-WRITE-TEMP-STORAGE                      
                           MOVE ASC-BLOCK-ENTRIES                               
                                     (PC-MAX-BLOCKS-IN-ARRAY)                   
                                      TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
                           ADD +1        TO ASC-BLK-SUB                         
                           INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
                           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
                                         TO TRUE                                
                       END-IF                                                   
                   END-IF                                                       
                   ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
                   MOVE ASC-NUMBER-OF-ITEMS-READ                                
                                         TO ASC-LIST-ITEM-NUMBER                
                                            (ASC-BLK-SUB ASC-ITEM-SUB)          
                   PERFORM 4350-READ-TMNEMONIC-ROW                              
                   MOVE SPACES  TO ASC-SELECTED-SW                              
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE TMENUITEM-ITEM-MNEMONIC-CODE                            
                                TO ASC-MNEMONIC                                 
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE TMENUITEM-SEQUENCE-NMBR                                 
                                TO ASC-SEQ   (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE TMENUITEM-IN-APLCTN-COMM-CDE                            
                                TO ASC-FMT   (ASC-BLK-SUB ASC-ITEM-SUB)         
                   IF PV-DESC-NULL-IND  <  ZEROS                                
                       MOVE TMNEMONIC-PANEL-TITLE-NAME                          
                                TO ASC-TITLE (ASC-BLK-SUB ASC-ITEM-SUB)         
                   ELSE                                                         
                       MOVE TMENUITEM-DESC                                      
                                TO ASC-TITLE (ASC-BLK-SUB ASC-ITEM-SUB)         
                   END-IF                                                       
                   MOVE TMNEMONIC-PGM-NAME                                      
                                TO ASC-PROGRAM                                  
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
                   MOVE SPACES  TO ASC-RESULT                                   
                                             (ASC-BLK-SUB ASC-ITEM-SUB)         
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF ASC-ITEMS-LEFT-ON-DB                                              
               MOVE ASC-SEQ (ASC-BLK-SUB ASC-ITEM-SUB)                          
                                     TO ASC-LAST-SEQ-READ                       
           END-IF.                                                              
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
               MOVE +1               TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
               ADD +1                TO ASC-BLK-SUB                             
               PERFORM 5100-WRITE-TEMP-STORAGE                                  
                                                                                
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE MESSAGE CURSOR.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       4310-FETCH.                                                              
                                                                                
           EXEC SQL                                                             
               FETCH MENU-CURSOR                                                
               INTO  :TMENUITEM-MENU-NAME                                       
                   , :TMENUITEM-ITEM-MNEMONIC-CODE                              
                   , :TMENUITEM-DESC:PV-DESC-NULL-IND                           
                   , :TMENUITEM-SEQUENCE-NMBR                                   
                   , :TMENUITEM-IN-APLCTN-COMM-CDE                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4310-FETCH-CURSOR'                                    
                                     TO DP013-PARAGRAPH                         
                    MOVE 'FETCH MENU CURSOR'                                    
                                     TO DP013-MESSAGE-TEXT (1)                  
                    MOVE SQLCA       TO DP013-SQLCA                             
                    SET DP013-DB2-ABEND                                         
                                     TO TRUE                                    
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET ASC-NO-ITEMS-LEFT-ON-DB                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
       4350-READ-TMNEMONIC-ROW.                                                 
                                                                                
             EXEC SQL                                                           
                 SELECT MNEMONIC_CODE                                           
                      , PGM_NAME                                                
                      , PANEL_TITLE_NAME                                        
                      , TRANS_ID_CODE                                           
                   INTO :TMNEMONIC-MNEMONIC-CODE                                
                      , :TMNEMONIC-PGM-NAME                                     
                      , :TMNEMONIC-PANEL-TITLE-NAME                             
                      , :TMNEMONIC-TRANS-ID-CODE                                
                   FROM TMNEMONIC                                               
                  WHERE MNEMONIC_CODE = :TMENUITEM-ITEM-MNEMONIC-CODE           
             END-EXEC.                                                          
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT = SPACE                                        
               WHEN SQLCODE NOT = ZERO                                          
                    MOVE '4350-READ-TMNEMONIC'                                  
                                          TO DP013-PARAGRAPH                    
                    MOVE 'FETCH MENU CURSOR'                                    
                                          TO DP013-MESSAGE-TEXT (1)             
                    MOVE 'TMNEMONIC'      TO DP013-DB2-TABLE-NAME (1)           
                    MOVE SQLCA            TO DP013-SQLCA                        
                    SET DP013-DB2-ABEND   TO TRUE                               
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   ADD +1     TO ASC-BLK-SUB                                    
                   PERFORM 5100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER                                   
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1 TO PV-BLOCK-NUMBER                                
                   END-IF                                                       
                   MOVE +1    TO ASC-BLK-SUB                                    
                   PERFORM 4410-READ-TEMP-STORAGE                               
                   ADD +1     TO ASC-BLK-SUB                                    
                                 PV-BLOCK-NUMBER                                
                   PERFORM 4410-READ-TEMP-STORAGE                               
           END-IF.                                                              
                                                                                
           MOVE ASC-KEY-MENU                   TO AMNEMO.                       
           MOVE DP015-WHITE                    TO AMNEMC.                       
           MOVE DP015-PRO-BRT-MDT              TO AMNEMA.                       
           MOVE ASC-SCREEN-TITLE               TO AMNTTLEO.                     
           MOVE DP015-WHITE                    TO AMNTTLEC.                     
           MOVE DP015-PRO-BRT-MDT              TO AMNTTLEA.                     
                                                                                
           MOVE +1            TO ASC-BLK-SUB.                                   
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
      *                                                                         
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1         TO ASC-BLK-SUB                                    
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
      *                                                                         
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
           PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
           PERFORM 4420-FORMAT-LIST-LINES                                       
               VARYING PV-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PV-SUB < +1.                                               
      *                                                                         
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
                                            ASTRTITO.                           
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           IF  ASC-NO-ITEMS-LEFT-ON-DB                                          
               MOVE ASC-NUMBER-OF-ITEMS-READ                                    
                                         TO ATOTITO                             
           ELSE                                                                 
               MOVE SPACES               TO ATOTITI                             
           END-IF.                                                              
      *                                                                         
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  (ASC-ITEMS-LEFT-ON-DB)                                       
                 OR  (ASC-ITEM-AT-BOT-OF-PANEL                                  
                                    < ASC-NUMBER-OF-ITEMS-READ)                 
      *            --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL TO TRUE                          
               ELSE                                                             
                   IF  ASC-ITEM-AT-BOT-OF-PANEL =                               
                                              ASC-NUMBER-OF-ITEMS-READ          
      *                --- BOTTOM OF LIST ----                                  
                       MOVE PC-TSYMSG-00070                                     
                                         TO DP020-MSG-NUMBER                    
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
                                                                                
       4410-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4410-READ-TEMP-STORAGE'                                    
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *----------------------------------------------------------------*        
                                                                                
       4411-DELETE-TEMP-STORAGE.                                                
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP (NORMAL))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND     TO TRUE                                 
               SET DP013-NO-ROLLBACK    TO TRUE                                 
               MOVE '4411-DELETE-TEMP-STORAGE'                                  
                                        TO DP013-PARAGRAPH                      
               MOVE 'ERROR TRYING TO DELETE TEMP STORAGE QUEUE'                 
                                        TO DP013-MESSAGE-TEXT(1)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *----------------------------------------------------------------*        
                                                                                
       4420-FORMAT-LIST-LINES.                                                  
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA  (PV-SUB)                  
               MOVE DP015-HL-OFF       TO MR-SELECTH  (PV-SUB)                  
               MOVE SPACE              TO MR-SELECTI  (PV-SUB)                  
                                          MR-MNEMONIC (PV-SUB)                  
                                          MR-SEQ      (PV-SUB)                  
                                          MR-FMT      (PV-SUB)                  
                                          MR-TITLE    (PV-SUB)                  
                                          MR-PROGRAM  (PV-SUB)                  
                                          MR-RESULT   (PV-SUB)                  
           ELSE                                                                 
               IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                                         > SPACE                
                   MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-SELECTI (PV-SUB)                   
               ELSE                                                             
                   MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
                                       TO MR-SELECTI (PV-SUB)                   
               END-IF                                                           
      *                                                                         
               IF  DP030-SET-CURSOR-TRAILER                                     
                   IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
                       > SPACE)                                                 
                     OR (PV-SUB = 1))                                           
                      SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
                      MOVE -1          TO MR-SELECTL (PV-SUB)                   
                   END-IF                                                       
               END-IF                                                           
      *                                                                         
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   MOVE ASC-MNEMONIC (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-MNEMONIC (PV-SUB)                                   
                   MOVE ASC-SEQ      (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-SEQ      (PV-SUB)                                   
                   MOVE ASC-FMT      (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-FMT      (PV-SUB)                                   
                   MOVE ASC-TITLE    (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-TITLE    (PV-SUB)                                   
                   MOVE ASC-PROGRAM  (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-PROGRAM  (PV-SUB)                                   
                   MOVE ASC-RESULT   (ASC-BLK-SUB ASC-ITEM-SUB)                 
                     TO  MR-RESULT   (PV-SUB)                                   
               END-IF                                                           
           END-IF.                                                              
           SUBTRACT +1 FROM ASC-ITEM-SUB                                        
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
      ******************************************************************        
      ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **        
      ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **        
      ** ISSUE A WARNING.                                             **        
      ******************************************************************        
       4430-CHECK-FOR-BEGIN-AND-END.                                            
           IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
               IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
      *            -- NO END-OF-RANGE SPECFIED ---                              
                   MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           ELSE                                                                 
               IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
      *            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
                   MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-WARNING TO TRUE                                
                   PERFORM 4431-POSITION-AT-WARNING                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **        
      ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **        
      ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **        
      ** POSITION THE CURSOR THERE.                                   **        
      ******************************************************************        
       4431-POSITION-AT-WARNING.                                                
           COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
      *                                                                         
           COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
                          - PV-TOP-ITEM + 1.                                    
           IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
               MOVE -1                   TO MR-SELECTL (PV-SUB)                 
               MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
               MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
      * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
      * FOR THE SELECTED ITEMS QUEUE.                                  *        
      *                                                                *        
      * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
      * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
      * THE ARRAY IS FULL.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PREPARE-SEL-QUEUE.                                                  
                                                                                
           MOVE +1                   TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  ASC-BLK-SUB.                           
           PERFORM 5100-WRITE-TEMP-STORAGE.                                     
                                                                                
           PERFORM 6010-DELETE-SEL-QUEUE.                                       
                                                                                
           SET ASC-UPDATE-TSQ        TO  TRUE.                                  
                                                                                
           PERFORM  VARYING DP002-IDX FROM 1 BY 1                               
                      UNTIL DP002-IDX > DP002-MAX-ITEMS                         
               INITIALIZE DP002-SELECTED-ITEM-ARRAY (DP002-IDX)                 
           END-PERFORM.                                                         
           SET DP002-IDX             TO  1.                                     
                                                                                
           PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
               VARYING PV-ITEM FROM 1 BY 1                                      
                 UNTIL PV-ITEM > ASC-HIGHEST-BLOCK-WRITTEN.                     
                                                                                
           IF  DP002-IDX > 1                                                    
               PERFORM 5205-WRITE-SEL-QUEUE                                     
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *----------------------------------------------------------------*        
                                                                                
       5100-WRITE-TEMP-STORAGE.                                                 
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 5105-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 5110-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
      ******************************************************************        
      ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5105-REWRITE-ASC-LISTQ.                                                  
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH               
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **        
      ** SPECIFIC COMMAREA.                                           **        
      ******************************************************************        
       5110-WRITE-ASC-LISTQ.                                                    
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5110-WRITE-ASC-LISTQ'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE TEMP STORAGE QUEUE'                    
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
      * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
      *----------------------------------------------------------------*        
                                                                                
       5200-FILL-SEL-QUEUE-RECORD.                                              
                                                                                
           PERFORM 6112-READ-LIST-QUEUE.                                        
                                                                                
           IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
               PERFORM  VARYING LQW-IDX FROM 1 BY 1                             
                          UNTIL LQW-IDX > PC-ITEMS-PER-PANEL                    
                             OR LQW-LIST-ITEM-NUMBER (LQW-IDX) = ZERO           
                                                                                
                   IF  LQW-SELECT (LQW-IDX)                                     
                       MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
                                         TO DP002-ITEM (DP002-IDX)              
                       MOVE LQW-MNEMONIC (LQW-IDX)                              
                                         TO DP002-MNEMONIC (DP002-IDX)          
                       SET DP002-IDX UP BY 1                                    
                       IF  DP002-IDX > DP002-MAX-ITEMS                          
                           PERFORM 5205-WRITE-SEL-QUEUE                         
                           PERFORM  VARYING DP002-IDX FROM 1 BY 1               
                                      UNTIL DP002-IDX > DP002-MAX-ITEMS         
                               INITIALIZE DP002-SELECTED-ITEM-ARRAY             
                                                            (DP002-IDX)         
                           END-PERFORM                                          
                           SET DP002-IDX TO  1                                  
                       END-IF                                                   
                   END-IF                                                       
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
      *----------------------------------------------------------------*        
                                                                                
       5205-WRITE-SEL-QUEUE.                                                    
                                                                                
           MOVE ASC-NUMBER-OF-SELECTED-ITEMS TO DP002-SELECTED-ITEMS.           
           MOVE ASC-SEL-QUEUE-NAME           TO DP055-TSQB-QUEUE-NAME.          
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    FROM  (DP002-ARCH-MAINT-TSQB-REC)                           
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO TRUE                                
               SET DP013-NO-ROLLBACK     TO TRUE                                
               MOVE '5205-WRITE-SEL-QUEUE'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
                                         TO DP013-MESSAGE-TEXT(1)               
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      ** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **        
      ** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **        
      ** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **        
      ** THE USER.                                                    **        
      ******************************************************************        
       6000-REFRESH-PANEL-FROM-TEMP.                                            
           IF  ASC-UPDATE-TSQ                                                   
               SET ASC-NO-TSQ-UPDATE TO TRUE                                    
               MOVE ZERO                 TO PV-SAVE-ITEM                        
                                            PV-SEL-QUEUE-ITEM                   
               MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
                                         TO PV-HOLD-SELECTED-ITEMS              
               MOVE +1                   TO PV-ITEMS-PROCESSED                  
               PERFORM 6100-UPDATE-TSQ                                          
                   UNTIL PV-ITEMS-PROCESSED >                                   
                                           PV-HOLD-SELECTED-ITEMS               
               PERFORM 6010-DELETE-SEL-QUEUE                                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
      *----------------------------------------------------------------*        
       6010-DELETE-SEL-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-SEL-QUEUE-NAME)                                   
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
      *                                                                         
           IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
              (PV-CICS-RESP = DFHRESP(QIDERR)))                                 
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '6010-DELETE-SEL-QUEUE'                                     
                              TO DP013-PARAGRAPH                                
               MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *----------------------------------------------------------------*        
                                                                                
       6100-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-SEL-QUEUE-ITEM.                     
           PERFORM 6105-READ-SEL-QUEUE.                                         
           IF ASC-RETURN-1                                                      
               PERFORM 6110-PROCESS-ITEMS                                       
               ADD +1                TO PV-ITEMS-PROCESSED                      
           ELSE                                                                 
           PERFORM                                                              
               VARYING DP002-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL ((PV-ITEMS-PROCESSED >                                     
                                    PV-HOLD-SELECTED-ITEMS)                     
               OR (DP002-IDX > DP002-MAX-ITEMS))                                
               IF  DP002-ACTION-SW (DP002-IDX) > SPACE                          
                   PERFORM 6110-PROCESS-ITEMS                                   
               END-IF                                                           
               ADD +1                TO PV-ITEMS-PROCESSED                      
           END-PERFORM.                                                         
           PERFORM 6111-WRITE-LIST-QUEUE.                                       
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *----------------------------------------------------------------*        
                                                                                
       6105-READ-SEL-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                    QUEUE (ASC-SEL-QUEUE-NAME)                                  
                    INTO  (DP002-ARCH-MAINT-TSQB-REC)                           
                    ITEM  (PV-SEL-QUEUE-ITEM)                                   
                    RESP  (PV-CICS-RESP)                                        
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND      TO  TRUE                               
               SET DP013-NO-ROLLBACK     TO  TRUE                               
               MOVE '6105-READ-SEL-QUEUE'                                       
                                         TO  DP013-PARAGRAPH                    
               MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
                                         TO  DP013-MESSAGE-TEXT(1)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *----------------------------------------------------------------*        
                                                                                
       6110-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM = 1 + ((DP002-ITEM (DP002-IDX) - 1)                  
                           / PC-ITEMS-PER-PANEL).                               
                                                                                
           IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
               IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
                   PERFORM 6111-WRITE-LIST-QUEUE                                
               END-IF                                                           
               MOVE PV-ITEM          TO PV-SAVE-ITEM                            
                                                                                
               PERFORM 6112-READ-LIST-QUEUE                                     
               IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
                   CONTINUE                                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           COMPUTE PV-SUB = (DP002-ITEM (DP002-IDX)                             
                          - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
           SET LQW-DESELECT (PV-SUB) TO    TRUE                                 
           MOVE LQW-SELECTED-SW (PV-SUB)                                        
                                     TO LQW-TEMP-SELECTED-SW (PV-SUB).          
           IF DP002-ACTION-SW (DP002-IDX) > SPACES                              
               EVALUATE DP002-ACTION-SW (DP002-IDX)                             
                 WHEN 'I'   MOVE PC-INQUIRED      TO LQW-RESULT (PV-SUB)        
                 WHEN 'U'   MOVE PC-UPDATED       TO LQW-RESULT (PV-SUB)        
                 WHEN 'D'   MOVE PC-DELETED       TO LQW-RESULT (PV-SUB)        
                 WHEN 'E'   MOVE PC-ERROR-OCCURED TO LQW-RESULT (PV-SUB)        
                 WHEN OTHER MOVE PC-UNKNOWN-ERROR TO LQW-RESULT (PV-SUB)        
               END-EVALUATE                                                     
           ELSE                                                                 
               EVALUATE DP055-ACTION                                            
                 WHEN '1'   MOVE PC-CREATED       TO LQW-RESULT (PV-SUB)        
                 WHEN '2'   MOVE PC-UPDATED       TO LQW-RESULT (PV-SUB)        
                 WHEN '3'   MOVE PC-DELETED       TO LQW-RESULT (PV-SUB)        
                 WHEN '4'   MOVE PC-SELECTED      TO LQW-RESULT (PV-SUB)        
                 WHEN '5'   MOVE PC-ERROR-OCCURED TO LQW-RESULT (PV-SUB)        
                 WHEN '6'   MOVE PC-NOT-FOUND     TO LQW-RESULT (PV-SUB)        
                 WHEN OTHER MOVE PC-UNKNOWN-ERROR TO LQW-RESULT (PV-SUB)        
               END-EVALUATE.                                                    
                                                                                
           SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
                                                                                
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *----------------------------------------------------------------*        
                                                                                
       6111-WRITE-LIST-QUEUE.                                                   
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-SAVE-ITEM)                                         
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE '6111-WRITE-LIST-QUEUE'                                     
                                     TO  DP013-PARAGRAPH                        
               MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF LQW-LIST-ITEM-NUMBER (1) = ASC-LIST-ITEM-NUMBER (1 1)             
               MOVE LQW-LIST-QUEUE-WORK-AREA TO ASC-BLOCK-ENTRIES (1)           
           ELSE                                                                 
               MOVE LQW-LIST-QUEUE-WORK-AREA TO ASC-BLOCK-ENTRIES (2)           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *----------------------------------------------------------------*        
                                                                                
       6112-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
                   ITEM  (PV-ITEM)                                              
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
