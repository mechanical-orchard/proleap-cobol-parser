       IDENTIFICATION DIVISION.                                         00010020
       PROGRAM-ID.              MLKUT364.                               00020020
       AUTHOR.                  THERESE.RAMSEYER.                       00030020
       INSTALLATION.            KOHLS DEPARTMENT STORES.                00040020
       DATE-WRITTEN.            12-04-2009.                             00050020
       DATE-COMPILED.                                                   00060020
      ******************************************************************00070020
      *                 EXTRACT GROUP BRAND                            *00080020
      ******************************************************************00090020
      *  THIS PROGRAM WILL EXTRACT GROUP AND BRAND FROM THE DB2 TABLE  *00100020
      *  MLT_VND_ALLOC TO A FLAT FILE. THERE IS ONE MANDATORY INPUT    *00110020
      *  FILE WHICH REQUIRES A VALID FISCAL WEEK END DATE.             *00120020
      *  THE GROUP ID FOR EACH OUTPUT RECORD IS ASSIGNED AND INCREMENTED00130020
      *  FOR EACH RECORD WITH A UNIQUE ENT_ID. ALL ARE ENT_TYP = 3.    *00140020
      *                                                                *00150020
      *  SEE EXAMPLE BELOW OF OUTPUT FILE:                             *00160020
      *                                                                *00170020
      *  BRND   ENT_ID  GRP_ID                                         *00180020
      *  ---- --------- ----                                           *00190020
      *  1154 000008330 0001                                           *00200020
      *  0053 000072959 0002                                           *00210020
      *  0499 000072959 0002                                           *00220020
      *  2137 000077175 0003                                           *00230020
      *  1841 000084088 0004                                           *00240020
      *  2496 000087521 0005                                           *00250020
      *  2510 000087743 0006                                           *00260020
      *  2688 000087743 0006                                           *00270020
      *  2504 000090070 0007                                           *00280020
      *                                                                *00290020
      ******************** HISTORY OF CHANGES **************************00300020
      * CHG ID/                                                        *00310020
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00320020
      * -------  ----------  ----------------------------------------  *00330020
      * WR37965  2009-12-04  INITIAL IMPLEMENTATION.                   *00340020
      *                      THERESE RAMSEYER.                         *00350020
      ******************************************************************00360020
257901* 257901   2021-06-01  INCREASED THE LENGTH OF LABEL SEQ NUMBER  *00362022
257901*                      FROM 4 TO 5 IN COPY BOOK MLRD364 TO AVOID *00363022
257901*                      TRUNCATION OF LABEL SEQUENCE NUMBER WHICH *00364022
257901*                      IS GREATER THAN 4 DIGITS.                 *00365022
257901*                      - SRINIVASAN SIVARAJ                      *00366022
257901*                        CHANGE NUMBER : CHG0257901              *00367022
************************************************************************00368021
                                                                        00370020
       ENVIRONMENT DIVISION.                                            00380020
       CONFIGURATION SECTION.                                           00390020
       INPUT-OUTPUT SECTION.                                            00400020
       FILE-CONTROL.                                                    00410020
                                                                        00420020
           SELECT DATE-FILE                                             00430020
               ASSIGN TO INP01.                                         00440020
                                                                        00450020
           SELECT BRAND-GROUP-FILE                                      00460020
               ASSIGN TO OUT01.                                         00470020
                                                                        00480020
       DATA DIVISION.                                                   00490020
       FILE SECTION.                                                    00500020
                                                                        00510020
       FD  DATE-FILE                                                    00520020
           RECORDING MODE IS F                                          00530020
           LABEL RECORDS ARE STANDARD                                   00540020
           BLOCK CONTAINS 0 RECORDS.                                    00550020
       01  DATE-REC                        PIC X(80).                   00560020
                                                                        00570020
       FD  BRAND-GROUP-FILE                                             00580020
           RECORDING MODE IS F                                          00590020
           LABEL RECORDS ARE STANDARD                                   00600020
           BLOCK CONTAINS 0 RECORDS.                                    00610020
       01  BRAND-GROUP-REC                 PIC X(80).                   00620020
                                                                        00630020
       WORKING-STORAGE SECTION.                                         00640020
                                                                        00650020
       01  FILLER                          PIC X(40)       VALUE        00660020
           '*** WORKING STORAGE AREA BEGIN ***'.                        00670020
                                                                        00680020
      ***************************************************************** 00690020
      *    DATE CONTROL CARD                                            00700020
      ***************************************************************** 00710020
       01  CC-DATE-CONTROL-CARD.                                        00720020
           05  FILLER                    PIC  X(03).                    00730020
           05  CC-FSCL-WK-END-DTE        PIC  X(10).                    00740020
           05  FILLER                    PIC  X(67).                    00750020
                                                                        00760020
       01  FILLER                          PIC X(40)       VALUE        00770020
           '*** PROGRAMMING CONSTANT BEGIN ***'.                        00780020
                                                                        00790020
       01  PC-PROGRAM-CONSTANTS-AREA.                                   00800020
           05  PC-MAX-RETRIES              PIC S9(03) VALUE +3 COMP-3.  00810020
           05  PC-DEFAULT-NINES            PIC X(10) VALUE '9999-12-31'.00820020
           05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'MLKUT364'. 00830020
           05  PC-LICENSE-FEE-VALUE        PIC  S9(4) COMP VALUE +3.    00840020
                                                                        00850020
       01  FILLER                          PIC X(40)       VALUE        00860020
           '*** PROGRAMMING SWITCHES BEGIN ***'.                        00870020
                                                                        00880020
       01  PS-SWITCHES-AREA.                                            00890020
           05  PS-END-OF-DATE-FILE-SW      PIC X           VALUE 'N'.   00900020
               88  PS-END-OF-DATE-FILE                     VALUE 'Y'.   00910020
               88  PS-NOT-END-OF-DATE-FILE                 VALUE 'N'.   00920020
           05  PS-END-OF-BRND-GRP-CSR-SW   PIC X           VALUE 'N'.   00930020
               88  PS-NOT-END-OF-BRND-GRP-CSR              VALUE 'N'.   00940020
               88  PS-END-OF-BRND-GRP-CSR                  VALUE 'Y'.   00950020
           05  PS-LF-FOUND-SW              PIC X           VALUE 'N'.   00960020
               88  PS-LF-FOUND                             VALUE 'Y'.   00970020
               88  PS-LF-NOT-FOUND                         VALUE 'N'.   00980020
                                                                        00990020
       01  FILLER                          PIC X(40)       VALUE        01000020
           '*** REPORT REQUEST CONTROL REC ***'.                        01010020
                                                                        01020020
       01  FILLER                          PIC X(40)       VALUE        01030020
           '*** PROGRAMMING VARIABLE BEGIN ***'.                        01040020
                                                                        01050020
       01  PV-PROGRAM-VARIABLES-AREA.                                   01060020
           05  PV-RETRY-COUNTER            PIC S9(04)   COMP.           01070020
           05  PV-DTE-IS-VALID             PIC X(01)    VALUE SPACES.   01080020
           05  PV-PREV-ENT-ID              PIC S9(09)   COMP VALUE +0.  01090020
           05  PV-PREV-LBL-SEQ-NBR         PIC S9(04)   COMP VALUE +0.  01100020
           05  PV-OPERATION-MSG-1          PIC X(30)    VALUE SPACES.   01110020
           05  PV-OPERATION-MSG-2          PIC X(30)    VALUE SPACES.   01120020
           05  PV-PARAGRAPH-NAME           PIC X(30)    VALUE SPACES.   01130020
                                                                        01140020
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01150020
           05  PE-MSG-01                       PIC X(30) VALUE          01160020
                'FISCAL WEEK END DTE REQUIRED  '.                       01170020
           05  PE-MSG-02                       PIC X(30) VALUE          01180020
                'INPUT FILE IS EMPTY           '.                       01190020
           05  PE-MSG-03                       PIC X(30) VALUE          01200020
                'LICENSE FEES INTERNAL TABLE   '.                       01210020
           05  PE-MSG-04                       PIC X(30) VALUE          01220020
                'LIMIT EXCEEDED                '.                       01230020
                                                                        01240020
       01  FILLER                          PIC X(40)       VALUE        01250020
           '*** PROGRAMMING ACCUMULATORS BEGIN ***'.                    01260020
                                                                        01270020
       01  PA-PROGRAM-ACCUMULATORS.                                     01280020
           05  PA-TOTALS.                                               01290020
               10 PA-TOT-CSR-FETCHES         PIC S9(09) VALUE +0 COMP-3.01300020
               10 PA-TOT-RECS-WRITTEN        PIC S9(09) VALUE +0 COMP-3.01310020
                                                                        01320020
       01  PT-LIC-FEES-LBL-TBL.                                         01330020
           05  PT-LF-INDX-MAX              PIC S9(05)      VALUE +1000. 01340020
           05  PT-LIC-FEES OCCURS 1000 TIMES                            01350020
                                           INDEXED BY PT-LF-INDX        01360020
                                                      PT-LF-INDX-TOT.   01370020
               10  PT-LF-LBL-SEQ-NBR       PIC S9(04) COMP   VALUE +0.  01380020
               10  PT-LF-GROUP-ID          PIC  9(04)        VALUE  0.  01390020
                                                                        01400020
       01  FILLER                          PIC X(40)       VALUE        01410020
           '*** SQL WORK AREA BEGIN ***'.                               01420020
                                                                        01430020
      ***************************************************************** 01440020
      *  SQL ABEND ROUTINE WORKING STORAGE                              01450020
      ***************************************************************** 01460020
      *01  DP004-DB2-ERROR-FIELDS.                                      01470020
           COPY DPWS004.                                                01480020
                                                                        01490020
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES 01500020
                               COMP SYNC.                               01510020
           88  ABEND-4003                                  VALUE +4003. 01520020
                                                                        01530020
           EXEC SQL                                                     01540020
               INCLUDE SQLCA                                            01550020
           END-EXEC.                                                    01560020
                                                                        01570020
      ***************************************************************** 01580020
      *    MLT_VND_ALLOC  - ML'S VENDOR ALLOCATION TABLE              * 01590020
      ***************************************************************** 01600020
           EXEC SQL                                                     01610020
               INCLUDE MLT00025                                         01620020
           END-EXEC.                                                    01630020
                                                                        01640020
      ***************************************************************** 01650020
      *    TDTATTR - DATE ATTRIBUTE TABLE                             * 01660020
      ***************************************************************** 01670020
           EXEC SQL                                                     01680020
               INCLUDE TDTATTR                                          01690020
           END-EXEC.                                                    01700020
                                                                        01710020
      ***************************************************************** 01720020
      *    BRAND GROUP CURSOR                                         * 01730020
      ***************************************************************** 01740020
                                                                        01750020
           EXEC SQL   DECLARE BRND_GRP_CSR CURSOR FOR                   01760020
               SELECT ENT_ID                                            01770020
                     ,EXP_TYP_NBR                                       01780020
                     ,LBL_SEQ_NBR                                       01790020
                 FROM MLT_VND_ALLOC                                     01800020
                WHERE EXP_TYP_NBR = :PC-LICENSE-FEE-VALUE               01810020
                  AND (  (:CC-FSCL-WK-END-DTE BETWEEN EFF_BEG_DTE AND   01820020
                                                  EFF_END_DTE )         01830020
                   OR (EFF_BEG_DTE  <= :CC-FSCL-WK-END-DTE              01840020
                        AND EFF_END_DTE = :PC-DEFAULT-NINES)  )         01850020
             GROUP BY ENT_ID                                            01860020
                     ,EXP_TYP_NBR                                       01870020
                     ,LBL_SEQ_NBR                                       01880020
                     ,EFF_BEG_DTE                                       01890020
            ORDER BY  ENT_ID                                            01900020
                     ,EXP_TYP_NBR                                       01910020
                     ,LBL_SEQ_NBR                                       01920020
                     ,EFF_BEG_DTE DESC                                  01930020
                 WITH UR                                                01940020
           END-EXEC.                                                    01950020
                                                                        01960020
      ******************************************************************01970020
      * OUTPUT LATOUT -  BRAND GROUP FILE LAYOUT                        01980020
      ******************************************************************01990020
           COPY MLRD364.                                                02000020
      ******************************************************************02010020
                                                                        02020020
       01  FILLER                          PIC X(40)       VALUE        02030020
           '*** WORKING STORAGE END ***'.                               02040020
                                                                        02050020
       PROCEDURE DIVISION.                                              02060020
                                                                        02070020
       0000-MAIN-MODULE.                                                02080020
      ******************************************************************02090020
      *  MAIN PROCESSING.                                               02100020
      ******************************************************************02110020
                                                                        02120020
           PERFORM 1000-INITIALIZATION.                                 02130020
                                                                        02140020
           PERFORM 7100-FETCH-BRND-GRP-CSR                              02150020
               UNTIL PS-END-OF-BRND-GRP-CSR.                            02160020
                                                                        02170020
           PERFORM 7200-CLOSE-BRND-GRP-CSR.                             02180020
                                                                        02190020
           PERFORM 8000-EOJ-ROUTINE.                                    02200020
           PERFORM 8100-DISPLAY-SUMMARY-STATS.                          02210020
                                                                        02220020
           STOP RUN.                                                    02230020
                                                                        02240020
      ******************************************************************02250020
      * OPEN FILES, INITIALIZE VARIABLES, READ INPUT, OPEN CURSOR       02260020
      ******************************************************************02270020
       1000-INITIALIZATION.                                             02280020
                                                                        02290020
           OPEN INPUT  DATE-FILE.                                       02300020
                                                                        02310020
           OPEN OUTPUT BRAND-GROUP-FILE.                                02320020
                                                                        02330020
           INITIALIZE ML364-BRND-GRP-REC.                               02340020
           MOVE 0 TO ML364-GRP-ID                                       02350020
                     PV-PREV-ENT-ID                                     02360020
                     PV-PREV-LBL-SEQ-NBR.                               02370020
           SET PT-LF-INDX-TOT TO 1.                                     02380020
                                                                        02390020
           PERFORM 5000-READ-DATE-FILE.                                 02400020
           PERFORM 7000-OPEN-BRND-GRP-CSR.                              02410020
                                                                        02420020
      ******************************************************************02430020
      *    READ INPUT 1 FISCAL WEEK END DATE FROM  CONTROL CARD         02440020
      *    A VALID FISCAL WEEK END DATE ID REQUIRED                     02450020
      ******************************************************************02460020
       5000-READ-DATE-FILE.                                             02470020
                                                                        02480020
           READ DATE-FILE                                               02490020
               INTO CC-DATE-CONTROL-CARD                                02500020
                 AT END                                                 02510020
                    MOVE '5000-READ-DATE-FILE' TO PV-PARAGRAPH-NAME     02520020
                    MOVE  PE-MSG-01            TO PV-OPERATION-MSG-1    02530020
                    MOVE  PE-MSG-02            TO PV-OPERATION-MSG-2    02540020
                    PERFORM 9997-NON-SQL-ABEND                          02550020
                    SET PS-END-OF-DATE-FILE TO TRUE                     02560020
                 NOT AT END                                             02570020
                    PERFORM 6000-VALIDATE-DATE                          02580020
           END-READ.                                                    02590020
                                                                        02600020
      ****************************************************************  02610020
      * MAKE SURE THE DATE SUPPLIED IS A VALID FISCAL WEEK END DATE     02620020
      * FOUND ON TDTATTR.                                               02630020
      ****************************************************************  02640020
       6000-VALIDATE-DATE.                                              02650020
                                                                        02660020
           PERFORM                                                      02670020
               WITH TEST AFTER                                          02680020
               VARYING PV-RETRY-COUNTER                                 02690020
               FROM 1 BY 1                                              02700020
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               02710020
                         OR (SQLCODE = ZERO))                           02720020
                  EXEC SQL                                              02730020
                      SELECT 'Y'                                        02740020
                        INTO :PV-DTE-IS-VALID                           02750020
                        FROM TDTATTR                                    02760020
                       WHERE KY_DTE          = :CC-FSCL-WK-END-DTE      02770020
                         AND FSCL_WK_END_DTE = :CC-FSCL-WK-END-DTE      02780020
                  END-EXEC                                              02790020
                                                                        02800020
                  EVALUATE TRUE                                         02810020
                      WHEN SQLCODE = 0                                  02820020
                           DISPLAY 'INPUT FSCL WK END DTE: '            02830020
                                                   CC-FSCL-WK-END-DTE   02840020
                                                                        02850020
                      WHEN SQLCODE = -904                               02860020
                      WHEN SQLCODE = -913                               02870020
                           CONTINUE                                     02880020
                      WHEN SQLWARN0 NOT = SPACES                        02890020
                      WHEN SQLCODE < +0                                 02900020
                      WHEN SQLCODE > +0                                 02910020
                        DISPLAY '**************************************'02920020
                        DISPLAY '** ABEND                            **'02930020
                        DISPLAY '** PROGRAM = MLKUT364               **'02940020
                        DISPLAY '** PARA = 6000-VALIDATE-DATE        **'02950020
                        DISPLAY '** SELECT TDTATTR                   **'02960020
                        DISPLAY '** INPUT DATE IS NOT FISCAL WEEK END**'02970020
                        DISPLAY '** CC-FSCL-WK-END-DTE  = '             02980020
                                                   CC-FSCL-WK-END-DTE   02990020
                        DISPLAY '**************************************'03000020
                        PERFORM 9998-SQL-ABEND                          03010020
                  END-EVALUATE                                          03020020
           END-PERFORM.                                                 03030020
                                                                        03040020
           IF PV-RETRY-COUNTER > PC-MAX-RETRIES                         03050020
               DISPLAY '****************************************'       03060020
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       03070020
               DISPLAY '** SELECT TDTATTR                     **'       03080020
               DISPLAY '** PARA = 6000-VALIDATE-DATE          **'       03090020
               DISPLAY '** CC-FSCL-WK-END-DTE  = '                      03100020
                                              CC-FSCL-WK-END-DTE        03110020
               DISPLAY '****************************************'       03120020
               PERFORM 9998-SQL-ABEND                                   03130020
           END-IF.                                                      03140020
                                                                        03150020
      ******************************************************************03160020
      *  OPEN THE BRAND GROUP CURSOR                                    03170020
      ******************************************************************03180020
       7000-OPEN-BRND-GRP-CSR.                                          03190020
                                                                        03200020
           PERFORM                                                      03210020
               WITH TEST AFTER                                          03220020
               VARYING PV-RETRY-COUNTER                                 03230020
               FROM 1 BY 1                                              03240020
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               03250020
                         OR (SQLCODE = ZERO))                           03260020
                                                                        03270020
               EXEC SQL                                                 03280020
                   OPEN BRND_GRP_CSR                                    03290020
               END-EXEC                                                 03300020
                                                                        03310020
               EVALUATE TRUE                                            03320020
                 WHEN SQLCODE = ZERO                                    03330020
                     CONTINUE                                           03340020
                 WHEN SQLWARN0 NOT EQUAL SPACE                          03350020
                 WHEN SQLCODE NOT EQUAL ZERO                            03360020
                     DISPLAY '**************************************'   03370020
                     DISPLAY '** ABEND                            **'   03380020
                     DISPLAY '** PROGRAM = MLKUT364               **'   03390020
                     DISPLAY '** PARA = 7000-OPEN-BRND-GRP-CSR    **'   03400020
                     DISPLAY '** OPEN BRND_GRP_CSR                **'   03410020
                     DISPLAY '**************************************'   03420020
                     PERFORM 9998-SQL-ABEND                             03430020
             END-EVALUATE                                               03440020
           END-PERFORM.                                                 03450020
                                                                        03460020
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    03470020
               (SQLCODE NOT = ZERO)                                     03480020
               DISPLAY '****************************************'       03490020
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       03500020
               DISPLAY '** OPEN BRND_GRP_CSR                  **'       03510020
               DISPLAY '** PARA = 7000-OPEN-BRND-GRP-CSR      **'       03520020
               DISPLAY '****************************************'       03530020
               PERFORM 9998-SQL-ABEND                                   03540020
           END-IF.                                                      03550020
                                                                        03560020
      ******************************************************************03570020
      *  FETCH ROWS FOR BRND_GRP_CSR - FOR EACH HIT, WITH A UNIQUE      03580020
      *  BRAND-ID, WRITE A RECORD TO THE BRAND GROUP FILE               03590020
      ******************************************************************03600020
       7100-FETCH-BRND-GRP-CSR.                                         03610020
                                                                        03620020
           PERFORM                                                      03630020
               WITH TEST AFTER                                          03640020
               VARYING PV-RETRY-COUNTER                                 03650020
               FROM 1 BY 1                                              03660020
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               03670020
                         OR (SQLCODE = ZERO)                            03680020
                         OR (SQLCODE = +100))                           03690020
                                                                        03700020
              EXEC SQL                                                  03710020
                FETCH                                                   03720020
                  FROM BRND_GRP_CSR                                     03730020
                  INTO                                                  03740020
                    :MLT00025-ENT-ID                                    03750020
                   ,:MLT00025-EXP-TYP-NBR                               03760020
                   ,:MLT00025-LBL-SEQ-NBR                               03770020
              END-EXEC                                                  03780020
                                                                        03790020
              EVALUATE TRUE                                             03800020
                  WHEN SQLCODE = ZERO                                   03810020
                    ADD +1                    TO PA-TOT-CSR-FETCHES     03820020
                                                                        03830020
                    IF MLT00025-ENT-ID = PV-PREV-ENT-ID                 03840020
                     AND MLT00025-LBL-SEQ-NBR = PV-PREV-LBL-SEQ-NBR     03850020
                       CONTINUE                                         03860020
                    ELSE                                                03870020
                          SET PS-LF-NOT-FOUND       TO TRUE             03880020
                          PERFORM VARYING PT-LF-INDX FROM 1 BY 1        03890020
                           UNTIL PT-LF-INDX > PT-LF-INDX-TOT            03900020
                              OR PS-LF-FOUND                            03910020
                               IF  PT-LF-LBL-SEQ-NBR (PT-LF-INDX) =     03920020
                                                    MLT00025-LBL-SEQ-NBR03930020
                                   SET PS-LF-FOUND      TO TRUE         03940020
                               END-IF                                   03950020
                          END-PERFORM                                   03960020
                          IF PS-LF-FOUND                                03970020
                              CONTINUE                                  03980020
                          ELSE                                          03990020
                              MOVE MLT00025-LBL-SEQ-NBR                 04000020
                                                    TO ML364-LBL-SEQ-NBR04010020
                              MOVE MLT00025-EXP-TYP-NBR                 04020020
                                                    TO ML364-EXP-TYP    04030020
                              MOVE SPACES           TO ML364-ENT-ID     04040020
      *                       MOVE MLT00025-ENT-ID  TO ML364-ENT-ID     04050020
                              IF MLT00025-ENT-ID = PV-PREV-ENT-ID       04060020
                                 CONTINUE                               04070020
                              ELSE                                      04080020
                                 ADD +1             TO ML364-GRP-ID     04090020
                              END-IF                                    04100020
                              PERFORM 7300-WRITE-BRND-GRP-REC           04110020
                              IF PT-LF-INDX-TOT > PT-LF-INDX-MAX        04120020
                                  MOVE '7100-FETCH-BRND-GRP-CSR'        04130020
                                               TO PV-PARAGRAPH-NAME     04140020
                                  MOVE  PE-MSG-03 TO PV-OPERATION-MSG-1 04150020
                                  MOVE  PE-MSG-04 TO PV-OPERATION-MSG-2 04160020
                                  PERFORM 9997-NON-SQL-ABEND            04170020
                              END-IF                                    04180020
                              MOVE MLT00025-LBL-SEQ-NBR TO              04190020
                                      PT-LF-LBL-SEQ-NBR (PT-LF-INDX-TOT)04200020
                              SET PT-LF-INDX-TOT UP BY 1                04210020
                          END-IF                                        04220020
                    END-IF                                              04230020
                                                                        04240020
                    MOVE MLT00025-ENT-ID        TO PV-PREV-ENT-ID       04250020
                    MOVE MLT00025-LBL-SEQ-NBR   TO PV-PREV-LBL-SEQ-NBR  04260020
                                                                        04270020
                  WHEN SQLCODE = +100                                   04280020
                      SET PS-END-OF-BRND-GRP-CSR TO TRUE                04290020
                                                                        04300020
                  WHEN SQLWARN0 NOT EQUAL SPACE                         04310020
                  WHEN SQLCODE NOT EQUAL ZERO                           04320020
                      DISPLAY '***************************************' 04330020
                      DISPLAY '** ABEND                             **' 04340020
                      DISPLAY '** PROGRAM = MLKUT364                **' 04350020
                      DISPLAY '** PARA =  7100-FETCH-BRND-GRP-CSR   **' 04360020
                      DISPLAY '** FETCH BRND_GRP_CSR                **' 04370020
                      DISPLAY '** LAST ENT ID FETCHED:'                 04380020
                                                   MLT00025-ENT-ID      04390020
                      DISPLAY '** TOT CSR FETCHES: '                    04400020
                                            PA-TOT-CSR-FETCHES          04410020
                      DISPLAY '**************************************'  04420020
                      PERFORM 9998-SQL-ABEND                            04430020
              END-EVALUATE                                              04440020
           END-PERFORM.                                                 04450020
                                                                        04460020
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    04470020
               (SQLCODE NOT = ZERO) AND                                 04480020
               (SQLCODE NOT = +100)                                     04490020
               DISPLAY '****************************************'       04500020
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       04510020
               DISPLAY '** FETCH BRND_GRP_CSR                   **'     04520020
               DISPLAY '** PARA = 7100-FETCH-BRND-GRP-CSR       **'     04530020
               DISPLAY '** LAST ENT ID FETCHED:' MLT00025-ENT-ID        04540020
               DISPLAY '** TOT CSR FETCHES: '                           04550020
                                            PA-TOT-CSR-FETCHES          04560020
               DISPLAY '****************************************'       04570020
               PERFORM 9998-SQL-ABEND                                   04580020
           END-IF.                                                      04590020
                                                                        04600020
      ******************************************************************04610020
      *  CLOSE BRND_GRP_CSR CURSOR                                      04620020
      ******************************************************************04630020
       7200-CLOSE-BRND-GRP-CSR.                                         04640020
                                                                        04650020
           PERFORM                                                      04660020
               WITH TEST AFTER                                          04670020
               VARYING PV-RETRY-COUNTER                                 04680020
               FROM 1 BY 1                                              04690020
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               04700020
                         OR (SQLCODE = ZERO))                           04710020
                                                                        04720020
               EXEC SQL                                                 04730020
                   CLOSE BRND_GRP_CSR                                   04740020
               END-EXEC                                                 04750020
                                                                        04760020
               EVALUATE TRUE                                            04770020
                 WHEN SQLCODE = ZERO                                    04780020
                     CONTINUE                                           04790020
                 WHEN SQLWARN0 NOT EQUAL SPACE                          04800020
                 WHEN SQLCODE NOT EQUAL ZERO                            04810020
                     DISPLAY '**************************************'   04820020
                     DISPLAY '** ABEND                            **'   04830020
                     DISPLAY '** PROGRAM = MLKUT364               **'   04840020
                     DISPLAY '** PARA = 7200-CLOSE-BRND-GRP-CSR     **' 04850020
                     DISPLAY '** CLOSE BRND_GRP_CSR                 **' 04860020
                     DISPLAY '**************************************'   04870020
                     PERFORM 9998-SQL-ABEND                             04880020
             END-EVALUATE                                               04890020
           END-PERFORM.                                                 04900020
                                                                        04910020
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                    04920020
               (SQLCODE NOT = ZERO)                                     04930020
               DISPLAY '****************************************'       04940020
               DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO **'       04950020
               DISPLAY '** CLOSE BRND_GRP_CSR                   **'     04960020
               DISPLAY '** PARA = 7200-CLOSE-BRND-GRP-CSR       **'     04970020
               DISPLAY '****************************************'       04980020
               PERFORM 9998-SQL-ABEND                                   04990020
           END-IF.                                                      05000020
                                                                        05010020
      ******************************************************************05020020
      * WRITE ADJUSTMENT TRANSACTION (MLRD254 FORMAT)                   05030020
      ******************************************************************05040020
       7300-WRITE-BRND-GRP-REC.                                         05050020
                                                                        05060020
           WRITE BRAND-GROUP-REC                                        05070020
            FROM ML364-BRND-GRP-REC.                                    05080020
           ADD 1 TO PA-TOT-RECS-WRITTEN.                                05090020
                                                                        05100020
           INITIALIZE  ML364-LBL-SEQ-NBR                                05110020
                       ML364-ENT-ID                                     05120020
                       ML364-EXP-TYP                                    05130020
                       ML364-DESC.                                      05140020
                                                                        05150020
      ******************************************************************05160020
      *  CLOSE FILES                                                    05170020
      ******************************************************************05180020
       8000-EOJ-ROUTINE.                                                05190020
                                                                        05200020
           CLOSE DATE-FILE                                              05210020
                 BRAND-GROUP-FILE.                                      05220020
                                                                        05230020
      ******************************************************************05240020
      * DISPLAY CURSOR FETCH AND OUTREC WRITTEN COUNTS                  05250020
      ******************************************************************05260020
       8100-DISPLAY-SUMMARY-STATS.                                      05270020
                                                                        05280020
           DISPLAY ' ********************************************'      05290020
           DISPLAY '     * MLKUT364 SUMMARY STATISTICS *         '      05300020
           DISPLAY ' ********************************************'      05310020
           DISPLAY ' '                                                  05320020
           DISPLAY ' TOTAL BRND GRP CSR FETCHES....:'                   05330020
                                          PA-TOT-CSR-FETCHES.           05340020
           DISPLAY ' TOTAL OUTPUT RECS WRITTEN.....:'                   05350020
                                          PA-TOT-RECS-WRITTEN.          05360020
                                                                        05370020
      ******************************************************************05380020
      *   9997-NON-SQL-ABEND   FOR NON SQL ERROR                        05390020
      ******************************************************************05400020
       9997-NON-SQL-ABEND.                                              05410020
                                                                        05420020
           DISPLAY '*********************'                              05430020
           DISPLAY '** ABEND           **'.                             05440020
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          05450020
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        05460020
           DISPLAY '** OPER MSG 1      ** = ' PV-OPERATION-MSG-1.       05470020
           DISPLAY '** OPER MSG 2      ** = ' PV-OPERATION-MSG-2.       05480020
           DISPLAY '*********************   '.                          05490020
           CALL 'ILBOABN0' USING ABEND-CODE.                            05500020
                                                                        05510020
           PERFORM 9999-ABEND.                                          05520020
                                                                        05530020
      ******************************************************************05540020
      *    SQL ERROR - ABEND                                            05550020
      ******************************************************************05560020
       9998-SQL-ABEND.                                                  05570020
                                                                        05580020
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         05590020
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        05600020
                                                                        05610020
           COPY DPPD004.                                                05620020
                                                                        05630020
           PERFORM 9999-ABEND.                                          05640020
                                                                        05650020
      ******************************************************************05660020
      *    ABEND CALL                                                   05670020
      ******************************************************************05680020
       9999-ABEND.                                                      05690020
                                                                        05700020
           MOVE +4013          TO ABEND-CODE.                           05710020
           CALL 'ILBOABN0'  USING ABEND-CODE.                           05720020
                                                                        05730020
