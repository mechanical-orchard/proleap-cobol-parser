       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP016.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           01/28/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_BUS_AREA REPORT                                00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_BUS_AREA INTO A REPORT                 00100000
      ******************************************************************00110000
      *DATE:01/28/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.                        IBM-3090.                00160000
       OBJECT-COMPUTER.                        IBM-3090.                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO RPT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS ARE STANDARD                                   00310000
           BLOCK CONTAINS 0 CHARACTERS.                                 00320000
       01  INPUT-RECORD                    PIC X(80).                   00330000
                                                                        00340000
       FD  REPORT-FILE                                                  00350000
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 CHARACTERS.                                 00380000
       01  REPORT-RECORD                   PIC X(133).                  00390000
                                                                        00400000
       WORKING-STORAGE SECTION.                                         00410000
      ******************************************************************00420000
      *SWITCHES                                                         00430000
      ******************************************************************00440000
       01  PS-PROGRAM-SWITCHES.                                         00450000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00460000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00470000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00480000
      ******************************************************************00490000
      **TIME FIELDS                                                     00500000
      ******************************************************************00510000
       01  DF-DATETIME-FIELDS.                                          00520000
           05  DF-ACCEPT-TIME.                                          00530000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00540000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00550000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00560000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00570000
           05  DF-ACCEPT-DATE.                                          00580000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00590000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00600000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00610000
      ******************************************************************00620000
      *PROGRAM CONSTANTS                                                00630000
      ******************************************************************00640000
       01  PC-PROGRAM-CONSTANTS.                                        00650000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP016'.   00660000
      ******************************************************************00670000
      **PROGRAM COUNTS                                                  00680000
      ******************************************************************00690000
       01  PC-PROGRAM-COUNTERS.                                         00700000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00710000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00720000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00730000
      ***************************************************************** 00740000
      **ABEND CODE FIELDS                                               00750000
      ***************************************************************** 00760000
       01  ABEND-CODE                     PIC S9(4)                     00770000
                                                 VALUE ZEROS COMP SYNC. 00780000
       01  PV-MISC-ABEND-FIELDS.                                        00790000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00800000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00810000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00820000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00830000
      ***************************************************************** 00840000
      **REPORT HEADINGS                                                 00850000
      ***************************************************************** 00860000
       01  REPORT-HEADING-1.                                            00870000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00880000
           05  FILLER                     PIC  X(22)                    00890000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00900000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00910000
       01  REPORT-HEADING-2.                                            00920000
           05  FILLER                     PIC  X(52)  VALUE SPACES.     00930000
           05  FILLER                     PIC  X(29)                    00940000
                                 VALUE 'XST_BUS_AREA INTEGRITY REPORT'. 00950000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00960000
       01  REPORT-HEADING-3.                                            00970000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     00980000
           05  FILLER                     PIC  X(11)                    00990000
                                 VALUE 'BUS_AREA_ID'.                   01000000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01010000
           05  FILLER                     PIC  X(13)                    01020000
                                 VALUE 'BUS_AREA_DESC'.                 01030000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01040000
       01  REPORT-HEADING-4.                                            01050000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01060000
           05  FILLER                     PIC  X(11)  VALUE ALL '-'.    01070000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01080000
           05  FILLER                     PIC  X(25) VALUE ALL '-'.     01090000
      ***************************************************************** 01100000
      **REPORT DETAIL                                                   01110000
      ***************************************************************** 01120000
       01  REPORT-DETAIL-1.                                             01130000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01140000
           05  RD-BUS-AREA-ID             PIC  Z9.                      01150000
           05  FILLER                     PIC  X(11)  VALUE SPACES.     01160000
           05  RD-BUS-AREA-DESC           PIC  X(25).                   01170000
                                                                        01180000
       01  REPORT-DETAIL-2.                                             01190000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01200000
           05  RD-VSM-BUS-AREA-ID         PIC  Z9.                      01210000
           05  FILLER                     PIC  X(11)  VALUE SPACES.     01220000
           05  RD-VSM-BUS-AREA-DESC       PIC  X(25).                   01230000
                                                                        01240000
       01  RF-END-OF-REPORT.                                            01250000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01260000
           05  FILLER                     PIC X(21)                     01270000
                                 VALUE '***END OF REPORT***'.           01280000
           05  FILLER                     PIC X(57)  VALUE SPACES.      01290000
      ***************************************************************** 01300000
      **COPYBOOKS                                                       01310000
      ***************************************************************** 01320000
      ****ABEND ROUTINE INFORMATION                                     01330000
           COPY DPWS900.                                                01340000
      ****ABEND ROUTINE INFORMATION                                     01350000
           COPY DPWS004.                                                01360034
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01370000
           COPY DPWS132O.                                               01380000
      ***INPUT RECORD                                                   01390000
           COPY XSRD002.                                                01400000
                                                                        01410000
       PROCEDURE DIVISION.                                              01420000
      ******************************************************************01430000
       0000-MAINLINE.                                                   01440000
                                                                        01450000
           PERFORM 1000-INITIALIZATION.                                 01460000
           PERFORM 2000-PROCESS-FILES                                   01470000
                   UNTIL PS-EOF-INPUT-FILE.                             01480000
           PERFORM 9999-WRAPUP.                                         01490000
           GOBACK.                                                      01500000
                                                                        01510000
       EJECT.                                                           01520000
      ******************************************************************01530000
      *INITIALIZATION ROUTINE                                           01540000
      ******************************************************************01550000
       1000-INITIALIZATION.                                             01560000
                                                                        01570000
           OPEN INPUT   INPUT-FILE                                      01580000
                OUTPUT  REPORT-FILE.                                    01590000
                                                                        01600000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01610000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01620000
                                                                        01630000
           PERFORM 1100-SETUP-HEADINGS.                                 01640000
                                                                        01650000
           PERFORM 9999-READ-INPUT-FILE.                                01660000
                                                                        01670000
       EJECT.                                                           01680000
      ******************************************************************01690000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     01700000
      ******************************************************************01710000
       1100-SETUP-HEADINGS.                                             01720000
                                                                        01730000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             01740000
                                                                        01750000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               01760000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              01770000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                01780000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               01790000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             01800000
                                                                        01810000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                01820000
           MOVE 1                TO DP132O-REPORT-NUMBER.               01830000
                                                                        01840000
           PERFORM 4000-PRINT-HEADINGS.                                 01850000
                                                                        01860000
       EJECT.                                                           01870000
      ******************************************************************01880000
      *PROCESS BOTH FILES                                               01890000
      ******************************************************************01900000
       2000-PROCESS-FILES.                                              01910000
                                                                        01920000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              01930000
                                                                        01940000
           PERFORM 4100-PRINT-DETAIL.                                   01950000
                                                                        01960000
           IF  PC-LINE-COUNT > 60                                       01970000
               PERFORM 4000-PRINT-HEADINGS                              01980000
           END-IF.                                                      01990000
                                                                        02000000
           PERFORM 9999-READ-INPUT-FILE.                                02010000
                                                                        02020000
       EJECT.                                                           02030000
      ******************************************************************02040000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            02050000
      ******************************************************************02060000
       4000-PRINT-HEADINGS.                                             02070000
                                                                        02080000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             02090000
                                                                        02100000
           INITIALIZE PC-LINE-COUNT.                                    02110000
           ADD +1                     TO PC-PAGE-COUNT.                 02120000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            02130000
                                                                        02140000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      02150000
                                                  AFTER ADVANCING PAGE. 02160000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    02170000
                                               AFTER ADVANCING 2 LINES. 02180000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    02190000
                                               AFTER ADVANCING 1 LINES. 02200000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    02210000
                                               AFTER ADVANCING 3 LINES. 02220000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    02230000
                                               AFTER ADVANCING 1 LINES. 02240000
                                                                        02250000
           ADD +7                     TO PC-LINE-COUNT.                 02260000
                                                                        02270000
       EJECT.                                                           02280000
      ******************************************************************02290000
      *PRINTS DETAIL REPORT                                             02300000
      ******************************************************************02310000
       4100-PRINT-DETAIL.                                               02320000
                                                                        02330000
           MOVE '4100-PRINT-DETAIL' TO PV-PARAGRAPH-NAME.               02340000
                                                                        02350000
           MOVE XSRD002-XS-BUS-AREA-ID TO RD-BUS-AREA-ID.               02360000
           MOVE XSRD002-XS-BUS-AREA-DESC TO RD-BUS-AREA-DESC.           02370000
           MOVE XSRD002-VSM-BUS-AREA-ID TO RD-VSM-BUS-AREA-ID.          02380000
           MOVE XSRD002-VSM-BUS-AREA-DESC TO RD-VSM-BUS-AREA-DESC.      02390000
                                                                        02400000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     02410000
                                            AFTER ADVANCING 2 LINES.    02420000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     02430000
                                            AFTER ADVANCING 1 LINES.    02440000
                                                                        02450000
           ADD +3                   TO PC-LINE-COUNT.                   02460000
                                                                        02470000
       EJECT.                                                           02480000
      ******************************************************************02490000
      *READS INPUT FILE                                                 02500000
      ******************************************************************02510000
       9999-READ-INPUT-FILE.                                            02520000
                                                                        02530000
           READ INPUT-FILE INTO XSRD002-BUSAREA-ERR-RECORD              02540000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     02550000
           END-READ.                                                    02560000
                                                                        02570000
           ADD +1  TO PC-RECORDS-READ.                                  02580000
                                                                        02590000
       EJECT.                                                           02600000
      ******************************************************************02610000
      *PROGRAM END PROCESSING                                           02620000
      ******************************************************************02630000
       9999-WRAPUP.                                                     02640000
                                                                        02650000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    02660000
                                    AFTER ADVANCING 2 LINES.            02670000
                                                                        02680000
           CLOSE INPUT-FILE                                             02690000
                 REPORT-FILE.                                           02700000
                                                                        02710000
           DISPLAY '*************************************************'. 02720000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     02730000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      02740000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      02750000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          02760000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          02770000
           DISPLAY '*************************************************'. 02780000
                                                                        02790000
       EJECT.                                                           02800000
                                                                        02810000
       EJECT.                                                           02820000
