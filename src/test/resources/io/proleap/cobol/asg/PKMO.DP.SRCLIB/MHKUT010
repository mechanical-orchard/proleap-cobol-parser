       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MHKUT010.                                                 
       AUTHOR.        JIM LEIKNESS.                                             
       DATE-WRITTEN.  OCTOBER, 2005.                                            
      *****************************************************************         
      *  MH -  MERGE DAILY EXTRACT FILES                                        
      *****************************************************************         
      *  THE PURPOSE OF THIS PROGRAM IS TO MATCH TSKST TO                       
      *  TMRITST/TMRITEM FOR THE DAILY EXTRACT OF SERVICE LEVEL                 
      *  DATA.  ONLY ACTIVE ITEM/STORES WITH AN OUT-OF-STOCK CONDITION          
      *  WILL BE OUTPUT.                                                        
      *****************************************************************         
      *  FIXES -                                                                
      *  RILEY KEHOE  05-29-2007 - IF ON-HAND QTY OR USERMIN-QTY ARE            
      *                            BOTH LESS THAN 1 MOVE PC-1 TO                
      *                            DAYS-BELOW-MINIMUM.                          
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT TSKST-UNLOAD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITST-ITEM-FILE                                                
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT SVC-LVL-EXTRACT-FILE                                          
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
       FD  TSKST-UNLOAD-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  TSKST-UNLOAD-RECORD      PIC  X(18).                                 
                                                                                
       FD  ITST-ITEM-FILE                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ITST-ITEM-RECORD         PIC  X(24).                                 
                                                                                
       FD  SVC-LVL-EXTRACT-FILE                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SVC-LVL-EXTRACT-RECORD   PIC  X(35).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-OUTPUT-RECS          PIC 9(11) VALUE ZERO.                    
           05  PV-PREV-ITEM            PIC S9(15)V COMP-3  VALUE ZERO.          
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.                 
                                                                                
           05  PV-SKU-STATUS           PIC  X(02) VALUE SPACES.                 
               88  VALID-STATUS          VALUES '20', '25', '10', '40'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-TSKST-SW         PIC  X(01) VALUE 'N'.                    
               88  PS-END-TSKST-FILE              VALUE 'Y'.                    
           05  PS-END-ITST-ITEM-SW     PIC  X(01) VALUE 'N'.                    
               88  PS-END-ITST-ITEM-FILE          VALUE 'Y'.                    
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-1                    PIC S9(3)  VALUE +1                      
                                                  COMP-3.                       
                                                                                
       01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
                                                                                
                                                                                
      *-----------------------------------------------------------------        
      *  INPUT RECORD LAYOUT FOR THE TSKST UNLOAD FILE.                         
      *-----------------------------------------------------------------        
           COPY MHRD004.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  INPUT RECORD LAYOUT FOR THE TMRITST/TMRITEM FILE.                      
      *-----------------------------------------------------------------        
           COPY MHRD005.                                                        
                                                                                
      *-----------------------------------------------------------------        
      *  OUTPUT RECORD LAYOUT FOR THE DAILY SERVICE LEVEL FILE.                 
      *-----------------------------------------------------------------        
           COPY MHRD006.                                                        
                                                                                
      *****************************************************************         
      * DB2 AREA FOR TSKXREF TABLE                                              
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 AREA FOR COMMUNICATIONS                                             
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING                    
      * SQL CODES FOR DISPLAY                                                   
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *   ERROR MESSAGE AREA FOR DB2                                            
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
               EJECT                                                            
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE.                                                           
      *-----------------------------------------------------------------        
           OPEN OUTPUT SVC-LVL-EXTRACT-FILE                                     
                 INPUT TSKST-UNLOAD-FILE                                        
                       ITST-ITEM-FILE.                                          
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS UNTIL PS-END-TSKST-FILE.                        
                                                                                
           CLOSE SVC-LVL-EXTRACT-FILE                                           
                 TSKST-UNLOAD-FILE                                              
                 ITST-ITEM-FILE.                                                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'EXTRACT RECORDS WRITTEN -- '                                
                   PV-OUTPUT-RECS.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
      *----------------------------------------------------------------*        
           PERFORM 7000-READ-TSKST-UNLOAD.                                      
                                                                                
           PERFORM 7100-READ-ITST-ITEM.                                         
                                                                                
      *----------------------------------------------------------------*        
      *  MATCH TMRITST FILE AND TSKST FILE RECORDS                              
      *----------------------------------------------------------------*        
       2000-PROCESS.                                                            
                                                                                
           IF PS-END-ITST-ITEM-FILE                                             
              MOVE 'N'  TO MH006-MR-IND                                         
                                                                                
              PERFORM 3000-VALIDATE-STATUS-OH                                   
              PERFORM 7000-READ-TSKST-UNLOAD                                    
           ELSE                                                                 
                                                                                
           IF MH004-ITEM-NMBR = MH005-ITEM-NBR                                  
              IF MH004-LOC-NBR = MH005-STR-NBR                                  
                                                                                
      ***        --- FS & MR UNLOADS MATCH (REPLENISHMENT ITEM) ---             
                 MOVE 'Y'  TO MH006-MR-IND                                      
                                                                                
                 PERFORM 3000-VALIDATE-STATUS-OH                                
                                                                                
                 PERFORM 7000-READ-TSKST-UNLOAD                                 
                 PERFORM 7100-READ-ITST-ITEM                                    
                                                                                
              ELSE                                                              
      *          --- ITEMS EQUAL, STORE NUMBERS NOT ---                         
                 IF ((MH004-LOC-NBR > MH005-STR-NBR)                            
                    AND NOT PS-END-ITST-ITEM-FILE)                              
                       PERFORM 7100-READ-ITST-ITEM                              
                 ELSE                                                           
      *             --- NO MATCH (NOT REPLENISHMENT ITEM) ---                   
                    MOVE 'N' TO MH006-MR-IND                                    
                                                                                
                    PERFORM 3000-VALIDATE-STATUS-OH                             
                    PERFORM 7000-READ-TSKST-UNLOAD                              
                 END-IF                                                         
              END-IF                                                            
                                                                                
           ELSE                                                                 
      *       --- ITEM NUMBERS DO NOT MATCH ---                                 
              IF MH004-ITEM-NMBR > MH005-ITEM-NBR                               
                 IF PS-END-ITST-ITEM-FILE                                       
                                                                                
      *             --- NO MATCH (NOT REPLENISHMENT ITEM) ---                   
                    MOVE 'N'  TO MH006-MR-IND                                   
                                                                                
                    PERFORM 3000-VALIDATE-STATUS-OH                             
                    PERFORM 7000-READ-TSKST-UNLOAD                              
                 ELSE                                                           
                    PERFORM 7100-READ-ITST-ITEM                                 
                 END-IF                                                         
              ELSE                                                              
      *          --- NO MATCH (NOT REPLENISHMENT ITEM) ---                      
                 MOVE 'N'  TO MH006-MR-IND                                      
                                                                                
                 PERFORM 3000-VALIDATE-STATUS-OH                                
                 PERFORM 7000-READ-TSKST-UNLOAD                                 
              END-IF                                                            
           END-IF                                                               
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    CHECK FOR ACTIVE SKU STATUS; SET OUT-OF-STOCK INDICATORS             
      *    BASED ON ONHAND; WRITE OUT RECORD IF OUT OF STOCK                    
      *----------------------------------------------------------------*        
       3000-VALIDATE-STATUS-OH.                                                 
                                                                                
           IF MH004-ITEM-NMBR NOT = PV-PREV-ITEM                                
              PERFORM 7500-GET-STATUS                                           
                                                                                
              MOVE MH004-ITEM-NMBR TO PV-PREV-ITEM                              
           END-IF.                                                              
                                                                                
           IF VALID-STATUS                                                      
              PERFORM 3200-SET-INDICATORS                                       
                                                                                
              IF (MH006-DAYS-OUT-STOCK = PC-1) OR                               
                 (MH006-DAYS-BELOW-MIN = PC-1)                                  
                  PERFORM 8000-WRITE-EXTRACT                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    SET OUT-OF-STOCK INDICATORS BASED ON WHETHER IT'S A                  
      *    REPLENISHMENT ITEM, AND FT CODE IF IT IS                             
      *----------------------------------------------------------------*        
       3200-SET-INDICATORS.                                                     
           MOVE ZERO TO MH006-DAYS-OUT-STOCK                                    
                        MH006-DAYS-BELOW-MIN.                                   
                                                                                
           IF MH006-MR-IND EQUAL 'N'                                            
              IF MH004-ONHAND-QTY < PC-1                                        
                 MOVE PC-1 TO MH006-DAYS-OUT-STOCK                              
                              MH006-DAYS-BELOW-MIN                              
              END-IF                                                            
                                                                                
           ELSE                                                                 
      *       ----- REPLENISHMENT ITEM -----                                    
              IF MH004-ONHAND-QTY < PC-1                                        
                 MOVE PC-1 TO MH006-DAYS-OUT-STOCK                              
              END-IF                                                            
                                                                                
              IF MH005-FT-CDE = 'O'                                             
                 IF MH004-ONHAND-QTY < MH005-CSTOCK-QTY OR                      
                    MH004-ONHAND-QTY < PC-1                                     
                    MOVE PC-1 TO MH006-DAYS-BELOW-MIN                           
                 END-IF                                                         
                                                                                
              ELSE                                                              
      *          ----- FT CODE = U -----                                        
                 IF MH004-ONHAND-QTY < MH005-USERMIN-QTY OR                     
                    MH004-ONHAND-QTY < PC-1                                     
                    MOVE PC-1 TO MH006-DAYS-BELOW-MIN                           
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       7000-READ-TSKST-UNLOAD.                                                  
      *-----------------------------------------------------------------        
           READ TSKST-UNLOAD-FILE                                               
               INTO MH004-TSKST-RECORD                                          
             AT END                                                             
                SET PS-END-TSKST-FILE TO TRUE.                                  
                                                                                
      *-----------------------------------------------------------------        
       7100-READ-ITST-ITEM.                                                     
      *-----------------------------------------------------------------        
           READ ITST-ITEM-FILE                                                  
               INTO MH005-MR-UNLOAD-RECORD                                      
             AT END                                                             
                SET PS-END-ITST-ITEM-FILE TO TRUE.                              
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       7500-GET-STATUS.                                                         
      *-----------------------------------------------------------------        
                                                                                
           EXEC SQL                                                             
             SELECT SKU_STAT_CDE                                                
               INTO :SKXREF-SKU-STAT-CDE                                        
               FROM TSKXREF                                                     
              WHERE ITM_NBR = :MH004-ITEM-NMBR                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                  MOVE SKXREF-SKU-STAT-CDE TO PV-SKU-STATUS                     
               WHEN SQLCODE = +100                                              
                  MOVE ZERO TO PV-SKU-STATUS                                    
                  DISPLAY 'ITEM: ' MH004-ITEM-NMBR                              
                          ' NOT FOUND ON TSKXREF--RECORDS BYPASSED'             
               WHEN OTHER                                                       
                  MOVE '7500-GET-STATUS' TO PV-PARAGRAPH-NAME                   
                  MOVE 'SELECT TSKXREF'  TO PV-DB2-OPER-ATTEMPTED               
                  PERFORM Z999-DB2-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       8000-WRITE-EXTRACT.                                                      
      *-----------------------------------------------------------------        
                                                                                
           MOVE MH004-ITEM-NMBR    TO MH006-ITEM-NBR.                           
           MOVE MH004-LOC-NBR      TO MH006-LOC-NBR.                            
           MOVE MH004-ONHAND-QTY   TO MH006-ONHAND-QTY.                         
           MOVE PV-SKU-STATUS      TO MH006-SKU-STATUS.                         
                                                                                
           IF MH006-MR-IND EQUAL 'N'                                            
              MOVE SPACE  TO MH006-FT-CDE                                       
              MOVE    1   TO MH006-CSTOCK-QTY                                   
              MOVE    1   TO MH006-USERMIN-QTY                                  
           ELSE                                                                 
              MOVE MH005-FT-CDE       TO MH006-FT-CDE                           
              MOVE MH005-CSTOCK-QTY   TO MH006-CSTOCK-QTY                       
              MOVE MH005-USERMIN-QTY  TO MH006-USERMIN-QTY                      
           END-IF.                                                              
                                                                                
           WRITE SVC-LVL-EXTRACT-RECORD                                         
               FROM MH006-SVC-LVL-DAILY-RECORD.                                 
                                                                                
           INITIALIZE SVC-LVL-EXTRACT-RECORD.                                   
           INITIALIZE MH006-SVC-LVL-DAILY-RECORD.                               
           ADD +1 TO PV-OUTPUT-RECS.                                            
                                                                                
                                                                                
      *****************************************************************         
       Z999-DB2-ABEND.                                                          
      *                                                                         
           SET ABEND-4013-DB2-ERROR TO TRUE.                                    
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = MHKUT010'.                              
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
