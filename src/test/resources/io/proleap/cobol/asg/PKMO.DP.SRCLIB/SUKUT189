000100*TITLE 'EXTRACT MANUAL AUDIT INFO PER CONTROL DATES'.                     
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    SUKUT189.                                                 
000400 AUTHOR.        SIVARAMAKRISHNAN.                                         
000500 DATE-WRITTEN.  7-7-2017.                                                 
000600 ENVIRONMENT DIVISION.                                                    
000700 INPUT-OUTPUT SECTION.                                                    
000800                                                                          
000900******************************************************************        
001000                                                                          
001100* THIS PROGRAM EXTRACT MANUAL AUDIT INFO PER CONTROL DATES.               
001200******************************************************************        
001300 FILE-CONTROL.                                                            
001400     SELECT AUDIT-EXTRACT-FILE      ASSIGN TO OUT01.                      
001500                                                                          
001600 DATA DIVISION.                                                           
001700 FILE SECTION.                                                            
001800                                                                          
001900 FD  AUDIT-EXTRACT-FILE                                                   
002000     RECORDING MODE IS F                                                  
002100     LABEL RECORDS ARE STANDARD                                           
002200     RECORD CONTAINS 232                                                  
002300     BLOCK CONTAINS 0 RECORDS                                             
002400     DATA RECORD IS AUDIT-EXTRACT-RECORD.                                 
002500 01  AUDIT-EXTRACT-RECORD             PIC X(232).                         
002600                                                                          
002700 WORKING-STORAGE SECTION.                                                 
002800                                                                          
002900 01  PV-ABEND-CODE                    PIC S9(04)   VALUE ZERO             
003000                                                   COMP SYNC.             
003100                                                                          
003200 01  PS-PROGRAM-SWITCHES.                                                 
003300     05  PS-END-CSR-SW                PIC X(1)   VALUE 'N'.               
003400         88  PS-END-CSR                          VALUE 'Y'.               
003500                                                                          
003600                                                                          
003700 01  PV-PROGRAM-VARIABLES.                                                
003800     05  PV-SQLCODE                   PIC 9(9)   VALUE ZERO.              
003900     05  PV-DISP-NBR                  PIC Z(8)9  VALUE ZERO.              
004000     05  PV-WEEK-END-DATE             PIC X(06)  VALUE SPACE.             
004100     05  PV-VENDOR-ID                 PIC X(10)  VALUE SPACE.             
004200     05  PV-PO-NBR                    PIC X(09)  VALUE SPACE.             
004300     05  PV-DEPT-NBR                  PIC X(10)  VALUE SPACE.             
004400     05  PV-RCVR-SEQ-NBR              PIC X(10)  VALUE SPACE.             
004500     05  PV-PO-TYP-CDE                PIC X(02)  VALUE SPACE.             
004600     05  PV-PKGNG-TYPE                PIC X(02)  VALUE SPACE.             
004700     05  PV-VENDOR-AUDIT-RANK         PIC X(06)  VALUE SPACE.             
004800     05  PV-AUD-CNTL-NBR              PIC X(13)  VALUE SPACE.             
004900     05  PV-AUD-STRT-TMST             PIC X(26)  VALUE SPACE.             
005000     05  PV-CRTNS-AUDITED             PIC X(13)  VALUE SPACE.             
005100     05  PV-CRTNS-W-PICK-ERR          PIC X(13)  VALUE SPACE.             
005200     05  PV-UNITS-AUDITED             PIC X(13)  VALUE SPACE.             
005300     05  PV-UNITS-PICK-ERR            PIC X(13)  VALUE SPACE.             
005400     05  PV-UNITS-PACK-ERR            PIC X(13)  VALUE SPACE.             
005500     05  PV-PACKS-AUDITED             PIC X(13)  VALUE SPACE.             
005600     05  PV-PACKS-W-PACK-ERR          PIC X(13)  VALUE SPACE.             
005700     05  PV-FAILURE-ERROR-SW          PIC X(01)  VALUE SPACE.             
005800     05  PV-AUD-STAT-CDE              PIC X(01)  VALUE SPACE.             
005900     05  PV-UNITS-TICK-ERR            PIC X(13)  VALUE SPACE.             
006000     05  PV-ANCHOR-CONSTANT           PIC X(01)  VALUE  'X'.              
006100                                                                          
006200 01  PV-COUNTERS COMP-3.                                                  
006300     05  PV-WRITE-COUNT               PIC S9(9)    VALUE ZERO.            
006400     05  PV-FETCH-COUNT               PIC S9(9)    VALUE ZERO.            
006500     05  PV-COUNT                     PIC S9(2)    VALUE ZERO.            
006600     05  PV-COUNT1                    PIC S9(2)    VALUE ZERO.            
006700                                                                          
006800 01  PV-ABEND-FIELDS.                                                     
006900     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'SUKUT189'.        
007000     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
007100     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
007200     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
007300     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
007400     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
007500/                                                                         
007600*    AUDIT-EXTRACT RECORD                                                 
007700     COPY SURD159.                                                        
007800/                                                                         
007900*    SUT_PO_AUD_REQ                                                       
008000     EXEC SQL                                                             
008100         INCLUDE SUT00015                                                 
008200     END-EXEC                                                             
008300                                                                          
008400*    SHIP UNIT AUDITS                                                     
008500     EXEC SQL                                                             
008600         INCLUDE TSUNAUD                                                  
008700     END-EXEC                                                             
008800                                                                          
008900     EXEC SQL                                                             
009000         INCLUDE TSUATRN                                                  
009100     END-EXEC                                                             
009200                                                                          
009300     EXEC SQL                                                             
009400         INCLUDE TDTATTR                                                  
009500     END-EXEC                                                             
009600                                                                          
009700     EXEC SQL                                                             
009800         INCLUDE TPURCHS                                                  
009900     END-EXEC                                                             
010000                                                                          
010100     EXEC SQL                                                             
010200         INCLUDE XLT00010                                                 
010300     END-EXEC                                                             
010400                                                                          
010500     EXEC SQL                                                             
010600         INCLUDE TCOCNTL                                                  
010700     END-EXEC                                                             
010800/                                                                         
010900     EXEC SQL                                                             
011000         DECLARE AUDIT-CSR CURSOR FOR                                     
011100           SELECT DISTINCT CONCAT(C.FSCL_YR_NBR,C.FSCL_MN_NBR)            
011200                 ,RPAD(CHAR(E.ENT_ID),10,' ')                             
011300                 ,RPAD(CHAR(D.TRN_NBR),20,' ')                            
011400                 ,CHAR(E.DEPT_NBR,10)                                     
011500                 ,RPAD(CHAR(D.REC_SEQ_NBR),10,' ')                        
011600                 ,CHAR(E.PO_TYP_CDE,10)                                   
011700                 ,CHAR(CASE E.PKGNG_CDE                                   
011800                            WHEN 'B' THEN 'BC'                            
011900                            WHEN 'C' THEN 'BC'                            
012000                            WHEN ' ' THEN 'PS'                            
012100                            WHEN 'I' THEN 'PP'                            
012200                            WHEN 'M' THEN 'PP'                            
012300                                                                          
012400                       END,10)                                            
012500                 , '          '                                           
012600                 , LPAD( DIGITS( A.AUD_CNTL_NBR ), 13, '0')               
012700                 , A.AUD_STP_TMST                                         
012800                 , '0000000000000'                                        
012900                 , '0000000000000'                                        
013000                 , '0000000000000'                                        
013100                 , '0000000000000'                                        
013200                 , '0000000000000'                                        
013300                 , '0000000000000'                                        
013400                 , '0000000000000'                                        
013500                 , 'N'                                                    
013600                 , 'P'                                                    
013700                 , '0000000000000'                                        
013800                 , 'X'                                                    
013900            FROM TSUNAUD A                                                
014000               , XLT_LOC B                                                
014100               , TDTATTR C                                                
014200               , TSUATRN D                                                
014300               , TPURCHS E                                                
014400           WHERE DATE(A.AUD_STP_TMST) IN                                  
014500                 (SELECT BTCH_PRC_DTE                                     
014600                    FROM TCOCNTL                                          
014700                   WHERE OPER_UNT_ID = 90)                                
014800             AND A.AUD_CAT_CDE = 'C'                                      
014900             AND B.LOC_NBR = A.OPER_UNT_ID                                
015000             AND B.LOC_TYP_CDE = 'DC'                                     
015100             AND NOT EXISTS                                               
015200                 (SELECT *                                                
015300                    FROM SUT_PO_AUD_REQ AA                                
015400                   WHERE AA.AUD_CNTL_NBR = A.AUD_CNTL_NBR)                
015500             AND C.KY_DTE = DATE(A.AUD_STP_TMST)                          
015600             AND D.AUD_CNTL_NBR = A.AUD_CNTL_NBR                          
015700             AND E.PO_NBR = D.TRN_NBR                                     
015800             AND E.VER_STATUS_CDE = 'A'                                   
015900             WITH UR                                                      
016000     END-EXEC.                                                            
016100                                                                          
016200*  USED FOR DB2 ERROR MESSAGE BUILDING                                    
016300     COPY DPWS004.                                                        
016400                                                                          
016500*   DB2 SQL COMMUNICATION AREA                                            
016600     EXEC SQL                                                             
016700          INCLUDE SQLCA                                                   
016800     END-EXEC.                                                            
016900/                                                                         
017000 PROCEDURE DIVISION.                                                      
017100                                                                          
017200     PERFORM 1000-INIT.                                                   
017300                                                                          
017400     PERFORM 2000-PROCESS UNTIL PS-END-CSR.                               
017500                                                                          
017600     PERFORM 9000-TERMINATE.                                              
017700                                                                          
017800     GOBACK.                                                              
017900/                                                                         
018000 1000-INIT.                                                               
018100                                                                          
018200     OPEN OUTPUT AUDIT-EXTRACT-FILE                                       
018300                                                                          
018400     PERFORM 7000-OPEN-AUDIT-CSR.                                         
018500     PERFORM 7010-FETCH-AUDIT-CSR.                                        
018600/                                                                         
018700 2000-PROCESS.                                                            
018800                                                                          
018900     PERFORM 3000-BUILD-RECORDS.                                          
019000                                                                          
019100     PERFORM 8100-WRITE-EXTRACT-REC.                                      
019200                                                                          
019300     PERFORM 7010-FETCH-AUDIT-CSR.                                        
019400/                                                                         
019500 3000-BUILD-RECORDS.                                                      
019600                                                                          
019700     INITIALIZE PV-COUNT                                                  
019800                PV-COUNT1.                                                
019900                                                                          
020000     MOVE PV-WEEK-END-DATE     TO SU159-WEEK-END-DATE                     
020100     MOVE PV-VENDOR-ID         TO SU159-VENDOR-ID                         
020200                                                                          
020300     INSPECT FUNCTION REVERSE(PV-PO-NBR) TALLYING                         
020400             PV-COUNT FOR LEADING SPACES.                                 
020500     COMPUTE PV-COUNT1 = LENGTH OF PV-PO-NBR - PV-COUNT.                  
020600     MOVE PV-PO-NBR(1:PV-COUNT1) TO SU159-PO-NBR                          
020700                                                                          
020800     MOVE PV-DEPT-NBR          TO SU159-DEPT-NBR                          
020900     MOVE PV-RCVR-SEQ-NBR      TO SU159-RCVR-SEQ-NBR                      
021000     MOVE PV-PO-TYP-CDE        TO SU159-PO-TYP-CDE                        
021100     MOVE PV-PKGNG-TYPE        TO SU159-PKGNG-TYPE                        
021200     MOVE PV-VENDOR-AUDIT-RANK TO SU159-VENDOR-AUDIT-RANK                 
021300     MOVE PV-AUD-CNTL-NBR      TO SU159-AUD-CNTL-NBR                      
021400     MOVE PV-AUD-STRT-TMST     TO SU159-AUD-STP-TMST                      
021500     MOVE PV-CRTNS-AUDITED     TO SU159-CRTNS-AUDITED                     
021600     MOVE PV-CRTNS-W-PICK-ERR  TO SU159-CRTNS-W-PICK-ERR                  
021700     MOVE PV-UNITS-AUDITED     TO SU159-UNITS-AUDITED                     
021800     MOVE PV-UNITS-PICK-ERR    TO SU159-UNITS-PICK-ERR                    
021900     MOVE PV-UNITS-PACK-ERR    TO SU159-UNITS-PACK-ERR                    
022000     MOVE PV-PACKS-AUDITED     TO SU159-PACKS-AUDITED                     
022100     MOVE PV-PACKS-W-PACK-ERR  TO SU159-PACKS-W-PACK-ERR                  
022200     MOVE PV-FAILURE-ERROR-SW  TO SU159-FAILURE-ERROR-SW                  
022300     MOVE PV-AUD-STAT-CDE      TO SU159-AUD-STAT-CDE                      
022400     MOVE PV-UNITS-TICK-ERR    TO SU159-UNITS-TICK-ERR                    
022500     MOVE PV-ANCHOR-CONSTANT   TO SU159-ANCHOR-CONSTANT.                  
022600/                                                                         
022700 7000-OPEN-AUDIT-CSR.                                                     
022800     EXEC SQL                                                             
022900         OPEN AUDIT-CSR                                                   
023000     END-EXEC.                                                            
023100                                                                          
023200     EVALUATE TRUE                                                        
023300         WHEN SQLCODE = ZERO                                              
023400             CONTINUE                                                     
023500         WHEN SQLWARN0 NOT = SPACE                                        
023600         WHEN SQLCODE  NOT = ZERO                                         
023700             MOVE '7000-OPEN-AUDIT-CSR'                                   
023800                                       TO PV-ABEND-PARAGRAPH              
023900             MOVE 'OPEN'               TO PV-ABEND-OPERATION              
024000             PERFORM 9900-DB2-ABEND                                       
024100     END-EVALUATE.                                                        
024200/                                                                         
024300 7010-FETCH-AUDIT-CSR.                                                    
024400     EXEC SQL                                                             
024500         FETCH AUDIT-CSR                                                  
024600         INTO                                                             
024700             :PV-WEEK-END-DATE                                            
024800            ,:PV-VENDOR-ID                                                
024900            ,:PV-PO-NBR                                                   
025000            ,:PV-DEPT-NBR                                                 
025100            ,:PV-RCVR-SEQ-NBR                                             
025200            ,:PV-PO-TYP-CDE                                               
025300            ,:PV-PKGNG-TYPE                                               
025400            ,:PV-VENDOR-AUDIT-RANK                                        
025500            ,:PV-AUD-CNTL-NBR                                             
025600            ,:PV-AUD-STRT-TMST                                            
025700            ,:PV-CRTNS-AUDITED                                            
025800            ,:PV-CRTNS-W-PICK-ERR                                         
025900            ,:PV-UNITS-AUDITED                                            
026000            ,:PV-UNITS-PICK-ERR                                           
026100            ,:PV-UNITS-PACK-ERR                                           
026200            ,:PV-PACKS-AUDITED                                            
026300            ,:PV-PACKS-W-PACK-ERR                                         
026400            ,:PV-FAILURE-ERROR-SW                                         
026500            ,:PV-AUD-STAT-CDE                                             
026600            ,:PV-UNITS-TICK-ERR                                           
026700            ,:PV-ANCHOR-CONSTANT                                          
026800     END-EXEC.                                                            
026900                                                                          
027000     EVALUATE TRUE                                                        
027100         WHEN SQLCODE = ZERO                                              
027200             ADD 1                    TO PV-FETCH-COUNT                   
027300             CONTINUE                                                     
027400         WHEN SQLCODE = +100                                              
027500             SET PS-END-CSR           TO TRUE                             
027600         WHEN SQLWARN0 NOT = SPACE                                        
027700         WHEN SQLCODE  NOT = ZERO                                         
027800             MOVE '7010-FETCH-AUDIT-CSR'                                  
027900                                       TO PV-ABEND-PARAGRAPH              
028000             MOVE 'FETCH'              TO PV-ABEND-OPERATION              
028100             PERFORM 9900-DB2-ABEND                                       
028200     END-EVALUATE.                                                        
028300/                                                                         
028400 7020-CLOSE-AUDIT-CSR.                                                    
028500     EXEC SQL                                                             
028600         CLOSE AUDIT-CSR                                                  
028700     END-EXEC.                                                            
028800                                                                          
028900     EVALUATE TRUE                                                        
029000         WHEN SQLCODE = ZERO                                              
029100             CONTINUE                                                     
029200         WHEN SQLWARN0 NOT = SPACE                                        
029300         WHEN SQLCODE  NOT = ZERO                                         
029400             MOVE '7020-CLOSE-AUDIT-CSR'                                  
029500                                       TO PV-ABEND-PARAGRAPH              
029600             MOVE 'CLOSE'              TO PV-ABEND-OPERATION              
029700             PERFORM 9900-DB2-ABEND                                       
029800     END-EVALUATE.                                                        
029900/                                                                         
030000 8100-WRITE-EXTRACT-REC.                                                  
030100                                                                          
030200     WRITE AUDIT-EXTRACT-RECORD FROM SU159-RECORD.                        
030300                                                                          
030400     ADD 1                         TO PV-WRITE-COUNT.                     
030500/                                                                         
030600 9000-TERMINATE.                                                          
030700                                                                          
030800     PERFORM 7020-CLOSE-AUDIT-CSR.                                        
030900                                                                          
031000     CLOSE AUDIT-EXTRACT-FILE                                             
031100                                                                          
031200     DISPLAY '************************'.                                  
031300     DISPLAY '******* SUKUT189 *******'.                                  
031400     DISPLAY '************************'.                                  
031500     DISPLAY SPACES                                                       
031600                                                                          
031700     MOVE PV-FETCH-COUNT              TO PV-DISP-NBR                      
031800     DISPLAY 'EXTRACT RECS  FETCHED      = ' PV-DISP-NBR                  
031900     DISPLAY SPACES                                                       
032000     DISPLAY '************************'.                                  
032100                                                                          
032200     MOVE PV-WRITE-COUNT              TO PV-DISP-NBR                      
032300     DISPLAY 'EXTRACT RECS  WRITTEN      = ' PV-DISP-NBR                  
032400     DISPLAY SPACES                                                       
032500     DISPLAY '************************'.                                  
032600/                                                                         
032700 9900-DB2-ABEND.                                                          
032800                                                                          
032900     MOVE SQLCODE TO PV-SQLCODE.                                          
033000     DISPLAY '*** DB2 ERROR '.                                            
033100     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
033200     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
033300     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
033400     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
033500     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
033600     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
033700     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
033800                                                                          
033900     COPY DPPD004.                                                        
034000                                                                          
034100     MOVE +4013                  TO PV-ABEND-CODE.                        
034200     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
034300                                                                          
