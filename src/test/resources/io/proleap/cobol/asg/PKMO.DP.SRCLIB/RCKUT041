000100 IDENTIFICATION DIVISION.                                         RCK00187
000200 PROGRAM-ID.    RCKUT041.                                            CL**2
000300 AUTHOR.        DENNIS STEFFEN - OMNI RESOURCES.                     CL**2
000400 INSTALLATION.  KOHLS.                                               CL**2
000500 DATE-WRITTEN   MARCH, 1996.                                         CL**2
000600 DATE-COMPILED.                                                      CL**2
000700                                                                     CL**2
000800******************************************************************   CL**2
000900*RCKUT041 - THIS PROGRAM READS IN THE RECEIPT DISTRIBUTION AUDIT  12/28/95
001000*           FILE AND WRITES A SUMMARY RECORD FOR EACH BEFORE AND  RCK00191
001100*           AFTER PAIR.  THE INPUT MUST BE IN THIS SEQUENCE:         LV001
001200*                                                                 RCK00191
001300*    ORDER NUMBER, RECEIVER SEQUENCE, ORDER LINE, STORE,          RCK00191
001400*    NOTIFICATION NUMBER, UPDATE TIMESTAMP (ASCENDING) AND        RCK00191
001500*    AUDIT TYPE CODE (DESCENDING).                                RCK00191
001600*                                                                 RCK00191
001700******************************************************************   CL**2
001800                                                                     CL**2
001900******************************************************************   CL**2
002000 ENVIRONMENT DIVISION.                                               CL**2
002100******************************************************************   CL**2
002200                                                                     CL**2
002300 CONFIGURATION SECTION.                                              CL**2
002400                                                                     CL**2
002500 SOURCE-COMPUTER.        IBM-3090.                                   CL**2
002600 OBJECT-COMPUTER.        IBM-3090.                                   CL**2
002700                                                                     CL**2
002800 INPUT-OUTPUT SECTION.                                               CL**2
002900                                                                     CL**2
003000 FILE-CONTROL.                                                       CL**2
003100     SELECT RECEIPT-DIST-AUDIT-FILE                                  CL**3
003200         ASSIGN TO IN01.                                             CL**2
003300                                                                     CL**2
003400     SELECT RECEIPT-AUDIT-SUM-FILE                                   CL**2
003500         ASSIGN TO OUT01.                                            CL**9
003600                                                                     CL**2
003700******************************************************************   CL**2
003800 DATA DIVISION.                                                      CL**2
003900******************************************************************   CL**2
004000                                                                     CL**2
004100 FILE SECTION.                                                       CL**2
004200                                                                     CL**2
004300 FD  RECEIPT-DIST-AUDIT-FILE                                         CL**2
004400     RECORDING MODE IS F                                             CL**2
004500     LABEL RECORDS ARE STANDARD                                      CL**2
004600     BLOCK CONTAINS 0 RECORDS.                                       CL**2
004700                                                                     CL**2
004800 01  RECEIPT-DIST-AUDIT-RECORD  PIC  X(175).                         CL**7
004900                                                                     CL**2
005000 FD  RECEIPT-AUDIT-SUM-FILE                                          CL**2
005100     RECORDING MODE IS F                                             CL**2
005200     LABEL RECORDS ARE STANDARD                                      CL**2
005300     BLOCK CONTAINS 0 RECORDS.                                       CL**2
005400                                                                     CL**2
005500 01  RECEIPT-AUDIT-SUM-RECORD  PIC  X(135).                          CL**7
005600*                                                                    CL**2
005700*                                                                    CL**2
005800******************************************************************   CL**2
005900 WORKING-STORAGE SECTION.                                            CL**2
006000******************************************************************   CL**2
006100*                                                                    CL**2
006200 01  PS-PROGRAM-SWITCHES.                                            CL**2
006300     05  PS-EOF-RECEIPT-FILE-SW      PIC  X(01)    VALUE SPACE.      CL**3
006400         88  PS-EOF-RECEIPT-FILE                   VALUE 'Y'.        CL**3
006500     05  PS-SEQUENCE-SW              PIC  X(01)    VALUE SPACE.      CL**3
006600         88  PS-SEQUENCE-ERROR                     VALUE 'Y'.        CL**3
006700                                                                     CL**2
006800 01  ABEND-CODE                      PIC S9(04)    VALUE ZERO.       CL**2
006900                                                                     CL**2
007000 01  PV-PROGRAM-VARIABLES.                                           CL**2
007100     05  PV-SUM                      PIC  S9(9) COMP VALUE ZERO.  RCR00007
007200     05  PV-READ-COUNTER             PIC S9(07)    VALUE ZERO        CL**2
007300                                                   COMP-3.           CL**5
007400     05  PV-WRITE-COUNTER            PIC S9(07)    VALUE ZERO        CL**5
007500                                                   COMP-3.           CL**5
007600     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                CL**5
007700     05  PV-DISP-PO                  PIC 9(08)     VALUE ZERO.       CL**2
007800     05  PV-DISP-SEQ-NBR             PIC 9(03)     VALUE ZERO.       CL**3
007900     05  PV-DISP-LNE-NBR             PIC 9(03)     VALUE ZERO.       CL**2
008000     05  PV-DISP-STR-NBR             PIC 9(04)     VALUE ZERO.       CL**2
008100     EJECT                                                                
008200********************************************************             CL**2
008300* THIS IS THE FIRST RECORD OF A PAIR FROM THE INPUT FILE             CL**2
008400* IN THE SAME FORMAT AS RCRD014                                      CL**2
008500********************************************************             CL**2
008600*                                                                    CL**2
008700 01  PV001-TRECDIS-AUDIT-RECORD.                                  RCR00007
008800     05  PV001-ORD-NBR              PIC  S9(9) COMP.              RCR00007
008900     05  PV001-ORD-LNE-NBR          PIC  S9(9) COMP.              RCR00007
009000     05  PV001-REC-SEQ-NBR          PIC  S9(9) COMP.              RCR00007
009100     05  PV001-OPER-UNT-ID          PIC  S9(4) COMP.              RCR00007
009200     05  PV001-FCLTY-ID             PIC  S9(4) COMP.              RCR00007
009300     05  PV001-CRE-TMST             PIC   X(26).                  RCR00007
009400     05  PV001-CRE-UID              PIC   X(8).                   RCR00007
009500     05  PV001-VER-STATUS-CDE       PIC   X(1).                   RCR00007
009600     05  PV001-EXPTED-ITM-QTY       PIC  S9(9) COMP.              RCR00007
009700     05  PV001-VERIFY-ITM-QTY       PIC  S9(9) COMP.              RCR00007
009800     05  PV001-AP-VERIFY-ITM-QTY    PIC  S9(9) COMP.              RCR00007
009900     05  PV001-STR-NBR              PIC  S9(4) COMP.              RCR00007
010000     05  PV001-STR-FCLTY-ID         PIC  S9(4) COMP.              RCR00007
010100     05  PV001-PLAN-ITM-QTY         PIC  S9(4) COMP.              RCR00007
010200     05  PV001-PKSL-NBR             PIC   X(10).                  RCR00007
010300     05  PV001-AP-PRC-CDE           PIC   X(1).                   RCR00007
010400     05  PV001-AP-PRC-DTE           PIC   X(10).                  RCR00007
010500     05  PV001-UPD-TMST             PIC   X(26).                     CL**3
010600     05  PV001-UPD-UID              PIC   X(8).                      CL**3
010700     05  PV001-UPD-PGM-ID           PIC   X(8).                      CL**3
010800     05  PV001-AUD-TYP-CDE          PIC   X(1).                      CL**3
010900         88  PV001-AUD-TYP-BEFORE                    VALUE 'B'.      CL**3
011000         88  PV001-AUD-TYP-AFTER                     VALUE 'A'.      CL**3
011100     05  PV001-FUNC-CDE             PIC   X(1).                      CL**3
011200         88  PV001-FUNC-INSERT                       VALUE 'I'.      CL**3
011300         88  PV001-FUNC-UPDATE                       VALUE 'U'.      CL**3
011400         88  PV001-FUNC-DELETE                       VALUE 'D'.      CL**3
011500     05  PV001-ROW-UNLD-SW          PIC   X(1).                      CL**7
011600         88  PV001-ROW-UNLOADED                      VALUE 'U'.      CL**7
011700     05  PV001-NOTIF-NBR            PIC  S9(9) COMP.                 CL**5
011701     05  RC001-ADJ-CYC-NBR          PIC  S9(9) COMP.                 CL**5
011702     05  FILLER                     PIC   X(31)      VALUE SPACES.   CL**7
011900*                                                                    CL**2
012000********************************************************             CL**2
012100* THIS IS THE SECOND RECORD OF A PAIR FROM THE INPUT FILE            CL**2
012200* IN THE SAME FORMAT AS RCRD014                                      CL**2
012300********************************************************             CL**2
012400*                                                                    CL**2
012500 01  PV002-TRECDIS-AUDIT-RECORD.                                  RCR00007
012600     05  PV002-ORD-NBR              PIC  S9(9) COMP.              RCR00007
012700     05  PV002-ORD-LNE-NBR          PIC  S9(9) COMP.              RCR00007
012800     05  PV002-REC-SEQ-NBR          PIC  S9(9) COMP.              RCR00007
012900     05  PV002-OPER-UNT-ID          PIC  S9(4) COMP.              RCR00007
013000     05  PV002-FCLTY-ID             PIC  S9(4) COMP.              RCR00007
013100     05  PV002-CRE-TMST             PIC   X(26).                  RCR00007
013200     05  PV002-CRE-UID              PIC   X(8).                   RCR00007
013300     05  PV002-VER-STATUS-CDE       PIC   X(1).                   RCR00007
013400     05  PV002-EXPTED-ITM-QTY       PIC  S9(9) COMP.              RCR00007
013500     05  PV002-VERIFY-ITM-QTY       PIC  S9(9) COMP.              RCR00007
013600     05  PV002-AP-VERIFY-ITM-QTY    PIC  S9(9) COMP.              RCR00007
013700     05  PV002-STR-NBR              PIC  S9(4) COMP.              RCR00007
013800     05  PV002-STR-FCLTY-ID         PIC  S9(4) COMP.              RCR00007
013900     05  PV002-PLAN-ITM-QTY         PIC  S9(4) COMP.              RCR00007
014000     05  PV002-PKSL-NBR             PIC   X(10).                  RCR00007
014100     05  PV002-AP-PRC-CDE           PIC   X(1).                   RCR00007
014200     05  PV002-AP-PRC-DTE           PIC   X(10).                  RCR00007
014300     05  PV002-UPD-TMST             PIC   X(26).                     CL**3
014400     05  PV002-UPD-UID              PIC   X(8).                      CL**3
014500     05  PV002-UPD-PGM-ID           PIC   X(8).                      CL**3
014600     05  PV002-AUD-TYP-CDE          PIC   X(1).                      CL**3
014700         88  PV002-AUD-TYP-BEFORE                    VALUE 'B'.      CL**3
014800         88  PV002-AUD-TYP-AFTER                     VALUE 'A'.      CL**3
014900     05  PV002-FUNC-CDE             PIC   X(1).                      CL**3
015000         88  PV002-FUNC-INSERT                       VALUE 'I'.      CL**3
015100         88  PV002-FUNC-UPDATE                       VALUE 'U'.      CL**3
015200         88  PV002-FUNC-DELETE                       VALUE 'D'.      CL**3
015300     05  PV002-ROW-UNLD-SW          PIC   X(1).                      CL**7
015400         88  PV002-ROW-UNLOADED                      VALUE 'U'.      CL**7
015500     05  PV002-NOTIF-NBR            PIC  S9(9) COMP.                 CL**5
015501     05  RC002-ADJ-CYC-NBR          PIC  S9(9) COMP.                 CL**5
015502     05  FILLER                     PIC   X(31)      VALUE SPACES.   CL**7
015700     EJECT                                                                
015800     COPY RCRD015.                                                   CL**2
015900     EJECT                                                           CL**2
016000 PROCEDURE DIVISION.                                                 CL**2
016100                                                                     CL**2
016200 0000-PROGRAM-MAINLINE.                                              CL**2
016300                                                                     CL**2
016400     PERFORM 1000-INITIALIZE.                                        CL**2
016500                                                                          
016600     PERFORM 2000-PROCESS-AUDIT-RECPT-DIST                           CL**3
016700             UNTIL PS-SEQUENCE-ERROR OR PS-EOF-RECEIPT-FILE          CL**3
016800                                                                          
016900     PERFORM 9000-EOJ-ROUTINE.                                       CL**2
017000                                                                     CL**2
017100     STOP RUN.                                                       CL**2
017200                                                                          
017300 1000-INITIALIZE.                                                    CL**2
017400                                                                     CL**2
017500     OPEN INPUT RECEIPT-DIST-AUDIT-FILE                              CL**4
017600                                                                     CL**2
017700     PERFORM 7000-READ-RECEIPT-FILE                                       
017800*    IF PV-READ-COUNTER NOT = 2                                      CL**2
017900*        DISPLAY '*** NOT ENOUGH RECORDS IN FILE IN01 ***'                
018000*        MOVE +1                 TO ABEND-CODE                       CL**2
018100*        CALL 'ILBOABN0' USING ABEND-CODE                            CL**2
018200*    END-IF.                                                              
018300                                                                          
018400     OPEN OUTPUT RECEIPT-AUDIT-SUM-FILE.                             CL**2
018500                                                                          
018600 2000-PROCESS-AUDIT-RECPT-DIST.                                      CL**4
018700******************************************************************   CL**2
018800*    1-  CHECK SEQUENCE                                              CL**2
018900*    2-  IF EQUAL, COMPUTE SUMMARY VALUE                             CL**2
019000*    3-  WRITE SUMMARY RECORD (RCRD015)                              CL**2
019100*    4-  READ THE NEXT 2 INPUT RECORDS (RCRD014)                     CL**2
019200******************************************************************   CL**2
019300                                                                     CL**2
019400                                                                     CL**2
019500     IF PV001-ORD-NBR       = PV002-ORD-NBR                AND    RCR00007
019600        PV001-ORD-LNE-NBR   = PV002-ORD-LNE-NBR            AND    RCR00007
019700        PV001-REC-SEQ-NBR   = PV002-REC-SEQ-NBR            AND    RCR00007
019800        PV001-STR-NBR       = PV002-STR-NBR                AND    RCR00007
019900        PV001-UPD-TMST      = PV002-UPD-TMST               AND       CL**3
020000        PV001-NOTIF-NBR     = PV002-NOTIF-NBR              AND       CL**5
020100        PV001-AUD-TYP-CDE = 'B' AND PV002-AUD-TYP-CDE = 'A'          CL**3
020200         PERFORM 2100-BUILD-SUMMRY-REC                               CL**2
020300         PERFORM 2200-SUM-AND-WRITE                                  CL**2
020400     ELSE                                                                 
020500         SET PS-SEQUENCE-ERROR     TO TRUE                           CL**3
020600         MOVE PV001-ORD-NBR   TO PV-DISP-PO                       RCR00007
020700         MOVE PV001-ORD-LNE-NBR TO PV-DISP-LNE-NBR                RCR00007
020800         MOVE PV001-REC-SEQ-NBR TO PV-DISP-SEQ-NBR                RCR00007
020900         MOVE PV001-STR-NBR   TO PV-DISP-STR-NBR                  RCR00007
021000         DISPLAY SPACES                                              CL**2
021100         DISPLAY '*** SEQUENCE ERROR  ****'                               
021200         DISPLAY 'PO= ' PV-DISP-PO                                        
021300             '; RCVR= ' PV-DISP-SEQ-NBR                                   
021400             '; LINE= ' PV-DISP-LNE-NBR                                   
021500             '; TYPE1= ' PV001-AUD-TYP-CDE                                
021600             '; TYPE2= ' PV002-AUD-TYP-CDE                                
021700             '; READ-COUNTER= ' PV-READ-COUNTER                      CL**2
021800         DISPLAY SPACES                                              CL**2
021900         MOVE +2                 TO ABEND-CODE                       CL**2
022000         CALL 'ILBOABN0' USING ABEND-CODE                            CL**2
022100     END-IF.                                                              
022200                                                                     CL**2
022300     PERFORM 7000-READ-RECEIPT-FILE.                                      
022400     EJECT                                                                
022500 2100-BUILD-SUMMRY-REC.                                              CL**2
022510                                                                          
022520     INITIALIZE RC015-TRECDIS-ADJ-RECORD.                                 
022530                                                                          
022600     MOVE PV001-ORD-NBR             TO RC015-ORD-NBR              RCR00007
022700     MOVE PV001-ORD-LNE-NBR         TO RC015-ORD-LNE-NBR          RCR00007
022800     MOVE PV001-REC-SEQ-NBR         TO RC015-REC-SEQ-NBR          RCR00007
022900     MOVE PV001-OPER-UNT-ID         TO RC015-OPER-UNT-ID          RCR00007
023000     MOVE PV001-FCLTY-ID            TO RC015-FCLTY-ID-ID          RCR00007
023700     MOVE PV001-STR-NBR             TO RC015-STR-NBR              RCR00007
024300     MOVE PV001-UPD-TMST            TO RC015-UPD-TMST                CL**3
024400     MOVE PV001-UPD-UID             TO RC015-UPD-UID                 CL**3
024400     MOVE PV001-UPD-PGM-ID          TO RC015-UPD-PGM-ID              CL**3
024900     MOVE PV001-NOTIF-NBR           TO RC015-NOTIF-NBR               CL**5
026900     MOVE RC001-ADJ-CYC-NBR         TO RC015-ADJ-CYC-NBR.                 
026901                                                                          
026902*   POPULATED BY RCKUT039              RC015-ADJ-RTL-EXTN-AMT     .       
026903*                                      RC015-ADJ-COST-EXTN-AMT    .       
026904*                                      RC015-PRC-FCLTY-AREA-ID            
026905*                                      RC015-REC-LOC-CDE                  
026906*                                      RC015-VERFYD-DATE                  
026907*                                      RC015-VERFYD-UID                   
027000     EJECT                                                                
027100 2200-SUM-AND-WRITE.                                                 CL**2
027200                                                                          
027200****SAVE BEFORE AND AFTER VERIFY QUANTITY ON OUTPUT FILE                  
027200     MOVE PV001-AP-VERIFY-ITM-QTY TO RC015-B4-ITM-QTY.                    
027200     MOVE PV002-AP-VERIFY-ITM-QTY TO RC015-AFT-ITM-QTY.                   
027200                                                                          
027300     EVALUATE TRUE                                                        
027400     WHEN PV001-AP-VERIFY-ITM-QTY = PV002-AP-VERIFY-ITM-QTY       RCR00007
027500         CONTINUE                                                         
027600                                                                          
027700     WHEN PV001-AP-VERIFY-ITM-QTY > PV002-AP-VERIFY-ITM-QTY       RCR00007
028000         COMPUTE PV-SUM =                                                 
028100             (PV001-AP-VERIFY-ITM-QTY - PV002-AP-VERIFY-ITM-QTY)  RCR00007
028200             * -1                                                         
028310         MOVE PV-SUM               TO  RC015-ADJ-ITM-QTY                  
028400         PERFORM 7100-WRITE-SUMMARY-REC                              CL**2
028500                                                                          
028600     WHEN PV001-AP-VERIFY-ITM-QTY < PV002-AP-VERIFY-ITM-QTY       RCR00007
028900         COMPUTE PV-SUM =                                                 
029000              PV002-AP-VERIFY-ITM-QTY - PV001-AP-VERIFY-ITM-QTY   RCR00007
029110         MOVE PV-SUM               TO  RC015-ADJ-ITM-QTY                  
029200         PERFORM 7100-WRITE-SUMMARY-REC                              CL**2
029300     END-EVALUATE.                                                        
029400                                                                          
029500 7000-READ-RECEIPT-FILE.                                                  
029600                                                                          
029700     READ RECEIPT-DIST-AUDIT-FILE                                    CL**3
029800         INTO PV001-TRECDIS-AUDIT-RECORD                                  
029900     AT END                                                               
030000         SET PS-EOF-RECEIPT-FILE TO TRUE                             CL**3
030100     NOT AT END                                                           
030200         ADD 1                   TO PV-READ-COUNTER                  CL**2
030300     END-READ.                                                            
030400                                                                          
030500     IF NOT PS-EOF-RECEIPT-FILE                                      CL**3
030600         READ RECEIPT-DIST-AUDIT-FILE                                CL**3
030700             INTO PV002-TRECDIS-AUDIT-RECORD                              
030800         AT END                                                           
030900             SET PS-EOF-RECEIPT-FILE TO TRUE                         CL**3
031000         NOT AT END                                                       
031100             ADD 1                   TO PV-READ-COUNTER              CL**2
031200     END-READ.                                                            
031300     EJECT                                                           CL**2
031400 7100-WRITE-SUMMARY-REC.                                             CL**2
031500                                                                     CL**4
031600     WRITE RECEIPT-AUDIT-SUM-RECORD                                  CL**7
031700         FROM RC015-TRECDIS-ADJ-RECORD                               CL**3
031800                                                                     CL**5
031900     ADD +1 TO PV-WRITE-COUNTER.                                     CL**5
032000     EJECT                                                           CL**2
032100 9000-EOJ-ROUTINE.                                                   CL**2
032200                                                                     CL**2
032300     MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                       CL**5
032400     DISPLAY 'TOTAL AUDIT RECORD READ       = ' PV-DISPLAY-COUNT.    CL**5
032500                                                                     CL**2
032600     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                      CL**5
032700     DISPLAY 'TOTAL SUMMARY RECORDS WRITTEN = ' PV-DISPLAY-COUNT.    CL**5
032800                                                                     CL**2
032900     CLOSE RECEIPT-DIST-AUDIT-FILE                                   CL**4
033000           RECEIPT-AUDIT-SUM-FILE.                                   CL**2
