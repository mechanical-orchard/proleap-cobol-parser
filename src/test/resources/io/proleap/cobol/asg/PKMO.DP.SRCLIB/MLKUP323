       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP323.                                         00030000
       AUTHOR.        PAUL KEBER.                                       00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  2-21-2000.                                        00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP323 - WEEKLY MAJOR CLASS MERCHANDISE LEDGER     *00100000
      *                      TABLE UPDATE (TWKMCL)                     *00110000
      ******************************************************************00120000
      *     THIS PROGRAM WILL MAINTAIN THE WEEKLY MAJOR CLASS DB2      *00130000
      * TABLE WITH THE CURRENT WEEK'S DATA.  THE TABLE IS READ USING   *00140000
      * THE TABLE KEY FROM EACH INPUT FILE RECORD.  IF THE ROW IS      *00150000
      * FOUND, THE ROW IS UPDATED WITH DATA FROM THE INPUT FILE RECORD.*00160000
      * IF THE ROW IS NOT FOUND, A NEW ROW IS INSERTED ON THE DB2      *00170000
      * TABLE (TWKMCL).  COMMITS ARE TAKEN EVERY TEN SECONDS, AND      *00180000
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                      *00190000
      *                                                                *00200000
W26603******************************************************************00210000
W26603* CHG ID   DATE        DESCRIPTION                               *00220000
W26603* ------   ----------  ----------------------------------------- *00230000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00240000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00250000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00260000
W26603*                      A DB2 SELECT.                             *00270000
W26603*                                                                *00280000
W26603*                      - RONNA MCDONALD                          *00290000
      ******************************************************************00300000
                                                                        00310000
                                                                        00320000
       ENVIRONMENT DIVISION.                                            00330000
                                                                        00340000
       CONFIGURATION SECTION.                                           00350000
       SOURCE-COMPUTER.        IBM-3090.                                00360000
       OBJECT-COMPUTER.        IBM-3090.                                00370000
       INPUT-OUTPUT SECTION.                                            00380000
       FILE-CONTROL.                                                    00390000
           SELECT UPDATE-FILE                                           00400000
               ASSIGN TO INP01.                                         00410000
                                                                        00420000
                                                                        00430000
       DATA DIVISION.                                                   00440000
       FILE SECTION.                                                    00450000
                                                                        00460000
       FD  UPDATE-FILE                                                  00470000
           LABEL RECORDS ARE STANDARD                                   00480000
           RECORDING MODE IS F                                          00490000
           BLOCK CONTAINS 0 RECORDS.                                    00500000
       01  UPDATE-RECORD               PIC X(100).                      00510000
      *                                                                 00520000
                                                                        00530000
       WORKING-STORAGE SECTION.                                         00540000
                                                                        00550000
       01  FILLER                      PIC X(28)   VALUE                00560000
                'BEGINNING OF WORKING STORAGE'.                         00570000
                                                                        00580000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00590000
                                                                        00600000
       01  PC-PROGRAM-CONSTANTS.                                        00610000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK287'.    00620000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP323'.  00630000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00640000
                                                                        00650000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00660000
           05  PE-MSG-01                       PIC X(50) VALUE          00670000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00680000
           05  PE-MSG-02                       PIC X(50) VALUE          00690000
                'ATTEMPT TO UPDATE ROW ON TABLE TWKMCL FAILED    '.     00700000
           05  PE-MSG-03                       PIC X(50) VALUE          00710000
                'ATTEMPT TO INSERT ROW ON TABLE TWKMCL FAILED    '.     00720000
           05  PE-MSG-04                       PIC X(50) VALUE          00730000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00740000
           05  PE-MSG-05                       PIC X(50) VALUE          00750000
                'UPDATE TO TCKRSTINF FAILED                      '.     00760000
           05  PE-MSG-06                       PIC X(50) VALUE          00770000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00780000
           05  PE-MSG-07                       PIC X(50) VALUE          00790000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00800000
      *                                                                 00810000
       01  PS-PROGRAM-SWITCHES.                                         00820000
           05  PS-EOF-UPDATE-FILE-SW        PIC X(01)     VALUE 'N'.    00830000
               88  PS-EOF-UPDATE-FILE                     VALUE 'Y'.    00840000
           05  PS-WKMCL-CURSOR-EOF-SW       PIC X(01)     VALUE 'N'.    00850000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00860000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00870000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00880000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00890000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00900000
                                                                        00910000
       01  PI-PROGRAM-INDICATORS.                                       00920000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00930000
               88  PI-UPDATE-WKMCL-ROW                    VALUE 'U'.    00940000
               88  PI-INSERT-WKMCL-ROW                    VALUE 'I'.    00950000
      *                                                                 00960000
       01  PROGRAM-VARIABLES.                                           00970000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00980000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00990000
      *                                                                 01000000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01010000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01020000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01030000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01040000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01050000
      *                                                                 01060000
       01  PA-PROGRAM-ACCUMULATORS.                                     01070000
           05  PA-RECORD-COUNTS.                                        01080000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01090000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01100000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01110000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01120000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01130000
                                                                        01140000
       01  PD-PROGRAM-DISPLAYS.                                         01150000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01160000
                                                                        01170000
       01  SAVE-AREA.                                                   01180000
           05  SV-PRIMARY-KEY.                                          01190000
               10  SV-FSCL-WK-NBR        PIC X(02)  VALUE SPACES.       01200000
               10  SV-FSCL-WK-END-DTE    PIC X(10)  VALUE SPACES.       01210000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01220000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01230000
           05  SV-UPDATE-FIELDS.                                        01240000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01250000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01260000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01270000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01280000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01290000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01300000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01310000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01320000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01330000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01340000
      *                                                                 01350000
      ******************************************************************01360000
      * MLRD118 - M/L WEEKLY MAJOR CLASS DATABASE UPDATE TRANS LAYOUT   01370000
      ******************************************************************01380000
           COPY MLRD139.                                                01390000
      *                                                                 01400000
      ******************************************************************01410000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01420000
      ******************************************************************01430000
           COPY DPWS004.                                                01440000
      *                                                                 01450000
      ******************************************************************01460000
      *  USED FOR DB2 ERRORS                                            01470000
      ******************************************************************01480000
           EXEC SQL                                                     01490000
               INCLUDE SQLCA                                            01500000
           END-EXEC.                                                    01510000
      *                                                                 01520000
      ******************************************************************01530000
      *  M/L WEEK-TO-DATE MAJOR CLASS TABLE                             01540000
      ******************************************************************01550000
           EXEC SQL                                                     01560000
               INCLUDE TWKMCL                                           01570000
           END-EXEC.                                                    01580000
      *                                                                 01590000
      ***************************************************************** 01600000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01610000
      ***************************************************************** 01620000
           EXEC SQL                                                     01630000
               INCLUDE TCKRSTCN                                         01640000
           END-EXEC.                                                    01650000
      *                                                                 01660000
           EXEC SQL                                                     01670000
               INCLUDE TCKRSTIN                                         01680000
           END-EXEC.                                                    01690000
      *                                                                 01700000
      ******************************************************************01710000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01720000
      ******************************************************************01730000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01740000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +28  COMP. 01750000
           05  CR-CHECKPOINT-RESTART-VAR.                               01760000
               10  CR-PREV-DTL-KEY          PIC  X(17) VALUE SPACES.    01770000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01780000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01790000
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                 01800000
                   15  CR-DEPT-NBR          PIC  X(03).                 01810000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01820000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01830000
      *                                                                 01840000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01850000
           05  CK-SAVE-PREV-KEY         PIC  X(17) VALUE SPACES.        01860000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01870000
               10  CK-FISCAL-NBR        PIC  X(02).                     01880000
               10  CK-FISCAL-WK-END-DTE PIC  X(10).                     01890000
               10  CK-DEPT-NBR          PIC  X(03).                     01900000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     01910000
                                                                        01920000
                                                                        01930000
      *-------------------*                                             01940000
       PROCEDURE DIVISION.                                              01950000
      *-------------------*                                             01960000
                                                                        01970000
       0000-MLKUP323.                                                   01980000
                                                                        01990000
           PERFORM 1000-INITIALIZATION.                                 02000000
      *                                                                 02010000
           PERFORM 2000-PROCESS-INPUT                                   02020000
                UNTIL PS-EOF-UPDATE-FILE.                               02030000
      *                                                                 02040000
           PERFORM 8000-EOJ-ROUTINE.                                    02050000
                                                                        02060000
           STOP RUN.                                                    02070000
                                                                        02080000
      ******************************************************************02090000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02100000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02110000
      ******************************************************************02120000
      *                                                                 02130000
       1000-INITIALIZATION.                                             02140000
      *                                                                 02150000
           OPEN INPUT UPDATE-FILE.                                      02160000
                                                                        02170000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02180000
                                                                        02190000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02200000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02210000
               PERFORM 7500-SELECT-RESTART-INFO                         02220000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02230000
                                            CR-CHECKPOINT-RESTART-VAR   02240000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02250000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02260000
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 02270000
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          02280000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02290000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02300000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02310000
           ELSE                                                         02320000
               SET PS-FIRST-COMMIT TO TRUE                              02330000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02340000
           END-IF.                                                      02350000
      *                                                                 02360000
           PERFORM 4000-READ-UPDATE-FILE UNTIL                          02370000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02380000
            OR PS-EOF-UPDATE-FILE.                                      02390000
      *                                                                 02400000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02410000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02420000
                        PA-INPUT-COUNT                                  02430000
               DISPLAY 'FISCAL WEEK NBR       ' ML139-FSCL-WK-NBR       02440000
               DISPLAY 'FISCAL WEEK END DATE  ' ML139-FSCL-WK-END-DTE   02450000
               DISPLAY 'DEPARTMENT NBR        ' ML139-DEPT-NBR          02460000
               DISPLAY 'MAJOR CLASS NBR       ' ML139-MAJ-CL-NBR        02470000
           END-IF.                                                      02480000
                                                                        02490000
           IF PS-EOF-UPDATE-FILE                                        02500000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02510000
                   CONTINUE                                             02520000
               ELSE                                                     02530000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02540000
           ELSE                                                         02550000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02560000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02570000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02580000
           END-IF.                                                      02590000
                                                                        02600000
      ******************************************************************02610000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02620000
      ******************************************************************02630000
       2000-PROCESS-INPUT.                                              02640000
      *                                                                 02650000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02660000
      *                                                                 02670000
           PERFORM 7000-SELECT-DB-ROW.                                  02680000
      *                                                                 02690000
           IF PS-PROGRAM-DEADLOCKED                                     02700000
               CONTINUE                                                 02710000
           ELSE                                                         02720000
               EVALUATE TRUE                                            02730000
                   WHEN PI-UPDATE-WKMCL-ROW                             02740000
                       PERFORM 7100-UPDATE-WKMCL-ROW                    02750000
                   WHEN PI-INSERT-WKMCL-ROW                             02760000
                       PERFORM 7200-INSERT-WKMCL-ROW                    02770000
                   WHEN OTHER                                           02780000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02790000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02800000
                       PERFORM 9999-SQL-ABEND                           02810000
               END-EVALUATE                                             02820000
           END-IF.                                                      02830000
      *                                                                 02840000
           IF PS-PROGRAM-DEADLOCKED                                     02850000
               CONTINUE                                                 02860000
           ELSE                                                         02870000
               PERFORM 7700-COMMIT-PROCESSING                           02880000
               PERFORM 4000-READ-UPDATE-FILE                            02890000
           END-IF.                                                      02900000
      *                                                                 02910000
      *                                                                 02920000
      ******************************************************************02930000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02940000
      ******************************************************************02950000
       2100-CALCULATE-UPDATES.                                          02960000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02970000
      *                                                                 02980000
           COMPUTE SV-SLS-RTL-AMT       =                               02990000
                   WKMCL-SLS-RTL-AMT       + ML139-SLS-RTL-AMT.         03000000
           COMPUTE SV-MKDN-RTL-AMT      =                               03010000
                   WKMCL-MKDN-RTL-AMT      + ML139-MKDN-RTL-AMT.        03020000
           COMPUTE SV-MKUP-RTL-AMT      =                               03030000
                   WKMCL-MKUP-RTL-AMT      + ML139-MKUP-RTL-AMT.        03040000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03050000
                   WKMCL-XFER-IN-RTL-AMT   + ML139-XFER-IN-RTL-AMT.     03060000
           COMPUTE SV-XFER-OUT-RTL      =                               03070000
                   WKMCL-XFER-OUT-RTL-AMT  + ML139-XFER-OUT-RTL-AMT.    03080000
           COMPUTE SV-RCPT-RTL-AMT      =                               03090000
                   WKMCL-RCPT-RTL-AMT      + ML139-RCPT-RTL-AMT.        03100000
           COMPUTE SV-RCPT-NET-COST     =                               03110000
                   WKMCL-RCPT-NET-COST-AMT + ML139-RCPT-NET-COST-AMT.   03120000
           COMPUTE SV-PADJ-RTL-AMT      =                               03130000
                   WKMCL-PADJ-RTL-AMT      + ML139-PADJ-RTL-AMT.        03140000
           COMPUTE SV-PADJ-NET-COST     =                               03150000
                   WKMCL-PADJ-NET-COST-AMT + ML139-PADJ-NET-COST-AMT.   03160000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03170000
                    WKMCL-INV-ADJ-RTL-AMT  + ML139-INV-ADJ-RTL-AMT.     03180000
      *                                                                 03190000
      ******************************************************************03200000
      * READS THE CONDENSED FILE INTO THE TWKMCL LAYOUT                 03210000
      ******************************************************************03220000
       4000-READ-UPDATE-FILE.                                           03230000
           READ UPDATE-FILE INTO ML139-UPDATE-REC                       03240000
              AT END                                                    03250000
                MOVE 'Y' TO PS-EOF-UPDATE-FILE-SW.                      03260000
     *                                                                  03270000
           IF PS-EOF-UPDATE-FILE                                        03280000
               MOVE ML139-FSCL-WK-NBR         TO CR-FISCAL-NBR          03290000
               MOVE ML139-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   03300000
               MOVE ML139-DEPT-NBR            TO CR-DEPT-NBR            03310000
               MOVE ML139-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03320000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03330000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03340000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03350000
                                             TCKRSTINF-WORK-STRG-DESC   03360000
               EXEC SQL                                                 03370000
                 UPDATE TCKRSTINF                                       03380000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03390000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03400000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03410000
               END-EXEC                                                 03420000
               IF SQLCODE = 0                                           03430000
                   CONTINUE                                             03440000
               ELSE                                                     03450000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03460000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03470000
                   PERFORM 9999-SQL-ABEND                               03480000
               END-IF                                                   03490000
            ELSE                                                        03500000
                ADD 1                        TO PA-INPUT-COUNT          03510000
            END-IF.                                                     03520000
      *                                                                 03530000
      ******************************************************************03540000
      * SELECT A ROW FROM THE MTHLY MCL STR TAB  THAT MATCHES INPUT KEY 03550000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03560000
      ******************************************************************03570000
       7000-SELECT-DB-ROW.                                              03580000
      *                                                                 03590000
           EXEC SQL                                                     03600000
               SELECT SLS_RTL_AMT                                       03610000
                     ,MKDN_RTL_AMT                                      03620000
                     ,MKUP_RTL_AMT                                      03630000
                     ,XFER_IN_RTL_AMT                                   03640000
                     ,XFER_OUT_RTL_AMT                                  03650000
                     ,RCPT_RTL_AMT                                      03660000
                     ,RCPT_NET_COST_AMT                                 03670000
                     ,PADJ_RTL_AMT                                      03680000
                     ,PADJ_NET_COST_AMT                                 03690000
                     ,INV_ADJ_RTL_AMT                                   03700000
                 INTO :WKMCL-SLS-RTL-AMT                                03710000
                     ,:WKMCL-MKDN-RTL-AMT                               03720000
                     ,:WKMCL-MKUP-RTL-AMT                               03730000
                     ,:WKMCL-XFER-IN-RTL-AMT                            03740000
                     ,:WKMCL-XFER-OUT-RTL-AMT                           03750000
                     ,:WKMCL-RCPT-RTL-AMT                               03760000
                     ,:WKMCL-RCPT-NET-COST-AMT                          03770000
                     ,:WKMCL-PADJ-RTL-AMT                               03780000
                     ,:WKMCL-PADJ-NET-COST-AMT                          03790000
                     ,:WKMCL-INV-ADJ-RTL-AMT                            03800000
               FROM TWKMCL                                              03810000
              WHERE   FSCL_WK_NBR         = :ML139-FSCL-WK-NBR          03820000
                AND   FSCL_WK_END_DTE     = :ML139-FSCL-WK-END-DTE      03830000
                AND   DEPT_NBR            = :ML139-DEPT-NBR             03840000
                AND   MAJ_CL_NBR          = :ML139-MAJ-CL-NBR           03850000
W26603        WITH UR                                                   03860000
           END-EXEC.                                                    03870000
      *                                                                 03880000
           EVALUATE SQLCODE                                             03890000
               WHEN 0                                                   03900000
                   SET PI-UPDATE-WKMCL-ROW        TO TRUE               03910000
               WHEN +100                                                03920000
                   SET PI-INSERT-WKMCL-ROW        TO TRUE               03930000
               WHEN -911                                                03940000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            03950000
                   ADD +1             TO PV-DEADLOCK-COUNT              03960000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03970000
                       MOVE '7000-SELECT-DB-ROW' TO                     03980000
                                                    PV-PARAGRAPH-NAME   03990000
                       PERFORM 9000-DEADLOCK-ERROR                      04000000
                   ELSE                                                 04010000
                       PERFORM 7999-RESTART-PROCESS                     04020000
                   END-IF                                               04030000
               WHEN OTHER                                               04040000
                  DISPLAY ML139-FSCL-WK-NBR          ' '                04050000
                          ML139-FSCL-WK-END-DTE      ' '                04060000
                          ML139-DEPT-NBR             ' '                04070000
                          ML139-MAJ-CL-NBR           ' '                04080000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04090000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04100000
                   PERFORM 9999-SQL-ABEND                               04110000
           END-EVALUATE.                                                04120000
      *                                                                 04130000
      *                                                                 04140000
      ***************************************************************** 04150000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04160000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04170000
      * FROM INPUT REC MLRD139                                          04180000
      ***************************************************************** 04190000
       7100-UPDATE-WKMCL-ROW.                                           04200000
      *                                                                 04210000
           PERFORM 2100-CALCULATE-UPDATES.                              04220000
      *                                                                 04230000
           EXEC SQL                                                     04240000
               UPDATE TWKMCL                                            04250000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04260000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          04270000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04280000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          04290000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            04300000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04310000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04320000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04330000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           04340000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04350000
              WHERE   FSCL_WK_NBR          = :ML139-FSCL-WK-NBR         04360000
                AND   FSCL_WK_END_DTE      = :ML139-FSCL-WK-END-DTE     04370000
                AND   DEPT_NBR             = :ML139-DEPT-NBR            04380000
                AND   MAJ_CL_NBR           = :ML139-MAJ-CL-NBR          04390000
           END-EXEC                                                     04400000
      *                                                                 04410000
           EVALUATE SQLCODE                                             04420000
               WHEN 0                                                   04430000
                   CONTINUE                                             04440000
               WHEN -911                                                04450000
                   ADD +1             TO PV-DEADLOCK-COUNT              04460000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04470000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04480000
                       MOVE '7100-UPDATE-WKMCL-ROW' TO                  04490000
                                                    PV-PARAGRAPH-NAME   04500000
                       PERFORM 9000-DEADLOCK-ERROR                      04510000
                   ELSE                                                 04520000
                       PERFORM 7999-RESTART-PROCESS                     04530000
                   END-IF                                               04540000
               WHEN OTHER                                               04550000
                   MOVE '7100-UPDATE-WKMCL-ROW' TO PV-PARAGRAPH-NAME    04560000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04570000
                   PERFORM 9999-SQL-ABEND                               04580000
           END-EVALUATE.                                                04590000
      *                                                                 04600000
           ADD 1 TO PA-UPDATE-COUNT.                                    04610000
      *                                                                 04620000
      ***************************************************************** 04630000
      * THERE WAS NO MATCHING ROW ON THE WEEKLY MCL TABLE. INITIAL    * 04640000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04650000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN     * 04660000
      *   RECALC                                                      * 04670000
      ***************************************************************** 04680000
       7200-INSERT-WKMCL-ROW.                                           04690000
      *                                                                 04700000
      *                                                                 04710000
           EXEC SQL                                                     04720000
               INSERT INTO TWKMCL                                       04730000
                   (   FSCL_WK_NBR                                      04740000
                     , FSCL_WK_END_DTE                                  04750000
                     , DEPT_NBR                                         04760000
                     , MAJ_CL_NBR                                       04770000
                     , RCPT_RTL_AMT                                     04780000
                     , RCPT_NET_COST_AMT                                04790000
                     , PADJ_RTL_AMT                                     04800000
                     , PADJ_NET_COST_AMT                                04810000
                     , SLS_RTL_AMT                                      04820000
                     , MKDN_RTL_AMT                                     04830000
                     , MKUP_RTL_AMT                                     04840000
                     , XFER_IN_RTL_AMT                                  04850000
                     , XFER_OUT_RTL_AMT                                 04860000
                     , INV_ADJ_RTL_AMT                                  04870000
                     , SHNK_RTL_AMT                                     04880000
                     , END_INV_RTL_AMT                                  04890000
                     , CUM_MKUP_PCT                                     04900000
                     , END_INV_COST_AMT                                 04910000
                     , SLS_COST_AMT                                     04920000
                     , GRO_MRGN_AMT      )                              04930000
               VALUES                                                   04940000
                   (   :ML139-FSCL-WK-NBR                               04950000
                     , :ML139-FSCL-WK-END-DTE                           04960000
                     , :ML139-DEPT-NBR                                  04970000
                     , :ML139-MAJ-CL-NBR                                04980000
                     , :ML139-RCPT-RTL-AMT                              04990000
                     , :ML139-RCPT-NET-COST-AMT                         05000000
                     , :ML139-PADJ-RTL-AMT                              05010000
                     , :ML139-PADJ-NET-COST-AMT                         05020000
                     , :ML139-SLS-RTL-AMT                               05030000
                     , :ML139-MKDN-RTL-AMT                              05040000
                     , :ML139-MKUP-RTL-AMT                              05050000
                     , :ML139-XFER-IN-RTL-AMT                           05060000
                     , :ML139-XFER-OUT-RTL-AMT                          05070000
                     , :ML139-INV-ADJ-RTL-AMT                           05080000
                     , 0                                                05090000
                     , 0                                                05100000
                     , 0                                                05110000
                     , 0                                                05120000
                     , 0                                                05130000
                     , 0                       )                        05140000
           END-EXEC                                                     05150000
      *                                                                 05160000
           EVALUATE SQLCODE                                             05170000
               WHEN 0                                                   05180000
                   CONTINUE                                             05190000
               WHEN -911                                                05200000
                   ADD +1             TO PV-DEADLOCK-COUNT              05210000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05220000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05230000
                       MOVE '7200-INSERT-WKMCL-ROW' TO                  05240000
                                                    PV-PARAGRAPH-NAME   05250000
                       PERFORM 9000-DEADLOCK-ERROR                      05260000
                   ELSE                                                 05270000
                       PERFORM 7999-RESTART-PROCESS                     05280000
                   END-IF                                               05290000
               WHEN OTHER                                               05300000
                  DISPLAY ML139-FSCL-WK-NBR          ' '                05310000
                          ML139-FSCL-WK-END-DTE      ' '                05320000
                          ML139-DEPT-NBR             ' '                05330000
                          ML139-MAJ-CL-NBR           ' '                05340000
                   MOVE '7200-INSERT-WKMCL-ROW' TO PV-PARAGRAPH-NAME    05350000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05360000
                   PERFORM 9999-SQL-ABEND                               05370000
           END-EVALUATE.                                                05380000
                                                                        05390000
           ADD 1 TO PA-INSERT-COUNT.                                    05400000
                                                                        05410000
      ******************************************************************05420000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05430000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05440000
      *            CONTROL TABLE                                       *05450000
      ******************************************************************05460000
       7300-GET-COMMIT-TIMESTAMP.                                       05470000
                                                                        05480000
           EXEC SQL                                                     05490000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05500000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05510000
               INTO :PV-COMMIT-TIMESTAMP                                05520000
               FROM TCKRSTCNTL                                          05530000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05540000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05550000
           END-EXEC.                                                    05560000
                                                                        05570000
           EVALUATE TRUE                                                05580000
               WHEN SQLCODE = 0                                         05590000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05600000
                   CONTINUE                                             05610000
               WHEN SQLCODE NOT EQUAL ZERO                              05620000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05630000
                   PERFORM 9999-SQL-ABEND                               05640000
           END-EVALUATE.                                                05650000
      *                                                                 05660000
      *                                                                 05670000
      ******************************************************************05680000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05690000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05700000
      *            IF WE ARE IN A RESTART CONDITION.                   *05710000
      ******************************************************************05720000
       7400-INIT-CHECKPOINT-SELECT.                                     05730000
                                                                        05740000
           EXEC SQL                                                     05750000
             SELECT  CKPT_STAT_CODE                                     05760000
                    ,CKPT_FRQNCY_QTY                                    05770000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05780000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05790000
               FROM  TCKRSTCNTL                                         05800000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05810000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05820000
           END-EXEC.                                                    05830000
                                                                        05840000
           IF SQLCODE = 0                                               05850000
               CONTINUE                                                 05860000
           ELSE                                                         05870000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05880000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05890000
               PERFORM 9999-SQL-ABEND                                   05900000
           END-IF.                                                      05910000
      *                                                                 05920000
      *                                                                 05930000
      ******************************************************************05940000
      * 7500-SELECT-RESTART-INFO                                       *05950000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05960000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05970000
      ******************************************************************05980000
       7500-SELECT-RESTART-INFO.                                        05990000
                                                                        06000000
           EXEC SQL                                                     06010000
             SELECT  WORK_STRG_DESC                                     06020000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          06030000
               FROM  TCKRSTINF                                          06040000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06050000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06060000
           END-EXEC.                                                    06070000
                                                                        06080000
           IF SQLCODE = 0                                               06090000
               CONTINUE                                                 06100000
           ELSE                                                         06110000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06120000
               PERFORM 9999-SQL-ABEND                                   06130000
           END-IF.                                                      06140000
                                                                        06150000
      ******************************************************************06160000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06170000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06180000
      ******************************************************************06190000
       7600-SET-CURRENT-TIMESTAMP.                                      06200000
                                                                        06210000
           EXEC SQL                                                     06220000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06230000
           END-EXEC.                                                    06240000
                                                                        06250000
           IF SQLCODE = 0                                               06260000
               CONTINUE                                                 06270000
           ELSE                                                         06280000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06290000
               PERFORM 9999-SQL-ABEND                                   06300000
           END-IF.                                                      06310000
      *                                                                 06320000
      *                                                                 06330000
      ******************************************************************06340000
      * 7700-COMMIT-PROCESSING.                                        *06350000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06360000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06370000
      ******************************************************************06380000
       7700-COMMIT-PROCESSING.                                          06390000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06400000
                                                                        06410000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06420000
                                                                        06430000
      * PREPARE COMMIT RECORD FOR UPDATE                                06440000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06450000
               MOVE ML139-FSCL-WK-NBR         TO CR-FISCAL-NBR          06460000
               MOVE ML139-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   06470000
               MOVE ML139-DEPT-NBR            TO CR-DEPT-NBR            06480000
               MOVE ML139-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06490000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06500000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06510000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06520000
                                             TCKRSTINF-WORK-STRG-DESC   06530000
               IF PS-FIRST-COMMIT                                       06540000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06550000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06560000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06570000
               END-IF                                                   06580000
               PERFORM 7800-COMMIT-CHANGES                              06590000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06600000
           END-IF.                                                      06610000
      *                                                                 06620000
      *                                                                 06630000
      ******************************************************************06640000
      * 7800-COMMIT-CHANGES.                                            06650000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06660000
      *            TAKE A COMMIT.                                       06670000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06680000
      ******************************************************************06690000
       7800-COMMIT-CHANGES.                                             06700000
                                                                        06710000
           EXEC SQL                                                     06720000
             UPDATE TCKRSTINF                                           06730000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06740000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06750000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06760000
           END-EXEC.                                                    06770000
                                                                        06780000
           IF SQLCODE = 0                                               06790000
               CONTINUE                                                 06800000
           ELSE                                                         06810000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06820000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06830000
               PERFORM 9999-SQL-ABEND                                   06840000
           END-IF.                                                      06850000
                                                                        06860000
           EXEC SQL                                                     06870000
               COMMIT                                                   06880000
           END-EXEC.                                                    06890000
                                                                        06900000
           IF SQLCODE = 0                                               06910000
               CONTINUE                                                 06920000
           ELSE                                                         06930000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06940000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06950000
               PERFORM 9999-SQL-ABEND                                   06960000
           END-IF.                                                      06970000
      *                                                                 06980000
      ******************************************************************06990000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07000000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07010000
      ******************************************************************07020000
       7900-UPDATE-CHECKPOINT-STATUS.                                   07030000
                                                                        07040000
           EXEC SQL                                                     07050000
             UPDATE  TCKRSTCNTL                                         07060000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07070000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07080000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07090000
           END-EXEC.                                                    07100000
                                                                        07110000
           IF SQLCODE = 0                                               07120000
               CONTINUE                                                 07130000
           ELSE                                                         07140000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07150000
               PERFORM 9999-SQL-ABEND                                   07160000
           END-IF.                                                      07170000
                                                                        07180000
           EJECT                                                        07190000
      *                                                                 07200000
      *                                                                 07210000
      ******************************************************************07220000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKMCL ROW FOR  UPDATE07230000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07240000
      ******************************************************************07250000
       7999-RESTART-PROCESS.                                            07260000
      *                                                                 07270000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07280000
      *                                                                 07290000
           CLOSE UPDATE-FILE.                                           07300000
      *                                                                 07310000
           PERFORM 7500-SELECT-RESTART-INFO.                            07320000
      *                                                                 07330000
           DISPLAY '**** RESTART **** '.                                07340000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07350000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07360000
                                                                        07370000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07380000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07390000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07400000
           IF PS-FIRST-COMMIT                                           07410000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07420000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07430000
                      ' BEGINNING'                                      07440000
           ELSE                                                         07450000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07460000
                        TCKRSTINF-WORK-STRG-DESC (1:28)                 07470000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07480000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07490000
                    CR-CHECKPOINT-RESTART-AREA                          07500000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07510000
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 07520000
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          07530000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07540000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 07550000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07560000
           END-IF.                                                      07570000
                                                                        07580000
           OPEN INPUT UPDATE-FILE.                                      07590000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07600000
      *                                                                 07610000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07620000
           PERFORM 4000-READ-UPDATE-FILE                                07630000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07640000
                 OR PS-EOF-UPDATE-FILE.                                 07650000
           IF PS-EOF-UPDATE-FILE                                        07660000
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07670000
           ELSE                                                         07680000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07690000
                        PA-INPUT-COUNT                                  07700000
               DISPLAY 'FISCAL WEEK NBR       ' ML139-FSCL-WK-NBR       07710000
               DISPLAY 'FISCAL WEEK END DATE  ' ML139-FSCL-WK-END-DTE   07720000
               DISPLAY 'DEPARTMENT NBR        ' ML139-DEPT-NBR          07730000
               DISPLAY 'MAJOR CLASS NBR       ' ML139-MAJ-CL-NBR        07740000
           END-IF.                                                      07750000
      *                                                                 07760000
      ******************************************************************07770000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07780000
      ******************************************************************07790000
       8000-EOJ-ROUTINE.                                                07800000
      *                                                                 07810000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07820000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07830000
                                                                        07840000
      *                                                                 07850000
           DISPLAY SPACES.                                              07860000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07870000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07880000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07890000
           DISPLAY SPACES.                                              07900000
      *                                                                 07910000
           CLOSE UPDATE-FILE.                                           07920000
      *                                                                 07930000
      *                                                                 07940000
      ******************************************************************07950000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07960000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07970000
      ******************************************************************07980000
       9000-DEADLOCK-ERROR.                                             07990000
      *                                                                 08000000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     08010000
               PERFORM 9999-SQL-ABEND.                                  08020000
      *                                                                 08030000
      ******************************************************************08040000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08050000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08060000
      ******************************************************************08070000
       9999-SQL-ABEND.                                                  08080000
                                                                        08090000
           MOVE +4013                 TO ABEND-CODE.                    08100000
           DISPLAY '**  ABEND     **'.                                  08110000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08120000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08130000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       08140000
                                                                        08150000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08160000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08170000
                                                                        08180000
           COPY DPPD004.                                                08190000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08200000
