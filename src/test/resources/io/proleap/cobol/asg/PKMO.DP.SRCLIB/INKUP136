       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUP136.                                                 
       AUTHOR.        PATTY JAEGER/CODEWORKS.                                   
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/26/08.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WILL READ THE DSNTIAUL UNLOAD RECS FROM TINCNTL            
      * TABLE OF ALL ACTIVE CYCLES.                                             
      *                                                                         
      * FOR EACH CYCLE, WE SET ALL ACTL DATES FOR ALL DC LOCATIONS              
      * AND WE CHECK TO SEE IF THERE ARE ANY STORE (TINVPAR) RECS               
      * THAT ARE STILL ACTIVE - FINANCIAL BOOK DATE = '9999-09-09'              
      *                                                                         
      * IF THERE ARE NO STORES THAT ARE ACTIVE, THEN WE WILL                    
      * DEACTIVATE THE CYCLE (TINCNTL)                                          
      *                                                                         
      * A COMMIT WILL BE DONE AT THE END OF THE PROGRAM.                        
      * ALL ACTIVE STORES FOR A CYCLE WILL BE DISPLAYED IN TWO                  
      * EMAILS:  IS-IN-ALERT                                                    
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES         *        
      * ------- ---------- ------------ -------------------------------*        
      * W33304  11-26-2008 PATTY JAEGER/ NEW PROGRAM CREATED AS PART   *        
      *                    CODEWORKS     OF HOLDING INVENTORY LEDGER   *        
      *                                  OPEN - HILO                   *        
      * P40716  04-26-2013 COGNIZANT     MODIFIED THE PROGRAM TO ACCEPT*        
      *                                  RV & CS LOCATIONS ALONG WITH  *        
      *                                  DC TO UPDATE ACTUAL DATES.    *        
      *                                  FINANCIAL STATUS CODE WILL BE *        
      *                                  FLIPPED TO TP FOR RV AND CS   *        
      *                                  LOCATIONS THAT DID NOT HAVE   *        
      *                                  ANY FINANCIAL ADJUSTMENTS.    *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT ACT-CYCLE-FILE                                                
               ASSIGN TO INP01.                                                 
           SELECT STORE-ACT-OUT-FILE                                            
               ASSIGN TO OUT01.                                                 
           SELECT CYCLE-DE-ACT-FILE                                             
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  ACT-CYCLE-FILE                                                       
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS ACT-CYCLE-RECORD.                                     
       01  ACT-CYCLE-RECORD           PIC X(10).                                
                                                                                
       FD  STORE-ACT-OUT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
                                                                                
       01  STORE-ACT-OUT-RECORD       PIC  X(80).                               
                                                                                
       FD  CYCLE-DE-ACT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
                                                                                
       01  CYCLE-DE-ACT-REC           PIC  X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DB-DATA-BASE-LOAD-FIELDS.                                            
           05  DB-TOT-CNT-ZERO-QTY       PIC S9(9)    COMP VALUE ZERO.          
           05  DB-TOT-CNT-ZERO-QTY-IND   PIC S9(4)    COMP VALUE ZERO.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC  X(08)   VALUE 'INKUP136'.         
           05  PC-STATUS-CODE            PIC  X(02)   VALUE 'IN'.               
           05  PC-MAX-RETRIES            PIC S9(04)   VALUE +3                  
                                                      COMP SYNC.                
           05  PC-INKUP136               PIC X(08)   VALUE 'INKUP136'.  INKUT119
                                                                                
       01  PV-ACT-CYCLE-REC.                                                    
           05  PV-ACT-CYCLE-ID           PIC S9(09)   VALUE ZERO COMP.          
           05  FILLER                    PIC  X(06)   VALUE SPACES.             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME         PIC  X(30)   VALUE SPACES.             
           05  PV-STORE-NBR              PIC S9(04)   VALUE +0 COMP.            
           05  PV-PREV-STORE-NBR         PIC S9(04)   VALUE +0 COMP.            
           05  PV-ABEND-MESSAGE          PIC  X(75)   VALUE SPACE.              
           05  PV-SQL-SUB                PIC  9(03)   VALUE ZERO.               
           05  PV-DB2-OPER-ATTEMPTED     PIC  X(30)   VALUE SPACES.             
           05  PV-READ-CNT               PIC  9(09)   VALUE ZEROES.             
           05  PV-ACTIVE-COUNT           PIC  9(09)   VALUE ZEROES.             
           05  PV-OUT-WRITE-CNT          PIC  9(09)   VALUE ZEROES.             
           05  PV-DEACT-CNT              PIC  9(09)   VALUE ZEROES.             
                                                                                
           05  PV-INV-ID                 PIC ZZZZ9.                             
           05  PV-CYCLE-DE-ACT-REC.                                             
               10  FILLER                PIC  X(16) VALUE                       
                                                     'INVENTORY CYCLE '.        
               10  PV-CYCLE-INV-ID       PIC ZZZZ9  VALUE ZERO.                 
               10  FILLER                PIC  X(13) VALUE                       
                                                     ' DE-ACTIVATED'.           
               10  FILLER                PIC  X(46) VALUE SPACES.               
                                                                                
       01  PROGRAM-UPDATE-IDENTIFIER.                                           
           05  PG-UPDATE-TYPE            PIC X(01).                             
               88  NO-UPDATE-REQUIRED                 VALUE '0'.                
               88  ACTUAL-INV-DATE-UPDATE             VALUE '1'.                
               88  ACTUAL-UNIT-BK-UPDATE              VALUE '2'.                
               88  ACTUAL-FIN-BK-UPDATE               VALUE '3'.                
               88  ACTUAL-INV-RECD-UPDATE             VALUE '4'.                
P40716         88  FINCL-INV-STAT-CDE-UPDATE          VALUE '5'.                
                                                                                
       01  INPUT-OUTPUT-FIELDS.                                                 
           05  PV-STORE-OUT.                                                    
               10  PV-LOC-NBR               PIC  X(05)    VALUE SPACES.         
               10  FILLER                   PIC  X(02)    VALUE ' ('.           
               10  PV-LOC-SCHD              PIC  X(02)    VALUE SPACES.         
               10  FILLER                   PIC  X(02)    VALUE ') '.           
               10  PV-INV-NM                PIC  X(25).                         
               10  FILLER                   PIC  X.                             
               10  PV-COUNT                 PIC  X(09)    VALUE                 
                                                          'INV DATE'.           
               10  FILLER                   PIC  X        VALUE ' '.            
               10  PV-INV-DATE              PIC  X(10)    VALUE SPACES.         
               10  PV-BK-DATE               PIC  X(08)    VALUE                 
                                                          ' FIN BK '.           
               10  FILLER                   PIC  X        VALUE ' '.            
               10  PV-FIN-DATE              PIC  X(10)    VALUE SPACES.         
                                                                                
           05  PV-OUTPUT-DETAIL.                                                
               10  FILLER                   PIC  X(8)                           
                   VALUE 'CYCLE:  '.                                            
               10  PV-CYCLE-ID              PIC  9(9)     VALUE ZEROES.         
               10  FILLER                   PIC  X(2)     VALUE SPACES.         
               10  FILLER                   PIC  X(6)                           
                   VALUE 'LOC:  '.                                              
               10  PV-STORE-LOCATION        PIC  9(5)     VALUE ZEROES.         
               10  FILLER                   PIC  X(2)     VALUE SPACES.         
               10  FILLER                   PIC  X(19)    VALUE                 
                   'PLANNED INV DATE:  '.                                       
               10  PV-PLAN-INV-DATE         PIC  X(10)    VALUE SPACES.         
               10  FILLER                   PIC  X(19)    VALUE SPACES.         
           05  PV-COMMIT-COUNT              PIC  9(09)    VALUE ZEROES. 06030000
                                                                                
       01  SAVE-AREA-DB2-VARIABLES.                                             
           05  SV-STORE-NBR                    PIC 9(04)  VALUE ZEROES.         
           05  SV-LOC-NBR                      PIC S9(4) COMP.                  
           05  SV-ACT-INV-DTE                  PIC X(10)  VALUE SPACES.         
           05  SV-ACT-UNIT-BK-DTE              PIC X(10)  VALUE SPACES.         
           05  SV-ACT-FIN-BK-DTE               PIC X(10)  VALUE SPACES.         
           05  SV-ACT-INV-RECD-DTE             PIC X(10)  VALUE SPACES.         
           05  SV-PLN-INV-DTE                  PIC X(10)  VALUE SPACES.         
           05  SV-PLN-UNIT-BK-DTE              PIC X(10)  VALUE SPACES.         
           05  SV-PLN-FIN-BK-DTE               PIC X(10)  VALUE SPACES.         
P40716     05  SV-FINCL-INV-STAT-CDE           PIC X(02)  VALUE SPACES.         
                                                                                
       01  PROGRAM-ACCUMULATORS.                                                
           05  PA-ACTL-INV-DATE                PIC 9(03)  VALUE ZEROES.         
           05  PA-ACTL-UNIT-BK-DATE            PIC 9(03)  VALUE ZEROES.         
           05  PA-ACTL-FIN-BK-DATE             PIC 9(03)  VALUE ZEROES.         
           05  PA-ACTL-INV-RECD-DATE           PIC 9(03)  VALUE ZEROES.         
P40716     05  PA-FINCL-INV-STAT-CDE           PIC 9(03)  VALUE ZEROES.         
                                                                                
       01  PROGRAM-ERROR-MESSAGES.                                              
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'OPEN UPDATE_ACTL_CSR CURSOR  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'FETCH UPDATE_ACTL_CSR ROW    '.                                
           05  PE-MSG-06                       PIC X(30) VALUE                  
                'CLOSE UPDATE_ACTL_CSR CURSOR '.                                
           05  PE-MSG-07                       PIC X(30) VALUE                  
                'ATTEMPT TO UPDATE  DB2 COLUMN'.                                
           05  PE-MSG-08                       PIC X(30) VALUE                  
                'SELECT LOCATION XMT DB2 COLUMN'.                               
                                                                                
       01  SW-SWITCHES.                                                         
           05  PS-END-OF-ACT-CYCLE-SW        PIC X         VALUE 'N'.           
               88  PS-END-OF-ACT-CYCLE-YES                 VALUE 'Y'.           
               88  PS-END-OF-ACT-CYCLE-NO                  VALUE 'N'.           
           05  PS-END-OF-FETCHES-SW          PIC X         VALUE 'N'.           
               88 PS-END-OF-FETCHES-YES                    VALUE 'Y'.           
               88 PS-END-OF-FETCHES-NO                     VALUE 'N'.           
           05  PS-FIRST-TIME-SW              PIC X         VALUE 'N'.           
               88 PS-FIRST-TIME-YES                        VALUE 'Y'.           
               88 PS-FIRST-TIME-NO                         VALUE 'N'.           
           05  PS-EOF-ACTLCSR-SW               PIC X(01)  VALUE 'N'.            
               88  PS-ACTL-CURSOR-EOF-SW                  VALUE 'Y'.            
               88  PS-ACTL-CURSOR-EOF-NO                  VALUE 'N'.            
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * DB2 STORE TABLE DCLGEN.                                                 
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 LOCATION TABLE DCLGEN.                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 CYCLE TABLE DCLGEN.                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *                                                                         
      * DB2 COMMUNICATION AREA 2                                                
      *                                                                         
      * DECLARE DB2 CURSOR INV_PARM_CSR                                         
                                                                                
           EXEC SQL                                                             
             DECLARE INV_PARM_CSR CURSOR FOR                                    
             SELECT           INV_ID,                                           
                              LOC_NBR,                                          
                              PLND_INV_DTE                                      
             FROM TINVPAR                                                       
             WHERE INV_ID = :PV-ACT-CYCLE-ID                                    
               AND ACTL_FIN_BK_DTE = '9999-09-09'                               
               AND LOC_INV_STAT_CDE = 'IN'                                      
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * UPDATE_ACTL_CSR - CURSOR USED TO SELECT 'ACTUAL' TYPE RECORDS  *        
      *    WITH DATES OF '9999-09-09' THAT WILL BE UPDATE PROCESSED    *        
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE  UPDATE_ACTL_CSR CURSOR WITH HOLD FOR                    
               SELECT  LOC_NBR                                                  
                      ,ACTL_INV_DTE                                             
                      ,ACTL_UNIT_BK_DTE                                         
                      ,ACTL_FIN_BK_DTE                                          
                      ,PLND_INV_DTE                                             
                      ,PLND_UNIT_BK_DTE                                         
                      ,PLND_FIN_BK_DTE                                          
                      ,ACTL_INV_RECD_DTE                                        
P40716                ,FINCL_INV_STAT_CDE                                       
                 FROM  TINVPAR                                                  
                WHERE  INV_ID = :PV-ACT-CYCLE-ID                                
                  AND  FINCL_INV_STAT_CDE IN ('BK','TC')                        
                  AND  LOC_INV_STAT_CDE = 'IN'                                  
                  AND ((ACTL_INV_DTE     = '9999-09-09') OR                     
                       (ACTL_UNIT_BK_DTE = '9999-09-09') OR                     
                       (ACTL_FIN_BK_DTE  = '9999-09-09') OR                     
                       (ACTL_INV_RECD_DTE= '9999-09-09'))                       
               FOR UPDATE OF                                                    
                        ACTL_INV_DTE                                            
                       ,ACTL_UNIT_BK_DTE                                        
                       ,ACTL_FIN_BK_DTE                                         
                       ,ACTL_INV_RECD_DTE                                       
P40716                 ,FINCL_INV_STAT_CDE                                      
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATIONS.                                        
                                                                                
           PERFORM 2000-PROCESS-DATA                                            
              UNTIL PS-END-OF-ACT-CYCLE-YES.                                    
                                                                                
           PERFORM 3000-DISPLAY-UPDATE-COUNTS.                                  
                                                                                
           CLOSE ACT-CYCLE-FILE                                                 
                 STORE-ACT-OUT-FILE                                             
                 CYCLE-DE-ACT-FILE.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   INITIALIZATION PARA                                          *        
      ******************************************************************        
                                                                                
       1000-INITIALIZATIONS.                                                    
                                                                                
           OPEN INPUT  ACT-CYCLE-FILE                                           
                OUTPUT STORE-ACT-OUT-FILE                                       
                       CYCLE-DE-ACT-FILE.                                       
                                                                                
           SET PS-END-OF-ACT-CYCLE-NO       TO   TRUE.                          
                                                                                
           INITIALIZE   PV-PROGRAM-VARIABLES                                    
                        DCLTINVPAR                                              
                        DCLTINCNTL.                                             
                                                                                
           PERFORM 1200-READ-ACT-CYCLE-FILE.                                    
                                                                                
      ******************************************************************        
      * READS THE INPUT FILE.                                                   
      ******************************************************************        
       1200-READ-ACT-CYCLE-FILE.                                                
                                                                                
           READ ACT-CYCLE-FILE              INTO PV-ACT-CYCLE-REC               
              AT END                                                            
                  SET PS-END-OF-ACT-CYCLE-YES TO TRUE                           
              NOT AT END                                                        
                  ADD 1                        TO   PV-READ-CNT                 
                  MOVE PV-ACT-CYCLE-ID         TO   PV-CYCLE-ID                 
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH RETRIEVES THE INVENTORY ADJUSTED STORE FOR THE           
      * CORRESPONDING CYCLE FROM THE TABLE INVPAR AND UPDATES THE               
      * INVENTORY ADJUSTMENT INFORMATION.                                       
      ******************************************************************        
       2000-PROCESS-DATA.                                                       
                                                                                
           MOVE '2000-PROCESS-DATA' TO PV-PARAGRAPH-NAME.                       
                                                                                
      *    PROCESS DC LOCATION LOGIC HERE                                       
           SET PS-ACTL-CURSOR-EOF-NO TO TRUE.                                   
                                                                                
           PERFORM 7000-ACTL-DATE-CURSOR.                                       
           PERFORM 7700-FETCH-DATE-CURSOR                                       
               UNTIL PS-ACTL-CURSOR-EOF-SW.                                     
                                                                                
           PERFORM 7800-CLOSE-DATE-CURSOR.                                      
           PERFORM 2500-COUNT-ACTIVE-STORES.                                    
                                                                                
           EVALUATE TRUE                                                        
               WHEN PV-ACTIVE-COUNT = 0                                         
                   PERFORM 4000-UPDATE-TINCNTL-TBL                              
                   PERFORM 4100-WRITE-CYCLE-DE-ACT-REC                  INKUT119
                   PERFORM 8700-COMMIT-CHANGES                                  
               WHEN PV-ACTIVE-COUNT > 0                                         
                   PERFORM 5000-WRITE-ACTIVE-RECS                               
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1200-READ-ACT-CYCLE-FILE.                                    
                                                                                
      *****************************************************************         
      * THIS PARAGRAPH WILL TAKE A COUNT OF HOW MANY STORES                     
      *   ARE ACTIVE IN THIS CYCLE.                                             
      *****************************************************************         
       2500-COUNT-ACTIVE-STORES.                                                
                                                                                
           MOVE '2500-COUNT-ACTIVE-STORES' TO PV-PARAGRAPH-NAME.                
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
               EXEC SQL                                                         
                   SELECT COUNT (*)                                             
                     INTO  :DB-TOT-CNT-ZERO-QTY                                 
                           :DB-TOT-CNT-ZERO-QTY-IND                             
                     FROM TINVPAR                                               
                    WHERE INV_ID = :PV-ACT-CYCLE-ID                             
                        AND ACTL_FIN_BK_DTE = '9999-09-09'                      
                        AND LOC_INV_STAT_CDE = 'IN'                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       MOVE 0 TO PV-ACTIVE-COUNT                                
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED ON COUNT INV_PARM'          
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                   WHEN SQLCODE = 0                                             
                        MOVE DB-TOT-CNT-ZERO-QTY TO PV-ACTIVE-COUNT             
                   WHEN OTHER                                                   
                        MOVE 'COUNT OF ACT STORES'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                        DISPLAY '**** FAILED INVPAR COUNT'                      
                        DISPLAY '**** FOR CYCLE: ' PV-ACT-CYCLE-ID              
                        DISPLAY 'SQLCODE IS: ' SQLCODE                          
                        DISPLAY '**** ON DB2 TINVPAR TABLE'                     
                        PERFORM 9000-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
      *****************************************************************         
      * THIS PARAGRAPH DISPLAYS PROGRAM COUNTS.                                 
      *****************************************************************         
       3000-DISPLAY-UPDATE-COUNTS.                                              
      *                                                                         
           DISPLAY 'INKUP136  PROGRAM'.                                         
           DISPLAY 'ACTIVE RECORDS OUTPUTTED IS: ' PV-OUT-WRITE-CNT.            
           DISPLAY 'NUMBER OF CYCLES DEACT   IS: ' PV-DEACT-CNT.                
                                                                                
           DISPLAY '    ******************************************'             
           DISPLAY '    * TABLE (TINVPAR) COUNTS-UPDATED COLUMNS *'             
           DISPLAY '    ******************************************'.            
           DISPLAY '    *                                        *'.            
           DISPLAY '    *   ACTUAL INVENTORY DATE UPDATES  ',                   
                              PA-ACTL-INV-DATE, '   *'.                         
           DISPLAY '    *   ACTUAL UNIT BOOK DATE UPDATES  ',                   
                              PA-ACTL-UNIT-BK-DATE, '   *'.                     
           DISPLAY '    *   ACTUAL FINANCIAL BOOK UPDATES  ',                   
                              PA-ACTL-FIN-BK-DATE, '   *'.                      
           DISPLAY '    *   ACTUAL INV RECD DATE UPDATES   ',                   
                              PA-ACTL-INV-RECD-DATE, '   *'.                    
P40716     DISPLAY '    *   FINCL INV STAT CODE  UPDATES   ',                   
P40716                        PA-FINCL-INV-STAT-CDE, '   *'.                    
           DISPLAY '    *                                        *'.            
           DISPLAY '    ******************************************'.            
                                                                                
                                                                                
      *****************************************************************         
      * THIS PARAGRAPH UPDATES TINCNTL TABLE.                                   
      *****************************************************************         
       4000-UPDATE-TINCNTL-TBL.                                                 
                                                                                
           MOVE '4000-UPDATE-TINCNTL-TBL' TO PV-PARAGRAPH-NAME.                 
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
               EXEC SQL                                                         
                    UPDATE TINCNTL                                              
                       SET ACTV_IND          = 'N'                              
                    WHERE INV_ID = :PV-ACT-CYCLE-ID                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       DISPLAY '*** INKUP136 ABEND ***'                         
                       DISPLAY '**** FAILED CYCLE UPDATE'                       
                                                PV-ACT-CYCLE-ID                 
                       DISPLAY '**** ON DB2 TINCNTL TABLE'                      
                       MOVE 'UPDATING CYCLE TABLE'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9000-SQL-ABEND                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED ON OPEN OF INV_PARM'        
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                   WHEN SQLCODE = 0                                             
                        MOVE PV-ACT-CYCLE-ID     TO PV-INV-ID                   
                                                    PV-CYCLE-INV-ID     INKUT119
                        DISPLAY 'INVENTORY CYCLE '  PV-INV-ID           INKUT119
                                                 '  DE-ACTIVATED'       INKUT119
                        ADD 1 TO PV-DEACT-CNT                                   
                   WHEN OTHER                                                   
                       MOVE 'UPDATING CYCLE TABLE'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9000-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
      ******************************************************************INKUT119
      *  WRITE A RECORD TO THE CYCLES DE-ACTIVATED FILE                 INKUT119
      ******************************************************************INKUT119
       4100-WRITE-CYCLE-DE-ACT-REC.                                     INKUT119
                                                                        INKUT119
           MOVE PV-CYCLE-DE-ACT-REC TO CYCLE-DE-ACT-REC.                INKUT119
           WRITE CYCLE-DE-ACT-REC.                                      INKUT119
                                                                        INKUT119
                                                                                
      *****************************************************************         
      * THIS WRITES THE ACTIVE STORE RECS FOR THE OPEN CYCLE                    
      *****************************************************************         
       5000-WRITE-ACTIVE-RECS.                                                  
                                                                                
           MOVE '5000-WRITE-ACTIVE-RECS' TO PV-PARAGRAPH-NAME.                  
                                                                                
           PERFORM 5100-OPEN-CURSOR-TINVPAR.                                    
                                                                                
           SET PS-END-OF-FETCHES-NO TO TRUE.                                    
           SET PS-FIRST-TIME-YES    TO TRUE.                                    
                                                                                
           PERFORM 5400-FETCH-NEXT-TINVPAR.                                     
                                                                                
           PERFORM 5200-WRITE-ACTIVE-RECS                                       
             UNTIL PS-END-OF-FETCHES-YES.                                       
                                                                                
           PERFORM 6000-CLOSE-CURSOR-TINVPAR.                                   
                                                                                
      *****************************************************************         
      * THIS WRITES THE ACTIVE STORE RECS FOR THE OPEN CYCLE                    
      *****************************************************************         
       5100-OPEN-CURSOR-TINVPAR.                                                
                                                                                
           MOVE '5100-OPEN-CURSOR-TINVPAR' TO PV-PARAGRAPH-NAME.                
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
              EXEC SQL                                                          
                  OPEN INV_PARM_CSR                                             
              END-EXEC                                                          
                                                                                
              EVALUATE TRUE                                                     
                  WHEN SQLCODE = ZERO                                           
                       CONTINUE                                                 
                  WHEN SQLCODE  = +100                                          
                       DISPLAY 'SQL ERROR +100 ON OPEN OF INV_PARM'             
                       PERFORM 9000-SQL-ABEND                                   
                  WHEN SQLCODE = -911                                           
                  WHEN SQLCODE = -904                                           
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED ON OPEN OF INV_PARM'        
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                  WHEN OTHER                                                    
                       DISPLAY 'ERROR ON OPEN OF INV_PARM'                      
                       PERFORM 9000-SQL-ABEND                                   
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *   WRITES INTO THE OUTPUT FILE                                  *        
      ******************************************************************        
       5200-WRITE-ACTIVE-RECS.                                                  
                                                                                
           MOVE '5200-WRITE-ACTIVE-RECS' TO PV-PARAGRAPH-NAME.                  
                                                                                
           MOVE INVPAR-INV-ID         TO PV-CYCLE-ID.                       INKR
           MOVE INVPAR-LOC-NBR        TO PV-STORE-LOCATION.                 INKR
           MOVE INVPAR-PLND-INV-DTE   TO PV-PLAN-INV-DATE.                  INKR
           WRITE STORE-ACT-OUT-RECORD FROM PV-OUTPUT-DETAIL.                    
           ADD 1                      TO PV-OUT-WRITE-CNT.                      
                                                                                
           PERFORM 5400-FETCH-NEXT-TINVPAR.                                     
                                                                                
      ******************************************************************        
      *   GET ANOTHER TINVPAR ROW FOR PROCESSING                       *        
      ******************************************************************        
       5400-FETCH-NEXT-TINVPAR.                                                 
                                                                                
           MOVE '5400-FETCH-NEXT-TINVPAR' TO PV-PARAGRAPH-NAME.                 
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
               EXEC SQL                                                     INKR
                   FETCH INV_PARM_CSR                                       INKR
                   INTO  :INVPAR-INV-ID                                     INKR
                       , :INVPAR-LOC-NBR                                    INKR
                       , :INVPAR-PLND-INV-DTE                               INKR
               END-EXEC                                                     INKR
                                                                        INKRP101
              EVALUATE TRUE                                                     
                  WHEN SQLCODE = ZERO                                           
                       SET PS-FIRST-TIME-NO   TO TRUE                           
                  WHEN SQLCODE  = +100                                          
                       PERFORM 5500-CHECK-FIRST-TIME                            
                  WHEN SQLCODE = -911                                           
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED CLOSE OF INV_PARM'          
                      MOVE 'FETCH ROW FROM CURSOR'                      INKRP101
                                            TO PV-DB2-OPER-ATTEMPTED    INKRP101
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                  WHEN OTHER                                                    
                       MOVE 'FETCH ROW FROM CURSOR'                     INKRP101
                                            TO PV-DB2-OPER-ATTEMPTED    INKRP101
                       DISPLAY 'ERROR ON FETCH OF INV_PARM'                     
                       PERFORM 9000-SQL-ABEND                                   
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
                                                                                
      ******************************************************************        
      *   A ROW SHOULD BE FOUND ON THE FIRST FETCH                     *        
      ******************************************************************        
       5500-CHECK-FIRST-TIME.                                                   
                                                                                
           MOVE '5500-CHECK-FIRST-TIME' TO PV-PARAGRAPH-NAME.                   
                                                                                
           EVALUATE TRUE                                                        
      **************************************                                    
      *       ABEND IF WE CAN NOT FIND ANY RECS ON THE FIRST TRY                
      **************************************                                    
              WHEN PS-FIRST-TIME-YES                                            
                   MOVE 'FETCHING STORE TABLE'                                  
                                 TO  PV-DB2-OPER-ATTEMPTED                      
                   PERFORM 9000-SQL-ABEND                                       
      **************************************                                    
      *       THIS IS AT END OF FETCHES                                         
      **************************************                                    
              WHEN OTHER                                                        
                   SET PS-END-OF-FETCHES-YES TO TRUE                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *   CLOSE THE TINVPAR TABLE CURSOR                                        
      ******************************************************************        
       6000-CLOSE-CURSOR-TINVPAR.                                               
                                                                                
           MOVE '6000-CLOSE-CURSOR-TINVPAR' TO PV-PARAGRAPH-NAME.               
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
              EXEC SQL                                                          
                  CLOSE INV_PARM_CSR                                            
              END-EXEC                                                          
                                                                                
              EVALUATE TRUE                                                     
                  WHEN SQLCODE = ZERO                                           
                       CONTINUE                                                 
                  WHEN SQLCODE  = +100                                          
                       PERFORM 9000-SQL-ABEND                                   
                  WHEN SQLCODE = -911                                           
                  WHEN SQLCODE = -904                                           
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED ON CLOSE INV_PARM'          
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                  WHEN OTHER                                                    
                       DISPLAY 'ERROR ON CLOSE OF INV_PARM'                     
                       PERFORM 9000-SQL-ABEND                                   
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  PARAGRAPH SERVES TO OPEN THE ACTUAL DATE CURSOR FOR PROCESSING         
      ******************************************************************        
       7000-ACTL-DATE-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN UPDATE_ACTL_CSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7000-ACTL-DATE-CURSOR'                                 
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE PE-MSG-04  TO PV-DB2-OPER-ATTEMPTED                     
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * PARAGRAPH CONTROLS THE FETCH AND DISPOSITION OF DATA RETURNED. *        
      *  FETCH VALUES FROM DB2 CALL INTO COBOL VARIABLES AND EXAMINES  *        
      *  THE SQLCODE RETURNED. IF FETCH WAS SUCCESSFUL, THEN PERFORM   *        
      *  3000-PROCESS-ENTRIES AND UPDATE THE DB2 COLUMN REQUIRED.      *        
      ******************************************************************        
       7700-FETCH-DATE-CURSOR.                                                  
                                                                                
           PERFORM WITH TEST AFTER                                              
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    ((SQLCODE = ZERO)                                           
                             OR                                                 
                    (SQLCODE = +100))                                           
           EXEC SQL                                                             
                FETCH UPDATE_ACTL_CSR                                           
                INTO  :INVPAR-LOC-NBR                                           
                   ,  :INVPAR-ACTL-INV-DTE                                      
                   ,  :INVPAR-ACTL-UNIT-BK-DTE                                  
                   ,  :INVPAR-ACTL-FIN-BK-DTE                                   
                   ,  :INVPAR-PLND-INV-DTE                                      
                   ,  :INVPAR-PLND-UNIT-BK-DTE                                  
                   ,  :INVPAR-PLND-FIN-BK-DTE                                   
                   ,  :INVPAR-ACTL-INV-RECD-DTE                                 
P40716             ,  :INVPAR-FINCL-INV-STAT-CDE                                
           END-EXEC                                                             
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                     PERFORM 7900-CHECK-DC-LOC-TYP                              
P40716               IF XMT00001-LOC-TYP-CDE = 'DC' OR 'RV' OR 'CS'             
                         PERFORM 8000-PROCESS-ENTRIES                           
                     END-IF                                                     
                 WHEN SQLCODE = +100                                            
                     SET PS-ACTL-CURSOR-EOF-SW TO TRUE                          
                 WHEN SQLCODE = -904                                            
                 WHEN SQLCODE = -913                                            
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                        MOVE '7700-FETCH-DATE-CURSOR'                           
                                              TO PV-PARAGRAPH-NAME              
                        MOVE PE-MSG-05        TO PV-DB2-OPER-ATTEMPTED          
                        PERFORM 9000-SQL-ABEND                                  
                    END-IF                                                      
                 WHEN SQLWARN0 NOT = SPACES                                     
                 WHEN SQLCODE < ZERO                                            
                 WHEN SQLCODE > ZERO                                            
                     MOVE '7700-FETCH-DATE-CURSOR'                              
                              TO PV-PARAGRAPH-NAME                              
                     MOVE PE-MSG-05 TO PV-DB2-OPER-ATTEMPTED                    
                     PERFORM 9000-SQL-ABEND                                     
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  PARAGRAPH CLOSE THE DATE CURSOR                                        
      ******************************************************************        
       7800-CLOSE-DATE-CURSOR.                                                  
                                                                                
           EXEC SQL                                                             
               CLOSE UPDATE_ACTL_CSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7800-CLOSE-DATE-CURSOR'                                
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE PE-MSG-06  TO PV-DB2-OPER-ATTEMPTED                     
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  PARAGRAPH CHECK DC LOCATION                                            
      ******************************************************************        
       7900-CHECK-DC-LOC-TYP.                                                   
                                                                                
           EXEC SQL                                                             
               SELECT LOC_TYP_CDE                                               
                 INTO :XMT00001-LOC-TYP-CDE                                     
                 FROM XMT_LOC                                                   
                WHERE LOC_NBR = :INVPAR-LOC-NBR                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7900-CHECK-DC-LOC-TYP'                                 
                                   TO PV-PARAGRAPH-NAME                         
                   MOVE PE-MSG-08  TO PV-DB2-OPER-ATTEMPTED                     
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SAVE THE TINVPAR DATA, CHECK DATES AND UPDATE TINVPAR                  
      *  ROW IF ONE OF THE DATES IS NOT SET YET                                 
      ******************************************************************        
       8000-PROCESS-ENTRIES.                                                    
                                                                                
           MOVE INVPAR-LOC-NBR            TO SV-LOC-NBR                         
                                             SV-STORE-NBR.                      
           MOVE INVPAR-ACTL-INV-DTE       TO SV-ACT-INV-DTE.                    
           MOVE INVPAR-ACTL-UNIT-BK-DTE   TO SV-ACT-UNIT-BK-DTE.                
           MOVE INVPAR-ACTL-FIN-BK-DTE    TO SV-ACT-FIN-BK-DTE.                 
           MOVE INVPAR-ACTL-INV-RECD-DTE  TO SV-ACT-INV-RECD-DTE.               
           MOVE INVPAR-PLND-INV-DTE       TO SV-PLN-INV-DTE.                    
           MOVE INVPAR-PLND-UNIT-BK-DTE   TO SV-PLN-UNIT-BK-DTE.                
           MOVE INVPAR-PLND-FIN-BK-DTE    TO SV-PLN-FIN-BK-DTE.                 
P40716     MOVE INVPAR-FINCL-INV-STAT-CDE TO SV-FINCL-INV-STAT-CDE.             
                                                                                
           IF INVPAR-ACTL-INV-DTE  = '9999-09-09'                               
              SET ACTUAL-INV-DATE-UPDATE    TO TRUE                             
              PERFORM 8200-UPDATE-DB2-DATE                                      
           END-IF.                                                              
      *                                                                         
           IF INVPAR-ACTL-UNIT-BK-DTE      = '9999-09-09'                       
              SET ACTUAL-UNIT-BK-UPDATE     TO TRUE                             
              PERFORM 8200-UPDATE-DB2-DATE                                      
           END-IF.                                                              
      *                                                                         
           IF INVPAR-ACTL-FIN-BK-DTE     = '9999-09-09'                         
              SET ACTUAL-FIN-BK-UPDATE      TO TRUE                             
              PERFORM 8200-UPDATE-DB2-DATE                                      
           END-IF.                                                              
                                                                                
      *                                                                         
           IF INVPAR-ACTL-INV-RECD-DTE    = '9999-09-09'                        
              SET ACTUAL-INV-RECD-UPDATE      TO TRUE                           
              PERFORM 8200-UPDATE-DB2-DATE                                      
           END-IF.                                                              
                                                                                
P40716     IF XMT00001-LOC-TYP-CDE = 'RV' OR 'CS'                               
P40716        SET FINCL-INV-STAT-CDE-UPDATE TO TRUE                             
P40716        MOVE 'TP' TO INVPAR-FINCL-INV-STAT-CDE                            
P40716        PERFORM 8200-UPDATE-DB2-DATE                                      
P40716     END-IF.                                                              
                                                                                
      *****************************************************************         
      * PERFORMED BY 3000-PROCESS-ENTRIES.SETS WHICH DATE WAS INVALID *         
      *             (1) ACTUAL INVENTORY DATE                         *         
      *             (2) ACTUAL UNIT BOOK DATE                         *         
      *             (3) ACTUAL FINANCIAL BOOK DATE                    *         
      *****************************************************************         
       8200-UPDATE-DB2-DATE.                                                    
                                                                                
           EVALUATE PG-UPDATE-TYPE                                              
               WHEN '1'                                                         
                       EXEC SQL                                                 
                           UPDATE TINVPAR                                       
                           SET ACTL_INV_DTE    = :INVPAR-PLND-INV-DTE           
                           WHERE CURRENT OF UPDATE_ACTL_CSR                     
                       END-EXEC                                                 
                       PERFORM 8300-DISPLAY-MESSAGES                            
               WHEN '2'                                                         
                       EXEC SQL                                                 
                         UPDATE TINVPAR                                         
                         SET ACTL_UNIT_BK_DTE = :INVPAR-PLND-UNIT-BK-DTE        
                         WHERE CURRENT OF UPDATE_ACTL_CSR                       
                       END-EXEC                                                 
                       PERFORM 8300-DISPLAY-MESSAGES                            
               WHEN '3'                                                         
                       EXEC SQL                                                 
                         UPDATE TINVPAR                                         
                         SET ACTL_FIN_BK_DTE  = :INVPAR-PLND-FIN-BK-DTE         
                         WHERE CURRENT OF UPDATE_ACTL_CSR                       
                       END-EXEC                                                 
                       PERFORM 8300-DISPLAY-MESSAGES                            
               WHEN '4'                                                         
                       EXEC SQL                                                 
                         UPDATE TINVPAR                                         
                         SET ACTL_INV_RECD_DTE = :INVPAR-PLND-INV-DTE           
                         WHERE CURRENT OF UPDATE_ACTL_CSR                       
                       END-EXEC                                                 
                       PERFORM 8300-DISPLAY-MESSAGES                            
P40716         WHEN '5'                                                         
P40716                 EXEC SQL                                                 
P40716                   UPDATE TINVPAR                                         
P40716                   SET FINCL_INV_STAT_CDE                                 
P40716                                     = :INVPAR-FINCL-INV-STAT-CDE         
P40716                   WHERE CURRENT OF UPDATE_ACTL_CSR                       
P40716                 END-EXEC                                                 
P40716                 PERFORM 8300-DISPLAY-MESSAGES                            
               WHEN OTHER                                                       
                   MOVE '8200-UPDATE-DB2-DATE'                                  
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE PE-MSG-07    TO PV-DB2-OPER-ATTEMPTED                   
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * DISPLAYS MESSAGES ABOUT STORE NUMBER WHOSE VALUES WERE UPDATED *        
      * VALUES DISPLAYED ARE BEFORE (DEFAULT) DATE AND UPDATED DATE    *        
      *   EACH WHEN CLAUSE WILL ADD '1' TO THAT COLUMN'S ACCUMULATOR   *        
      ******************************************************************        
       8300-DISPLAY-MESSAGES.                                                   
                                                                                
           EVALUATE PG-UPDATE-TYPE                                              
               WHEN '1'                                                         
                  DISPLAY '  STORE NUMBER UPDATED   ', SV-STORE-NBR             
                  DISPLAY '    ACTUAL INV DATE BEFORE UPDATE ',                 
                                                SV-ACT-INV-DTE                  
                  DISPLAY '    ACTUAL INV DATE AFTER UPDATE  ',                 
                                                INVPAR-PLND-INV-DTE             
                  DISPLAY '                           '                         
                  ADD 1 TO PA-ACTL-INV-DATE                                     
               WHEN '2'                                                         
                  DISPLAY '  STORE NUMBER UPDATED   ', SV-STORE-NBR             
                  DISPLAY '    ACTUAL UNIT BOOK DATE BEFORE UPDATE ',           
                                                SV-ACT-UNIT-BK-DTE              
                  DISPLAY '    ACTUAL UNIT BOOK DATE AFTER UPDATE  ',           
                                                INVPAR-PLND-UNIT-BK-DTE         
                  DISPLAY '                           '                         
                  ADD 1 TO PA-ACTL-UNIT-BK-DATE                                 
               WHEN '3'                                                         
                  DISPLAY '  STORE NUMBER UPDATED   ', SV-STORE-NBR             
                  DISPLAY '    ACTUAL FINANCIAL BK DATE BEFORE UPDATE ',        
                                                SV-ACT-FIN-BK-DTE               
                  DISPLAY '    ACTUAL FINANCIAL BK DATE AFTER UPDATE  ',        
                                                INVPAR-PLND-FIN-BK-DTE          
                  DISPLAY '                           '                         
                  ADD 1 TO PA-ACTL-FIN-BK-DATE                                  
               WHEN '4'                                                         
                  DISPLAY '  STORE NUMBER UPDATED   ', SV-STORE-NBR             
                  DISPLAY '    ACTUAL INVENTORY RECD DATE B4 UPDATE   ',        
                                                SV-ACT-INV-RECD-DTE             
                  DISPLAY '    ACTUAL INVENTORY RECD DATE AFTER UPDTE ',        
                                                INVPAR-PLND-INV-DTE             
                  DISPLAY '                           '                         
                  ADD 1 TO PA-ACTL-INV-RECD-DATE                                
P40716         WHEN '5'                                                         
P40716            DISPLAY '  STORE NUMBER UPDATED   ', SV-STORE-NBR             
P40716            DISPLAY '     FINCL-INV-STAT-CDE   BEFORE UPDATE   ',         
P40716                                          SV-FINCL-INV-STAT-CDE           
P40716            DISPLAY '     FINCL-INV-STAT-CDE   AFTER UPDATE ',            
P40716                                      INVPAR-FINCL-INV-STAT-CDE           
P40716            DISPLAY '                           '                         
P40716            ADD 1 TO PA-FINCL-INV-STAT-CDE                                
      *        WHEN OTHER                                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * COMMIT CHANGES MADE                                                     
      ******************************************************************        
       8700-COMMIT-CHANGES.                                                     
                                                                                
           MOVE '8700-COMMIT-CHANGES' TO PV-PARAGRAPH-NAME.                     
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
               EXEC SQL                                                     0610
                   COMMIT                                                   0611
               END-EXEC                                                     0612
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                       ADD +1 TO PV-COMMIT-COUNT                        06030000
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                    IF PV-SQL-SUB > PC-MAX-RETRIES                              
                      DISPLAY 'MAX RETRIES EXCEEDED ON COMMIT TINCNTL'          
                      PERFORM 9000-SQL-ABEND                                    
                    END-IF                                                      
                   WHEN OTHER                                                   
                       MOVE 'COMMIT OF CYCLE TABLE CHANGE BAD'                  
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9000-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUP136'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
