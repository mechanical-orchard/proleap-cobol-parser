000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    AHKUT604.                                                 
000300 AUTHOR.        MIKE FRIESS.                                              
000400 INSTALLATION.  KOHLS.                                                    
000500 DATE-WRITTEN   AUG, 2003.                                                
000600 DATE-COMPILED.                                                           
000700                                                                          
000800* ************************************************************** *        
000900* * AHKUT604.  "ARCHIVE HISTORY" PROJECT.  EXTRACT PROGRAM.    * *        
001000* *                                                            * *        
001100* * PURPOSE:                                                   * *        
001200* * THIS PROGRAM CREATES AN EXTRACT FILE FROM: TSHIPST.        * *        
001300* * THIS EXTRACT FILE CONSISTS OF ROWS ELIGIBLE TO BE:         * *        
001400* * 1) LOADED TO THE "ARCHIVE/HISTORY" VERSIONS OF THE TABLES. * *        
001500* * 2) DELETED FROM THE OPERATIONAL VERSIONS OF THE TABLES.    * *        
001600* *                                                            * *        
001700* * PROCESSING OVERVIEW:                                       * *        
001800* * THIS PROGRAM READS A "DRIVER" FILE. FOR EACH RECORD READ,  * *        
001900* * 1) INCREMENT COUNT OF RECORDS READ.                        * *        
002000* * 2) ATTEMPT TO FIND RELATED ROW(S) ON THE TABLE.            * *        
002100* *    2.A) IF NO MATCH(ES):                                   * *        
002200* *         2.A.1) PROCESSING CONTINUES                        * *        
002300* *         2.A.2) DO NOT PERFORM ERROR/REPORTING ROUTINES;    * *        
002400* *                THIS SYSTEM DOES NOT CLEANSE DATA NOR REPORT* *        
002500* *                ON UNCLEAN DATA.  IT SIMPLY MIGRATES        * *        
002600* *                CONDITIONS OCCURRING IN OPERATIONAL         * *        
002700* *                TABLES OVER TO THE ARCHIVE/HISTORY TABLES.  * *        
002800* *    2.B) IF MATCH(ES):                                      * *        
002900* *         2.B.1) INCREMENT COUNT OF ROW(S) FOUND.            * *        
003000* *         2.B.2) WRITE THESE ROW(S) TO THE EXTRACT FILE.     * *        
003100* *         2.B.3) INCREMENT COUNT OF RECORDS WRITTEN.         * *        
003200* *         2.B.4) DO NOT REMOVE THE ROW(S) FROM THE TABLE     * *        
003300* *                AT THIS TIME.  THEY WILL BE REMOVED IN OTHER* *        
003400* *                PROCESSING, AFTER HAVING BEEN LOADED TO THE * *        
003500* *                ARCHIVE/HISTORY TABLE.                      * *        
003600* * 3) EVERY NTH RECORD (IE: INITIAL SETTING 1000), REPORT     * *        
003700* *    RECORD COUNT AND CURRENT DATE/TIME.  THIS WILL ASSIST   * *        
003800* *    WITH RUN-TIME ESTIMATE OF TIME REMAINING TO COMPLETE.   * *        
003900**************************************************************** *        
004202 ENVIRONMENT DIVISION.                                                    
004203 CONFIGURATION SECTION.                                                   
004204 SOURCE-COMPUTER.        IBM-3090.                                        
004300 OBJECT-COMPUTER.        IBM-3090.                                        
004400                                                                          
004500 INPUT-OUTPUT SECTION.                                                    
004600 FILE-CONTROL.                                                            
004700                                                                          
004800     SELECT IN-DRIVER-FILE                ASSIGN TO INP01.                
004900     SELECT OUT-EXTRACT-FILE              ASSIGN TO OUT01.                
005000                                                                          
005100******************************************************************        
005200 DATA DIVISION.                                                           
005300 FILE SECTION.                                                            
005400                                                                          
005410**** TSHIPMT:                                                             
005500 FD  IN-DRIVER-FILE                                                       
005600     RECORDING MODE IS F                                                  
005700     LABEL RECORDS ARE STANDARD                                           
005800     BLOCK CONTAINS 0 RECORDS.                                            
005900 01  TSHIPMT-EXTRACT-DATA            PIC  X(500).                         
006000                                                                          
006100**** TSHIPST:                                                             
006200 FD  OUT-EXTRACT-FILE                                                     
006300     RECORDING MODE IS F                                                  
006400     LABEL RECORDS ARE STANDARD                                           
006500     BLOCK CONTAINS 0 RECORDS.                                            
006610 01  OUT-EXTRACT-RECORD              PIC  X(120).                         
006700                                                                          
006800                                                                          
006900                                                                          
007000 WORKING-STORAGE SECTION.                                                 
007100                                                                          
007200 01  PC-PROGRAM-CONSTANTS.                                                
007300     05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'AHKUT604'.          
007400     05  PC-PGM-PROGRESS-DESC.                                            
007500         10  PC-BEGINNING            PIC X(09) VALUE 'BEGINNING'.         
007600         10  PC-CONTINUING           PIC X(10) VALUE                      
007700                                               'CONTINUING'.              
007800         10  PC-ENDING               PIC X(06) VALUE 'ENDING'.            
007900         10  PC-ABENDING             PIC X(08) VALUE 'ABENDING'.          
008000     05  PC-ABEND-INFO.                                                   
008100         10  PC-ABEND                PIC X(11) VALUE                      
008200                                               '** ABEND **'.             
008300         10  PC-ABEND-MSG-PARA       PIC X(15) VALUE                      
008400                                               '- PARA       : '.         
008500         10  PC-ABEND-MSG-DISPLAY-PARA                                    
008600                                     PIC X(15) VALUE                      
008700                                               '  CALLED PARA: '.         
008800         10  PC-ABEND-MSG-NUMBER     PIC X(15) VALUE                      
008900                                               '  FOR ABEND# : '.         
009000         10  PC-ABEND-MSG-DESC       PIC X(15) VALUE                      
009100                                               '- ERROR IS   : '.         
009200         10  PC-ABEND-MSG-INDENT-15  PIC X(15) VALUE SPACES.              
009300         10  PC-ABEND-MSG-ACTION     PIC X(15) VALUE                      
009400                                               '- ACTION     : '.         
009500     05  PC-HOW-OFTEN-TO-DISPLAY     PIC 9(9)  VALUE 5000.                
009600                                                                          
009700 01  PS-PROGRAM-SWITCHES.                                                 
009800     05  PS-IN-DRIVER-EOF-SW         PIC X(01) VALUE 'N'.                 
009900         88  PS-IN-DRIVER-EOF                  VALUE 'Y'.                 
010000         88  PS-IN-DRIVER-NOT-EOF              VALUE 'N'.                 
010100     05  PS-CURSOR-HAS-DATA-SW       PIC X(01) VALUE 'N'.                 
010200         88  PS-CURSOR-HAS-DATA                VALUE 'Y'.                 
010300         88  PS-CURSOR-HAS-NO-DATA             VALUE 'N'.                 
010400     05  PS-TABLE-LOOP-DONE-SW       PIC X(01) VALUE 'N'.                 
010500         88  PS-TABLE-LOOP-DONE                VALUE 'Y'.                 
010600         88  PS-TABLE-LOOP-NOT-DONE            VALUE 'N'.                 
010700                                                                          
010800 01  PV-PROGRAM-VARIABLES.                                                
010900     05  PV-LENGTH-TSHIPMT           PIC 9(09) VALUE ZERO.                
011100     05  PV-IN-DRIVER-RECS-READ-CNT  PIC  9(9) VALUE 0.                   
011200     05  PV-RECS-WITH-MATCHES-CNT    PIC  9(9) VALUE 0.                   
011300     05  PV-TABLE-ROW-SELECT-CNT     PIC  9(9) VALUE 0.                   
011400     05  PV-EXTRACT-WRITTEN-CNT      PIC  9(9) VALUE 0.                   
011500                                                                          
011600     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
011700     05  PV-DB2-OPERATION-MSG-1      PIC X(79) VALUE SPACES.              
011800     05  PV-DB2-TABLE-NAME           PIC X(18) VALUE SPACES.              
011900     05  PV-DISPLAY-NTH-MULTIPLE     PIC 9(9)  VALUE 0.                   
012000     05  PV-DISPLAY-NTH-REMAINDER    PIC 9(9)  VALUE 0.                   
012100     05  PV-SQLCODE                  PIC +999  VALUE ZERO.                
012110     05  PV-NULL                     PIC S9(04) VALUE +0                  
012120                                                   COMP.                  
012200                                                                          
012300     05  PV-TMST                     PIC X(26) VALUE SPACES.              
012400     05  PV-TIMESTAMP-DETAIL                                              
012500         REDEFINES                                                        
012600         PV-TMST.                                                         
012700         10  PV-TIMESTAMP-19.                                             
012800             15  PV-TMS-DATE.                                             
012900                 20  PV-TMS-YEAR.                                         
013000                     25  PV-TMS-CC   PIC X(02).                           
013100                     25  PV-TMS-YY   PIC X(02).                           
013200                 20  FILLER          PIC X(01).                           
013300                 20  PV-TMS-MONTH    PIC X(02).                           
013400                 20  FILLER          PIC X(01).                           
013500                 20  PV-TMS-DAY      PIC X(02).                           
013600             15  FILLER              PIC X(01).                           
013700             15  PV-TMS-HOUR         PIC X(02).                           
013800             15  FILLER              PIC X(01).                           
013900             15  PV-TMS-MINUTE       PIC X(02).                           
014000             15  FILLER              PIC X(01).                           
014100             15  PV-TMS-SECOND       PIC X(02).                           
014200         10  FILLER                  PIC X(01).                           
014300         10  PV-TMS-MSECOND          PIC X(06).                           
014400                                                                          
014500 01  DM-DISPLAY-MESSAGE.                                                  
014600     05  DM-01-10.                                                        
014700         10  DM-PROGRAM              PIC X(08) VALUE SPACE.               
014800         10  FILLER                  PIC X(02) VALUE ': '.                
014900     05  DM-11-80.                                                        
015000         10  FILLER                  PIC X(37) VALUE                      
015100                        'SELECT ROWS MATCH SHPTPO DRIVER FILE '.          
015200         10  FILLER                  PIC X(30) VALUE                      
015300                        'CREATING TSHIPST EXTRACT FILE.'.                 
015400         10  FILLER                  PIC X(01) VALUE SPACE.               
015500         10  FILLER                  PIC X(01) VALUE ALL '*'.             
015600     05  DM-81-120.                                                       
015700         10  FILLER                  PIC X(40) VALUE ALL '*'.             
015800                                                                          
015900 01  DP-DISPLAY-PROGRESS-MSG.                                             
016000     05  DP-PROGRAM                  PIC X(08) VALUE SPACE.               
016100     05  FILLER                      PIC X(02) VALUE SPACE.               
016200     05  DP-STATUS-DESCRIPTION       PIC X(10) VALUE SPACE.               
016300     05  FILLER                      PIC X(04) VALUE ' AT '.              
016400     05  DP-TIMESTAMP-19             PIC X(19) VALUE SPACE.               
016500     05  FILLER                      PIC X(02) VALUE '. '.                
016600     05  FILLER                      PIC X(03) VALUE                      
016700                                         'IN:'.                           
016800     05  DP-IN-CNT                   PIC Z,ZZZ,ZZ9                        
016900                                               VALUE ZERO.                
017000     05  FILLER                      PIC X(02) VALUE ', '.                
017600     05  FILLER                      PIC X(14) VALUE                      
017700                                         'ROWS SELECTED:'.                
017800     05  DP-ROWS-SELECTED-CNT        PIC Z,ZZZ,ZZ9                        
017900                                               VALUE ZERO.                
018000     05  FILLER                      PIC X(02) VALUE ', '.                
018100     05  FILLER                      PIC X(04) VALUE                      
018200                                         'OUT:'.                          
018300     05  DP-OUT-CNT                  PIC Z,ZZZ,ZZ9                        
018400                                               VALUE ZERO.                
018401     05  FILLER                      PIC X(02) VALUE ', '.                
018402     05  FILLER                      PIC X(08)                            
018403                                     VALUE 'SHIP ID:'.                    
018410     05  DPSHIPT-ID                  PIC 9(09) VALUE ZERO.                
018500     05  FILLER                      PIC X(01) VALUE '.'.                 
018600                                                                          
018700 01  ABEND-CODE                      PIC S9(04)                           
018800                                               VALUE ZEROES               
018900                                               COMP SYNC.                 
019000                                                                          
019100*---------------------------------------------------------------*         
019200*  DB2 FORMATTED MESSAGE AREA                                             
019300*---------------------------------------------------------------*         
019400     COPY DPWS004.                                                        
019500                                                                          
020300*---------------------------------------------------------------*         
020400*    DB2 AREA FOR TSHIPMT - USE COBOL VARIABLES FOR RECORD      *         
020410*                           LAYOUT DRIVER FILE, INPUT.          *         
020500*---------------------------------------------------------------*         
020600     EXEC SQL                                                             
020700          INCLUDE TSHIPMT                                                 
020800     END-EXEC.                                                            
020900                                                                          
020910*---------------------------------------------------------------*         
020920*    DB2 AREA FOR TSHIPST                                       *         
020930*---------------------------------------------------------------*         
020940     EXEC SQL                                                             
020950          INCLUDE TSHIPST                                                 
020960     END-EXEC.                                                            
020970                                                                          
021000*---------------------------------------------------------------*         
021100*    DB2 AREA FOR COMMUNICATIONS                                *         
021200*---------------------------------------------------------------*         
021300     EXEC SQL                                                             
021400          INCLUDE SQLCA                                                   
021500     END-EXEC.                                                            
021600                                                                          
021700*---------------------------------------------------------------*         
021800*    DEFINE CURSOR FOR TSHIPST :                                *         
021900*    PULL ALL AVAILABLE COLUMNS, SO EXTRACT FILE WILL CONTAIN   *         
022000*    ALL DATA TO POPULATE "ARCHIVE HISTORY" COPY OF THIS TABLE. *         
022100*---------------------------------------------------------------*         
022200     EXEC SQL                                                             
022300          DECLARE TABLE-CURSOR CURSOR FOR                                 
022400           SELECT *                                                       
027502           FROM   TSHIPST                                                 
027503          WHERE  SHIPT_ID = :SHIPMT-SHIPT-ID                              
027507            FOR  FETCH ONLY                                               
027508           WITH  UR                                                       
027600     END-EXEC.                                                            
027700                                                                          
027800                                                                          
027900 PROCEDURE DIVISION.                                                      
028000********************                                                      
028100                                                                          
028200 0000-PROGRAM-MAINLINE.                                                   
028300                                                                          
028400     PERFORM 1000-INITIALIZE.                                             
028500                                                                          
028600     SET PS-IN-DRIVER-NOT-EOF TO TRUE.                                    
028700     PERFORM 2000-PROCESS-DRIVER-FILE                                     
028800         UNTIL PS-IN-DRIVER-EOF.                                          
028900                                                                          
029000     PERFORM 6000-EOJ-ROUTINE.                                            
029100     STOP RUN.                                                            
029200                                                                          
029300                                                                          
029400* ************************************************************** *        
029500* * INITIALIZATIONS                                            * *        
029600* ************************************************************** *        
029700 1000-INITIALIZE.                                                         
029800                                                                          
029900     MOVE PC-CURRENT-PROGRAM-NAME TO DM-PROGRAM.                          
030000     DISPLAY DM-DISPLAY-MESSAGE.                                          
030100                                                                          
030200     MOVE PC-BEGINNING              TO DP-STATUS-DESCRIPTION.             
030300     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
030310                                                                          
030320     COMPUTE PV-LENGTH-TSHIPMT = LENGTH OF DCLTSHIPMT.                    
030400                                                                          
030500     PERFORM 9000-OPEN-IN-DRIVER-FILE.                                    
030600     PERFORM 9700-OPEN-OUT-EXTRACT-FILE.                                  
030700                                                                          
030800                                                                          
030900* ************************************************************** *        
031000* * 1) GET NEXT RECORD FROM DRIVER FILE, AND                   * *        
031100* *    1A- POPULATE KEY (WHERE CLAUSE) FOR TABLE CURSOR        * *        
031200* *    1B- OPEN TABLE CURSOR                                   * *        
031300* *    1C- LOOP THRU TABLE CURSOR TO EXTRACT ALL               * *        
031400* *        ROWS FOR THIS INPUT DRIVER RECORD                   * *        
031500* *    2) FOR EACH ROW,                                        * *        
031600* *       2A- INCREMENT COUNTS                                 * *        
031700* *       2B- WRITE EXTRACT RECORD                             * *        
031800* *    1D- CLOSE TABLE CURSOR                                  * *        
031900* ************************************************************** *        
032000 2000-PROCESS-DRIVER-FILE.                                                
032100                                                                          
032200     PERFORM 9050-READ-IN-DRIVER-FILE.                                    
032300     IF PS-IN-DRIVER-EOF                                                  
032400         CONTINUE                                                         
032500     ELSE                                                                 
032800         PERFORM 2100-CHECK-PGM-PROGRESS                                  
032900         SET PS-TABLE-LOOP-NOT-DONE   TO TRUE                             
033000         SET PS-CURSOR-HAS-NO-DATA    TO TRUE                             
033100         PERFORM 9100-OPEN-TABLE-CURSOR                                   
033200         PERFORM 3000-EXTRACT-ROWS-FROM-TABLE                             
033300             UNTIL PS-TABLE-LOOP-DONE                                     
033400         PERFORM 9190-CLOSE-TABLE-CURSOR                                  
033500     END-IF.                                                              
033600                                                                          
033700                                                                          
033800* ************************************************************** *        
033900* *    EVERY NTH RECORD (IE: INITIAL SETTING 1000), REPORT     * *        
034000* *    RECORD COUNT AND CURRENT DATE/TIME.  THIS WILL ASSIST   * *        
034100* *    WITH RUN-TIME ESTIMATE OF TIME REMAINING TO COMPLETE.   * *        
034200* ************************************************************** *        
034300 2100-CHECK-PGM-PROGRESS.                                                 
034400     DIVIDE    PV-IN-DRIVER-RECS-READ-CNT                                 
034500         BY        PC-HOW-OFTEN-TO-DISPLAY                                
034600         GIVING    PV-DISPLAY-NTH-MULTIPLE                                
034700         REMAINDER PV-DISPLAY-NTH-REMAINDER.                              
034800                                                                          
034900     IF PV-DISPLAY-NTH-REMAINDER = ZERO                                   
035000         MOVE PC-CONTINUING         TO DP-STATUS-DESCRIPTION              
035100         PERFORM 6200-DISPLAY-PGM-PROGRESS                                
035200     END-IF.                                                              
035300                                                                          
035400                                                                          
035500* ************************************************************** *        
035600* * EXTRACT ROWS FROM TABLE, FOR THE CURRENT DRIVER RECORD     * *        
035700* ************************************************************** *        
035800 3000-EXTRACT-ROWS-FROM-TABLE.                                            
035810                                                                          
035900     INITIALIZE DCLTSHIPST.                                               
036000     PERFORM 9150-FETCH-TABLE-CURSOR.                                     
036100     IF PS-TABLE-LOOP-DONE                                                
036200         CONTINUE                                                         
036300     ELSE                                                                 
036400         PERFORM 9750-WRITE-EXTRACT-RECORD                                
036500     END-IF.                                                              
036600                                                                          
036700                                                                          
036800* ************************************************************** *        
036900* * DISPLAY END OF RUN MESSAGE, CLOSE THE OUTPUT FILES         * *        
037000* ************************************************************** *        
037100 6000-EOJ-ROUTINE.                                                        
037200                                                                          
037300     PERFORM 9090-CLOSE-IN-DRIVER-FILE.                                   
037400     PERFORM 9790-CLOSE-OUT-EXTRACT-FILE.                                 
037500                                                                          
037600     MOVE PC-ENDING                 TO DP-STATUS-DESCRIPTION.             
037700     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
037800                                                                          
037900                                                                          
038000* ************************************************************** *        
038100* * DISPLAY PROGRAM PROGRESS.                                  * *        
038200* ************************************************************** *        
038300 6200-DISPLAY-PGM-PROGRESS.                                               
038400                                                                          
038500     EXEC SQL                                                             
038600          SET :PV-TMST = CURRENT TIMESTAMP                                
038700     END-EXEC.                                                            
038800                                                                          
038900     MOVE PC-CURRENT-PROGRAM-NAME   TO DP-PROGRAM.                        
039000     MOVE PV-TIMESTAMP-19           TO DP-TIMESTAMP-19.                   
039100     MOVE PV-IN-DRIVER-RECS-READ-CNT                                      
039200                                    TO DP-IN-CNT.                         
039400     MOVE PV-TABLE-ROW-SELECT-CNT   TO DP-ROWS-SELECTED-CNT.              
039500     MOVE PV-EXTRACT-WRITTEN-CNT    TO DP-OUT-CNT.                        
039510     MOVE SHIPMT-SHIPT-ID           TO DPSHIPT-ID.                        
039600                                                                          
039700     DISPLAY DP-DISPLAY-PROGRESS-MSG.                                     
039800                                                                          
039900                                                                          
040000* ************************************************************** *        
040100* * FILE AND DB2 TABLE ACCESS PARAGRAPHS.                      * *        
040200* * 90XX- INPUT:  DRIVER FILE                                  * *        
040300* * 91XX- INPUT:  TABLE CURSOR                                 * *        
040400* * 97XX- OUTPUT: TABLE EXTRACT                                * *        
040500* ************************************************************** *        
040600                                                                          
040700* ************************************************************** *        
040800* * OPEN  INPUT DRIVER FILE                                    * *        
040900* ************************************************************** *        
041000 9000-OPEN-IN-DRIVER-FILE.                                                
041100     OPEN  INPUT  IN-DRIVER-FILE.                                         
041200                                                                          
041300                                                                          
041400* ************************************************************** *        
041500* * READ  INPUT DRIVER FILE OF ELIGIBLE DATA TO EXTRACT        * *        
041600* ************************************************************** *        
041700 9050-READ-IN-DRIVER-FILE.                                                
041800     READ IN-DRIVER-FILE                                                  
042000          AT END                                                          
042100              SET PS-IN-DRIVER-EOF TO TRUE.                               
042200                                                                          
042300     IF PS-IN-DRIVER-EOF                                                  
042400         CONTINUE                                                         
042500     ELSE                                                                 
042600         ADD +1 TO PV-IN-DRIVER-RECS-READ-CNT                             
042610         MOVE TSHIPMT-EXTRACT-DATA (1:PV-LENGTH-TSHIPMT)                  
042620                             TO DCLTSHIPMT                                
042700     END-IF.                                                              
042800                                                                          
042900                                                                          
043000* ************************************************************** *        
043100* * CLOSE INPUT DRIVER FILE                                    * *        
043200* ************************************************************** *        
043300 9090-CLOSE-IN-DRIVER-FILE.                                               
043400     CLOSE IN-DRIVER-FILE.                                                
043500                                                                          
043600                                                                          
043700* ************************************************************** *        
043800* * OPEN  TABLE CURSOR                                         * *        
043900* ************************************************************** *        
044000 9100-OPEN-TABLE-CURSOR.                                                  
044100     EXEC SQL                                                             
044200          OPEN TABLE-CURSOR                                               
044300     END-EXEC.                                                            
044400                                                                          
044500     EVALUATE TRUE                                                        
044600         WHEN SQLCODE = ZERO                                              
044700              ADD +1 TO PV-RECS-WITH-MATCHES-CNT                          
044800              SET PS-CURSOR-HAS-DATA TO TRUE                              
044900         WHEN SQLCODE = +100                                              
045000              SET PS-TABLE-LOOP-DONE TO TRUE                              
045100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
045200         WHEN SQLCODE  NOT EQUAL ZERO                                     
045300              MOVE '9100-OPEN-TABLE-CURSOR'                               
045400                                 TO PV-PARAGRAPH-NAME                     
045500              MOVE 'OPEN TABLE CURSOR'                                    
045600                                 TO PV-DB2-OPERATION-MSG-1                
045700              MOVE 'TSHIPST'     TO PV-DB2-TABLE-NAME                     
045800              PERFORM 9999-DB2-ABEND                                      
045900     END-EVALUATE.                                                        
046000                                                                          
046100                                                                          
046200* ************************************************************** *        
046300* * FETCH TABLE CURSOR                                         * *        
046400* ************************************************************** *        
046500 9150-FETCH-TABLE-CURSOR.                                                 
046600     EXEC SQL                                                             
046700         FETCH TABLE-CURSOR                                               
046800          INTO                                                            
046900             :SHIPST-SHIPT-ID:PV-NULL                                     
047000            ,:SHIPST-SHIPT-STATUS-CDE:PV-NULL                             
047100            ,:SHIPST-STATUS-DTE:PV-NULL                                   
047200            ,:SHIPST-STATUS-TME:PV-NULL                                   
047300            ,:SHIPST-STATUS-UID:PV-NULL                                   
047400            ,:SHIPST-DELAY-RSN-CDE:PV-NULL                                
047500            ,:SHIPST-CARR-STATUS-CDE:PV-NULL                              
047600            ,:SHIPST-TIME-ZONE-CDE:PV-NULL                                
047700            ,:SHIPST-CITY-NAME:PV-NULL                                    
047800            ,:SHIPST-STATE-CDE:PV-NULL                                    
051902     END-EXEC.                                                            
051903                                                                          
051904     EVALUATE TRUE                                                        
051905         WHEN SQLCODE = ZERO                                              
051906              ADD +1                 TO PV-TABLE-ROW-SELECT-CNT           
051907              SET PS-CURSOR-HAS-DATA TO TRUE                              
052000         WHEN SQLCODE = +100                                              
052100              SET PS-TABLE-LOOP-DONE TO TRUE                              
052200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
052300         WHEN SQLCODE NOT EQUAL ZERO                                      
052400              MOVE '9150-FETCH-TABLE-CURSOR'                              
052500                                 TO PV-PARAGRAPH-NAME                     
052600              MOVE 'FETCH TABLE CURSOR'                                   
052700                                 TO PV-DB2-OPERATION-MSG-1                
052800              MOVE 'TSHIPST'     TO PV-DB2-TABLE-NAME                     
052900              PERFORM 9999-DB2-ABEND                                      
053000     END-EVALUATE.                                                        
053100                                                                          
053200                                                                          
053300* ************************************************************** *        
053400* * CLOSE TABLE CURSOR                                         * *        
053500* ************************************************************** *        
053600 9190-CLOSE-TABLE-CURSOR.                                                 
053700     EXEC SQL                                                             
053800          CLOSE TABLE-CURSOR                                              
053900     END-EXEC.                                                            
054000                                                                          
054100     EVALUATE TRUE                                                        
054200         WHEN SQLCODE = ZERO                                              
054300              CONTINUE                                                    
054400         WHEN SQLWARN0 NOT EQUAL SPACE                                    
054500         WHEN SQLCODE NOT EQUAL ZERO                                      
054600              MOVE '9190-CLOSE-TABLE-CURSOR'                              
054700                                 TO PV-PARAGRAPH-NAME                     
054800              MOVE 'CLOSE TABLE CURSOR'                                   
054900                                 TO PV-DB2-OPERATION-MSG-1                
055000              MOVE 'TSHIPST'     TO PV-DB2-TABLE-NAME                     
055100              PERFORM 9999-DB2-ABEND                                      
055200     END-EVALUATE.                                                        
055300                                                                          
055400                                                                          
055500* ************************************************************** *        
055600* * OPEN OUTPUT EXTRACT FILE                                   * *        
055700* ************************************************************** *        
055800 9700-OPEN-OUT-EXTRACT-FILE.                                              
055900     OPEN OUTPUT OUT-EXTRACT-FILE.                                        
056000                                                                          
056100                                                                          
056200* ************************************************************** *        
056300* * WRITE A RECORD TO THE TABLE EXTRACT FILE                   * *        
056400* ************************************************************** *        
056500 9750-WRITE-EXTRACT-RECORD.                                               
056510     MOVE SPACES TO OUT-EXTRACT-RECORD                                    
056520     MOVE DCLTSHIPST TO OUT-EXTRACT-RECORD                                
056600     WRITE  OUT-EXTRACT-RECORD                                            
056700     ADD +1 TO PV-EXTRACT-WRITTEN-CNT.                                    
056800                                                                          
056900                                                                          
057000* ************************************************************** *        
057100* * CLOSE OUTPUT EXTRACT FILE                                  * *        
057200* ************************************************************** *        
057300 9790-CLOSE-OUT-EXTRACT-FILE.                                             
057400     CLOSE OUT-EXTRACT-FILE.                                              
057500                                                                          
057600                                                                          
057700* ************************************************************** *        
057800* * DISPLAY INFORMATION NEEDED FOR DEBUGGING/MAINTENANCE/SUPPORT *        
057900* * AND CALL THE ABEND PROCESSOR IN THE EVENT THAT AN          * *        
058000* * SQL ERROR OR WARNING CODE OCCURS.                          * *        
058100* ************************************************************** *        
058200 9999-DB2-ABEND.                                                          
058300                                                                          
058400     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058500             ' 9999-DB2-ABEND'.                                           
058600     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058700             ' ** ABEND           **'.                                    
058800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
058900             ' ** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.               
058910     MOVE SHIPMT-SHIPT-ID  TO DPSHIPT-ID.                                 
059000     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059100             ' ** SHIPMENT ID     ** = ' DPSHIPT-ID.                      
059110     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059120             ' ** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME.               
059200     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059300             ' ** OPERATION       ** = ' PV-DB2-OPERATION-MSG-1.          
059400                                                                          
059500     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059600             ' ** SQLCODE         ** = ' SQLCODE.                         
059700     MOVE SQLCODE TO PV-SQLCODE.                                          
059800     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
059900             ' ** PV-SQLCODE      ** = ' PV-SQLCODE '.'.                  
060000                                                                          
060100     DISPLAY PC-CURRENT-PROGRAM-NAME                                      
060200             ' ** SQLWARN0        ** = ' SQLWARN0 '.'.                    
060300                                                                          
060400                                                                          
060500*----------------------------------------------------------------*        
060600* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
060700* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
060800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
060900* FORMAT.                                                        *        
061000*----------------------------------------------------------------*        
061100     COPY DPPD004.                                                        
061200                                                                          
061300     MOVE PC-ABENDING               TO DP-STATUS-DESCRIPTION.             
061400     PERFORM 6200-DISPLAY-PGM-PROGRESS.                                   
061500     PERFORM 9090-CLOSE-IN-DRIVER-FILE.                                   
061600     PERFORM 9790-CLOSE-OUT-EXTRACT-FILE.                                 
061700                                                                          
061800*----------------------------------------------------------------*        
061900* CALL ABEND                                                     *        
062000*----------------------------------------------------------------*        
062100     MOVE +4013                  TO ABEND-CODE.                           
062200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
062300*                                                                         
