      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT095.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           APRIL 2003.                                      
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT095 -                                                  *        
      *      LOG ON TO ORACLE.  ROLLUP THE PMT_PROMO_ITM_MDSE_SELN_FCT *        
      *      TABLE BASED ON THE DATE, PROMO_ID, PROMO_ITM_ID, AND      *        
      *      STR_SELN_ID.  DO MAX, MIN AND SUM ON THE COLUMNDS BASED   *        
      *      ON THE GROUP BY STATEMENT.  INSERT EACH ROW SELECTED INTO *        
      *      THE PMT_PROMO_ITM_FCT TABLE.  COMMIT THE CHANGES JUST     *        
      *      MADE AND DISPLAY TOTALS.                                  *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    07/17/2003 - AMANDA NEWHART - WR21770                       *        
      *     - DO NOT ABEND WHEN THE SALES DATE FILE IS EMPTY.  JUST    *        
      *       DISPLAY A MESSAGE INSTEAD.                               *        
      *                                                                *        
      *    07/26/2003 - COLLEEN CUMMINGS - ABEND                       *        
      *     - U0000 - ORA-01476: DIVISOR IS EQUAL TO ZERO.  ADDED      *        
      *       DIVIDE BY ZERO DECODE STATEMENT INTO CURSORS FOR         *        
      *       TOT-MKUP-PCT CALCULATION.                                *        
      *                                                                *        
      *    07/30/2018 - COGNIZANT  - CHG0204126                        *        
      *     - ORACLE LOGON STANDARDS PROGRAM CHANGE                    *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-DATE-FILE                                               
               ASSIGN                       TO INP01.                           
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941         ASSIGN                       TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-DATE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-DATE-LINE                  PIC X(80).                          
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  CC-SALES-DATE-FILE.                                                  
           05  CC-SALES-DATE                 PIC X(10).                         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01) VALUE 'N'.                
070941         88  PS-END-OF-LOGON-FILE               VALUE 'Y'.                
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01) VALUE 'N'.                
070941         88  PS-ORA-SRC-EMPTY                   VALUE 'Y'.                
           05  PS-SALES-DATE-EXIST-SWITCH   PIC X(01) VALUE 'N'.                
               88  PS-SALES-DATE-EXIST                VALUE 'Y'.                
               88  PS-SALES-DATE-NOT-EXIST            VALUE 'N'.                
           05  PS-SALES-DATE-FILE-SWITCH    PIC X(01) VALUE 'N'.                
               88  PS-SALES-DATE-FILE-END             VALUE 'Y'.                
               88  PS-SALES-DATE-FILE-START           VALUE 'N'.                
           05  PS-MDSE-SELN-CSR-SWITCH      PIC X(01) VALUE 'N'.                
               88  PS-END-MDSE-SELN-CSR               VALUE 'Y'.                
               88  PS-NOT-END-MDSE-SELN-CSR           VALUE 'N'.                
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                         VALUE '/'.              
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                       'PMKUT095'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)   VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PC-AUTO-LOGON                PIC  X(01)  VALUE '/'.              
           05  PV-SQL                       PIC S9(04)  VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
                                                                                
       01  PV-PROMO-VARIABLES.                                                  
           05  PV-TM-DIM-KY-DTE             PIC  X(10)   VALUE SPACES.          
           05  PV-STR-SELN-ID               PIC  S9(05)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-PROMO-ID                  PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-PROMO-ITM-ID              PIC  S9(10)   COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MIN-RTL-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MAX-RTL-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MIN-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKDN-MAX-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKUP-MIN-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-UNT-MKUP-MAX-RTL-PCT      PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-REG-RTL-AMT       PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-REG-RTL-AMT       PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-PROMO-RTL-AMT     PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-PROMO-RTL-AMT     PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MIN-UNT-REG-COST-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MAX-UNT-REG-COST-AMT      PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-UNT-QTY               PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-COST-AMT              PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-SLS-RTL-AMT               PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-MKDN-RTL-AMT              PIC S9(08)V99 COMP-3                
                                                VALUE ZEROS.                    
           05  PV-TOT-MKUP-PCT              PIC S9(03)V999 COMP-3               
                                                VALUE ZEROS.                    
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-MDSE-SELN-CNT             PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-INSERT                    PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
           05  PA-COMMIT-COUNTER            PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - RUN PARAGRAPH TO LOAD THE PMT_PROMO_ITM_FCT TABLE.         *         
      *  - COMMIT WHAT WAS JUST INSERTED.                             *         
      *  - DISPLAY TOTALS.                                            *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B200-READ-SALES-DATE-FILE.                                   
                                                                                
           IF PS-SALES-DATE-EXIST                                               
                                                                                
               PERFORM C200-DECLARE-MDSE-SELN-CSR                               
               PERFORM C300-OPEN-MDSE-SELN-CSR                                  
               PERFORM C400-FETCH-MDSE-SELN-CSR                                 
               PERFORM C500-PROCESS-MDSE-SELN-CSR                               
                   UNTIL PS-END-MDSE-SELN-CSR                                   
               PERFORM C600-CLOSE-MDSE-SELN-CSR                                 
                                                                                
               PERFORM C120-COMMIT                                              
                                                                                
               DISPLAY 'FINAL MDSE SELN COUNT  = ' PA-MDSE-SELN-CNT             
               DISPLAY 'FINAL INSERT COUNT     = ' PA-INSERT                    
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE INPUT FILE                                        *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  SALES-DATE-FILE.                                         
                                                                                
      *****************************************************************         
      * B200-READ-SALES-DATE-FILE.                                    *         
      * ==> PROCESSES:                                                *         
      *  - READ IN THE SALES DATE FILE                                *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B200-READ-SALES-DATE-FILE.                                               
                                                                                
           READ SALES-DATE-FILE INTO CC-SALES-DATE-FILE                         
              AT END                                                            
                 SET PS-SALES-DATE-FILE-END TO TRUE                             
           END-READ.                                                            
                                                                                
           IF (PS-SALES-DATE-FILE-END) OR (CC-SALES-DATE-FILE = SPACES)         
               DISPLAY 'SALES DATE HAS NOTHING TO PROCESS'                      
               DISPLAY '**** END OF PROGRAM ****'                               
               SET PS-SALES-DATE-NOT-EXIST TO TRUE                              
           ELSE                                                                 
               DISPLAY 'CC-SALES-DATE = ' CC-SALES-DATE                         
               SET PS-SALES-DATE-EXIST TO TRUE                                  
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SALES DATE FILE                                  *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  SALES-DATE-FILE.                                              
                                                                                
      *****************************************************************         
      * C100-INSERT-ITM-FCT                                           *         
      * ==> PROCESSES:                                                *         
      *  - INSERT THE ROW THAT WAS SELECTED FROM THE                  *         
      *    PMT_PROMO_ITM_MDSE_SELN_FCT TABLE.                         *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = INSERT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM: C500-PROCESS-MDSE-SELN-CSR                *         
      *****************************************************************         
       C100-INSERT-ITM-FCT.                                                     
           EXEC SQL                                                             
               INSERT INTO PMT_PROMO_ITM_FCT                                    
                   (PROMO_ID                                                    
                   ,PROMO_ITM_ID                                                
                   ,TM_DIM_KY_DTE                                               
                   ,STR_SELN_ID                                                 
                   ,UNT_MKDN_MIN_RTL_AMT                                        
                   ,UNT_MKDN_MAX_RTL_AMT                                        
                   ,UNT_MKDN_MIN_RTL_PCT                                        
                   ,UNT_MKDN_MAX_RTL_PCT                                        
                   ,UNT_MKUP_MIN_RTL_PCT                                        
                   ,UNT_MKUP_MAX_RTL_PCT                                        
                   ,MIN_UNT_REG_RTL_AMT                                         
                   ,MAX_UNT_REG_RTL_AMT                                         
                   ,MIN_UNT_PROMO_RTL_AMT                                       
                   ,MAX_UNT_PROMO_RTL_AMT                                       
                   ,MIN_UNT_REG_COST_AMT                                        
                   ,MAX_UNT_REG_COST_AMT                                        
                   ,SLS_UNT_QTY                                                 
                   ,SLS_COST_AMT                                                
                   ,SLS_RTL_AMT                                                 
                   ,MKDN_RTL_AMT                                                
                   ,TOT_MKUP_PCT)                                               
               VALUES                                                           
                   (:PV-PROMO-ID                                                
                   ,:PV-PROMO-ITM-ID                                            
                   ,:PV-TM-DIM-KY-DTE                                           
                   ,:PV-STR-SELN-ID                                             
                   ,:PV-UNT-MKDN-MIN-RTL-AMT                                    
                   ,:PV-UNT-MKDN-MAX-RTL-AMT                                    
                   ,:PV-UNT-MKDN-MIN-RTL-PCT                                    
                   ,:PV-UNT-MKDN-MAX-RTL-PCT                                    
                   ,:PV-UNT-MKUP-MIN-RTL-PCT                                    
                   ,:PV-UNT-MKUP-MAX-RTL-PCT                                    
                   ,:PV-MIN-UNT-REG-RTL-AMT                                     
                   ,:PV-MAX-UNT-REG-RTL-AMT                                     
                   ,:PV-MIN-UNT-PROMO-RTL-AMT                                   
                   ,:PV-MAX-UNT-PROMO-RTL-AMT                                   
                   ,:PV-MIN-UNT-REG-COST-AMT                                    
                   ,:PV-MAX-UNT-REG-COST-AMT                                    
                   ,:PV-SLS-UNT-QTY                                             
                   ,:PV-SLS-COST-AMT                                            
                   ,:PV-SLS-RTL-AMT                                             
                   ,:PV-MKDN-RTL-AMT                                            
                   ,:PV-TOT-MKUP-PCT)                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 ADD 1 TO PA-INSERT                                             
                                                                                
             WHEN OTHER                                                         
               DISPLAY 'PROMO ID = ' PV-PROMO-ID                                
               MOVE SQLCODE               TO PV-SQL                             
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-INSERT-ITM-FCT' TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C120-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = COMMIT WAS SUCCEFULL                                   *         
      * ==> PERFORMED FROM:  A100-MAINLINE                            *         
      *****************************************************************         
       C120-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
067400                                                                          
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               ADD 1    TO PA-COMMIT-COUNTER                                    
067600         CONTINUE                                                         
             WHEN OTHER                                                         
067800         MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
067900         MOVE 'C120-COMMIT'               TO PV-PARAGRAPH-NAME            
068000         PERFORM Z910-ABEND                                               
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C200-DECLARE-MDSE-SELN-CSR                                    *         
      * ==> PROCESSES:                                                *         
      *  - ROLLUP THE PMT_PROMO_ITM_MDSE_SELN_FCT TABLE BASED ON      *         
      *    DATE, PROMO ID, PROMO ITM AND STR SELN ID.                 *         
      *  - IN THE CURSOR USE MAX TO FIND THE MAX NUMBER IN THE        *         
      *    FIELD, MIN TO FIND THE MIN NUMBER IN THE FIELD, AND SUM    *         
      *    TO ADD UP ALL THE ROWS THAT ARE GROUPED TOGETHER.          *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C200-DECLARE-MDSE-SELN-CSR.                                              
                                                                                
           EXEC SQL                                                             
               DECLARE MDSESELNCSR                                              
               CURSOR FOR                                                       
                   SELECT DISTINCT                                              
                        A.TM_DIM_KY_DTE                                         
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.STR_SELN_ID                                           
                       ,MIN(A.UNT_MKDN_MIN_RTL_AMT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_AMT)                             
                       ,MIN(A.UNT_MKDN_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKDN_MAX_RTL_PCT)                             
                       ,MIN(A.UNT_MKUP_MIN_RTL_PCT)                             
                       ,MAX(A.UNT_MKUP_MAX_RTL_PCT)                             
                       ,MIN(A.MIN_UNT_REG_RTL_AMT)                              
                       ,MAX(A.MAX_UNT_REG_RTL_AMT)                              
                       ,MIN(A.MIN_UNT_PROMO_RTL_AMT)                            
                       ,MAX(A.MAX_UNT_PROMO_RTL_AMT)                            
                       ,MIN(A.MIN_UNT_REG_COST_AMT)                             
                       ,MAX(A.MAX_UNT_REG_COST_AMT)                             
                       ,SUM(A.SLS_UNT_QTY)                                      
                       ,SUM(A.SLS_COST_AMT)                                     
                       ,SUM(A.SLS_RTL_AMT)                                      
                       ,SUM(A.MKDN_RTL_AMT)                                     
                       ,DECODE (SUM(A.SLS_RTL_AMT), 0, 0,                       
                          ROUND(((SUM(A.SLS_RTL_AMT) -                          
                                  SUM(A.SLS_COST_AMT))                          
                                / SUM(A.SLS_RTL_AMT)) * 100,3))                 
                   FROM                                                         
                        PMT_PROMO_ITM_MDSE_SELN_FCT A                           
                   WHERE                                                        
                        A.TM_DIM_KY_DTE =                                       
                                TO_DATE(:CC-SALES-DATE, 'YYYY-MM-DD')           
                   GROUP BY                                                     
                        A.TM_DIM_KY_DTE                                         
                       ,A.PROMO_ID                                              
                       ,A.PROMO_ITM_ID                                          
                       ,A.STR_SELN_ID                                           
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * C300-OPEN-MDSE-SELN-CSR.                                      *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE MDSE CURSOR.                                      *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = WAS OPENED SUCCESSFULLY.                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C300-OPEN-MDSE-SELN-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                 OPEN MDSESELNCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'C300-OPEN-MDSE-SELN-CSR   '                            
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'OPEN ERROR ON MDSESELNCSR '                            
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C400-FETCH-MDSE-SELN-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  *         
      *    VARIABLES LISTED.                                          *         
      * ==> SQLCODE:                                                  *         
      *  - 0    = A ROW WAS FOUND                                     *         
      *  - 1403 = WHEN NO DATA WAS FOUND                              *         
      * ==> PERFORMED FROM: A100-MAINLINE, C500-PROCESS-SIMP-PROMO-CSR*         
      *****************************************************************         
       C400-FETCH-MDSE-SELN-CSR.                                                
                                                                                
           EXEC SQL                                                             
               FETCH MDSESELNCSR                                                
                INTO :PV-TM-DIM-KY-DTE                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-ITM-ID                                           
                    ,:PV-STR-SELN-ID                                            
                    ,:PV-UNT-MKDN-MIN-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-AMT                                   
                    ,:PV-UNT-MKDN-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKDN-MAX-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MIN-RTL-PCT                                   
                    ,:PV-UNT-MKUP-MAX-RTL-PCT                                   
                    ,:PV-MIN-UNT-REG-RTL-AMT                                    
                    ,:PV-MAX-UNT-REG-RTL-AMT                                    
                    ,:PV-MIN-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MAX-UNT-PROMO-RTL-AMT                                  
                    ,:PV-MIN-UNT-REG-COST-AMT                                   
                    ,:PV-MAX-UNT-REG-COST-AMT                                   
                    ,:PV-SLS-UNT-QTY                                            
                    ,:PV-SLS-COST-AMT                                           
                    ,:PV-SLS-RTL-AMT                                            
                    ,:PV-MKDN-RTL-AMT                                           
                    ,:PV-TOT-MKUP-PCT                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-MDSE-SELN-CNT                                    
                                                                                
               WHEN SQLCODE = 1403                                              
                   DISPLAY 'MDSE SELN COUNT FROM FETCH = '                      
                                                   PA-MDSE-SELN-CNT             
                   DISPLAY 'END OF MDSE SELN CSR'                               
                   SET PS-END-MDSE-SELN-CSR TO TRUE                             
                                                                                
               WHEN OTHER                                                       
                   DISPLAY 'PROMO ID = ' PV-PROMO-ID                            
                   DISPLAY 'PV-PROMO-ITM-ID = ' PV-PROMO-ITM-ID                 
                   DISPLAY 'PV-STR-SELN-ID  = ' PV-STR-SELN-ID                  
                   MOVE 'C400-FETCH-MDSE-SELN-CSR     '                         
                                     TO PV-PARAGRAPH-NAME                       
                   MOVE 'FETCH ERROR ON MDSESELNCSR   '                         
                                     TO PV-OPERATION-MSG-1                      
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C500-PROCESS-MDSE-SELN-CSR.                                   *         
      * ==> PROCESSES:                                                *         
      *  - CALL THE INSERT PARAGRAPH                                  *         
      *  - INITIALIZE THE VARIABLES TO MAKE SURE NO DATA IS LEFT OVER *         
      *    FROM THE PREVIOUS FETCH.                                   *         
      *  - CALL FETCH PARAGRAPH TO GET THE NEXT ROW FROM THE CURSOR   *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C500-PROCESS-MDSE-SELN-CSR.                                              
                                                                                
           PERFORM C100-INSERT-ITM-FCT.                                         
                                                                                
           INITIALIZE PV-PROMO-VARIABLES.                                       
           PERFORM C400-FETCH-MDSE-SELN-CSR.                                    
                                                                                
      *****************************************************************         
      * C600-CLOSE-MDSE-SELN-CSR.                                     *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE MDSE SELN CURSOR                                 *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = THE CLOSE WAS SUCCESSFUL                               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       C600-CLOSE-MDSE-SELN-CSR.                                                
                                                                                
           EXEC SQL                                                             
                CLOSE MDSESELNCSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                  MOVE 'C600-CLOSE-MDSE-SELN-CSR   '                            
                                    TO PV-PARAGRAPH-NAME                        
                  MOVE 'CLOSE ERROR ON MDSESELNCSR     '                        
                                    TO PV-OPERATION-MSG-1                       
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM 9900-ORACLE-ABEND                                    
           END-EVALUATE.                                                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
