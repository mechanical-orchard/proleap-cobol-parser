      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    MXKUP002.                                                 
       AUTHOR.        ROB SWAGER KFORCE.                                        
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN   02/27/12.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * CHANGE LOG                                                              
      ******************************************************************        
      * 03/27/2012  ROB SWAGER INITIAL IMPLEMENTATION  WR/IR #: WR37507         
242755******************************************************************        
242755* 04/14/2020  UPDATED PROGRAM TO DO REGULAR COMMIT AFTER EACH             
242755*             SUCCESSFUL DELETE TO DECREASE THE VOLUME IN THE             
242755*             DB2 LOGS.                                                   
242755******************************************************************        
      *   INSERT / DELETE SKU'S (ITEMS) ON MR ITEM TABLES.                      
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT MPX-INPUT-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT COMMIT-CONTROL-FILE                                           
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATE-CONTROL-FILE                                             
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  MPX-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 64 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MPX-INPUT-RECORD         PIC X(64).                                  
                                                                                
       FD  COMMIT-CONTROL-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  COMMIT-CONTROL-RECORD    PIC X(80).                                  
                                                                                
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATE-CONTROL-RECORD    PIC X(80).                                    
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE F                                                     
           RECORD CONTAINS 64 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS OUTPUT-ERROR-RECORD.                                  
                                                                                
       01  OUTPUT-ERROR-RECORD         PIC X(64).                               
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(4) COMP SYNC VALUE ZERO.          
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
           88  ABEND-4002-READ-ERROR                       VALUE +4002.         
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
           88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009.         
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME             PIC  X(8) VALUE 'MXKUP002'.              
           05  PC-MAX-RETRIES          PIC S9(3) VALUE 3   COMP-3.              
           05  PC-MAX-BEFORE-COMMIT    PIC S9(04) COMP SYNC                     
                                                  VALUE +0.                     
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SWITCH           PIC X     VALUE 'N'.                     
               88  PS-NOT-EOF                    VALUE 'N'.                     
               88  PS-EOF                        VALUE 'Y'.                     
014700     05  PS-PS-END-OF-DATE-SW    PIC X(1)  VALUE 'N'.                     
014800         88  PS-END-OF-DATE-FILE           VALUE 'Y'.                     
           05  PS-TBL-LOCK-SW          PIC X(1)  VALUE 'N'.                     
               88  PS-TBL-LOCKED                 VALUE 'Y'.                     
               88  PS-TBL-NOT-LOCKED             VALUE 'N'.                     
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
010900     05  PV-DB2-DATE-TIMESTAMP.                                           
               10  PV-DB2-CCYY-MM-DD PIC  X(10)   VALUE SPACE.                  
               10  PV-TIME-STAMP     PIC  X(16)                                 
                   VALUE '-19.00.00.000000'.                                    
010900     05  PV-DB2-DATE-TMSTMP-DEL.                                          
               10  PV-DB2-CCYY-MM-DD-DEL PIC  X(10)   VALUE SPACE.              
               10  PV-TIME-STAMP-DEL     PIC  X(16)                             
                   VALUE '-19.00.00.000001'.                                    
           05  PV-COMMIT-CNT           PIC S9(04) COMP SYNC                     
                                                  VALUE 0.                      
           05  PV-DLT-COMMIT-CNT       PIC S9(04) COMP SYNC                     
                                                  VALUE 0.                      
           05  PV-INPUT-CNT            PIC  9(9)  VALUE 0.                      
           05  PV-UPDATE-CNT           PIC  9(9)  VALUE 0.                      
           05  PV-ERROR-REC-CNT        PIC  9(9)  VALUE 0.                      
           05  PV-TMRITEM-INSERT-CNT   PIC  9(9)  VALUE 0.                      
           05  PV-MRTAUDT-INSERT-CNT   PIC  9(9)  VALUE 0.                      
           05  PV-TMRITEM-DELETE-CNT   PIC  9(9)  VALUE 0.                      
                                                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30) VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30) VALUE SPACES.                 
                                                                                
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                 PIC X(02)  VALUE SPACES.              
               10  PV-DELAY-TIME          PIC 9(08)  VALUE 6000.                
           05  WS-SYSWAIT                 PIC X(08)  VALUE 'SYSWAIT'.           
                                                                                
           05  PV-COMMIT-CONTROL-RECORD.                                        
               10  PV-COMMIT-CONTROL-CNT  PIC 9(09).                            
               10  FILLER                 PIC X(71)  VALUE SPACES.              
           05  PV-RETRY-CNT               PIC S9(04)  VALUE ZEROS.              
                                                                                
020100******************************************************************        
020200* RECORD LAYOUT FOR INPUT DATE CARD                                       
020300******************************************************************        
020400 01  CC-CONTROL-CARD.                                                     
020500     05  CC-PROCDT-CCYY-MM-DD.                                            
020600         10 CC-PROCESS-DT-CCYY       PIC X(04)  VALUE SPACES.             
020700         10 FILLER                   PIC X(01)  VALUE SPACES.             
020800         10 CC-PROCESS-DT-MM         PIC X(02)  VALUE SPACES.             
020900         10 FILLER                   PIC X(01)  VALUE SPACES.             
021000         10 CC-PROCESS-DT-DD         PIC X(02)  VALUE SPACES.             
021100     05  FILLER                      PIC X(70)  VALUE SPACES.             
021200                                                                          
      ******************************************************************        
      *   INPUT MPX FILE LAYOUT                                                 
      ******************************************************************        
           COPY MXRD004.                                                        
                                                                                
      ******************************************************************        
      *   ERROR MESSAGE AREA FOR DB2                                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
       EJECT                                                                    
      ************************                                                  
      * DB2 TABLES                                                              
      ************************                                                  
                                                                                
      *****************************************************************         
      *  TMRITEM - MR ITEM TABLE                                                
      *****************************************************************         
           EXEC SQL                                                        CL**2
               INCLUDE TMRITEM                                             CL**2
           END-EXEC.                                                       CL**2
                                                                                
      *****************************************************************         
      *  MRT00000 - MR ITEM AUDIT                                               
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE MRT00000                                                
           END-EXEC.                                                            
                                                                                
      ************************                                                  
      * DB2 COMMUNICATION AREA                                                  
      ************************                                                  
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-PRE-PROCESSING.                                         
                                                                                
           PERFORM 2000-PROCESS-INFILE                                          
                   UNTIL PS-EOF.                                                
                                                                                
           PERFORM 9000-POST-PROCESSING.                                        
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *   OPEN FILES                                                            
      *   READ FIRST INPUT RECORD                                               
      ******************************************************************        
       1000-PRE-PROCESSING.                                                     
                                                                                
           OPEN INPUT  MPX-INPUT-FILE                                           
                       COMMIT-CONTROL-FILE                                      
                       DATE-CONTROL-FILE                                        
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '** PROGRAM: ' PC-PGM-NAME.                                  
           DISPLAY SPACES.                                                      
                                                                                
           PERFORM 7000-READ-COMMIT-CONTROL-FILE.                               
                                                                                
           PERFORM 7010-READ-DATE-CONTROL-FILE.                                 
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
                                                                                
      ******************************************************************        
      *   COMMIT CHANGES AFTER SPECIFIED NUMBER OF UPDATES                      
      *   READ THE NEXT INPUT RECORD                                            
      ******************************************************************        
       2000-PROCESS-INFILE.                                                     
                                                                                
           PERFORM 2100-PROCESS-INPUT-MPXREC.                                   
                                                                                
           IF  PV-COMMIT-CNT = PC-MAX-BEFORE-COMMIT                             
               PERFORM 7600-COMMIT-UPDATE                                       
           END-IF.                                                              
                                                                                
           PERFORM 7050-READ-MPX-INPUT-FILE.                                    
                                                                                
242755*    IF MX004-ISRT-DEL-IND NOT = 'D'                                      
               IF PV-DLT-COMMIT-CNT > 0                                         
                  DISPLAY 'COMMITING DELETES TO DATABASE '                      
                  PERFORM 7600-COMMIT-UPDATE                                    
               END-IF.                                                          
242755*    END-IF.                                                              
                                                                                
      ******************************************************************        
      *   PROCESS THE INPUT RECORD DEPENDING ON VALUE OF                        
      *   MX004-ISRT-DEL-IND                                                    
      *   FROM THE INPUT RECORD.                                                
      ******************************************************************        
       2100-PROCESS-INPUT-MPXREC.                                               
                                                                                
           MOVE '2100-PROCESS-INPUT-MPXREC' TO PV-PARAGRAPH-NAME.               
                                                                                
           IF MX004-ISRT-DEL-IND = 'I'                                          
              PERFORM 4000-FORMAT-INSERT-TMRITEM                                
              PERFORM 6000-INSERT-TMRITEM                                       
           ELSE                                                                 
           IF MX004-ISRT-DEL-IND = 'U'                                          
              PERFORM 4010-FORMAT-UPDATE-TMRITEM                                
              PERFORM 7200-UPDATE-TMRITEM                                       
           ELSE                                                                 
           IF MX004-ISRT-DEL-IND = 'D'                                          
              PERFORM 6050-DELETE-TMRITEM                                       
           ELSE                                                                 
              DISPLAY '** ABEND     **'                                         
              DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                          
              DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                    
              DISPLAY '** INVALID ISRT DEL IND IN INPUT FILE **'                
              SET ABEND-4008-DATA-ERROR TO TRUE                                 
              CALL 'ILBOABN0' USING ABEND-CODE                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4000-FORMAT-INSERT-TMRITEM.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - FORMATS A ROW INTO TMRITEM TO APPLY DAILY SKU'S           *        
      ******************************************************************        
       4000-FORMAT-INSERT-TMRITEM.                                              
                                                                                
           MOVE MX004-ITM-NBR      TO MRITEM-ITEM-NBR.                          
           MOVE MX004-UCYCLE-CDE   TO MRITEM-UCYCLE-CDE.                        
           MOVE MX004-RT-NBR       TO MRITEM-RT-NBR.                            
           MOVE MX004-PACKSW-CDE   TO MRITEM-PACKSW-CDE.                        
           MOVE MX004-DSER-CDE     TO MRITEM-DSER-CDE.                          
           MOVE MX004-LT-QTY       TO MRITEM-LT-QTY.                            
           MOVE MX004-RT-DAY-NBR   TO MRITEM-RT-DAY-NBR.                        
           MOVE MX004-ORD-GROUP-SW TO MRITEM-ORD-GROUP-SW.                      
           MOVE .1                 TO MRITEM-TSL-NBR.                           
           MOVE 1.0                TO MRITEM-BYPSCL-NBR.                        
           MOVE 0                  TO MRITEM-MINBI-FCTR                         
                                      MRITEM-IMIN-QTY                           
                                      MRITEM-IMAX-QTY                           
                                      MRITEM-SSADJ-PCT.                         
           MOVE 0.5                TO MRITEM-RTF-FCTR.                          
           MOVE 'N'                TO MRITEM-BYPDDU-CDE                         
                                      MRITEM-FRCDDU-CDE                         
                                      MRITEM-BYPZSL-CDE                         
                                      MRITEM-BYPTFP-CDE                         
                                      MRITEM-BYPZOH-CDE                         
                                      MRITEM-LUMPSW-CDE                         
                                      MRITEM-OUTLSW-CDE                         
                                      MRITEM-ZSSTOCK-CDE                        
                                      MRITEM-DDUPRM-SW.                         
           MOVE '0'                TO MRITEM-BYPBEF-CDE                         
                                      MRITEM-BYPAFT-CDE.                        
           MOVE 'U'                TO MRITEM-DDFTSW-CDE.                        
           MOVE 'A'                TO MRITEM-ZOPSW-CDE                          
                                      MRITEM-STAT-CDE.                          
                                                                                
      ******************************************************************        
      * 4010-FORMAT-UPDATE-TMRITEM.                                    *        
      *  ==> PROCESSES:                                                *        
      *    - FORMATS A ROW INTO TMRITEM TO UPDATE DAILY SKU'S          *        
      ******************************************************************        
       4010-FORMAT-UPDATE-TMRITEM.                                              
                                                                                
           MOVE MX004-ITM-NBR      TO MRITEM-ITEM-NBR.                          
           MOVE MX004-UCYCLE-CDE   TO MRITEM-UCYCLE-CDE.                        
           MOVE MX004-RT-NBR       TO MRITEM-RT-NBR.                            
           MOVE MX004-PACKSW-CDE   TO MRITEM-PACKSW-CDE.                        
           MOVE MX004-DSER-CDE     TO MRITEM-DSER-CDE.                          
           MOVE MX004-LT-QTY       TO MRITEM-LT-QTY.                            
           MOVE MX004-RT-DAY-NBR   TO MRITEM-RT-DAY-NBR.                        
           MOVE MX004-ORD-GROUP-SW TO MRITEM-ORD-GROUP-SW.                      
                                                                                
      ******************************************************************        
      * 6000-INSERT-TMRITEM.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - INSERTS A ROW INTO TMRITEM TO APPLY DAILY SKU'S           *        
      ******************************************************************        
       6000-INSERT-TMRITEM.                                                     
           MOVE '6000-INSERT-TMRITEM' TO PV-PARAGRAPH-NAME.                     
                                                                                
           EXEC SQL                                                             
               INSERT INTO TMRITEM                                              
                 ( ITEM_NBR                                                     
                  ,UCYCLE_CDE                                                   
                  ,TSL_NBR                                                      
                  ,BYPDDU_CDE                                                   
                  ,BYPSCL_NBR                                                   
                  ,FRCDDU_CDE                                                   
                  ,BYPZSL_CDE                                                   
                  ,BYPTFP_CDE                                                   
                  ,BYPZOH_CDE                                                   
                  ,BYPBEF_CDE                                                   
                  ,BYPAFT_CDE                                                   
                  ,LUMPSW_CDE                                                   
                  ,MINBI_FCTR                                                   
                  ,DDFTSW_CDE                                                   
                  ,RT_NBR                                                       
                  ,RTF_FCTR                                                     
                  ,ZOPSW_CDE                                                    
                  ,OUTLSW_CDE                                                   
                  ,ZSSTOCK_CDE                                                  
                  ,PACKSW_CDE                                                   
                  ,DSER_CDE                                                     
                  ,LT_QTY                                                       
                  ,IMIN_QTY                                                     
                  ,IMAX_QTY                                                     
                  ,SSADJ_PCT                                                    
                  ,RT_DAY_NBR                                                   
                  ,DDUPRM_SW                                                    
                  ,ORD_GROUP_SW                                                 
                  ,STAT_CDE )                                                   
          VALUES (:MRITEM-ITEM-NBR,                                             
                  :MRITEM-UCYCLE-CDE,                                           
                  :MRITEM-TSL-NBR,                                              
                  :MRITEM-BYPDDU-CDE,                                           
                  :MRITEM-BYPSCL-NBR,                                           
                  :MRITEM-FRCDDU-CDE,                                           
                  :MRITEM-BYPZSL-CDE,                                           
                  :MRITEM-BYPTFP-CDE,                                           
                  :MRITEM-BYPZOH-CDE,                                           
                  :MRITEM-BYPBEF-CDE,                                           
                  :MRITEM-BYPAFT-CDE,                                           
                  :MRITEM-LUMPSW-CDE,                                           
                  :MRITEM-MINBI-FCTR,                                           
                  :MRITEM-DDFTSW-CDE,                                           
                  :MRITEM-RT-NBR,                                               
                  :MRITEM-RTF-FCTR,                                             
                  :MRITEM-ZOPSW-CDE,                                            
                  :MRITEM-OUTLSW-CDE,                                           
                  :MRITEM-ZSSTOCK-CDE,                                          
                  :MRITEM-PACKSW-CDE,                                           
                  :MRITEM-DSER-CDE,                                             
                  :MRITEM-LT-QTY,                                               
                  :MRITEM-IMIN-QTY,                                             
                  :MRITEM-IMAX-QTY,                                             
                  :MRITEM-SSADJ-PCT,                                            
                  :MRITEM-RT-DAY-NBR,                                           
                  :MRITEM-DDUPRM-SW,                                            
                  :MRITEM-ORD-GROUP-SW,                                         
                  :MRITEM-STAT-CDE )                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-TMRITEM-INSERT-CNT                         
                              PV-COMMIT-CNT                                     
                   PERFORM 8010-INSERT-AUDIT-RECORD                             
                                                                                
               WHEN SQLCODE = -803                                              
                   PERFORM 4010-FORMAT-UPDATE-TMRITEM                           
                   PERFORM 7210-UPDATE-TMRITEM                                  
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT TMRITEM TABLE'                                  
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * 6050-DELETE-TMRITEM.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - DELETE THE INPUT MX004-ITM-NBR FROM TMRITEM.              *        
      *       THE DELETE WILL CASCADE TO TMRITST, TMRISFE AND TMRISHS. *        
      *    - IF DELETE WAS NOT SUCCESSFUL WRITE DISPLAY OF ITM-NBR     *        
      *       WRITE INPUT RECORD TO THE OUTPUT ERROR FILE.             *        
      *    - IF DELETE WAS SUCCESSFUL FORMAT A DELETE AUDIT RECORD AND *        
      *       INSERT DELETE AUDIT RECORD TO MR_ITM_AUD.                         
      ******************************************************************        
       6050-DELETE-TMRITEM.                                                     
                                                                                
           IF PS-TBL-NOT-LOCKED                                                 
              PERFORM 6500-LOCK-TABLES                                          
           END-IF.                                                              
                                                                                
           MOVE '6050-DELETE-TMRITEM' TO PV-PARAGRAPH-NAME.                     
           MOVE MX004-ITM-NBR         TO MRITEM-ITEM-NBR.                       
                                                                                
           EXEC SQL                                                             
              DELETE FROM TMRITEM                                               
              WHERE       ITEM_NBR   = :MRITEM-ITEM-NBR                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  ADD +1       TO PV-TMRITEM-DELETE-CNT                         
                                  PV-DLT-COMMIT-CNT                             
                  PERFORM 8020-DELETE-AUDIT-RECORD                              
              WHEN SQLCODE = +100                                               
               DISPLAY 'TMRITEM DELETE ROW NOT FOUND FOR ITM NBR '              
                        MX004-ITM-NBR                                           
              WHEN OTHER                                                        
                  MOVE 'DELETE TMRITEM TABLE'                                   
                                   TO PV-DB2-OPER-ATTEMPTED                     
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      **LOCK THE NECESSARY TABLES                                               
      ******************************************************************        
       6500-LOCK-TABLES.                                                        
                                                                                
           PERFORM 6510-LOCK-TMRITEM.                                           
                                                                                
           PERFORM 6520-LOCK-TMRITST.                                           
                                                                                
           PERFORM 6530-LOCK-TMRISFE.                                           
                                                                                
           PERFORM 6540-LOCK-TMRISHS.                                           
                                                                                
           SET PS-TBL-LOCKED TO TRUE.                                           
                                                                                
      ******************************************************************        
      **LOCK THE TMRITEM TABLE                                                  
      ******************************************************************        
       6510-LOCK-TMRITEM.                                                       
           MOVE '6510-LOCK-TMRITEM ' TO PV-PARAGRAPH-NAME.                      
                                                                                
           INITIALIZE PV-RETRY-CNT.                                             
           PERFORM WITH TEST AFTER                                              
             VARYING PV-RETRY-CNT FROM 1 BY 1                                   
             UNTIL SQLCODE = ZERO                                               
                OR PV-RETRY-CNT > PC-MAX-RETRIES                                
                   EXEC SQL                                                     
                       LOCK TABLE TMRITEM IN EXCLUSIVE MODE                     
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                            CONTINUE                                            
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = -913                                      
                            CONTINUE                                            
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                            MOVE 'LOCK TMRITEM PROBLEM'                         
                                              TO  PV-DB2-OPER-ATTEMPTED         
                            PERFORM 9999-SQL-ABEND                              
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-CNT > PC-MAX-RETRIES                                     
               MOVE 'LOCK TMRITEM RETRIES EXCEEDED '                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **LOCK THE TMRITST TABLE                                                  
      ******************************************************************        
       6520-LOCK-TMRITST.                                                       
           MOVE '6520-LOCK-TMRITST ' TO PV-PARAGRAPH-NAME.                      
                                                                                
           INITIALIZE PV-RETRY-CNT.                                             
           PERFORM WITH TEST AFTER                                              
             VARYING PV-RETRY-CNT FROM 1 BY 1                                   
             UNTIL SQLCODE = ZERO                                               
                OR PV-RETRY-CNT > PC-MAX-RETRIES                                
                   EXEC SQL                                                     
                       LOCK TABLE TMRITST IN EXCLUSIVE MODE                     
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                            CONTINUE                                            
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = -913                                      
                            CONTINUE                                            
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                            MOVE 'LOCK TMRITST PROBLEM'                         
                                              TO  PV-DB2-OPER-ATTEMPTED         
                            PERFORM 9999-SQL-ABEND                              
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-CNT > PC-MAX-RETRIES                                     
               MOVE 'LOCK TMRITST RETRIES EXCEEDED '                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **LOCK THE TMRISFE TABLE                                                  
      ******************************************************************        
       6530-LOCK-TMRISFE.                                                       
           MOVE '6530-LOCK-TMRISFE ' TO PV-PARAGRAPH-NAME.                      
                                                                                
           INITIALIZE PV-RETRY-CNT.                                             
           PERFORM WITH TEST AFTER                                              
             VARYING PV-RETRY-CNT FROM 1 BY 1                                   
             UNTIL SQLCODE = ZERO                                               
                OR PV-RETRY-CNT > PC-MAX-RETRIES                                
                   EXEC SQL                                                     
                       LOCK TABLE TMRISFE IN EXCLUSIVE MODE                     
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                            CONTINUE                                            
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = -913                                      
                            CONTINUE                                            
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                            MOVE 'LOCK TMRISFE PROBLEM'                         
                                              TO  PV-DB2-OPER-ATTEMPTED         
                            PERFORM 9999-SQL-ABEND                              
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-CNT > PC-MAX-RETRIES                                     
               MOVE 'LOCK TMRISFE RETRIES EXCEEDED '                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **LOCK THE TMRISHS TABLE                                                  
      ******************************************************************        
       6540-LOCK-TMRISHS.                                                       
           MOVE '6540-LOCK-TMRISHS ' TO PV-PARAGRAPH-NAME.                      
                                                                                
           INITIALIZE PV-RETRY-CNT.                                             
           PERFORM WITH TEST AFTER                                              
             VARYING PV-RETRY-CNT FROM 1 BY 1                                   
             UNTIL SQLCODE = ZERO                                               
                OR PV-RETRY-CNT > PC-MAX-RETRIES                                
                   EXEC SQL                                                     
                       LOCK TABLE TMRISHS IN EXCLUSIVE MODE                     
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = ZERO                                      
                            CONTINUE                                            
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = -913                                      
                            CONTINUE                                            
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                            MOVE 'LOCK TMRISHS PROBLEM'                         
                                              TO  PV-DB2-OPER-ATTEMPTED         
                            PERFORM 9999-SQL-ABEND                              
                   END-EVALUATE                                                 
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-CNT > PC-MAX-RETRIES                                     
               MOVE 'LOCK TMRISHS RETRIES EXCEEDED '                            
                                      TO  PV-DB2-OPER-ATTEMPTED                 
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *   READ COMMIT CONTROL RECORD TO GET COMMIT COUNT MAX.                   
      ******************************************************************        
       7000-READ-COMMIT-CONTROL-FILE.                                           
                                                                                
           MOVE '7000-READ-COMMIT-CONTROL-FILE' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ COMMIT-CONTROL-FILE                                             
               INTO PV-COMMIT-CONTROL-RECORD                                    
               AT END                                                           
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                     
                   DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME               
                   DISPLAY '** EMPTY COMMIT CONTROL CARD **'                    
                   SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                       
                   CALL 'ILBOABN0' USING ABEND-CODE.                            
                                                                                
           MOVE PV-COMMIT-CONTROL-CNT TO PC-MAX-BEFORE-COMMIT.                  
                                                                                
      ******************************************************************        
      *   READ DATE CONTROL RECORD.                                             
      ******************************************************************        
       7010-READ-DATE-CONTROL-FILE.                                             
                                                                                
           MOVE '7010-READ-DATE-CONTROL-FILE' TO PV-PARAGRAPH-NAME.             
055000                                                                          
054700     READ DATE-CONTROL-FILE INTO CC-CONTROL-CARD                          
054800         AT END                                                           
054900             SET PS-END-OF-DATE-FILE TO TRUE.                             
055000                                                                          
055100     IF PS-END-OF-DATE-FILE                                               
              DISPLAY '** ABEND     **'                                         
              DISPLAY '** PROGRAM   ** = ' PC-PGM-NAME                          
              DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME                    
              DISPLAY '** EMPTY DATE CONTROL CARD **'                           
              SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                            
              CALL 'ILBOABN0' USING ABEND-CODE                                  
055600     END-IF.                                                              
055700                                                                          
055800     DISPLAY PC-PGM-NAME                                                  
055900             ' - CONTROL CARD = (' CC-PROCDT-CCYY-MM-DD ')'.              
056000                                                                          
056100     MOVE CC-PROCDT-CCYY-MM-DD TO PV-DB2-CCYY-MM-DD                       
056100                                  PV-DB2-CCYY-MM-DD-DEL.                  
056200                                                                          
056300     CLOSE DATE-CONTROL-FILE.                                             
056400                                                                          
      ******************************************************************        
      *   READ MPX INPUT RECORD.                                                
      ******************************************************************        
       7050-READ-MPX-INPUT-FILE.                                                
                                                                                
           READ MPX-INPUT-FILE                                                  
               INTO MX004-ITM-RECORD                                            
               AT END                                                           
                   SET PS-EOF TO TRUE.                                          
                                                                                
           IF PS-NOT-EOF                                                        
              ADD 1 TO PV-INPUT-CNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE OF THE TMRITEM TABLE        *        
      ******************************************************************        
       7200-UPDATE-TMRITEM.                                                     
                                                                                
           MOVE '7200-UPDATE-TMRITEM'  TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE TMRITEM'       TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           EXEC SQL                                                             
               UPDATE TMRITEM                                                   
                  SET  ITEM_NBR   = :MRITEM-ITEM-NBR                            
                      ,UCYCLE_CDE = :MRITEM-UCYCLE-CDE                          
                      ,RT_NBR     = :MRITEM-RT-NBR                              
                      ,PACKSW_CDE = :MRITEM-PACKSW-CDE                          
                      ,DSER_CDE   = :MRITEM-DSER-CDE                            
                      ,LT_QTY     = :MRITEM-LT-QTY                              
                      ,RT_DAY_NBR = :MRITEM-RT-DAY-NBR                          
                      ,ORD_GROUP_SW = :MRITEM-ORD-GROUP-SW                      
                WHERE ITEM_NBR    =  :MRITEM-ITEM-NBR                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                            PV-COMMIT-CNT                                       
                   DISPLAY 'TMRITEM UPDATED FOR: ' MX004-ITM-NBR                
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITEM ROW NOT UPDATED, ROW NOT FOUND'             
                           ' MX004-ITEM NBR = ' MX004-ITM-NBR                   
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * THIS PARAGRAPH PERFORMS THE UPDATE OF THE TMRITEM TABLE WHEN AN*        
      * INSERT FAILED BECAUSE ROW WAS ALREADY THERE.                   *        
      ******************************************************************        
       7210-UPDATE-TMRITEM.                                                     
                                                                                
           MOVE '7210-UPDATE-TMRITEM'  TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE TMRITEM'       TO PV-DB2-OPER-ATTEMPTED.                
                                                                                
           EXEC SQL                                                             
               UPDATE TMRITEM                                                   
                  SET  ITEM_NBR   = :MRITEM-ITEM-NBR                            
                      ,UCYCLE_CDE = :MRITEM-UCYCLE-CDE                          
                      ,RT_NBR     = :MRITEM-RT-NBR                              
                      ,PACKSW_CDE = :MRITEM-PACKSW-CDE                          
                      ,DSER_CDE   = :MRITEM-DSER-CDE                            
                      ,LT_QTY     = :MRITEM-LT-QTY                              
                      ,RT_DAY_NBR = :MRITEM-RT-DAY-NBR                          
                      ,ORD_GROUP_SW = :MRITEM-ORD-GROUP-SW                      
                WHERE ITEM_NBR    =  :MRITEM-ITEM-NBR                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PV-UPDATE-CNT                                       
                            PV-COMMIT-CNT                                       
                   DISPLAY 'INSERT CONVERTED TO UPDATE FOR ITM NBR: '           
                           MX004-ITM-NBR                                        
                                                                                
               WHEN SQLCODE = +100                                              
                   DISPLAY 'TMRITEM ROW NOT UPDATED, ROW NOT FOUND'             
                           ' MX004-ITEM NBR = ' MX004-ITM-NBR                   
                   PERFORM 8000-WRITE-ERROR-RECORD                              
                                                                                
               WHEN OTHER                                                       
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    COMMIT THE UPDATES AND RESET COMMIT COUNTER TO 0.                    
      ******************************************************************        
       7600-COMMIT-UPDATE.                                                      
                                                                                
           EXEC SQL                                                             
              COMMIT                                                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
               IF PV-DLT-COMMIT-CNT > 0                                         
                  DISPLAY '** ' PV-DLT-COMMIT-CNT ' RECORDS COMMITTED.'         
                  MOVE 0 TO PV-DLT-COMMIT-CNT                                   
               ELSE                                                             
                  DISPLAY '** ' PV-COMMIT-CNT ' RECORDS COMMITTED.'             
                  MOVE 0 TO PV-COMMIT-CNT                                       
               END-IF                                                           
           ELSE                                                                 
               MOVE '7600-COMMIT-UPDATE' TO PV-PARAGRAPH-NAME                   
               MOVE 'COMMIT'             TO PV-DB2-OPER-ATTEMPTED               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *   WRITE ERROR RECORD TO OUTPUT FILE (FROM INPUT FILE).                  
      ******************************************************************        
       8000-WRITE-ERROR-RECORD.                                                 
                                                                                
           WRITE OUTPUT-ERROR-RECORD                                            
               FROM MPX-INPUT-RECORD.                                           
                                                                                
           ADD 1 TO PV-ERROR-REC-CNT.                                           
                                                                                
      ******************************************************************        
      *   PERFORM INSERT OF ADD RECORD TO MR ITEM AUDIT RECORD TABLE.           
      ******************************************************************        
       8010-INSERT-AUDIT-RECORD.                                                
                                                                                
           MOVE '8010-INSERT-AUDIT-RECORD' TO PV-PARAGRAPH-NAME.                
                                                                                
           MOVE PV-DB2-DATE-TIMESTAMP TO MRT00000-EFF-TMST                      
                                         MRT00000-CRE-TMST.                     
           MOVE 'A'                   TO MRT00000-ACTN-CDE.                     
           MOVE 'MXKUP002'            TO MRT00000-CRE-BY-USR-ID.                
           MOVE MX004-ITM-NBR         TO MRT00000-ITM-NBR.                      
                                                                                
           EXEC SQL                                                             
               INSERT INTO MRT_ITM_AUD                                          
                 ( EFF_TMST                                                     
                  ,ITM_NBR                                                      
                  ,ACTN_CDE                                                     
                  ,CRE_TMST                                                     
                  ,CRE_BY_USR_ID )                                              
           VALUES (:MRT00000-EFF-TMST,                                          
                  :MRT00000-ITM-NBR,                                            
                  :MRT00000-ACTN-CDE,                                           
                  :MRT00000-CRE-TMST,                                           
                  :MRT00000-CRE-BY-USR-ID )                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-MRTAUDT-INSERT-CNT                         
                                                                                
               WHEN SQLCODE = -803                                              
                   DISPLAY 'INSERT TO MRT AUDIT ITEM - DUP INSERT'              
                           ',BY PASSING INSERT IN '                             
                           '8010-INSERT-AUDIT-RECORD FOR'                       
                           ' MX004 ITEM NBR = ' MX004-ITM-NBR                   
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT AUDIT ITEM TABLE'                               
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM INSERT OF DELETE RECORD TO MR ITEM AUDIT RECORD TABLE.        
      ******************************************************************        
       8020-DELETE-AUDIT-RECORD.                                                
                                                                                
           MOVE '8020-DELETE-AUDIT-RECORD' TO PV-PARAGRAPH-NAME.                
                                                                                
           MOVE PV-DB2-DATE-TMSTMP-DEL TO MRT00000-EFF-TMST                     
                                          MRT00000-CRE-TMST.                    
           MOVE 'D'                   TO MRT00000-ACTN-CDE.                     
           MOVE 'MXKUP002'            TO MRT00000-CRE-BY-USR-ID.                
           MOVE MX004-ITM-NBR         TO MRT00000-ITM-NBR.                      
                                                                                
           EXEC SQL                                                             
               INSERT INTO MRT_ITM_AUD                                          
                 ( EFF_TMST                                                     
                  ,ITM_NBR                                                      
                  ,ACTN_CDE                                                     
                  ,CRE_TMST                                                     
                  ,CRE_BY_USR_ID )                                              
          VALUES (:MRT00000-EFF-TMST,                                           
                  :MRT00000-ITM-NBR,                                            
                  :MRT00000-ACTN-CDE,                                           
                  :MRT00000-CRE-TMST,                                           
                  :MRT00000-CRE-BY-USR-ID )                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD +1      TO PV-MRTAUDT-INSERT-CNT                         
                                                                                
               WHEN SQLCODE = -803                                              
                   DISPLAY 'INSERT TO MRT AUDIT ITEM - DUP INSERT'              
                           ',BY PASSING INSERT IN '                             
                           '8020-DELETE-AUDIT-RECORD FOR'                       
                           ' MX004 ITEM NBR = ' MX004-ITM-NBR                   
                   MOVE 0 TO SQLCODE                                            
                                                                                
               WHEN OTHER                                                       
                   MOVE 'INSERT AUDIT ITEM TABLE FOR DELETE'                    
                                    TO PV-DB2-OPER-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM FINAL COMMIT, DISPLAY END OF PGM MESSAGES,                    
      *   AND CLOSE FILES                                                       
      ******************************************************************        
       9000-POST-PROCESSING.                                                    
                                                                                
           PERFORM 7600-COMMIT-UPDATE.                                          
                                                                                
           DISPLAY 'MPX SKU INPUT RECORDS READ    = ' PV-INPUT-CNT.             
           DISPLAY 'MPX SKU ERROR RECORDS WRITTEN = ' PV-ERROR-REC-CNT.         
           DISPLAY 'TMRITEM RECORDS UPDATED  = ' PV-UPDATE-CNT.                 
           DISPLAY 'TMRITEM ROWS INSERTED = ' PV-TMRITEM-INSERT-CNT.            
           DISPLAY 'MRT AUD ROWS INSERTED = ' PV-MRTAUDT-INSERT-CNT.            
           DISPLAY 'TMRITEM ROWS DELETED  = ' PV-TMRITEM-DELETE-CNT.            
                                                                                
           CLOSE MPX-INPUT-FILE                                                 
                 COMMIT-CONTROL-FILE                                            
                 OUTPUT-FILE.                                                   
                                                                                
                                                                                
      ******************************************************************        
      *   PERFORM SQL ABEND ROUTINE                                             
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           SET ABEND-4013-DB2-ERROR  TO TRUE.                                   
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PGM-NAME.                          
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
           DISPLAY ' '.                                                         
           DISPLAY ' MX004 ITEM NBR   = ' MX004-ITM-NBR.                        
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *****************************************************************         
      ***********************END OF PROGRAM****************************         
