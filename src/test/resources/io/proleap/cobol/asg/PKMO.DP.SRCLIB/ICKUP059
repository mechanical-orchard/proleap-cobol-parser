       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ICKUP059.                                                 
       AUTHOR.        R. KELLEN.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/01/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * ICKUP059 - MAINAINT QTYS ON ICT_SKU_LOC_CNT TABLE                       
      *****************************************************************         
      *        THIS PROGRAM WILL READ A FILE CONTAINING COUNT/SKU/STORE         
      *        QTY DATA AND MAINTAIN THE UNT_RTL_AMT,LOC_EXPTED_UNT_QTY         
      *        ,IN_TRNST_UNT_QTY,ACPTD_TRUK_UNT_QTY,RTL_SAL_UNT_QTY             
      *        ,RTL_RET_UNT_QTY,XFER_IN_UNT_QTY,XFER_OUT_UNT_QTY                
      *        ,SCND_UPC_NBR,SCND_UNT_QTY,CNT_GP_CDE,MDSE_EXPRN_DTE ON          
      *        THE ICT_SKU_LOC_CNT TABLE. AN 'UPD' ROW IS ADDED TO THE          
      *        ICT_LOC_CNT_STAT TABLE FOR EACH LOCATION PROCESSED.              
      *****************************************************************         
      *                                                                         
      *  TABLES UPDATED ARE:                                                    
      *      ICT_SKU_LOC_CNT  - IC COUNT EXECUTION TABLE                        
      *  FILES READ ARE:                                                        
      *      NONE                                                               
      *****************************************************************         
C63538**09/25/13 - COGNIZANT        - PRB 40294                                 
C63538*   THIS IS A NEW PROGRAM ICKUP059 BUT MODIFIED VERSION OF                
C63538*   ICKUP056.                                                             
C63538*   PERFORMANCE IMPROVEMENT - MULTI ROW INSERT AND                        
C63538*   SPLITTING ICK056 EXECUTION IN 6 PARALLEL INSTANCES.                   
C63538*   ICK451 TO ICK456 ARE THE NEW JOBS REPLACING ICK056.                   
C63538*****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT IC-COUNT-QTY-MAINT-FILE    ASSIGN TO INP01.                   
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  IC-COUNT-QTY-MAINT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS IC-COUNT-QTY-MAINT-REC.                               
       01  IC-COUNT-QTY-MAINT-REC     PIC  X(60).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08)  VALUE 'ICKUP059'.           
           05  PC-JOB-NAME               PIC X(08)  VALUE SPACES.               
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01 PV-WORK-VARIABLES.                                                    
         05 PV-PARAGRAPH-NAME              PIC  X(30) VALUE SPACES.             
         05 PV-DB2-OPERATION-ATTEMPTED     PIC  X(30) VALUE SPACES.             
         05 PV-SQLCODE-DISPLAY             PIC +(8)9  VALUE ZEROES.             
         05 PV-STD-TM-ZN-CDE-NI            PIC S9(04) COMP VALUE 0.             
           88 PV-STD-TM-ZN-CDE-IS-NULL                     VALUE -1.            
         05 PV-DST-IND-NI                  PIC S9(04) COMP VALUE 0.             
           88 PV-DST-IND-IS-NULL                           VALUE -1.            
         05 PV-EXEC-TMST-NI                PIC S9(04) COMP VALUE ZERO.          
           88 PV-EXTR-TMST-IS-NULL                         VALUE -1.            
           88 PV-EXTR-TMST-NOT-NULL                        VALUE 0.             
         05 PV-MDSE-EXPRN-DTE-NI           PIC S9(04) COMP VALUE ZERO.          
           88 PV-MDSE-EXPRN-DTE-IS-NULL                    VALUE -1.            
           88 PV-MDSE-EXPRN-DTE-NOT-NULL                   VALUE 0.             
         05 PV-ORIG-CLR-DTE-DTE-NI         PIC S9(04) COMP VALUE ZERO.          
           88 PV-ORIG-CLR-DTE-IS-NULL                      VALUE -1.            
           88 PV-ORIG-CLR-DTE-NOT-NULL                     VALUE 0.             
         05 PV-SKU-COMMIT-INTERVAL         PIC S9(09) COMP-3 VALUE +50.         
         05 PV-SKU-COMMIT-CNTR             PIC S9(09) VALUE +1 COMP-3.          
         05 WS-TABLE-COUNT                 PIC S9(09) VALUE 0  COMP.            
         05 PV-CURRENT-DATE                PIC X(10).                           
         05 PV-CURRENT-TMST                PIC X(26).                           
         05 PV-LAST-RUN-TMST               PIC X(26).                           
         05 PV-I-CNTR                      PIC 9(05) COMP VALUE 0.              
         05 PV-J                           PIC S9(3) COMP VALUE 0.              
         05 PV-J-CNTR                      PIC 9(10) COMP VALUE 0.              
         05 PV-J-MSD-INS-CNTR              PIC 9(3)  COMP VALUE 0.              
      ******************************************************************01620000
      * ARRAY OF ICT_SKU_LOC_CNT                                       *01630000
      ******************************************************************01640000
           COPY ICRD054.                                                        
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01 PS-PROGRAM-SWITCHES.                                                  
         05 PS-END-OF-MNT-INPUT-FL-SW      PIC  X(01)   VALUE 'N'.              
           88  PS-END-OF-MNT-INPUT-FL                   VALUE 'Y'.              
         05 PS-PROCESS-STOP-SW             PIC  X(01)   VALUE 'N'.              
           88  PS-PROCESS-STOP                          VALUE 'Y'.              
         05 PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'Y'.              
           88  PS-FIRST-COMMIT                          VALUE 'Y'.              
         05 PS-INPUT-SKU-FOUND-SW          PIC  X(01)   VALUE 'Y'.              
           88  PS-INPUT-SKU-FOUND                       VALUE 'Y'.              
           88  PS-INPUT-SKU-NOT-FOUND                   VALUE 'N'.              
                                                                                
       01 ABEND-CODE                      PIC S9(04) VALUE +0 COMP SYNC.        
       01 CR-CHECKPOINT-RESTART-AREA.                                           
         05 CR-AREA-LEN                     PIC S9(04)  VALUE +5 COMP.          
         05 CR-CHECKPOINT-RESTART-VAR.                                          
           10 CR-MNT-READ-CNT               PIC 9(09)   VALUE 0 COMP-3.         
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-MNT-CNTR                  PIC  9(09)  VALUE ZEROS.            
           05  DI-SKU-COMMIT-CNTR           PIC  9(09)  VALUE ZEROS.            
                                                                                
      *****************************************************************         
      *    TIMEZONE CONVERSION ROUTINE COPYBOOK                                 
      *****************************************************************         
           COPY ICWS010.                                                        
      *****************************************************************         
      *    INPUT/OUTPUT COPYBOOKS                                               
      *****************************************************************         
           COPY ICRD057.                                                        
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART CONTROL TABLE                            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART INFORMATION TABLE                        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
      *****************************************************************         
      *  XST_SKU_LOC TABLE                                                      
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00008                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_SKU_LOC_CNT TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00011                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_LOC_CNT_STAT TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00020                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  XST_LOC TABLE                                                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME                         
                                                                                
           PERFORM 1000-INITIALIZATION                                          
                                                                                
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL                                 
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
             DISPLAY 'STARTING RECORD - ' ICRD057-IC-CNT-QTY-MAINT-FILE         
           END-IF                                                               
           PERFORM 2000-PROCESS-COUNT-MNT                                       
             UNTIL PS-END-OF-MNT-INPUT-FL                                       
           PERFORM 2100-SKU-MISSED-INSERTS                                      
           PERFORM 8000-SQL-COMMIT                                              
           DISPLAY 'COUNT MAINT PROCESSING DONE'                                
                                                                                
           PERFORM 3000-EOJ-PROCESSING                                          
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
           ACCEPT PC-JOB-NAME                                                   
           PERFORM 8100-INIT-CHECKPOINT-SELECT.                                 
           MOVE TCKRSTCNTL-CKPT-FRQNCY-QTY TO PV-SKU-COMMIT-INTERVAL.           
      ******************************************************************        
      *  SELECT SAVED DATA TO SEE THE PROGRAM IS IN A RESTART STATUS            
      *  FROM THE RESTART INFO TABLE AND THEN ONLY PROCESS NON-COMPLETED        
      *  RECORDS AS OF THE LAST COMMIT. IF NOT, THEN THE VARIABLES WILL         
      *  CONTAIN THEIR INITIAL VALUES AND THE PROGRAM WILL PROCESS ALL          
      *  RECORDS ON THE INPUT FILE.                                             
      ******************************************************************        
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
               PERFORM 8150-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                 TO CR-CHECKPOINT-RESTART-VAR                                   
               DISPLAY SPACES                                                   
               DISPLAY '** ICKUP059 IS BEING RESTARTED FROM RECORD# '           
                 CR-MNT-READ-CNT.                                               
                                                                                
           OPEN INPUT  IC-COUNT-QTY-MAINT-FILE.                                 
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL CR-MNT-READ-CNT TIMES.          
           IF CR-MNT-READ-CNT > 0                                               
             DISPLAY 'RESTART ON RECORD KEY - ' ICRD057-CNT-STUP-ID             
                                          ' - ' ICRD057-LOC-NBR                 
                                          ' - ' ICRD057-SKU-NBR.                
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-DATE = CURRENT DATE                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
           PERFORM 9000-TIMEZONE-CONVERSION-SETUP                               
           .                                                                    
                                                                                
      ******************************************************************        
      * 2000-PROCESS-COUNT-MNT.                                        *        
      *  ==> INSERT  ROWS FOR EACH ACTIVE COUNT/LOC/SKU RECORD         *        
      ******************************************************************        
       2000-PROCESS-COUNT-MNT.                                                  
           MOVE '2000-PROCESS-COUNT-MNT'    TO PV-PARAGRAPH-NAME.               
           MOVE  ICRD057-CNT-STUP-ID        TO  ICT00011-CNT-STUP-ID            
           MOVE  ICRD057-LOC-NBR            TO  ICT00011-LOC-NBR                
           MOVE  ICRD057-SKU-NBR            TO  ICT00011-SKU-NBR                
           MOVE  ICRD057-CNT-SEQ-NBR        TO  ICT00011-CNT-SEQ-NBR            
           PERFORM 8300-SEL-SKU-LOC-CNT                                         
           IF ICRD057-CNT-STUP-ID   NOT =  ICT00020-CNT-STUP-ID                 
            OR ICRD057-LOC-NBR      NOT =  ICT00020-LOC-NBR                     
            OR ICRD057-CNT-SEQ-NBR  NOT =  ICT00020-CNT-SEQ-NBR                 
             MOVE  ICRD057-LOC-NBR          TO  ICT00020-LOC-NBR                
             MOVE  ICRD057-CNT-STUP-ID      TO  ICT00020-CNT-STUP-ID            
             MOVE  ICRD057-CNT-SEQ-NBR      TO  ICT00020-CNT-SEQ-NBR            
      *** ADJUST TO STORE LOCAL TIME ZONE.                                      
             MOVE ICRD057-LOC-NBR        TO  XST00001-LOC-NBR                   
             PERFORM 8500-SELECT-XST-LOC                                        
             MOVE XST00001-STD-TM-ZN-CDE TO  ICWS010-TZC-TZ-IN-TM-ZN-CDE        
             MOVE XST00001-DST-IND       TO  ICWS010-TZC-TZ-IND-ST-IND          
             PERFORM 9000-TIMEZONE-CONVERSION                                   
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST           
             PERFORM 8400-INS-SKU-LOC-CNT-ST                                    
             ADD   1                     TO  PV-I-CNTR                          
           END-IF                                                               
                                                                                
           IF PS-INPUT-SKU-FOUND                                                
             PERFORM 2200-MOVE-VALUE-IC054                                      
             PERFORM 8600-DELETE-SKU-LOC                                        
             IF PV-J = 100                                                      
               ADD 1                        TO PV-J-CNTR                        
               PERFORM 8200-INS-SKU-LOC-CNT                                     
             END-IF                                                             
           END-IF                                                               
           IF PV-SKU-COMMIT-CNTR > PV-SKU-COMMIT-INTERVAL                       
             PERFORM 2100-SKU-MISSED-INSERTS                                    
             PERFORM 8000-SQL-COMMIT                                            
           END-IF                                                               
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL                                 
           .                                                                    
                                                                                
      ******************************************************************        
      **INSERT THE RECORDS THAT ARE STILL IN ARRAY**********************        
      ******************************************************************        
       2100-SKU-MISSED-INSERTS.                                                 
           IF PV-J > 0                                                          
             ADD PV-J                         TO PV-J-MSD-INS-CNTR              
             PERFORM 8200-INS-SKU-LOC-CNT                                       
           END-IF                                                               
           .                                                                    
                                                                                
      ******************************************************************        
      **MOVE VALUES TO ICRD054 ARRAY FOR INSERT   **********************        
      ******************************************************************        
       2200-MOVE-VALUE-IC054.                                                   
           MOVE ICT00011-EXEC-TMST         OF DCLICT00011                       
             TO IC054-EXEC-TMST(PV-J)                                           
           MOVE ICT00011-LAST-ACTN-CDE     OF DCLICT00011                       
             TO IC054-LAST-ACTN-CDE(PV-J)                                       
C63538     MOVE PV-MDSE-EXPRN-DTE-NI  TO IC054-MDSE-EXPRN-DTE-NI(PV-J)          
C63538     MOVE ICT00011-MDSE-EXPRN-DTE TO                                      
C63538                                IC054-MDSE-EXPRN-DTE(PV-J)                
           MOVE  ICRD057-CNT-STUP-ID  TO  IC054-CNT-STUP-ID(PV-J)               
           MOVE  ICRD057-LOC-NBR      TO  IC054-LOC-NBR(PV-J)                   
           MOVE  ICRD057-SKU-NBR      TO  IC054-SKU-NBR(PV-J)                   
           MOVE  ICRD057-CNT-SEQ-NBR  TO  IC054-CNT-SEQ-NBR(PV-J)               
           MOVE ICRD057-UNT-RTL-AMT   TO IC054-UNT-RTL-AMT(PV-J)                
           MOVE ICRD057-LOC-EXPTED-UNT-QTY TO                                   
                                        IC054-LOC-EXPTED-UNT-QTY(PV-J)          
           MOVE 0                     TO IC054-TRUK-UNT-QTY-NI(PV-J)            
           MOVE ICRD057-ACPTD-TRUK-UNT-QTY TO                                   
                                        IC054-ACPTD-TRUK-UNT-QTY(PV-J)          
           MOVE -1                    TO IC054-SCND-UPC-NBR-NI(PV-J)            
           MOVE -1                    TO IC054-SCND-UNT-QTY-NI(PV-J)            
           MOVE -1                    TO IC054-CNT-GP-CDE-NI(PV-J)              
           MOVE 0                     TO IC054-RTL-SAL-UNT-QTY-NI(PV-J)         
           MOVE ICRD057-SALES-QTY     TO IC054-RTL-SAL-UNT-QTY(PV-J)            
           MOVE 0                     TO IC054-RTL-RET-UNT-QTY-NI(PV-J)         
           MOVE ICRD057-RETURN-QTY    TO IC054-RTL-RET-UNT-QTY(PV-J)            
           MOVE 0                     TO IC054-XFER-IN-UNT-QTY-NI(PV-J)         
           MOVE ICRD057-XFER-IN-QTY   TO IC054-XFER-IN-UNT-QTY(PV-J)            
           MOVE 0                     TO IC054-XFER-OUT-UNT-QTY-NI(PV-J)        
           MOVE ICRD057-XFER-OUT-QTY  TO IC054-XFER-OUT-UNT-QTY(PV-J)           
           MOVE -1                    TO IC054-CNT-GP-CDE-NI(PV-J)              
           MOVE ICRD057-INTRANSIT-QTY TO IC054-IN-TRNST-UNT-QTY(PV-J)           
           MOVE 0                     TO IC054-TRNST-UNT-QTY-NI(PV-J)           
      ***                                                                       
      * IF THE SKU IS ACTIVE, THE ORIG CLR DATE SHOULD BE NULL                  
      ***                                                                       
           IF ICRD057-ACTIVE-IND   = 'Y'                                        
             SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                              
             MOVE -1                   TO IC054-MDSE-EXPRN-DTE-NI(PV-J)         
           ELSE                                                                 
      ***                                                                       
      * IF THE SKU IS CLEARANCE, CHECK TO SEE IF ORIG CLR DATE HAS NOT          
      * ALREADY BEEN SET. IF IT HAS BEEN, LEAVE IT, ELSE SET IT TO              
      * CLEARANCE DATE FROM INPUT FILE.                                         
      ***                                                                       
             IF PV-ORIG-CLR-DTE-IS-NULL                                         
               MOVE ICRD057-ORIG-CLR-DTE TO IC054-MDSE-EXPRN-DTE(PV-J)          
               SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                           
             END-IF                                                             
           END-IF                                                               
      *** THIS TMST HAS BEEN ADJUSTED (ABOVE) TO STORE LOCAL TIME ZONE.         
C11840     MOVE ICWS010-TZC-LOCATION-TMST TO IC054-LAST-UPD-TMST(PV-J)          
           .                                                                    
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILE THAT WAS OPENED IN PARA 1000.                  *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - SETS CHEKPOINT STATUS CODE TO 0 (PROGRAM NOT IN PROCESS OR*        
      *      IN RESTART STATUS.                                        *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE IC-COUNT-QTY-MAINT-FILE                                        
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE                                
           PERFORM 8160-UPDATE-CHECKPOINT-STATUS                                
           PERFORM 9990-DISPLAY-CONTROL-INFO                                    
           .                                                                    
                                                                                
      *----------------------------------------------------------               
      * 7000-READ-COUNT-QTY-MAINT-FL.                                           
      *----------------------------------------------------------               
       7000-READ-COUNT-QTY-MAINT-FL.                                            
           MOVE '7000-READ-COUNT-QTY-MAINT-FL'   TO PV-PARAGRAPH-NAME.          
           READ IC-COUNT-QTY-MAINT-FILE                                         
             INTO ICRD057-IC-CNT-QTY-MAINT-FILE                                 
           AT END SET PS-END-OF-MNT-INPUT-FL TO TRUE.                           
           ADD +1 TO DI-MNT-CNTR                                                
           .                                                                    
                                                                                
      *----------------------------------------------------------               
      * 8000-SQL-COMMIT.                                                        
      *----------------------------------------------------------               
       8000-SQL-COMMIT.                                                         
           MOVE '8000-SQL-COMMIT' TO PV-PARAGRAPH-NAME.                         
           MOVE 'COMMIT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           MOVE DI-MNT-CNTR   TO CR-MNT-READ-CNT.                               
           MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
                                                                                
           IF PS-FIRST-COMMIT                                                   
             MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                       
             PERFORM 8160-UPDATE-CHECKPOINT-STATUS                              
             MOVE 'N'        TO PS-FIRST-COMMIT-SW                              
           END-IF                                                               
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               CONTINUE                                                         
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 CHECKPOINT HAD SQLCODE ' SQLCODE            
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
             COMMIT WORK                                                        
           END-EXEC.                                                            
           DISPLAY 'SKU COMMIT COMPLETED'                                       
           ADD +1 TO DI-SKU-COMMIT-CNTR                                         
           MOVE 1 TO PV-SKU-COMMIT-CNTR                                         
           .                                                                    
                                                                                
      *----------------------------------------------------------               
      *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF             
      *  THE PROGRAM IS IN A RESTART CONDITION.                                 
      *----------------------------------------------------------               
       8100-INIT-CHECKPOINT-SELECT.                                             
           MOVE '8100-INIT-CHECKPOINT-SELECT' TO  PV-PARAGRAPH-NAME.            
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                             
             SELECT CKPT_STAT_CODE                                              
                   ,CKPT_FRQNCY_QTY                                             
             INTO   :TCKRSTCNTL-CKPT-STAT-CODE                                  
                   ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                 
             FROM  TCKRSTCNTL                                                   
             WHERE  JOB_NAME  = :PC-JOB-NAME                                    
               AND  PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               DISPLAY 'FRQNCY-QTY' TCKRSTCNTL-CKPT-FRQNCY-QTY                  
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 SELECT CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO                
      *  OBTAIN THE PROGRAMS SAVED VARIABLES.                                   
      ******************************************************************        
       8150-SELECT-RESTART-INFO.                                                
           MOVE '8150-SELECT-RESTART-INFO ' TO  PV-PARAGRAPH-NAME.              
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                             
             SELECT WORK_STRG_DESC                                              
             INTO   :TCKRSTINF-WORK-STRG-DESC                                   
             FROM   TCKRSTINF                                                   
             WHERE  JOB_NAME  = :PC-JOB-NAME                                    
               AND  PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               CONTINUE                                                         
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 SELECT XRST'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE                      
      ******************************************************************        
       8160-UPDATE-CHECKPOINT-STATUS.                                           
           MOVE '8160-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME.           
           MOVE 'UPDATE' TO PV-DB2-OPERATION-ATTEMPTED.                         
           DISPLAY 'JOB '    PC-JOB-NAME                                        
           DISPLAY 'PGM NAME ' PC-PROGRAM-NAME                                  
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               CONTINUE                                                         
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 UPDATE CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               DISPLAY ' KEY - '   PC-JOB-NAME                                  
                                   PC-PROGRAM-NAME                              
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE                                                         
           .                                                                    
                                                                                
      *-------------------------------------------------------------            
      * 8200-INS-SKU-LOC-CNT.                                                   
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE            
      *-------------------------------------------------------------            
       8200-INS-SKU-LOC-CNT.                                                    
           MOVE '8200-INS-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.                    
           MOVE 'INSERT' TO PV-DB2-OPERATION-ATTEMPTED.                         
             EXEC SQL                                                     062000
               INSERT INTO ICT_SKU_LOC_CNT                                062100
                    (LOC_NBR                                              062200
                    ,SKU_NBR                                              062300
                    ,CNT_STUP_ID                                          062400
                    ,LAST_UPD_TMST                                        062500
                    ,CNT_SEQ_NBR                                          062600
                    ,LAST_UPD_USR_ID                                      062700
                    ,LAST_ACTN_CDE                                        062800
                    ,UNT_RTL_AMT                                          062900
                    ,LOC_EXPTED_UNT_QTY                                   063000
                    ,IN_TRNST_UNT_QTY                                     063100
                    ,ACPTD_TRUK_UNT_QTY                                   063200
                    ,RTL_SAL_UNT_QTY                                      063300
                    ,RTL_RET_UNT_QTY                                      063400
                    ,XFER_IN_UNT_QTY                                      063500
                    ,XFER_OUT_UNT_QTY                                     063600
                    ,SCND_UPC_NBR                                         063700
                    ,SCND_UNT_QTY                                         063800
                    ,CNT_GP_CDE                                           063900
                    ,EXEC_TMST                                            064000
                    ,MDSE_EXPRN_DTE)                                      064100
              VALUES                                                      064200
                  (:IC054-LOC-NBR                                         064300
                  ,:IC054-SKU-NBR                                         064400
                  ,:IC054-CNT-STUP-ID                                     064500
                  ,:IC054-LAST-UPD-TMST                                   064600
                  ,:IC054-CNT-SEQ-NBR                                     064700
                  ,'SYSTEM'                                               064800
                  ,:IC054-LAST-ACTN-CDE                                   064900
                  ,:IC054-UNT-RTL-AMT                                     065000
                  ,:IC054-LOC-EXPTED-UNT-QTY                              065100
                  ,:IC054-IN-TRNST-UNT-QTY:IC054-TRNST-UNT-QTY-NI         065200
                  ,:IC054-ACPTD-TRUK-UNT-QTY:IC054-TRUK-UNT-QTY-NI        065300
                  ,:IC054-RTL-SAL-UNT-QTY:IC054-RTL-SAL-UNT-QTY-NI        065400
                  ,:IC054-RTL-RET-UNT-QTY:IC054-RTL-RET-UNT-QTY-NI        065500
                  ,:IC054-XFER-IN-UNT-QTY:IC054-XFER-IN-UNT-QTY-NI        065600
                  ,:IC054-XFER-OUT-UNT-QTY:IC054-XFER-OUT-UNT-QTY-NI      065700
                  ,:IC054-SCND-UPC-NBR:IC054-SCND-UPC-NBR-NI              065800
                  ,:IC054-SCND-UNT-QTY:IC054-SCND-UNT-QTY-NI              065900
                  ,:IC054-CNT-GP-CDE:IC054-CNT-GP-CDE-NI                  066000
                  ,:IC054-EXEC-TMST:IC054-EXEC-TMST-NI                    066100
                  ,:IC054-MDSE-EXPRN-DTE:IC054-MDSE-EXPRN-DTE-NI)         066200
                  FOR :PV-J ROWS ATOMIC                                   066300
             END-EXEC                                                     066500
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = 0                                                 
                 ADD 1                 TO PV-SKU-COMMIT-CNTR                    
                 MOVE 0                TO PV-J                                  
                 INITIALIZE ARRAY-ISLC                                          
               WHEN OTHER                                                       
                 SET PS-PROCESS-STOP    TO TRUE                                 
                 DISPLAY '** ICKUP059 INSERT ICT_SKU_LOC_CNT'                   
                         ' HAD SQLCODE OF ' SQLCODE                             
                 PERFORM 9999-SQL-ABEND                                         
             END-EVALUATE                                                       
           .                                                                    
                                                                                
      *-------------------------------------------------------------            
      * 8300-SEL-SKU-LOC-CNT.                                                   
      *      SELECT CURRENT ICT_SKU_LOC_CNT ROW.                                
      *-------------------------------------------------------------            
       8300-SEL-SKU-LOC-CNT.                                                    
           MOVE '8300-SEL-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.                    
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
             EXEC SQL                                                           
               SELECT LAST_ACTN_CDE                                             
                    ,EXEC_TMST                                                  
                    ,MDSE_EXPRN_DTE                                             
               INTO :ICT00011-LAST-ACTN-CDE                                     
                   ,:ICT00011-EXEC-TMST:PV-EXEC-TMST-NI                         
                   ,:ICT00011-MDSE-EXPRN-DTE:PV-MDSE-EXPRN-DTE-NI               
               FROM  ICT_SKU_LOC_CNT                                            
               WHERE LOC_NBR       = :ICT00011-LOC-NBR                          
                 AND SKU_NBR       = :ICT00011-SKU-NBR                          
                 AND CNT_STUP_ID   = :ICT00011-CNT-STUP-ID                      
                 AND CNT_SEQ_NBR   = :ICT00011-CNT-SEQ-NBR                      
               ORDER BY LAST_UPD_TMST DESC                                      
               FETCH FIRST 1 ROW ONLY                                           
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = 0                                                 
                 ADD 1                        TO PV-J                           
                 SET PS-INPUT-SKU-FOUND       TO TRUE                     070700
                 MOVE ICT00011-MDSE-EXPRN-DTE    OF DCLICT00011                 
                   TO IC054-MDSE-EXPRN-DTE(PV-J)                                
                 MOVE 0                 TO IC054-MDSE-EXPRN-DTE-NI(PV-J)        
               WHEN SQLCODE = -305                                              
                 MOVE 0                       TO SQLCODE                        
                 ADD 1                        TO PV-J                           
                 SET PS-INPUT-SKU-FOUND       TO TRUE                           
                 MOVE -1                TO IC054-MDSE-EXPRN-DTE-NI(PV-J)        
               WHEN SQLCODE = +100                                              
                 SET PS-INPUT-SKU-NOT-FOUND TO TRUE                             
                 DISPLAY 'KEY NF IS - ' ICT00011-LOC-NBR                        
                               ' - '      ICT00011-SKU-NBR                      
                               ' - '      ICT00011-CNT-STUP-ID                  
                               ' - '      ICT00011-CNT-SEQ-NBR                  
               WHEN OTHER                                                       
                 SET PS-PROCESS-STOP    TO TRUE                                 
                 DISPLAY '** ICKUP059 SELECT ICT_SKU_LOC_CNT'                   
                         ' HAD SQLCODE OF ' SQLCODE                             
                 PERFORM 9999-SQL-ABEND                                         
             END-EVALUATE                                                       
           .                                                                    
                                                                                
      *-------------------------------------------------------------            
      * 8400-INS-SKU-LOC-CNT-ST.                                                
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE            
      *-------------------------------------------------------------            
       8400-INS-SKU-LOC-CNT-ST.                                                 
           MOVE '8400-INS-SKU-LOC-CNT-ST' TO PV-PARAGRAPH-NAME.                 
           MOVE 'INSERT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                      0737002
             INSERT INTO ICT_LOC_CNT_STAT                                0738002
               (CNT_STAT_CDE                                             0739002
               ,LOC_NBR                                                  0740002
               ,CNT_STUP_ID                                              0741002
               ,CNT_SEQ_NBR                                              0742002
               ,LAST_UPD_TMST                                            0743002
               ,LAST_UPD_USR_ID                                          0744002
               ,LAST_ACTN_CDE)                                           0745002
             VALUES                                                      0746002
               ('UPD'                                                    0747002
               ,:ICT00020-LOC-NBR                                               
               ,:ICT00020-CNT-STUP-ID                                           
               ,:ICT00020-CNT-SEQ-NBR                                           
               ,:ICT00020-LAST-UPD-TMST                                         
               ,'SYSTEM'                                                 0752002
               ,'A')                                                     0753002
           END-EXEC.                                                    07560020
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
             WHEN SQLCODE = -803                                                
                  CONTINUE                                                      
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 INSERT ICT_LOC_CNT_STAT'                    
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE                                                         
           .                                                                    
                                                                                
      *----------------------------------------------------------               
      *  SELECT FROM XST_LOC TO GET A STORE TIMEZONE.                           
      *----------------------------------------------------------               
       8500-SELECT-XST-LOC.                                                     
           MOVE '8500-SELECT-XST-LOC' TO  PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
             SELECT  STD_TM_ZN_CDE                                              
                    ,DST_IND                                                    
               INTO  :XST00001-STD-TM-ZN-CDE:PV-STD-TM-ZN-CDE-NI                
                    ,:XST00001-DST-IND:PV-DST-IND-NI                            
               FROM  XST_LOC                                                    
              WHERE  LOC_NBR   = :XST00001-LOC-NBR                              
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = 0                                                   
               IF PV-DST-IND-IS-NULL                                            
                 MOVE 'Y' TO XST00001-DST-IND                                   
               END-IF                                                           
               IF PV-STD-TM-ZN-CDE-IS-NULL                                      
                 MOVE 'CST' TO XST00001-STD-TM-ZN-CDE                           
               END-IF                                                           
             WHEN OTHER                                                         
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP059 SELECT XST_LOC'                             
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE                                                         
           .                                                                    
                                                                                
      ******************************************************************        
      *  ==> PROCESSES:                                                *        
      *    - DELETE ALL ROWS WITH SPECIFIED CNT-STUP-ID, LOC-NBR,      *        
      *      CNT-SEQ-NBR AND SKU-NBR.                                  *        
      ******************************************************************        
       8600-DELETE-SKU-LOC.                                                     
           MOVE '8600-DELETE-SKU-LOC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           EXEC SQL                                                             
              DELETE                                                            
              FROM  ICT_SKU_LOC_CNT                                             
              WHERE CNT_STUP_ID      = :ICT00011-CNT-STUP-ID                    
                AND LOC_NBR          = :ICT00011-LOC-NBR                        
                AND CNT_SEQ_NBR      = :ICT00011-CNT-SEQ-NBR                    
                AND SKU_NBR          = :ICT00011-SKU-NBR                        
CL3132          AND LAST_UPD_USR_ID  = 'SYSTEM'                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
               CONTINUE                                                         
             WHEN SQLCODE = +100                                                
               DISPLAY 'NOT FOUND - ' ICT00011-CNT-STUP-ID                      
                                ' - ' ICT00011-LOC-NBR                          
                                ' - ' ICT00011-CNT-SEQ-NBR                      
                                ' - ' ICT00011-SKU-NBR                          
             WHEN OTHER                                                         
               DISPLAY 'DELETE KEY- ' ICT00011-CNT-STUP-ID                      
                                ' - ' ICT00011-LOC-NBR                          
                                ' - ' ICT00011-CNT-SEQ-NBR                      
                                ' - ' ICT00011-SKU-NBR                          
               MOVE 'DELETE ICT_SKU_LOC' TO PV-DB2-OPERATION-ATTEMPTED          
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE                                                         
           .                                                                    
                                                                                
      ******************************************************************        
           COPY ICPD010.                                                        
      ******************************************************************        
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           SUBTRACT 1 FROM DI-MNT-CNTR.                                         
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUP059 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'NO OF INSERTS HAPPENED IN ICT_LOC_CNT_STAT TABLE            
      -            '      :' PV-I-CNTR.                                         
           DISPLAY 'NO OF MULTI-ROW INSERTS HAPPENED IN ICT_SKU_LOC_CNT         
      -            'TABLE :' PV-J-CNTR.                                         
           DISPLAY 'MISSED INSERTS HAPPENED IN ICT_SKU_LOC_CNT TABLE            
      -            '      :' PV-J-MSD-INS-CNTR.                                 
           DISPLAY 'MAINTENANCE RECORDS PROCESSED                               
      -            '      :' DI-MNT-CNTR.                                       
           DISPLAY 'SKU COMMITS TAKEN                                           
      -            '      :' DI-SKU-COMMIT-CNTR.                                
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUP059  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ICKUP059 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE                                     
           .                                                                    
