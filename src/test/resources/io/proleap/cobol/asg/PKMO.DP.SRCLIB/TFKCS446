      *-----------------------*                                         00010027
       IDENTIFICATION DIVISION.                                         00020027
      *-----------------------*                                         00030027
       PROGRAM-ID.    TFKCS446.                                         00040027
       AUTHOR.        JACK KRAMER.                                      00050027
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060027
       DATE-WRITTEN.  MARCH 1997.                                       00070027
       DATE-COMPILED.                                                   00080027
      *----------------------------------------------------------------*00090027
      *    P446 - FROM STORE OVERRIDE.                                 *00100027
      *                                                                *00110027
      *    THIS PROGRAM ALLOWS THE USER TO ENTER A FROM STORE NUMBER   *00120027
      *    TO BE USED IN COMPLETING A TRANSFER FROM A STORE OTHER THAN *00130027
      *    THEIR DEFAULT STORE LOCATION - USED ONLY BY CORPORATE AND   *00140027
      *    DC PERSONNEL WHO HAVE SECURITY ACCESS.                      *00150027
      *----------------------------------------------------------------*00160027
      ******************************************************************00161027
      * 6/02/2003 - RAY GRAF                                            00162027
      *  PROCESS NONINTELLIGENT SKU.                                    00163027
      ******************************************************************00164027
080204**DATE:08/02/2004 CHANGED BY: RAY GRAF                            00165027
080204**     RECOMPILE DUE TO PRINTER EXPANSION IN TFWS001              00166027
      ***************************************************************** 00167027
060110*  DATE 06/01/10 JACKIE HELING                                    00168033
060110*  BLOCK CERTAIN LOCATIONS FROM SENDING/RECEIVING TRANSFERS.      00169033
060110******************************************************************00169133
033111* WR39649 -  DAVID WELLER - 03/31/2011                            00169235
033111*            MLOF - DO NOT EXCLUDE LOCATIONS 807, 808 AND 809     00169335
033111***************************************************************** 00169435
CICS  * CHANGED 11/17/2022    BY JIM HUNTER      WR: CHG0278345         00169536
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL        00169636
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.       00169736
CICS  *-----------------------------------------------------------------00169836
       ENVIRONMENT DIVISION.                                            00170027
                                                                        00180027
       DATA DIVISION.                                                   00190027
                                                                        00200027
       WORKING-STORAGE SECTION.                                         00210027
      *                                                                 00220027
       01  DK-DB2-KEYS.                                                 00230027
           05  DK-STORE-NBR                   PIC  S9(4)  VALUE ZERO    00240027
                                                          COMP.         00250027
      *                                                                 00260027
       01  PC-PROGRAM-CONSTANTS.                                        00270027
           05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TF446A  '. 00280027
           05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TFKM446 '. 00290027
           05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TFKCS446'. 00300027
           05  PC-COMPLETE-SETUP         PIC  X(01)  VALUE  '1'.        00310027
           05  PC-WRKSHT-CRIT-SPEC       PIC  X(01)  VALUE  '2'.        00320027
           05  PC-TSYMSG-NUMBERS.                                       00400027
               10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.      00410027
               10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.      00420027
               10  PC-TSYMSG-00017       PIC  9(05)  VALUE  00017.      00430027
060110         10  PC-TSYMSG-01539       PIC  9(05)  VALUE  01539.      00440033
           05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       00460027
                                                          COMP-3.       00470027
      *                                                                 00480027
       01  PV-PROGRAM-VARABLES.                                         00490027
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE +0      00500027
                                                          COMP SYNC.    00510027
      *                                                                 00520027
       01  PS-PROGRAM-SWITCHES.                                         00530027
           05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        00540027
               88  PS-ERROR                          VALUE  'Y'.        00550027
               88  PS-NO-ERROR                       VALUE  'N'.        00560027
           05  PS-STORE-FOUND-SW              PIC  X(01)  VALUE 'Y'.    00570027
               88  PS-STORE-FOUND                         VALUE 'Y'.    00580027
               88  PS-STORE-NOT-FOUND                     VALUE 'N'.    00590027
      *                                                                 00600027
       01  PM-PROGRAM-MESSAGES.                                         00610027
           05  PM-ABEND-INFO-A.                                         00620027
               10  FILLER                PIC X(15)   VALUE              00630027
                                         'DP020-SRC-AID: '.             00640027
               10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.      00650027
               10  FILLER                PIC X(19)   VALUE              00660027
                                         '. DP020-FK-ACTION: '.         00670027
               10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.      00680027
               10  FILLER                PIC X(22)   VALUE              00690027
                                         '. DP020-FK-DISPN-IND: '.      00700027
               10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.      00710027
               10  FILLER                PIC X(01)   VALUE '.'.         00720027
               10  FILLER                PIC X(19)   VALUE SPACE.       00730027
                                                                        00740027
                                                                        00750027
      *----------------------------------------------------------------*00760027
      *    MAP LAYOUT                                                  *00770027
      *----------------------------------------------------------------*00780027
                                                                        00790027
           COPY TFKM446.                                                00800027
           EJECT                                                        00810027
      *                                                                 00820027
060110*----------------------------------------------------------------*00821033
060110*  LIST OF LOCATION EXCLUSIONS                                    00822033
060110*----------------------------------------------------------------*00823033
060110     COPY TFWS010.                                                00824033
                                                                        00825030
      *----------------------------------------------------------------*00830027
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *00840027
      *----------------------------------------------------------------*00850027
                                                                        00860027
           COPY DPWS015.                                                00870027
                                                                        00880027
      *----------------------------------------------------------------*00890027
      *    FUNCTION KEYS COPYBOOK                                      *00900027
      *----------------------------------------------------------------*00910027
                                                                        00920027
           COPY DPWS016.                                                00930027
       EJECT                                                            00940027
      ******************************************************************00950027
      ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **00960027
      ** SUB-ROUTINE (DPKUT100).                                      **00970027
      ******************************************************************00980027
           COPY DPWS010I.                                               00990027
       EJECT                                                            01000027
      ******************************************************************01010027
      ** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **01020027
      ** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **01030027
      ******************************************************************01040027
           COPY DPWS030.                                                01050027
CICS       COPY DPWS930.                                                01051036
       EJECT                                                            01060027
      ***************************************************************** 01070027
      *    STANDARD COMMAREA.                                           01080027
      ***************************************************************** 01090027
                                                                        01100027
           COPY DPWS020.                                                01110027
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                01120027
                                                                        01130027
      ***************************************************************** 01140027
      *    INTER-APPLICATION COMMAREA.                                  01150027
      ***************************************************************** 01160027
                                                                        01170027
               10  INTER-APPL-COMM-AREA PIC X(200).                     01180027
      *---------------------------------------------------------------* 01190027
      * INTER-APPLICATION COMMAREA.                                   * 01200027
      *   TFWS001 - IS THE INPUT LAYOUT WHEN COMING FROM COMPLETE     * 01210027
      *             TRANSFER SETUP OR CRITERIA SPEC AND IS ALSO THE   * 01220027
      *             OUTPUT LAYOUT THAT WILL BE RETURNED TO THOSE      * 01230027
      *             PROGRAMS.                                         * 01240027
      *---------------------------------------------------------------* 01290027
           COPY TFWS001.                                                01300027
      ***************************************************************** 01320027
      *    SPECIFIC COMMMAREA FOR TFKCS446.                             01330027
      ***************************************************************** 01340027
               10  ASC-SPECIFIC-COMMAREA.                               01350027
                   15  ASC-STORE-NBR-X.                                 01360027
100404*                20  ASC-STORE-NBR           PIC  9(03).          01370028
100404                 20  ASC-STORE-NBR           PIC  9(04).          01371029
                   15  ASC-STORE-NAM               PIC  X(10).          01380027
           EJECT                                                        01400027
      *----------------------------------------------------------------*01410027
      *    ABEND PROCESSING COPYBOOK.                                  *01420027
      *----------------------------------------------------------------*01430027
                                                                        01440027
           COPY DPWS013.                                                01450027
                                                                        01460027
      *---------------------------------------------------------------* 01470027
      *    DB2 AREA FOR STORE TABLE                                   * 01480027
      *---------------------------------------------------------------* 01490027
           EXEC SQL                                                     01500027
                INCLUDE XST00001                                        01510028
           END-EXEC.                                                    01520027
      *---------------------------------------------------------------* 01530027
      *    DB2 AREA FOR COMMUNICATIONS                                * 01540027
      *---------------------------------------------------------------* 01550027
           EXEC SQL                                                     01560027
                INCLUDE SQLCA                                           01570027
           END-EXEC.                                                    01580027
                                                                        01590027
       EJECT                                                            01600027
       LINKAGE SECTION.                                                 01610027
                                                                        01620027
       01  DFHCOMMAREA.                                                 01630027
           05  FILLER                         OCCURS  1 TO 4072 TIMES   01640027
                                              DEPENDING ON EIBCALEN.    01650027
               10  FILLER                     PIC X.                    01660027
                                                                        01670027
                                                                        01680027
       PROCEDURE DIVISION.                                              01690027
      ***************************************************************** 01700027
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** 01710027
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** 01720027
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** 01730027
      **                                                             ** 01740027
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** 01750027
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** 01760027
      ***************************************************************** 01770027
       0000-MAIN-MODULE.                                                01780027
           INITIALIZE DP030-CICS-API-FIELDS.                            01790027
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       01800027
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          01810027
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         01820027
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       01830027
           MOVE LENGTH OF TF446AI        TO DP030-MAP-LENGTH (1).       01840027
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     01850027
           PERFORM 0001-CALL-CICS-ARCH-API.                             01860027
      *                                                                 01870027
           PERFORM 1000-CONTROL-PROCESSING                              01880027
      *                                                                 01890027
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     01900027
           PERFORM 0001-CALL-CICS-ARCH-API.                             01910027
                                                                        01920027
       0001-CALL-CICS-ARCH-API.                                         01930027
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      01940036
                                          DFHCOMMAREA                   01950036
                                          DP030-CICS-API-FIELDS         01960036
                                          DP020-STANDARD-COMMAREA       01970036
                                          TF446AI.                      01980036
      *                                                                 01990027
           IF  DP030-RC-CALL-SUCCESSFUL                                 02000027
               CONTINUE                                                 02010027
           ELSE                                                         02020027
               SET DP013-NO-ROLLBACK                                    02030027
                   DP013-XCTL-DISPLAY-RESTART                           02040027
                   DP013-CICS-ABEND      TO TRUE                        02050027
               MOVE 'BEFORE 0000-MAIN-MODULE'                           02060027
                                         TO DP013-PARAGRAPH             02070027
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O02071036
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      02072036
               MOVE DP030-RETURN-CODE                                   02100027
                                         TO DP013-MESSAGE-TEXT (3)      02110027
               PERFORM DP013-0000-PROCESS-ABEND                         02120027
           END-IF.                                                      02130027
       EJECT                                                            02140027
      *----------------------------------------------------------------*02150027
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *02160027
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *02170027
      *----------------------------------------------------------------*02180027
                                                                        02190027
       1000-CONTROL-PROCESSING.                                         02200027
                                                                        02210027
           EVALUATE TRUE                                                02220027
               WHEN DP020-NEXT-ACT-INITIAL                              02230027
                   INITIALIZE ASC-SPECIFIC-COMMAREA                     02240027
                   MOVE TF001-XFER-FROM-STR  TO ASC-STORE-NBR           02250027
                   PERFORM 4000-BUILD-INITIAL-PANEL                     02260027
                                                                        02270027
               WHEN DP020-NEXT-ACT-READ-MAP                             02280027
                   PERFORM 2000-FKEY-PROCESSING                         02290027
                                                                        02300027
               WHEN DP020-NEXT-ACT-RETURN                               02310027
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02320027
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02330027
                                                                        02340027
               WHEN OTHER                                               02350027
                   SET DP013-LOGIC-ABEND                                02360027
                       DP013-NO-ROLLBACK TO TRUE                        02370027
                   MOVE '1000-CONTROL-PROCESSING'                       02380027
                                         TO  DP013-PARAGRAPH            02390027
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   02400027
                                         TO  DP013-MESSAGE-TEXT(1)      02410027
                   MOVE DP020-NEXT-APPL-ACTIVITY                        02420027
                                         TO DP013-MESSAGE-TEXT(2)       02430027
                   PERFORM DP013-0000-PROCESS-ABEND                     02440027
           END-EVALUATE.                                                02450027
                                                                        02460027
      *----------------------------------------------------------------*02620027
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*02630027
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*02640027
      * EDITS.                                                         *02650027
      *----------------------------------------------------------------*02660027
                                                                        02670027
       2000-FKEY-PROCESSING.                                            02680027
                                                                        02690027
           EVALUATE TRUE                                                02700027
                                                                        02710027
               WHEN DP020-SRC-AID = DP016-CLEAR                         02720027
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02730027
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02740027
                                                                        02750027
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     02760027
                   PERFORM 4000-BUILD-INITIAL-PANEL                     02770027
                                                                        02780027
               WHEN OTHER                                               02790027
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 02800027
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                   02810027
                   IF  PS-NO-ERROR                                      02820027
                       SET DP030-CONTINUE-GO TO TRUE                    02830027
                       PERFORM 2100-CHECK-FUNCTION-KEY                  02840027
                   ELSE                                                 02850027
                       SET DP030-OVERRIDE-GO TO TRUE                    02860027
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             02870027
                   END-IF                                               02880027
           END-EVALUATE.                                                02890027
      ******************************************************************02900027
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **02910027
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **02920027
      ** FROM THE CICS ARCHITECTURE API.                              **02930027
      ******************************************************************02940027
       2100-CHECK-FUNCTION-KEY.                                         02950027
           EVALUATE TRUE                                                02960027
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)              02970027
      * ------  CONTINUE  ----------------------------------------------02980027
                   PERFORM 2110-PREPARE-INTER-APPL-COMM                 02990027
                                                                        03000027
               WHEN DP020-SRC-AID = DP016-ENTER                         03010027
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 03020027
                                                                        03030027
               WHEN OTHER                                               03040027
                   SET DP013-NO-ROLLBACK                                03050027
                       DP013-XCTL-DISPLAY-RESTART                       03060027
                       DP013-CICS-ABEND  TO TRUE                        03070027
                   MOVE '2100-CHECK-FUNCTION-KEY'                       03080027
                                         TO DP013-PARAGRAPH             03090027
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      03100027
                                         TO DP013-MESSAGE-TEXT (1)      03110027
                   MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:' 03120027
                                         TO DP013-MESSAGE-TEXT (2)      03130027
                   MOVE DP020-SRC-AID    TO PM-A-SRC-AID                03140027
                   MOVE DP020-FK-ACTION (DP020-SRC-AID)                 03150027
                                         TO PM-A-FK-ACTION              03160027
                   MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)              03170027
                                         TO PM-A-FK-DISPN               03180027
                   MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)      03190027
                   MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'        03200027
                                         TO DP013-MESSAGE-TEXT (4)      03210027
                   PERFORM DP013-0000-PROCESS-ABEND                     03220027
           END-EVALUATE.                                                03230027
      ******************************************************************03240027
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **03250027
      ** INTER-APPLICATION COMMAREA.                                  **03260027
      ******************************************************************03270027
       2110-PREPARE-INTER-APPL-COMM.                                    03280027
           INITIALIZE TF001-INTER-APPL-COMMAREA.                        03310027
           MOVE ASC-STORE-NBR         TO TF001-XFER-FROM-STR.           03320027
           SET TF001-FR-STR-OVERRIDE  TO TRUE.                          03330027
       EJECT                                                            03390027
      *----------------------------------------------------------------*03400027
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*03410027
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *03420027
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *03430027
      *----------------------------------------------------------------*03440027
                                                                        03450027
       2200-MOVE-SCREEN-TO-COMMAREA.                                    03460027
           IF  ASTRNBRL > 0                                             03470027
               MOVE ASTRNBRI             TO ASC-STORE-NBR-X             03480027
           ELSE                                                         03490027
               IF  ASTRNBRF = DP015-ERASE-EOF                           03500027
                   MOVE SPACE            TO ASC-STORE-NBR-X             03510027
               END-IF                                                   03520027
           END-IF.                                                      03530027
                                                                        03540027
      *----------------------------------------------------------------*03550027
      * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *03560027
      * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *03570027
      * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*03580027
      * IN THE DESTINATION PROGRAM, NOT HERE.                          *03590027
      *----------------------------------------------------------------*03600027
                                                                        03610027
       3000-EDIT-DATA-IN-COMMAREA.                                      03620027
           PERFORM 3200-RESET-ATTRIBUTES.                               03630027
           IF ASC-STORE-NBR-X NOT > SPACE                               03640027
               SET DP020-MSG-FATAL   TO TRUE                            03650027
      *        -- REQUIRED FIELD --                                     03660027
               MOVE DP015-UNP-ALP-BRT-OFF                               03670027
                                     TO ASTRNBRA                        03680027
               MOVE DP015-RED        TO ASTRNBRC                        03690027
               MOVE DP015-REVERSE    TO ASTRNBRH                        03700027
               IF PS-NO-ERROR                                           03710027
                   MOVE PC-TSYMSG-00007 TO DP020-MSG-NUMBER             03720027
                   MOVE -1           TO ASTRNBRL                        03730027
                   SET DP030-SET-CURSOR-APPL-1                          03740027
                                     TO TRUE                            03750027
                   SET PS-ERROR      TO TRUE                            03760027
               END-IF                                                   03770027
           ELSE                                                         03780027
               IF ASC-STORE-NBR-X NOT NUMERIC                           03790027
                   SET DP020-MSG-FATAL   TO TRUE                        03800027
      *        -- MUST BE NUMERIC --                                    03810027
                   MOVE DP015-UNP-ALP-BRT-OFF                           03820027
                                     TO ASTRNBRA                        03830027
                   MOVE DP015-RED        TO ASTRNBRC                    03840027
                   MOVE DP015-REVERSE    TO ASTRNBRH                    03850027
                   IF PS-NO-ERROR                                       03860027
                       MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER         03870027
                       MOVE -1           TO ASTRNBRL                    03880027
                       SET DP030-SET-CURSOR-APPL-1                      03890027
                                         TO TRUE                        03900027
                       SET PS-ERROR      TO TRUE                        03910027
                   END-IF                                               03920027
               ELSE                                                     03930027
                   SET PS-STORE-NOT-FOUND TO TRUE                       03940027
                   PERFORM 4500-READ-STORE                              03950027
                   IF PS-STORE-NOT-FOUND                                03960027
                       SET DP020-MSG-FATAL   TO TRUE                    03970027
      *        -- STORE DOES NOT EXIST --                               03980027
                       MOVE DP015-UNP-ALP-BRT-OFF                       03990027
                                             TO ASTRNBRA                04000027
                       MOVE DP015-RED        TO ASTRNBRC                04010027
                       MOVE DP015-REVERSE    TO ASTRNBRH                04020027
                       IF PS-NO-ERROR                                   04030027
                           MOVE PC-TSYMSG-00017 TO DP020-MSG-NUMBER     04040027
                           MOVE -1           TO ASTRNBRL                04050027
                           SET DP030-SET-CURSOR-APPL-1                  04060027
                                             TO TRUE                    04070027
                           SET PS-ERROR      TO TRUE                    04080027
                       END-IF                                           04090027
                   END-IF                                               04100027
               END-IF                                                   04110027
           END-IF.                                                      04120027
                                                                        04120131
060110     MOVE ASC-STORE-NBR-X TO PV-LOC-EXCLUDE                       04120233
060110     IF PV-EXCLUDE                                                04121033
060110         SET DP020-MSG-FATAL   TO TRUE                            04122033
060110*        -- INVALID LOCATION --                                   04123033
060110         MOVE DP015-UNP-ALP-BRT-OFF                               04124033
060110                               TO ASTRNBRA                        04125033
060110         MOVE DP015-RED        TO ASTRNBRC                        04126033
060110         MOVE DP015-REVERSE    TO ASTRNBRH                        04127033
060110         IF PS-NO-ERROR                                           04128033
060110             MOVE PC-TSYMSG-01539 TO DP020-MSG-NUMBER             04129033
060110             MOVE -1           TO ASTRNBRL                        04129133
060110             SET DP030-SET-CURSOR-APPL-1                          04129233
060110                               TO TRUE                            04129333
060110             SET PS-ERROR      TO TRUE                            04129433
060110         END-IF                                                   04129533
060110     END-IF.                                                      04129633
                                                                        04129731
           IF  PS-NO-ERROR                                              04130027
               MOVE -1                   TO ASTRNBRL                    04140027
               SET DP030-SET-CURSOR-APPL-1                              04150027
                                         TO TRUE                        04160027
           END-IF.                                                      04170027
      ******************************************************************04180027
      ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **04190027
      ** MAP.                                                         **04200027
      ******************************************************************04210027
       3200-RESET-ATTRIBUTES.                                           04220027
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRNBRA.                   04230027
           MOVE DP015-GREEN              TO ASTRNBRC.                   04240027
           MOVE DP015-UNDERLINE          TO ASTRNBRH.                   04250027
       EJECT                                                            04260027
      *                                                                 04270027
      *----------------------------------------------------------------*04280027
      *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *04290027
      *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *04300027
      *  IT HERE.                                                      *04310027
      *----------------------------------------------------------------*04320027
       4000-BUILD-INITIAL-PANEL.                                        04330027
                                                                        04340027
           SET DP030-SET-CURSOR-APPL-1   TO TRUE                        04350027
           MOVE -1                       TO ASTRNBRL.                   04360027
           PERFORM 3000-EDIT-DATA-IN-COMMAREA                           04370027
           PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        04380027
      *----------------------------------------------------------------*04390027
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *04400027
      *----------------------------------------------------------------*04410027
       4400-MOVE-COMMAREA-TO-SCREEN.                                    04420027
           MOVE ASC-STORE-NBR            TO ASTRNBRO.                   04430027
           MOVE ASC-STORE-NAM            TO ASTRNAMO.                   04440027
       EJECT                                                            04450027
      *----------------------------------------------------------------*04460027
      *    GET THE 10 CHARACTER ABBREVIATED STORE NAME.                *04470027
      *----------------------------------------------------------------*04480027
       4500-READ-STORE.                                                 04490027
           MOVE ASC-STORE-NBR            TO DK-STORE-NBR.               04500027
           PERFORM WITH TEST AFTER                                      04510027
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       04520027
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                  04530027
                    OR SQLCODE = ZERO                                   04540027
                    OR SQLCODE = +100                                   04550027
               EXEC SQL                                                 04560027
100404             SELECT LOC_10_ABBR_NM                                04570028
100404                 INTO :XST00001-LOC-10-ABBR-NM                    04580028
100404                 FROM XST_LOC                                     04590028
100404                 WHERE LOC_NBR     = :DK-STORE-NBR                04600028
               END-EXEC                                                 04610027
               EVALUATE TRUE                                            04620027
                   WHEN SQLCODE = ZERO                                  04621028
100404                 MOVE XST00001-LOC-10-ABBR-NM TO                  04622028
                                       ASC-STORE-NAM                    04623028
                       SET PS-STORE-FOUND  TO TRUE                      04624028
                   WHEN SQLCODE = +100                                  04625028
                       MOVE 'INVALID'      TO ASC-STORE-NAM             04626028
                   WHEN SQLCODE = -904                                  04630027
                   WHEN SQLCODE = -913                                  04640027
                       CONTINUE                                         04650027
                   WHEN OTHER                                           04720028
                        MOVE '4500-READ-STORE'                          04750027
                                          TO DP013-PARAGRAPH            04760027
                        MOVE 'READ A ROW FROM THE STORE TABLE'          04770027
                                          TO DP013-MESSAGE-TEXT(1)      04780027
                        MOVE SQLCA        TO DP013-SQLCA                04790027
100404                  MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (1)   04800028
                        SET DP013-DB2-ABEND                             04810027
                            DP013-XCTL-DISPLAY-RESTART TO TRUE          04820027
                        PERFORM DP013-0000-PROCESS-ABEND                04830027
               END-EVALUATE                                             04840027
           END-PERFORM.                                                 04850027
                                                                        04860027
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                           04870027
               MOVE '4500-READ-STORE' TO DP013-PARAGRAPH                04880027
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO READ'       04890027
                                          TO DP013-MESSAGE-TEXT (1)     04900027
               MOVE 'THE STORE TABLE'     TO DP013-MESSAGE-TEXT (2)     04910027
               MOVE SQLCA                 TO DP013-SQLCA                04920027
100404         MOVE 'XST_LOC'             TO DP013-DB2-TABLE-NAME (1)   04930028
               SET DP013-DB2-ABEND                                      04940027
                   DP013-XCTL-DISPLAY-RESTART                           04950027
                                          TO TRUE                       04960027
               PERFORM DP013-0000-PROCESS-ABEND                         04970027
           END-IF.                                                      04980027
       EJECT                                                            04990027
      *----------------------------------------------------------------*05000027
      *    ABEND PROCESSOR MODULE                                      *05010027
      *----------------------------------------------------------------*05020027
                                                                        05030027
           COPY DPPD013.                                                05040027
