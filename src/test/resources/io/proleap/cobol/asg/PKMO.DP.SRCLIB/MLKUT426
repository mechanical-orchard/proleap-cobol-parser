000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    MLKUT426.                                                 
000300 AUTHOR.        TESS BESTE.                                               
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  12-08-99.                                                 
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800*                                                                         
000900*       MLKUT426 - DEPARTMENT ELIMINATION PROCESSING                      
001000*                                                                         
001100* THIS PROGRAM PERFORMS THE PROCESSING ASSOCIATED WITH                    
001200* ELIMINATING A DEPARTMENT FROM MERCHANDISE LEDGER.  ELIMINATING          
001300* A DEPARTMENT CAUSES ANY REMAINING INVENTORY IN THE DEPARTMENT           
001400* TO BE 'MOVED' TO ANOTHER DEPARTMENT VIA PURCHASE ADJUSTMENT             
001500* TRANSACTIONS.  THE HISTORY FOR THE DEPARTMENT BEING ELIMINATED          
001600* IS NOT AFFECTED, NOR IS THE DEPARTMENT PURGED FROM THE                  
001700* MERCHANDISE LEDGER TABLES.                                              
001800*                                                                         
001900* THIS PROGRAM CREATES THE PURCHASE ADJUSTMENT TRANSACTIONS               
002000* THAT WILL 'MOVE' THE ENDING INVENTORY RETAIL FROM THE DEPARTMENT        
002100* BEING ELIMINATED TO ANOTHER DEPARTMENT. (THE PURCHASE                   
002200* ADJUSTMENTS ARE PROCESSED BY THE NORMAL LEDGER UPDATE PROCESS.)         
002300*                                                                         
002400* ALL THE ENDING INVENTORY IN THE 'FROM' DEPARTMENT WILL BE               
002500* MOVED INTO ONE SUBCLASS IN THE 'TO' DEPARTMENT.  IT IS NOT              
002600* IMPORTANT WHICH PARTICULAR SUBCLASS IN THE 'TO' DEPARTMENT              
002700* IS USED - IT JUST HAS TO BE A VALID SUBCLASS UNDER THE 'TO'             
002800* DEPARTMENT.                                                             
002900*                                                                         
003000* THE PROGRAM ALSO CREATES A DETAIL REPORT, A TRANSACTION TOTAL           
003100* REPORT, AND AN EXTRACT FILE FOR A REPORT BY BUYER THAT IS               
003200* CREATED BY MLKRP221.                                                    
003300* MLKRP221.                                                               
003400*                                                                         
003500* THE DEPARTMENTS TO BE ELIMINATED ARE SPECIFIED VIA CONTROL              
003600* CARDS.  EACH CONTROL CARD CONTAINS A DEPARTMENT TO BE                   
003700* ELIMINATED (THE 'FROM' OR 'DEBIT' DEPARTMENT) AND THE                   
003800* DEPARTMENT TO WHICH ITS INVENTORY SHOULD BE MOVED (THE 'TO' OR          
003900* 'CREDIT' DEPARTMENT).                                                   
004000*                                                                         
004100* WHEN A CONTROL CARD IS READ, THE ENDING INVENTORY RETAIL THAT           
004200* IS CURRENTLY IN THE OPEN FISCAL MONTH FOR THE 'FROM' DEPARTMENT         
004300* IS RETRIEVED FROM TMNSCLS.  THIS ENDING INVENTORY, WHICH IS AT          
004400* THE SUBCLASS/STORE LEVEL, DRIVES THE CREATION OF THE PURCHASE           
004500* ADJUSTMENT TRANSACTIONS.  TWO TRANSACTIONS ARE CREATED FOR EACH         
004600* SUBCLASS/STORE RETRIEVED. THE FIRST IS A DEBIT TRANSACTION FOR          
004700* THE 'FROM' DEPARTMENT. THIS TRANSACTION WILL CAUSE THE ENDING           
004800* INVENTORY IN THE 'FROM' DEPARTMENT TO BE REDUCED TO ZERO. THE           
004900* SECOND IS A CREDIT TRANSACTION FOR THE 'TO' DEPARTMENT. THIS            
005000* TRANSACTION WILL CAUSE THE ENDING INVENTORY IN THE 'TO'                 
005100* DEPARTMENT TO BE INCREASED BY THE SAME AMOUNT.                          
005300*                                                                         
005400* EACH TIME A PAIR OF PURCHASE ADJUSTMENT TRANSACTIONS IS                 
005500* CREATED, A LINE IS WRITTEN TO THE DETAIL REPORT.  THE LINE              
005600* CONTAINS THE FROM DEPT, TO DEPT, SUBCLASS, STORE, AND END INV           
005700* RETAIL.  THE DETAIL REPORT ALSO CONTAINS SUBCLASS TOTALS,               
005800* DEPARTMENT TOTALS, GRAND TOTALS, AND A COUNT OF THE TOTAL NUMBER        
005900* OF DEPARTMENTS ELIMINATED.                                              
006000*                                                                         
006100* THE TRANSACTION TOTAL REPORT IS CREATED AT THE END OF THE               
006200* PROGRAM.  IT REPORTS THE TOTAL CREDIT AND TOTAL CHARGE (DEBIT).         
006300*                                                                         
006400* THE BUYER REPORT EXTRACT FILE IS CREATED AT THE SUBCLASS LEVEL.         
006500* EACH TIME A SUBCLASS BREAK IS ENCOUNTERED, TWO RECORDS ARE              
006600* WRITTEN TO THE EXTRACT FILE USING THE SUBCLASS TOTALS THAT HAVE         
006700* BEEN ACCUMULATED FOR THE DETAIL REPORT.  THE FIRST IS FOR THE           
006800* 'FROM' (DEBIT) DEPARTMENT.  THE SECOND IS FOR THE 'TO' (CREDIT)         
006900* DEPARTMENT.                                                             
007000*                                                                         
007100* NOTE:  PURCHASE ADJUSTMENTS GET ADDED TO ENDING INVENTORY, SO           
007200* IN ORDER FOR THE DEBIT PURCHASE ADJUSTMENT TO REDUCE ENDING             
007300* INVENTORY, THE SIGN ON THE AMOUNT ON THE TRANSACTION MUST BE            
007400* REVERSED.  HOWEVER, THE LEDGER UPDATE PROCESS AUTOMATICALLY             
007500* REVERSES THE AMOUNT ON DEBIT PURCHASE ADJUSTMENTS AT THE TIME IT        
007600* PROCESSESS THEM, SO IT IS NOT NECESSARY TO REVERSE THE SIGN WHEN        
007700* THE ADJUSTMENT IS CREATED.  THE DETAIL REPORT DOES NOT REPORT           
007800* SEPARATE DEBIT AND CREDIT TOTALS, SO IT IS NOT NECESSARY TO             
007900* REVERSE THE SIGN ON THE AMOUNTS REPORTED ON THE DETAIL REPORT           
008000* EITHER.  THE ONLY TIME THE SIGN MUST BE REVERSED IS WHEN                
008100* WRITING THE DEBIT TOTALS TO THE TRANSACTION TOTAL REPORT AND THE        
008200* BUYER REPORT EXTRACT FILE.                                              
008300******************************************************************        
      * 04/19/2000   PAUL KEBER                                                 
      * ADDED FSCL_MN_NBR TO THE TMNSCLS-CSR CURSOR IN ORDER TO UTILIZE         
      * THE PRIMARY INDEX.                                                      
W26603* W26603    03-08-2004 R. JANSSEN - MERCH LEDGER STORE EXPANSION          
W34309* W34309    04-25-2008 B.GRAHAM - STRATAGEM                               
W34309* W34309               ADD INITIALIZE FOR MLRD015                         
008300******************************************************************        
008400                                                                          
008700 ENVIRONMENT DIVISION.                                                    
008900 CONFIGURATION SECTION.                                                   
009100 SOURCE-COMPUTER.        IBM-3090.                                        
009200 OBJECT-COMPUTER.        IBM-3090.                                        
009400 INPUT-OUTPUT SECTION.                                                    
009500 FILE-CONTROL.                                                            
009600                                                                          
009700     SELECT CTRL-CARD-FILE                                                
009800         ASSIGN TO INP01.                                                 
009900                                                                          
010000     SELECT PURCH-ADJ-FILE                                                
010100         ASSIGN TO OUT01.                                                 
010200                                                                          
010300     SELECT BYR-RPT-EXTRCT-FILE                                           
010400         ASSIGN TO OUT02.                                                 
010500                                                                          
010600     SELECT DEPT-ELIM-DTL-RPT-FILE                                        
010700         ASSIGN TO RPT01.                                                 
010800                                                                          
010900     SELECT DEPT-ELIM-TRANTOT-RPT-FILE                                    
011000         ASSIGN TO RPT02.                                                 
011100                                                                          
011400 DATA DIVISION.                                                           
011600 FILE SECTION.                                                            
011700                                                                          
011800 FD  CTRL-CARD-FILE                                                       
011900     LABEL RECORDS ARE STANDARD                                           
012000     RECORDING MODE IS F                                                  
012100     BLOCK CONTAINS 0 RECORDS.                                            
012200 01  CONTROL-RECORD              PIC X(80).                               
012300                                                                          
012400 FD  PURCH-ADJ-FILE                                                       
012500     RECORDING MODE IS F                                                  
012600     LABEL RECORDS ARE STANDARD                                           
012700     BLOCK CONTAINS 0 RECORDS.                                            
012800                                                                          
W26603 01  PURCH-ADJ-REC               PIC X(150).                              
013000                                                                          
013100 FD  BYR-RPT-EXTRCT-FILE                                                  
013200     RECORDING MODE IS F                                                  
013300     LABEL RECORDS ARE STANDARD                                           
013400     BLOCK CONTAINS 0 RECORDS.                                            
013500                                                                          
013600 01  BYR-RPT-EXTRCT-REC          PIC X(80).                               
013700                                                                          
013800 FD  DEPT-ELIM-DTL-RPT-FILE                                               
013900     RECORDING MODE IS F                                                  
014000     LABEL RECORDS ARE STANDARD                                           
014100     BLOCK CONTAINS 0 RECORDS.                                            
014200                                                                          
014300 01  DEPT-ELIM-DTL-RPT-REC       PIC X(132).                              
014400                                                                          
014500 FD  DEPT-ELIM-TRANTOT-RPT-FILE                                           
014600     RECORDING MODE IS F                                                  
014700     LABEL RECORDS ARE STANDARD                                           
014800     BLOCK CONTAINS 0 RECORDS.                                            
014900                                                                          
015000 01  DEPT-ELIM-TRANTOT-RPT-REC   PIC X(132).                              
015100                                                                          
015200******************************************************************        
015300 WORKING-STORAGE SECTION.                                                 
015400******************************************************************        
015500                                                                          
015600*    INPUT CONTROL CARDS                                                  
015700 01  CC-CTRL-CARD.                                                        
015800     05  CC-FROM-DEPT-NBR        PIC X(03).                               
015900     05  FILLER                  PIC X.                                   
016000     05  CC-TO-DEPT-NBR          PIC X(03).                               
016100     05  FILLER                  PIC X(73).                               
016200                                                                          
016300*   OUTPUT PURCHASE ADJUSTMENT TRANSACTIONS                               
016400     COPY MLRD015.                                                        
016500                                                                          
016600*   EXTRCT RECORDS FOR DEPARTMENT ELIMINATION REPORT BY BUYER             
016700     COPY MLRD119.                                                        
016800                                                                          
017000 01  PC-PROGRAM-CONSTANTS.                                                
017100     05  PC-PGM-NAME             PIC X(08)     VALUE 'MLKUT426'.          
017200     05  PC-LINE-CNT-MAX         PIC S9(03)    VALUE +64.                 
017300     05  PC-OPERCO-NBR           PIC X(02)     VALUE '03'.                
017400                                                                          
017500     05  PC-LV-CDE-DPT           PIC X(03)     VALUE 'DPT'.               
017600     05  PC-LV-CDE-SCL           PIC X(03)     VALUE 'SCL'.               
017700                                                                          
017800     05  PC-AP-TRANS-CDE-CR      PIC X(02)     VALUE '52'.                
017900     05  PC-AP-TRANS-CDE-DR      PIC X(02)     VALUE '53'.                
018000                                                                          
018100     05  PC-TRANS-CDE-DR         PIC X(02)     VALUE '02'.                
018200     05  PC-TRANS-CDE-CR         PIC X(02)     VALUE '01'.                
018300                                                                          
018400     05  PC-UNKNOWN-VEND-NBR     PIC X(05)     VALUE '05161'.             
018500                                                                          
018600     05  PC-POS-AMT-LIMIT        PIC S9(05)V99 VALUE +99999.99.           
018700     05  PC-NEG-AMT-LIMIT        PIC S9(05)V99 VALUE -99999.99.           
018800                                                                          
018900     05  PC-SCL-TOT-LABEL        PIC X(24)     VALUE                      
019000                                       '*** SUBCLASS TOTAL ***'.          
019100     05  PC-DEPT-TOT-LABEL       PIC X(24)     VALUE                      
019200                                       '*** DEPARTMENT TOTAL ***'.        
019300     05  PC-GRND-TOT-LABEL       PIC X(24)     VALUE                      
019400                                       '*** GRAND TOTAL ***'.             
019500     05  PC-CREDIT-TOT-LABEL     PIC X(24)     VALUE                      
019600                                       '*** TOTAL CREDIT ***'.            
019700     05  PC-CHARGE-TOT-LABEL     PIC X(24)     VALUE                      
019800                                       '*** TOTAL CHARGE ***'.            
019900                                                                          
020000                                                                          
020100 01  PV-PROGRAM-VARIABLES.                                                
020200                                                                          
020300     05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
020400     05  PV-PARAGRAPH-NAME            PIC X(30).                          
020500     05  PV-ABEND-CODE                PIC S9(04) COMP SYNC                
020600                                                 VALUE +0.                
020700     05  PV-DISPLAY-CNT               PIC ZZZ,ZZZ,ZZ9.                    
020800     05  PV-DB2-DEPT-NBR              PIC X(03).                          
020900     05  PV-DB2-SUB-CL-NBR            PIC X(03)  VALUE '000'.             
021000     05  PV-FROM-DEPT-BYR-NBR         PIC X(03).                          
021100     05  PV-FROM-DEPT-DESC            PIC X(25).                          
021200     05  PV-TO-DEPT-BYR-NBR           PIC X(03).                          
021300     05  PV-TO-DEPT-DESC              PIC X(25).                          
021400     05  PV-TO-SUB-CL-NBR             PIC X(02).                          
021500     05  PV-TO-SUB-CL-DESC            PIC X(25).                          
021600     05  PV-PREV-DEPT-NBR             PIC X(03)     VALUE SPACE.          
021610     05  PV-PREV-SUB-CL-NBR           PIC X(02).                          
021700     05  PV-FROM-SUB-CL-DESC          PIC X(25).                          
W26603     05  PV-STR-NBR                   PIC 9(05).                          
021900                                                                          
022000     05  PV-DR-BATCH-NBR.                                                 
022100         10 FILLER                    PIC X         VALUE 'Q'.            
022200         10  PV-DR-BATCH-NBR-DD       PIC X(02).                          
022300     05  PV-CR-BATCH-NBR.                                                 
022400         10  FILLER                   PIC X         VALUE 'R'.            
022500         10  PV-CR-BATCH-NBR-DD       PIC X(02).                          
022600                                                                          
022700     05  PV-TIME-HHMMSS.                                                  
022800         10  PV-TIME-HHMMSS-HH        PIC X(02).                          
022900         10  PV-TIME-HHMMSS-MM        PIC X(02).                          
023000         10  PV-TIME-HHMMSS-SS        PIC X(02).                          
023100                                                                          
023300     05  PV-REMAINING-END-INV-RTL     PIC S9(09)V99 COMP-3.               
023400                                                                          
023500*    **  COUNTS.                                                          
023600     05  PV-LINE-CNT                  PIC S9(03) COMP-3 VALUE +0.         
023700     05  PV-PAGE-CNT                  PIC S9(07) COMP-3 VALUE +0.         
023800     05  PV-NBR-DEPTS-ELIM            PIC S9(03) COMP-3 VALUE +0.         
023900     05  PV-DR-PURCH-ADJ-RECS-WRITTEN PIC S9(09) COMP-3 VALUE +0.         
024000     05  PV-CR-PURCH-ADJ-RECS-WRITTEN PIC S9(09) COMP-3 VALUE +0.         
024100     05  PV-BYR-RPT-EXTRCT-RECS-WRITTEN                                   
024200                                      PIC S9(09) COMP-3 VALUE +0.         
024300                                                                          
024400                                                                          
024500*    **  DATES.                                                           
024600     05  PV-CURR-DTE-YYMMDD.                                              
024700         10  PV-CURR-DTE-YYMMDD-YY    PIC  X(02).                         
024800         10  PV-CURR-DTE-YYMMDD-MM    PIC  X(02).                         
024900         10  PV-CURR-DTE-YYMMDD-DD    PIC  X(02).                         
025000                                                                          
025100*    **  TOTALS.                                                          
025200     05  PV-END-INV-RTL-SCL-TOT   PIC S9(09)V99 COMP-3 VALUE +0.          
025400     05  PV-END-INV-RTL-DEPT-TOT  PIC S9(09)V99 COMP-3 VALUE +0.          
025600     05  PV-END-INV-RTL-GRND-TOT  PIC S9(11)V99 COMP-3 VALUE +0.          
025800     05  PV-END-INV-RTL-GRND-TOT-RVRSD                                    
025900                                  PIC S9(11)V99 COMP-3 VALUE +0.          
026200                                                                          
026300                                                                          
026400 01  PS-PROGRAM-SWITCHES.                                                 
026500     05  PS-END-OF-CTRL-CARD-FILE-SW   PIC X VALUE 'N'.                   
026600         88  PS-END-OF-CTRL-CARD-FILE        VALUE 'Y'.                   
026700     05  PS-TMDSEAR-FOUND-SW           PIC X VALUE 'N'.                   
026800         88  PS-TMDSEAR-FOUND                VALUE 'Y'.                   
026900         88  PS-TMDSEAR-NOT-FOUND            VALUE 'N'.                   
027000     05  PS-END-OF-TMNSCLS-CSR-SW      PIC X VALUE 'N'.                   
027100         88  PS-END-OF-TMNSCLS-CSR           VALUE 'Y'.                   
027200         88  PS-NOT-END-OF-TMNSCLS-CSR       VALUE 'N'.                   
027600     05  PS-END-OF-RTL-AMTS-SW         PIC X VALUE 'N'.                   
027700         88  PS-END-OF-RTL-AMTS              VALUE 'Y'.                   
027800         88  PS-NOT-END-OF-RTL-AMTS          VALUE 'N'.                   
027900                                                                          
028000******************************************************************        
028100*  STANDARD KOHL'S HEADING LINE FOR A 132 COLUMN REPORT                   
028200******************************************************************        
028300     COPY DPWS132O.                                                       
028400                                                                          
028500 01  R1H1-RPT1-HEADING1.                                                  
028600     05  FILLER                  PIC X(48)   VALUE SPACE.                 
028700     05  FILLER                  PIC X(45)   VALUE                        
028800         'DEPARTMENT ELIMINATION STORE DETAIL'.                           
028900                                                                          
029000*    THIS HEADING IS USED BY BOTH THE -1 AND -2 REPORTS.                  
029100 01  RH2-RPT-HEADING2.                                                    
029200     05  FILLER                  PIC X(36)   VALUE SPACE.                 
029300     05  FILLER                  PIC X(23)   VALUE                        
029400         'FOR FISCAL MONTH ENDING'.                                       
029500     05  FILLER                  PIC X(01)   VALUE SPACE.                 
029600     05  RH2-FROM-DATE.                                                   
029700         10  RH2-FROM-MM         PIC X(02)   VALUE ZEROS.                 
029800         10  FILLER              PIC X(01)   VALUE '/'.                   
029900         10  RH2-FROM-DD         PIC X(02)   VALUE ZEROS.                 
030000         10  FILLER              PIC X(01)   VALUE '/'.                   
030100         10  RH2-FROM-CCYY       PIC X(04)   VALUE ZEROS.                 
030200     05  FILLER                  PIC X(17)                                
030300                                 VALUE ' - BATCH NUMBERS '.               
030400     05  RH2-DR-BATCH-NBR        PIC X(3).                                
030500     05  FILLER                  PIC X(3)    VALUE ' - '.                 
030600     05  RH2-CR-BATCH-NBR        PIC X(3).                                
030700                                                                          
030800 01  R1H3-RPT1-HEADING3.                                                  
030900     05  FILLER                  PIC X(02)   VALUE SPACE.                 
031000     05  FILLER                  PIC X(04)   VALUE                        
031100         'FROM'.                                                          
031200     05  FILLER                  PIC X(13)   VALUE SPACE.                 
031300     05  FILLER                  PIC X(04)   VALUE                        
031400         'FROM'.                                                          
031500     05  FILLER                  PIC X(17)   VALUE SPACE.                 
031600     05  FILLER                  PIC X(02)   VALUE                        
031700         'TO'.                                                            
031800     05  FILLER                  PIC X(14)   VALUE SPACE.                 
031900     05  FILLER                  PIC X(02)   VALUE                        
032000         'TO'.                                                            
032100                                                                          
032200 01  R1H4-RPT1-HEADING4.                                                  
032300     05  FILLER                  PIC X(08)   VALUE                        
032400         'DEPT-SCL'.                                                      
032500     05  FILLER                  PIC X(03)   VALUE SPACE.                 
032600     05  FILLER                  PIC X(20)   VALUE                        
032700         'SUBCLASS DESCRIPTION'.                                          
032800     05  FILLER                  PIC X(06)   VALUE SPACE.                 
032900     05  FILLER                  PIC X(08)   VALUE                        
033000         'DEPT-SCL'.                                                      
033100     05  FILLER                  PIC X(03)   VALUE SPACE.                 
033200     05  FILLER                  PIC X(20)   VALUE                        
033300         'SUBCLASS DESCRIPTION'.                                          
033400     05  FILLER                  PIC X(04)   VALUE SPACE.                 
033500     05  FILLER                  PIC X(09)   VALUE                        
033600         'STORE NBR'.                                                     
033700     05  FILLER                  PIC X(11)   VALUE SPACE.                 
033800     05  FILLER                  PIC X(06)   VALUE                        
033900         'RETAIL'.                                                        
034300                                                                          
034400 01  R1DTL-RPT1-DETAIL-LINE.                                              
034500     05  FILLER                  PIC X       VALUE SPACE.                 
034600     05  R1DTL-FROM-DEPT-NBR     PIC X(03)   VALUE SPACE.                 
034700     05  FILLER                  PIC X       VALUE '-'.                   
034800     05  R1DTL-FROM-SUB-CL-NBR   PIC X(02)   VALUE SPACE.                 
034900     05  FILLER                  PIC X(03)   VALUE SPACE.                 
035000     05  R1DTL-FROM-SUB-CL-DESC  PIC X(25)   VALUE SPACE.                 
035100     05  FILLER                  PIC X(03)   VALUE SPACE.                 
035200     05  R1DTL-TO-DEPT-NBR       PIC X(03)   VALUE SPACE.                 
035300     05  FILLER                  PIC X       VALUE '-'.                   
035400     05  R1DTL-TO-SUB-CL-NBR     PIC X(02)   VALUE SPACE.                 
035500     05  FILLER                  PIC X(03)   VALUE SPACE.                 
035600     05  R1DTL-TO-SUB-CL-DESC    PIC X(25)   VALUE SPACE.                 
035700     05  FILLER                  PIC X(03)   VALUE SPACE.                 
W26603     05  R1DTL-STR-NBR           PIC 9(05).                               
W26603     05  FILLER                  PIC X(01)   VALUE SPACE.                 
W26603     05  R1DTL-RTL-AMT           PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.                 
036300                                                                          
036400 01  R1TL-RPT1-TOTAL-LINE.                                                
036500     05  R1TL-TOTAL-LABEL        PIC X(24).                               
036600     05  FILLER                  PIC X(57)   VALUE SPACES.                
W26603     05  R1TL-RTL-AMT            PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.                 
037000                                                                          
037100 01  R1ND-NUM-DEPTS-LINE.                                                 
037200         05  FILLER              PIC X(39) VALUE                          
037300            'TOTAL NUMBER OF DEPARTMENTS ELIMINATED:'.                    
037400         05 FILLER               PIC X(03) VALUE SPACE.                   
037500         05 R1ND-NBR-DEPTS-ELIM  PIC ZZ9.                                 
037600                                                                          
037700 01  R1-END-OF-RPT-LINE          PIC X(21) VALUE                          
037800         '*** END OF REPORT ***'.                                         
037900                                                                          
038000 01  R2H1-RPT2-HEADING1.                                                  
038100     05  FILLER                  PIC X(44)   VALUE SPACE.                 
038200     05  FILLER                  PIC X(41)   VALUE                        
038300         'DEPARTMENT ELIMINATION TRANSACTION TOTALS'.                     
038400                                                                          
038500 01  R2H3-RPT2-HEADING3.                                                  
038600     05  FILLER                  PIC X(43)   VALUE SPACE.                 
038700     05  FILLER                  PIC X(06)   VALUE 'RETAIL'.              
039000                                                                          
039100 01  R2TL-RPT2-TOTAL-LINE.                                                
039200     05  R2TL-TOTAL-LABEL        PIC X(24).                               
039300     05  FILLER                  PIC X(08)   VALUE SPACES.                
039400     05  R2TL-RTL-AMT            PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.                 
039700                                                                          
039800                                                                          
039900******************************************************************        
040000*  DB2 COMMUNICATION AREA                                                 
040100******************************************************************        
040200     EXEC SQL                                                             
040300          INCLUDE SQLCA                                                   
040400     END-EXEC.                                                            
040500                                                                          
040600******************************************************************        
040700*  DB2 ERROR MESSAGES FORMAT AREA.                                        
040800******************************************************************        
040900     COPY DPWS004.                                                        
041000                                                                          
041100******************************************************************        
041200*  DB2 MERCHANDISE LEDGER CONTROL TABLE                                   
041300******************************************************************        
041400     EXEC SQL                                                             
041500          INCLUDE TMLCNTL                                                 
041600     END-EXEC.                                                            
041700                                                                          
041800******************************************************************        
041900*  DB2 TMDSEAR TABLE LAYOUT                                               
042000******************************************************************        
042100     EXEC SQL                                                             
042200          INCLUDE TMDSEAR                                                 
042300     END-EXEC.                                                            
042400                                                                          
042500******************************************************************        
042600*  DB2 TSK TABLE LAYOUT                                                   
042700******************************************************************        
042800     EXEC SQL                                                             
042900          INCLUDE TMNSCLS                                                 
043000     END-EXEC.                                                            
043100                                                                          
043200******************************************************************        
043300*  MONTHLY SUBCLASS/STORE TABLE CURSOR.                                   
043400*                                                                         
043500* THIS CURSOR RETRIEVES ALL EXISTING ENDING INVENTORY AT COST             
043600* AND RETAIL FOR THE DEPARTMENT TO BE ELIMINATED.  THIS IS THE            
043700* ENDING INVENTORY THAT MUST BE MOVED TO ANOTHER DEPARTMENT.              
043800******************************************************************        
043900     EXEC SQL                                                             
044000         DECLARE  TMNSCLS-CSR CURSOR FOR                                  
044100          SELECT  DEPT_NBR                                                
044200                 ,SUB_CL_NBR                                              
044300                 ,STR_NBR                                                 
044400                 ,END_INV_RTL_AMT                                         
044600          FROM  TMNSCLS                                                   
044700          WHERE FSCL_MN_NBR     = :MLCNTL-OPN-FSCL-MN-NBR                 
044700            AND FSCL_MN_END_DTE = :MLCNTL-OPN-FSCL-MN-DTE                 
044800            AND  DEPT_NBR       = :CC-FROM-DEPT-NBR                       
044900            AND  END_INV_RTL_AMT <> 0                                     
045100        ORDER BY  DEPT_NBR                                                
045200                 ,SUB_CL_NBR                                              
045300                 ,STR_NBR                                                 
045400     END-EXEC.                                                            
045500                                                                          
045600                                                                          
045700                                                                          
045800 PROCEDURE DIVISION.                                                      
045900                                                                          
046000 0000-MAIN.                                                               
046100                                                                          
046200     OPEN INPUT  CTRL-CARD-FILE                                           
046300          OUTPUT PURCH-ADJ-FILE                                           
046400                 DEPT-ELIM-DTL-RPT-FILE                                   
046500                 DEPT-ELIM-TRANTOT-RPT-FILE                               
046600                 BYR-RPT-EXTRCT-FILE.                                     
046700                                                                          
046800     PERFORM 1000-INITIALIZE.                                             
046900                                                                          
047000     PERFORM 6000-READ-CTRL-CARD-FILE.                                    
047100                                                                          
047200     IF PS-END-OF-CTRL-CARD-FILE                                          
047300         MOVE +4003 TO PV-ABEND-CODE                                      
047400         DISPLAY '** ABEND **'                                            
047500         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
047600         DISPLAY '** PARAGRAPH =  0000-MAIN'                              
047700         DISPLAY '** CONTROL CARD FILE IS EMPTY.'                         
047800         PERFORM 9999-ABEND                                               
047900     END-IF.                                                              
048000                                                                          
048100     PERFORM 2000-PROCESS-CTRL-CARDS                                      
048200       UNTIL PS-END-OF-CTRL-CARD-FILE.                                    
048300                                                                          
048400     PERFORM  7175-WRITE-FINAL-RPT1-LINES.                                
048500                                                                          
048600     PERFORM 7200-BUILD-AND-WRITE-RPT2.                                   
048700                                                                          
048800     PERFORM 5000-DISPLAY-PROCESS-SUMMARY.                                
048900                                                                          
049000     CLOSE CTRL-CARD-FILE                                                 
049100           PURCH-ADJ-FILE                                                 
049200           DEPT-ELIM-DTL-RPT-FILE                                         
049300           DEPT-ELIM-TRANTOT-RPT-FILE                                     
049400           BYR-RPT-EXTRCT-FILE.                                           
049500                                                                          
049600     STOP RUN.                                                            
049700                                                                          
049900 1000-INITIALIZE.                                                         
050000                                                                          
050100     PERFORM 8000-GET-OPN-FSCL-DTES.                                      
050200                                                                          
050200     INITIALIZE ML015-PURCHASE-RECORD.                                    
050200                                                                          
050300*    FILL IN THE FIELDS ON THE PURCHASE ADJUSTMENT                        
050400*    RECORD THAT WILL THE SAME FOR ALL RECORDS.                           
050500     MOVE '1'                    TO ML015-PURCH-CDE.                      
W26603     MOVE PC-UNKNOWN-VEND-NBR TO ML015-PURCH-VENDOR-INHSE-NMBR.           
W26603     MOVE 5161                TO ML015-PURCH-ENT-ID.                      
W26603     MOVE MLCNTL-OPN-FSCL-WK-DTE (6:2)                                    
W26603                                 TO ML015-PURCH-INV-DTE-MM.       MLRD015 
W26603     MOVE MLCNTL-OPN-FSCL-WK-DTE (9:2)                                    
W26603                                 TO ML015-PURCH-INV-DTE-DD.       MLRD015 
W26603     MOVE MLCNTL-OPN-FSCL-WK-DTE (3:2)                                    
W26603                                 TO ML015-PURCH-INV-DTE-YY.       MLRD015 
W26603     MOVE ALL '0'                TO ML015-PURCH-PO-NBR.                   
051400     MOVE MLCNTL-OPN-FSCL-WK-NBR TO ML015-PURCH-KOHLS-MM.                 
W26603     MOVE MLCNTL-OPN-FSCL-WK-DTE (6:2)                                    
W26603                                 TO ML015-PURCH-DOC-DTE-MM.       MLRD015 
W26603     MOVE MLCNTL-OPN-FSCL-WK-DTE (9:2)                                    
W26603                                 TO ML015-PURCH-DOC-DTE-DD.       MLRD015 
W26603     MOVE ALL '0'                TO ML015-PURCH-DOC-5-NBR.                
052000     MOVE ZERO                   TO ML015-PURCH-DISCT-AMT.                
052100     MOVE ZERO                   TO ML015-PURCH-FRGHT-AMT.                
052200                                                                          
052300*    BUILD THE BATCH NUMBERS.                                             
052400     ACCEPT PV-CURR-DTE-YYMMDD FROM DATE.                                 
052500     MOVE PV-CURR-DTE-YYMMDD-DD  TO PV-DR-BATCH-NBR-DD                    
052600                                    PV-CR-BATCH-NBR-DD.                   
052700                                                                          
052800*    BUILD AND WRITE THE COMMON REPORT HEADINGS.                          
052900     PERFORM 1100-BUILD-COMMON-RPT-HEADINGS.                              
053000     PERFORM 7050-WRITE-RPT1-HEADINGS.                                    
053100                                                                          
053200*    BUILD AND WRITE THE HEADER RECORD FOR THE BUYER REPORT               
053300*    EXTRACT FILE.                                                        
053400     MOVE SPACE TO ML119-DEPT-ELIM-BY-BYR-EXTRACT.                        
053500     SET ML119-REC-TYP-HDR TO TRUE.                                       
053600     MOVE MLCNTL-OPN-FSCL-MN-DTE TO ML119-FSCL-MN-END-DTE.                
053700     MOVE PV-DR-BATCH-NBR        TO ML119-DR-BATCH-NBR.                   
053800     MOVE PV-CR-BATCH-NBR        TO ML119-CR-BATCH-NBR.                   
053900     WRITE BYR-RPT-EXTRCT-REC                                             
054000      FROM ML119-DEPT-ELIM-BY-BYR-EXTRACT.                                
054100     ADD +1 TO PV-BYR-RPT-EXTRCT-RECS-WRITTEN.                            
054200     MOVE SPACE TO ML119-DEPT-ELIM-BY-BYR-EXTRACT.                        
054300     SET ML119-REC-TYP-DTL TO TRUE.                                       
054400                                                                          
054500                                                                          
054600 1100-BUILD-COMMON-RPT-HEADINGS.                                          
054700                                                                          
054800     MOVE PV-CURR-DTE-YYMMDD-YY        TO DP132O-RUN-YEAR.                
054900     MOVE PV-CURR-DTE-YYMMDD-MM        TO DP132O-RUN-MONTH.               
055000     MOVE PV-CURR-DTE-YYMMDD-DD        TO DP132O-RUN-DAY.                 
055100                                                                          
055200     ACCEPT PV-TIME-HHMMSS FROM TIME.                                     
055300     MOVE PV-TIME-HHMMSS-HH            TO DP132O-RUN-HOUR.                
055400     MOVE PV-TIME-HHMMSS-MM            TO DP132O-RUN-MINUTE.              
055500     MOVE PC-PGM-NAME                  TO DP132O-PROGRAM-NAME.            
055600     MOVE 1                            TO DP132O-REPORT-NUMBER.           
055700                                                                          
055800     MOVE MLCNTL-OPN-FSCL-MN-DTE (6:2) TO RH2-FROM-MM.                    
055900     MOVE MLCNTL-OPN-FSCL-MN-DTE (9:2) TO RH2-FROM-DD.                    
056000     MOVE MLCNTL-OPN-FSCL-MN-DTE (1:4) TO RH2-FROM-CCYY.                  
056100                                                                          
056200     MOVE PV-DR-BATCH-NBR              TO RH2-DR-BATCH-NBR.               
056300     MOVE PV-CR-BATCH-NBR              TO RH2-CR-BATCH-NBR.               
056400                                                                          
056500                                                                          
056600******************************************************************        
056700* FOR EACH CONTROL CARD: EDIT THE CONTROL CARD, RETRIEVE THE     *        
056800* ENDING INVENTORY TO BE MOVED,  BUILD A PAIR OF DEBIT/CREDIT    *        
056900* PURCHASE ADJUSTMENTS FOR EACH SUBCLASS/STORE FOR WHICH ENDING  *        
057000* INVENTORY WAS RETRIEVED, AND WRITE A LINE TO THE DETAIL REPORT *        
057100* FOR EACH SUBCLASS/STORE.                                       *        
057200******************************************************************        
057300 2000-PROCESS-CTRL-CARDS.                                                 
057400                                                                          
057500     DISPLAY 'PROCESSING CONTROL CARD: ' CC-CTRL-CARD.                    
057600     PERFORM 2100-EDIT-CTRL-CARD.                                         
057700                                                                          
057800     SET PS-NOT-END-OF-TMNSCLS-CSR TO TRUE.                               
057900     PERFORM 8100-OPEN-TMNSCLS-CSR.                                       
058000     PERFORM 8150-FETCH-TMNSCLS-CSR.                                      
058100                                                                          
058200     IF PS-NOT-END-OF-TMNSCLS-CSR                                         
058300         PERFORM 3300-PREP-FOR-NEW-SCL                                    
058400         PERFORM 3600-PREP-FOR-NEW-DEPT                                   
058500         PERFORM 3000-PROCESS-TMNSCLS-CSR                                 
058600           UNTIL PS-END-OF-TMNSCLS-CSR                                    
058700         PERFORM 3200-FINISH-PREV-SCL                                     
058800         PERFORM 3500-FINISH-PREV-DEPT                                    
058900         ADD +1 TO PV-NBR-DEPTS-ELIM                                      
059000     ELSE                                                                 
059100         DISPLAY '  ** NO ENDING INVENTORY FOUND ON LEDGER FOR '          
059200         DISPLAY '  ** DEPARTMENT ' CC-FROM-DEPT-NBR                      
059300     END-IF.                                                              
059400                                                                          
059500     PERFORM 8175-CLOSE-TMNSCLS-CSR.                                      
059600                                                                          
059700     PERFORM 6000-READ-CTRL-CARD-FILE.                                    
059800                                                                          
059900                                                                          
060000 2100-EDIT-CTRL-CARD.                                                     
060100                                                                          
060210*    VALIDATE THE 'FROM' DEPT.                                            
060300     MOVE CC-FROM-DEPT-NBR TO PV-DB2-DEPT-NBR.                            
060400     PERFORM 8200-GET-TMDSEAR-FOR-DPT.                                    
060500     IF PS-TMDSEAR-NOT-FOUND                                              
060600         MOVE +4003 TO PV-ABEND-CODE                                      
060700         DISPLAY '** ABEND **'                                            
060800         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
060900         DISPLAY '** PARAGRAPH =  2100-EDIT-CTRL-CARD'                    
061000         DISPLAY '** INVALID CC-FROM-DEPT-NBR ON CONTROL CARD.'           
061100         DISPLAY '** CC-FROM-DEPT-NBR = ' CC-FROM-DEPT-NBR                
061200         PERFORM 9999-ABEND                                               
061300     END-IF.                                                              
061400                                                                          
061500     MOVE MDSEAR-BYR-NBR TO PV-FROM-DEPT-BYR-NBR.                         
061600     MOVE MDSEAR-DESC    TO PV-FROM-DEPT-DESC.                            
061700                                                                          
061710*    MAKE SURE THE 'FROM' DEPT IS NOT A DUPLICATE.                        
061720     IF CC-FROM-DEPT-NBR = PV-PREV-DEPT-NBR                               
061721         MOVE +4003 TO PV-ABEND-CODE                                      
061722         DISPLAY '** ABEND **'                                            
061723         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
061724         DISPLAY '** PARAGRAPH =  2100-EDIT-CTRL-CARD'                    
061725         DISPLAY '** DUPLICATE CC-FROM-DEPT-NBR ON CONTROL CARD.'         
061726         DISPLAY '** CC-FROM-DEPT-NBR = ' CC-FROM-DEPT-NBR                
061727         PERFORM 9999-ABEND                                               
061728     END-IF.                                                              
061729     MOVE CC-FROM-DEPT-NBR TO PV-PREV-DEPT-NBR.                           
061730                                                                          
061800*    VALIDATE THE 'TO' DEPT.                                              
061900     MOVE CC-TO-DEPT-NBR TO PV-DB2-DEPT-NBR.                              
062000     PERFORM 8200-GET-TMDSEAR-FOR-DPT.                                    
062100     IF PS-TMDSEAR-NOT-FOUND                                              
062200         MOVE +4003 TO PV-ABEND-CODE                                      
062300         DISPLAY '** ABEND **'                                            
062400         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
062500         DISPLAY '** PARAGRAPH =  2100-EDIT-CTRL-CARD'                    
062600         DISPLAY '** INVALID CC-TO-DEPT-NBR ON CONTROL CARD.'             
062700         DISPLAY '** CC-TO-DEPT-NBR = ' CC-TO-DEPT-NBR                    
062800         PERFORM 9999-ABEND                                               
062900     END-IF.                                                              
063000                                                                          
063100     MOVE MDSEAR-BYR-NBR TO PV-TO-DEPT-BYR-NBR.                           
063200     MOVE MDSEAR-DESC    TO PV-TO-DEPT-DESC.                              
063300                                                                          
063400*    MAKE SURE THE 'FROM' AND 'TO' DEPTS ARE NOT THE SAME.                
063500     IF CC-FROM-DEPT-NBR = CC-TO-DEPT-NBR                                 
063600         MOVE +4003 TO PV-ABEND-CODE                                      
063700         DISPLAY '** ABEND **'                                            
063800         DISPLAY '** PROGRAM   = ' PC-PGM-NAME                            
063900         DISPLAY '** PARAGRAPH =  2100-EDIT-CTRL-CARD'                    
064000         DISPLAY '** FROM AND TO DEPT-NBRS ON CONTROL CARD '              
064100         DISPLAY '** ARE THE SAME.'                                       
064200         PERFORM 9999-ABEND                                               
064300     END-IF.                                                              
064400                                                                          
064500                                                                          
064600 3000-PROCESS-TMNSCLS-CSR.                                                
064700                                                                          
064800*    CHECK FOR CONTROL BREAKS.                                            
064900     IF MNSCLS-SUB-CL-NBR NOT = PV-PREV-SUB-CL-NBR                        
065000         PERFORM 3100-SUB-CL-BREAK                                        
065100     END-IF.                                                              
065200                                                                          
065300*    PERFORM THE DETAIL PROCESSING.                                       
065400     PERFORM 4000-CREATE-PURCH-ADJS.                                      
065500     PERFORM 7100-WRITE-RPT1-DETAIL-LINE.                                 
065600     ADD MNSCLS-END-INV-RTL-AMT  TO PV-END-INV-RTL-SCL-TOT.               
065800                                                                          
065900     PERFORM 8150-FETCH-TMNSCLS-CSR.                                      
066000                                                                          
066100                                                                          
066200 3100-SUB-CL-BREAK.                                                       
066300                                                                          
066400     PERFORM 3200-FINISH-PREV-SCL.                                        
066500     PERFORM 3300-PREP-FOR-NEW-SCL.                                       
066600                                                                          
066700                                                                          
066800 3200-FINISH-PREV-SCL.                                                    
066900                                                                          
067000     PERFORM 7125-WRITE-RPT1-SCL-TOT-LINE.                                
067100     ADD PV-END-INV-RTL-SCL-TOT  TO PV-END-INV-RTL-DEPT-TOT.              
067300     PERFORM 4400-CREATE-BYR-RPT-EXTRCT.                                  
067400                                                                          
067500                                                                          
067600 3300-PREP-FOR-NEW-SCL.                                                   
067700                                                                          
067800     MOVE CC-FROM-DEPT-NBR  TO PV-DB2-DEPT-NBR.                           
067900     MOVE MNSCLS-SUB-CL-NBR TO PV-DB2-SUB-CL-NBR(2:2).                    
068000     PERFORM 8300-GET-SUB-CL-DESC.                                        
068100     MOVE MDSEAR-DESC       TO PV-FROM-SUB-CL-DESC.                       
068200                                                                          
068300     MOVE ZERO TO PV-END-INV-RTL-SCL-TOT.                                 
068500                                                                          
068600     MOVE MNSCLS-SUB-CL-NBR TO PV-PREV-SUB-CL-NBR.                        
068700                                                                          
068800                                                                          
068900 3500-FINISH-PREV-DEPT.                                                   
069000                                                                          
069100     PERFORM 7150-WRITE-RPT1-DEPT-TOT-LINE.                               
069200     ADD PV-END-INV-RTL-DEPT-TOT  TO PV-END-INV-RTL-GRND-TOT.             
069400                                                                          
069500                                                                          
069600 3600-PREP-FOR-NEW-DEPT.                                                  
069700                                                                          
069800     MOVE ZERO TO PV-END-INV-RTL-DEPT-TOT.                                
070000     PERFORM 8400-GET-SCL-FOR-TO-DEPT.                                    
070100     MOVE MDSEAR-SUB-CL-NBR(2:2) TO PV-TO-SUB-CL-NBR.                     
070200     MOVE MDSEAR-DESC            TO PV-TO-SUB-CL-DESC.                    
070300                                                                          
070400                                                                          
070500******************************************************************        
070600* THIS PARAGRAPH CREATES THE PURCHASE ADJUSTMENT TRANSACTIONS.            
070700* IT CREATES A DEBIT TRANSACTION FIRST, THEN IT CREATES A                 
070800* CORRESPONDING CREDIT TRANSACTION.                                       
070900*                                                                         
071000* NOTE THAT THE AMOUNT ON THE DEBIT TRANSACTION DOES NOT HAVE TO          
071100* HAVE ITS SIGN REVERSED BECAUSE THE LEDGER UPDATE PROCESS WILL           
071200* AUTOMATICALLY REVERSE THE SIGN WHEN IT PROCESSES THEM.                  
071300*                                                                         
071400* THE FIELDS THAT ARE THE SAME ON EVERY TRANSACTION WERE FILLED           
071500* IN BY THE INITIALIZATION PROCESSING AT THE BEGINNING OF THE             
071600* PROGRAM.                                                                
071700******************************************************************        
071800 4000-CREATE-PURCH-ADJS.                                                  
071900                                                                          
072000     MOVE CC-FROM-DEPT-NBR       TO ML015-PURCH-DEPT-NBR.                 
072100     MOVE MNSCLS-SUB-CL-NBR      TO ML015-PURCH-CLASS-NBR.                
072200     MOVE PV-DR-BATCH-NBR        TO ML015-PURCH-BATCH-NBR.                
072300     MOVE PC-AP-TRANS-CDE-DR TO ML015-PURCH-AP-TRANS-CDE.                 
072400     MOVE MNSCLS-STR-NBR         TO PV-STR-NBR.                           
072500     MOVE PV-STR-NBR             TO ML015-PURCH-STORE-NBR.                
072600     MOVE PC-TRANS-CDE-DR        TO ML015-PURCH-TRANS-CDE.                
073800     MOVE ZERO                   TO ML015-PURCH-MERCH-AMT                 
074000                                    ML015-PURCH-NET.                      
072700                                                                          
072800     IF MNSCLS-END-INV-RTL-AMT  > PC-POS-AMT-LIMIT                        
072900     OR MNSCLS-END-INV-RTL-AMT  < PC-NEG-AMT-LIMIT                        
073200*        THE PURCH ADJ RECORD LAYOUT HAS A LIMITATION ON THE SIZE         
073300*        OF THE AMOUNTS IT CAN HOLD.  IF THE ENDING INVENTORY             
073400*        RETAIL AMOUNTS EXCEEDS THE LIMIT, SPLIT THE AMOUNT ACROSS        
073500*        MULTIPLE PURCHASE ADJUSTMENTS.                                   
073600         PERFORM 4200-SPLIT-PURCH-ADJ                                     
073700     ELSE                                                                 
074100         MOVE MNSCLS-END-INV-RTL-AMT                                      
074200                                 TO ML015-PURCH-RETAIL                    
074300         WRITE PURCH-ADJ-REC FROM ML015-PURCHASE-RECORD                   
074400         ADD 1 TO PV-DR-PURCH-ADJ-RECS-WRITTEN                            
074500         PERFORM 4100-CREATE-CREDIT-PURCH-ADJ                             
074600     END-IF.                                                              
074700                                                                          
074800                                                                          
074900******************************************************************        
075000* THIS PARAGRAPH TAKES THE DEBIT TRANSACTION THAT WAS JUST                
075100* CREATED AND CHANGES SOME OF THE FIELDS TO CREATE THE                    
075200* CORRESPONDING CREDIT TRANSACTION.                                       
075300******************************************************************        
075400 4100-CREATE-CREDIT-PURCH-ADJ.                                            
075500                                                                          
075600     MOVE CC-TO-DEPT-NBR         TO ML015-PURCH-DEPT-NBR.                 
075700     MOVE PV-TO-SUB-CL-NBR       TO ML015-PURCH-CLASS-NBR.                
075800     MOVE PV-CR-BATCH-NBR        TO ML015-PURCH-BATCH-NBR.                
075900     MOVE PC-AP-TRANS-CDE-CR     TO ML015-PURCH-AP-TRANS-CDE.             
076000     MOVE PC-TRANS-CDE-CR        TO ML015-PURCH-TRANS-CDE.                
076100                                                                          
076200     WRITE PURCH-ADJ-REC FROM ML015-PURCHASE-RECORD.                      
076300     ADD 1 TO PV-CR-PURCH-ADJ-RECS-WRITTEN.                               
076400                                                                          
076500                                                                          
076600******************************************************************        
076700* THIS PARAGRAPH IS PERFORMED WHEN THE ENDING INVENTORY RETAIL            
076800* FROM TMNSCLS IS TOO LARGE TO FIT ON ONE PURCHASE ADJUSTMENT             
076900* IT SPLITS THE ENDING INVENTORY RETAIL ACROSS MULTIPLE PURCHASE          
077000* ADJUSTMENTS.                                                            
077100******************************************************************        
077200 4200-SPLIT-PURCH-ADJ.                                                    
077300                                                                          
077500     SET  PS-NOT-END-OF-RTL-AMTS TO TRUE.                                 
077600                                                                          
077900     MOVE MNSCLS-END-INV-RTL-AMT TO PV-REMAINING-END-INV-RTL.             
078000                                                                          
078100     PERFORM 4300-SPLIT-END-INV-RTL                                       
078300         UNTIL PS-END-OF-RTL-AMTS.                                        
078400                                                                          
078500                                                                          
078600******************************************************************        
078700* DETERMINE THE ENDING INVENTORY RETAIL TO BE PLACED ON THE               
078800* PURCHASE ADJUSTMENT BASED UPON THE AMOUNT REMAINING IN THE              
078900* BUCKET.  WRITE THE PURCHASE ADJUSTMENT.                                 
079000****************************************************************          
079100 4300-SPLIT-END-INV-RTL.                                                  
079200                                                                          
082000     IF PV-REMAINING-END-INV-RTL > PC-POS-AMT-LIMIT                       
082100         SUBTRACT PC-POS-AMT-LIMIT FROM PV-REMAINING-END-INV-RTL          
082200         MOVE PC-POS-AMT-LIMIT TO ML015-PURCH-RETAIL                      
082300     ELSE                                                                 
082400         IF PV-REMAINING-END-INV-RTL < PC-NEG-AMT-LIMIT                   
082500             SUBTRACT PC-NEG-AMT-LIMIT                                    
082600                 FROM PV-REMAINING-END-INV-RTL                            
082700             MOVE PC-NEG-AMT-LIMIT                                        
082800                                 TO ML015-PURCH-RETAIL                    
082900         ELSE                                                             
083000             IF PV-REMAINING-END-INV-RTL NOT = 0                          
083100                 MOVE PV-REMAINING-END-INV-RTL                            
083200                                 TO ML015-PURCH-RETAIL                    
083300                 MOVE +0         TO PV-REMAINING-END-INV-RTL              
083400                 SET PS-END-OF-RTL-AMTS TO TRUE                           
083500             ELSE                                                         
083600                 MOVE PV-REMAINING-END-INV-RTL                            
083700                                 TO ML015-PURCH-RETAIL                    
083800                 SET PS-END-OF-RTL-AMTS TO TRUE                           
083900             END-IF                                                       
084000         END-IF                                                           
084100     END-IF.                                                              
084200                                                                          
084300     MOVE CC-FROM-DEPT-NBR       TO ML015-PURCH-DEPT-NBR.                 
084400     MOVE MNSCLS-SUB-CL-NBR      TO ML015-PURCH-CLASS-NBR.                
084500     MOVE PV-DR-BATCH-NBR        TO ML015-PURCH-BATCH-NBR.                
084600     MOVE PC-AP-TRANS-CDE-DR     TO ML015-PURCH-AP-TRANS-CDE.             
084700     MOVE PC-TRANS-CDE-DR        TO ML015-PURCH-TRANS-CDE.                
084800     WRITE PURCH-ADJ-REC FROM ML015-PURCHASE-RECORD.                      
084900     ADD 1 TO PV-DR-PURCH-ADJ-RECS-WRITTEN.                               
085000     PERFORM 4100-CREATE-CREDIT-PURCH-ADJ.                                
085100                                                                          
085200                                                                          
085300******************************************************************        
085400* THIS PARAGRAPH IS PERFORMED WHEN A BREAK OCCURS ON SUBCLASS.            
085500* IT CREATES THE BUYER REPORT EXTRCT RECORDS FOR THE SUBCLASS.            
085600* TWO RECORDS ARE WRITTEN: ONE FOR THE 'FROM' (DEBIT) DEPARTMENT          
085700* AND ONE FOR THE 'TO' (CREDIT) DEPARTMENT.                               
085800******************************************************************        
085900 4400-CREATE-BYR-RPT-EXTRCT.                                              
086000                                                                          
086100                                                                          
086200*    CREATE THE RECORD FOR THE 'FROM' (DEBIT) DEPARTMENT.                 
086300     MOVE PV-FROM-DEPT-BYR-NBR   TO ML119-BYR-NBR.                        
086400     MOVE CC-FROM-DEPT-NBR       TO ML119-DEPT-NBR.                       
086500     MOVE PV-FROM-DEPT-DESC      TO ML119-DEPT-DESC.                      
086600     MOVE PV-PREV-SUB-CL-NBR     TO ML119-SUB-CL-NBR.                     
086700     MOVE PV-FROM-SUB-CL-DESC    TO ML119-SUB-CL-DESC.                    
086800                                                                          
086900     COMPUTE ML119-END-INV-RTL-AMT                                        
087000          =  PV-END-INV-RTL-SCL-TOT  * -1.                                
087300                                                                          
087400     WRITE BYR-RPT-EXTRCT-REC                                             
087500      FROM ML119-DEPT-ELIM-BY-BYR-EXTRACT.                                
087600                                                                          
087700     ADD +1 TO PV-BYR-RPT-EXTRCT-RECS-WRITTEN.                            
087800                                                                          
087900*    CREATE THE RECORD FOR THE 'TO' (CREDIT) DEPARTMENT.                  
088000     MOVE PV-TO-DEPT-BYR-NBR      TO ML119-BYR-NBR.                       
088100     MOVE CC-TO-DEPT-NBR          TO ML119-DEPT-NBR.                      
088200     MOVE PV-TO-DEPT-DESC         TO ML119-DEPT-DESC.                     
088300     MOVE PV-TO-SUB-CL-NBR        TO ML119-SUB-CL-NBR.                    
088400     MOVE PV-TO-SUB-CL-DESC       TO ML119-SUB-CL-DESC.                   
088500                                                                          
088600     MOVE PV-END-INV-RTL-SCL-TOT  TO ML119-END-INV-RTL-AMT.               
088800                                                                          
088900     WRITE BYR-RPT-EXTRCT-REC                                             
089000      FROM ML119-DEPT-ELIM-BY-BYR-EXTRACT.                                
089100                                                                          
089200     ADD +1 TO PV-BYR-RPT-EXTRCT-RECS-WRITTEN.                            
089300                                                                          
089400                                                                          
089500 5000-DISPLAY-PROCESS-SUMMARY.                                            
089600                                                                          
089700     DISPLAY ' '.                                                         
089800     DISPLAY '  PROGRAM MLKUT426 PROCESSING SUMMARY'.                     
089900     DISPLAY '  -----------------------------------'.                     
090000     DISPLAY ' '.                                                         
090100     MOVE PV-DR-PURCH-ADJ-RECS-WRITTEN        TO PV-DISPLAY-CNT.          
090200     DISPLAY 'DEBIT  PURCH ADJ RECS WRITTEN:   ' PV-DISPLAY-CNT.          
090300                                                                          
090400     MOVE PV-CR-PURCH-ADJ-RECS-WRITTEN        TO PV-DISPLAY-CNT.          
090500     DISPLAY 'CREDIT PURCH ADJ RECS WRITTEN:   ' PV-DISPLAY-CNT.          
090600                                                                          
090700     MOVE PV-BYR-RPT-EXTRCT-RECS-WRITTEN      TO PV-DISPLAY-CNT.          
090800     DISPLAY 'BYR RPT EXTRCT RECORDS WRITTEN: ' PV-DISPLAY-CNT.           
090900                                                                          
091000                                                                          
091100 6000-READ-CTRL-CARD-FILE.                                                
091200                                                                          
091300     READ CTRL-CARD-FILE INTO CC-CTRL-CARD                                
091400         AT END SET  PS-END-OF-CTRL-CARD-FILE  TO TRUE.                   
091500                                                                          
091600                                                                          
091700 7050-WRITE-RPT1-HEADINGS.                                                
091800                                                                          
091900     ADD 1                       TO PV-PAGE-CNT.                          
092000     MOVE PV-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
092100     WRITE DEPT-ELIM-DTL-RPT-REC                                          
092200       FROM DP132O-STANDRD-HEADER-132-COLS                                
092300         AFTER ADVANCING PAGE.                                            
092400                                                                          
092500     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1H1-RPT1-HEADING1                  
092600         AFTER ADVANCING 1 LINE.                                          
092700                                                                          
092800     WRITE DEPT-ELIM-DTL-RPT-REC FROM RH2-RPT-HEADING2                    
092900         AFTER ADVANCING 1 LINE.                                          
093000                                                                          
093100     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1H3-RPT1-HEADING3                  
093200         AFTER ADVANCING 2 LINES.                                         
093300     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1H4-RPT1-HEADING4                  
093400         AFTER ADVANCING 1 LINES.                                         
093500                                                                          
093600     MOVE SPACES                 TO DEPT-ELIM-DTL-RPT-REC.                
093700     WRITE DEPT-ELIM-DTL-RPT-REC                                          
093800         AFTER ADVANCING 1 LINE.                                          
093900                                                                          
094000     MOVE +7                     TO PV-LINE-CNT.                          
094100                                                                          
094200                                                                          
094300 7100-WRITE-RPT1-DETAIL-LINE.                                             
094400                                                                          
094500     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
094600         PERFORM 7050-WRITE-RPT1-HEADINGS.                                
094700                                                                          
094800     MOVE CC-FROM-DEPT-NBR        TO R1DTL-FROM-DEPT-NBR.                 
094900     MOVE MNSCLS-SUB-CL-NBR       TO R1DTL-FROM-SUB-CL-NBR.               
095000     MOVE PV-FROM-SUB-CL-DESC     TO R1DTL-FROM-SUB-CL-DESC.              
095100     MOVE CC-TO-DEPT-NBR          TO R1DTL-TO-DEPT-NBR.                   
095200     MOVE PV-TO-SUB-CL-NBR        TO R1DTL-TO-SUB-CL-NBR.                 
095300     MOVE PV-TO-SUB-CL-DESC       TO R1DTL-TO-SUB-CL-DESC.                
095400     MOVE MNSCLS-STR-NBR          TO R1DTL-STR-NBR.                       
095500     MOVE MNSCLS-END-INV-RTL-AMT  TO R1DTL-RTL-AMT.                       
095700                                                                          
095800     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1DTL-RPT1-DETAIL-LINE              
095900         AFTER ADVANCING 1 LINES.                                         
096000                                                                          
096100     ADD +1                      TO PV-LINE-CNT.                          
096200                                                                          
096300                                                                          
096400 7125-WRITE-RPT1-SCL-TOT-LINE.                                            
096500                                                                          
096600     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
096700         PERFORM 7050-WRITE-RPT1-HEADINGS.                                
096800                                                                          
096900     MOVE PC-SCL-TOT-LABEL TO R1TL-TOTAL-LABEL.                           
097000     MOVE PV-END-INV-RTL-SCL-TOT  TO R1TL-RTL-AMT.                        
097200                                                                          
097300     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1TL-RPT1-TOTAL-LINE                
097400         AFTER ADVANCING 1 LINES.                                         
097500     MOVE SPACE TO DEPT-ELIM-DTL-RPT-REC.                                 
097600     WRITE DEPT-ELIM-DTL-RPT-REC                                          
097700         AFTER ADVANCING 1 LINES.                                         
097800     ADD +2 TO PV-LINE-CNT.                                               
097900                                                                          
098000                                                                          
098100 7150-WRITE-RPT1-DEPT-TOT-LINE.                                           
098200                                                                          
098300     IF PV-LINE-CNT > PC-LINE-CNT-MAX                                     
098400         PERFORM 7050-WRITE-RPT1-HEADINGS.                                
098500                                                                          
098600     MOVE PC-DEPT-TOT-LABEL        TO R1TL-TOTAL-LABEL.                   
098700     MOVE PV-END-INV-RTL-DEPT-TOT  TO R1TL-RTL-AMT.                       
098900                                                                          
099000     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1TL-RPT1-TOTAL-LINE                
099100         AFTER ADVANCING 1 LINES.                                         
099200     MOVE SPACE TO DEPT-ELIM-DTL-RPT-REC.                                 
099300     WRITE DEPT-ELIM-DTL-RPT-REC                                          
099400         AFTER ADVANCING 1 LINES.                                         
099500     ADD +2 TO PV-LINE-CNT.                                               
099600                                                                          
099700                                                                          
099800 7175-WRITE-FINAL-RPT1-LINES.                                             
099900                                                                          
100000     PERFORM 7050-WRITE-RPT1-HEADINGS.                                    
100100                                                                          
100200     MOVE PC-GRND-TOT-LABEL        TO R1TL-TOTAL-LABEL.                   
100300     MOVE PV-END-INV-RTL-GRND-TOT  TO R1TL-RTL-AMT.                       
100500                                                                          
100600     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1TL-RPT1-TOTAL-LINE                
100700         AFTER ADVANCING 1 LINES.                                         
100800                                                                          
100900     MOVE PV-NBR-DEPTS-ELIM TO R1ND-NBR-DEPTS-ELIM.                       
101000     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1ND-NUM-DEPTS-LINE                 
101100         AFTER ADVANCING 2 LINES.                                         
101200                                                                          
101300     WRITE DEPT-ELIM-DTL-RPT-REC FROM R1-END-OF-RPT-LINE                  
101400         AFTER ADVANCING 3 LINES.                                         
101500                                                                          
101600                                                                          
101700 7200-BUILD-AND-WRITE-RPT2.                                               
101800                                                                          
101900     PERFORM 7250-WRITE-RPT2-HEADINGS.                                    
102000                                                                          
102100     MOVE PC-CREDIT-TOT-LABEL      TO R2TL-TOTAL-LABEL.                   
102200     MOVE PV-END-INV-RTL-GRND-TOT  TO R2TL-RTL-AMT.                       
102400     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM R2TL-RPT2-TOTAL-LINE            
102500         AFTER ADVANCING 1 LINE.                                          
102600                                                                          
102700     MOVE PC-CHARGE-TOT-LABEL            TO R2TL-TOTAL-LABEL.             
102800     COMPUTE PV-END-INV-RTL-GRND-TOT-RVRSD                                
102900           = PV-END-INV-RTL-GRND-TOT  * -1.                               
103200     MOVE PV-END-INV-RTL-GRND-TOT-RVRSD  TO R2TL-RTL-AMT.                 
103400     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM R2TL-RPT2-TOTAL-LINE            
103500         AFTER ADVANCING 1 LINE.                                          
103600                                                                          
103700     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM R1-END-OF-RPT-LINE              
103800         AFTER ADVANCING 3 LINES.                                         
103900                                                                          
104000                                                                          
104100 7250-WRITE-RPT2-HEADINGS.                                                
104200                                                                          
104300     MOVE 2              TO DP132O-REPORT-NUMBER.                         
104400     MOVE 1              TO DP132O-PAGE-NUMBER.                           
104500                                                                          
104600     WRITE DEPT-ELIM-TRANTOT-RPT-REC                                      
104700       FROM DP132O-STANDRD-HEADER-132-COLS                                
104800         AFTER ADVANCING PAGE.                                            
104900                                                                          
105000     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM R2H1-RPT2-HEADING1              
105100         AFTER ADVANCING 1 LINE.                                          
105200                                                                          
105300     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM RH2-RPT-HEADING2                
105400         AFTER ADVANCING 1 LINE.                                          
105500                                                                          
105600     WRITE DEPT-ELIM-TRANTOT-RPT-REC FROM R2H3-RPT2-HEADING3              
105700         AFTER ADVANCING 2 LINES.                                         
105800                                                                          
105900 8000-GET-OPN-FSCL-DTES.                                                  
106000                                                                          
106100     EXEC SQL                                                             
106300         SELECT  OPN_FSCL_MN_NBR                                          
106300                ,OPN_FSCL_MN_DTE                                          
106300                ,OPN_FSCL_WK_DTE                                          
106500           INTO :MLCNTL-OPN-FSCL-MN-NBR                                   
106500               ,:MLCNTL-OPN-FSCL-MN-DTE                                   
106500               ,:MLCNTL-OPN-FSCL-WK-DTE                                   
106600           FROM  TMLCNTL                                                  
106700     END-EXEC.                                                            
106800                                                                          
106900     EVALUATE TRUE                                                        
107000         WHEN SQLCODE = ZERO                                              
107100             CONTINUE                                                     
107200         WHEN OTHER                                                       
107300             MOVE '8000-GET-OPN-FSCL-DTES' TO PV-PARAGRAPH-NAME           
 07400             MOVE 'SELECT FROM TMLCNTL'   TO PV-DB2-OPER-ATTEMPTED        
107500             PERFORM 9998-SQL-ABEND                                       
107600     END-EVALUATE.                                                        
107700                                                                          
107800                                                                          
107900 8100-OPEN-TMNSCLS-CSR.                                                   
108000                                                                          
108100     EXEC SQL                                                             
108200          OPEN TMNSCLS-CSR                                                
108300     END-EXEC.                                                            
108400                                                                          
108500     EVALUATE TRUE                                                        
108600         WHEN SQLCODE = ZERO                                              
108700             CONTINUE                                                     
108800         WHEN OTHER                                                       
108900             MOVE '8100-OPEN-TMNSCLS-CSR'                                 
109000                                      TO PV-PARAGRAPH-NAME                
109100             MOVE 'OPEN TMNSCLS-CSR'  TO PV-DB2-OPER-ATTEMPTED            
109200             PERFORM 9998-SQL-ABEND                                       
109300     END-EVALUATE.                                                        
109400                                                                          
109500                                                                          
109600 8150-FETCH-TMNSCLS-CSR.                                                  
109700                                                                          
109800     EXEC SQL                                                             
109900         FETCH                                                            
110000             TMNSCLS-CSR                                                  
110100         INTO                                                             
110200             :MNSCLS-DEPT-NBR                                             
110300           , :MNSCLS-SUB-CL-NBR                                           
110400           , :MNSCLS-STR-NBR                                              
110500           , :MNSCLS-END-INV-RTL-AMT                                      
110700     END-EXEC.                                                            
110800                                                                          
110900     EVALUATE TRUE                                                        
111000         WHEN SQLCODE = ZERO                                              
111100             CONTINUE                                                     
111200         WHEN SQLCODE = +100                                              
111300             SET PS-END-OF-TMNSCLS-CSR TO TRUE                            
111400         WHEN OTHER                                                       
111500             MOVE '8150-FETCH-TMNSCLS-CSR'                                
111600                                          TO PV-PARAGRAPH-NAME            
111700             MOVE 'FETCH TMNSCLS-CSR'                             D       
111800                                          TO PV-DB2-OPER-ATTEMPTEDD       
111900             PERFORM 9998-SQL-ABEND                                       
112000     END-EVALUATE.                                                        
112100                                                                          
112200                                                                          
112300 8175-CLOSE-TMNSCLS-CSR.                                                  
112400                                                                          
112500     EXEC SQL                                                             
112600         CLOSE TMNSCLS-CSR                                                
112700     END-EXEC.                                                            
112800                                                                          
112900     EVALUATE TRUE                                                        
113000         WHEN SQLCODE = ZERO                                              
113100             CONTINUE                                                     
113200         WHEN OTHER                                                       
113300             MOVE '8175-CLOSE-TMNSCLS-CSR'                                
113400                                          TO PV-PARAGRAPH-NAME            
113500             MOVE 'CLOSE TMNSCLS-CSR'     TO PV-DB2-OPER-ATTEMPTEDD       
113600             PERFORM 9998-SQL-ABEND                                       
113700     END-EVALUATE.                                                        
113800                                                                          
113900                                                                          
114000 8200-GET-TMDSEAR-FOR-DPT.                                                
114100                                                                          
114200     EXEC SQL                                                             
114300         SELECT BYR_NBR,                                                  
114400                MDSEAR_DESC                                               
114500           INTO :MDSEAR-BYR-NBR,                                          
114600                :MDSEAR-DESC                                              
114700           FROM TMDSEAR                                                   
114800          WHERE OPERCO_NBR       = :PC-OPERCO-NBR                         
114900            AND DEPT_NBR         = :PV-DB2-DEPT-NBR                       
115000            AND MDSELV_CDE       = :PC-LV-CDE-DPT                         
115100            AND :MLCNTL-OPN-FSCL-MN-DTE BETWEEN                           
115200                 STRT_DTE AND END_DTE                                     
115300     END-EXEC.                                                            
115400                                                                          
115500     EVALUATE TRUE                                                        
115600         WHEN SQLCODE = 0                                                 
115700             SET PS-TMDSEAR-FOUND       TO TRUE                           
115800         WHEN SQLCODE = +100                                              
115900             SET PS-TMDSEAR-NOT-FOUND   TO TRUE                           
116000         WHEN OTHER                                                       
116100             MOVE '8200-GET-TMDSEAR-FOR-DPT'                              
116200                                        TO PV-PARAGRAPH-NAME              
116300             MOVE 'SELECT FROM TMDSEAR' TO  PV-DB2-OPER-ATTEMPTED         
116400             PERFORM 9998-SQL-ABEND                                       
116500     END-EVALUATE.                                                        
116600                                                                          
116700                                                                          
116710******************************************************************        
116720* THIS PARAGRAPH GETS THE SUBCLASS DESCRIPTION FROM MBS.  IF THE          
116721* SUBCLASS IS NOT FOUND, AN ABEND OCCURS BECAUSE ANY PURCHASE             
116722* ADJUSTMENTS CREATED FOR THIS SUBCLASS WOULD ERROR OUT OF THE            
116723* LEDGER EDIT IF ALLOWED TO GO THROUGH.                                   
116730******************************************************************        
116740                                                                          
116800 8300-GET-SUB-CL-DESC.                                                    
116900                                                                          
117000     EXEC SQL                                                             
117100         SELECT MDSEAR_DESC                                               
117200           INTO :MDSEAR-DESC                                              
117300           FROM TMDSEAR                                                   
117400          WHERE OPERCO_NBR       = :PC-OPERCO-NBR                         
117500            AND DEPT_NBR         = :PV-DB2-DEPT-NBR                       
117600            AND SUB_CL_NBR       = :PV-DB2-SUB-CL-NBR                     
117700            AND MDSELV_CDE       = :PC-LV-CDE-SCL                         
117800            AND :MLCNTL-OPN-FSCL-MN-DTE BETWEEN                           
117900                 STRT_DTE AND END_DTE                                     
118000     END-EXEC.                                                            
118100                                                                          
118200     EVALUATE TRUE                                                        
118300         WHEN SQLCODE = 0                                                 
118400             CONTINUE                                                     
118700         WHEN OTHER                                                       
                   DISPLAY 'PV-DB2-DEPT-NBR  : ' PV-DB2-DEPT-NBR                
                   DISPLAY 'PV-DB2-SUB-CL-NBR: ' PV-DB2-SUB-CL-NBR              
118800             MOVE '8300-GET-SUB-CL-DESC'                                  
118900                                        TO PV-PARAGRAPH-NAME              
119000             MOVE 'SELECT FROM TMDSEAR' TO PV-DB2-OPER-ATTEMPTED          
119100             PERFORM 9998-SQL-ABEND                                       
119200     END-EVALUATE.                                                        
119300                                                                          
119400                                                                          
119500******************************************************************        
119600* THIS PARAGRAPH WILL GET THE SUBCLASS TO WHICH ALL THE ENDING            
119700* INVENTORY WILL BE MOVED.  IT IS NOT IMPORTANT WHICH PARTICULAR          
119800* SUBCLASS IS USED, AS LONG AS IT IS A VALID SUBCLASS UNDER THE           
119900* 'TO' DEPARMENT.  THE MINIMUM VALID SUBCLASS FOR THE 'TO'                
120000* DEPARTMENT WILL BE USED.                                                
120100*                                                                         
120200* IF THERE ARE NO VALID SUBCLASSES UNDER THE 'TO' DEPARTMENT,             
120300* AN ABEND OCCURS.                                                        
120400******************************************************************        
120500 8400-GET-SCL-FOR-TO-DEPT.                                                
120600                                                                          
120700     EXEC SQL                                                             
120800       SELECT   SUB_CL_NBR                                                
120900              , MDSEAR_DESC                                               
121000         INTO  :MDSEAR-SUB-CL-NBR                                         
121100              ,:MDSEAR-DESC                                               
121200         FROM   TMDSEAR                                                   
121300         WHERE OPERCO_NBR = :PC-OPERCO-NBR                                
121400           AND DEPT_NBR   = :CC-TO-DEPT-NBR                               
121500           AND SUB_CL_NBR = (SELECT MIN(SUB_CL_NBR)                       
121600                              FROM   TMDSEAR                              
121700                              WHERE  OPERCO_NBR = :PC-OPERCO-NBR          
121800                                AND  DEPT_NBR   = :CC-TO-DEPT-NBR         
121900                                AND  MDSELV_CDE = :PC-LV-CDE-SCL          
122000                                AND :MLCNTL-OPN-FSCL-MN-DTE               
122100                                     BETWEEN STRT_DTE AND END_DTE)        
122200           AND :MLCNTL-OPN-FSCL-MN-DTE                                    
122300                BETWEEN STRT_DTE AND END_DTE                              
122400     END-EXEC.                                                            
122500                                                                          
122600     EVALUATE TRUE                                                        
122700         WHEN SQLCODE = 0                                                 
122800             CONTINUE                                                     
122900         WHEN OTHER                                                       
123000             MOVE '8400-GET-SCL-FOR-TO-DEPT'                              
123100                                        TO PV-PARAGRAPH-NAME              
123200             MOVE 'SELECT FROM TMDSEAR' TO PV-DB2-OPER-ATTEMPTED          
123300             PERFORM 9998-SQL-ABEND                                       
123400     END-EVALUATE.                                                        
123500                                                                          
123600                                                                          
123700 9998-SQL-ABEND.                                                          
123800                                                                          
123900     MOVE +4013 TO PV-ABEND-CODE.                                         
124000     DISPLAY '** ABEND *   = SQLERROR'.                                   
124100     DISPLAY '** PROGRAM   = ' PC-PGM-NAME.                               
124200     DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
124300     DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                     
124400                                                                          
124500     COPY DPPD004.                                                        
124600     PERFORM 9999-ABEND.                                                  
124700                                                                          
124800                                                                          
124900 9999-ABEND.                                                              
125000                                                                          
125100     CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
