       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      XSKUT017.                                       00020000
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    10/02/2002.                                     00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **XS DOMAIN TABLE INTEGRITY PROCESS - FOR TABLE XST_DEPT          00080000
      ******************************************************************00090000
      **THIS PROGRAM VALIDATES THE DATA IN THE XS DOMAIN IS THE SAME    00100000
      **AS THE DATA IN THE SOR - VSM.                                   00110000
      ******************************************************************00120000
      *DATE:10/02/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00130000
      ******************************************************************00140043
092404*DATE:09/24/2004   CHANGED BY:  GAYLE HAUPT    WR27107            00150043
092404*      FIELDS ADDED:  DMA-NBR, STR-ZN-CDE, BYR-NBR,               00160043
092404*      CONT-CLR-EXCLN-IND                                         00170043
      ******************************************************************00180000
       ENVIRONMENT DIVISION.                                            00190000
       CONFIGURATION SECTION.                                           00200000
       SOURCE-COMPUTER.        IBM-3090.                                00210000
       OBJECT-COMPUTER.        IBM-3090.                                00220000
       INPUT-OUTPUT SECTION.                                            00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT CONTROL-CARD                                          00260000
                  ASSIGN TO INP00.                                      00270000
           SELECT VSM-EXTRACT                                           00280000
                  ASSIGN TO INP01.                                      00290000
           SELECT XS-EXTRACT                                            00300000
                  ASSIGN TO INP02.                                      00310000
           SELECT DIFFERENCES-FILE                                      00320000
                  ASSIGN TO OUT01.                                      00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONTROL-CARD                                                 00380000
           RECORDING MODE F                                             00390000
           BLOCK CONTAINS 0 RECORDS                                     00400000
           LABEL RECORDS ARE OMITTED                                    00410000
           DATA RECORD IS CONTROL-CARD-RECORD.                          00420000
       01  CONTROL-CARD-RECORD             PIC X(080).                  00430000
                                                                        00440000
       FD  VSM-EXTRACT                                                  00450000
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           LABEL RECORDS ARE OMITTED                                    00480000
           DATA RECORD IS VSM-RECORD.                                   00490000
       01  VSM-RECORD                      PIC X(054).                  00500000
                                                                        00510000
       FD  XS-EXTRACT                                                   00520000
           RECORDING MODE F                                             00530000
           BLOCK CONTAINS 0 RECORDS                                     00540000
           LABEL RECORDS ARE OMITTED                                    00550000
           DATA RECORD IS XS-RECORD.                                    00560000
       01  XS-RECORD                       PIC X(038).                  00570043
                                                                        00580000
       FD  DIFFERENCES-FILE                                             00590000
           RECORDING MODE F                                             00600000
           BLOCK CONTAINS 0 RECORDS                                     00610000
           LABEL RECORDS ARE OMITTED                                    00620000
           DATA RECORD IS DIFFERENCES-RECORD.                           00630000
       01  DIFFERENCES-RECORD              PIC X(164).                  00640045
                                                                        00650000
       WORKING-STORAGE SECTION.                                         00660000
      ******************************************************************00670000
      **SWITCHES                                                        00680000
      ******************************************************************00690000
       01  PS-PROGRAM-SWITCHES.                                         00700000
           05  PS-END-OF-VSM-EXTRACT-SW        PIC X(01) VALUE 'N'.     00710000
               88  PS-END-OF-VSM-EXTRACT                 VALUE 'Y'.     00720000
               88  PS-NOT-END-OF-VSM-EXTRACT             VALUE 'N'.     00730000
           05  PS-END-OF-XS-EXTRACT-SW         PIC X(01) VALUE 'N'.     00740000
               88  PS-END-OF-XS-EXTRACT                  VALUE 'Y'.     00750000
               88  PS-NOT-END-OF-XS-EXTRACT              VALUE 'N'.     00760000
           05  PS-ERROR-FOUND-SW               PIC X(01) VALUE 'N'.     00770000
               88  PS-ERROR-FOUND                        VALUE 'Y'.     00780000
               88  PS-ERROR-NOT-FOUND                    VALUE 'N'.     00790000
      ******************************************************************00800000
      **CONTROL CARD LAYOUT                                             00810000
      ******************************************************************00820000
       01  CONTROL-CARD-LAYOUT.                                         00830000
           05  CC-START-POINT                  PIC 9(05) VALUE ZEROS.   00840000
           05  FILLER                          PIC X(01) VALUE SPACES.  00850000
           05  CC-READ-FREQUENCY               PIC 9(05) VALUE ZEROS.   00860000
           05  FILLER                          PIC X(69) VALUE SPACES.  00870000
      ******************************************************************00880000
      **TIMESTAMP VARIABLES                                             00890000
      ******************************************************************00900000
       01  SD-TIMESTAMP                        PIC X(26) VALUE SPACES.  00910000
       01  FILLER REDEFINES SD-TIMESTAMP.                               00920000
           05  SD-CURR-DTE                     PIC X(10).               00930000
           05  FILLER                          PIC X(01).               00940000
           05  SD-HOUR                         PIC X(02).               00950000
           05  FILLER                          PIC X(01).               00960000
           05  SD-MINUTE                       PIC X(02).               00970000
           05  FILLER                          PIC X(01).               00980000
           05  SD-SECOND                       PIC X(02).               00990000
           05  FILLER                          PIC X(01).               01000000
           05  SD-MICROSEC                     PIC X(06).               01010000
      ******************************************************************01020000
      **PROGRAM VARIABLES                                               01030000
      ******************************************************************01040000
       01  PC-PROGRAM-VARIABLES.                                        01050000
           05  PV-BUS-GP-NBR                   PIC 9(03) VALUE ZEROS.   01060000
      ******************************************************************01070000
      **PROGRAM CONSTANTS                                               01080000
      ******************************************************************01090000
       01  PC-PROGRAM-CONSTANTS.                                        01100000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01110000
                                                       VALUE 'XSKUT017'.01120000
      ******************************************************************01130000
      **PROGRAM COUNTERS                                                01140000
      ******************************************************************01150000
       01  PC-PROGRAM-COUNTER.                                          01160000
           05  PC-RECORD-COUNT                 PIC S9(09) VALUE ZEROS   01170000
                                                          COMP-3.       01180000
           05  PC-VSM-EXTRACT-RECS-READ        PIC 9(09) VALUE ZEROS.   01190000
           05  PC-XS-EXTRACT-RECS-READ         PIC 9(09) VALUE ZEROS.   01200000
           05  PC-ERROR-RECORD-WRITTEN         PIC 9(09) VALUE ZEROS.   01210000
      ******************************************************************01220000
      **ERROR HANDLING                                                  01230000
      ******************************************************************01240000
       01  PV-MISC-ABEND-FIELDS.                                        01250000
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01260000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01270000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01280000
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01290000
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01300000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01310000
      ******************************************************************01320000
      **COPYBOOKS                                                       01330000
      ******************************************************************01340000
      ***VSM EXTRACT FILE LAYOUT                                        01350000
           COPY HMRD008.                                                01360000
      ***XS EXTRACT FILE LAYOUT                                         01370000
           COPY XSRD005.                                                01380000
      ***XS ERROR FILE LAYOUT                                           01390000
           COPY XSRD006.                                                01400000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01410000
           COPY DPWS004.                                                01420000
      ***DB2 ABEND COPYBOOK                                             01430000
           COPY DPWS900.                                                01440000
      ***DB2 MESSAGE AREA                                               01450000
           COPY SQLCA2.                                                 01460000
      ******************************************************************01470000
      **TABLE INCLUSIONS                                                01480000
      ******************************************************************01490000
      ***DB2 COMMUNICATIONS AREA                                        01500000
           EXEC SQL                                                     01510000
                INCLUDE SQLCA                                           01520000
           END-EXEC.                                                    01530000
      ***TABLE XST_DEPT                                                 01540000
           EXEC SQL                                                     01550000
                INCLUDE XST00015                                        01560000
           END-EXEC.                                                    01570000
                                                                        01580000
       PROCEDURE DIVISION.                                              01590000
      ***************************************************************** 01600000
       000-MAINLINE.                                                    01610000
                                                                        01620000
           PERFORM 1000-INITIALIZATION.                                 01630000
           PERFORM 2000-PROCESS-RECORDS                                 01640000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01650000
                      OR PS-END-OF-XS-EXTRACT.                          01660000
           PERFORM 3000-PROCESS-EXTRA-RECORDS                           01670000
                   UNTIL PS-END-OF-VSM-EXTRACT                          01680000
                     AND PS-END-OF-XS-EXTRACT.                          01690000
           PERFORM 9999-WRAPUP.                                         01700000
           GOBACK.                                                      01710000
                                                                        01720000
       EJECT.                                                           01730000
      ******************************************************************01740000
      **INITIALIZATION AREA                                             01750000
      ******************************************************************01760000
       1000-INITIALIZATION.                                             01770000
                                                                        01780000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01790000
                                                                        01800000
           INITIALIZE PC-PROGRAM-COUNTER.                               01810000
                                                                        01820000
           OPEN INPUT  CONTROL-CARD                                     01830000
                       VSM-EXTRACT                                      01840000
                       XS-EXTRACT                                       01850000
                OUTPUT DIFFERENCES-FILE.                                01860000
                                                                        01870000
           PERFORM 1100-SELECT-DB2-TIME.                                01880000
           PERFORM 1200-SELECT-RECORD-COUNT.                            01890000
                                                                        01900000
           PERFORM 9999-READ-CONTROL-CARD.                              01910000
           PERFORM 9999-READ-VSM-EXTRACT CC-START-POINT TIMES.          01920000
           PERFORM 9999-READ-XS-EXTRACT  CC-START-POINT TIMES.          01930000
                                                                        01940000
       EJECT.                                                           01950000
      ******************************************************************01960000
      **RETRIEVE TIME FROM DB2                                          01970000
      ******************************************************************01980000
       1100-SELECT-DB2-TIME.                                            01990000
                                                                        02000000
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02010000
                                                                        02020000
           EXEC SQL                                                     02030000
                SELECT CURRENT TIMESTAMP                                02040000
                INTO  :SD-TIMESTAMP                                     02050000
                FROM   SYSIBM.SYSDUMMY1                                 02060000
           END-EXEC.                                                    02070000
                                                                        02080000
           EVALUATE TRUE                                                02090000
              WHEN SQLCODE = ZERO                                       02100000
                   CONTINUE                                             02110000
              WHEN OTHER                                                02120000
               PERFORM 9999-ABEND-ROUTINE                               02130000
           END-EVALUATE.                                                02140000
                                                                        02150000
       EJECT.                                                           02160000
      ******************************************************************02170000
      **RETRIEVE TIME FROM DB2                                          02180000
      ******************************************************************02190000
       1200-SELECT-RECORD-COUNT.                                        02200000
                                                                        02210000
           MOVE '1200-SELECT-RECORD-COUNT' TO PV-PARAGRAPH-NAME.        02220000
                                                                        02230000
           EXEC SQL                                                     02240000
                SELECT COUNT(*)                                         02250000
                INTO  :PC-RECORD-COUNT                                  02260000
                FROM   XST_DEPT                                         02270000
           END-EXEC.                                                    02280000
                                                                        02290000
           EVALUATE TRUE                                                02300000
              WHEN SQLCODE = ZERO                                       02310000
                   CONTINUE                                             02320000
              WHEN OTHER                                                02330000
                   PERFORM 9999-ABEND-ROUTINE                           02340000
           END-EVALUATE.                                                02350000
                                                                        02360000
       EJECT.                                                           02370000
      ******************************************************************02380000
      **PROCESSES INPUT FILE                                            02390000
      ******************************************************************02400000
       2000-PROCESS-RECORDS.                                            02410000
                                                                        02420000
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            02430000
                                                                        02440000
           INITIALIZE XSRD006-DEPT-ERR-RECORD.                          02450000
                                                                        02460000
           EVALUATE TRUE                                                02470000
              WHEN HM008-DEPT-NBR = XSRD005-DEPT-NBR                    02480000
                   PERFORM 2100-COMPARE-FIELDS                          02490000
                   PERFORM 9999-READ-XS-EXTRACT  CC-READ-FREQUENCY TIMES02500000
                   PERFORM 9999-READ-VSM-EXTRACT CC-READ-FREQUENCY TIMES02510000
              WHEN HM008-DEPT-NBR > XSRD005-DEPT-NBR                    02520000
                   PERFORM 9999-MOVE-XS-VALUES                          02530000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02540000
                   PERFORM 9999-READ-XS-EXTRACT                         02550000
              WHEN HM008-DEPT-NBR < XSRD005-DEPT-NBR                    02560000
                   PERFORM 9999-MOVE-VSM-VALUES                         02570000
                   PERFORM 9999-WRITE-ERROR-RECORD                      02580000
                   PERFORM 9999-READ-VSM-EXTRACT                        02590000
           END-EVALUATE.                                                02600000
                                                                        02610000
       EJECT.                                                           02620000
      ******************************************************************02630000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   02640000
      ******************************************************************02650000
       2100-COMPARE-FIELDS.                                             02660000
                                                                        02670000
           MOVE '2100-COMPARE-FIELDS' TO PV-PARAGRAPH-NAME.             02680000
                                                                        02690000
           INITIALIZE PS-ERROR-FOUND-SW.                                02700000
                                                                        02710000
           IF  XSRD005-DEPT-NBR     NOT EQUAL HM008-DEPT-NBR            02720000
               SET PS-ERROR-FOUND     TO TRUE                           02730000
           END-IF.                                                      02740000
                                                                        02750000
           MOVE XSRD005-BUS-GP-NBR    TO PV-BUS-GP-NBR.                 02760000
           IF  PV-BUS-GP-NBR        NOT EQUAL HM008-BUS-GP-NBR          02770000
               SET PS-ERROR-FOUND     TO TRUE                           02780000
           END-IF.                                                      02790000
                                                                        02800000
           IF  XSRD005-DEPT-NM      NOT EQUAL HM008-STR-DEPT-NM         02810000
               SET PS-ERROR-FOUND     TO TRUE                           02820000
           END-IF.                                                      02830000
                                                                        02840000
           IF  XSRD005-MDSE-LNE-CDE NOT EQUAL HM008-MDSE-LNE-CDE        02850000
               SET PS-ERROR-FOUND     TO TRUE                           02860000
           END-IF.                                                      02870000
                                                                        02880000
092404     IF  XSRD005-DMA-NBR      NOT EQUAL HM008-DMA-NBR             02890047
092404         SET PS-ERROR-FOUND     TO TRUE                           02900047
092404     END-IF.                                                      02910047
092404                                                                  02920047
092404     IF  XSRD005-STR-ZN-CDE   NOT EQUAL HM008-STR-ZNE-CDE         02930047
092404         SET PS-ERROR-FOUND     TO TRUE                           02940047
092404     END-IF.                                                      02950047
092404                                                                  02960047
092404     IF  XSRD005-BYR-NBR      NOT EQUAL HM008-BYR-NBR             02970047
092404         SET PS-ERROR-FOUND     TO TRUE                           02980047
092404     END-IF.                                                      02990047
092404                                                                  03000047
092404     IF  XSRD005-CONT-CLR-EXCLN-IND                               03010047
092404                              NOT EQUAL HM008-CONT-CLR-EXCLN-IND  03020047
092404         SET PS-ERROR-FOUND     TO TRUE                           03030047
092404     END-IF.                                                      03040047
                                                                        03050044
           IF  PS-ERROR-FOUND                                           03060000
               PERFORM 9999-MOVE-XS-VALUES                              03070000
               PERFORM 9999-MOVE-VSM-VALUES                             03080000
               PERFORM 9999-WRITE-ERROR-RECORD                          03090000
           END-IF.                                                      03100000
                                                                        03110000
       EJECT.                                                           03120000
      ******************************************************************03130000
      **COMPARES FIELDS BETWEEN FILES TO SEE IF ANY DIFFERENCES EXIST   03140000
      ******************************************************************03150000
       3000-PROCESS-EXTRA-RECORDS.                                      03160000
                                                                        03170000
           MOVE '3000-PROCESS-EXTRA-RECORDS' TO PV-PARAGRAPH-NAME.      03180000
                                                                        03190000
           INITIALIZE DIFFERENCES-RECORD.                               03200000
                                                                        03210000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03220000
               PERFORM 9999-MOVE-VSM-VALUES                             03230000
               PERFORM 9999-WRITE-ERROR-RECORD                          03240000
               PERFORM 9999-READ-VSM-EXTRACT                            03250000
           END-IF.                                                      03260000
                                                                        03270000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03280000
               PERFORM 9999-MOVE-XS-VALUES                              03290000
               PERFORM 9999-WRITE-ERROR-RECORD                          03300000
               PERFORM 9999-READ-XS-EXTRACT                             03310000
           END-IF.                                                      03320000
                                                                        03330000
       EJECT.                                                           03340000
      ***************************************************************** 03350000
      **READ CONTROL CARD FOR FREQUENCY OF COMPARE                      03360000
      ***************************************************************** 03370000
       9999-READ-CONTROL-CARD.                                          03380000
                                                                        03390000
           READ CONTROL-CARD INTO CONTROL-CARD-LAYOUT.                  03400000
                                                                        03410000
       EJECT.                                                           03420000
      ***************************************************************** 03430000
      **READ VSM EXTRACT FILE                                           03440000
      ***************************************************************** 03450000
       9999-READ-VSM-EXTRACT.                                           03460000
                                                                        03470000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03480000
               READ VSM-EXTRACT INTO HMRD008-RECORD                     03490000
                    AT END                                              03500000
                       SET PS-END-OF-VSM-EXTRACT TO TRUE                03510000
               END-READ                                                 03520000
           END-IF.                                                      03530000
                                                                        03540000
           IF  PS-NOT-END-OF-VSM-EXTRACT                                03550000
               ADD +1                       TO PC-VSM-EXTRACT-RECS-READ 03560000
           END-IF.                                                      03570000
                                                                        03580000
       EJECT.                                                           03590000
      ***************************************************************** 03600000
      **READ XS EXTRACT FILE                                            03610000
      ***************************************************************** 03620000
       9999-READ-XS-EXTRACT.                                            03630000
                                                                        03640000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03650000
               READ XS-EXTRACT INTO XSRD005-DEPT-UNLOAD-FILE            03660000
                    AT END                                              03670000
                       SET PS-END-OF-XS-EXTRACT TO TRUE                 03680000
               END-READ                                                 03690000
           END-IF.                                                      03700000
                                                                        03710000
           IF  PS-NOT-END-OF-XS-EXTRACT                                 03720000
               ADD +1                       TO PC-XS-EXTRACT-RECS-READ  03730000
           END-IF.                                                      03740000
                                                                        03750000
       EJECT.                                                           03760000
      ***************************************************************** 03770000
      **MOVES XS EXTRACT VALUES TO ERROR RECORD                         03780000
      ***************************************************************** 03790000
       9999-MOVE-XS-VALUES.                                             03800000
                                                                        03810000
           MOVE XSRD005-DEPT-NBR     TO XSRD006-XS-DEPT-NBR.            03820000
           MOVE XSRD005-BUS-GP-NBR   TO XSRD006-XS-BUS-GP-NBR.          03830000
           MOVE XSRD005-DEPT-NM      TO XSRD006-XS-BUS-DEPT-NM.         03840000
           MOVE XSRD005-MDSE-LNE-CDE TO XSRD006-XS-MERCH-LNE-CDE.       03850000
092404     MOVE XSRD005-DMA-NBR      TO XSRD006-XS-DMA-NBR.             03860047
092404     MOVE XSRD005-STR-ZN-CDE   TO XSRD006-XS-STR-ZN-CDE.          03870047
092404     MOVE XSRD005-BYR-NBR      TO XSRD006-XS-BYR-NBR.             03880047
092404     MOVE XSRD005-CONT-CLR-EXCLN-IND                              03890047
092404                               TO XSRD006-XS-CONT-CLR-EXCLN-IND.  03900047
                                                                        03910000
       EJECT.                                                           03920000
      ***************************************************************** 03930000
      **MOVES VSM EXTRACT VALUES TO ERROR RECORD                        03940000
      ***************************************************************** 03950000
       9999-MOVE-VSM-VALUES.                                            03960000
                                                                        03970000
           MOVE HM008-DEPT-NBR        TO XSRD006-VSM-DEPT-NBR.          03980000
           MOVE HM008-BUS-GP-NBR      TO XSRD006-VSM-BUS-GP-NBR.        03990000
           MOVE HM008-STR-DEPT-NM     TO XSRD006-VSM-BUS-DEPT-NM.       04000000
           MOVE HM008-MDSE-LNE-CDE    TO XSRD006-VSM-MERCH-LEN-CDE.     04010000
092404     MOVE HM008-DMA-NBR         TO XSRD006-VSM-DMA-NBR.           04020047
092404     MOVE HM008-STR-ZNE-CDE     TO XSRD006-VSM-STR-ZN-CDE.        04030047
092404     MOVE HM008-BYR-NBR         TO XSRD006-VSM-BYR-NBR.           04040047
092404     MOVE HM008-CONT-CLR-EXCLN-IND                                04050047
092404                                TO XSRD006-VSM-CON-CLR-EXCLN-IND. 04060047
                                                                        04070000
       EJECT.                                                           04080000
      ***************************************************************** 04090000
      **ERROR FILE                                                      04100000
      ***************************************************************** 04110000
       9999-WRITE-ERROR-RECORD.                                         04120000
                                                                        04130000
           WRITE DIFFERENCES-RECORD FROM XSRD006-DEPT-ERR-RECORD.       04140000
                                                                        04150000
           ADD +1                       TO PC-ERROR-RECORD-WRITTEN.     04160000
                                                                        04170000
       EJECT.                                                           04180000
      ****************************************************************  04190000
      *END OF PROGRAM ROUTINE                                           04200000
      ****************************************************************  04210000
       9999-WRAPUP.                                                     04220000
                                                                        04230000
           CLOSE CONTROL-CARD                                           04240000
                 VSM-EXTRACT                                            04250000
                 XS-EXTRACT                                             04260000
                 DIFFERENCES-FILE.                                      04270000
                                                                        04280000
           PERFORM 9999-DISPLAY-STATISTICS.                             04290000
                                                                        04300000
           IF  PC-ERROR-RECORD-WRITTEN > 0                              04310000
               MOVE 04               TO RETURN-CODE                     04320000
           END-IF.                                                      04330000
                                                                        04340000
       EJECT.                                                           04350000
      ****************************************************************  04360000
      *DISPLAY PROGRAM STATISTICS                                       04370000
      ****************************************************************  04380000
       9999-DISPLAY-STATISTICS.                                         04390000
                                                                        04400000
           DISPLAY '*************************************************'. 04410000
           DISPLAY '* PROGRAM..............: ' PC-CURRENT-PROGRAM-NAME. 04420000
           DISPLAY '* DATE/TIMESTAMP.......: ' SD-TIMESTAMP.            04430000
           DISPLAY '* RECORDS IN TABLE.....: ' PC-RECORD-COUNT.         04440000
           DISPLAY '* VSM EXTRACT RECORDS..: ' PC-VSM-EXTRACT-RECS-READ.04450000
           DISPLAY '* XS EXTRACT RECORDS...: ' PC-XS-EXTRACT-RECS-READ. 04460000
           DISPLAY '* ERROR RECORDS WRITTEN: ' PC-ERROR-RECORD-WRITTEN. 04470000
           DISPLAY '*************************************************'. 04480000
                                                                        04490000
       EJECT.                                                           04500000
      ******************************************************************04510000
      **SQL ABEND ROUTINE                                               04520000
      ******************************************************************04530000
       9999-ABEND-ROUTINE.                                              04540000
                                                                        04550000
           DISPLAY '** ABEND     **'.                                   04560000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        04570000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04580000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               04590000
      ******************************************************************04600000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     04610000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      04620000
      ******************************************************************04630000
           COPY DPPD004.                                                04640000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04650000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04660000
                                                                        04670000
       EJECT                                                            04680000
