       IDENTIFICATION DIVISION.                                         12/28/93
00003  PROGRAM-ID.         INKUT117.                                       LV001
00004  AUTHOR.             TROY BIRK.                                   INKUT117
00005  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKUT117
00006  DATE-WRITTEN.       11-93.                                       INKUT117
00007  DATE-COMPILED.                                                   INKUT117
00008                                                                   INKUT117
00009 ******************************************************************INKUT117
00010 *  THIS PROGRAM WILL READ THE TSK/STORE TABLE TO COME UP WITH     INKUT117
00011 *  ADJUSTMENTS WHICH WILL BE WRITTEN OUT IN FSK025 FORMAT.        INKUT117
00012 ******************************************************************INKUT117
      *  CHANGE MADE:  CHANGED PC-BATCH-NUM TO 'INAD' TO INDICATE      *        
      *                INVENTORY ADJUSTMENTS. TO BE USED TO            *        
      *                AUTOMATICALLY UPDATE THE TINVPAR TABLE WITH     *        
      *                ADJUSTMENT POSTING DATE.  DO NOT WRITE FASHION  *        
      *                RECORD IF ADJUST QTY = 0.                       *        
      *      MADE BY:  JEFF HARTIG/COMPUWARE                           *        
      *         DATE:  06/24/98                 WR# 6258               *        
      ******************************************************************        
      *  CHANGE MADE:  ADDED ITEM-NUMBER TO OUTPUT FILE.  THE          *        
      *                TSKST ON-HAND UPDATE FILE MUST BE SORTED IN     *        
      *                STORE-NBR/ITEM-NUMBER SEQUENCE BEFORE IT CAN    *        
      *                BE PROCESSED THE FASHION SALES APPLICATION.     *        
      *                                                                *        
      *                THIS CHANGE WAS MADE TO IMPROVE THE RUN TIME    *        
      *                OF THE TSKST ON-HAND-UPATE JOB                  *        
      *                                                                *        
      *      MADE BY:  JULIE SEIDLER                                   *        
      *         DATE:  07/14/00                 WR#                    *        
      ******************************************************************        
RB0201*  CHANGE MADE:  CHANGE ADJUSTMENT CALCULATION FOR STORES        *        
RB0201*                USING MANIFESTING                               *        
RB0201*                FROM:                                           *        
RB0201*                    ADJUSTMENT AMT = QTY - (OLD ONHAND QTY      *        
RB0201*                                               - AFTER RECEIPTS)*        
RB0201*                TO:                                             *        
RB0201*                    ADJUSTMENT AMT = QTY - (OLD ONHAND QTY      *        
RB0201*                                            - CUTOFF INTRANSIT) *        
RB0201*                                                                *        
RB0201*      MADE BY:  RONNA BUTZ                                      *        
RB0201*         DATE:  03/14/01                 WR#                    *        
RB0201******************************************************************        
W21732******************************************************************        
W21732* CHANGE LOG                                                              
W21732* DATE       WHO     CHANGE DESC                                          
W21732* ---------- ------- -----------------------------------------            
W21732* 04-12-2002 ASHOK,  NON-INTELLIGENT SKU CONVERSION. WR# 21732            
W21732*            IGSI    INWS016 WAS INCREASED IN SIZE TO 80 BYTES.           
W21732*                    COPYBOOK KDC1801 IS REPLACED BY FSRD035.             
R30410* 04-10-03   RONNA BUTZ   INCLUDE BATCH NUMBER IN THE OUTPUT FILE.        
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
W28545* W28545  08-10-2006   B. GRAHAM - STRATAGEM                              
W28545* W28545               DEPARTMENT INVENTORY PROJECT                       
W28545* W28545               TINVPAR/TINCNTL JOIN                               
S28545* S28545               TINVPAR/TINCNTL JOIN (SCOTT)                       
W33304* W33304  05-01-2008  JILL LIN                                            
W33304*                     HOLDING INVENTORY LEDGER OPEN ACROSS                
W33304*                     PERIODS INVENTORY SHORTAGE REPORTING                
R30410******************************************************************        
                                                                        INKUT117
00014  ENVIRONMENT DIVISION.                                            INKUT117
00016  CONFIGURATION SECTION.                                           INKUT117
00018  SOURCE-COMPUTER.    IBM-3090.                                    INKUT117
00019  OBJECT-COMPUTER.    IBM-3090.                                    INKUT117
00021  INPUT-OUTPUT SECTION.                                            INKUT117
00023  FILE-CONTROL.                                                    INKUT117
00024                                                                   INKUT117
00025      SELECT INPUT-FILE                                            INKUT117
00026      ASSIGN TO INP01.                                             INKUT117
00027                                                                   INKUT117
00028      SELECT FASHION-OUT-FILE                                      INKUT117
00029      ASSIGN TO OUT01.                                             INKUT117
00030                                                                   INKUT117
00031      SELECT OUTPUT-FILE                                           INKUT117
00032      ASSIGN TO OUT02.                                             INKUT117
00033                                                                   INKUT117
00035  DATA DIVISION.                                                   INKUT117
00037  FILE SECTION.                                                    INKUT117
00038                                                                   INKUT117
00039  FD  FASHION-OUT-FILE                                             INKUT117
00040      LABEL RECORDS ARE STANDARD                                   INKUT117
00041      RECORDING MODE IS F                                          INKUT117
00042      BLOCK CONTAINS 0 RECORDS.                                    INKUT117
W21732 01  FASHION-OUT-RECORD               PIC X(200).                 INKUT117
00044                                                                   INKUT117
00045  FD  INPUT-FILE                                                   INKUT117
00046      LABEL RECORDS ARE STANDARD                                   INKUT117
00047      RECORDING MODE IS F                                          INKUT117
00048      BLOCK CONTAINS 0 RECORDS.                                    INKUT117
W21732 01  INPUT-RECORD                     PIC X(80).                  INKUT117
00050                                                                   INKUT117
00051  FD  OUTPUT-FILE                                                  INKUT117
00052      LABEL RECORDS ARE STANDARD                                   INKUT117
00053      RECORDING MODE IS F                                          INKUT117
00054      BLOCK CONTAINS 0 RECORDS.                                    INKUT117
W21732 01  OUTPUT-RECORD                    PIC X(80).                  INKUT117
00056                                                                   INKUT117
W21732/*****************************************************************        
00059  WORKING-STORAGE SECTION.                                         INKUT117
W21732******************************************************************        
00061  01  ABEND-CODE               PIC S9(04)  VALUE ZERO COMP SYNC.   INKUT117
00062 *                                                                 INKUT117
00063  01  PC-PROGRAM-CONSTANTS.                                        INKUT117
6258       05  PC-BATCH-NUM           PIC X(4)      VALUE 'INAD'.       INKUT117
00065      05  PC-TRAN-CODE           PIC X(8)      VALUE 'DFSMSA70'.   INKUT117
00066      05  PC-MINUS-ONE           PIC S9(01)    VALUE -1.           INKUT117
00067 *                                                                 INKUT117
00068  01  PS-PROGRAM-SWITCHES.                                         INKUT117
00069      05  PS-MORE-RECS-SW        PIC X         VALUE 'Y'.          INKUT117
00070          88  PS-MORE-RECS                     VALUE 'Y'.          INKUT117
00071          88  PS-NO-MORE-RECS                  VALUE 'N'.          INKUT117
00072 *                                                                 INKUT117
RB0201 01  PV-PROGRAM-VARIABLES.                                        INKUT117
W26600     05  PV-LAST-STORE          PIC X(04)     VALUE SPACES.       INKUT117
W26600     05  PV-STORE               PIC 9(04)     VALUE ZEROES.       INKUT117
RB0201     05  PV-MFSTNG              PIC X(01)     VALUE 'Y'.          INKUT117
RB0201     05  PV-PARAGRAPH-NAME      PIC X(30)     VALUE SPACES.       01060000
RB0201     05  PV-DB2-OPER-ATTEMPTED  PIC X(30)     VALUE SPACES.       01070000
00067 *                                                                 INKUT117
00073 ***************************************************************** INKUT117
00074 * INPUT FILE RECORD LAYOUT                                        INKUT117
00075 ***************************************************************** INKUT117
W26600     COPY INWS016.                                                INKUT117
00077 *                                                                 INKUT117
00078 ***************************************************************** INKUT117
00079 * FASHION SALES INPUT FILE FORMAT.                                INKUT117
00080 ***************************************************************** INKUT117
W21732     COPY FSRD035.                                                INKUT117
00082 *                                                                 INKUT117
RB0201*****************************************************************         
RB0201* ERROR MESSAGE AREA FOR DB2                                              
RB0201*****************************************************************         
RB0201     COPY DPWS004.                                                        
RB0201*                                                                         
RB0201******************************************************************        
RB0201*  USED FOR DB2 ERRORS                                                    
RB0201******************************************************************        
RB0201     EXEC SQL                                                             
RB0201         INCLUDE SQLCA                                                    
RB0201     END-EXEC.                                                            
RB0201                                                                          
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
RB0201******************************************************************        
RB0201*  LAYOUT USED FOR INVENTORY PARMS TABLE                                  
RB0201******************************************************************        
RB0201     EXEC SQL                                                             
RB0201         INCLUDE TINVPAR                                                  
RB0201     END-EXEC.                                                            
RB0201                                                                          
00087 ******************************************************************INKUT117
00085  PROCEDURE DIVISION.                                              INKUT117
00089 ******************************************************************INKUT117
00090  A100-MAINLINE-PROCESSING.                                        INKUT117
00091                                                                   INKUT117
00092      PERFORM B100-OPEN-FILES.                                     INKUT117
00093      PERFORM Z100-READ-INPUT-FILE.                                INKUT117
00094                                                                   INKUT117
00095      PERFORM B200-PROCESS-STORES                                  INKUT117
00096          UNTIL PS-NO-MORE-RECS.                                   INKUT117
00097                                                                   INKUT117
00098      PERFORM B300-CLOSE-FILES.                                    INKUT117
00099                                                                   INKUT117
00100      STOP RUN.                                                    INKUT117
00101 *                                                                 INKUT117
00102 ******************************************************************INKUT117
00103 *    OPEN ALL FILES.                                              INKUT117
00104 ******************************************************************INKUT117
00105  B100-OPEN-FILES.                                                 INKUT117
00106                                                                   INKUT117
00107      OPEN INPUT  INPUT-FILE                                       INKUT117
00108           OUTPUT FASHION-OUT-FILE                                 INKUT117
00109                  OUTPUT-FILE.                                     INKUT117
00110 *                                                                 INKUT117
00111 ****************************************************************  INKUT117
00112 *                                                                 INKUT117
00113 ****************************************************************  INKUT117
00114  B200-PROCESS-STORES.                                             INKUT117
00115 *                                                                 INKUT117
RB0201     IF PV-LAST-STORE NOT = IN016-STORE                                   
RB0201         MOVE IN016-STORE    TO PV-LAST-STORE                             
W26600         MOVE IN016-STORE-9  TO INVPAR-LOC-NBR                            
RB0201         PERFORM B800-GET-MANIFEST-IND                                    
RB0201     END-IF.                                                              
RB0201     IF PV-MFSTNG = 'Y'                                                   
RB0201         COMPUTE IN016-ADJUSTMENT-AMT  =                                  
RB0201                 IN016-QTY -                                              
W27808                 (IN016-OLD-ONHAND-QTY                                    
W27808                   -(IN016-STR-EXP-OH-QTY                                 
W27808                   - IN016-CUTOFF-INTRANSIT))                             
00119          END-COMPUTE                                              INKUT117
RB0201     ELSE                                                                 
00116          COMPUTE IN016-ADJUSTMENT-AMT  =                          INKUT117
00117                  IN016-QTY -                                      INKUT117
00118                  (IN016-OLD-ONHAND-QTY - IN016-AFTER-RECEIPTS)    INKUT117
00119          END-COMPUTE                                              INKUT117
RB0201     END-IF.                                                              
00120 *                                                                 INKUT117
W21732     INITIALIZE FS035-ADJUSTMENT-RECORD.                          INKUT117
W21732     MOVE IN016-ITEM-NUMBER TO FS035-ITM-NBR.                             
W21732     MOVE IN016-STORE       TO PV-STORE.                          INKUT117
W21732     MOVE PV-STORE          TO FS035-LOC-NBR.                     INKUT117
W21732     MOVE 'INKUT117'        TO FS035-ADJ-SRC-PGM-NM.              INKUT117
00125      IF IN016-ADJUSTMENT-AMT >= ZERO                              INKUT117
W21732         MOVE IN016-ADJUSTMENT-AMT   TO FS035-ADJ-QTY             INKUT117
W21732         SET FS035-ACTN-PLS          TO TRUE                      INKUT117
00128      ELSE                                                         INKUT117
W21732         COMPUTE FS035-ADJ-QTY   =                                INKUT117
00130                  PC-MINUS-ONE * IN016-ADJUSTMENT-AMT              INKUT117
00131          END-COMPUTE                                              INKUT117
W21732         SET FS035-ACTN-NEG   TO TRUE                             INKUT117
00133      END-IF.                                                      INKUT117
W21732     SET FS035-OH-ADJ     TO TRUE.                                INKUT117
R30410     MOVE PC-BATCH-NUM    TO FS035-BTCH-NBR.                              
W21732     IF FS035-ADJ-QTY  NOT = ZERO                                 INKUT117
00137          PERFORM Z300-WRITE-FASHION-OUT-REC                       INKUT117
00139          PERFORM Z200-WRITE-OUTPUT-RECORD.                        INKUT117
00140                                                                   INKUT117
00141      PERFORM Z100-READ-INPUT-FILE.                                INKUT117
00142 *                                                                 INKUT117
00143 ******************************************************************INKUT117
00144 *   CLOSE ALL FILES.                                              INKUT117
00145 ******************************************************************INKUT117
00146  B300-CLOSE-FILES.                                                INKUT117
00147                                                                   INKUT117
00148      CLOSE       INPUT-FILE                                       INKUT117
00149                  FASHION-OUT-FILE                                 INKUT117
00150                  OUTPUT-FILE.                                     INKUT117
00151 *                                                                 INKUT117
RB0201******************************************************************INKUT117
RB0201*   GET THE MANIFESTING INDICATOR FOR THE STORE.                  INKUT117
RB0201******************************************************************INKUT117
RB0201 B800-GET-MANIFEST-IND.                                           INKUT117
RB0201                                                                  INKUT117
RB0201     EXEC SQL                                                     INKUT117
RB0201                                                                          
RB0201*        SELECT MFSTNG_IND                                                
RB0201*          INTO :INVPAR-MFSTNG-IND                                        
28545 *          FROM TINVPAR A                                                 
28545 *              ,TINCNTL B                                                 
W26600*         WHERE LOC_NBR = :INVPAR-LOC-NBR                                 
28545 *           AND A.INV_ID = B.INV_ID                                       
28545 *           AND B.ACTV_IND = 'Y'                                          
RB0201*           AND ACTL_INV_DTE =                                            
RB0201*               (SELECT MAX(ACTL_INV_DTE)                                 
RB0201*                FROM TINVPAR                                             
W26600*                WHERE LOC_NBR = :INVPAR-LOC-NBR)                         
28545 *                  AND A.INV_ID = B.INV_ID                                
28545 *                  AND B.ACTV_IND = 'Y'                                   
S28545         SELECT MFSTNG_IND                                                
S28545           INTO :INVPAR-MFSTNG-IND                                        
S28545           FROM TINVPAR A                                                 
S28545               ,TINCNTL B                                                 
S28545          WHERE LOC_NBR = :INVPAR-LOC-NBR                                 
S28545            AND A.INV_ID = B.INV_ID                                       
S28545            AND B.ACTV_IND = 'Y'                                          
W33304            AND A.LOC_INV_STAT_CDE = 'IN'                                 
S28545            AND ACTL_INV_DTE =                                            
S28545                (SELECT MAX(ACTL_INV_DTE)                                 
S28545                 FROM TINVPAR A                                           
S28545                     ,TINCNTL B                                           
S28545                 WHERE LOC_NBR = :INVPAR-LOC-NBR                          
S28545                   AND A.INV_ID = B.INV_ID                                
W33304                   AND A.LOC_INV_STAT_CDE = 'IN'                          
S28545                   AND B.ACTV_IND = 'Y')                                  
RB0201         WITH UR                                                          
RB0201     END-EXEC                                                     INKUT117
RB0201                                                                  INKUT117
RB0201     EVALUATE TRUE                                                        
RB0201         WHEN SQLCODE = 0                                                 
RB0201              MOVE INVPAR-MFSTNG-IND TO PV-MFSTNG                         
RB0201         WHEN SQLCODE NOT = ZERO                                          
W26600                 DISPLAY 'ERROR FOR STORE: ' IN016-STORE                  
RB0201                 MOVE 'B800-GET-MANIFEST-IND'                             
RB0201                               TO  PV-PARAGRAPH-NAME                      
RB0201                 MOVE 'STORE NOT FOUND UPDATING TINVPAR'                  
RB0201                               TO  PV-DB2-OPER-ATTEMPTED                  
RB0201                 PERFORM Z900-SQL-ABEND                                   
RB0201     END-EVALUATE.                                                        
RB0201                                                                  INKUT117
00151 *                                                                 INKUT117
00152 ****************************************************************  INKUT117
00153 *  READS THE INPUT FILE OF STORES TO HAVE UNITS BOOKED.           INKUT117
00154 ****************************************************************  INKUT117
00155  Z100-READ-INPUT-FILE.                                            INKUT117
00156                                                                   INKUT117
00157      READ INPUT-FILE                                              INKUT117
00158          INTO IN016-PHYSICAL-COUNT-RECORD                         INKUT117
00159            AT END                                                 INKUT117
00160              SET PS-NO-MORE-RECS TO TRUE.                         INKUT117
00161 *                                                                 INKUT117
00162 ******************************************************************INKUT117
00163 *                                                                 INKUT117
00164 ******************************************************************INKUT117
00165  Z200-WRITE-OUTPUT-RECORD.                                        INKUT117
00166                                                                   INKUT117
00167      WRITE OUTPUT-RECORD                                          INKUT117
00168          FROM IN016-PHYSICAL-COUNT-RECORD.                        INKUT117
00169 *                                                                 INKUT117
00170 ******************************************************************INKUT117
00171 * Z300-WRITE-FASHION-OUT-REC.                                     INKUT117
00172 ******************************************************************INKUT117
00173                                                                   INKUT117
00174  Z300-WRITE-FASHION-OUT-REC.                                      INKUT117
00175                                                                   INKUT117
00176      WRITE FASHION-OUT-RECORD                                     INKUT117
W21732         FROM FS035-ADJUSTMENT-RECORD.                            INKUT117
00178                                                                   INKUT117
RB0201******************************************************************        
RB0201*  STANDARD DB2 ERROR ABEND ROUTINE                                       
RB0201*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
RB0201******************************************************************        
RB0201 Z900-SQL-ABEND.                                                          
RB0201                                                                          
RB0201     MOVE +4013                 TO ABEND-CODE.                            
RB0201     DISPLAY '**  ABEND     **'.                                          
RB0201     DISPLAY '**  PROGRAM   **  =  INKUT117'.                             
RB0201     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
RB0201     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
RB0201                                                                          
RB0201*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
RB0201*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
RB0201                                                                          
RB0201     COPY DPPD004.                                                        
RB0201                                                                          
RB0201     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
RB0201                                                                  INKUT117
