      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    DPKCS007.                                                 
       AUTHOR.        RICK TULLOCH.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/12/90.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  CICS PROGRAM - DPKCS007 - Z007                                *        
      *                                                                *        
      *       CICS ARCHITECTURE - MENU PROCESSOR                       *        
      *                                                                *        
      *                                                                *        
      *                                                                *        
      *     CHANGE   PROG-                                             *        
      *      DATE    RAMMER   DESCRIPTION OF CHANGE                    *        
      *    --------  -------  -------------------------------------    *        
      *    02/19/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.                
      *                       ARCHITECTURE CHANGES TO ALLOW CICS       *        
      *                       TO BE UP 24HRS/DAY, 7DAYS/WEEK:          *        
      *                       1) CHGD DPSAVCOM ESDS VSAM DATASET       *        
      *                          TO:  DPSAVCMK KSDS VSAM DATASET.      *        
      *                          SEE NEW COPYBOOK DPRD515.             *        
      *                       2) DELETE APPROPRIATE RECORD FROM        *        
      *                          DPSAVCMK KSDS VSAM DATASET, WHEN:     *        
      *                          A) ITEM IS ROLLED OFF OF STACK (DUE   *        
      *                             TO OVERFLOW SITUATION).            *        
      *    08/24/98  TKMA464  CONVERTED "START" TO "RETURN IMMEDIATE". *        
      *    09/21/98  TKMA464  FIXED PRODUCTION BUG.                    *        
      *                       CHANGED QUEUE SIZE TO MATCH CA FIELD.    *        
      *    10/08/98  TKMA464  CHECK IF OTHER REGIONS ARE AVAILABLE.    *        
      *    10/20/99  TKMA464  IMPLEMENT ACCURATE LOCATION REQUIRED     *        
      *                       OPTION.                                  *        
      *    05/17/00  TKMA464  DISPLAY MESSAGE 99912 WHEN A REGION IS   *        
      *                       DOWN MAKING OPTIONS NOT AVAILABLE.       *        
      *    12/22/03  TKMA464  MADE CHANGES FOR EXPANDING LOCATION FROM *        
      *                       3 TO 5 DIGITS.                           *        
262023******************************************************************        
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023                
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE            
262023*  MAKING THE CALL DYNAMIC VS STATIC                                      
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *                                                                         
      *  CICS ARCHITECTURE STANDARD COMMAREA                                    
      *                                                                         
                                                                                
           COPY DPWS020.                                                        
                                                                                
      *                                                                         
      *  APPLICATION SPECIFIC COMMAREA - REDEFINED FOR THIS TRANSACTION         
      *                                                                         
                                                                                
               10  CA-COMMAREA         REDEFINES DP020-APPL-COMM-AREA.          
                   15  FILLER          PIC X(2).                                
                   15  CA-SELECTION-NUMBER                                      
                                       PIC X(3).                                
                   15  CA-NMBR-OF-MENU-ITEMS                                    
                                       PIC S9(4)                                
                                       COMP.                                    
                   15  CA-NMBR-OF-AVAIL-MENU-ITEMS                              
                                       PIC S9(4)                                
                                       COMP.                                    
                   15  CA-NMBR-OF-BLKS-WRITTEN-TO-TS                            
                                       PIC S9(4)                                
                                       COMP.                                    
                   15  CA-ITEM-AT-TOP-OF-OLD-PANEL                              
                                       PIC S9(4)                                
                                       COMP.                                    
                   15  CA-MENU-BEING-DISPLAYED                                  
                                       PIC X(8).                                
                   15  CA-BLOCK-ARRAY  OCCURS 2 TIMES                           
                                       INDEXED BY CA-BLOCK-INDEX.               
                                                                                
      **** ANY CHANGES TO THIS ARRAY MUST ALSO BE MADE IN BB-ITEM-ARRAY         
                       20  CA-ITEM-ARRAY                                        
                                       OCCURS 19 TIMES                          
                                       INDEXED BY CA-ITEM-INDEX.                
                           25  CA-MENU-ITEM.                                    
                               30  CA-MENU-ITEM-NUMBER                          
                                       PIC S9(4)                                
                                       COMP.                                    
                               30  CA-MENU-ITEM-MNEMONIC                        
                                       PIC X(8).                                
                               30  CA-MENU-ITEM-DESCRIPTION                     
                                       PIC X(30).                               
                               30  CA-MENU-ITEM-TRAN-ID                         
                                       PIC X(4).                                
                               30  CA-MENU-ITEM-AVAILABILITY-IND                
                                       PIC X(1).                                
                                   88  CA-MENU-ITEM-IS-AVAILABLE                
                                                    VALUE 'Y' 'R'.              
                                   88  CA-MENU-ITEM-IS-NOT-AVAILABLE            
                                                    VALUE 'N'.                  
                                   88  CA-MENU-ITEM-IS-REMOTE                   
                                                    VALUE 'R' 'U'.              
                                   88  CA-MENU-ITEM-IS-UNCONNECTED              
                                                    VALUE 'U'.                  
                               30  CA-MENU-ITEM-COMMAREA-FORMAT                 
                                       PIC X(1).                                
                                   88  CA-MENU-ITEM-COMMAREA-STD                
                                                    VALUE 'K'.                  
                                   88  CA-MENU-ITEM-COMMAREA-NOT-STD            
                                                    VALUE 'N'.                  
                               30  CA-INT-APPL-FORMAT-IND                       
                                       PIC X(1).                                
                               30  CA-ACRT-LOC-REQR-IND                         
                                       PIC X(1).                                
                                   88  CA-ACRT-LOC-REQR                         
                                                    VALUE 'Y'.                  
                                   88  CA-ACRT-LOC-NOT-REQR                     
                                                    VALUE 'N' ' '.              
                   15  FILLER          PIC X(1020).                             
                                                                                
      *                                                                         
      *  STANDARD ATTENTION IDENTIFIER LIST                                     
           COPY DFHAID.                                                         
                                                                                
      *                                                                         
      *  STANDARD ATTENTION IDENTIFIER LIST (AS USED IN THE NEW CICS            
      *  ARCHITECTURE)                                                          
           COPY DPWS016.                                                        
                                                                                
      *                                                                         
      *  STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST                  
           COPY DPWS015.                                                        
                                                                                
      *                                                                         
      *  COMMAREA AND WORKING-STORAGE VARIABLES REQUIRED FOR THE                
      *  SECURITY SUBROUTINE (CICSAUTH).                                        
           COPY DPWS008.                                                        
                                                                                
      *                                                                         
      *  WORKING-STORAGE VARIABLES REQUIRED FOR THE LOAD FUNCTION KEY           
      *  LINES ROUTINE (DPPD009).                                               
           COPY DPWS009.                                                        
                                                                                
      *                                                                         
      *  WORKING-STORAGE VARIABLES REQUIRED FOR THE PROCESS ABEND               
      *  ROUTINE (DPPD013).                                                     
           COPY DPWS013.                                                        
                                                                                
      *                                                                         
      *  WORKING-STORAGE VARIABLES REQUIRED FOR THE GET CURRENT DATE            
      *  AND TIME ROUTINE (DPPD017).                                            
           COPY DPWS017.                                                        
                                                                                
      *                                                                         
      *  PARAMETER LIST FOR THE NUMERIC EDIT SUBROUTINE (DPKUT100).             
           COPY DPWS010I.                                                       
                                                                                
      *                                                                         
      *  WORKING FIELDS NEEDED TO REFORMAT THE CICS SYSTEM MESSAGES.            
           COPY DPWS006.                                                        
                                                                                
      *                                                                         
      *  PARAMETER LIST FOR THE INTERROGATE EIBAID AND COMMAND LINE             
      *  SUBROUTINE (DPKUT045).                                                 
           COPY DPWS045.                                                        
                                                                                
      *                                                                         
      *  MNEMONIC FILE RECORD LAYOUT                                            
           COPY DPRD503.                                                        
                                                                                
      *                                                                         
      *  MENU ITEM FILE RECORD LAYOUT                                           
           COPY DPRD504.                                                        
                                                                                
      *                                                                         
      *  SYSTEM MESSAGES RECORD LAYOUT                                          
           COPY DPRD507I.                                                       
                                                                                
      *                                                                         
      *  OBJECT LOCKING RECORD LAYOUT                                           
           COPY DPRD509.                                                        
                                                                                
      *                                                                         
      *  KSDS VSAM FILE FOR SAVING EACH TRAN'S STANDARD COMMAREA LAYOUT.        
           COPY DPRD515.                                                        
                                                                                
      *                                                                         
      *  DSECT FOR THE DPKM007 MAPSET                                           
           COPY DPKM007.                                                        
                                                                                
      *                                                                         
      *  REDEFINITION OF THE MAP DATA AREA, REQUIRED TO USE THE GET             
      *  PANEL (DPPD007) AND WRITE PANEL (DPPD008) ROUTINES.                    
      *                                                                         
      *  THE ADDITION REDEFINITION OF THE DPKM007 DSECT IS DONE TO              
      *  MAKE USE OF ARRAYS.  ONLY REPEATED SCREEN FIELDS WILL BE               
      *  SPECIFICALLY DEFINED.  ANY CHANGES TO THIS MAP LAYOUT MUST             
      *  BE REFLECTED IN BOTH THIS REDEFINITION, AND THE ACTUAL MAP.            
      *                                                                         
                                                                                
       01  MD-MAP-DATA-AREA            REDEFINES DP007AI                        
                                       PIC X(1264).                             
       01  FILLER                      REDEFINES DP007AI.                       
           05  FILLER                  PIC X(124).                              
           05  MD-MENU-ITEM-INPUT-AREA OCCURS 15 TIMES                          
                                       INDEXED BY MD-ITEM-INDEX-I.              
               10  MD-AITEML           PIC S9(4)                                
                                       COMP.                                    
               10  MD-AITEMA           PIC X(1).                                
               10  FILLER              PIC X(2).                                
               10  MD-AITEMI           PIC X(3).                                
               10  MD-AMNEML           PIC S9(4)                                
                                       COMP.                                    
               10  MD-AMNEMA           PIC X(1).                                
               10  FILLER              PIC X(2).                                
               10  MD-AMNEMI           PIC X(8).                                
               10  MD-ADESCL           PIC S9(4)                                
                                       COMP.                                    
               10  MD-ADESCA           PIC X(1).                                
               10  FILLER              PIC X(2).                                
               10  MD-ADESCI           PIC X(30).                               
           05  FILLER                  PIC X(300).                              
       01  FILLER                      REDEFINES DP007AI.                       
           05  FILLER                  PIC X(124).                              
           05  MD-MENU-ITEM-OUTPUT-AREA                                         
                                       OCCURS 15 TIMES                          
                                       INDEXED BY MD-ITEM-INDEX-O.              
               10  FILLER              PIC X(3).                                
               10  MD-AITEMC           PIC X(1).                                
               10  MD-AITEMH           PIC X(1).                                
               10  MD-AITEMO           PIC ZZ9                                  
                                       BLANK WHEN ZERO.                         
               10  FILLER              PIC X(3).                                
               10  MD-AMNEMC           PIC X(1).                                
               10  MD-AMNEMH           PIC X(1).                                
               10  MD-AMNEMO           PIC X(8).                                
               10  FILLER              PIC X(3).                                
               10  MD-ADESCC           PIC X(1).                                
               10  MD-ADESCH           PIC X(1).                                
               10  MD-ADESCO           PIC X(30).                               
           05  FILLER                  PIC X(300).                              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MAP-SEND-TYPE        PIC X(1)     VALUE '1'.                  
               88  PS-SEND-MAP-N-DATA-FRSET         VALUE '1'.                  
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN   VALUE '2'.                  
               88  PS-SEND-DATA-ONLY-FRSET          VALUE '3'.                  
               88  PS-SEND-DATA-ONLY                VALUE '4'.                  
               88  PS-SEND-DATA-ONLY-ERASEAUP       VALUE '5'.                  
               88  PS-SEND-MAP-ONLY-FRSET           VALUE '6'.                  
           05  PS-CHECK-FOR-E-MAIL-IND PIC X(1)     VALUE 'N'.                  
               88  PS-CHECK-FOR-E-MAIL              VALUE 'Y'.                  
               88  PS-DONT-CHECK-FOR-E-MAIL         VALUE 'N'.                  
           05  PS-MORE-ITEMS-IND       PIC X(1)     VALUE 'N'.                  
               88  PS-MORE-ITEMS                    VALUE 'Y'.                  
               88  PS-NO-MORE-ITEMS                 VALUE 'N'.                  
           05  PS-USER-AUTHORIZATION-IND                                        
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-USER-IS-AUTHORIZED-FOR-ITEM   VALUE 'Y'.                  
               88  PS-USER-NOT-AUTHORIZD-FOR-ITEM   VALUE 'N'.                  
           05  PS-ITEM-AVAILABILITY-IND                                         
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-ITEM-IS-AVAILABLE             VALUE 'Y' 'R'.              
               88  PS-ITEM-IS-NOT-AVAILABLE         VALUE 'N'.                  
               88  PS-ITEM-IS-REMOTE                VALUE 'R'.                  
           05  PS-EDIT-ERROR-IND       PIC X(1)     VALUE 'N'.                  
               88  PS-EDIT-ERROR                    VALUE 'Y'.                  
               88  PS-NO-EDIT-ERROR                 VALUE 'N'.                  
           05  PS-MNEMONIC-RECORD-FOUND-IND                                     
                                       PIC X(1)     VALUE 'N'.                  
               88  PS-MNEMONIC-FOUND                VALUE 'Y'.                  
               88  PS-MNEMONIC-NOT-FOUND            VALUE 'N'.                  
           05  PS-START-WITH-COMMAREA-IND                                       
                                       PIC X(1)     VALUE 'Y'.                  
               88  PS-START-WITH-COMMAREA           VALUE 'Y'.                  
               88  PS-START-WITHOUT-COMMAREA        VALUE 'N'.                  
           05  PS-START-BROWSE-IND     PIC X(1)     VALUE 'N'.                  
               88  PS-SUCCESSFUL-START-BROWSE       VALUE 'Y'.                  
               88  PS-UNSUCCESSFUL-START-BROWSE     VALUE 'N'.                  
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)     VALUE 'DPKCS007'.           
           05  PC-CURRENT-TRAN-ID      PIC X(4)     VALUE 'Z007'.               
           05  PC-CURRENT-MAP-NAME     PIC X(8)     VALUE 'DP007A'.             
           05  PC-CURRENT-MAPSET-NAME  PIC X(8)     VALUE 'DPKM007'.            
           05  PC-OBJLOK-BASE-DD       PIC  X(08)   VALUE 'DPOBJLOK'.           
           05  PC-OBJLOK-AIX-TASK-DD   PIC  X(08)   VALUE 'DPOBJLK1'.           
      *    05  PC-SYSM-MESSAGE-PROGRAM-NAME                                     
      *                                PIC X(8)     VALUE 'SYSM0037'.           
           05  PC-E-MAIL-QUERY-PROGRAM-NAME                                     
                                       PIC X(8)     VALUE 'DPKCS025'.           
           05  PC-MENU-TRAN-ID         PIC X(4)     VALUE 'Z007'.               
           05  PC-NAVIGATOR-TRAN-ID    PIC X(4)     VALUE 'Z008'.               
           05  PC-CORP-MENU-MNEMONIC   PIC X(8)     VALUE 'CORPMENU'.           
           05  PC-MAXIMUM-COMMAREA-LENGTH                                       
                                       PIC S9(4)    VALUE +4072                 
                                       COMP SYNC.                               
           05  PC-GLOBAL-COMMAREA-LENGTH                                        
                                       PIC S9(4)    VALUE +1207                 
                                       COMP SYNC.                               
           05  PC-MESSAGE-AREA-LENGTH  PIC S9(4)    VALUE +79                   
                                       COMP SYNC.                               
           05  PC-NUMBER-OF-ITEMS-PER-PANEL                                     
                                       PIC S9(4)    VALUE +15                   
                                       COMP SYNC.                               
           05  PC-NUMBER-OF-ITEMS-PER-BLOCK                                     
                                       PIC S9(4)    VALUE +19                   
                                       COMP SYNC.                               
           05  PC-NMBR-OF-BLOCKS-IN-COMMAREA                                    
                                       PIC S9(4)    VALUE +2                    
                                       COMP SYNC.                               
           05  PC-NUMBER-OF-ENTRIES-IN-STACK                                    
                                       PIC S9(4)    VALUE +10                   
                                       COMP SYNC.                               
           05  PC-NUMBER-OF-AID-KEYS   PIC S9(4)    VALUE +29                   
                                       COMP SYNC.                               
           05  PC-ITEM-NOT-AVAILABLE-LITERAL                                    
                                       PIC X(3)     VALUE 'N/A'.                
           05  PC-ACRT-LOC-REQR-LITERAL                                         
                                       PIC X(3)     VALUE 'LOC'.                
           05  PC-SYSTEM-MESSAGE-NUMBERS.                                       
               10  PC-TSYMSG-00045     PIC  9(05)   VALUE 00045.                
               10  PC-TSYMSG-00069     PIC  9(05)   VALUE 00069.                
               10  PC-TSYMSG-00070     PIC  9(05)   VALUE 00070.                
               10  PC-TSYMSG-99903     PIC  9(05)   VALUE 99903.                
               10  PC-TSYMSG-99904     PIC  9(05)   VALUE 99904.                
               10  PC-TSYMSG-99906     PIC  9(05)   VALUE 99906.                
               10  PC-TSYMSG-99907     PIC  9(05)   VALUE 99907.                
               10  PC-TSYMSG-99909     PIC  9(05)   VALUE 99909.                
               10  PC-TSYMSG-99911     PIC  9(05)   VALUE 99911.                
               10  PC-TSYMSG-99912     PIC  9(05)   VALUE 99912.                
           05  PC-STACK-KEY-LENGTH      PIC S9(04)  VALUE +15                   
                                                    COMP.                       
           05  PC-EIBDATE-EIBTIME-7-LENGTH                                      
                                        PIC S9(04)  VALUE +15                   
                                                    COMP.                       
           05  PC-MAX-APPL-QUEUE-SEQ    PIC S9(04)  VALUE +5                    
                                                    COMP.                       
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE        PIC S9(9)    VALUE ZERO                  
                                       COMP SYNC.                               
      *    05  PV-TWA-LENGTH           PIC S9(4)    VALUE ZERO                  
      *                                COMP SYNC.                               
           05  PV-CICS-LENGTH          PIC S9(04)  VALUE +0                     
                                                   COMP SYNC.                   
           05  PV-CICS-KEYLENGTH       PIC S9(04)  VALUE +0                     
                                                   COMP SYNC.                   
           05  PV-ITEM-AT-TOP-OF-NEW-PANEL                                      
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-TENTATIVE-ITEM-AT-TOP                                         
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-LAST-ITEM-ON-NEW-PANEL                                        
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-LAST-ITEM-IN-COMMAREA                                         
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-SINGLE-MENU-ITEM-NUMBER                                       
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-BLOCK-NUMBER         PIC 9(4)     VALUE ZERO.                 
           05  PV-ITEM-NUMBER          PIC 9(4)     VALUE ZERO.                 
           05  PV-ITEM-POINTER         PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-ITEM-MNEMONIC        PIC X(8)     VALUE SPACES.               
           05  PV-SYSID-SPECIFIED-LITERAL                                       
                                       PIC X(10)    VALUE SPACES.               
               88  PV-SYSID-SPECIFIED               VALUE 'WITH SYSID'.         
           05  PV-SYSID                PIC X(4)     VALUE SPACES.               
           05  PV-NAVIGATOR-SYSID      PIC X(4)     VALUE SPACES.               
           05  PV-NAV-KEY-DATA         PIC X(25)    VALUE SPACES.               
           05  PV-CURRENT-TASK-NUMBER  PIC S9(7)    VALUE ZERO                  
                                       COMP-3.                                  
           05  PV-PRT-TASK-NBR         PIC  9(07)   VALUE ZERO.                 
           05  PV-NUMBER-OF-ITEMS-IN-BUFFER                                     
                                       PIC S9(4)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-TRANSACTION-STATUS-CODE                                       
                                       PIC S9(9)    VALUE ZERO                  
                                       COMP SYNC.                               
           05  PV-REMOTE-SYSTEM         PIC  X(04)  VALUE SPACE.                
           05  PV-REMOTE-STATUS         PIC S9(09)  COMP SYNC                   
                                                    VALUE ZERO.                 
               88  PV-REMOTE-ACQUIRED               VALUE +69.                  
               88  PV-REMOTE-RELEASED               VALUE +70.                  
               88  PV-REMOTE-FREEING                VALUE +94.                  
               88  PV-REMOTE-AVAILABLE              VALUE +95.                  
               88  PV-REMOTE-OBTAINING              VALUE +96.                  
           05  PV-PRIOR-SYSTEM          PIC  X(04)  VALUE SPACE.                
           05  PV-PRIOR-AVAIL           PIC  X(01)  VALUE SPACE.                
           05  PV-DELETEQ-TRAN-ID       PIC  X(04)  VALUE SPACE.                
           05  PV-ACT-DPSAVCMK-LENGTH   PIC S9(04)  VALUE ZERO                  
                                                    COMP.                       
           05  PV-UNPACK-NUMERIC        PIC 9(4)    VALUE ZERO.                 
           05  PV-DISPLAY-LINE-2.                                               
               10  FILLER.                                                      
                   15  FILLER           PIC  X(04)  VALUE 'KEY='.               
                   15  PV-DISPLAY-KEY   PIC  X(15)  VALUE SPACE.                
                   15  FILLER           PIC  X(01)  VALUE ','.                  
               10  FILLER.                                                      
                   15  PV-DISPLAY-FIELD2-HDR                                    
                                        PIC  X(07)  VALUE SPACE.                
                   15  PV-DISPLAY-FIELD2                                        
                                        PIC  X(04)  VALUE SPACE.                
                   15  FILLER           PIC  X(01)  VALUE ','.                  
               10  FILLER.                                                      
                   15  PV-DISPLAY-FIELD3-HDR                                    
                                        PIC  X(07)  VALUE SPACE.                
                   15  PV-DISPLAY-FIELD3                                        
                                        PIC  X(04)  VALUE SPACE.                
      *                                                                         
                                                                                
       01  BB-BLOCK-BUFFER.                                                     
           05  BB-ITEM-ARRAY           OCCURS 19 TIMES                          
                                       INDEXED BY BB-ITEM-INDEX.                
               10  BB-MENU-ITEM.                                                
                   15  BB-MENU-ITEM-NUMBER                                      
                                       PIC S9(4)                                
                                       COMP.                                    
                   15  BB-MENU-ITEM-MNEMONIC                                    
                                       PIC X(8).                                
                   15  BB-MENU-ITEM-DESCRIPTION                                 
                                       PIC X(30).                               
                   15  BB-MENU-ITEM-TRAN-ID                                     
                                       PIC X(4).                                
                   15  BB-MENU-ITEM-AVAILABILITY-IND                            
                                       PIC X(1).                                
                       88  BB-MENU-ITEM-IS-AVAILABLE                            
                                                    VALUE 'Y' 'R'.              
                       88  BB-MENU-ITEM-IS-NOT-AVAILABLE                        
                                                    VALUE 'N'.                  
                       88  BB-MENU-ITEM-IS-REMOTE                               
                                                    VALUE 'R' 'U'.              
                       88  BB-MENU-ITEM-IS-UNCONNECTED                          
                                                    VALUE 'U'.                  
                   15  BB-MENU-ITEM-COMMAREA-FORMAT                             
                                       PIC X(1).                                
                       88  BB-MENU-ITEM-COMMAREA-STD                            
                                                    VALUE 'K'.                  
                       88  BB-MENU-ITEM-COMMAREA-NOT-STD                        
                                                    VALUE 'N'.                  
                   15  BB-INT-APPL-FORMAT-IND                                   
                                       PIC X(1).                                
                   15  BB-ACRT-LOC-REQR-IND                                     
                                       PIC X(1).                                
                       88  BB-ACRT-LOC-REQR         VALUE 'Y'.                  
                       88  BB-ACRT-LOC-NOT-REQR     VALUE 'N' ' '.              
                                                                                
       01  SI-SELECTED-ITEM-DETAIL.                                             
           05  SI-SELECTED-ITEM-NUMBER PIC 9(3)     VALUE ZERO.                 
           05  SI-SEL-ITEM-AVAILABILITY-IND                                     
                                       PIC X(1)     VALUE 'N'.                  
               88  SI-SEL-ITEM-IS-AVAILABLE         VALUE 'Y'.                  
               88  SI-SEL-ITEM-IS-REMOTE            VALUE 'R'.                  
               88  SI-SEL-ITEM-IS-NOT-AVAILABLE     VALUE 'N' 'U'.              
           05  SI-SEL-ITEM-TRAN-ID     PIC X(4)     VALUE SPACES.               
           05  SI-SEL-ITEM-MNEMONIC    PIC X(8)     VALUE SPACES.               
           05  SI-SEL-ITEM-COMMAREA-FORMAT                                      
                                       PIC X(1)     VALUE SPACES.               
               88  SI-COMMAREA-STANDARD             VALUE 'K'.                  
               88  SI-COMMAREA-NOT-STANDARD         VALUE 'N'.                  
           05  SI-SEL-ITEM-INT-APPL-FORMAT                                      
                                       PIC X(1)     VALUE SPACES.               
           05  SI-ACRT-LOC-REQR-IND    PIC X(1)     VALUE SPACES.               
               88  SI-ACRT-LOC-REQR                 VALUE 'Y'.                  
               88  SI-ACRT-LOC-NOT-REQR             VALUE 'N' ' '.              
                                                                                
       01  SM-STACK-MANIPULATION-FIELDS.                                        
           05  SM-STACK-MAX             PIC S9(04)  VALUE +10                   
                                                    COMP SYNC.                  
           05  SM-AID-MAX               PIC S9(04)  VALUE +29                   
                                                    COMP SYNC.                  
           05  SM-FK-SUB                PIC S9(04)  VALUE ZERO                  
                                                    COMP SYNC.                  
           05  SM-STACK-KEY.                                                    
               10  SM-STACK-KEY-NETNAME.                                        
                   15 SM-STACK-KEY-LINE PIC  X(02)  VALUE SPACE.                
                   15 SM-STACK-KEY-UNIT PIC  X(02)  VALUE SPACE.                
                   15 SM-STACK-KEY-TERM PIC  X(04)  VALUE SPACE.                
               10  SM-STACK-KEY-TASK    PIC  9(07)  VALUE ZERO.                 
      *                                                                         
      *01  TS-TEMP-STORAGE-QUEUE-INFO.                                          
      *    05  TS-SCA-QUEUE-INFO.                                               
      *        10  TS-SCA-QUEUE-NAME.                                           
      *            15  TS-SCA-QUEUE-TERMINAL                                    
      *                                PIC X(4)     VALUE SPACES.               
      *            15  TS-SCA-QUEUE-LITERAL                                     
      *                                PIC X(4)     VALUE 'SCA '.               
      *        10  TS-SCA-QUEUE-ITEM-NUMBER                                     
      *                                PIC S9(4)    VALUE ZERO                  
      *                                COMP SYNC.                               
      *    05  TS-APPL-QUEUE-INFO.                                              
      *        10  TS-APPL-QUEUE-NAME.                                          
      *            15  TS-APPL-QUEUE-TERMINAL                                   
      *                                PIC X(4)     VALUE SPACES.               
      *            15  TS-APPL-QUEUE-TASK                                       
      *                                PIC S9(7)    VALUE ZERO                  
      *                                COMP-3.                                  
      *        10  TS-APPL-QUEUE-LENGTH                                         
      *                                PIC S9(4)    VALUE +893                  
      *                                COMP SYNC.                               
      *        10  TS-APPL-QUEUE-ITEM-NUMBER                                    
      *                                PIC S9(4)    VALUE ZERO                  
      *                                COMP SYNC.                               
      *                                                                         
       01  TS-TEMP-STORAGE-QUEUE-INFO.                                          
           05  TS-SCA-QUEUE-INFO.                                               
               10  TS-SCA-QUEUE-NAME.                                           
                   15  TS-SCA-QUEUE-TERMINAL                                    
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-SCA-QUEUE-LITERAL                                     
                                        PIC  X(04)  VALUE 'SCA '.               
               10  TS-SCA-QUEUE-ITEM-NUMBER                                     
                                        PIC S9(04)  VALUE ZERO                  
                                        COMP SYNC.                              
           05  TS-APPL-QUEUE-INFO.                                              
               10  TS-APPL-QUEUE-NAME.                                          
                   15  TS-APPL-QUEUE-TERMINAL                                   
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-APPL-QUEUE-TASK                                       
                                        PIC S9(07)  VALUE ZERO                  
                                        COMP-3.                                 
               10  TS-APPL-QUEUE-LENGTH                                         
                                        PIC S9(4)    VALUE +893                 
                                        COMP SYNC.                              
               10  TS-APPL-QUEUE-ITEM-NUMBER                                    
                                        PIC S9(4)    VALUE ZERO                 
                                        COMP SYNC.                              
               10  TS-APPL-MULT-QUEUE-ID.                                       
                   15  TS-APPL-MULT-QUEUE-TERM                                  
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-APPL-MULT-QUEUE-TASK                                  
                                        PIC S9(05)  VALUE ZERO                  
                                        COMP-3.                                 
                   15  TS-APPL-MULT-QUEUE-SEQ                                   
                                        PIC  9(01)  VALUE ZERO.                 
      *                                                                         
       01  FD-FILE-DDNAME-AREA.                                                 
           05  FD-MNEMONIC-FILE        PIC X(8)     VALUE 'DPMNEMIC'.           
      *    05  FD-SAVED-COMMAREA-FILE  PIC X(8)     VALUE 'DPSAVCOM'.           
           05  FD-SAVED-COMMAREA-FILE  PIC X(8)     VALUE 'DPSAVCMK'.           
           05  FD-MENU-ITEM-FILE       PIC X(8)     VALUE 'DPMNUITM'.           
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       LINKAGE SECTION.                                                         
      ******************************************************************        
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
      *                                                                         
      *  TRANSACTION WORK AREA REQUIRED TO ACCESS SYSM0037.  THIS MODULE        
      *  HAS BEEN REPLACED BY DPKCS025, WHICH SERVES THE SAME BASIC             
      *  FUNCTION, BUT WORKS WITHIN AN MRO ENVIRONMENT.  ALL SYSM0037           
      *  PROCEDURES HAVE BEEN LEFT IN THE PROGRAM, BUT COMMENTED OUT,           
      *  FOR FUTURE REFERENCE IN CASE WE NEED TO MAKE USE OF IT.                
      *                                                                         
                                                                                
      *01  ST-SYSM-TWA.                                                         
      *    05  ST-RESERVED             PIC X(2).                                
      *    05  ST-TWADRETN-RETURN-CODE PIC S9(4)                                
      *                                COMP.                                    
      *        88  ST-TWADRETN-NORMAL               VALUE ZERO.                 
      *        88  ST-TWADRETN-INVALID-USERID       VALUE +4.                   
      *        88  ST-TWADRETN-FILE-I-O-ERROR       VALUE +8.                   
      *    05  ST-TWADFPGM-REQ-PROGRAM-NAME                                     
      *                                PIC X(8).                                
      *    05  ST-TWADMOID-SYSM-USER-ID                                         
      *                                PIC X(11).                               
      *    05  ST-TWADTCNT-UNREAD-MSG-COUNT                                     
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-TWADHCNT-HI-PRI-U-MSG-COUNT                                   
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-TWADMG29-RESPONSE-MESSAGE                                     
      *                                PIC X(29).                               
      *    05  ST-TWADDFCT-DEFERRED-MSG-COUNT                                   
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-TWADECNT-CALNDR-EVENT-COUNT                                   
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-TWADPCNT-PRIV-U-D-MSG-COUNT                                   
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-TWADTMCT-TOTAL-MSG-COUNT                                      
      *                                PIC S9(5)                                
      *                                COMP-3.                                  
      *    05  ST-FILLER               PIC X(58).                               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *                                                                         
      *  A SPECIFICATION OF "RETURN WITHOUT TRANID", AT THIS POINT,             
      *  INDICATES THAT EITHER NO COMMAREA WAS FOUND, OR THAT A NON-            
      *  STANDARD COMMAREA WAS FOUND.  EITHER SITUATION LEADS TO A              
      *  START OF THE NAVIGATOR, LEAVING THIS TRANSACTION TO FALL               
      *  THROUGH ALL REMAINING PROCESSING, UNTIL IT CAN TERMINATE THE           
      *  TASK BY ISSUING A RETURN TO NATIVE CICS.                               
      *                                                                         
                                                                                
           IF DP020-SRC-RETURN-W-TRAN-ID                                        
               PERFORM 1000-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  FOLLOW THE PROCESSING PATH REQUIRED TO CARRY OUT THE "NEXT    *        
      *  ACTIVITY" INDICATED IN THE COMMAREA.                          *        
      ******************************************************************        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   MOVE EIBCPOSN TO DP020-APPL-CURSOR-POSITION                  
                   SET PS-SEND-DATA-ONLY-FRSET TO TRUE                          
                   PERFORM 2000-PROCESS-MENU                                    
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   MOVE EIBTASKN TO DP020-SRC-TASK-NUMBER                       
                   MOVE DP020-NAV-KEY-DATA TO PV-NAV-KEY-DATA                   
                   INITIALIZE DP020-NAV-KEY-DATA                                
                              DP020-APPL-CURSOR-POSITION                        
                              CA-COMMAREA                                       
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   PERFORM 3000-DISPLAY-MENU                                    
               WHEN DP020-NEXT-ACT-RETURN                                       
                   INITIALIZE DP020-NAV-KEY-DATA                                
                   IF DP020-SRC-DONT-SAVE-COMMAREA                              
                       INITIALIZE CA-COMMAREA                                   
                                  DP020-APPL-CURSOR-POSITION                    
                   ELSE                                                         
                       INITIALIZE CA-SELECTION-NUMBER                           
                                  DP020-APPL-CURSOR-POSITION                    
                   END-IF                                                       
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   PERFORM 3000-DISPLAY-MENU                                    
               WHEN DP020-NEXT-ACT-ERROR                                        
                   INITIALIZE DP020-APPL-CURSOR-POSITION                        
                   SET PS-SEND-DATA-ONLY-FRSET TO TRUE                          
                   IF DP020-NAV-MNEMONIC NOT = SPACES                           
                       MOVE DP015-RED TO ANAVCMDC                               
                       MOVE DP015-REVERSE TO ANAVCMDH                           
                       MOVE DP015-UNP-ALP-BRT-OFF TO ANAVCMDA                   
                   ELSE                                                         
                       INITIALIZE ANAVCMDI                                      
                       MOVE DP015-GREEN TO ANAVCMDC                             
                       MOVE DP015-UNDERLINE TO ANAVCMDH                         
                       MOVE DP015-UNP-ALP-NOR-OFF TO ANAVCMDA                   
                   END-IF                                                       
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                        
                   PERFORM DP002-0000-FORMAT-TITLE-LINES                        
                   PERFORM 3320-FORMAT-MESSAGE-LINE                             
                   PERFORM DP008-0000-WRITE-PANEL                               
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   MOVE '0000-MAINLINE' TO DP013-PARAGRAPH                      
                   MOVE 'ATTEMPTING TO EVALUATE THE NEXT APPLICATION ACT        
      -                      'IVITY' TO DP013-MESSAGE-TEXT (1)                  
                   STRING 'AN INVALID CODE OF "'      DELIMITED BY SIZE         
                          DP020-NEXT-APPL-ACTIVITY    DELIMITED BY SIZE         
                          '" WAS FOUND.'              DELIMITED BY SIZE         
                       INTO DP013-MESSAGE-TEXT (2)                              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
           SET DP020-NEXT-ACT-READ-MAP TO TRUE.                                 
                                                                                
      ******************************************************************        
      *  LOGIC NECESSARY TO READ MAP AND TAKE ACTION BASED ON          *        
      *  AID KEY PRESSED AND ITEN NUMBER SELECTED.                     *        
      ******************************************************************        
                                                                                
       2000-PROCESS-MENU.                                                       
           PERFORM DP007-0000-GET-PANEL.                                        
                                                                                
           IF ASELL > ZERO                                                      
               MOVE ASELI TO CA-SELECTION-NUMBER                                
           ELSE                                                                 
               IF ASELA = DP015-ERASE-EOF                                       
                   INITIALIZE CA-SELECTION-NUMBER                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *                                                                         
      *  RESET ATTRIB.  ERROR MESSAGE WILL HAVE BEEN DELETED, NO                
      *  OUTSTANDING ERRORS CAN REMAIN HIGHLIGHTED.  IF PF KEY                  
      *  W/SELECTION, PF FIRST.  THEN ENTER, THEN EDIT AND ACT ON IT.           
      *  IF TO BE TOTALLY CONSISTENT, I WOULD HAVE TO EDIT EACH TIME            
      *  (THROUGH AVAILABILITY).                                                
      *                                                                         
                                                                                
           IF DP020-SRC-AID NOT = DP016-PA2                                     
               MOVE DP015-GREEN TO ASELC                                        
               MOVE DP015-UNDERLINE TO ASELH                                    
               MOVE DP015-UNP-ALP-NOR-OFF TO ASELA                              
           END-IF.                                                              
                                                                                
           EVALUATE TRUE  ALSO  DP020-SRC-AID                                   
               WHEN DP020-FK-LOCAL-KEY (DP020-SRC-AID)   ALSO                   
                                                            DP016-ENTER         
                   IF DP020-NAV-MNEMONIC NOT = SPACES                           
                       PERFORM 9000-START-NAVIGATOR                             
                   ELSE                                                         
                       MOVE CA-ITEM-AT-TOP-OF-OLD-PANEL TO                      
                                 PV-ITEM-AT-TOP-OF-NEW-PANEL                    
                       PERFORM 2100-EDIT-MENU                                   
                       IF PS-NO-EDIT-ERROR                                      
                           PERFORM 4200-FIND-SELECTION-TEST-AVAIL               
                           IF PS-NO-EDIT-ERROR                                  
                               PERFORM 4300-START-SELECTED-ITEM                 
                           ELSE                                                 
                               PERFORM 3300-DISPLAY-MENU-PANEL                  
                           END-IF                                               
                       ELSE                                                     
                           PERFORM 3300-DISPLAY-MENU-PANEL                      
                       END-IF                                                   
                   END-IF                                                       
               WHEN DP020-FK-LOCAL-KEY (DP020-SRC-AID)   ALSO                   
                                                            DP016-CLEAR         
                   MOVE CA-SELECTION-NUMBER TO ASELO                            
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   MOVE CA-ITEM-AT-TOP-OF-OLD-PANEL TO                          
                             PV-ITEM-AT-TOP-OF-NEW-PANEL                        
                   PERFORM 3300-DISPLAY-MENU-PANEL                              
               WHEN DP020-FK-LOCAL-KEY (DP020-SRC-AID)   ALSO                   
                                                            DP016-PA2           
                   SET PS-SEND-DATA-ONLY TO TRUE                                
                   MOVE -1 TO ASELL                                             
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                        
                   PERFORM DP002-0000-FORMAT-TITLE-LINES                        
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP008-0000-WRITE-PANEL                               
      ****     WHEN DP020-FK-LOCAL-KEY (DP020-SRC-AID)   ALSO                   
      ****                                                  DP016-PF13          
      ****         SET PS-CHECK-FOR-E-MAIL TO TRUE                              
      ****         MOVE CA-ITEM-AT-TOP-OF-OLD-PANEL TO                          
      ****                   PV-ITEM-AT-TOP-OF-NEW-PANEL                        
      ****         PERFORM 3300-DISPLAY-MENU-PANEL                              
               WHEN DP020-FK-LOCAL-KEY (DP020-SRC-AID)   ALSO  ANY              
      *            -- LOCAL KEY IS NOT HANDLED BY THE PROGRAM --                
                   SET PS-EDIT-ERROR                                            
                       DP020-MSG-FATAL TO TRUE                                  
                   MOVE PC-TSYMSG-00045 TO DP020-MSG-NUMBER                     
                   MOVE -1 TO ASELL                                             
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                        
                   PERFORM DP002-0000-FORMAT-TITLE-LINES                        
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM 3320-FORMAT-MESSAGE-LINE                             
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)     ALSO  ANY              
                   INITIALIZE CA-COMMAREA                                       
                              ASELO                                             
                              DP020-NAV-MNEMONIC                                
                              ANAVCMDO                                          
                              DP020-NAV-KEY-DATA                                
                              ANAVKEYO                                          
                              DP020-APPL-CURSOR-POSITION                        
                   PERFORM 3000-DISPLAY-MENU                                    
               WHEN DP020-FK-FIRST-PAGE (DP020-SRC-AID)  ALSO  ANY              
                   MOVE 1 TO PV-ITEM-AT-TOP-OF-NEW-PANEL                        
                   PERFORM 4400-ENSURE-ITEMS-ARE-IN-CA                          
                   PERFORM 3300-DISPLAY-MENU-PANEL                              
               WHEN DP020-FK-PREV-PAGE (DP020-SRC-AID)   ALSO  ANY              
                   COMPUTE PV-TENTATIVE-ITEM-AT-TOP =                           
                             (CA-ITEM-AT-TOP-OF-OLD-PANEL -                     
                             PC-NUMBER-OF-ITEMS-PER-PANEL)                      
                   IF PV-TENTATIVE-ITEM-AT-TOP < 1                              
      *                -- LIST ALREADY AT TOP --                                
                       MOVE 1 TO PV-ITEM-AT-TOP-OF-NEW-PANEL                    
                       MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                 
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   ELSE                                                         
                       MOVE PV-TENTATIVE-ITEM-AT-TOP TO                         
                                 PV-ITEM-AT-TOP-OF-NEW-PANEL                    
                   END-IF                                                       
                   PERFORM 4400-ENSURE-ITEMS-ARE-IN-CA                          
                   PERFORM 3300-DISPLAY-MENU-PANEL                              
               WHEN DP020-FK-NEXT-PAGE (DP020-SRC-AID)   ALSO  ANY              
                   COMPUTE PV-TENTATIVE-ITEM-AT-TOP =                           
                             (CA-ITEM-AT-TOP-OF-OLD-PANEL +                     
                             PC-NUMBER-OF-ITEMS-PER-PANEL)                      
                   IF PV-TENTATIVE-ITEM-AT-TOP > CA-NMBR-OF-MENU-ITEMS          
      *                -- LIST ALREADY AT BOTTOM --                             
                       MOVE CA-ITEM-AT-TOP-OF-OLD-PANEL TO                      
                                 PV-ITEM-AT-TOP-OF-NEW-PANEL                    
                       MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                 
                       SET DP020-MSG-INFORMATIONAL TO TRUE                      
                   ELSE                                                         
                       MOVE PV-TENTATIVE-ITEM-AT-TOP TO                         
                                 PV-ITEM-AT-TOP-OF-NEW-PANEL                    
                   END-IF                                                       
                   PERFORM 4400-ENSURE-ITEMS-ARE-IN-CA                          
                   PERFORM 3300-DISPLAY-MENU-PANEL                              
               WHEN OTHER                                                       
                   PERFORM 9000-START-NAVIGATOR                                 
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  EDITS FOR THE ITEM NUMBER SELECTED FIELD ON THE SCREEN        *        
      ******************************************************************        
                                                                                
       2100-EDIT-MENU.                                                          
           IF CA-SELECTION-NUMBER = SPACES                                      
      *        -- ENTER A SELECTION NUMBER --                                   
               SET PS-EDIT-ERROR                                                
                   DP020-MSG-INFORMATIONAL TO TRUE                              
               MOVE PC-TSYMSG-99906 TO DP020-MSG-NUMBER                         
               MOVE -1 TO ASELL                                                 
           ELSE                                                                 
               MOVE CA-SELECTION-NUMBER TO DP010I-UNEDITED-FIELD                
               MOVE 3 TO DP010I-MAXIMUM-DIGITS                                  
               MOVE 0 TO DP010I-MAXIMUM-DECIMALS                                
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
               IF ((DP010I-ERROR-DETECTED)                                      
                       OR (DP010I-NUMERIC-FIELD < 1)                            
                       OR (DP010I-NUMERIC-FIELD >                               
                                              CA-NMBR-OF-MENU-ITEMS))           
      *            -- INVALID SELECTION NUMBER ENTERED --                       
                   SET PS-EDIT-ERROR                                            
                       DP020-MSG-FATAL        TO TRUE                           
                   MOVE PC-TSYMSG-99907       TO DP020-MSG-NUMBER               
                   MOVE DP015-RED             TO ASELC                          
                   MOVE DP015-REVERSE         TO ASELH                          
                   MOVE DP015-UNP-ALP-BRT-OFF TO ASELA                          
                   MOVE -1                    TO ASELL                          
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD TO SI-SELECTED-ITEM-NUMBER         
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC NECESSARY TO DISPLAY THE 1ST PAGE OF THE MENU. ALL      *        
      *  ITEMS ASSOCIATED WIT THE REQUESTED MENU ARE RETRIEVED FROM    *        
      *  THE DPMNUITM VSAM FILE.  EACH ITEM IS THEN CHECKED TO SEE     *        
      *  IF THE USER HAS SECURITY ACCESS TO IT. IF NOT, IT IS EXCLUDED *        
      *  FROM THE LIST OF ITEMS TO BE DISPLAYED ON THE MENU.           *        
      ******************************************************************        
                                                                                
       3000-DISPLAY-MENU.                                                       
           PERFORM 3100-CHECK-SECURITY-FOR-MENU.                                
           PERFORM 3200-GATHER-MENU-ITEMS.                                      
                                                                                
           IF ((DP020-NEXT-ACT-INITIAL)                                         
                   AND (CA-NMBR-OF-AVAIL-MENU-ITEMS = 1))                       
               MOVE PV-SINGLE-MENU-ITEM-NUMBER TO                               
                         SI-SELECTED-ITEM-NUMBER                                
               MOVE 1 TO CA-ITEM-AT-TOP-OF-OLD-PANEL                            
               PERFORM 4200-FIND-SELECTION-TEST-AVAIL                           
               IF PS-NO-EDIT-ERROR                                              
                   IF SI-COMMAREA-STANDARD                                      
                       MOVE PV-NAV-KEY-DATA TO DP020-NAV-KEY-DATA               
                   END-IF                                                       
                   PERFORM 4300-START-SELECTED-ITEM                             
               END-IF                                                           
           ELSE                                                                 
               PERFORM 3300-DISPLAY-MENU-PANEL                                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * LOGIC NECESSARY TO VALIDATE THAT THE USER HAS SECURITY ACCESS  *        
      * TO THE MENU REQUESTED TO BE DISPLAYED. IF USER HAS INSUFFICIENT*        
      * CLEARANCE FOR THE MENU, THIS PARAGRAPH RESETS THE SOURCE TRAN  *        
      * VARIABLES SO THAT SUBSEQUENT ROUTINES WILL RETURN TO THE       *        
      * SOURCE TRANSACTION WITH AN ERROR MESSAGE.                      *        
      ******************************************************************        
                                                                                
       3100-CHECK-SECURITY-FOR-MENU.                                            
           IF ((DP020-NEXT-ACT-RETURN)                                          
                   AND (DP020-SRC-SAVE-COMMAREA))                               
               MOVE DP020-SRC-MNEMONIC TO CA-MENU-BEING-DISPLAYED               
           ELSE                                                                 
               IF DP020-SRC-TRAN-ID = PC-MENU-TRAN-ID                           
                   SET DP008-RSRC-TYPE-MENU TO TRUE                             
                   MOVE DP020-SRC-MNEMONIC TO DP008-RESOURCE-NAME               
               ELSE                                                             
                   SET DP008-RSRC-TYPE-TRAN TO TRUE                             
                   MOVE DP020-SRC-TRAN-ID TO DP008-RESOURCE-NAME                
               END-IF                                                           
               SET DP008-ACCESS-TYPE-USE TO TRUE                                
               PERFORM 9200-LINK-TO-SECURITY-MODULE                             
               IF DP008-CALL-SUCCESSFUL                                         
                   IF DP008-ACCESS-ALLOWED                                      
                       MOVE DP020-SRC-MNEMONIC TO                               
                                 CA-MENU-BEING-DISPLAYED                        
                   ELSE                                                         
      *                -- NO SECURITY CLEARANCE FOR MNEMONIC --                 
                       SET DP020-MSG-FATAL TO TRUE                              
                       MOVE PC-TSYMSG-99903 TO DP020-MSG-NUMBER                 
                       MOVE PC-CORP-MENU-MNEMONIC TO                            
                                 CA-MENU-BEING-DISPLAYED                        
                                 DP020-SRC-MNEMONIC                             
                                 SI-SEL-ITEM-MNEMONIC                           
                       PERFORM 6200-RESET-SOURCE-VARIABLES                      
                       INITIALIZE SI-SEL-ITEM-MNEMONIC                          
                       PERFORM 6300-FORMAT-COMMAREA                             
                       MOVE EIBTASKN TO DP020-SRC-TASK-NUMBER                   
                   END-IF                                                       
               ELSE                                                             
                   SET DP013-SECURITY-ABEND TO TRUE                             
                   MOVE '3100-CHECK-SECURITY-FOR-MENU' TO                       
                             DP013-PARAGRAPH                                    
                   MOVE DP008-SECURITY-COMMAREA TO DP013-SECURITY-INFO          
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  RETRIEVES ALL RECORDS FROM THE VSAM FILE ASSOCIATED WITH THE  *        
      *  REQUESTED MENU AND STORES THEM IN THE COMMAREA ARRAY. WHEN    *        
      *  THE COMMAREA ARRAY IS FULL IT WRITES OUT A TEMP STROAGE QUEUE.*        
      ******************************************************************        
                                                                                
       3200-GATHER-MENU-ITEMS.                                                  
           IF ((DP020-NEXT-ACT-RETURN)                                          
                   AND (DP020-SRC-SAVE-COMMAREA)                                
                   AND (CA-NMBR-OF-BLKS-WRITTEN-TO-TS = ZERO))                  
               MOVE CA-ITEM-AT-TOP-OF-OLD-PANEL TO                              
                         PV-ITEM-AT-TOP-OF-NEW-PANEL                            
           ELSE                                                                 
               INITIALIZE CA-NMBR-OF-MENU-ITEMS                                 
                          CA-NMBR-OF-BLKS-WRITTEN-TO-TS                         
               MOVE EIBTRMID TO TS-APPL-QUEUE-TERMINAL                          
               MOVE DP020-SRC-TASK-NUMBER TO TS-APPL-QUEUE-TASK                 
               INITIALIZE PV-SYSID                                              
               PERFORM 8100-DELETE-APPL-TS-QUEUE                                
               MOVE CA-MENU-BEING-DISPLAYED TO DP504-MENU-NAME                  
               INITIALIZE DP504-MENU-ITEM-ORDER-NUMBER                          
               PERFORM 7300-START-MENU-ITEM-FILE                                
               PERFORM 7400-READ-NEXT-MENU-ITEM                                 
               PERFORM                                                          
                       UNTIL PS-NO-MORE-ITEMS                                   
                   PERFORM 3210-CHECK-MENU-ITEM-SECURITY                        
                   IF PS-USER-IS-AUTHORIZED-FOR-ITEM                            
                       ADD 1 TO CA-NMBR-OF-MENU-ITEMS                           
                       PERFORM 3220-CHECK-MENU-ITEM-AVAIL                       
                       IF PS-ITEM-IS-AVAILABLE                                  
                           ADD 1 TO CA-NMBR-OF-AVAIL-MENU-ITEMS                 
                           MOVE CA-NMBR-OF-MENU-ITEMS TO                        
                                     PV-SINGLE-MENU-ITEM-NUMBER                 
                       END-IF                                                   
                       PERFORM 3230-STORE-MENU-ITEM                             
                   END-IF                                                       
                   PERFORM 7400-READ-NEXT-MENU-ITEM                             
               END-PERFORM                                                      
               IF PS-SUCCESSFUL-START-BROWSE                                    
                   PERFORM 7500-END-MENU-ITEM-FILE                              
               END-IF                                                           
               IF CA-NMBR-OF-BLKS-WRITTEN-TO-TS > ZERO                          
                   PERFORM 8400-WRITE-BUFFER-TO-TS-QUEUE                        
               END-IF                                                           
               MOVE 1 TO PV-ITEM-AT-TOP-OF-NEW-PANEL                            
           END-IF.                                                              
                                                                                
           IF ((CA-NMBR-OF-MENU-ITEMS  = ZERO)                                  
                   AND ((DP020-MSG-NUMBER-X =  SPACES)                          
                    OR (DP020-MSG-NUMBER-X = '00000')))                         
      *        -- NO ITEMS WITH SECURITY CLEARANCE --                           
               SET DP020-MSG-WARNING TO TRUE                                    
               MOVE PC-TSYMSG-99909 TO DP020-MSG-NUMBER                         
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS TO SEE IF USER HAS ACCESS TO THIS MENU ITEM            *        
      ******************************************************************        
                                                                                
       3210-CHECK-MENU-ITEM-SECURITY.                                           
           IF DP504-MENU-ITEM-TRAN-ID = PC-MENU-TRAN-ID                         
               SET DP008-RSRC-TYPE-MENU TO TRUE                                 
               MOVE DP504-MENU-ITEM-MNEMONIC TO DP008-RESOURCE-NAME             
           ELSE                                                                 
               SET DP008-RSRC-TYPE-TRAN TO TRUE                                 
               MOVE DP504-MENU-ITEM-TRAN-ID TO DP008-RESOURCE-NAME              
           END-IF.                                                              
                                                                                
           SET DP008-ACCESS-TYPE-USE TO TRUE.                                   
                                                                                
           PERFORM 9200-LINK-TO-SECURITY-MODULE.                                
                                                                                
           IF DP008-CALL-SUCCESSFUL                                             
               IF DP008-ACCESS-ALLOWED                                          
                   SET PS-USER-IS-AUTHORIZED-FOR-ITEM TO TRUE                   
               ELSE                                                             
                   SET PS-USER-NOT-AUTHORIZD-FOR-ITEM TO TRUE                   
               END-IF                                                           
           ELSE                                                                 
               SET DP013-SECURITY-ABEND                                         
                   DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               MOVE '3210-CHECK-MENU-ITEM-SECURITY' TO DP013-PARAGRAPH          
               MOVE DP008-SECURITY-COMMAREA TO DP013-SECURITY-INFO              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS TO SEE IF THIS ITEM IS AVAILABLE. AVAILABILITY IS A    *        
      *  TWO STEP STATE. AN AVAILABILITY INDICATOR EXISTS ON THE       *        
      *  DPMNEMIC VSAM FILE. IF IT IS AVAILABLE BASED ON THIS INDICATOR*        
      *  THIS PARAGRAPH ALSO CHECKS THE TRANSACTION STATUS IN CICS.    *        
      ******************************************************************        
                                                                                
       3220-CHECK-MENU-ITEM-AVAIL.                                              
                                                                                
           MOVE DP504-MENU-ITEM-AVAIL-IND TO PS-ITEM-AVAILABILITY-IND.          
                                                                                
           IF ((DP504-MENU-ITEM-TRAN-ID NOT = PC-MENU-TRAN-ID)                  
                   AND (PS-ITEM-IS-AVAILABLE))                                  
               EXEC CICS                                                        
                   INQUIRE                                                      
                       TRANSACTION  (DP504-MENU-ITEM-TRAN-ID)                   
                       STATUS       (PV-TRANSACTION-STATUS-CODE)                
                       REMOTESYSTEM (PV-REMOTE-SYSTEM)                          
                       RESP         (PV-CICS-RESPONSE)                          
               END-EXEC                                                         
               IF ((PV-CICS-RESPONSE NOT = DFHRESP(NORMAL))                     
                       OR (PV-TRANSACTION-STATUS-CODE NOT =                     
                                                    DFHVALUE(ENABLED)))         
                   SET PS-ITEM-IS-NOT-AVAILABLE TO TRUE                         
               ELSE                                                             
                   IF PV-REMOTE-SYSTEM NOT = SPACES                             
                       SET PS-ITEM-IS-REMOTE TO TRUE                            
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  MOVES THE ITEM TO THE COMMAREA ARRAY                          *        
      ******************************************************************        
                                                                                
       3230-STORE-MENU-ITEM.                                                    
           IF CA-NMBR-OF-MENU-ITEMS > (PC-NUMBER-OF-ITEMS-PER-BLOCK *           
                                         PC-NMBR-OF-BLOCKS-IN-COMMAREA)         
               PERFORM 3231-STORE-ITEM-IN-TS                                    
           ELSE                                                                 
               COMPUTE PV-BLOCK-NUMBER =                                        
                         (1 + ((CA-NMBR-OF-MENU-ITEMS - 1) /                    
                         PC-NUMBER-OF-ITEMS-PER-BLOCK))                         
               SET CA-BLOCK-INDEX TO PV-BLOCK-NUMBER                            
               COMPUTE PV-ITEM-NUMBER =                                         
                         (CA-NMBR-OF-MENU-ITEMS - ((PV-BLOCK-NUMBER - 1)        
                         * PC-NUMBER-OF-ITEMS-PER-BLOCK))                       
               SET CA-ITEM-INDEX TO PV-ITEM-NUMBER                              
               MOVE CA-NMBR-OF-MENU-ITEMS TO                                    
                         CA-MENU-ITEM-NUMBER                                    
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-MENU-ITEM-MNEMONIC TO                                 
                         CA-MENU-ITEM-MNEMONIC                                  
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-MENU-ITEM-DESCRIPTION TO                              
                         CA-MENU-ITEM-DESCRIPTION                               
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-MENU-ITEM-TRAN-ID TO                                  
                         CA-MENU-ITEM-TRAN-ID                                   
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE PS-ITEM-AVAILABILITY-IND TO                                 
                         CA-MENU-ITEM-AVAILABILITY-IND                          
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-ACRT-LOC-REQR-IND TO                                  
                         CA-ACRT-LOC-REQR-IND                                   
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-MENU-ITEM-COMMAREA-FRMT TO                            
                         CA-MENU-ITEM-COMMAREA-FORMAT                           
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
               MOVE DP504-INT-APPL-FORMAT-IND      TO                           
                         CA-INT-APPL-FORMAT-IND                                 
                                        (CA-BLOCK-INDEX, CA-ITEM-INDEX)         
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITES OUT ONE OF THE COMMAREA ARRAY'S TWO BLOCKS TO TS QUEUE.*        
      ******************************************************************        
                                                                                
       3231-STORE-ITEM-IN-TS.                                                   
           IF CA-NMBR-OF-BLKS-WRITTEN-TO-TS = ZERO                              
               PERFORM                                                          
                       VARYING CA-BLOCK-INDEX                                   
                       FROM 1 BY 1                                              
                       UNTIL CA-BLOCK-INDEX >                                   
                                          PC-NMBR-OF-BLOCKS-IN-COMMAREA         
                   MOVE CA-BLOCK-ARRAY (CA-BLOCK-INDEX) TO                      
                             BB-BLOCK-BUFFER                                    
                   MOVE EIBTRMID TO TS-APPL-QUEUE-TERMINAL                      
                   MOVE DP020-SRC-TASK-NUMBER TO TS-APPL-QUEUE-TASK             
                   PERFORM 8400-WRITE-BUFFER-TO-TS-QUEUE                        
               END-PERFORM                                                      
               INITIALIZE PV-NUMBER-OF-ITEMS-IN-BUFFER                          
           END-IF.                                                              
                                                                                
           ADD 1 TO PV-NUMBER-OF-ITEMS-IN-BUFFER.                               
                                                                                
           IF PV-NUMBER-OF-ITEMS-IN-BUFFER >                                    
                                           PC-NUMBER-OF-ITEMS-PER-BLOCK         
               PERFORM 8400-WRITE-BUFFER-TO-TS-QUEUE                            
               MOVE 1 TO PV-NUMBER-OF-ITEMS-IN-BUFFER                           
           END-IF.                                                              
                                                                                
           SET BB-ITEM-INDEX TO PV-NUMBER-OF-ITEMS-IN-BUFFER.                   
           MOVE CA-NMBR-OF-MENU-ITEMS                                           
             TO    BB-MENU-ITEM-NUMBER (BB-ITEM-INDEX).                         
           MOVE DP504-MENU-ITEM-MNEMONIC                                        
             TO    BB-MENU-ITEM-MNEMONIC (BB-ITEM-INDEX).                       
           MOVE DP504-MENU-ITEM-DESCRIPTION                                     
             TO    BB-MENU-ITEM-DESCRIPTION (BB-ITEM-INDEX).                    
           MOVE DP504-MENU-ITEM-TRAN-ID                                         
             TO    BB-MENU-ITEM-TRAN-ID (BB-ITEM-INDEX).                        
           MOVE PS-ITEM-AVAILABILITY-IND                                        
             TO    BB-MENU-ITEM-AVAILABILITY-IND (BB-ITEM-INDEX).               
           MOVE DP504-MENU-ITEM-COMMAREA-FRMT                                   
             TO    BB-MENU-ITEM-COMMAREA-FORMAT (BB-ITEM-INDEX).                
           MOVE DP504-INT-APPL-FORMAT-IND                                       
             TO    BB-INT-APPL-FORMAT-IND (BB-ITEM-INDEX).                      
           MOVE DP504-ACRT-LOC-REQR-IND                                         
             TO    BB-ACRT-LOC-REQR-IND (BB-ITEM-INDEX).                        
                                                                                
      ******************************************************************        
      *  FORMAT MENU PANEL AND DISPLAY IT ON TERMINAL.                 *        
      ******************************************************************        
                                                                                
       3300-DISPLAY-MENU-PANEL.                                                 
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
           PERFORM DP002-0000-FORMAT-TITLE-LINES.                               
           MOVE DP020-USERID                TO AUSERIDO.                        
           MOVE DP020-LOCATION-NBR          TO ALOCATNO.                        
           IF DP020-LOCATION-DFLT                                               
               MOVE DP015-YELLOW            TO ALOCATNC.                        
           PERFORM 3310-FORMAT-ITEM-LINES.                                      
           PERFORM 3320-FORMAT-MESSAGE-LINE.                                    
           PERFORM DP009-0000-LOAD-FK-LINES.                                    
           IF PS-NO-EDIT-ERROR                                                  
               MOVE -1                      TO ASELL                            
           END-IF.                                                              
           PERFORM DP008-0000-WRITE-PANEL.                                      
           MOVE PV-ITEM-AT-TOP-OF-NEW-PANEL TO                                  
                     CA-ITEM-AT-TOP-OF-OLD-PANEL.                               
                                                                                
      ******************************************************************        
      *  MOVE INDIVIDUAL MENU ITEM'S FIELDS TO THE SCREEN              *        
      ******************************************************************        
                                                                                
       3310-FORMAT-ITEM-LINES.                                                  
           MOVE PV-ITEM-AT-TOP-OF-NEW-PANEL TO PV-ITEM-POINTER.                 
           SET MD-ITEM-INDEX-O                                                  
               CA-BLOCK-INDEX                                                   
               CA-ITEM-INDEX TO 1.                                              
                                                                                
           PERFORM PC-NUMBER-OF-ITEMS-PER-PANEL TIMES                           
               IF PV-ITEM-POINTER > CA-NMBR-OF-MENU-ITEMS                       
                   INITIALIZE MD-AITEMO (MD-ITEM-INDEX-O)                       
                              MD-AMNEMO (MD-ITEM-INDEX-O)                       
                              MD-ADESCO (MD-ITEM-INDEX-O)                       
               ELSE                                                             
                   PERFORM UNTIL ((CA-BLOCK-INDEX                               
                               >   PC-NMBR-OF-BLOCKS-IN-COMMAREA)               
                              OR  (CA-MENU-ITEM-NUMBER (CA-BLOCK-INDEX,         
                                                        CA-ITEM-INDEX)          
                               =   PV-ITEM-POINTER))                            
                       SET CA-ITEM-INDEX UP BY 1                                
                       IF CA-ITEM-INDEX > PC-NUMBER-OF-ITEMS-PER-BLOCK          
                           SET CA-ITEM-INDEX TO 1                               
                           SET CA-BLOCK-INDEX UP BY 1                           
                       END-IF                                                   
                   END-PERFORM                                                  
                   MOVE CA-MENU-ITEM-MNEMONIC                                   
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                             MD-AMNEMO (MD-ITEM-INDEX-O)                        
                   MOVE CA-MENU-ITEM-DESCRIPTION                                
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                             MD-ADESCO (MD-ITEM-INDEX-O)                        
                   IF CA-MENU-ITEM-IS-REMOTE                                    
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                       PERFORM 3311-CHECK-CONNECTION                            
                   END-IF                                                       
                   IF  CA-MENU-ITEM-IS-AVAILABLE                                
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                   AND (DP020-LOCATION-ACRT                                     
                   OR   CA-ACRT-LOC-NOT-REQR                                    
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX))           
                       MOVE CA-MENU-ITEM-NUMBER                                 
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                                 MD-AITEMO (MD-ITEM-INDEX-O)                    
                       MOVE DP015-PRO-NOR-OFF TO                                
                                 MD-AITEMA (MD-ITEM-INDEX-O)                    
                                 MD-AMNEMA (MD-ITEM-INDEX-O)                    
                                 MD-ADESCA (MD-ITEM-INDEX-O)                    
                       MOVE DP015-TURQUOISE TO                                  
                                 MD-AITEMC (MD-ITEM-INDEX-O)                    
                                 MD-AMNEMC (MD-ITEM-INDEX-O)                    
                                 MD-ADESCC (MD-ITEM-INDEX-O)                    
                   ELSE                                                         
                       SET MD-ITEM-INDEX-I TO MD-ITEM-INDEX-O                   
                       IF  CA-MENU-ITEM-IS-NOT-AVAILABLE                        
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                       OR  CA-MENU-ITEM-IS-UNCONNECTED                          
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                           MOVE PC-ITEM-NOT-AVAILABLE-LITERAL TO                
                                 MD-AITEMI (MD-ITEM-INDEX-I)                    
                       ELSE                                                     
                           MOVE PC-ACRT-LOC-REQR-LITERAL TO                     
                                 MD-AITEMI (MD-ITEM-INDEX-I)                    
                       END-IF                                                   
                       MOVE DP015-PRO-NOR-OFF TO                                
                                 MD-AITEMA (MD-ITEM-INDEX-O)                    
                                 MD-AMNEMA (MD-ITEM-INDEX-O)                    
                                 MD-ADESCA (MD-ITEM-INDEX-O)                    
                       MOVE DP015-PINK TO MD-AITEMC (MD-ITEM-INDEX-O)           
                                          MD-AMNEMC (MD-ITEM-INDEX-O)           
                                          MD-ADESCC (MD-ITEM-INDEX-O)           
                   END-IF                                                       
               END-IF                                                           
               ADD 1 TO PV-ITEM-POINTER                                         
               SET MD-ITEM-INDEX-O UP BY 1                                      
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  CHECK IF REMOTE CONNECTION IS NOW ACQUIRED.                   *        
      ******************************************************************        
                                                                                
       3311-CHECK-CONNECTION.                                                   
      *                                                                         
      *  REMOTE SYSTEM WAS DOWN WHEN MENU WAS BUILT.                            
      *  CHECK IF IT IS UP NOW.                                                 
      *                                                                         
           EXEC CICS INQUIRE                                                    
               TRANSACTION  (CA-MENU-ITEM-TRAN-ID                               
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX))           
               STATUS       (PV-TRANSACTION-STATUS-CODE)                        
               REMOTESYSTEM (PV-REMOTE-SYSTEM)                                  
               RESP         (PV-CICS-RESPONSE)                                  
           END-EXEC.                                                            
           IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL))                          
           OR (PV-TRANSACTION-STATUS-CODE NOT = DFHVALUE(ENABLED))              
               SET CA-MENU-ITEM-IS-NOT-AVAILABLE                                
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                TO TRUE                                                         
           END-IF.                                                              
                                                                                
           IF  (PV-REMOTE-SYSTEM NOT = SPACES)                                  
           AND (CA-MENU-ITEM-IS-REMOTE (CA-BLOCK-INDEX, CA-ITEM-INDEX))         
               IF PV-REMOTE-SYSTEM = PV-PRIOR-SYSTEM                            
                   MOVE PV-PRIOR-AVAIL                                          
                     TO CA-MENU-ITEM-AVAILABILITY-IND                           
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
               ELSE                                                             
                   EXEC CICS INQUIRE                                            
                       CONNECTION  (PV-REMOTE-SYSTEM)                           
                       CONNSTATUS  (PV-REMOTE-STATUS)                           
                       RESP        (PV-CICS-RESPONSE)                           
                   END-EXEC                                                     
                   IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL))                  
                   OR (NOT PV-REMOTE-ACQUIRED)                                  
                       SET CA-MENU-ITEM-IS-UNCONNECTED                          
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                                                   TO TRUE                      
                       IF DP020-MSG-NUMBER <= ZERO                              
                           SET DP020-MSG-INFORMATIONAL TO TRUE                  
                           MOVE PC-TSYMSG-99912    TO DP020-MSG-NUMBER          
                       END-IF                                                   
                   ELSE                                                         
                       MOVE 'R' TO CA-MENU-ITEM-AVAILABILITY-IND                
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                   END-IF                                                       
                   MOVE PV-REMOTE-SYSTEM TO PV-PRIOR-SYSTEM                     
                   MOVE CA-MENU-ITEM-AVAILABILITY-IND                           
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX)            
                     TO PV-PRIOR-AVAIL.                                         
                                                                                
      ******************************************************************        
      *  MOVE ANY MESSAGE FROM COMMAREA TO SCREEN AND SET COLOR ATTR.  *        
      ******************************************************************        
                                                                                
       3320-FORMAT-MESSAGE-LINE.                                                
      *                                                                         
      *  THIS ALSO CHECKS FOR E-MAIL EACH TIME NO SELECTION IS MADE             
      *  ON THE CORPORATE MENU, OR ON CLEAR OR SUCCESSFUL PAGING                
      *                                                                         
      **** IF  (DP020-MSG-NUMBER-X = SPACE                                      
      ****                        OR '00000')                                   
      ****     IF ((PS-CHECK-FOR-E-MAIL)                                        
      ****             OR (DP020-ENTRIES-IN-STACK = ZERO))                      
      *OLD         PERFORM 3321-CHECK-FOR-E-MAIL                                
      ****         PERFORM 9300-LINK-TO-E-MAIL-QUERY                            
      ****     END-IF                                                           
      **** END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
      ******************************************************************        
      *  THIS VERSION OF THE PARAGRAPH WHICH USES SYSM0037, HAS BEEN   *        
      *  REPLACED BY ONE THAT USES NEW MODULE DPKCS025.  THE SYSM0037  *        
      *  PROCEDURES HAVE BEEN LEFT IN THE PROGRAM, BUT COMMENTED OUT,  *        
      *  FOR FUTURE REFERENCE IN CASE WE NEED TO MAKE USE OF IT.       *        
      *                                                                *        
      *  NOTE:  IF THIS CODE IS REINSTATED APPROPRIATE MESSAGE NUMBERS *        
      *         WILL NEED TO BE SET UP TO REPLACE MOVING LITERALS TO   *        
      *         TO MESSAGE FIELD.                                      *        
      ******************************************************************        
                                                                                
      *3321-CHECK-FOR-E-MAIL.                                                   
      *    EXEC CICS                                                            
      *        ASSIGN                                                           
      *            TWALENG (PV-TWA-LENGTH)                                      
      *    END-EXEC.                                                            
      *                                                                         
      *    IF PV-TWA-LENGTH > ZERO                                              
      *        EXEC CICS                                                        
      *            ADDRESS                                                      
      *                TWA (ADDRESS OF ST-SYSM-TWA)                             
      *        END-EXEC                                                         
      *    END-IF.                                                              
      *                                                                         
      *    INITIALIZE ST-SYSM-TWA.                                              
      *                                                                         
      *    MOVE PC-CURRENT-PROGRAM-NAME TO                                      
      *              ST-TWADFPGM-REQ-PROGRAM-NAME.                              
      *                                                                         
      *    EXEC CICS                                                            
      *        ASSIGN                                                           
      *            USERID (ST-TWADMOID-SYSM-USER-ID)                            
      *    END-EXEC.                                                            
      *                                                                         
      *    PERFORM 9300-LINK-TO-E-MAIL-QUERY.                                   
      *                                                                         
      *    EVALUATE TRUE                                                        
      *        WHEN ST-TWADRETN-NORMAL                                          
      *            MOVE ST-TWADMG29-RESPONSE-MESSAGE TO                         
      *                      DP020-MSG-MESSAGE                                  
      *            SET DP020-MSG-INFORMATIONAL TO TRUE                          
      *        WHEN ST-TWADRETN-INVALID-USERID                                  
      *            MOVE 'USER DOES NOT HAVE ACCESS TO ELECTRONIC MAIL'          
      *                      TO DP020-MSG-MESSAGE                               
      *            SET DP020-MSG-WARNING TO TRUE                                
      *        WHEN OTHER                                                       
      *            MOVE 'ELECTRONIC MAIL IS CURRENTLY UNAVAILABLE' TO           
      *                      DP020-MSG-MESSAGE                                  
      *            SET DP020-MSG-WARNING TO TRUE                                
      *    END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  BASED ON THE ITEM NUMBER ENTERED IN THE ITEM NUMBER SELECTED  *        
      *  FIELD, THIS PARAGRAPH RETRIEVES THE TRANSACTION ID FROM THE   *        
      *  ARRAY IN THE COMMAREA AND CHECKS THE AVAILABILITY OF THE      *        
      *  TRANSACTION BEFORE PROCEEDING. IF UNAVAILABLE A MESSAGE IS    *        
      *  RETURNED TO THE SCREEN.                                       *        
      ******************************************************************        
                                                                                
       4200-FIND-SELECTION-TEST-AVAIL.                                          
           IF ((SI-SELECTED-ITEM-NUMBER < CA-ITEM-AT-TOP-OF-OLD-PANEL)          
                   OR (SI-SELECTED-ITEM-NUMBER >                                
                                  (CA-ITEM-AT-TOP-OF-OLD-PANEL                  
                                  + PC-NUMBER-OF-ITEMS-PER-PANEL - 1)))         
               MOVE SI-SELECTED-ITEM-NUMBER TO                                  
                         PV-ITEM-AT-TOP-OF-NEW-PANEL                            
               PERFORM 4400-ENSURE-ITEMS-ARE-IN-CA                              
           END-IF.                                                              
                                                                                
           SET CA-BLOCK-INDEX                                                   
               CA-ITEM-INDEX TO 1.                                              
           PERFORM                                                              
                   UNTIL ((CA-BLOCK-INDEX >                                     
                                      PC-NMBR-OF-BLOCKS-IN-COMMAREA)            
                           OR (CA-MENU-ITEM-NUMBER                              
                                      (CA-BLOCK-INDEX, CA-ITEM-INDEX) =         
                                      SI-SELECTED-ITEM-NUMBER))                 
               SET CA-ITEM-INDEX UP BY 1                                        
               IF CA-ITEM-INDEX > PC-NUMBER-OF-ITEMS-PER-BLOCK                  
                   SET CA-ITEM-INDEX TO 1                                       
                   SET CA-BLOCK-INDEX UP BY 1                                   
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF CA-BLOCK-INDEX > PC-NMBR-OF-BLOCKS-IN-COMMAREA                    
               SET DP013-LOGIC-ABEND TO TRUE                                    
               MOVE '4200-FIND-SELECTION-TEST-AVAIL' TO DP013-PARAGRAPH         
               MOVE 'ATTEMPTING TO FIND THE SELECTED MENU ITEM IN THE CO        
      -                  'MMAREA BLOCK ARRAY.' TO                               
                         DP013-MESSAGE-TEXT (1)                                 
               MOVE 'THE SELECTED ITEM NUMBER IS VALID, BUT THE ITEM DET        
      -                  'AIL COULD NOT BE LOCATED.' TO                         
                         DP013-MESSAGE-TEXT (2)                                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               IF CA-MENU-ITEM-IS-REMOTE                                        
                                 (CA-BLOCK-INDEX, CA-ITEM-INDEX)                
                   PERFORM 3311-CHECK-CONNECTION                                
               END-IF                                                           
               MOVE CA-MENU-ITEM-AVAILABILITY-IND                               
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-SEL-ITEM-AVAILABILITY-IND                           
               MOVE CA-MENU-ITEM-TRAN-ID                                        
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-SEL-ITEM-TRAN-ID                                    
               MOVE CA-MENU-ITEM-MNEMONIC                                       
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-SEL-ITEM-MNEMONIC                                   
               MOVE CA-MENU-ITEM-COMMAREA-FORMAT                                
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-SEL-ITEM-COMMAREA-FORMAT                            
               MOVE CA-INT-APPL-FORMAT-IND                                      
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-SEL-ITEM-INT-APPL-FORMAT                            
               MOVE CA-ACRT-LOC-REQR-IND                                        
                                     (CA-BLOCK-INDEX, CA-ITEM-INDEX) TO         
                         SI-ACRT-LOC-REQR-IND                                   
               IF  DP020-LOCATION-DFLT                                          
               AND SI-ACRT-LOC-REQR                                             
      *            -- SELECTED REQUIRES ACCURATE LOCATION --                    
                   SET PS-EDIT-ERROR                                            
                       DP020-MSG-FATAL        TO TRUE                           
                   MOVE PC-TSYMSG-99911       TO DP020-MSG-NUMBER               
                   MOVE DP015-RED             TO ASELC                          
                   MOVE DP015-REVERSE         TO ASELH                          
                   MOVE DP015-UNP-ALP-BRT-OFF TO ASELA                          
                   MOVE -1 TO ASELL                                             
               END-IF                                                           
               IF SI-SEL-ITEM-IS-NOT-AVAILABLE                                  
      *            -- SELECTED IS NOT AVAILABLE --                              
                   SET PS-EDIT-ERROR                                            
                       DP020-MSG-FATAL        TO TRUE                           
                   MOVE PC-TSYMSG-99904       TO DP020-MSG-NUMBER               
                   MOVE DP015-RED             TO ASELC                          
                   MOVE DP015-REVERSE         TO ASELH                          
                   MOVE DP015-UNP-ALP-BRT-OFF TO ASELA                          
                   MOVE -1 TO ASELL                                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS PARAGRAPH STARTS THE REQUESTED TRANSACTION. IF THE REQ-  *        
      *  UESTED TRANSACTION IS OUTSIDE THE ARCHITECTURE, THIS PARA-    *        
      *  WILL WRITE OUT THE ENTIRE COMMAREA TO A TEMP STORAGE QUEUE    *        
      *  NAMED TERMID +'SCA'.                                          *        
      ******************************************************************        
                                                                                
       4300-START-SELECTED-ITEM.                                                
           MOVE DP020-SRC-TASK-NUMBER TO PV-CURRENT-TASK-NUMBER.                
                                                                                
           IF SI-COMMAREA-STANDARD                                              
               PERFORM 6000-PREPARE-CA-FOR-DEST-TRAN                            
               SET DP020-NEXT-ACT-INITIAL TO TRUE                               
               SET PS-START-WITH-COMMAREA TO TRUE                               
           ELSE                                                                 
               IF CA-NMBR-OF-AVAIL-MENU-ITEMS > 1                               
                   PERFORM 6100-ADD-ENTRY-TO-STACK                              
               END-IF                                                           
               SET DP020-NEXT-ACT-RETURN TO TRUE                                
               MOVE EIBTRMID TO TS-SCA-QUEUE-TERMINAL                           
               PERFORM 8300-WRITE-SCA-TS-QUEUE                                  
               IF TS-SCA-QUEUE-ITEM-NUMBER NOT = 1                              
                   PERFORM 8200-DELETE-SCA-TS-QUEUE                             
                   PERFORM 8300-WRITE-SCA-TS-QUEUE                              
               END-IF                                                           
               SET PS-START-WITHOUT-COMMAREA TO TRUE                            
           END-IF.                                                              
                                                                                
           PERFORM 9100-START-DESTINATION-TRAN.                                 
                                                                                
           MOVE EIBTRMID TO TS-APPL-QUEUE-TERMINAL.                             
           MOVE PV-CURRENT-TASK-NUMBER TO TS-APPL-QUEUE-TASK.                   
           INITIALIZE PV-SYSID.                                                 
           PERFORM 8100-DELETE-APPL-TS-QUEUE.                                   
                                                                                
           SET DP020-SRC-RETURN-W-O-TRAN-ID TO TRUE.                            
                                                                                
      ******************************************************************        
      *  LOGIC TO VALIDATE THAT REQUESTED ITEM IS IN ARRAY IN COMMAREA *        
      ******************************************************************        
                                                                                
       4400-ENSURE-ITEMS-ARE-IN-CA.                                             
           IF CA-NMBR-OF-BLKS-WRITTEN-TO-TS > ZERO                              
               IF PV-ITEM-AT-TOP-OF-NEW-PANEL <                                 
                                            CA-MENU-ITEM-NUMBER (1, 1)          
                   PERFORM 4410-LOAD-TS-ITEMS-INTO-CA                           
               ELSE                                                             
                   COMPUTE PV-LAST-ITEM-ON-NEW-PANEL =                          
                             (PV-ITEM-AT-TOP-OF-NEW-PANEL +                     
                             PC-NUMBER-OF-ITEMS-PER-PANEL - 1)                  
                   IF CA-NMBR-OF-MENU-ITEMS < PV-LAST-ITEM-ON-NEW-PANEL         
                       MOVE CA-NMBR-OF-MENU-ITEMS TO                            
                                            PV-LAST-ITEM-ON-NEW-PANEL           
                   END-IF                                                       
                   COMPUTE PV-LAST-ITEM-IN-COMMAREA =                           
                             (CA-MENU-ITEM-NUMBER (1, 1) +                      
                             (PC-NUMBER-OF-ITEMS-PER-BLOCK *                    
                             PC-NMBR-OF-BLOCKS-IN-COMMAREA) - 1)                
                   IF CA-NMBR-OF-MENU-ITEMS < PV-LAST-ITEM-IN-COMMAREA          
                       MOVE CA-NMBR-OF-MENU-ITEMS TO                            
                                            PV-LAST-ITEM-IN-COMMAREA            
                   END-IF                                                       
                   IF PV-LAST-ITEM-ON-NEW-PANEL >                               
                                            PV-LAST-ITEM-IN-COMMAREA            
                       PERFORM 4410-LOAD-TS-ITEMS-INTO-CA                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC TO MOVE TS QUEUE JUST READ INTO THE COMMAREA ARRAY      *        
      ******************************************************************        
                                                                                
       4410-LOAD-TS-ITEMS-INTO-CA.                                              
           MOVE EIBTRMID TO TS-APPL-QUEUE-TERMINAL.                             
           MOVE DP020-SRC-TASK-NUMBER TO TS-APPL-QUEUE-TASK.                    
                                                                                
           COMPUTE PV-BLOCK-NUMBER =                                            
                     (1 + ((PV-ITEM-AT-TOP-OF-NEW-PANEL - 1) /                  
                     PC-NUMBER-OF-ITEMS-PER-BLOCK)).                            
                                                                                
           PERFORM                                                              
                   VARYING CA-BLOCK-INDEX                                       
                   FROM 1 BY 1                                                  
                   UNTIL CA-BLOCK-INDEX > PC-NMBR-OF-BLOCKS-IN-COMMAREA         
               IF PV-BLOCK-NUMBER NOT > CA-NMBR-OF-BLKS-WRITTEN-TO-TS           
                   MOVE PV-BLOCK-NUMBER TO TS-APPL-QUEUE-ITEM-NUMBER            
                   PERFORM 7100-READ-APPL-TS-QUEUE                              
               ELSE                                                             
                   INITIALIZE CA-BLOCK-ARRAY (CA-BLOCK-INDEX)                   
               END-IF                                                           
               ADD 1 TO PV-BLOCK-NUMBER                                         
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  THIS ROUTINE PREPARES THE COMMAREA FOR THE TRANSACTION TO BE  *        
      *  STARTED.                                                      *        
      ******************************************************************        
                                                                                
       6000-PREPARE-CA-FOR-DEST-TRAN.                                           
           INITIALIZE DP020-INTER-APPL-DATA-AREA.                               
           IF CA-NMBR-OF-AVAIL-MENU-ITEMS > 1                                   
               PERFORM 6100-ADD-ENTRY-TO-STACK                                  
           END-IF.                                                              
           PERFORM 6200-RESET-SOURCE-VARIABLES.                                 
           PERFORM 6300-FORMAT-COMMAREA.                                        
                                                                                
           SET DP020-SRC-RETURN-W-TRAN-ID TO TRUE.                              
           MOVE SI-SEL-ITEM-MNEMONIC TO DP020-SRC-MNEMONIC.                     
           MOVE SI-SEL-ITEM-INT-APPL-FORMAT                                     
                                     TO DP020-INT-APPL-FORMAT-IND.              
           INITIALIZE DP020-SRC-AID                                             
                      DP020-NAV-MNEMONIC.                                       
           SET DP020-SRC-NO-LOCK-HELD TO TRUE.                                  
                                                                                
      ******************************************************************        
      *  THIS ROUTINE ADDS THE MENU TO THE STACK. IF THE STACK IS AT   *        
      *  TWENTY ITEMS ALREADY, THIS ROUTINE DELETES ITEM ONE, ALONG    *        
      *  WITH ITS ASSOCIATED TS QUEUE, MOVES ITEMS TWO THRU TWENTY     *        
      *  TO ITEMS ONE THRU NINETEEN, AND MOVES MENU TO ITEM TWENTY.    *        
      ******************************************************************        
                                                                                
       6100-ADD-ENTRY-TO-STACK.                                                 
                                                                                
           IF DP020-ENTRIES-IN-STACK = PC-NUMBER-OF-ENTRIES-IN-STACK            
               MOVE EIBTRMID            TO TS-APPL-QUEUE-TERMINAL               
                                           TS-APPL-MULT-QUEUE-TERM              
               MOVE DP020-STACK-TASK-NUMBER (1)                                 
                                        TO TS-APPL-QUEUE-TASK                   
                                           TS-APPL-MULT-QUEUE-TASK              
               EXEC CICS                                                        
                   INQUIRE                                                      
                       TRANSACTION  (DP020-STACK-TRAN-ID (1))                   
                       REMOTESYSTEM (PV-SYSID)                                  
               END-EXEC                                                         
               MOVE DP020-STACK-TRAN-ID (1)                                     
                                        TO PV-DELETEQ-TRAN-ID                   
               PERFORM 8110-DELETE-APPL-TS-QUEUE                                
               IF DP020-STACK-LOCK-HELD (1)                                     
                   MOVE DP020-STACK-TASK-NUMBER (1) TO DP509-TASK-NBR           
                   PERFORM 8500-DELETE-TASK-RCD                                 
                   SET DP020-STACK-NO-LOCK-HELD (1) TO TRUE                     
               END-IF                                                           
               MOVE DP020-STACK-TASK-NUMBER (1)                                 
                                         TO SM-STACK-KEY-TASK                   
               PERFORM 8550-DELETE-FROM-DPSAVCMK                                
               PERFORM                                                          
                       VARYING DP020-STACK-INDEX                                
                       FROM 2 BY 1                                              
                       UNTIL DP020-STACK-INDEX >                                
                                         PC-NUMBER-OF-ENTRIES-IN-STACK          
                   MOVE DP020-STACK-TRANSACTION (DP020-STACK-INDEX) TO          
                             DP020-STACK-TRANSACTION                            
                                                (DP020-STACK-INDEX - 1)         
               END-PERFORM                                                      
           ELSE                                                                 
               ADD 1 TO DP020-ENTRIES-IN-STACK                                  
           END-IF.                                                              
                                                                                
           MOVE DP020-SRC-TRAN-ID                                               
                  TO DP020-STACK-TRAN-ID (DP020-ENTRIES-IN-STACK).              
           MOVE DP020-SRC-MNEMONIC                                              
                  TO DP020-STACK-MNEMONIC (DP020-ENTRIES-IN-STACK).             
           MOVE DP020-SRC-SAVE-COMMAREA-SW                                      
                  TO DP020-STACK-COMMAREA-SAVED-SW                              
                                          (DP020-ENTRIES-IN-STACK).             
           MOVE DP020-SRC-TASK-NUMBER                                           
                  TO DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK).          
           MOVE DP020-SRC-LOCK-IND                                              
                  TO DP020-STACK-LOCK-IND (DP020-ENTRIES-IN-STACK).             
           SET DP020-STACK-TRAN-ON (DP020-ENTRIES-IN-STACK)                     
                  TO TRUE.                                                      
                                                                                
           IF DP020-SRC-SAVE-COMMAREA                                           
               MOVE DP020-NETNAME       TO SM-STACK-KEY-NETNAME                 
               MOVE DP020-STACK-TASK-NUMBER                                     
                                    (DP020-ENTRIES-IN-STACK)                    
                                        TO SM-STACK-KEY-TASK                    
               MOVE SM-STACK-KEY        TO DP515-KEY                            
               MOVE EIBDATE             TO DP515-CREATE-EIBDATE                 
               MOVE EIBTIME             TO DP515-CREATE-EIBTIME                 
               MOVE '7'                 TO DP515-CREATE-PGM                     
               MOVE DP020-APPL-COMM-AREA                                        
                                        TO DP515-APPL-COMMAREA                  
               COMPUTE PV-ACT-DPSAVCMK-LENGTH =                                 
                       DP020-APPL-COMMAREA-LENGTH                               
                     + PC-STACK-KEY-LENGTH                                      
                     + PC-EIBDATE-EIBTIME-7-LENGTH                              
               PERFORM 8000-WRITE-SAVED-COMMAREA-RCD                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  SET UP FIELDS IN THE GLOBAL PORTION OF THE STANDARD COMMAREA  *        
      *  FOR THE NEXT TRANSACTION.                                     *        
      ******************************************************************        
                                                                                
       6200-RESET-SOURCE-VARIABLES.                                             
           IF SI-SEL-ITEM-MNEMONIC NOT = DP503-MNEMONIC                         
               PERFORM 7000-READ-MNEMONIC-FILE                                  
               IF PS-MNEMONIC-NOT-FOUND                                         
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   MOVE '6200-RESET-SRC-VARIABLES' TO DP013-PARAGRAPH           
                   STRING 'ATTEMPTING TO READ THE MNEMONIC FILE ('              
                                                      DELIMITED BY SIZE         
                          FD-MNEMONIC-FILE            DELIMITED BY SIZE         
                          ')'                         DELIMITED BY SIZE         
                       INTO DP013-MESSAGE-TEXT (1)                              
                   STRING 'MNEMONIC "'                DELIMITED BY SIZE         
                          SI-SEL-ITEM-MNEMONIC        DELIMITED BY SIZE         
                          '" WAS NOT FOUND'           DELIMITED BY SIZE         
                       INTO DP013-MESSAGE-TEXT (2)                              
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF DP503-COMMAREA-STANDARD                                           
               SET DP020-GOOD-COMMAREA TO TRUE                                  
           ELSE                                                                 
               SET DP020-BAD-COMMAREA TO TRUE                                   
           END-IF.                                                              
                                                                                
           MOVE DP503-TRAN-ID TO DP020-SRC-TRAN-ID.                             
           MOVE DP503-DB2-PKG-IND      TO DP020-SRC-DB2-PKG-IND.                
           MOVE DP503-SAVE-COMMAREA-SW TO DP020-SRC-SAVE-COMMAREA-SW.           
           MOVE EIBTASKN               TO DP020-SRC-TASK-NUMBER.                
                                                                                
           MOVE PC-GLOBAL-COMMAREA-LENGTH TO                                    
                     DP020-GLOBAL-COMMAREA-LENGTH.                              
           MOVE DP503-APPL-COMMAREA-LENGTH TO                                   
                     DP020-APPL-COMMAREA-LENGTH.                                
           COMPUTE DP020-COMMAREA-LENGTH =                                      
                     (DP020-GLOBAL-COMMAREA-LENGTH +                            
                     DP020-APPL-COMMAREA-LENGTH).                               
                                                                                
      ******************************************************************        
      *  LOAD THE FUNTION KEY ARRAY OF THE GLOBAL COMMAREA FOR THE NEXT*        
      *  TRANSACTION.                                                  *        
      ******************************************************************        
                                                                                
       6300-FORMAT-COMMAREA.                                                    
           INITIALIZE DP020-FUNCTION-KEY-ARRAY.                                 
           SET DP020-FK-DISPLAY-13-24 TO TRUE.                                  
           MOVE DP503-PANEL-TITLE TO DP020-PANEL-TITLE.                         
                                                                                
           PERFORM                                                              
                   VARYING DP503-FK-INDEX                                       
                   FROM 1 BY 1                                                  
                   UNTIL DP503-FK-INDEX > PC-NUMBER-OF-AID-KEYS                 
               SET DP020-FK-INDEX TO DP503-FK-INDEX                             
               IF  DP503-FK-TRAN-ID (DP503-FK-INDEX) = SPACES                   
                   MOVE DP503-FK-LABEL (DP503-FK-INDEX)                         
                        TO DP020-FK-MNEMONIC (DP020-FK-INDEX)                   
                   MOVE DP503-FK-TRAN-ID (DP503-FK-INDEX)                       
                        TO DP020-FK-TRAN-ID (DP020-FK-INDEX)                    
                   MOVE DP503-FK-ACTION (DP503-FK-INDEX)                        
                        TO DP020-FK-ACTION (DP020-FK-INDEX)                     
                   MOVE DP503-FK-STACK-IND (DP503-FK-INDEX)                     
                        TO DP020-FK-STACK-IND (DP020-FK-INDEX)                  
                   MOVE DP503-FK-ALT-LABEL-IND (DP503-FK-INDEX)                 
                        TO DP020-FK-ALT-LABEL-IND (DP020-FK-INDEX)              
                   MOVE DP503-FK-INT-APPL-FORMAT-IND (DP503-FK-INDEX)           
                        TO DP020-FK-INT-APPL-FORMAT-IND (DP020-FK-INDEX)        
                   MOVE DP503-FK-DISPN-IND (DP503-FK-INDEX)                     
                        TO DP020-FK-DISPN-IND (DP020-FK-INDEX)                  
               ELSE                                                             
                   SET DP008-ACCESS-TYPE-USE TO TRUE                            
                   IF DP503-FK-TRAN-ID (DP503-FK-INDEX) =                       
                                                        PC-MENU-TRAN-ID         
                       SET DP008-RSRC-TYPE-MENU TO TRUE                         
                       MOVE DP503-FK-MNEMONIC (DP503-FK-INDEX) TO               
                                 DP008-RESOURCE-NAME                            
                   ELSE                                                         
                       SET DP008-RSRC-TYPE-TRAN TO TRUE                         
                       MOVE DP503-FK-TRAN-ID (DP503-FK-INDEX) TO                
                                 DP008-RESOURCE-NAME                            
                   END-IF                                                       
                   PERFORM 9200-LINK-TO-SECURITY-MODULE                         
                   IF DP008-CALL-SUCCESSFUL                                     
                       IF DP008-ACCESS-ALLOWED                                  
                           MOVE DP503-FK-LABEL (DP503-FK-INDEX)                 
                                  TO DP020-FK-MNEMONIC (DP020-FK-INDEX)         
                           MOVE DP503-FK-TRAN-ID (DP503-FK-INDEX)               
                                  TO DP020-FK-TRAN-ID (DP020-FK-INDEX)          
                           MOVE DP503-FK-ACTION (DP503-FK-INDEX)                
                                  TO DP020-FK-ACTION (DP020-FK-INDEX)           
                           MOVE DP503-FK-STACK-IND (DP503-FK-INDEX)             
                                  TO DP020-FK-STACK-IND                         
                                                     (DP020-FK-INDEX)           
                           MOVE DP503-FK-ALT-LABEL-IND                          
                                                      (DP503-FK-INDEX)          
                                  TO DP020-FK-ALT-LABEL-IND                     
                                                       (DP020-FK-INDEX)         
                           MOVE DP503-FK-INT-APPL-FORMAT-IND                    
                                                      (DP503-FK-INDEX)          
                                  TO DP020-FK-INT-APPL-FORMAT-IND               
                                                      (DP020-FK-INDEX)          
                           MOVE DP503-FK-DISPN-IND (DP503-FK-INDEX)             
                                  TO DP020-FK-DISPN-IND (DP020-FK-INDEX)        
                       ELSE                                                     
                           INITIALIZE                                           
                               DP020-FUNCTION-KEY (DP020-FK-INDEX)              
                       END-IF                                                   
                   ELSE                                                         
                       SET DP013-SECURITY-ABEND TO TRUE                         
                       MOVE '6300-FORMAT-COMMAREA' TO DP013-PARAGRAPH           
                       MOVE DP008-SECURITY-COMMAREA TO                          
                                 DP013-SECURITY-INFO                            
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO RETREIVE A RECORD FROM DPMNEMIC FILE.    *        
      ******************************************************************        
                                                                                
       7000-READ-MNEMONIC-FILE.                                                 
           EXEC CICS                                                            
               READ                                                             
                   DATASET (FD-MNEMONIC-FILE)                                   
                   RIDFLD  (SI-SEL-ITEM-MNEMONIC)                               
                   INTO    (DP503-MNEMONIC-RECORD)                              
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE PV-CICS-RESPONSE                                            
               WHEN DFHRESP(NORMAL)                                             
                   SET PS-MNEMONIC-FOUND TO TRUE                                
               WHEN DFHRESP(NOTFND)                                             
                   SET PS-MNEMONIC-NOT-FOUND TO TRUE                            
               WHEN OTHER                                                       
                   SET DP013-CICS-ABEND                                         
                       DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '7000-READ-MNEMONIC-FILE' TO DP013-PARAGRAPH            
                   STRING 'ATTEMPTING TO READ THE MNEMONIC FILE ('              
                                                      DELIMITED BY SIZE         
                          FD-MNEMONIC-FILE            DELIMITED BY SIZE         
                          ')'                         DELIMITED BY SIZE         
                       INTO DP013-MESSAGE-TEXT (1)                              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO READ AN ITEM FROM THE TS QUEUE           *        
      ******************************************************************        
                                                                                
       7100-READ-APPL-TS-QUEUE.                                                 
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE  (TS-APPL-QUEUE-NAME)                                  
                   INTO   (CA-BLOCK-ARRAY (CA-BLOCK-INDEX))                     
                   LENGTH (TS-APPL-QUEUE-LENGTH)                                
                   ITEM   (TS-APPL-QUEUE-ITEM-NUMBER)                           
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '7100-READ-APPL-TS-QUEUE' TO DP013-PARAGRAPH                
               STRING 'ATTEMPTING TO READ THE APPLICATION TEMPORARY '           
                                                      DELIMITED BY SIZE         
                      'STORAGE QUEUE ('               DELIMITED BY SIZE         
                      TS-APPL-QUEUE-NAME              DELIMITED BY SIZE         
                      ')'                             DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (1)                                  
               STRING 'ITEM NUMBER ('                 DELIMITED BY SIZE         
                      PV-BLOCK-NUMBER                 DELIMITED BY SIZE         
                      ')'                             DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (2)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO INITIATE THE RETREIVAL OF THE MENU ITEMS *        
      *  FROM THE DPMNUITM VSAM FILE.                                  *        
      ******************************************************************        
                                                                                
       7300-START-MENU-ITEM-FILE.                                               
           EXEC CICS                                                            
               STARTBR                                                          
                   DATASET (FD-MENU-ITEM-FILE)                                  
                   RIDFLD  (DP504-MENU-ITEM-KEY)                                
                   GTEQ                                                         
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE PV-CICS-RESPONSE                                            
               WHEN DFHRESP(NORMAL)                                             
                   SET PS-MORE-ITEMS                                            
                       PS-SUCCESSFUL-START-BROWSE TO TRUE                       
               WHEN DFHRESP(ENDFILE)                                            
               WHEN DFHRESP(NOTFND)                                             
                   SET PS-NO-MORE-ITEMS                                         
                       PS-UNSUCCESSFUL-START-BROWSE TO TRUE                     
               WHEN OTHER                                                       
                   SET DP013-CICS-ABEND                                         
                       DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '7300-START-MENU-ITEM-FILE' TO DP013-PARAGRAPH          
                   STRING 'ATTEMPTING TO START A BROWSE ON THE MENU '           
                                                      DELIMITED BY SIZE         
                          'ITEM FILE ('               DELIMITED BY SIZE         
                          FD-MENU-ITEM-FILE           DELIMITED BY SIZE         
                          ')  RIDFLD = ('             DELIMITED BY SIZE         
                          DP504-MENU-ITEM-KEY         DELIMITED BY SIZE         
                          ')'                         DELIMITED BY SIZE         
                      INTO DP013-MESSAGE-TEXT (1)                               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO READ THE NEXT RECORD IN THE DPMNUITM     *        
      *  VSAM FILE.                                                    *        
      ******************************************************************        
                                                                                
       7400-READ-NEXT-MENU-ITEM.                                                
           EXEC CICS                                                            
               READNEXT                                                         
                   DATASET (FD-MENU-ITEM-FILE)                                  
                   RIDFLD  (DP504-MENU-ITEM-KEY)                                
                   INTO    (DP504-MENU-ITEM-RECORD)                             
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE PV-CICS-RESPONSE                                            
               WHEN DFHRESP(NORMAL)                                             
                   IF DP504-MENU-NAME NOT = CA-MENU-BEING-DISPLAYED             
                       SET PS-NO-MORE-ITEMS TO TRUE                             
                   END-IF                                                       
               WHEN DFHRESP(ENDFILE)                                            
               WHEN DFHRESP(NOTFND)                                             
                   SET PS-NO-MORE-ITEMS TO TRUE                                 
               WHEN OTHER                                                       
                   SET DP013-CICS-ABEND                                         
                       DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '7400-READ-NEXT-MENU-ITEM' TO DP013-PARAGRAPH           
                   STRING 'ATTEMPTING TO READ THE NEXT MENU ITEM FROM '         
                                                      DELIMITED BY SIZE         
                          'THE ITEM FILE ('           DELIMITED BY SIZE         
                          FD-MENU-ITEM-FILE           DELIMITED BY SIZE         
                          ')  RIDFLD = ('             DELIMITED BY SIZE         
                          DP504-MENU-ITEM-KEY         DELIMITED BY SIZE         
                          ')'                         DELIMITED BY SIZE         
                      INTO DP013-MESSAGE-TEXT (1)                               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO END THE BROWSING OF THE DPMNUITM VSAM    *        
      *  FILE.                                                         *        
      ******************************************************************        
                                                                                
       7500-END-MENU-ITEM-FILE.                                                 
           EXEC CICS                                                            
               ENDBR                                                            
                   DATASET (FD-MENU-ITEM-FILE)                                  
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '7500-END-MENU-ITEM-FILE' TO DP013-PARAGRAPH                
               STRING 'ATTEMPTING TO END A BROWSE ON THE MENU '                 
                                                      DELIMITED BY SIZE         
                      'ITEM FILE ('                   DELIMITED BY SIZE         
                      FD-MENU-ITEM-FILE               DELIMITED BY SIZE         
                      ')'                             DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE THE APPLICATION-SPECIFIC COMMAREA TO THE DPSAVCMK       *        
      *  KSDS VSAM FILE.                                               *        
      ******************************************************************        
                                                                                
       8000-WRITE-SAVED-COMMAREA-RCD.                                           
           EXEC CICS                                                            
               INQUIRE                                                          
                   TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                          
                   REMOTESYSTEM (PV-NAVIGATOR-SYSID)                            
           END-EXEC.                                                            
                                                                                
           IF PV-NAVIGATOR-SYSID = SPACES                                       
               INITIALIZE PV-SYSID-SPECIFIED-LITERAL                            
               EXEC CICS                                                        
                   WRITE                                                        
                       FILE      (FD-SAVED-COMMAREA-FILE)                       
                       FROM      (DP515-SAVED-COMMAREA-RECORD)                  
                       LENGTH    (PV-ACT-DPSAVCMK-LENGTH)                       
                       RESP      (PV-CICS-RESPONSE)                             
                       RIDFLD    (DP515-KEY)                                    
                       KEYLENGTH (PC-STACK-KEY-LENGTH)                          
               END-EXEC                                                         
           ELSE                                                                 
               SET PV-SYSID-SPECIFIED TO TRUE                                   
               EXEC CICS                                                        
                   WRITE                                                        
                       FILE      (FD-SAVED-COMMAREA-FILE)                       
                       FROM      (DP515-SAVED-COMMAREA-RECORD)                  
                       LENGTH    (PV-ACT-DPSAVCMK-LENGTH)                       
                       RESP      (PV-CICS-RESPONSE)                             
                       RIDFLD    (DP515-KEY)                                    
                       KEYLENGTH (PC-STACK-KEY-LENGTH)                          
                       SYSID     (PV-NAVIGATOR-SYSID)                           
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '8000-WRITE-SAVED-COMMAREA-RCD' TO DP013-PARAGRAPH          
               STRING 'ATTEMPTING TO WRITE A RECORD TO THE SAVED '              
                                                      DELIMITED BY SIZE         
                      'COMMAREA FILE ('               DELIMITED BY SIZE         
                      FD-SAVED-COMMAREA-FILE          DELIMITED BY SIZE         
                      ') '                            DELIMITED BY SIZE         
                      PV-SYSID-SPECIFIED-LITERAL      DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (1)                                  
               MOVE DP515-KEY                 TO PV-DISPLAY-KEY                 
               MOVE 'KEYLEN='                 TO PV-DISPLAY-FIELD2-HDR          
               MOVE PC-STACK-KEY-LENGTH       TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD2              
               MOVE 'RECLEN='                 TO PV-DISPLAY-FIELD2-HDR          
               MOVE PV-ACT-DPSAVCMK-LENGTH    TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD3              
               MOVE PV-DISPLAY-LINE-2         TO DP013-MESSAGE-TEXT (2)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPH TO DELETE A TS QUEUE                        *        
      ******************************************************************        
                                                                                
       8100-DELETE-APPL-TS-QUEUE.                                               
           IF PV-SYSID = SPACES                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       SYSID (PV-SYSID)                                         
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *  UTILITY PARAGRAPH TO DELETE A TS QUEUE ON STACK OVERFLOW      *        
      ******************************************************************        
                                                                                
       8110-DELETE-APPL-TS-QUEUE.                                               
           IF PV-SYSID = SPACES                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       SYSID (PV-SYSID)                                         
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
      *                                                                         
           PERFORM 8111-DELETE-APPL-MULT-TS-QUEUE                               
               VARYING TS-APPL-MULT-QUEUE-SEQ                                   
               FROM 1 BY 1                                                      
               UNTIL TS-APPL-MULT-QUEUE-SEQ > PC-MAX-APPL-QUEUE-SEQ.            
      ******************************************************************        
      **  DELETE THE 'MULTIPLE' TEMP STORAGE QUEUES AN APPLICATION    **        
      **  MAY HAVE BUILT.                                             **        
      ******************************************************************        
       8111-DELETE-APPL-MULT-TS-QUEUE.                                          
           IF PV-SYSID = SPACES                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-MULT-QUEUE-ID)                            
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-MULT-QUEUE-ID)                            
                       SYSID (PV-SYSID)                                         
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
      ******************************************************************        
      *  LOGIC NECESSARY TO DELETE THE 'SCA' TS QUEUE.                 *        
      ******************************************************************        
                                                                                
       8200-DELETE-SCA-TS-QUEUE.                                                
           EXEC CICS                                                            
               INQUIRE                                                          
                   TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                          
                   REMOTESYSTEM (PV-NAVIGATOR-SYSID)                            
           END-EXEC.                                                            
                                                                                
           IF PV-NAVIGATOR-SYSID = SPACES                                       
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-SCA-QUEUE-NAME)                                
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-SCA-QUEUE-NAME)                                
                       SYSID (PV-NAVIGATOR-SYSID)                               
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC NECESSARY TO WRITE OUT THE ENTIRE STANDARD COMMAREA     *        
      *  TO THE 'SCA' TS QUEUE PRIOR TO STARTING A TRANSACTION OUT-    *        
      *  SIDE THE ARCHITECTURE.                                        *        
      ******************************************************************        
                                                                                
       8300-WRITE-SCA-TS-QUEUE.                                                 
           EXEC CICS                                                            
               INQUIRE                                                          
                   TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                          
                   REMOTESYSTEM (PV-NAVIGATOR-SYSID)                            
           END-EXEC.                                                            
                                                                                
           IF PV-NAVIGATOR-SYSID = SPACES                                       
               INITIALIZE PV-SYSID-SPECIFIED-LITERAL                            
               EXEC CICS                                                        
                   WRITEQ TS                                                    
                       QUEUE  (TS-SCA-QUEUE-NAME)                               
                       FROM   (DP020-STANDARD-COMMAREA)                         
                       LENGTH (DP020-COMMAREA-LENGTH)                           
                       ITEM   (TS-SCA-QUEUE-ITEM-NUMBER)                        
                       RESP   (PV-CICS-RESPONSE)                                
               END-EXEC                                                         
           ELSE                                                                 
               SET PV-SYSID-SPECIFIED TO TRUE                                   
               EXEC CICS                                                        
                   WRITEQ TS                                                    
                       QUEUE  (TS-SCA-QUEUE-NAME)                               
                       FROM   (DP020-STANDARD-COMMAREA)                         
                       LENGTH (DP020-COMMAREA-LENGTH)                           
                       ITEM   (TS-SCA-QUEUE-ITEM-NUMBER)                        
                       SYSID  (PV-NAVIGATOR-SYSID)                              
                       RESP   (PV-CICS-RESPONSE)                                
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '8300-WRITE-SCA-TS-QUEUE' TO DP013-PARAGRAPH                
               MOVE 'ATTEMPTING TO WRITE A RECORD TO THE SAVED COMMAREA         
      -                  'TEMP STORAGE QUEUE' TO DP013-MESSAGE-TEXT (1)         
               STRING 'QUEUE NAME = ('                DELIMITED BY SIZE         
                      TS-SCA-QUEUE-NAME               DELIMITED BY SIZE         
                      ') '                            DELIMITED BY SIZE         
                      PV-SYSID-SPECIFIED-LITERAL      DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (2)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  UTILITY PARAGRAPGH TO WRITE OUT BUFFER TO TS QUEUE ITEM.      *        
      ******************************************************************        
                                                                                
       8400-WRITE-BUFFER-TO-TS-QUEUE.                                           
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE  (TS-APPL-QUEUE-NAME)                                  
                   FROM   (BB-BLOCK-BUFFER)                                     
                   LENGTH (TS-APPL-QUEUE-LENGTH)                                
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESPONSE = DFHRESP(NORMAL)                                
               ADD 1 TO CA-NMBR-OF-BLKS-WRITTEN-TO-TS                           
               INITIALIZE BB-BLOCK-BUFFER                                       
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '8400-WRITE-BUFFER-TO-TS-QUEUE' TO DP013-PARAGRAPH          
               MOVE 'ATTEMPTING TO WRITE A BUFFER OF MENU ITEMS TO THE A        
      -                  'PPLICATION ' TO DP013-MESSAGE-TEXT (1)                
               STRING 'TEMP STORAGE QUEUE  -  QUEUE NAME = ('                   
                                                      DELIMITED BY SIZE         
                      TS-APPL-QUEUE-NAME              DELIMITED BY SIZE         
                      ')'                             DELIMITED BY SIZE         
                   INTO DP013-MESSAGE-TEXT (2)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  REMOVE ANY OBJECT LOCKS THAT MAY HAVE BEEN SET, BY            *        
      *  DELETING ANY ROWS WITH THAT TASK NUMBER FROM THE LOCK TABLE.  *        
      ******************************************************************        
                                                                                
       8500-DELETE-TASK-RCD.                                                    
           EXEC CICS                                                            
               INQUIRE                                                          
                   TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                          
                   REMOTESYSTEM (PV-NAVIGATOR-SYSID)                            
           END-EXEC.                                                            
                                                                                
           IF PV-NAVIGATOR-SYSID = SPACES                                       
               EXEC CICS                                                        
                   DELETE                                                       
                       FILE   (PC-OBJLOK-AIX-TASK-DD)                           
                       RIDFLD (DP509-TASK-NBR)                                  
                       RESP   (PV-CICS-RESPONSE)                                
               END-EXEC                                                         
           ELSE                                                                 
               MOVE LENGTH OF DP509-OBJLOK-KEY                                  
                                     TO PV-CICS-KEYLENGTH                       
               EXEC CICS                                                        
                   DELETE                                                       
                       FILE      (PC-OBJLOK-AIX-TASK-DD)                        
                       RIDFLD    (DP509-TASK-NBR)                               
                       KEYLENGTH (PV-CICS-KEYLENGTH)                            
                       SYSID     (PV-NAVIGATOR-SYSID)                           
                       RESP      (PV-CICS-RESPONSE)                             
               END-EXEC                                                         
      *                                                                         
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
                           OR DFHRESP(NOTFND)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND       TO TRUE                               
               SET DP013-LINK-RETURN      TO TRUE                               
               MOVE '8500-DELETE-TASK-RCD'                                      
                                          TO DP013-PARAGRAPH                    
               MOVE 'BAD RESPONSE DELETING FROM OBJ LOCK VSAM'                  
                                          TO DP013-MESSAGE-TEXT (1)             
               MOVE DP509-TASK-NBR        TO PV-PRT-TASK-NBR                    
               MOVE PV-PRT-TASK-NBR       TO DP013-MESSAGE-TEXT (2)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  DELETE THE SAVED APPLICATION COMMAREA;                      **        
      **  DELETE A RECORD FROM THE KSDS VSAM FILE.                    **        
      ******************************************************************        
       8550-DELETE-FROM-DPSAVCMK.                                               
           MOVE DP020-NETNAME            TO SM-STACK-KEY-NETNAME.               
           MOVE DP020-STACK-TASK-NUMBER (1)                                     
                                         TO SM-STACK-KEY-TASK.                  
           MOVE SM-STACK-KEY             TO DP515-KEY.                          
      *                                                                         
           EXEC CICS                                                            
               DELETE                                                           
                   FILE      (FD-SAVED-COMMAREA-FILE)                           
                   RIDFLD    (DP515-KEY)                                        
                   RESP      (PV-CICS-RESPONSE)                                 
                   KEYLENGTH (PC-STACK-KEY-LENGTH)                              
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND          TO TRUE                            
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '8550-DELETE-FROM-DPSAVCMK' TO DP013-PARAGRAPH              
               MOVE 'BAD STATUS ON DELETE FROM DPSAVCMK'                        
                                             TO DP013-MESSAGE-TEXT (1)          
               MOVE DP515-KEY                 TO PV-DISPLAY-KEY                 
               MOVE 'KEYLEN='                 TO PV-DISPLAY-FIELD2-HDR          
               MOVE PC-STACK-KEY-LENGTH       TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD2              
               MOVE SPACES                    TO PV-DISPLAY-FIELD2-HDR          
               MOVE SPACES                    TO PV-DISPLAY-FIELD3              
               MOVE PV-DISPLAY-LINE-2         TO DP013-MESSAGE-TEXT (2)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      *  LOGIC NECESSARY TO START THE NAVIGATOR (Z808) TRANSACTION.    *        
      ******************************************************************        
                                                                                
       9000-START-NAVIGATOR.                                                    
           SET DP020-SRC-RETURN-W-O-TRAN-ID TO TRUE.                            
           INITIALIZE DP020-INTER-APPL-DATA-AREA.                               
                                                                                
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID  (PC-NAVIGATOR-TRAN-ID)                              
                   COMMAREA (DP020-STANDARD-COMMAREA)                           
                   LENGTH   (DP020-COMMAREA-LENGTH)                             
                   RESP     (PV-CICS-RESPONSE)                                  
           END-EXEC                                                             
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '9000-START-NAVIGATOR' TO DP013-PARAGRAPH                   
               STRING 'ATTEMPTING TO START THE NAVIGATOR ('                     
                                                     DELIMITED BY SIZE          
                      PC-NAVIGATOR-TRAN-ID           DELIMITED BY SIZE          
                      ')'                            DELIMITED BY SIZE          
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC NECESSARY TO START REQUESTED TRANSACTION.               *        
      ******************************************************************        
                                                                                
       9100-START-DESTINATION-TRAN.                                             
                                                                                
           IF PS-START-WITH-COMMAREA                                            
               EXEC CICS                                                        
                   RETURN IMMEDIATE                                             
                       TRANSID  (SI-SEL-ITEM-TRAN-ID)                           
                       COMMAREA (DP020-STANDARD-COMMAREA)                       
                       LENGTH   (DP020-COMMAREA-LENGTH)                         
                       RESP     (PV-CICS-RESPONSE)                              
               END-EXEC                                                         
               IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                        
                   SET DP013-CICS-ABEND TO TRUE                                 
                   MOVE '9100-START-DESTINATION-TRAN' TO                        
                             DP013-PARAGRAPH                                    
                   STRING 'ATTEMPTING TO START THE SELECTED MENU ITEM'          
                                                     DELIMITED BY SIZE          
                      ' (TRANSID = '                 DELIMITED BY SIZE          
                      SI-SEL-ITEM-TRAN-ID            DELIMITED BY SIZE          
                      ') WITH A COMMAREA'            DELIMITED BY SIZE          
                   INTO DP013-MESSAGE-TEXT (1)                                  
                 PERFORM DP013-0000-PROCESS-ABEND                               
               END-IF                                                           
           ELSE                                                                 
               EXEC CICS                                                        
                   RETURN IMMEDIATE                                             
                       TRANSID (SI-SEL-ITEM-TRAN-ID)                            
                       RESP    (PV-CICS-RESPONSE)                               
               END-EXEC                                                         
               IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                        
                   SET DP013-CICS-ABEND TO TRUE                                 
                   MOVE '9100-START-DESTINATION-TRAN' TO                        
                             DP013-PARAGRAPH                                    
                   STRING 'ATTEMPTING TO START THE SELECTED MENU ITEM'          
                                                     DELIMITED BY SIZE          
                      ' (TRANSID = '                 DELIMITED BY SIZE          
                      SI-SEL-ITEM-TRAN-ID            DELIMITED BY SIZE          
                      ') WITHOUT A COMMAREA'         DELIMITED BY SIZE          
                   INTO DP013-MESSAGE-TEXT (1)                                  
                 PERFORM DP013-0000-PROCESS-ABEND                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC NECESSARY TO CHECK SECURITY ACCESS ON RESOURCES.        *        
      ******************************************************************        
                                                                                
       9200-LINK-TO-SECURITY-MODULE.                                            
           EXEC CICS                                                            
               LINK                                                             
                   PROGRAM  (DP008-SECURITY-SUBROUTINE)                         
                   COMMAREA (DP008-SECURITY-COMMAREA)                           
                   LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)                    
                   RESP     (PV-CICS-RESPONSE)                                  
           END-EXEC.                                                            
                                                                                
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '9200-LINK-TO-SECURITY-MODULE' TO DP013-PARAGRAPH           
               STRING 'ATTEMPTING TO LINK TO THE SECURITY MODULE ('             
                                                     DELIMITED BY SIZE          
                      DP008-SECURITY-SUBROUTINE      DELIMITED BY SIZE          
                      ')'                            DELIMITED BY SIZE          
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  THIS VERSION OF THE PARAGRAPH WHICH USES SYSM0037, HAS BEEN   *        
      *  REPLACED BY ONE THAT USES NEW MODULE DPKCS025.  THE SYSM0037  *        
      *  PROCEDURES HAVE BEEN LEFT IN THE PROGRAM, BUT COMMENTED OUT,  *        
      *  FOR FUTURE REFERENCE IN CASE WE NEED TO MAKE USE OF IT.       *        
      ******************************************************************        
                                                                                
      *9300-LINK-TO-E-MAIL-QUERY.                                               
      *    EXEC CICS                                                            
      *        LINK                                                             
      *            PROGRAM (PC-SYSM-MESSAGE-PROGRAM-NAME)                       
      *            RESP    (PV-CICS-RESPONSE)                                   
      *    END-EXEC.                                                            
      *                                                                         
      *    IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
      *        SET DP013-CICS-ABEND TO TRUE                                     
      *        MOVE '9300-LINK-TO-E-MAIL-QUERY' TO DP013-PARAGRAPH              
      *        STRING 'ATTEMPTING TO LINK TO THE SYSM MESSAGE MODULE ('         
      *                                              DELIMITED BY SIZE          
      *               PC-SYSM-MESSAGE-PROGRAM-NAME   DELIMITED BY SIZE          
      *               ')'                            DELIMITED BY SIZE          
      *            INTO DP013-MESSAGE-TEXT (1)                                  
      *        PERFORM DP013-0000-PROCESS-ABEND                                 
      *    END-IF.                                                              
                                                                                
      ******************************************************************        
      *  LOGIC INVOKED WHEN F13 IS PRESSED. IT WILL LINK TO A PROGRAM  *        
      *  WHICH CHECKS SYSM TO SEE IF AN PENDING MESSAGES OR MEETINGS   *        
      *  ARE WAITING FOR THIS USER.                                    *        
      ******************************************************************        
                                                                                
      *9300-LINK-TO-E-MAIL-QUERY.                                               
      **** EXEC CICS                                                            
      ****     LINK                                                             
      ****         PROGRAM  (PC-E-MAIL-QUERY-PROGRAM-NAME)                      
      ****         COMMAREA (DP020-MESSAGE-AREA)                                
      ****         LENGTH   (PC-MESSAGE-AREA-LENGTH)                            
      ****         RESP     (PV-CICS-RESPONSE)                                  
      **** END-EXEC.                                                            
      ****                                                                      
      **** IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
      ****     SET DP013-CICS-ABEND TO TRUE                                     
      ****     MOVE '9300-LINK-TO-E-MAIL-QUERY' TO DP013-PARAGRAPH              
      ****     STRING 'ATTEMPTING TO LINK TO THE SYSM MESSAGE MODULE ('         
      ****                                           DELIMITED BY SIZE          
      ****            PC-E-MAIL-QUERY-PROGRAM-NAME   DELIMITED BY SIZE          
      ****            ')'                            DELIMITED BY SIZE          
      ****         INTO DP013-MESSAGE-TEXT (1)                                  
      ****     PERFORM DP013-0000-PROCESS-ABEND                                 
      **** END-IF.                                                              
                                                                                
      *                                                                         
      *  FORMAT TITLE LINES ROUTINE.                                            
           COPY DPPD022.                                                        
                                                                                
      *                                                                         
      *  RETURN TO CICS ROUTINE.                                                
           COPY DPPD003.                                                        
                                                                                
      *                                                                         
      *  FORMAT MESSAGE LINE ROUTINE.                                           
           COPY DPPD005.                                                        
                                                                                
      *                                                                         
      *  GET PANEL ROUTINE.                                                     
           COPY DPPD007.                                                        
                                                                                
      *                                                                         
      *  WRITE PANEL ROUTINE.                                                   
           COPY DPPD008.                                                        
                                                                                
      *                                                                         
      *  LOAD FUNCTION KEY LINES ROUTINE.                                       
           COPY DPPD009.                                                        
                                                                                
      *                                                                         
      *  PROCESS ABEND ROUTINE.                                                 
           COPY DPPD013.                                                        
                                                                                
      *                                                                         
      *  GET CURRENT DATE AND TIME ROUTINE.                                     
           COPY DPPD017.                                                        
                                                                                
      *                                                                         
      *  CHECK COMMUNICATIONS AREA MODULE                                       
           COPY DPPD015.                                                        
