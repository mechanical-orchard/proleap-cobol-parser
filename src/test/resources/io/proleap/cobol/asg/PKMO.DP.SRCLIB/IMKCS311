      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    IMKCS311.                                                 
       AUTHOR.        JACK KRAMER.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11-01-05.                                                 
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    I311 - BRAND VENDOR RELATIONSHIP SELECTION                  *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO ENTER DATA TO BE USED BY    *        
      *    SUBSEQUENT PROGRAMS TO ADD OR UPDATE BRAND VENDOR           *        
      *    RELATIONSHIPS.                                              *        
      *----------------------------------------------------------------*        
      *    CHANGE LOG:                                                 *        
      *    DATE  10/25/2021                                            *        
      *               - CHANGE CALL OF 'DPKUT100' TO                   *        
      *                 CALL DP010I-NUMERIC-EDIT-ROUTINE               *        
      *                 MAKING THE CALL DYNAMIC VS STATIC              *        
      *        MADE BY: GERMAN BANDA            WR/IR #: CHG0262978    *        
      *    DATE  01/16/2023                                            *        
      *               - CHANGE CALL OF 'DPKCS030' TO MAKE THE CALL     *        
      *                 DYNAMIC VS STATIC                              *        
      *        MADE BY: JEAN KURK               WR/IR #: CHG0276953    *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-DEPT-STATUS-CDE             PIC  X(01).                       
           05  PV-VEND-STATUS-CDE             PIC  X(01).                       
           05  PV-BRAND-STATUS-CDE            PIC  X(01).                       
           05  PV-ENT-ID                      PIC S9(09)   COMP.                
           05  PV-LBL-SEQ-NBR                 PIC S9(04)   COMP.                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'IM311A  '.         
           05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'IMKM311 '.         
           05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'IMKCS311'.         
           05  PC-UNKNOWN-BRAND-ID       PIC S9(9)   VALUE +2230                
                                                     COMP SYNC.                 
           05  PC-UNKNOWN-VENDOR-ID      PIC S9(9)   VALUE +5161                
                                                          COMP SYNC.            
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
               10  PC-TSYMSG-00564       PIC  9(05)  VALUE  00564.              
               10  PC-TSYMSG-00588       PIC  9(05)  VALUE  00588.              
               10  PC-TSYMSG-02627       PIC  9(05)  VALUE  02627.              
               10  PC-TSYMSG-03064       PIC  9(05)  VALUE  03064.              
               10  PC-TSYMSG-03450       PIC  9(05)  VALUE  03450.              
               10  PC-TSYMSG-03452       PIC  9(05)  VALUE  03452.              
               10  PC-TSYMSG-03453       PIC  9(05)  VALUE  03453.              
               10  PC-TSYMSG-03454       PIC  9(05)  VALUE  03454.              
           05  PC-OPERCO-NBR             PIC  X(02)  VALUE '03'.                
           05  PC-VENDOR-TYPE            PIC  X(01)  VALUE 'M'.                 
           05  PC-ENTNME-TYP-CDE         PIC  X(02)  VALUE '01'.                
           05  PC-DPT-LITERAL            PIC  X(03)  VALUE 'DPT'.               
           05  PC-CHARTC-LITERAL         PIC S9(04)  VALUE +03                  
                                                     COMP SYNC.                 
           05  PC-END-DATE               PIC  X(10)  VALUE '9999-09-09'.        
           05  PC-MAX-RETRIES            PIC S9(03)  VALUE 3                    
                                                          COMP-3.               
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
               88  PS-ERROR                          VALUE  'Y'.                
               88  PS-NO-ERROR                       VALUE  'N'.                
           05  PS-DEPT-FOUND-SW          PIC  X(01)  VALUE  'N'.                
               88  PS-DEPT-FOUND                     VALUE  'Y'.                
               88  PS-DEPT-NOT-FOUND                 VALUE  'N'.                
           05  PS-BRAND-FOUND-SW         PIC  X(01)  VALUE  'N'.                
               88  PS-BRAND-FOUND                    VALUE  'Y'.                
               88  PS-BRAND-NOT-FOUND                VALUE  'N'.                
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY IMKM311.                                                        
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
       EJECT                                                                    
      ******************************************************************        
      ** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
      ** SUB-ROUTINE (DPKUT100).                                      **        
      ******************************************************************        
           COPY DPWS010I.                                                       
       EJECT                                                                    
      ******************************************************************        
      ** PARAMETERS FOR CALLING CICS ARCHITECTURE API                 **        
      ******************************************************************        
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
       EJECT                                                                    
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY IMWS008.                                                        
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR IMKCS311.                                     
      *****************************************************************         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-CURRENT-DATE            PIC  X(10).                  
                   15  ASC-DEPT                    PIC  X(03).                  
                   15  ASC-DEPT-N REDEFINES ASC-DEPT                            
                                                   PIC  9(03).                  
                   15  ASC-DEPT-DESC               PIC  X(25).                  
                   15  ASC-BRAND                   PIC  X(20).                  
                   15  ASC-VENDOR                  PIC  X(30).                  
                   15  ASC-SHOW-ALL-IND            PIC  X(01).                  
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR VENDOR DEPARTMENT                             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TVNDDPT                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR EXTERNAL ENTERPRISE                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR ENTERPRISE NAME                               *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TENTNME                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MERCHANDISE AREA TABLE - TMDSEAR                                     
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TVSCHVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR - TDCVSVL                                               
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDCVSVL                                                  
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF IM311AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
      *                                                                         
           PERFORM 1000-CONTROL-PROCESSING                                      
      *                                                                         
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
      *    CALL 'DPKCS030' USING DFHEIBLK                                       
           CALL DP930-CICS-ARCH-API                                             
                           USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 DP030-CICS-API-FIELDS                          
                                 DP020-STANDARD-COMMAREA                        
                                 IM311AI.                                       
      *                                                                         
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
      *        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEX        
      *             'T LINE'             TO DP013-MESSAGE-TEXT (2)              
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                         TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE 'N'             TO ASC-SHOW-ALL-IND                     
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-FKEY-PROCESSING                                 
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   IF IM008-RETURN-BRAND                                        
                       MOVE IM008-RETURN-DATA     TO ASC-BRAND                  
                   ELSE                                                         
                       IF IM008-RETURN-VENDOR                                   
                           MOVE IM008-RETURN-DATA TO ASC-VENDOR                 
                       END-IF                                                   
                   END-IF                                                       
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO  DP013-PARAGRAPH                    
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO  DP013-MESSAGE-TEXT(1)              
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
      * EDITS.                                                         *        
      *----------------------------------------------------------------*        
                                                                                
       2000-FKEY-PROCESSING.                                                    
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE 'N'             TO ASC-SHOW-ALL-IND                     
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
                       SET DP030-CONTINUE-GO TO TRUE                            
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO TO TRUE                            
                   END-IF                                                       
           END-EVALUATE.                                                        
      ******************************************************************        
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
      ** FROM THE CICS ARCHITECTURE API.                              **        
      ******************************************************************        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-03(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-04(DP020-SRC-AID)                       
                   PERFORM 2110-PREPARE-INTER-APPL-COMM                         
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      ******************************************************************        
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
      ** INTER-APPLICATION COMMAREA.                                  **        
      ******************************************************************        
       2110-PREPARE-INTER-APPL-COMM.                                            
           INITIALIZE IM008-INTER-APPL-COMMAREA.                                
           MOVE ASC-DEPT                 TO IM008-DEPT.                         
           MOVE ASC-DEPT-DESC            TO IM008-DEPT-DESC.                    
           MOVE ASC-BRAND                TO IM008-BRAND.                        
           MOVE ASC-VENDOR               TO IM008-VENDOR.                       
           MOVE ASC-SHOW-ALL-IND         TO IM008-SHOW-ALL-IND.                 
           MOVE PV-ENT-ID                TO IM008-ENT-ID.                       
           MOVE PV-LBL-SEQ-NBR           TO IM008-LBL-SEQ-NBR.                  
       EJECT                                                                    
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
           IF  ADEPTL > ZERO                                                    
               MOVE ADEPTI               TO ASC-DEPT                            
           ELSE                                                                 
               IF  ADEPTF = DP015-ERASE-EOF                                     
                   MOVE SPACE            TO ASC-DEPT                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ABRANDL > ZERO                                                   
               MOVE ABRANDI              TO ASC-BRAND                           
           ELSE                                                                 
               IF  ABRANDF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-BRAND                           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AVENDRL > ZERO                                                   
               MOVE AVENDRI              TO ASC-VENDOR                          
           ELSE                                                                 
               IF  AVENDRF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-VENDOR                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASHOALLL > ZERO                                                  
               MOVE ASHOALLI             TO ASC-SHOW-ALL-IND                    
           ELSE                                                                 
               IF  ASHOALLF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO ASC-SHOW-ALL-IND                    
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
      * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
      * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
      * IN THE DESTINATION PROGRAM, NOT HERE.                          *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
           PERFORM 3200-RESET-ATTRIBUTES.                                       
           IF  ASC-DEPT > SPACE                                                 
               MOVE ASC-DEPT             TO DP010I-UNEDITED-FIELD               
               MOVE LENGTH OF ASC-DEPT   TO DP010I-MAXIMUM-DIGITS               
               MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
               CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
                         USING DP010I-NUMERIC-EDIT-AREA                         
               IF  DP010I-ERROR-DETECTED                                        
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ADEPTA                              
                   MOVE DP015-RED        TO ADEPTC                              
                   MOVE DP015-REVERSE    TO ADEPTH                              
                   MOVE -1               TO ADEPTL                              
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO ASC-DEPT-N                          
                   MOVE ASC-DEPT         TO ADEPTO                              
               END-IF                                                           
           ELSE                                                                 
              SET PS-ERROR                                                      
                  DP020-MSG-FATAL   TO TRUE                                     
              MOVE PC-TSYMSG-02627  TO DP020-MSG-NUMBER                         
              MOVE DP015-UNP-ALP-BRT-OFF                                        
                                    TO ADEPTA                                   
              MOVE DP015-RED        TO ADEPTC                                   
              MOVE DP015-REVERSE    TO ADEPTH                                   
              MOVE -1               TO ADEPTL                                   
              SET DP030-SET-CURSOR-APPL-1 TO TRUE                               
           END-IF.                                                              
                                                                                
           IF PS-NO-ERROR                                                       
              SET PS-DEPT-NOT-FOUND      TO TRUE                                
              PERFORM 5000-GET-DEPT-DESC                                        
              IF PS-DEPT-NOT-FOUND                                              
                  SET PS-ERROR                                                  
                      DP020-MSG-FATAL   TO TRUE                                 
                  MOVE PC-TSYMSG-00588  TO DP020-MSG-NUMBER                     
                  MOVE DP015-UNP-ALP-BRT-OFF                                    
                                        TO ADEPTA                               
                  MOVE DP015-RED        TO ADEPTC                               
                  MOVE DP015-REVERSE    TO ADEPTH                               
                  MOVE -1               TO ADEPTL                               
                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                           
              END-IF                                                            
           END-IF.                                                              
           IF PS-NO-ERROR                                                       
              IF ASC-SHOW-ALL-IND = 'Y' OR 'N'                                  
                  CONTINUE                                                      
              ELSE                                                              
                  SET PS-ERROR                                                  
                      DP020-MSG-FATAL   TO TRUE                                 
                  MOVE PC-TSYMSG-03064  TO DP020-MSG-NUMBER                     
                  MOVE DP015-UNP-ALP-BRT-OFF                                    
                                        TO ASHOALLA                             
                  MOVE DP015-RED        TO ASHOALLC                             
                  MOVE DP015-REVERSE    TO ASHOALLH                             
                  MOVE -1               TO ASHOALLL                             
                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                           
              END-IF                                                            
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
             EVALUATE TRUE                                                      
               WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
               WHEN DP020-FK-LOCAL-FUNC-02(DP020-SRC-AID)                       
                   PERFORM 3110-B-AND-V-VALID-OR-BLANK                          
                                                                                
               WHEN OTHER                                                       
                   CONTINUE                                                     
             END-EVALUATE                                                       
           END-IF.                                                              
                                                                                
      *                                                                         
           IF  PS-NO-ERROR                                                      
               MOVE -1                   TO ADEPTL                              
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           END-IF.                                                              
      *----------------------------------------------------------------*        
      * ENSURE THAT BRAND AND VENDOR ARE VALID, IF THEY ARE ENTERED    *        
      *----------------------------------------------------------------*        
       3110-B-AND-V-VALID-OR-BLANK.                                             
           IF ASC-BRAND NOT = SPACES                                            
               PERFORM 5100-VALIDATE-BRAND                                      
           END-IF.                                                              
           IF  PS-NO-ERROR                                                      
               IF ASC-VENDOR NOT = SPACES                                       
                   PERFORM 5200-VALIDATE-VENDOR                                 
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      ** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
      ** MAP.                                                         **        
      ******************************************************************        
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ADEPTA                              
                                            ABRANDA                             
                                            AVENDRA                             
                                            ASHOALLA.                           
           MOVE DP015-GREEN              TO ADEPTC                              
                                            ABRANDC                             
                                            AVENDRC                             
                                            ASHOALLC.                           
           MOVE DP015-UNDERLINE          TO ADEPTH                              
                                            ABRANDH                             
                                            AVENDRH                             
                                            ASHOALLH.                           
      *                                                                         
      *----------------------------------------------------------------*        
      *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
      *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
      *  IT HERE.                                                      *        
      *----------------------------------------------------------------*        
       4000-BUILD-INITIAL-PANEL.                                                
           EXEC SQL                                                             
               SET  :ASC-CURRENT-DATE = CURRENT DATE                            
           END-EXEC.                                                            
           SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
           MOVE -1                       TO ADEPTL.                             
      *                                                                         
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           MOVE ASC-DEPT-N               TO ADEPTO.                             
           MOVE ASC-DEPT-DESC            TO ADESCO.                             
           MOVE ASC-BRAND                TO ABRANDO.                            
           MOVE ASC-VENDOR               TO AVENDRO.                            
           MOVE ASC-SHOW-ALL-IND         TO ASHOALLO.                           
      *----------------------------------------------------------------*        
      *  GET DEPARTMENT DESCRIPTION                                             
      *----------------------------------------------------------------*        
       5000-GET-DEPT-DESC.                                                      
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                    SELECT MDSEAR_DESC                                          
                     INTO  :MDSEAR-DESC                                         
                     FROM  TMDSEAR                                              
                    WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                       
                      AND  MDSELV_CDE    = :PC-DPT-LITERAL                      
                      AND  END_DTE       = :PC-END-DATE                         
                      AND  DEPT_NBR      = :ASC-DEPT                            
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5000-GET-DEPT-DESC'                                
                                         TO DP013-PARAGRAPH                     
                       MOVE 'SELECT DEPARTMENT DESCRIPTION:'                    
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-DEPT     TO DP013-MESSAGE-TEXT (2)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TMDSEAR'    TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5000-GET-DEPT-DESC' TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'DEPT DESCRIPTION FOR:'                                     
                                         TO DP013-MESSAGE-TEXT (2)              
               MOVE ASC-DEPT             TO DP013-MESSAGE-TEXT (3)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TMDSEAR'            TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
           IF SQLCODE = ZERO                                                    
              SET PS-DEPT-FOUND          TO TRUE                                
              MOVE MDSEAR-DESC           TO ASC-DEPT-DESC                       
           ELSE                                                                 
              MOVE SPACES                TO ASC-DEPT-DESC                       
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  READ BRAND STATUS FOR VALIDATION                                       
      *----------------------------------------------------------------*        
       5100-VALIDATE-BRAND.                                                     
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT C.STAT_CDE                                              
                       ,C.CHARTC_SEQ_NBR                                        
                   INTO :PV-BRAND-STATUS-CDE                                    
                       ,:PV-LBL-SEQ-NBR                                         
                   FROM TMDSEAR A                                               
                       ,TDCVSVL B                                               
                       ,TVSCHVL C                                               
                  WHERE A.OPERCO_NBR      = :PC-OPERCO-NBR                      
                    AND A.MDSELV_CDE      = :PC-DPT-LITERAL                     
                    AND A.END_DTE         > :ASC-CURRENT-DATE                   
                    AND A.MDSEAR_DKEY     = B.MDSEAR_ID                         
                    AND C.CHARTC_CDE      = :PC-CHARTC-LITERAL                  
                    AND B.CHARTC_CDE      = C.CHARTC_CDE                        
                    AND A.DEPT_NBR        = :ASC-DEPT                           
                    AND B.CHARTC_SEQ_NBR  = C.CHARTC_SEQ_NBR                    
                    AND B.CHARTC_SEQ_NBR <> :PC-UNKNOWN-BRAND-ID                
                    AND C.CHARTC_VAL_DESC = :ASC-BRAND                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5100-VALIDATE-BRAND'                               
                                         TO DP013-PARAGRAPH                     
                       MOVE 'VALIDATE BRAND:'                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-BRAND    TO DP013-MESSAGE-TEXT (2)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TMDSEAR'    TO DP013-DB2-TABLE-NAME (1)            
                       MOVE 'TDCVSVL'    TO DP013-DB2-TABLE-NAME (2)            
                       MOVE 'TVSCHVL'    TO DP013-DB2-TABLE-NAME (3)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5100-VALIDATE-BRAND' TO DP013-PARAGRAPH                    
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'BRAND STATUS FOR'   TO DP013-MESSAGE-TEXT (2)              
               MOVE ASC-BRAND            TO DP013-MESSAGE-TEXT (3)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TMDSEAR'            TO DP013-DB2-TABLE-NAME (1)            
               MOVE 'TDCVSVL'            TO DP013-DB2-TABLE-NAME (2)            
               MOVE 'TVSCHVL'            TO DP013-DB2-TABLE-NAME (3)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03452      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ABRANDA                             
                   MOVE DP015-RED        TO ABRANDC                             
                   MOVE DP015-REVERSE    TO ABRANDH                             
                   MOVE -1               TO ABRANDL                             
               ELSE                                                             
                   IF SQLCODE = ZERO                                            
                       EVALUATE TRUE                                            
                         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)             
                             IF PV-BRAND-STATUS-CDE NOT = 'A'                   
                                 SET PS-ERROR                                   
                                     DP020-MSG-FATAL                            
                                     DP030-SET-CURSOR-APPL-1 TO TRUE            
                                 MOVE PC-TSYMSG-03453                           
                                                   TO DP020-MSG-NUMBER          
                                 MOVE DP015-UNP-ALP-BRT-OFF                     
                                                       TO ABRANDA               
                                 MOVE DP015-RED        TO ABRANDC               
                                 MOVE DP015-REVERSE    TO ABRANDH               
                                 MOVE -1               TO ABRANDL               
                             END-IF                                             
                         WHEN OTHER                                             
                             CONTINUE                                           
                       END-EVALUATE                                             
                   ELSE                                                         
                       MOVE '5100-VALIDATE-BRAND' TO DP013-PARAGRAPH            
                       MOVE 'INVALID RETURN ATTEMPTING TO SELECT'               
                                            TO DP013-MESSAGE-TEXT (1)           
                       MOVE 'BRAND STATUS FOR' TO DP013-MESSAGE-TEXT(2)         
                       MOVE ASC-BRAND      TO DP013-MESSAGE-TEXT(3)             
                       MOVE SQLCA          TO DP013-SQLCA                       
                       MOVE 'TMDSEAR'      TO DP013-DB2-TABLE-NAME(1)           
                       MOVE 'TDCVSVL'      TO DP013-DB2-TABLE-NAME(2)           
                       MOVE 'TVSCHVL'      TO DP013-DB2-TABLE-NAME(3)           
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                           TO TRUE                              
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *----------------------------------------------------------------*        
      *  READ VENDOR AND DEPARTMENT STATUSES FOR VALIDATION                     
      *----------------------------------------------------------------*        
       5200-VALIDATE-VENDOR.                                                    
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO)                                    
                         OR (SQLCODE = +100))                                   
                                                                                
               EXEC SQL                                                         
                 SELECT A.VND_DEPT_STAT_CD                                      
                       ,B.STATUS_CDE                                            
                       ,C.ENT_ID                                                
                   INTO :PV-DEPT-STATUS-CDE                                     
                       ,:PV-VEND-STATUS-CDE                                     
                       ,:PV-ENT-ID                                              
                   FROM TVNDDPT A                                               
                       ,TEXTENT B                                               
                       ,TENTNME C                                               
                  WHERE A.DEPT_NBR        = :ASC-DEPT                           
                    AND A.VEND_TYP_IND    = :PC-VENDOR-TYPE                     
                    AND C.ENT_NAME_DESC   = :ASC-VENDOR                         
                    AND B.ENT_ID          = A.ENT_ID                            
                    AND C.ENT_ID          = A.ENT_ID                            
                    AND B.ENT_ID         <> :PC-UNKNOWN-VENDOR-ID               
                    AND C.TYPE_CDE        = :PC-ENTNME-TYP-CDE                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5200-VALIDATE-VENDOR'                              
                                         TO DP013-PARAGRAPH                     
                       MOVE 'VALIDATE VENDOR:'                                  
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE ASC-VENDOR   TO DP013-MESSAGE-TEXT (2)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TVNDDPT'    TO DP013-DB2-TABLE-NAME (1)            
                       MOVE 'TEXTENT'    TO DP013-DB2-TABLE-NAME (2)            
                       MOVE 'TENTNME'    TO DP013-DB2-TABLE-NAME (3)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5200-VALIDATE-VENDOR' TO DP013-PARAGRAPH                   
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'VENDOR STATUS FOR'  TO DP013-MESSAGE-TEXT (2)              
               MOVE ASC-VENDOR           TO DP013-MESSAGE-TEXT (3)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TVNDDPT'            TO DP013-DB2-TABLE-NAME (1)            
               MOVE 'TEXTENT'            TO DP013-DB2-TABLE-NAME (2)            
               MOVE 'TENTNME'            TO DP013-DB2-TABLE-NAME (3)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL                                          
                       DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE PC-TSYMSG-03450      TO DP020-MSG-NUMBER                
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AVENDRA                             
                   MOVE DP015-RED        TO AVENDRC                             
                   MOVE DP015-REVERSE    TO AVENDRH                             
                   MOVE -1               TO AVENDRL                             
               ELSE                                                             
                   IF SQLCODE = ZERO                                            
                       EVALUATE TRUE                                            
                         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)             
                             IF PV-VEND-STATUS-CDE NOT = 'A'                    
                                 SET PS-ERROR                                   
                                     DP020-MSG-FATAL                            
                                     DP030-SET-CURSOR-APPL-1 TO TRUE            
                                 MOVE PC-TSYMSG-00564                           
                                                 TO DP020-MSG-NUMBER            
                                 MOVE DP015-UNP-ALP-BRT-OFF                     
                                                       TO AVENDRA               
                                 MOVE DP015-RED        TO AVENDRC               
                                 MOVE DP015-REVERSE    TO AVENDRH               
                                 MOVE -1               TO AVENDRL               
                             ELSE                                               
                                 IF PV-DEPT-STATUS-CDE NOT = 'A'                
                                     SET PS-ERROR                               
                                         DP020-MSG-FATAL                        
                                         DP030-SET-CURSOR-APPL-1 TO TRUE        
                                     MOVE PC-TSYMSG-03454                       
                                                     TO DP020-MSG-NUMBER        
                                     MOVE DP015-UNP-ALP-BRT-OFF                 
                                                           TO AVENDRA           
                                     MOVE DP015-RED        TO AVENDRC           
                                     MOVE DP015-REVERSE    TO AVENDRH           
                                     MOVE -1               TO AVENDRL           
                                 END-IF                                         
                             END-IF                                             
                         WHEN OTHER                                             
                             CONTINUE                                           
                       END-EVALUATE                                             
                   ELSE                                                         
                       MOVE '5200-VALIDATE-VENDOR' TO DP013-PARAGRAPH           
                       MOVE 'INVALID RETURN ATTEMPTING TO SELECT'               
                                            TO DP013-MESSAGE-TEXT (1)           
                       MOVE 'VENDOR STATUS FOR' TO DP013-MESSAGE-TEXT(2)        
                       MOVE ASC-VENDOR     TO DP013-MESSAGE-TEXT(3)             
                       MOVE SQLCA          TO DP013-SQLCA                       
                       MOVE 'TVNDDPT'      TO DP013-DB2-TABLE-NAME(1)           
                       MOVE 'TEXTENT'      TO DP013-DB2-TABLE-NAME(2)           
                       MOVE 'TENTNME'      TO DP013-DB2-TABLE-NAME(3)           
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                           TO TRUE                              
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
