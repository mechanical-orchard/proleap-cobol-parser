       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    CWKUT040.                                                 
       AUTHOR.        THOMAS DUFFY.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07/15/05.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM CREATES THE CW DAILY ON-ORDER EXTRACT FILE FROM THE        
      * PO ON-ORDER EXTRACT FILE.                                               
      ******************************************************************        
TD0406*    CHANGED: 04/25/06       BY: THOMAS DUFFY   WR: 30444                 
TD0406*    ASSIGNMENT OF WEEKLY PO ARRIVAL CODE CHANGED TO USE                  
TD0406*    PO018-ONORD-IN-DC-DATE INSTEAD OF PO018-ONORD-WEEK-DATE.             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT PROCESS-DATE-CONTROL-CARD                                     
                                    ASSIGN TO INP01.                            
           SELECT PO-ONORD-EXT-FILE ASSIGN TO INP02.                            
           SELECT CW-ONORD-EXT-FILE ASSIGN TO OUT01.                            
                                                                                
      ****************************************************************          
      * DATA DIVISION:                                                          
      ****************************************************************          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PROCESS-DATE-CONTROL-CARD                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PROCESS-DATE-CONTROL-RECORD      PIC X(80).                          
                                                                                
       FD  PO-ONORD-EXT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PO-ONORD-EXT-RECORD              PIC X(104).                         
                                                                                
       FD  CW-ONORD-EXT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CW-ONORD-EXT-RECORD              PIC X(62).                          
                                                                                
      ****************************************************************          
      * WORKING STORAGE:                                                        
      ****************************************************************          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'CWKUT040'.        
           05  PC-PAST-ARRVL                PIC X(01)  VALUE 'P'.               
           05  PC-CURRENT-ARRVL             PIC X(01)  VALUE 'C'.               
           05  PC-FUTURE-ARRVL              PIC X(01)  VALUE 'F'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA-NAME                 PIC X(30)  VALUE SPACES.            
           05  PV-4-WEEKS-OUT-DATE          PIC X(10)  VALUE SPACES.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05 PA-INPUT-REC-COUNT            PIC S9(9)  COMP-3 VALUE 0.          
           05 PA-OUTPUT-REC-COUNT           PIC S9(9)  COMP-3 VALUE 0.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-PO-ONORD-EOF-SW           PIC X(01)  VALUE 'N'.               
               88  PS-PO-ONORD-EOF                     VALUE 'Y'.               
           05  PS-EOF-PROCESS-DATE-CC-SW    PIC X(01)  VALUE 'N'.               
               88  PS-EOF-PROCESS-DATE-CC              VALUE 'Y'.               
                                                                                
       01  PD-PROCESS-DATE-RECORD.                                              
           05  PD-PROCESS-DATE              PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(70)  VALUE SPACES.            
                                                                                
      ********************************************************                  
      * INPUT RECORD LAYOUT - PO ON-ORDER EXTRACT FILE                          
      ********************************************************                  
           COPY PORD018.                                                        
                                                                                
      ********************************************************                  
      * OUTPUT RECORD LAYOUT - CW ON-ORDER EXTRACT FILE                         
      ********************************************************                  
           COPY CWRD040.                                                        
                                                                                
      ******************************************************************        
      *  PROCEDURE DIVISION.                                                    
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-SECTION.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-EXTRACT-FILE                                    
               UNTIL PS-PO-ONORD-EOF.                                           
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  SET UP FOR PROGRAM PROCESSING:                                         
      *    OPEN ALL FILES.                                                      
      *    READ THE DATE CONTROL CARD.                                          
      *    DETERMINE THE 4-WEEKS-OUT-DATE.                                      
      *    READ THE FIRST PO ON-ORDER EXTRACT RECORD.                           
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  PROCESS-DATE-CONTROL-CARD                                
                       PO-ONORD-EXT-FILE                                        
                OUTPUT CW-ONORD-EXT-FILE.                                       
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
                                                                                
           PERFORM 1100-READ-PROCESS-DATE-CC.                                   
                                                                                
           PERFORM 7000-READ-PO-ONORD-EXT-FILE.                                 
                                                                                
      ******************************************************************        
      *  READ THE DATE CONTROL CARD.                                   *        
      ******************************************************************        
       1100-READ-PROCESS-DATE-CC.                                               
           MOVE '1100-READ-PROCESS-DATE-CC' TO PV-PARA-NAME                     
                                                                                
           READ PROCESS-DATE-CONTROL-CARD                                       
               INTO PD-PROCESS-DATE-RECORD                                      
               AT END                                                           
                   SET PS-EOF-PROCESS-DATE-CC TO TRUE                           
           END-READ.                                                            
                                                                                
           IF PS-EOF-PROCESS-DATE-CC                                            
               DISPLAY '** ABEND     **'                                        
               DISPLAY '** PROGRAM   = ' PC-PROGRAM-NAME ' **'                  
               DISPLAY '** PARAGRAPH = ' PV-PARA-NAME ' **'                     
               DISPLAY '** EMPTY CONTROL CARD **'                               
               MOVE +4008 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               DISPLAY '** PROCESSING DATE: ' PD-PROCESS-DATE                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  PROCESS THE PO ON-ORDER EXTRACT FILE.                                  
      *  FORMAT AND WRITE A CW ON-ORDER EXTRACT RECORD.                         
      *  WHEN FORMATTING THE RECORD, COMPARE THE IN-DC-DATE WITH THE            
      *    CURRENT PROCESSING DATE TO DETERMINE THE IN-DC-DTE AND               
      *    THE PO-ARRVL-CDE VALUES - BOTH DAILY AND WEEKLY                      
      ******************************************************************        
       2000-PROCESS-EXTRACT-FILE.                                               
                                                                                
           INITIALIZE CW040-ONORD-EXTRACT-RECORD.                               
                                                                                
           MOVE PO018-ONORD-ITEM-NBR        TO CW040-ONORD-ITM-NBR.             
           MOVE PO018-ONORD-STORE-NBR       TO CW040-ONORD-PHY-SITE-ID.         
           MOVE PO018-ONORD-PO-NBR          TO CW040-ONORD-PO-NBR.              
           MOVE PO018-ONORD-PO-TYP-CDE      TO CW040-ONORD-PO-TYP-CDE.          
           MOVE PO018-ONORD-UNIT-QTY        TO CW040-ONORD-UNT-QTY.             
           MOVE PO018-ONORD-COST-AMT        TO CW040-ONORD-COST-AMT.            
           MOVE PO018-ONORD-RTL-AMT         TO CW040-ONORD-RTL-AMT.             
                                                                                
           IF PO018-ONORD-IN-DC-DATE > PD-PROCESS-DATE                          
               MOVE PO018-ONORD-IN-DC-DATE                                      
                                       TO CW040-ONORD-DLY-IN-DC-DTE             
TD0406                                    CW040-ONORD-WKLY-IN-DC-DTE            
               MOVE PC-FUTURE-ARRVL    TO CW040-ONORD-DLY-PO-ARRVL-CDE          
TD0406                                    CW040-ONORD-WKLY-PO-ARRVL-CDE         
           ELSE                                                                 
               IF PO018-ONORD-IN-DC-DATE = PD-PROCESS-DATE                      
                  MOVE PD-PROCESS-DATE TO CW040-ONORD-DLY-IN-DC-DTE             
TD0406                                    CW040-ONORD-WKLY-IN-DC-DTE            
                  MOVE PC-CURRENT-ARRVL TO CW040-ONORD-DLY-PO-ARRVL-CDE         
TD0406                                    CW040-ONORD-WKLY-PO-ARRVL-CDE         
               ELSE                                                             
                  MOVE PD-PROCESS-DATE TO CW040-ONORD-DLY-IN-DC-DTE             
TD0406                                    CW040-ONORD-WKLY-IN-DC-DTE            
                  MOVE PC-PAST-ARRVL TO CW040-ONORD-DLY-PO-ARRVL-CDE            
TD0406                                  CW040-ONORD-WKLY-PO-ARRVL-CDE           
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 8000-WRITE-OUTPUT-RECORD.                                    
                                                                                
           PERFORM 7000-READ-PO-ONORD-EXT-FILE.                                 
                                                                                
      ******************************************************************        
      *  READ A PO ON-ORDER EXTRACT RECORD.                                     
      ******************************************************************        
       7000-READ-PO-ONORD-EXT-FILE.                                             
                                                                                
           READ PO-ONORD-EXT-FILE INTO PO018-ONORD-EXTRACT-RECORD               
             AT END                                                             
                MOVE 'Y' TO PS-PO-ONORD-EOF-SW.                                 
                                                                                
           IF NOT PS-PO-ONORD-EOF                                               
                ADD 1 TO PA-INPUT-REC-COUNT.                                    
                                                                                
      ******************************************************************        
      *  WRITE AN CW ON-ORDER EXTRACT RECORD.                                   
      ******************************************************************        
       8000-WRITE-OUTPUT-RECORD.                                                
                                                                                
           WRITE CW-ONORD-EXT-RECORD                                            
               FROM CW040-ONORD-EXTRACT-RECORD.                                 
                                                                                
           ADD 1 TO PA-OUTPUT-REC-COUNT.                                        
                                                                                
      ******************************************************************        
      *  DISPLAY CONTROL TOTALS AND CLOSE ALL FILES.                            
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
                                                                                
           DISPLAY ' PO ON-ORDER RECORDS READ:    ' PA-INPUT-REC-COUNT.         
           DISPLAY ' CW ON-ORDER RECORDS WRITTEN: ' PA-OUTPUT-REC-COUNT.        
                                                                                
           CLOSE PROCESS-DATE-CONTROL-CARD                                      
                 PO-ONORD-EXT-FILE                                              
                 CW-ONORD-EXT-FILE.                                             
                                                                                
