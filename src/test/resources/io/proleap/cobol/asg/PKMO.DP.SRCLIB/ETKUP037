00001  IDENTIFICATION DIVISION.                                         05/15/95
00002                                                                   ETKUP037
00003                                                                      LV004
00004  PROGRAM-ID.    ETKUP037.                                            CL**4
00005  AUTHOR.        STEVE FLUNKER.                                       CL**1
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00007  DATE-WRITTEN.  04-14-95.                                            CL**1
00008  DATE-COMPILED.                                                      CL**1
00009                                                                      CL**1
00010 ******************************************************************   CL**1
00011 *      ETKUP037 - INV RECYCLE (INSERT/UPDATE)                        CL**4
00012 *                                                                    CL**1
00013 * THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.         CL**1
00014 * IT READS THE PREPARED EDI INVOICES TO BE RECYLCLED FILE FROM       CL**1
00015 * ETKUT037. FOR EACH RECORD READ, IT INSERTS A ROW INTO THE          CL**4
00016 * APPROPRIATE TABLE.                                                 CL**1
00017 *                                                                    CL**1
00018 ******************************************************************   CL**1
260107*  CHANGED 08/12/2021    BY JIM HUNTER      WR: CHG0260107                
260107*                                                                         
260107* ADDED COPYBOOK DPWS991 TO CHANGE CALL TO DPKUT901 FROM STATIC           
260107* TO DYNAMIC.                                                             
260107******************************************************************   CL**1
00019                                                                      CL**1
00020                                                                      CL**1
00021                                                                      CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023                                                                      CL**1
00024                                                                      CL**1
00025  CONFIGURATION SECTION.                                              CL**1
00026                                                                      CL**1
00027  SOURCE-COMPUTER.    IBM-3090.                                       CL**1
00028  OBJECT-COMPUTER.    IBM-3090.                                       CL**1
00029                                                                      CL**1
00030                                                                      CL**1
00031  INPUT-OUTPUT SECTION.                                               CL**1
00032                                                                      CL**1
00033  FILE-CONTROL.                                                       CL**1
00034                                                                      CL**1
00035  DATA DIVISION.                                                      CL**1
00036                                                                      CL**1
00037                                                                      CL**1
00038  WORKING-STORAGE SECTION.                                            CL**1
00039                                                                      CL**1
00040  01  ABEND-CODE                       PIC S9(04)    COMP SYNC        CL**1
00041                                                     VALUE +0.        CL**1
00042  01  INV-INSERT-RECORD.                                              CL**1
00043      05 I-TABLE-NAME              PIC X(07).                         CL**1
00044      05 I-TP-DKEY                 PIC S9(9) COMP.                    CL**1
00045      05 I-DATA-AREA               PIC X(354).                        CL**1
00046      05 I-TFGTRHL-AREA  REDEFINES I-DATA-AREA.                       CL**1
00047         10  I-TRN-SET-CDE         PIC X(3).                          CL**1
00048         10  I-TRN-DTL-TXT         PIC X(22).                         CL**2
00049         10  I-RLSE-IND            PIC X(01).                         CL**1
00050         10  I-RLSE-DTE            PIC X(10).                         CL**1
00051         10  I-USER-ID             PIC X(08).                         CL**1
00052         10  FILLER                PIC X(310).                        CL**2
00053      05 I-TTRPOHL-AREA  REDEFINES I-DATA-AREA.                       CL**1
00054         10  I-PO-NBR              PIC 9(9).                          CL**1
00055         10  I-DEPT-NBR            PIC X(03).                         CL**1
00056         10  I-PO-RLSE-IND         PIC X(01).                         CL**1
00057         10  FILLER                PIC X(341).                        CL**1
00058      05 I-TTRTRDT-AREA  REDEFINES I-DATA-AREA.                       CL**1
00059         10  I-TRN-SET-SEQ-NBR     PIC S9(9) COMP.                    CL**1
00060         10  I-TRN-SET-DATA        PIC X(350).                        CL**1
00061                                                                      CL**1
00062  01  WS-SAVE-TIMESTAMP                PIC X(26) VALUE SPACES.        CL**1
00063  01  WS-INV-NBR-CONVERT.                                             CL**2
00064      05  WS-INV-NBR                   PIC X(22) VALUE SPACES.        CL**2
00065      05  FILLER                       PIC X(08) VALUE SPACES.        CL**2
00066  01  WS-PO-NBR-22.                                                   CL**2
00067      05  WS-PO-NBR                    PIC X(09) VALUE SPACES.        CL**2
00068      05  FILLER                       PIC X(13) VALUE SPACES.        CL**2
00069                                                                      CL**1
00070  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00071      05  PC-MAX-RETRIES               PIC S9(04)    COMP SYNC        CL**1
00072                                                     VALUE +3.        CL**1
00073      05  PC-MAX-DEADLOCKS             PIC S9(04)    COMP SYNC        CL**1
00074                                                     VALUE +3.        CL**1
00075      05  PC-CKPT-JOB-NAME             PIC  X(08)    VALUE            CL**1
00076                                                          'ETK140'.   CL**4
00077      05  PC-CKPT-PLAN-NAME            PIC  X(08)    VALUE            CL**1
00078                                                        'ETKUP037'.   CL**4
00079      05  PC-TRAN-PRES-FUNC-CODES.                                    CL**1
00080          10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05)    VALUE 'OPEN'.    CL**1
00081          10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05)    VALUE 'TRANS'.   CL**1
00082          10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05)    VALUE 'RSTRT'.   CL**1
00083          10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05)    VALUE 'CLOSE'.   CL**1
00084                                                                      CL**1
00085  01  PS-PROGRAM-SWITCHES.                                            CL**1
00086      05  PS-RESTART-REQUIRED-SW       PIC X         VALUE 'N'.       CL**1
00087          88  PS-RESTART-REQUIRED                    VALUE 'Y'.       CL**1
00088          88  PS-RESTART-NOT-REQUIRED                VALUE 'N'.       CL**1
00089                                                                      CL**1
00090  01  PV-PROGRAM-VARIABLES.                                           CL**1
00091      05  PV-SQL-SUB                   PIC 9(03)     VALUE ZERO.      CL**1
00092      05  PV-DEADLOCK-COUNTER          PIC 9(03)     VALUE ZERO.      CL**1
00093      05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.     CL**1
00094      05  PV-DB2-OPER-ATTEMPTED        PIC X(30)     VALUE SPACE.     CL**1
00095                                                                      CL**1
00096 *****************************************************************    CL**1
00097 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD TABLE.                       CL**1
00098 *****************************************************************    CL**1
00099                                                                      CL**1
00100      EXEC SQL                                                        CL**1
00101          INCLUDE TFGTRHL                                             CL**1
00102      END-EXEC.                                                       CL**1
00103                                                                      CL**1
00104 *****************************************************************    CL**1
00105 *  DCLGEN FOR THE PO HELD TABLE.                                     CL**1
00106 *****************************************************************    CL**1
00107                                                                      CL**1
00108      EXEC SQL                                                        CL**1
00109          INCLUDE TTRPOHL                                             CL**1
00110      END-EXEC.                                                       CL**1
00111                                                                      CL**1
00112 *****************************************************************    CL**1
00113 *  DCLGEN FOR THE FUNCTIONAL GROUP HELD DATA TABLE.                  CL**1
00114 *****************************************************************    CL**1
00115                                                                      CL**1
00116      EXEC SQL                                                        CL**1
00117          INCLUDE TFGTRDT                                             CL**1
00118      END-EXEC.                                                       CL**1
00119                                                                      CL**1
00120 *****************************************************************    CL**1
00121 *  COMMUNICATIONS AREA FOR DB2                                       CL**1
00122 *****************************************************************    CL**1
00123                                                                      CL**1
00124      COPY SQLCA2.                                                    CL**1
00125                                                                      CL**1
00126                                                                      CL**1
00127 *****************************************************************    CL**1
00128 *  DB2 FORMATTED ERROR MESSAGE                                       CL**1
00129 *****************************************************************    CL**1
00130                                                                      CL**1
00131      COPY DPWS004.                                                   CL**1
00132                                                                      CL**1
00133                                                                      CL**1
00134 *****************************************************************    CL**1
00135 *  TRANSACTION PRESENTATION MODULE COMMUNICATIONS AREA               CL**1
00136 *****************************************************************    CL**1
00137                                                                      CL**1
260107     COPY DPWS991.                                                   CL**1
00137                                                                      CL**1
00138      COPY DPLS001.                                                   CL**1
00139      05  WORKING-STORAGE-PASSED.                                     CL**1
00140          10  FILLER                   PIC X.                         CL**1
00141                                                                      CL**1
00142  01  FILLER                           PIC  X(4096)  VALUE SPACE.     CL**1
00143                                                                      CL**1
00144 ******************************************************************   CL**1
00145 *  COMMUNICATIONS AREA FOR DB2                                   *   CL**1
00146 ******************************************************************   CL**1
00147                                                                      CL**1
00148      EXEC SQL                                                        CL**1
00149          INCLUDE SQLCA                                               CL**1
00150      END-EXEC.                                                       CL**1
00151  EJECT                                                               CL**1
00152  PROCEDURE DIVISION.                                                 CL**1
00153                                                                      CL**1
00154                                                                      CL**1
00155  A100-PROGRAM-MAINLINE.                                              CL**1
00156                                                                      CL**1
00157                                                                      CL**1
00158      MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.                 CL**1
00159      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**1
00160                                                                      CL**1
00161      PERFORM B200-PROCESS-INV-RECYCLE-RECS                           CL**1
00162          UNTIL DP001-PREP-TRANS-EOF.                                 CL**1
00163                                                                      CL**1
00164      MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.                CL**1
00165      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**1
00166                                                                      CL**1
00167                                                                      CL**1
00168      STOP RUN.                                                       CL**1
00169                                                                      CL**1
00170                                                                      CL**1
00171 ******************************************************************   CL**1
00172 * PROCESS THE RECORDS ON THE INV RECLYCLE FILE. FOR EACH             CL**1
00173 * RECORD ON THE FILE, BUILD AND INSERT A ROW IN THE APPROPRIATE      CL**1
00174 * TABLE.                                                             CL**1
00175 ******************************************************************   CL**1
00176  B200-PROCESS-INV-RECYCLE-RECS.                                      CL**1
00177                                                                      CL**1
00178      SET PS-RESTART-NOT-REQUIRED TO TRUE.                            CL**1
00179                                                                      CL**1
00180      PERFORM C100-BUILD-INSERT-ROW.                                  CL**1
00181                                                                      CL**1
00182 *    *------------------------------------------------------------   CL**1
00183 *    * IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN    CL**1
00184 *    * A TRANS CALL. THIS WILL ALLOW PROCESSING TO RESUME WITH       CL**1
00185 *    * THE FIRST TRANSACTION AFTER THE LAST COMMIT.                  CL**1
00186 *    *------------------------------------------------------------   CL**1
00187      IF PS-RESTART-REQUIRED                                          CL**1
00188          MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE             CL**1
00189      ELSE                                                            CL**1
00190          MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE             CL**1
00191      END-IF.                                                         CL**1
00192                                                                      CL**1
00193      PERFORM Z100-BUILD-COMM-AREA-TRAN-PRES.                         CL**1
00194 *                                                                    CL**1
00195 ******************************************************************   CL**1
00196 * DETERMINE WHICH TABLE THE ROW SHOULD BE INSERTED INTO AND          CL**1
00197 * PERFORM THE APPROPRIATE INSERT.                                    CL**1
00198 ******************************************************************   CL**1
00199  C100-BUILD-INSERT-ROW.                                              CL**1
00200                                                                      CL**1
00201      IF I-TABLE-NAME = 'TFGTRHL'                                     CL**1
00202         MOVE I-TRN-DTL-TXT TO WS-INV-NBR                             CL**2
00203                                                                      CL**1
00204         EXEC SQL                                                     CL**1
00205            SET :WS-SAVE-TIMESTAMP = CURRENT TIMESTAMP                CL**1
00206         END-EXEC                                                     CL**1
00207                                                                      CL**1
00208         EVALUATE TRUE                                                CL**1
00209            WHEN SQLCODE = 0                                          CL**1
00210               CONTINUE                                               CL**1
00211            WHEN OTHER                                                CL**1
00212               MOVE 'C100-BUILD-INSERT-ROW' TO                        CL**1
00213                    PV-PARAGRAPH-NAME                                 CL**1
00214               MOVE 'SELECT CURRENT TIMESTAMP' TO                     CL**1
00215                    PV-DB2-OPER-ATTEMPTED                             CL**1
00216               PERFORM Z900-SQL-ABEND                                 CL**1
00217         END-EVALUATE                                                 CL**1
00218                                                                      CL**1
00219         PERFORM Z200-INSERT-TFGTRHL-ROW                              CL**1
00220      ELSE                                                            CL**1
00221         IF I-TABLE-NAME = 'TTRPOHL'                                  CL**1
00222            MOVE I-PO-NBR TO WS-PO-NBR                                CL**1
00223            PERFORM Z300-INSERT-TTRPOHL-ROW                           CL**1
00224         ELSE                                                         CL**1
00225            IF I-TABLE-NAME = 'TFGTRDT'                               CL**1
00226               PERFORM Z400-INSERT-TFGTRDT-ROW                        CL**1
00227            END-IF                                                    CL**1
00228         END-IF                                                       CL**1
00229      END-IF.                                                         CL**1
00230 *                                                                    CL**1
00231 ******************************************************************   CL**1
00232 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION         CL**1
00233 * PRESENTATION MODULE.                                               CL**1
00234 ******************************************************************   CL**1
00235                                                                      CL**1
00236  Z100-BUILD-COMM-AREA-TRAN-PRES.                                     CL**1
00237                                                                      CL**1
00238      MOVE LENGTH OF WORKING-STORAGE-PASSED                           CL**1
00239                              TO DP001-WORK-STRG-LENGTH.              CL**1
00240      MOVE PC-CKPT-JOB-NAME   TO DP001-JOB-NAME.                      CL**1
00241      MOVE PC-CKPT-PLAN-NAME  TO DP001-PLAN-NAME.                     CL**1
00242      PERFORM Z900-CALL-TRANS-PRES-MODULE.                            CL**1
00243      MOVE DP001-TRANS-RECORD TO INV-INSERT-RECORD.                   CL**3
00244      IF DP001-CKPT-TAKEN                                             CL**1
00245          INITIALIZE PV-DEADLOCK-COUNTER                              CL**1
00246      END-IF.                                                         CL**1
00247                                                                      CL**1
00248 ******************************************************************   CL**1
00249 *  INSERT ROW INTO THE TFGTRHL TABLE                                 CL**1
00250 ******************************************************************   CL**1
00251  Z200-INSERT-TFGTRHL-ROW.                                            CL**1
00252 *                                                                    CL**1
00253      PERFORM                                                         CL**1
00254          WITH TEST AFTER                                             CL**1
00255          VARYING PV-SQL-SUB                                          CL**1
00256          FROM 1 BY 1                                                 CL**1
00257          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00258                    OR SQLCODE = -911                                 CL**1
00259                    OR SQLCODE = 0)                                   CL**1
00260 *                                                                    CL**1
00261          EXEC SQL                                                    CL**1
00262              INSERT INTO TFGTRHL                                     CL**1
00263                     (TP_DKEY,                                        CL**1
00264                      TP_DKEY_TMST,                                   CL**1
00265                      TRN_SET_CDE,                                    CL**1
00266                      TRN_DTL_TXT,                                    CL**1
00267                      HOLD_DTE,                                       CL**1
00268                      RLSE_IND,                                       CL**1
00269                      RLSE_DTE,                                       CL**1
00270                      USER_ID)                                        CL**1
00271              VALUES (:I-TP-DKEY,                                     CL**1
00272                      :WS-SAVE-TIMESTAMP,                             CL**1
00273                      :I-TRN-SET-CDE,                                 CL**1
00274                      :WS-INV-NBR-CONVERT,                            CL**2
00275                      CURRENT DATE,                                   CL**1
00276                      :I-RLSE-IND,                                    CL**1
00277                      :I-RLSE-DTE,                                    CL**1
00278                      :I-USER-ID)                                     CL**1
00279          END-EXEC                                                    CL**1
00280 *                                                                    CL**1
00281          EVALUATE TRUE                                               CL**1
00282              WHEN SQLCODE = -904                                     CL**1
00283              WHEN SQLCODE = 0                                        CL**1
00284                  CONTINUE                                            CL**1
00285              WHEN SQLCODE = -911                                     CL**1
00286                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**1
00287                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**1
00288                      MOVE 'Z200-INSERT-TFGTRHL-ROW'                  CL**1
00289                                      TO PV-PARAGRAPH-NAME            CL**1
00290                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**1
00291                                      TO PV-DB2-OPER-ATTEMPTED        CL**1
00292                      PERFORM Z900-SQL-ABEND                          CL**1
00293                  ELSE                                                CL**1
00294                      SET PS-RESTART-REQUIRED TO TRUE                 CL**1
00295                  END-IF                                              CL**1
00296              WHEN SQLWARN NOT = SPACES                               CL**1
00297              WHEN SQLCODE NOT = ZERO                                 CL**1
00298                  MOVE 'Z200-INSERT-TFGTRHL-ROW'                      CL**1
00299                                TO  PV-PARAGRAPH-NAME                 CL**1
00300                  MOVE 'INSERT ROW INTO TFGTRHL'                      CL**1
00301                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00302                  PERFORM Z900-SQL-ABEND                              CL**1
00303          END-EVALUATE                                                CL**1
00304      END-PERFORM.                                                    CL**1
00305 *                                                                    CL**1
00306      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00307          MOVE 'Z200-INSERT-TFGTRHL-ROW'                              CL**1
00308                                TO  PV-PARAGRAPH-NAME                 CL**1
00309          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**1
00310                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00311          PERFORM Z900-SQL-ABEND                                      CL**1
00312      END-IF.                                                         CL**1
00313 *                                                                    CL**1
00314 ******************************************************************   CL**1
00315 *  INSERT ROW INTO THE TTRPOHL TABLE                                 CL**1
00316 ******************************************************************   CL**1
00317  Z300-INSERT-TTRPOHL-ROW.                                            CL**1
00318 *                                                                    CL**1
00319      PERFORM                                                         CL**1
00320          WITH TEST AFTER                                             CL**1
00321          VARYING PV-SQL-SUB                                          CL**1
00322          FROM 1 BY 1                                                 CL**1
00323          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00324                    OR SQLCODE = -911                                 CL**1
00325                    OR SQLCODE = 0)                                   CL**1
00326 *                                                                    CL**1
00327          EXEC SQL                                                    CL**1
00328              INSERT INTO TTRPOHL                                     CL**1
00329                     (TP_DKEY,                                        CL**1
00330                      TP_DKEY_TMST,                                   CL**1
00331                      PO_NBR,                                         CL**1
00332                      DEPT_NBR,                                       CL**1
00333                      PO_RLSE_IND)                                    CL**1
00334              VALUES (:I-TP-DKEY,                                     CL**1
00335                      :WS-SAVE-TIMESTAMP,                             CL**1
00336                      :WS-PO-NBR-22,                                  CL**1
00337                      :I-DEPT-NBR,                                    CL**1
00338                      :I-PO-RLSE-IND)                                 CL**1
00339          END-EXEC                                                    CL**1
00340 *                                                                    CL**1
00341          EVALUATE TRUE                                               CL**1
00342              WHEN SQLCODE = -904                                     CL**1
00343              WHEN SQLCODE = 0                                        CL**1
00344                  CONTINUE                                            CL**1
00345              WHEN SQLCODE = -911                                     CL**1
00346                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**1
00347                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**1
00348                      MOVE 'Z300-INSERT-TTRPOHL-ROW'                  CL**1
00349                                      TO PV-PARAGRAPH-NAME            CL**1
00350                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**1
00351                                      TO PV-DB2-OPER-ATTEMPTED        CL**1
00352                      PERFORM Z900-SQL-ABEND                          CL**1
00353                  ELSE                                                CL**1
00354                      SET PS-RESTART-REQUIRED TO TRUE                 CL**1
00355                  END-IF                                              CL**1
00356              WHEN SQLWARN NOT = SPACES                               CL**1
00357              WHEN SQLCODE NOT = ZERO                                 CL**1
00358                  MOVE 'Z300-INSERT-TTRPOHL-ROW'                      CL**1
00359                                TO  PV-PARAGRAPH-NAME                 CL**1
00360                  MOVE 'INSERT ROW INTO TTRPOHL'                      CL**1
00361                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00362                  PERFORM Z900-SQL-ABEND                              CL**1
00363          END-EVALUATE                                                CL**1
00364      END-PERFORM.                                                    CL**1
00365 *                                                                    CL**1
00366      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00367          MOVE 'Z300-INSERT-TTRPOHL-ROW'                              CL**1
00368                                TO  PV-PARAGRAPH-NAME                 CL**1
00369          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**1
00370                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00371          PERFORM Z900-SQL-ABEND                                      CL**1
00372      END-IF.                                                         CL**1
00373 *                                                                    CL**1
00374 ******************************************************************   CL**1
00375 *  INSERT ROW INTO THE TFGTRDT TABLE                                 CL**1
00376 ******************************************************************   CL**1
00377  Z400-INSERT-TFGTRDT-ROW.                                            CL**1
00378 *                                                                    CL**1
00379      PERFORM                                                         CL**1
00380          WITH TEST AFTER                                             CL**1
00381          VARYING PV-SQL-SUB                                          CL**1
00382          FROM 1 BY 1                                                 CL**1
00383          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL**1
00384                    OR SQLCODE = -911                                 CL**1
00385                    OR SQLCODE = 0)                                   CL**1
00386 *                                                                    CL**1
00387          EXEC SQL                                                    CL**1
00388              INSERT INTO TFGTRDT                                     CL**1
00389                     (TP_DKEY,                                        CL**1
00390                      TP_DKEY_TMST,                                   CL**1
00391                      TRN_SET_SEQ_NBR,                                CL**1
00392                      TRN_SET_DATA)                                   CL**1
00393              VALUES (:I-TP-DKEY,                                     CL**1
00394                      :WS-SAVE-TIMESTAMP,                             CL**1
00395                      :I-TRN-SET-SEQ-NBR,                             CL**1
00396                      :I-TRN-SET-DATA)                                CL**1
00397          END-EXEC                                                    CL**1
00398 *                                                                    CL**1
00399          EVALUATE TRUE                                               CL**1
00400              WHEN SQLCODE = -904                                     CL**1
00401              WHEN SQLCODE = 0                                        CL**1
00402                  CONTINUE                                            CL**1
00403              WHEN SQLCODE = -911                                     CL**1
00404                  ADD +1 TO PV-DEADLOCK-COUNTER                       CL**1
00405                  IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS           CL**1
00406                      MOVE 'Z400-INSERT-TFGTRDT-ROW'                  CL**1
00407                                      TO PV-PARAGRAPH-NAME            CL**1
00408                      MOVE 'MAX DEADLOCKS EXCEEDED'                   CL**1
00409                                      TO PV-DB2-OPER-ATTEMPTED        CL**1
00410                      PERFORM Z900-SQL-ABEND                          CL**1
00411                  ELSE                                                CL**1
00412                      SET PS-RESTART-REQUIRED TO TRUE                 CL**1
00413                  END-IF                                              CL**1
00414              WHEN SQLWARN NOT = SPACES                               CL**1
00415              WHEN SQLCODE NOT = ZERO                                 CL**1
00416                  MOVE 'Z400-INSERT-TFGTRDT-ROW'                      CL**1
00417                                TO  PV-PARAGRAPH-NAME                 CL**1
00418                  MOVE 'INSERT ROW INTO TFGTRDT'                      CL**1
00419                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00420                  PERFORM Z900-SQL-ABEND                              CL**1
00421          END-EVALUATE                                                CL**1
00422      END-PERFORM.                                                    CL**1
00423 *                                                                    CL**1
00424      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
00425          MOVE 'Z400-INSERT-TFGTRDT-ROW'                              CL**1
00426                                TO  PV-PARAGRAPH-NAME                 CL**1
00427          MOVE 'VERIFY RETRIES EXCEEDED'                              CL**1
00428                                TO  PV-DB2-OPER-ATTEMPTED             CL**1
00429          PERFORM Z900-SQL-ABEND                                      CL**1
00430      END-IF.                                                         CL**1
00431 *                                                                    CL**1
00432 ******************************************************************   CL**1
00433 * ABEND THE PROGRAM IN THE EVENT THAT AN SQL ERROR OR WARNING        CL**1
00434 * CODE OCCURS.                                                       CL**1
00435 ******************************************************************   CL**1
00436  Z900-SQL-ABEND.                                                     CL**1
00437                                                                      CL**1
00438      DISPLAY '** ABEND     **'.                                      CL**1
00439      DISPLAY '** PROGRAM   ** = ETKUP037'.                           CL**4
00440      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                 CL**1
00441      DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.             CL**1
00442                                                                      CL**1
00443                                                                      CL**1
00444 ******************************************************************   CL**1
00445 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
00446 * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE            CL**1
00447 * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE        CL**1
00448 * FORMAT.                                                            CL**1
00449 ******************************************************************   CL**1
00450      COPY DPPD004.                                                   CL**1
00451                                                                      CL**1
00452      MOVE +4013 TO ABEND-CODE.                                       CL**1
00453      CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
00454                                                                      CL**1
00455 ******************************************************************   CL**1
00456 * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-       CL**1
00457 * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION     CL**1
00458 * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.            CL**1
00459 ******************************************************************   CL**1
00460      COPY DPPD001.                                                   CL**1
