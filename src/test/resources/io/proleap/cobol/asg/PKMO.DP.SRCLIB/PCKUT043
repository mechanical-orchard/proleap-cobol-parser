000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'BUILD FASHION SALES EXTRACT'.                                     
000300                                                                          
000400 PROGRAM-ID.             PCKUT043.                                        
000500 AUTHOR.                 PATRICK BURKE.                                   
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            MAY 2005.                                        
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500 INPUT-OUTPUT SECTION.                                                    
001600 FILE-CONTROL.                                                            
001700                                                                          
001800     SELECT UNBALANCED-RPT-FILE                                           
001900         ASSIGN TO INP01.                                                 
002000     SELECT REPORT-EXTRACT-FILE                                           
002100         ASSIGN TO OUT01.                                                 
002200                                                                          
002300 DATA DIVISION.                                                           
002400 FILE SECTION.                                                            
002500                                                                          
002600 FD  UNBALANCED-RPT-FILE                                                  
002700     RECORDING MODE IS F                                                  
002800     LABEL RECORDS ARE STANDARD                                           
002900     BLOCK CONTAINS 0 RECORDS.                                            
003000                                                                          
003100 01  UNBALANCED-RPT-REC                PIC X(150).                        
003200                                                                          
003300 FD  REPORT-EXTRACT-FILE                                                  
003400     RECORDING MODE IS F                                                  
003500     LABEL RECORDS ARE STANDARD                                           
003600     BLOCK CONTAINS 0 RECORDS.                                            
003700                                                                          
003800 01  REPORT-EXTRACT-REC                PIC X(100).                        
003900                                                                          
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  FILLER.                                                              
004300     05  FILLER                      PIC  X(36)    VALUE                  
004400         ' ***** WORKING STORAGE PGM=PCKUT043 '.                          
004500     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
004600     05  FILLER                      PIC  X(10)    VALUE                  
004700         ' LOCATION='.                                                    
004800     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.           
004900     05  FILLER                      PIC  X(06)    VALUE                  
005000         ' *****'.                                                        
005100                                                                          
005200 01  PV-SYSIN-CARD.                                                       
005300     05  PV-SYSIN-DTE                 PIC X(10).                          
005400     05  FILLER                       PIC X(70).                          
005500                                                                          
005600 01  PS-SWITCHES.                                                         
005700     05  PS-EOF-SW                    PIC X(01) VALUE 'N'.                
005800         88  PS-EOF-EXTRACT-YES                 VALUE 'Y'.                
005900         88  PS-EOF-EXTRACT-NO                  VALUE 'N'.                
006000                                                                          
006100 01  PC-CONSTANTS.                                                        
006200     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
006300                                                   'PCKUT043'.            
006400     05  PC-MAX-RETRIES              PIC S9(04)    VALUE  +3              
006500                                                   COMP.                  
006600                                                                          
006700 01  PV-MISC-STUFF.                                                       
006800     05  PV-STR-NBR-COMP              PIC S9(05) COMP VALUE ZERO.         
006900     05  PV-STR-NBR                   PIC S9(05)      VALUE ZERO.         
007000     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
007100     05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(60) VALUE SPACE.             
007200     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
007300     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
007400                                                   COMP SYNC.             
007500     05  PV-RETRY-COUNTER             PIC S9(04) VALUE ZERO               
007600                                                 COMP SYNC.               
007700     05  PV-READ-COUNTER              PIC S9(09) COMP-3                   
007800                                              VALUE ZERO.                 
007900     05  PV-WRITE-1-COUNTER           PIC S9(09) COMP-3                   
008000                                              VALUE ZERO.                 
008100     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZ9.                    
008200     05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
008300     05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
008400                                                                          
008500 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
008600                                                     COMP SYNC.           
008700     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
008800     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
008900     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
009000     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
009100     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
009200     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
009300     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
009400     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
009500     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
009600     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
009700                                                                          
009800 01  PV-COUNT-MSG-1.                                                      
009900     05  FILLER                       PIC X(26) VALUE                     
010000         'RECORDS WRITTEN TO OUT01 ='.                                    
010100     05  PV-MSG-1-COUNT               PIC Z,ZZZ,ZZZ,ZZ9.                  
010200                                                                          
010300                                                                          
010400 01  PV-COUNT-MSG-4.                                                      
010500     05  FILLER                       PIC X(26) VALUE                     
010600         'RECORDS READ             ='.                                    
010700     05  PV-MSG-READ-COUNT            PIC Z,ZZZ,ZZZ,ZZ9.                  
010800                                                                          
010900*    IN137-STR-EXPTED-RECORD                                              
011000     COPY INRD137.                                                        
011100                                                                          
011200*    REPORT EXTRACT FILE                                                  
011300     COPY PCRD038.                                                        
011400/                                                                         
011500     EXEC SQL                                                             
011600         INCLUDE TPURCHS                                                  
011700     END-EXEC.                                                            
011800                                                                          
011900     EXEC SQL                                                             
012000         INCLUDE SQLCA                                                    
012100     END-EXEC.                                                            
012200                                                                          
012300     COPY SQLCA2.                                                         
012400                                                                          
012500     COPY DPWS004.                                                        
012600*---------------------------------------------------------------*         
012700 PROCEDURE DIVISION.                                                      
012800                                                                          
012900     PERFORM 1000-INITIALIZATION.                                         
013000                                                                          
013100     PERFORM 2000-PROCESS UNTIL PS-EOF-EXTRACT-YES                        
013200                                                                          
013300     PERFORM 9000-QUIT.                                                   
013400                                                                          
013500     GOBACK.                                                              
013600/                                                                         
013700 1000-INITIALIZATION.                                                     
013800     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
013900                                                                          
014000     OPEN INPUT  UNBALANCED-RPT-FILE                                      
014100     OPEN OUTPUT REPORT-EXTRACT-FILE.                                     
014200                                                                          
014300     ACCEPT PV-SYSIN-CARD FROM SYSIN                                      
014400     IF PV-SYSIN-CARD = LOW-VALUES                                        
014500         DISPLAY ALL '!'                                                  
014600         DISPLAY ' DATE CARD IS MISSING - JOB GO BOOM'                    
014700         DISPLAY SPACES                                                   
014800         PERFORM 9200-CROAK                                               
014900     ELSE                                                                 
015000         DISPLAY 'PCKUT043 - DATE IS ' PV-SYSIN-DTE                       
015100     END-IF.                                                              
015200                                                                          
015300     PERFORM 8000-READ-EXTRACT.                                           
015400                                                                          
015500     IF PS-EOF-EXTRACT-YES                                                
015600         DISPLAY SPACES                                                   
015700         DISPLAY ALL '*'                                                  
015800         DISPLAY '  INPUT FILE IS EMPTY '                                 
015900         DISPLAY ALL '*'                                                  
016000         DISPLAY SPACES                                                   
016100     END-IF.                                                              
016200/                                                                         
016300 2000-PROCESS.                                                            
016400                                                                          
016500     MOVE '2000-PROCESS'                  TO PV-ABEND-PARAGRAPH.          
016600     PERFORM 2200-BUILD-REPORT-REC                                        
016700     PERFORM 8200-WRITE-IN137.                                            
016800                                                                          
016900     PERFORM 8000-READ-EXTRACT.                                           
017000/                                                                         
017100 2200-BUILD-REPORT-REC.                                                   
017200                                                                          
017300     INITIALIZE IN137-STR-EXPTED-RECORD                                   
017400     MOVE PC038-LOC-NBR               TO PV-STR-NBR-COMP.                 
017500     MOVE PV-STR-NBR-COMP             TO PV-STR-NBR.                      
017600     MOVE PV-STR-NBR                  TO IN137-STORE-NBR.                 
017700     MOVE PC038-SKU-9                 TO IN137-SKU-9.                     
017800     MOVE PC038-INTR-UPC-ID           TO IN137-INTR-UPC-ID.               
017900     MOVE PV-SYSIN-DTE                TO IN137-ADJ-DATE.                  
018000     MOVE PC038-STR-EXPECTED-UNITS    TO IN137-BAL-QTY.                   
018100     PERFORM 7200-SELECT-ENT-ID.                                          
018200     MOVE PURCHS-ENT-ID               TO IN137-ENT-ID.                    
018300     MOVE PC038-PO-NBR                TO IN137-PO-NBR.                    
018400     MOVE PC038-RCVR-SEQ-NBR          TO IN137-RCVR-SEQ-NBR.              
018500     MOVE PC038-OTBND-TRIP-1          TO IN137-OTBND-TRIP-ID.             
018600     IF PC038-OTBND-TRIP-2   >  ZERO                                      
018700        MOVE '*'                      TO IN137-OTBND-TRIP-MORE            
018800     END-IF.                                                              
018900                                                                          
019000/                                                                         
019100 7200-SELECT-ENT-ID.                                                      
019200                                                                          
019300     MOVE '7200-SELECT-ENT-ID'        TO PV-ABEND-PARAGRAPH.              
019400                                                                          
019500     MOVE PC038-PO-NBR                TO PURCHS-PO-NBR                    
019600     INITIALIZE PURCHS-ENT-ID                                             
019700                                                                          
019800     PERFORM                                                              
019900         WITH TEST AFTER                                                  
020000         VARYING PV-RETRY-COUNTER                                         
020100         FROM 1 BY 1                                                      
020200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
020300            OR  (SQLCODE = +100)                                          
020400            OR  (SQLCODE = +0))                                           
020500                                                                          
020600         EXEC SQL                                                         
020700             SELECT ENT_ID                                                
020800               INTO :PURCHS-ENT-ID                                        
020900               FROM  TPURCHS                                              
021000              WHERE PO_NBR          = :PURCHS-PO-NBR                      
021100                AND VER_STATUS_CDE  = 'A'                                 
021200              WITH UR                                                     
021300         END-EXEC                                                         
021400                                                                          
021500         EVALUATE TRUE                                                    
021600             WHEN SQLCODE = ZERO                                          
021700             WHEN SQLCODE = +100                                          
021800                 CONTINUE                                                 
021900             WHEN SQLCODE = -904                                          
022000             WHEN SQLCODE = -911                                          
022100             WHEN SQLCODE = -913                                          
022200                  CONTINUE                                                
022300                                                                          
022400             WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                      
022500                 DISPLAY '**************************************'         
022600                 DISPLAY 'WARNING ISSUED '                                
022700                 DISPLAY 'SELECT TPURCHS                        '         
022800                 DISPLAY '**************************************'         
022900                 PERFORM 9800-SQL-WARNING                                 
023000                                                                          
023100             WHEN SQLCODE < ZERO                                          
023200                 DISPLAY '**************************************'         
023300                 DISPLAY 'ERROR ISSUED ON                       '         
023400                 DISPLAY 'SELECT TPURCHS                        '         
023500                 DISPLAY '**************************************'         
023600                 PERFORM 9900-SQL-ERROR                                   
023700                                                                          
023800         END-EVALUATE                                                     
023900     END-PERFORM.                                                         
024000                                                                          
024100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES AND                            
024200         SQLCODE NOT = ZERO                                               
024300         DISPLAY '**************************************'                 
024400         DISPLAY 'MAX RETRIES EXCEEDED ATTEMPTING TO    '                 
024500         DISPLAY 'SELECT TPURCHS         '                                
024600         DISPLAY '**************************************'                 
024700         PERFORM 9900-SQL-ERROR                                           
024800     END-IF.                                                              
024900/                                                                         
025000 8000-READ-EXTRACT.                                                       
025100                                                                          
025200     MOVE '8000-READ-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
025300*                                                                         
025400     READ UNBALANCED-RPT-FILE                                             
025500         INTO                                                             
025600             PC038-SE-UNBALANCED-RPT-REC                                  
025700         AT END                                                           
025800             SET PS-EOF-EXTRACT-YES   TO TRUE                             
025900         NOT AT END                                                       
026000             ADD 1                    TO PV-READ-COUNTER                  
026100     END-READ.                                                            
026200                                                                          
026300 8200-WRITE-IN137.                                                        
026400     MOVE '8200-WRITE-IN137    '  TO PV-ABEND-PARAGRAPH.                  
026500     WRITE                                                                
026600         REPORT-EXTRACT-REC                                               
026700         FROM IN137-STR-EXPTED-RECORD                                     
026800     END-WRITE.                                                           
026900     ADD 1                            TO PV-WRITE-1-COUNTER.              
027000/                                                                         
027100 9000-QUIT.                                                               
027200                                                                          
027300     CLOSE UNBALANCED-RPT-FILE                                            
027400           REPORT-EXTRACT-FILE                                            
027500                                                                          
027600     MOVE PV-READ-COUNTER             TO PV-MSG-READ-COUNT                
027700     DISPLAY PV-COUNT-MSG-4.                                              
027800                                                                          
027900     MOVE PV-WRITE-1-COUNTER          TO PV-MSG-1-COUNT                   
028000     DISPLAY PV-COUNT-MSG-1.                                              
028100/                                                                         
028200 9100-SQL-ABEND.                                                          
028300******************************************************************        
028400*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
028500*  ERROR OR WARNING CODE OCCURS.                                          
028600******************************************************************        
028700     DISPLAY '9100-SQL-ABEND'.                                            
028800     DISPLAY '** ABEND     **'.                                           
028900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
029000     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
029100     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
029200     DISPLAY '**              ** = '                                      
029300     MOVE SQLCODE TO PV-SQL-CODE.                                         
029400     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
029500     MOVE +4013 TO PV-ABEND-CODE.                                         
029600     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
029700                                                                          
029800 9200-CROAK.                                                              
029900******************************************************************        
030000*  PERFORMS THE ABEND FOR THE PROGRAM                                     
030100******************************************************************        
030200     DISPLAY '9200-CROAK'.                                                
030300     DISPLAY '** ABEND     **'.                                           
030400     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
030500     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
030600     DISPLAY '**              ** = '                                      
030700     MOVE +4013 TO PV-ABEND-CODE.                                         
030800     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
030900                                                                          
031000******************************************************************        
031100*  THIS MODULE PERFORMS THE SQL WARNING.                         *        
031200******************************************************************        
031300                                                                          
031400 9800-SQL-WARNING.                                                        
031500                                                                          
031600     MOVE SQLCODE               TO SQLCODE2.                              
031700     MOVE SQLERRD(3)            TO SQLERRD3.                              
031800     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
031900     DISPLAY '** PROGRAM        ** = PCKUT043'.                           
032000     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
032100     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
032200     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
032300                                                                          
032400     COPY DPPD004.                                                        
032500     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
032600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
032700/                                                                         
032800******************************************************************        
032900* THIS MODULE PERFORMS THE SQL ERROR.                            *        
033000******************************************************************        
033100                                                                          
033200 9900-SQL-ERROR.                                                          
033300                                                                          
033400     MOVE SQLCODE               TO SQLCODE2.                              
033500     MOVE SQLERRD(3)            TO SQLERRD3.                              
033600     DISPLAY '** ABEND **       ** = SQLERROR'.                           
033700     DISPLAY '** PROGRAM        ** = PCKUT043'.                           
033800     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
033900     DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION                 
034000     DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1               
034100     DISPLAY SPACES                                                       
034200                                                                          
034300     COPY DPPD004.                                                        
034400     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
034500     CALL 'ILBOABN0' USING ABEND-CODE.                                    
