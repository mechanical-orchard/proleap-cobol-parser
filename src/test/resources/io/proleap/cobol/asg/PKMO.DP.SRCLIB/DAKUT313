       IDENTIFICATION DIVISION.                                         00010016
       PROGRAM-ID.           DAKUT313.                                  00020099
       AUTHOR.               JILL LIN.                                  00030099
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040099
       DATE-WRITTEN          MAY 2006.                                  00050099
       DATE-COMPILED.                                                   00060016
                                                                        00070016
      ******************************************************************00080017
      * PURPOSE: THIS PROGRAM EXTRACTS DATA FOR A REPORT THAT WILL     *00090099
      * SHOW DMM, BUYER AND VENDOR LISTING DA AVAILABLE VENDOR FUNDING *00100099
      * REMAINING AVAILABLE COST DOLLAR BALANCES.                      *00110099
      ******************************************************************00210099
      *    WR/PITS#     DATE        DESCRIPTION                        *00220099
      *    --------   ----------   ---------------------------------   *00230099
      ******************************************************************00330017
                                                                        00340016
       ENVIRONMENT DIVISION.                                            00350016
       CONFIGURATION SECTION.                                           00360016
       SOURCE-COMPUTER. IBM-3090.                                       00370016
       OBJECT-COMPUTER. IBM-3090.                                       00380016
                                                                        00390017
       INPUT-OUTPUT SECTION.                                            00400016
       FILE-CONTROL.                                                    00410016
                                                                        00420017
           SELECT IN-CC-FILE                 ASSIGN TO INP01.           00421099
                                                                        00429499
           SELECT IN-DA-FILE                 ASSIGN TO INP02.           00430099
                                                                        00440099
           SELECT OUT-DA-FILE                ASSIGN TO OUT01.           00450000
                                                                        00460016
       DATA DIVISION.                                                   00470016
       FILE SECTION.                                                    00480016
                                                                        00490017
       FD  IN-CC-FILE                                                   00491099
           RECORDING MODE IS F                                          00492099
           LABEL RECORDS ARE STANDARD                                   00493099
           BLOCK CONTAINS 0 RECORDS.                                    00494099
       01  IN-CC-RECORD               PIC X(80).                        00496099
                                                                        00497099
       FD  IN-DA-FILE                                                   00500099
           LABEL RECORDS ARE STANDARD                                   00510099
           RECORDING MODE IS F                                          00520099
           BLOCK CONTAINS 0 RECORDS.                                    00530099
       01  IN-DA-RECORD                    PIC X(46).                   00540099
                                                                        00660099
       FD  OUT-DA-FILE                                                  00670000
           LABEL RECORDS ARE STANDARD                                   00680000
           RECORDING MODE IS F                                          00690000
           BLOCK CONTAINS 0 RECORDS.                                    00700000
       01  OUT-DA-RECORD                   PIC X(165).                  00710099
                                                                        00720000
       WORKING-STORAGE SECTION.                                         00730016
                                                                        00740030
       01  PC-PROGRAM-CONSTANTS.                                        00750016
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        00780099
                                                           'DAKUT313'.  00790099
           05  PC-OPERCO-NBR               PIC X(02)       VALUE '03'.  00791099
           05  PC-MDSELV-GMA               PIC X(03)       VALUE 'GMA'. 00800099
           05  PC-MDSELV-DMA               PIC X(03)       VALUE 'DMA'. 00810099
           05  PC-MDSELV-DPT               PIC X(03)       VALUE 'DPT'. 00820099
           05  PC-RSPLVL-CDE-GMM           PIC X(03)       VALUE 'GMM'. 00840099
           05  PC-RSPLVL-CDE-DMM           PIC X(03)       VALUE 'DMM'. 00850099
           05  PC-RSPLVL-CDE-BYR           PIC X(03)       VALUE 'BYR'. 00860099
       01 PV-PROGRAM-VARIABLES.                                         00910019
           05  PV-PARAGRAPH-NAME           PIC X(40)       VALUE SPACES.00920099
           05  PV-DB2-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.00921099
           05  PV-ABEND-CODE  COMP SYNC    PIC S9(04)      VALUE +0000. 00930099
           05  PV-CURRENT-FCL-QTR-DTE      PIC X(10)       VALUE SPACES.00940099
           05  PV-DEPT-NBR                 PIC X(03)       VALUE SPACES.01140099
           05  PV-ENT-ID                   PIC S9(09)      VALUE ZEROES 01150099
                                                   COMP.                01160099
           05  PV-AVF-ENT-ID               PIC S9(09)      VALUE ZEROES 01180099
                                                   COMP.                01190099
                                                                        01214099
       01  PS-PROGRAM-SWITCHES.                                         01240099
           05  PS-INP02-DONE-SW            PIC X(01)       VALUE 'N'.   01321099
               88  INP02-PROCESS-COMPLETE                  VALUE 'Y'.   01330099
           05  PS-MBS-INFO-FOUND-SW        PIC X           VALUE 'N'.   01331099
               88  MBS-INFO-FOUND                          VALUE 'Y'.   01332099
               88  MBS-INFO-NOT-FOUND                      VALUE 'N'.   01333099
           05  PS-VND-INFO-FOUND-SW        PIC X           VALUE 'N'.   01334099
               88  VND-INFO-FOUND                          VALUE 'Y'.   01335099
               88  VND-INFO-NOT-FOUND                      VALUE 'N'.   01336099
                                                                        01340016
       01  CC-CONTROL-CARD.                                             01341099
           05  FILLER                      PIC X(08)       VALUE SPACES.01341199
           05  CC-DATE                     PIC X(10)       VALUE SPACES.01342099
           05  FILLER                      PIC X(62)       VALUE SPACES.01343099
                                                                        01344099
       01  AVL-VND-FUND-REC.                                            01344199
           05  AVF-ENT-ID                  PIC 9(09)       VALUE ZEROES.01345099
           05  FILLER                      PIC X(02)       VALUE SPACES.01346099
           05  AVF-DEPT-NBR                PIC X(03)       VALUE SPACES.01347199
           05  FILLER                      PIC X(02)       VALUE SPACES.01347299
           05  AVF-SUB-CL-NBR              PIC X(03)       VALUE SPACES.01348099
           05  FILLER                      PIC X(02)       VALUE SPACES.01348199
           05  AVF-EFF-BEG-DTE             PIC X(10)       VALUE SPACES.01349099
           05  FILLER                      PIC X(01)       VALUE SPACES.01349199
           05  AVF-RMNG-AVL-COST-AMT       PIC 9(11).99    VALUE ZEROES.01349299
                                                                        01349799
      ******************************************************************01881099
      * DB2 TABLE DCLGEN - TMDSEAR                                      01882099
      ******************************************************************01883099
           EXEC SQL                                                     01884099
               INCLUDE TMDSEAR                                          01885099
           END-EXEC.                                                    01886099
                                                                        01887099
      ******************************************************************01888099
      * DB2 TABLE DCLGEN - TRSPBAR                                      01889099
      ******************************************************************01889199
           EXEC SQL                                                     01889299
               INCLUDE TRSPBAR                                          01889399
           END-EXEC.                                                    01889499
                                                                        01889599
      ******************************************************************01889699
      * DB2 TABLE DCLGEN - TDTATTR                                      01889799
      ******************************************************************01889899
           EXEC SQL                                                     01889999
               INCLUDE TDTATTR                                          01890099
           END-EXEC.                                                    01890199
                                                                        01890299
      ******************************************************************01890399
      * DB2 TABLE DCLGEN - XMT00000                                     01890499
      ******************************************************************01890599
           EXEC SQL                                                     01890699
               INCLUDE XMT00000                                         01890799
           END-EXEC.                                                    01890899
                                                                        01890999
      ******************************************************************01891099
      * DB2 TABLE ERROR HANDLING                                        01891199
      ******************************************************************01891299
           EXEC SQL                                                     01891399
               INCLUDE SQLCA                                            01891499
           END-EXEC.                                                    01891599
                                                                        01891699
      ***************************************************************** 01892099
      * ORA COMMUNICATION AREA2                                         01900016
      ***************************************************************** 01910030
           COPY SQLCA2.                                                 01920016
                                                                        01930017
      ***************************************************************** 02031099
      *    DARD106 COPYBOOK.                                            02032099
      ***************************************************************** 02033099
           COPY DARD106.                                                02034099
                                                                        02035099
       PROCEDURE DIVISION.                                              02040016
      ******************************************************************02050099
      * MAIN PROCEDURE                                                  02060099
      ******************************************************************02070099
       0000-MAINLINE-LOGIC.                                             02080016
                                                                        02090016
           PERFORM 1000-INITIALIZATION.                                 02100016
                                                                        02110016
           PERFORM 9100-READ-IN-DA-FILE.                                02110199
                                                                        02110299
           PERFORM 2000-CREATE-EXTRACT-RECORD                           02111099
               UNTIL INP02-PROCESS-COMPLETE.                            02112099
                                                                        02130099
           PERFORM 9900-EOJ-LOGIC.                                      02170014
                                                                        02180016
           GOBACK.                                                      02200016
           EJECT                                                        02210016
      ******************************************************************02220099
      * 1000-INITIALIZATION.                                            02230099
      ******************************************************************02250099
       1000-INITIALIZATION.                                             02260016
           OPEN INPUT   IN-CC-FILE.                                     02270099
           OPEN INPUT   IN-DA-FILE.                                     02271099
           OPEN OUTPUT  OUT-DA-FILE.                                    02280001
                                                                        02292099
      * GET CURRENT PROCESS DATE TO OBTAIN CURRENT FISCAL QTR DATE      02292199
           PERFORM 9000-READ-INPUT-DATE.                                02293099
           PERFORM 3000-GET-FSCL-DATE.                                  02295099
                                                                        02296099
      ***************************************************************** 02450099
      * 2000-CREATE-EXTRACT-RECORD.                                     02460099
      ***************************************************************** 02480099
       2000-CREATE-EXTRACT-RECORD.                                      02480199
                                                                        02480299
      * GET THE MBS INFORMATION                                         02480399
           IF AVF-DEPT-NBR EQUAL PV-DEPT-NBR                            02480499
               CONTINUE                                                 02480699
           ELSE                                                         02480799
               MOVE AVF-DEPT-NBR TO PV-DEPT-NBR                         02480999
                                    DA106-DEPT-NBR                      02481099
               PERFORM 4000-GET-MBS-DESC                                02481499
           END-IF.                                                      02481599
                                                                        02481699
      * DETERMINE THE REPORT QTR FOR AVAILABLE COST AMOUNT              02481999
           IF AVF-EFF-BEG-DTE EQUAL PV-CURRENT-FCL-QTR-DTE              02482499
               MOVE AVF-RMNG-AVL-COST-AMT                               02482599
                   TO DA106-CRRNT-AVL-COST-AMT                          02482699
               MOVE ZEROES TO DA106-PREV-AVL-COST-AMT                   02482799
           ELSE                                                         02482899
               MOVE AVF-RMNG-AVL-COST-AMT                               02482999
                   TO DA106-PREV-AVL-COST-AMT                           02483099
               MOVE ZEROES TO DA106-CRRNT-AVL-COST-AMT                  02483199
           END-IF.                                                      02483299
                                                                        02483399
      * GET VENDOR INFORMATION                                          02483499
           MOVE AVF-ENT-ID TO PV-AVF-ENT-ID.                            02483599
           IF PV-AVF-ENT-ID EQUAL PV-ENT-ID                             02483699
               CONTINUE                                                 02483799
           ELSE                                                         02483899
               MOVE PV-AVF-ENT-ID TO PV-ENT-ID                          02483999
               PERFORM 5000-GET-VENDOR-INFO                             02484099
           END-IF.                                                      02484199
                                                                        02484299
           PERFORM 6000-WRITE-EXTRACT-REC.                              02484399
           PERFORM 9100-READ-IN-DA-FILE.                                02484499
                                                                        02484599
      ***************************************************************** 02484699
      * 3000-GET-FSCL-DATE.                                             02484799
      ***************************************************************** 02485099
       3000-GET-FSCL-DATE.                                              02486099
                                                                        02590099
           EXEC SQL                                                     02590199
               SELECT MIN(A.KY_DTE)                                     02590399
               INTO  :PV-CURRENT-FCL-QTR-DTE                            02591299
               FROM TDTATTR A                                           02592199
                   ,TDTATTR B                                           02592299
               WHERE A.FSCL_YR_NBR = B.FSCL_YR_NBR                      02592899
                 AND A.FSCL_QTR_NBR = B.FSCL_QTR_NBR                    02592999
                 AND B.KY_DTE = :CC-DATE                                02593299
           END-EXEC.                                                    02595799
                                                                        02595899
           EVALUATE TRUE                                                02595999
               WHEN SQLCODE = ZERO                                      02596299
                   CONTINUE                                             02596399
               WHEN SQLCODE = +100                                      02596499
                   DISPLAY ' '                                          02596599
                   DISPLAY 'QTR FSCL DTE NOT FOUND FOR '                02596699
                           'DATE ' CC-DATE                              02596799
               WHEN OTHER                                               02596999
                   MOVE '3000-GET-FSCL-DATE'                            02597099
                       TO PV-PARAGRAPH-NAME                             02597199
                   MOVE 'SELECT ROW FROM TDTATTR'                       02597299
                       TO PV-DB2-OPER-ATTEMPTED                         02597399
                   DISPLAY 'DATE ' CC-DATE                              02597699
                   PERFORM 9999-SQL-ERROR                               02597799
           END-EVALUATE.                                                02597899
                                                                        02597999
      ***************************************************************** 02599499
      * 4000-GET-MBS-DESC.                                              02599599
      *   GET THE CORRECT MBS DESCRIPTION/NAME FIELDS                   02599699
      ***************************************************************** 02599799
       4000-GET-MBS-DESC.                                               02599899
                                                                        02599999
      * GET MBS STRUCTURE (GMA/DMA/BUYER).                              02600099
           EXEC SQL                                                     02610099
               SELECT A.GMA_NBR                                         02610199
                     ,F.WORKER_FIR_NAME                                 02610299
                     ,F.WORKER_LAST_NAME                                02610399
                     ,B.DMA_NBR                                         02610499
                     ,G.WORKER_FIR_NAME                                 02610599
                     ,G.WORKER_LAST_NAME                                02610699
                     ,C.BYR_NBR                                         02610799
                     ,H.WORKER_FIR_NAME                                 02610899
                     ,H.WORKER_LAST_NAME                                02610999
               INTO  :DA106-GMM                                         02611699
                    ,:DA106-GMM-FIR-NAME                                02611799
                    ,:DA106-GMM-LAST-NAME                               02611899
                    ,:DA106-DMM                                         02611999
                    ,:DA106-DMM-FIR-NAME                                02612099
                    ,:DA106-DMM-LAST-NAME                               02612199
                    ,:DA106-BUYER                                       02612299
                    ,:DA106-BYR-FIR-NAME                                02612399
                    ,:DA106-BYR-LAST-NAME                               02612499
               FROM TMDSEAR A                                           02612599
                   ,TMDSEAR B                                           02612699
                   ,TMDSEAR C                                           02612799
                   ,TRSPBAR F                                           02613099
                   ,TRSPBAR G                                           02613199
                   ,TRSPBAR H                                           02613299
                                                                        02613399
               WHERE A.OPERCO_NBR = :PC-OPERCO-NBR                      02613499
                 AND B.OPERCO_NBR = A.OPERCO_NBR                        02613599
                 AND C.OPERCO_NBR = A.OPERCO_NBR                        02613699
                 AND A.MDSELV_CDE = :PC-MDSELV-GMA                      02613999
                 AND B.MDSELV_CDE = :PC-MDSELV-DMA                      02614099
                 AND C.MDSELV_CDE = :PC-MDSELV-DPT                      02614199
                                                                        02614499
                 AND A.GMA_NBR    = B.GMA_NBR                           02614599
                 AND B.DMA_NBR    = C.DMA_NBR                           02614699
                 AND C.DEPT_NBR   = :AVF-DEPT-NBR                       02614999
                                                                        02615299
                 AND :CC-DATE BETWEEN A.STRT_DTE AND A.END_DTE          02615399
                 AND :CC-DATE BETWEEN B.STRT_DTE AND B.END_DTE          02615499
                 AND :CC-DATE BETWEEN C.STRT_DTE AND C.END_DTE          02615599
                                                                        02615899
                 AND A.MDSEAR_DKEY = F.MDSEAR_DKEY                      02615999
                 AND F.RSPLVL_CDE  = :PC-RSPLVL-CDE-GMM                 02616099
                 AND :CC-DATE BETWEEN F.WORKER_STRT_DTE AND             02616199
                                      F.WORKER_END_DTE                  02616299
                                                                        02616399
                 AND B.MDSEAR_DKEY = G.MDSEAR_DKEY                      02616499
                 AND G.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM                 02616599
                 AND :CC-DATE BETWEEN G.WORKER_STRT_DTE AND             02616699
                                      G.WORKER_END_DTE                  02616799
                                                                        02616899
                 AND C.MDSEAR_DKEY = H.MDSEAR_DKEY                      02616999
                 AND H.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR                 02617099
                 AND :CC-DATE BETWEEN H.WORKER_STRT_DTE AND             02617199
                                      H.WORKER_END_DTE                  02617299
           END-EXEC.                                                    02618099
                                                                        02619799
           EVALUATE TRUE                                                02619899
               WHEN SQLCODE = ZERO                                      02619999
                   SET MBS-INFO-FOUND TO TRUE                           02620099
               WHEN SQLCODE = +100                                      02620199
                   DISPLAY ' '                                          02620299
                   DISPLAY 'MBS INFO NOT FOUND FOR '                    02620399
                           'DEPT ' AVF-DEPT-NBR                         02620499
                           '/' AVF-SUB-CL-NBR                           02620599
                           'DATE ' CC-DATE                              02620699
                   SET MBS-INFO-NOT-FOUND TO TRUE                       02620799
               WHEN OTHER                                               02620899
                   MOVE '4000-GET-MBS-DESC'                             02620999
                       TO PV-PARAGRAPH-NAME                             02621099
                   MOVE 'SELECT ROW FROM TMDSEAR/TRSPBAR'               02621199
                       TO PV-DB2-OPER-ATTEMPTED                         02621299
                   DISPLAY 'DEPT/SUB CL ' AVF-DEPT-NBR                  02621399
                           '/' AVF-SUB-CL-NBR                           02621499
                           'DATE ' CC-DATE                              02621599
                   PERFORM 9999-SQL-ERROR                               02621699
           END-EVALUATE.                                                02621799
                                                                        02622099
           IF MBS-INFO-NOT-FOUND                                        02630099
               PERFORM 4100-SET-MBS-TO-UNKNOWN                          02720099
           END-IF.                                                      02740099
                                                                        02750099
      ***************************************************************** 02760099
      * 4100-SET-MBS-TO-UNKNOWN.                                        02770099
      *   SET MBS MBS DESCRIPTION/NAME FIELDS TO "UNKNOWN" SINCE NOTHING02780099
      * WAS FOUND FOR THE DEPT BEING PROCESSED.                         02790099
      ***************************************************************** 02800099
       4100-SET-MBS-TO-UNKNOWN.                                         02810099
                                                                        02820099
           MOVE SPACES TO DA106-GMM                                     02830099
                          DA106-GMM-FIR-NAME                            02831099
                          DA106-GMM-LAST-NAME                           02832099
                          DA106-DMM                                     02840099
                          DA106-DMM-FIR-NAME                            02860099
                          DA106-DMM-LAST-NAME                           02870099
                          DA106-BUYER                                   02880099
                          DA106-BYR-FIR-NAME                            02890099
                          DA106-BYR-LAST-NAME.                          02891099
                                                                        02900099
      ******************************************************************02910099
      * 5000-GET-VENDOR-INFO.                                           02920099
      ******************************************************************02950099
       5000-GET-VENDOR-INFO.                                            02960099
                                                                        02960299
           EXEC SQL                                                     02960399
              SELECT A.PRIM_VND_NM                                      02960499
                    ,A.VND_NBR                                          02960599
              INTO  :DA106-VENDOR-NAME                                  02960799
                   ,:DA106-VENDOR-NBR                                   02960899
              FROM XMT_VND A                                            02961099
              WHERE                                                     02961199
                   A.ENT_ID = :PV-AVF-ENT-ID                            02961299
           END-EXEC.                                                    02961399
                                                                        02961499
           EVALUATE TRUE                                                02961599
               WHEN SQLCODE = ZERO                                      02961699
                   SET VND-INFO-FOUND TO TRUE                           02961799
               WHEN SQLCODE = +100                                      02961899
                   DISPLAY ' '                                          02961999
                   DISPLAY 'VND INFO NOT FOUND FOR '                    02962099
                           'ENTITY ' AVF-ENT-ID                         02962199
                   SET VND-INFO-NOT-FOUND TO TRUE                       02962299
               WHEN OTHER                                               02962399
                   MOVE '5000-GET-VENDOR-INFO'                          02962499
                       TO PV-PARAGRAPH-NAME                             02962599
                   MOVE 'SELECT ROW FROM XMT_VND'                       02962699
                       TO PV-DB2-OPER-ATTEMPTED                         02962799
                   DISPLAY 'ENTITY ' AVF-ENT-ID                         02962899
                   PERFORM 9999-SQL-ERROR                               02962999
           END-EVALUATE.                                                02963099
                                                                        02963199
           IF VND-INFO-NOT-FOUND                                        02963599
               PERFORM 5100-SET-VND-TO-UNKNOWN                          02963699
           END-IF.                                                      02963799
                                                                        02963899
      ***************************************************************** 02963999
      * 5100-SET-VND-TO-UNKNOWN.                                        02964099
      *   SET VENDOR DESCRIPTION/NAME FIELDS TO "UNKNOWN" SINCE NOTHING 02964199
      * WAS FOUND FOR THE DEPT BEING PROCESSED.                         02964299
      ***************************************************************** 02964399
       5100-SET-VND-TO-UNKNOWN.                                         02964499
                                                                        02964599
           MOVE SPACES TO DA106-VENDOR-NAME                             02964699
                          DA106-VENDOR-NBR.                             02964799
                                                                        02965099
      ******************************************************************02965199
      * 6000-WRITE-EXTRACT-REC.                                         02965299
      ******************************************************************02965399
       6000-WRITE-EXTRACT-REC.                                          02966099
                                                                        02970099
           WRITE OUT-DA-RECORD FROM DA106-AVL-VND-FUND-REC.             02980099
                                                                        02990099
      ******************************************************************05199299
      *  9000  READS DATE FROM INPUT FILE                               05199399
      ******************************************************************05199499
                                                                        05199599
       9000-READ-INPUT-DATE.                                            05199699
           READ IN-CC-FILE                                              05199799
               INTO CC-CONTROL-CARD                                     05199899
           END-READ.                                                    05199999
                                                                        05200099
      ******************************************************************05200199
      * 9100-READ-IN-DA-FILE.                                           05201099
      *     THE ROUTINE READS IN THE ORACLE SOURCE CONTROL CARD DACC010.05210099
      * THE CARD DEFINES WHICH ORACLE ENVIRONMENT WE ARE PROCESSING     05220099
      * FROM.                                                           05230099
      ******************************************************************05240099
       9100-READ-IN-DA-FILE.                                            05250099
                                                                        05260099
           READ IN-DA-FILE                                              05291099
               INTO AVL-VND-FUND-REC                                    05292099
                   AT END                                               05293099
                       MOVE 'Y' TO PS-INP02-DONE-SW                     05293199
           END-READ.                                                    05295099
      ******************************************************************05300099
      *  THIS PROCEDURE PROCESSES THE END OF JOB LOGIC.                 05310099
      ******************************************************************05320099
       9900-EOJ-LOGIC.                                                  05330014
                                                                        05350099
           CLOSE  OUT-DA-FILE.                                          05360014
           CLOSE  IN-DA-FILE.                                           05370099
           CLOSE  IN-CC-FILE.                                           05371099
                                                                        05380099
      ******************************************************************05420099
      *   9999-ABEND.                                                   05430099
      *      ERROR ABEND ROUTINE                                        05440099
      ******************************************************************05450099
       9999-SQL-ERROR.                                                  05460000
                                                                        05470000
           MOVE SQLCODE    TO SQLCODE2.                                 05480000
           MOVE SQLERRD(3) TO SQLERRD3.                                 05490000
           DISPLAY '** ABEND **       ** = SQLERROR'.                   05500000
           DISPLAY '** PROGRAM        ** = DAKUT313'.                   05510099
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  05520000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  05530000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   05540000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  05550000
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                05560000
                                                                        05570000
           MOVE +4013 TO PV-ABEND-CODE.                                 05660000
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         05670099
                                                                        05680000
      *---------------------------------------------------------------* 05690014
      *    END OF SOURCE CODE FOR DAKUT313                            * 05700099
      *---------------------------------------------------------------* 05710014
