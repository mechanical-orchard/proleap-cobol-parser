      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT084.                                        
       AUTHOR.                 COLLEEN CUMMINGS.                                
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           JUNE 2003.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT084 - ELIMINATE S/D STAT CDE DUPLICATE 090'S FOR       *        
      *               DATA INTEGRITY PROCESS (PMKUT078).               *        
      *    NOTE:  THIS IS A NON-DATABASE PROGRAM (NOT DB2 OR ORACLE).  *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      *    07/29/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *    IN B200- REMOVED COMPARE OF PM706-LAST-UPD-TMST TO          *        
      *    PV-PREV-LAST-UPD-TMST.  WAS CAUSING DUPLICATE RECORDS WHEN  *        
      *    THE SAME SKU HAD TWO SIMPLE PRICES ON PMT_MDSE_LNE_PRICG    *        
      *    BUT THE SAME GROUP PRICE (HAD A DIFFERENT LAST UPD TMST,    *        
      *    BUT EVERYTHING ELSE WAS THE SAME).                          *        
      *                                                                *        
      *    07/31/2003 - COLLEEN CUMMINGS - WR21770                     *        
      *    IN B200- REMOVED COMPARE OF PM706-ASSN-LVL-CDE TO           *        
      *    PV-PREV-ASSN-LVL-CDE.   WAS CAUSING DUPLICATE RECORDS WHEN  *        
      *    THE SAME SKU HAD SAME GROUP PRICE ON PMT_MDSE_LNE_PRICG     *        
      *    BUT DIFFERENT ASSN LVL CDES ON PMT_MDSE_LNE_PRICG BECAUSE   *        
      *    THE EACH PRICE WAS VERSIONED (BUT THE GROUP PRICE WASN'T).  *        
      *    ALSO REMOVED SOME DISPLAYS - TOO MUCH SYSOUT.               *        
      *                                                                *        
      *    MM/DD/YYYY - NAME HERE - WR NO. HERE                        *        
      *                                                                *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRICING-FILE                                                  
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT PRICING-FILE-OUT                                              
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRICING-FILE                                                         
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRICING-LINE                     PIC  X(258).                        
                                                                                
       FD  PRICING-FILE-OUT                                                     
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  PRICING-LINE-OUT                 PIC  X(258).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
          05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)     VALUE                 
                                                          'PMKUT084'.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-READ-FILE-SW           PIC  X(01)     VALUE 'N'.              
               88  PS-READ-FILE-END                     VALUE 'Y'.              
               88  PS-READ-FILE-START                   VALUE 'N'.              
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACES.         
           05  PV-OPERATION-MSG             PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-1           PIC  X(40)    VALUE SPACES.         
           05  PV-OPERATION-MSG-2           PIC  X(40)    VALUE SPACES.         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-SKIPPED-CNT           PIC 9(11) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-RECS-WRITTEN              PIC S9(11)  COMP-3                  
                                                           VALUE ZEROS.         
                                                                                
       01  PV-PREV-PM706-RECORD.                                                
         05 PV-PREV-BUSINESS-EVNT-ID       PIC 9(03) VALUE ZEROS.               
         05 PV-PREV-ACTION-CDE             PIC X(01) VALUE SPACES.              
         05 PM-PREV-BUSINESS-EVNT-CRE-TMST   PIC X(26) VALUE SPACES.            
         05 PV-PREV-PROMO-INTFC-ID         PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-PROMO-INTFC-SUB-ID     PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-PRCG-TYP-CDE           PIC X(03) VALUE SPACES.              
         05 PV-PREV-PROMO-ID               PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-PROMO-ITM-ID           PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-MDSE-LNE-ID            PIC 9(10) VALUE ZEROS.               
         05 PV-PREV-MDSE-HIER-CDE          PIC X(03) VALUE SPACES.              
         05 PV-PREV-DEPT-NBR               PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-MAJ-CL-NBR             PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-SUB-CL-NBR             PIC 9(04) VALUE ZEROS.               
         05 PV-PREV-STYLE-ID               PIC 9(08) VALUE ZEROS.               
         05 PV-PREV-SKU-NBR                PIC 9(08) VALUE ZEROS.               
         05 PV-PREV-ADV-IND                PIC X(01) VALUE SPACES.              
         05 PV-PREV-CLRNC-TYP-CDE          PIC X(01) VALUE SPACES.              
         05 PV-PREV-PRICG-AMT              PIC 9(07).9(03)                      
                                                     VALUE ZEROS.               
         05 PV-PREV-GP-QTY                 PIC 9(05) VALUE ZEROS.               
         05 PV-PREV-GP-AMT                 PIC 9(07).99                         
                                                     VALUE ZEROS.               
         05 PV-PREV-THRS-QTY               PIC 9(05) VALUE ZEROS.               
         05 PV-PREV-THRS-AMT               PIC 9(07).99                         
                                                     VALUE ZEROS.               
         05 PV-PREV-DISC-AMT               PIC 9(07).99                         
                                                     VALUE ZEROS.               
         05 PV-PREV-DISC-PCT               PIC 9(03).9(03)                      
                                                     VALUE ZEROS.               
         05 PV-PREV-RCPT-CMMT-TXT          PIC X(40) VALUE SPACES.              
         05 PV-PREV-BUY-THRS-AMT           PIC 9(07).9(03)                      
                                                     VALUE ZEROS.               
         05 PV-PREV-MUST-BUY-ALL-IND       PIC X(01) VALUE SPACES.              
         05 PV-PREV-RPETG-BWP-IND          PIC X(01) VALUE SPACES.              
         05 PV-PREV-ASSN-LVL-CDE           PIC X(01) VALUE SPACES.              
         05 PV-PREV-ESL-TOPR-TYP           PIC X(03) VALUE SPACES.              
         05 PV-PREV-STAT-CDE               PIC X(01) VALUE SPACES.              
         05 PV-PREV-LAST-UPD-TMST          PIC X(26) VALUE SPACES.              
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE PRICING BUSINESS EVENT.                             
      ******************************************************************        
           COPY PMRD706.                                                        
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE TOTAL RECORD                                        
      ******************************************************************        
           COPY PMRD700.                                                        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B150-READ-PRICING-FILE.                                      
           IF PS-READ-FILE-END                                                  
               MOVE 'NO PRICING FILE TO PROCESS'                                
                   TO PV-OPERATION-MSG-1                                        
               SET ABEND-4002-READ-ERROR   TO TRUE                              
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B200-PROCESS-AND-WRITE-FILE                                  
             UNTIL PS-READ-FILE-END.                                            
           DISPLAY 'RECORDS WRITTEN TO FILE = ' PA-RECS-WRITTEN.                
           DISPLAY 'RECORDS SKIPPED         = ' PA-RECS-SKIPPED-CNT.            
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES                                               *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE CONTROL CARD AND THE TWO OUTPUT FILES             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  PRICING-FILE                                             
                OUTPUT PRICING-FILE-OUT.                                        
                                                                                
      *****************************************************************         
      * B150-READ-PRICING-FILE                                        *         
      *****************************************************************         
       B150-READ-PRICING-FILE.                                                  
                                                                                
           MOVE 'B150-READ-PRICING-FILE'        TO PV-PARAGRAPH-NAME.           
           SET PS-READ-FILE-START       TO TRUE.                                
                                                                                
           READ PRICING-FILE INTO PM706-PRICING-RECORD                          
               AT END                                                           
                   SET PS-READ-FILE-END TO TRUE                                 
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * B200-PROCESS-AND-WRITE-FILE                                   *         
      *****************************************************************         
       B200-PROCESS-AND-WRITE-FILE.                                             
                                                                                
           IF PM706-ACTION-CDE = 'T'                                            
               MOVE PM706-BUSINESS-EVNT-ID TO PM700-BUSINESS-EVNT-ID            
               MOVE PA-RECS-WRITTEN TO PM700-TOTAL-NBR-OF-RECS                  
               WRITE PRICING-LINE-OUT FROM PM700-TOTAL-RECORD                   
           ELSE                                                                 
               IF PM706-PROMO-INTFC-ID           =                              
                      PV-PREV-PROMO-INTFC-ID                                    
                  AND PM706-PROMO-INTFC-SUB-ID   =                              
                      PV-PREV-PROMO-INTFC-SUB-ID                                
                  AND PM706-PRCG-TYP-CDE         =                              
                      PV-PREV-PRCG-TYP-CDE                                      
                  AND PM706-PROMO-ID             =                              
                      PV-PREV-PROMO-ID                                          
                  AND PM706-PROMO-ITM-ID         =                              
                      PV-PREV-PROMO-ITM-ID                                      
                  AND PM706-MDSE-LNE-ID          =                              
                      PV-PREV-MDSE-LNE-ID                                       
                  AND PM706-MDSE-HIER-CDE        =                              
                      PV-PREV-MDSE-HIER-CDE                                     
                  AND PM706-DEPT-NBR             =                              
                      PV-PREV-DEPT-NBR                                          
                  AND PM706-MAJ-CL-NBR           =                              
                      PV-PREV-MAJ-CL-NBR                                        
                  AND PM706-SUB-CL-NBR           =                              
                      PV-PREV-SUB-CL-NBR                                        
                  AND PM706-SKU-NBR              =                              
                      PV-PREV-SKU-NBR                                           
                  AND PM706-ADV-IND              =                              
                      PV-PREV-ADV-IND                                           
                  AND PM706-CLRNC-TYP-CDE        =                              
                      PV-PREV-CLRNC-TYP-CDE                                     
                  AND PM706-PRICG-AMT            =                              
                      PV-PREV-PRICG-AMT                                         
                  AND PM706-GP-QTY               =                              
                      PV-PREV-GP-QTY                                            
                  AND PM706-GP-AMT               =                              
                      PV-PREV-GP-AMT                                            
                  AND PM706-THRS-QTY             =                              
                      PV-PREV-THRS-QTY                                          
                  AND PM706-THRS-AMT             =                              
                      PV-PREV-THRS-AMT                                          
                  AND PM706-DISC-AMT             =                              
                      PV-PREV-DISC-AMT                                          
                  AND PM706-DISC-PCT             =                              
                      PV-PREV-DISC-PCT                                          
                  AND PM706-RCPT-CMMT-TXT        =                              
                      PV-PREV-RCPT-CMMT-TXT                                     
                  AND PM706-BUY-THRS-AMT         =                              
                      PV-PREV-BUY-THRS-AMT                                      
                  AND PM706-MUST-BUY-ALL-IND      =                             
                      PV-PREV-MUST-BUY-ALL-IND                                  
                  AND PM706-RPETG-BWP-IND        =                              
                      PV-PREV-RPETG-BWP-IND                                     
                  AND PM706-ESL-TOPR-TYP         =                              
                      PV-PREV-ESL-TOPR-TYP                                      
                                                                                
                  ADD +1 TO PA-RECS-SKIPPED-CNT                                 
                  DISPLAY 'SKIPPED THIS RECORD - DUP:'                          
                  DISPLAY 'PM706-PROMO-INTFC-ID         = '                     
                      PM706-PROMO-INTFC-ID                                      
                  DISPLAY 'PM706-PROMO-INTFC-SUB-ID     = '                     
                      PM706-PROMO-INTFC-SUB-ID                                  
                  DISPLAY 'PM706-PRCG-TYP-CDE           = '                     
                      PM706-PRCG-TYP-CDE                                        
                  DISPLAY 'PM706-MDSE-LNE-ID            = '                     
                      PM706-MDSE-LNE-ID                                         
                  DISPLAY 'PM706-ASSN-LVL-CDE           = '                     
                      PM706-ASSN-LVL-CDE                                        
                  DISPLAY 'PM706-STAT-CDE               = '                     
                      PM706-STAT-CDE                                            
                  DISPLAY 'PM706-LAST-UPD-TMST          = '                     
                      PM706-LAST-UPD-TMST                                       
               ELSE                                                             
                  WRITE PRICING-LINE-OUT FROM                                   
                        PRICING-LINE                                            
                  ADD +1 TO PA-RECS-WRITTEN                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-READ-FILE-START                                                
               MOVE PM706-PRICING-RECORD TO                                     
                    PV-PREV-PM706-RECORD                                        
               PERFORM B150-READ-PRICING-FILE                                   
           ELSE                                                                 
               DISPLAY 'END'                                                    
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE CONTROL CARD AND TWO OUTPUT FILES.               *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  PRICING-FILE                                                  
                  PRICING-FILE-OUT.                                             
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
