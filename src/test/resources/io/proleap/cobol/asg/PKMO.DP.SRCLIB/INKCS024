      *-----------------------*                                         12/20/93
00002  IDENTIFICATION DIVISION.                                         INKCS024
00003 *-----------------------*                                            LV032
00004  PROGRAM-ID.    INKCS024.                                         INKCS024
00005  AUTHOR.        TESS BESTE.                                       INKCS024
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS024
00007  DATE-WRITTEN.  08-10-93.                                         INKCS024
00008  DATE-COMPILED.                                                   INKCS024
00009                                                                   INKCS024
00010 *---------------------------------------------------------------* INKCS024
00011 *    I024 - STOCKROOM WITHDRAWAL SHEET INQUIRY PROGRAM          * INKCS024
00012 *                                                               * INKCS024
00013 *    THIS PROGRAM DISPLAYS THE SKU, UPC, QUANTITY, AND UNIT     * INKCS024
00014 *    PRICE FOR EACH LINE ON A SHEET.                            * INKCS024
00015 *                                                               * INKCS024
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- -------------------------------          
W21732* 10/25/2002 IGSI                CHG ID:W21732                            
W21732* ACCEPT NON-INTELLIGENT SKU IN PLACE OF THE CURRENT DEPT/CLASS/          
W21732* SEQ NUMBER.                                                             
00006 *                                                                         
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
      *****************************************************************         
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
00017                                                                   INKCS024
00018  ENVIRONMENT DIVISION.                                            INKCS024
00020  DATA DIVISION.                                                   INKCS024
00021                                                                   INKCS024
00022  WORKING-STORAGE SECTION.                                         INKCS024
00023                                                                   INKCS024
00024  01  PC-PROGRAM-CONSTANTS.                                        INKCS024
00025      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS024
00026                                                     'IN024A  '.   INKCS024
00027      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS024
00028                                                     'INKM024 '.   INKCS024
00029      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS024
00030                                                     'INKCS024'.   INKCS024
00031      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS024
00032      05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.    INKCS024
00033      05  PC-LINES-PER-PANEL             PIC S9(04)  VALUE +16        CL**3
00034                                                     COMP SYNC.    INKCS024
00035                                                                   INKCS024
00036  01  PC-TSYMSG-NUMBERS.                                           INKCS024
00037      05  PC-TSYMSG-00005                PIC 9(5)    VALUE 00005.     CL*23
00038      05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.     CL*23
00039      05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.  INKCS024
00040      05  PC-TSYMSG-00137                PIC 9(5)    VALUE 00137.     CL*18
00041      05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.  INKCS024
00042      05  PC-TSYMSG-00302                PIC 9(5)    VALUE 00302.  INKCS024
00043                                                                   INKCS024
00044  01  PS-PROGRAM-SUBSCRIPTS.                                       INKCS024
00045      05  PS-SUB                         PIC S9(04)   VALUE +0     INKCS024
00046                                                      COMP SYNC.   INKCS024
00047                                                                   INKCS024
00048  01  PS-PROGRAM-SWITCHES.                                         INKCS024
00049      05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.   INKCS024
00050          88  PS-NO-ERROR                             VALUE 'Y'.   INKCS024
00051          88  PS-ERROR                                VALUE 'N'.   INKCS024
00052      05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.   INKCS024
00053          88  PS-LIST-END                             VALUE 'Y'.   INKCS024
00054          88  PS-NO-LIST-END                          VALUE 'N'.   INKCS024
00055      EJECT                                                        INKCS024
00056  01  PV-PROGRAM-VARIABLES.                                        INKCS024
00057      05  PV-CICS-RESP                   PIC S9(04)   VALUE +0     INKCS024
00058                                                      COMP SYNC.   INKCS024
00059                                                                   INKCS024
00060  01  PV-DB2-CRITERIA-AREA.                                        INKCS024
W26600     15  PV-DB2-STORE-NBR-X             PIC  X(04).               INKCS024
W26600     15  PV-DB2-STORE-NBR REDEFINES                               INKCS024
W26600         PV-DB2-STORE-NBR-X             PIC  9(04).               INKCS024
00062      15  PV-DB2-SHEET-NBR               PIC S9(09)  VALUE ZERO    INKCS024
00063                                                     COMP.         INKCS024
00064                                                                   INKCS024
00065  EJECT                                                            INKCS024
00066 *---------------------------------------------------------------* INKCS024
00067 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS024
00068 *---------------------------------------------------------------* INKCS024
00069                                                                   INKCS024
00070      COPY DPWS030.                                                INKCS024
1122RM     COPY DPWS930.                                                INKCS024
00071  EJECT                                                            INKCS024
00072 *---------------------------------------------------------------* INKCS024
00073 *    STANDARD COMMAREA.                                         * INKCS024
00074 *---------------------------------------------------------------* INKCS024
00075                                                                   INKCS024
00076      COPY DPWS020.                                                INKCS024
00077      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS024
00078                                                                   INKCS024
00079 *---------------------------------------------------------------* INKCS024
00080 *    INTER-APPLICATION COMMAREA.                                * INKCS024
00081 *---------------------------------------------------------------* INKCS024
00082                                                                   INKCS024
00083          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS024
W26600     COPY INWS001.                                                   CL*32
00085 *---------------------------------------------------------------* INKCS024
00086 *    SPECIFIC COMMMAREA FOR INKCS024.                           * INKCS024
00087 *---------------------------------------------------------------* INKCS024
00088          10  ASC-SPECIFIC-COMMAREA.                               INKCS024
00089              15  ASC-LIST-KEYS.                                   INKCS024
W26600                 20  ASC-KEY-STORE-NBR-X             PIC  X(04).  INKCS024
00091                  20  ASC-KEY-SHEET-NBR-X.                            CL**9
00092                      25  ASC-KEY-SHEET-NBR           PIC  9(06).     CL*30
00093                                                                      CL**3
00094              15  ASC-STORE-NAME                      PIC  X(25).     CL**8
00095              15  ASC-ITEM-ARRAY.                                     CL**3
00096                  20  ASC-ITEM-ENTRIES OCCURS 16 TIMES.               CL**3
W21732                     25  ASC-SKU-NBR             PIC  X(08).         CL**3
00100                      25  ASC-UPC-NBR             PIC S9(15)          CL**3
00101                                                      COMP-3.         CL**3
00102                      25  ASC-INV-QTY             PIC S9(09)          CL**3
00103                                                      COMP.           CL**3
00104                      25  ASC-ITM-UNIT-PR-AMT     PIC S9(5)V99        CL**3
00105                                                      COMP-3.         CL**3
00106                                                                   INKCS024
00107              15  ASC-NUMBER-OF-SHEET-LINES           PIC S9(04)      CL*22
00108                                                      COMP SYNC.      CL*19
00109              15  ASC-SEL-SUB                         PIC S9(04)   INKCS024
00110                                                          COMP.    INKCS024
00111              15  ASC-SEL-ITEM                        PIC S9(04)   INKCS024
00112                                                          COMP.    INKCS024
00113              15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)   INKCS024
00114                                                          COMP-3.  INKCS024
00115              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS024
00116                  88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'. INKCS024
00117                  88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'. INKCS024
00118              15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).  INKCS024
00119                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. INKCS024
00120              15  ASC-SEL-QUEUE-NAME.                              INKCS024
00121                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS024
00122                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS024
00123                                                           COMP-3. INKCS024
00124                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS024
00125      EJECT                                                        INKCS024
00126 *---------------------------------------------------------------* INKCS024
00127 *    SHEET SELECTION TSQ B QUEUE RECORD LAYOUT.                 * INKCS024
00128 *---------------------------------------------------------------* INKCS024
00129                                                                   INKCS024
00130      COPY INWS022.                                                   CL*32
00131                                                                   INKCS024
00132      EJECT                                                        INKCS024
00133 *---------------------------------------------------------------* INKCS024
00134 *    ABEND PROCESSING COPYBOOK.                                 * INKCS024
00135 *---------------------------------------------------------------* INKCS024
00136                                                                   INKCS024
00137      COPY DPWS013.                                                INKCS024
00138                                                                   INKCS024
00139      EJECT                                                        INKCS024
00140 *---------------------------------------------------------------* INKCS024
00141 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS024
00142 *---------------------------------------------------------------* INKCS024
00143                                                                   INKCS024
00144      COPY DPWS015.                                                INKCS024
00145                                                                   INKCS024
00146 *---------------------------------------------------------------* INKCS024
00147 *    FUNCTION KEYS COPYBOOK.                                    * INKCS024
00148 *---------------------------------------------------------------* INKCS024
00149                                                                   INKCS024
00150      COPY DPWS016.                                                INKCS024
00151                                                                   INKCS024
00152 *---------------------------------------------------------------* INKCS024
00153 *    MAP LAYOUT                                                 * INKCS024
00154 *---------------------------------------------------------------* INKCS024
00155                                                                   INKCS024
W26600     COPY INKM024.                                                INKCS024
00157                                                                   INKCS024
00158  01  MR-OCCURS-LAYOUT          REDEFINES  IN024AO.                INKCS024
W26600     05  FILLER                PIC X(62).                            CL*31
00160      05  MR-DETAIL-LINE OCCURS 16 TIMES.                             CL*12
W21732         10  MR-SKUL           PIC S9(04) COMP.                   INKCS024
W21732         10  MR-SKUA           PIC  X(01).                        INKCS024
W21732         10  MR-SKUC           PIC  X(01).                        INKCS024
W21732         10  MR-SKUH           PIC  X(01).                        INKCS024
W21732         10  MR-SKU            PIC  X(08).                        INKCS024
00176          10  MR-UPCL           PIC S9(04) COMP.                   INKCS024
00177          10  MR-UPCA           PIC  X(01).                        INKCS024
00178          10  MR-UPDC           PIC  X(01).                        INKCS024
00179          10  MR-UPCH           PIC  X(01).                        INKCS024
00180          10  MR-UPC-X.                                            INKCS024
00181              15  MR-UPC        PIC  Z(15).                        INKCS024
00182          10  MR-QTYL           PIC S9(04) COMP.                   INKCS024
00183          10  MR-QTYA           PIC  X(01).                        INKCS024
00184          10  MR-QTYC           PIC  X(01).                        INKCS024
00185          10  MR-QTYH           PIC  X(01).                        INKCS024
00186          10  MR-QTY-X.                                            INKCS024
00187              15  MR-QTY        PIC  Z(07).                        INKCS024
00188          10  MR-PRCEL          PIC S9(04) COMP.                   INKCS024
00189          10  MR-PRCEA          PIC  X(01).                        INKCS024
00190          10  MR-PRCEC          PIC  X(01).                        INKCS024
00191          10  MR-PRCEH          PIC  X(01).                        INKCS024
00192          10  MR-PRCE-X.                                           INKCS024
00193              15  MR-PRCE       PIC  Z(07).99.                     INKCS024
00194                                                                   INKCS024
00195  EJECT                                                            INKCS024
00196 *---------------------------------------------------------------* INKCS024
00197 *    DB2 AREA FOR STOCKROOM WITHDRAWAL SHEET LINES              * INKCS024
00198 *---------------------------------------------------------------* INKCS024
00199      EXEC SQL                                                     INKCS024
00200           INCLUDE TSTKRMW                                         INKCS024
00201      END-EXEC.                                                    INKCS024
00202 *---------------------------------------------------------------* INKCS024
00203 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS024
00204 *---------------------------------------------------------------* INKCS024
00205      EXEC SQL                                                     INKCS024
00206           INCLUDE SQLCA                                           INKCS024
00207      END-EXEC.                                                    INKCS024
00208                                                                   INKCS024
00209 *---------------------------------------------------------------* INKCS024
00210 *    CURSOR TO OBTAIN ALL LINES FOR A STOCKROOM WITHDRAWAL SHEET* INKCS024
00211 *---------------------------------------------------------------* INKCS024
00212                                                                   INKCS024
00213      EXEC SQL                                                     INKCS024
00214           DECLARE LINE-NBR-CSR CURSOR FOR                         INKCS024
00215           SELECT LINE_NBR                                            CL**6
W21732               , SKU_NBR                                          INKCS024
00219                , UPC_NBR                                          INKCS024
00220                , INV_QTY                                          INKCS024
00221                , ITM_UNIT_PR_AMT                                  INKCS024
00222           FROM   TSTKRMW                                          INKCS024
W26600          WHERE  LOC_NBR = :STKRMW-LOC-NBR   AND                  INKCS024
W21732                 SHEET_NBR = :STKRMW-SHEET-NBR                    INKCS024
00225           ORDER BY                                                INKCS024
00226                  LINE_NBR                                         INKCS024
00227      END-EXEC.                                                    INKCS024
00228                                                                   INKCS024
00229  EJECT                                                            INKCS024
00230  LINKAGE SECTION.                                                 INKCS024
00231                                                                   INKCS024
00232  01  DFHCOMMAREA.                                                 INKCS024
00233      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS024
00234                                         DEPENDING ON EIBCALEN.    INKCS024
00235          10  FILLER                     PIC X.                    INKCS024
00236                                                                   INKCS024
00237  EJECT                                                            INKCS024
00238  PROCEDURE DIVISION.                                              INKCS024
00239                                                                   INKCS024
00240  0000-MAIN-MODULE.                                                INKCS024
00241                                                                   INKCS024
00242      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS024
00243      MOVE +1                      TO DP030-NUMBER-OF-MAPS.        INKCS024
00244      MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.           INKCS024
00245      MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).          INKCS024
00246      SET DP030-RECEIVE-APPL-MAP   TO TRUE.                        INKCS024
00247      MOVE LENGTH OF IN024AI       TO DP030-MAP-LENGTH (1).        INKCS024
00248      MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).      INKCS024
00249      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS024
00250                                                                   INKCS024
00251      PERFORM 1000-CONTROL-PROCESSING.                             INKCS024
00252                                                                   INKCS024
00253      MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).      INKCS024
00254      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS024
00255                                                                   INKCS024
00256 *----------------------------------------------------------------*INKCS024
00257 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS024
00258 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS024
00259 *----------------------------------------------------------------*INKCS024
00260                                                                   INKCS024
00261  0001-CALL-CICS-ARCH-API.                                         INKCS024
00262                                                                   INKCS024
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS024
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
00264                            DFHCOMMAREA                            INKCS024
00265                            DP030-CICS-API-FIELDS                  INKCS024
00266                            DP020-STANDARD-COMMAREA                INKCS024
00267                            IN024AI.                               INKCS024
00268                                                                   INKCS024
00269      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS024
00270          CONTINUE                                                 INKCS024
00271      ELSE                                                         INKCS024
00272          SET DP013-NO-ROLLBACK TO TRUE                            INKCS024
00273          SET DP013-XCTL-DISPLAY-RESTART TO TRUE                   INKCS024
00274          SET DP013-CICS-ABEND TO TRUE                             INKCS024
00275          MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH        INKCS024
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS024
1122RM*             'T LINE'         TO DP013-MESSAGE-TEXT (2)          INKCS024
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00278          MOVE DP030-RETURN-CODE                                   INKCS024
00279                                TO DP013-MESSAGE-TEXT (3)          INKCS024
00280          PERFORM DP013-0000-PROCESS-ABEND                         INKCS024
00281      END-IF.                                                      INKCS024
00282                                                                   INKCS024
00283 *----------------------------------------------------------------*INKCS024
00284 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS024
00285 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS024
00286 *----------------------------------------------------------------*INKCS024
00287                                                                   INKCS024
00288  1000-CONTROL-PROCESSING.                                         INKCS024
00289                                                                   INKCS024
00290      EVALUATE TRUE                                                INKCS024
00291          WHEN DP020-NEXT-ACT-INITIAL                              INKCS024
00292              PERFORM 1100-PROCESS-INTER-APPL-COMM                 INKCS024
00293              IF  DP020-NEXT-ACT-APPL-ERROR                           CL*13
00294                  CONTINUE                                            CL*13
00295              ELSE                                                 INKCS024
00296                  PERFORM 4000-BUILD-INITIAL-PANEL                 INKCS024
00297              END-IF                                               INKCS024
00298                                                                   INKCS024
00299          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS024
00300              PERFORM 2000-PROCESS-PANEL                           INKCS024
00301                                                                   INKCS024
00302          WHEN DP020-NEXT-ACT-RETURN                               INKCS024
00303              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS024
00304                                                                   INKCS024
00305          WHEN OTHER                                               INKCS024
00306              SET DP013-LOGIC-ABEND TO TRUE                        INKCS024
00307              SET DP013-NO-ROLLBACK TO TRUE                        INKCS024
00308              MOVE '1000-CONTROL-PROCESSING'                       INKCS024
00309                                TO  DP013-PARAGRAPH                INKCS024
00310              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    INKCS024
00311                                TO  DP013-MESSAGE-TEXT(1)          INKCS024
00312              PERFORM DP013-0000-PROCESS-ABEND                     INKCS024
00313      END-EVALUATE.                                                INKCS024
00314                                                                   INKCS024
00315  EJECT                                                            INKCS024
00316 *----------------------------------------------------------------*INKCS024
00317 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA.          CL*13
00318 *----------------------------------------------------------------*INKCS024
00319                                                                   INKCS024
00320  1100-PROCESS-INTER-APPL-COMM.                                    INKCS024
00321                                                                   INKCS024
00322      INITIALIZE ASC-SPECIFIC-COMMAREA.                            INKCS024
00323      MOVE IN001-STORE-NAME     TO ASC-STORE-NAME.                    CL**8
00324      MOVE IN001-STORE-NBR-X    TO ASC-KEY-STORE-NBR-X             INKCS024
00325                                   PV-DB2-STORE-NBR-X.             INKCS024
00326      EVALUATE DP020-INT-APPL-FORMAT-IND                           INKCS024
00327          WHEN PC-CRIT-SPEC-FORMAT                                 INKCS024
00328              IF IN001-SHEET-NBR-X NOT NUMERIC                        CL*13
00329                  SET DP020-NEXT-ACT-APPL-ERROR                       CL*13
00330                      DP020-MSG-FATAL           TO TRUE               CL*13
00331 *                ---- ASTERISK NOT PERMITTED FOR SELECTED ----       CL*22
00332 *                ---- FUNCTION                            ----       CL*22
00333                  MOVE PC-TSYMSG-00137 TO DP020-MSG-NUMBER            CL*18
00334              ELSE                                                    CL*13
00335                  MOVE IN001-SHEET-NBR TO ASC-KEY-SHEET-NBR           CL*13
00336                                          PV-DB2-SHEET-NBR            CL*13
00337              END-IF                                                  CL*13
00338          WHEN PC-LIST-FORMAT                                      INKCS024
00339              SET ASC-USE-SELECTION-QUEUE TO TRUE                  INKCS024
00340              MOVE IN001-NUMBER-OF-TS-ITEMS                        INKCS024
00341                                TO ASC-NUMBER-OF-TS-ITEMS          INKCS024
00342              MOVE IN001-SEL-QUEUE-NAME                            INKCS024
00343                                TO ASC-SEL-QUEUE-NAME              INKCS024
00344              PERFORM 1120-PREPARE-LIST-ENTRY                      INKCS024
00345          WHEN OTHER                                               INKCS024
00346 *            ---- DATA DOES NOT EXIST ----                           CL*15
00347              MOVE PC-TSYMSG-00148                                    CL**8
00348                            TO DP020-MSG-NUMBER                       CL**8
00349              SET DP020-MSG-FATAL TO TRUE                             CL**8
00350              SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                   CL**8
00351      END-EVALUATE.                                                INKCS024
00352                                                                   INKCS024
00353 *----------------------------------------------------------------*INKCS024
00354 *    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *INKCS024
00355 *----------------------------------------------------------------*INKCS024
00356                                                                   INKCS024
00357  1120-PREPARE-LIST-ENTRY.                                         INKCS024
00358                                                                   INKCS024
00359      MOVE IN022-MAX-ITEMS TO ASC-SEL-SUB.                         INKCS024
00360      MOVE ZERO            TO ASC-SEL-ITEM.                        INKCS024
00361      PERFORM 2140-GET-NEXT-SHEET.                                 INKCS024
00362  EJECT                                                            INKCS024
00363 *----------------------------------------------------------------*INKCS024
00364 *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *INKCS024
00365 *    ON WHAT FUNCTION KEY WAS SELECTED.                          *INKCS024
00366 *----------------------------------------------------------------*INKCS024
00367                                                                   INKCS024
00368  2000-PROCESS-PANEL.                                              INKCS024
00369                                                                   INKCS024
00370      EVALUATE TRUE                                                INKCS024
00371          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS024
00372              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS024
00373                                                                   INKCS024
00374          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS024
00375              MOVE ASC-KEY-STORE-NBR-X TO PV-DB2-STORE-NBR-X          CL**2
00376              MOVE ASC-KEY-SHEET-NBR   TO PV-DB2-SHEET-NBR         INKCS024
00377              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS024
00378                                                                   INKCS024
00379          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             INKCS024
00380              IF  ASC-USE-SELECTION-QUEUE                          INKCS024
00381                  PERFORM 2140-GET-NEXT-SHEET                      INKCS024
00382                  IF PS-LIST-END                                   INKCS024
00383                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         INKCS024
00384                      SET PS-NO-LIST-END TO TRUE                   INKCS024
00385                  ELSE                                             INKCS024
00386                      MOVE ASC-KEY-STORE-NBR-X                        CL**8
00387                                              TO PV-DB2-STORE-NBR-X   CL**8
00388                      PERFORM 4000-BUILD-INITIAL-PANEL             INKCS024
00389                  END-IF                                           INKCS024
00390              ELSE                                                 INKCS024
00391 *                ---- BOTTOM OF LIST ----                            CL*15
00392                  MOVE PC-TSYMSG-00070                             INKCS024
00393                                   TO DP020-MSG-NUMBER             INKCS024
00394                  SET DP020-MSG-INFORMATIONAL TO TRUE              INKCS024
00395              END-IF                                               INKCS024
00396                                                                   INKCS024
00397          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)             INKCS024
00398              IF  ASC-USE-SELECTION-QUEUE                          INKCS024
00399                  PERFORM 2150-GET-PREV-SHEET                         CL**2
00400                  IF PS-LIST-END                                   INKCS024
00401                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN         INKCS024
00402                      SET PS-NO-LIST-END TO TRUE                   INKCS024
00403                  ELSE                                             INKCS024
00404                      MOVE ASC-KEY-STORE-NBR-X                        CL**8
00405                                            TO PV-DB2-STORE-NBR-X     CL**8
00406                      PERFORM 4000-BUILD-INITIAL-PANEL             INKCS024
00407                  END-IF                                           INKCS024
00408              ELSE                                                 INKCS024
00409 *                ---- TOP OF LIST ----                               CL*15
00410                  MOVE PC-TSYMSG-00069                             INKCS024
00411                                   TO DP020-MSG-NUMBER             INKCS024
00412                  SET DP020-MSG-INFORMATIONAL TO TRUE              INKCS024
00413              END-IF                                               INKCS024
00414                                                                   INKCS024
00415          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS024
00416              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**3
00417                                                                   INKCS024
00418          WHEN OTHER                                               INKCS024
00419              SET DP013-NO-ROLLBACK TO TRUE                        INKCS024
00420              SET DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS024
00421              SET DP013-CICS-ABEND TO TRUE                         INKCS024
00422              MOVE '2000-PROCESS-PANEL' TO DP013-PARAGRAPH            CL**3
00423              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS024
00424                                    TO DP013-MESSAGE-TEXT (1)      INKCS024
00425              PERFORM DP013-0000-PROCESS-ABEND                     INKCS024
00426                                                                   INKCS024
00427      END-EVALUATE.                                                INKCS024
00428  EJECT                                                            INKCS024
00429 *----------------------------------------------------------------*INKCS024
00430 *    GET THE NEXT SHEET IF A LIST OF SHEETS IS BEING PROCESSED.  *INKCS024
00431 *----------------------------------------------------------------*INKCS024
00432                                                                   INKCS024
00433  2140-GET-NEXT-SHEET.                                             INKCS024
00434                                                                   INKCS024
00435      ADD +1                       TO ASC-SEL-SUB.                 INKCS024
00436      IF  ASC-SEL-SUB > IN022-MAX-ITEMS                            INKCS024
00437          ADD +1                   TO ASC-SEL-ITEM                 INKCS024
00438          MOVE +1                  TO ASC-SEL-SUB                  INKCS024
00439      END-IF.                                                      INKCS024
00440      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                INKCS024
00441          PERFORM 9100-READ-SEL-QUEUE                              INKCS024
00442          IF  IN022-ITEM (ASC-SEL-SUB) > ZERO                      INKCS024
00443              MOVE IN022-SHEET-NBR (ASC-SEL-SUB)                   INKCS024
00444                                    TO ASC-KEY-SHEET-NBR           INKCS024
00445                                       PV-DB2-SHEET-NBR            INKCS024
00446              SET IN022-INQ (ASC-SEL-SUB) TO TRUE                  INKCS024
00447              PERFORM 9200-REWRITE-SEL-QUEUE                       INKCS024
00448          ELSE                                                     INKCS024
00449              SUBTRACT +1 FROM ASC-SEL-SUB                         INKCS024
00450 *            ---- BOTTOM OF LIST ----                                CL*15
00451              MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER             INKCS024
00452              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS024
00453              SET PS-LIST-END             TO TRUE                  INKCS024
00454          END-IF                                                   INKCS024
00455      ELSE                                                         INKCS024
00456          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS024
00457          MOVE IN022-MAX-ITEMS     TO ASC-SEL-SUB                  INKCS024
00458 *        ---- BOTTOM OF LIST ----                                    CL*15
00459          MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER             INKCS024
00460          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS024
00461          SET PS-LIST-END             TO TRUE                      INKCS024
00462      END-IF.                                                      INKCS024
00463                                                                   INKCS024
00464 *----------------------------------------------------------------*INKCS024
00465 *    GET THE PREVIOUS SHEET IF A LIST OF SHEETS IS BEING         *INKCS024
00466 *    PROCESSED.                                                  *INKCS024
00467 *----------------------------------------------------------------*INKCS024
00468                                                                   INKCS024
00469  2150-GET-PREV-SHEET.                                             INKCS024
00470                                                                   INKCS024
00471      SUBTRACT +1 FROM ASC-SEL-SUB.                                INKCS024
00472      IF  ASC-SEL-SUB < +1                                         INKCS024
00473          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS024
00474          MOVE IN022-MAX-ITEMS     TO ASC-SEL-SUB                  INKCS024
00475      END-IF.                                                      INKCS024
00476                                                                   INKCS024
00477      IF  ASC-SEL-ITEM > ZERO                                      INKCS024
00478          PERFORM 9100-READ-SEL-QUEUE                              INKCS024
00479          MOVE IN022-SHEET-NBR (ASC-SEL-SUB)                       INKCS024
00480                                   TO ASC-KEY-SHEET-NBR            INKCS024
00481                                      PV-DB2-SHEET-NBR             INKCS024
00482      ELSE                                                         INKCS024
00483          MOVE +1                  TO ASC-SEL-ITEM                 INKCS024
00484                                      ASC-SEL-SUB                  INKCS024
00485 *        ---- TOP OF LIST ----                                       CL*15
00486          MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER             INKCS024
00487          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS024
00488          SET PS-LIST-END             TO TRUE                      INKCS024
00489      END-IF.                                                      INKCS024
00490                                                                   INKCS024
00491  EJECT                                                            INKCS024
00492 *----------------------------------------------------------------*INKCS024
00493 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS024
00494 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS024
00495 *    DISPLAY THE PANEL TO USER.                                  *INKCS024
00496 *----------------------------------------------------------------*INKCS024
00497                                                                   INKCS024
00498  4000-BUILD-INITIAL-PANEL.                                        INKCS024
00499                                                                   INKCS024
00500      INITIALIZE ASC-ITEM-ARRAY.                                      CL*13
00501      SET ASC-ITEMS-LEFT-ON-DB TO  TRUE.                              CL*13
00502                                                                      CL*13
00503      PERFORM 4010-OPEN-CURSOR.                                       CL*13
00504                                                                      CL*13
00505      PERFORM 4300-READ-ITEMS-FROM-DB.                                CL*13
00506                                                                      CL*13
00507      PERFORM 4020-CLOSE-CURSOR.                                      CL*13
00508                                                                      CL*13
00509      IF ASC-NUMBER-OF-SHEET-LINES = ZERO                             CL*22
00510          SET DP020-NEXT-ACT-APPL-ERROR                               CL*22
00511              DP020-MSG-FATAL           TO TRUE                       CL*22
00512 *        ---- DATA DOES NOT EXIST ----                               CL*24
00513          MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                    CL*24
00514      ELSE                                                            CL*22
00515          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                        CL*22
00516      END-IF.                                                         CL*22
00517  EJECT                                                            INKCS024
00518 *----------------------------------------------------------------*INKCS024
00519 * OPEN THE CURSOR.                                               *INKCS024
00520 *----------------------------------------------------------------*INKCS024
00521                                                                   INKCS024
00522  4010-OPEN-CURSOR.                                                INKCS024
00523                                                                   INKCS024
W26600      MOVE PV-DB2-STORE-NBR   TO STKRMW-LOC-NBR.                          
W21732      MOVE PV-DB2-SHEET-NBR   TO STKRMW-SHEET-NBR.                        
00524      EXEC SQL                                                     INKCS024
00525           OPEN LINE-NBR-CSR                                       INKCS024
00526      END-EXEC.                                                    INKCS024
00527                                                                   INKCS024
00528      EVALUATE TRUE                                                INKCS024
00529          WHEN SQLCODE = ZERO                                      INKCS024
00530               CONTINUE                                            INKCS024
00531          WHEN SQLWARN0 NOT EQUAL SPACE                            INKCS024
00532          WHEN SQLCODE NOT EQUAL ZERO                              INKCS024
00533               MOVE '4010-OPEN-CURSOR'                             INKCS024
00534                            TO  DP013-PARAGRAPH                    INKCS024
00535               MOVE 'OPEN STOCKROOM WITHDRAWAL LINE-NBR CURSOR'    INKCS024
00536                            TO  DP013-MESSAGE-TEXT (1)             INKCS024
00537               MOVE SQLCA   TO  DP013-SQLCA                        INKCS024
00538               SET DP013-DB2-ABEND                                 INKCS024
00539                            TO  TRUE                               INKCS024
00540               PERFORM DP013-0000-PROCESS-ABEND                    INKCS024
00541      END-EVALUATE.                                                INKCS024
00542                                                                   INKCS024
00543  EJECT                                                            INKCS024
00544 *----------------------------------------------------------------*INKCS024
00545 * CLOSE THE CURSOR.                                              *INKCS024
00546 *----------------------------------------------------------------*INKCS024
00547                                                                   INKCS024
00548  4020-CLOSE-CURSOR.                                               INKCS024
00549                                                                   INKCS024
00550      EXEC SQL                                                     INKCS024
00551          CLOSE LINE-NBR-CSR                                       INKCS024
00552      END-EXEC.                                                    INKCS024
00553                                                                   INKCS024
00554      EVALUATE TRUE                                                INKCS024
00555          WHEN SQLCODE = ZERO                                      INKCS024
00556               CONTINUE                                            INKCS024
00557          WHEN SQLWARN0 NOT = SPACE                                   CL*15
00558          WHEN SQLCODE  NOT = ZERO                                    CL*15
00559               MOVE '4020-CLOSE-CURSOR'                            INKCS024
00560                            TO  DP013-PARAGRAPH                    INKCS024
00561               MOVE 'CLOSE STOCKROOM WITHDRAWAL LINE-NBR CURSOR'   INKCS024
00562                            TO  DP013-MESSAGE-TEXT (1)             INKCS024
00563               MOVE SQLCA   TO  DP013-SQLCA                        INKCS024
00564               SET DP013-DB2-ABEND                                 INKCS024
00565                            TO  TRUE                               INKCS024
00566               PERFORM DP013-0000-PROCESS-ABEND                    INKCS024
00567      END-EVALUATE.                                                INKCS024
00568                                                                   INKCS024
00569  EJECT                                                            INKCS024
00570 *----------------------------------------------------------------*INKCS024
00571 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS024
00572 *    ARRAY.                                                          CL**4
00573 *----------------------------------------------------------------*INKCS024
00574                                                                   INKCS024
00575  4300-READ-ITEMS-FROM-DB.                                         INKCS024
00576                                                                   INKCS024
00577      MOVE ZERO TO ASC-NUMBER-OF-SHEET-LINES.                         CL*22
00578      PERFORM                                                      INKCS024
00579          VARYING PS-SUB                                              CL**4
00580          FROM 1 BY 1                                              INKCS024
00581          UNTIL (PS-SUB > PC-LINES-PER-PANEL)                         CL**4
00582             OR ASC-NO-ITEMS-LEFT-ON-DB                               CL**5
00583                                                                   INKCS024
00584          PERFORM 4310-FETCH                                       INKCS024
00585                                                                   INKCS024
00586          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS024
W21732             MOVE STKRMW-SKU-NBR TO ASC-SKU-NBR         (PS-SUB)     CL**4
00592              MOVE STKRMW-UPC-NBR TO ASC-UPC-NBR         (PS-SUB)     CL**4
00593              MOVE STKRMW-INV-QTY TO ASC-INV-QTY         (PS-SUB)     CL**4
00594              MOVE STKRMW-ITM-UNIT-PR-AMT                          INKCS024
00595                                  TO ASC-ITM-UNIT-PR-AMT (PS-SUB)     CL**4
00596              MOVE PS-SUB         TO ASC-NUMBER-OF-SHEET-LINES        CL*22
00597          END-IF                                                   INKCS024
00598      END-PERFORM.                                                 INKCS024
00599                                                                      CL**4
00600  EJECT                                                            INKCS024
00601 *----------------------------------------------------------------*INKCS024
00602 *    FETCH THE LINE-NBR CURSOR.                                  *   CL*25
00603 *----------------------------------------------------------------*INKCS024
00604                                                                   INKCS024
00605  4310-FETCH.                                                      INKCS024
00606                                                                   INKCS024
00607      EXEC SQL                                                        CL*14
00608           FETCH LINE-NBR-CSR                                         CL*14
00609           INTO  :STKRMW-LINE-NBR                                     CL*14
W21732              , :STKRMW-SKU-NBR                                      CL*14
00613               , :STKRMW-UPC-NBR                                      CL*14
00614               , :STKRMW-INV-QTY                                      CL*14
00615               , :STKRMW-ITM-UNIT-PR-AMT                              CL*14
00616      END-EXEC.                                                       CL*14
00617                                                                      CL*14
00618      EVALUATE TRUE                                                   CL*14
00619          WHEN SQLCODE = ZERO                                         CL*14
00620               CONTINUE                                               CL*19
00621          WHEN SQLCODE = +100                                         CL*14
00622              SET ASC-NO-ITEMS-LEFT-ON-DB TO TRUE                     CL*19
00623          WHEN SQLWARN0 NOT = SPACE                                   CL*14
00624          WHEN SQLCODE  NOT = ZERO                                    CL*14
00625              MOVE '4310-FETCH-CURSOR'                                CL*14
00626                           TO  DP013-PARAGRAPH                        CL*14
00627              MOVE 'FETCH STOCKROOM WITHDRAWAL LINE-NBR CURSOR'       CL*14
00628                           TO  DP013-MESSAGE-TEXT (1)                 CL*14
00629              MOVE SQLCA   TO  DP013-SQLCA                            CL*14
00630              SET DP013-DB2-ABEND                                     CL*14
00631                           TO  TRUE                                   CL*14
00632              PERFORM DP013-0000-PROCESS-ABEND                        CL*14
00633      END-EVALUATE.                                                   CL*14
00634                                                                      CL*19
00635                                                                      CL*19
00636  EJECT                                                            INKCS024
00637 *----------------------------------------------------------------*INKCS024
00638 *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *INKCS024
00639 *----------------------------------------------------------------*INKCS024
00640                                                                   INKCS024
00641  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS024
00642                                                                   INKCS024
W26600     MOVE ASC-KEY-STORE-NBR-X TO ASTRNBRO.                           CL**9
00643      MOVE ASC-STORE-NAME      TO ASTNAMEO.                           CL**9
00644      MOVE ASC-KEY-SHEET-NBR-X TO ASHEETO.                            CL**9
00645                                                                   INKCS024
00646      PERFORM VARYING PS-SUB FROM 1 BY 1                              CL*28
00647        UNTIL PS-SUB > PC-LINES-PER-PANEL                             CL*28
00648          IF  PS-SUB > ASC-NUMBER-OF-SHEET-LINES                      CL*28
W21732             MOVE SPACE TO MR-SKU    (PS-SUB)                        CL*28
00652                            MR-UPC-X  (PS-SUB)                        CL*28
00653                            MR-QTY-X  (PS-SUB)                        CL*28
00654                            MR-PRCE-X (PS-SUB)                        CL*28
00655          ELSE                                                        CL*28
00656              IF ASC-UPC-NBR (PS-SUB) = ZERO                          CL*28
W21732                 MOVE ASC-SKU-NBR (PS-SUB)    TO MR-SKU   (PS-SUB)   CL*28
00662                  MOVE SPACE                   TO MR-UPC-X (PS-SUB)   CL*28
00663              ELSE                                                    CL*28
W21732                 MOVE SPACE                   TO MR-SKU   (PS-SUB)   CL*28
00667                  MOVE ASC-UPC-NBR (PS-SUB)    TO MR-UPC   (PS-SUB)   CL*28
00668              END-IF                                                  CL*28
00669              MOVE ASC-INV-QTY (PS-SUB)        TO MR-QTY  (PS-SUB)    CL*28
00670              MOVE ASC-ITM-UNIT-PR-AMT (PS-SUB)                       CL*28
00671                                               TO MR-PRCE (PS-SUB)    CL*28
00672          END-IF                                                      CL*29
00673      END-PERFORM.                                                    CL*28
00674                                                                      CL*28
00675                                                                      CL*28
00676 *----------------------------------------------------------------*INKCS024
00677 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS024
00678 *----------------------------------------------------------------*INKCS024
00679                                                                   INKCS024
00680  9100-READ-SEL-QUEUE.                                             INKCS024
00681      EXEC CICS                                                    INKCS024
00682          READQ TS                                                 INKCS024
00683              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS024
00684              INTO  (IN022-SHEET-SEL-REC)                          INKCS024
00685              ITEM  (ASC-SEL-ITEM)                                 INKCS024
00686              RESP  (PV-CICS-RESP)                                 INKCS024
00687      END-EXEC.                                                    INKCS024
00688                                                                   INKCS024
00689      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS024
00690          CONTINUE                                                 INKCS024
00691      ELSE                                                         INKCS024
00692          SET DP013-LOGIC-ABEND                                    INKCS024
00693              DP013-NO-ROLLBACK     TO TRUE                        INKCS024
00694          MOVE '9100-READ-SEL-QUEUE'                               INKCS024
00695                                    TO DP013-PARAGRAPH             INKCS024
00696          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   INKCS024
00697                                    TO DP013-MESSAGE-TEXT (1)      INKCS024
00698          PERFORM DP013-0000-PROCESS-ABEND                         INKCS024
00699      END-IF.                                                      INKCS024
00700 *----------------------------------------------------------------*INKCS024
00701 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS024
00702 *----------------------------------------------------------------*INKCS024
00703  9200-REWRITE-SEL-QUEUE.                                          INKCS024
00704      EXEC CICS                                                    INKCS024
00705           WRITEQ TS                                               INKCS024
00706               QUEUE  (ASC-SEL-QUEUE-NAME)                         INKCS024
00707               FROM   (IN022-SHEET-SEL-REC)                        INKCS024
00708               ITEM   (ASC-SEL-ITEM)                               INKCS024
00709               RESP   (PV-CICS-RESP)                               INKCS024
00710               REWRITE                                             INKCS024
00711      END-EXEC.                                                    INKCS024
00712                                                                   INKCS024
00713      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                   INKCS024
00714          SET DP013-CICS-ABEND                                     INKCS024
00715              DP013-NO-ROLLBACK     TO TRUE                        INKCS024
00716          MOVE '9200-REWRITE-SEL-QUEUE'                            INKCS024
00717                                    TO DP013-PARAGRAPH             INKCS024
00718          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   INKCS024
00719                                    TO DP013-MESSAGE-TEXT (1)      INKCS024
00720          PERFORM DP013-0000-PROCESS-ABEND                         INKCS024
00721      END-IF.                                                      INKCS024
00722                                                                   INKCS024
00723  EJECT                                                            INKCS024
00724 *----------------------------------------------------------------*INKCS024
00725 *    ABEND PROCESSOR MODULE                                      *INKCS024
00726 *----------------------------------------------------------------*INKCS024
00727                                                                   INKCS024
00728      COPY DPPD013.                                                INKCS024
