       IDENTIFICATION DIVISION.                                         00010023
                                                                        00020024
       PROGRAM-ID.    IMKUT040                                          00030024
       AUTHOR.        SCOTT JACKSON.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050024
       DATE-WRITTEN.  10-10-2012.                                       00060024
       DATE-COMPILED.                                                   00070024
                                                                        00080024
      ******************************************************************00090024
      *     IMKUT040 -  UPT_PRD_MDM_BRG  TO UPT_PRD_MDM_BRG_NEW         00100024
      ******************************************************************00110024
      *     DATA IS EXTRACTED FOR INSERT IN SUBSEQUENT PROGRAM                  
      *     UPT_PRD_MDM_BRG     - SHADOW 1 TABLE (EXTRACT SOURCE)               
      *     UPT_PRD_MDM_BRG_NEW - SHADOW 2 TABLE (INSERT  TARGET)               
      ******************************************************************00270024
                                                                        00280024
                                                                        00290024
       ENVIRONMENT DIVISION.                                            00300024
                                                                        00310024
       CONFIGURATION SECTION.                                           00320024
       SOURCE-COMPUTER.        IBM-3090.                                00330024
       OBJECT-COMPUTER.        IBM-3090.                                00340024
       INPUT-OUTPUT SECTION.                                            00350024
       FILE-CONTROL.                                                    00360024
           SELECT MDM-SEQUENCE-CONTROL                                  00370024
               ASSIGN TO OUT01.                                         00380024
           SELECT MDM-BRIDGE-FILE                                       00370024
               ASSIGN TO OUT02.                                         00380024
                                                                        00390024
       DATA DIVISION.                                                   00410024
       FILE SECTION.                                                    00420024
                                                                        00430024
       FD  MDM-SEQUENCE-CONTROL                                         00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  MDM-MAX-SEQUENCE-REC        PIC X(80).                       00480024
                                                                        00500024
       FD  MDM-BRIDGE-FILE                                              00440024
           LABEL RECORDS ARE STANDARD                                   00450024
           RECORDING MODE IS F                                          00460024
           BLOCK CONTAINS 0 RECORDS.                                    00470024
       01  MDM-BRIDGE-RECORD           PIC X(628).                      00480024
                                                                        00500024
                                                                        00510024
       WORKING-STORAGE SECTION.                                         00520024
                                                                        00530024
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00540024
                                                                        00550024
       01  PC-PROGRAM-CONSTANTS.                                        00560024
           05  PC-JOB-NAME             PIC  X(06)  VALUE 'IMK100'.      00570024
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUT040'.    00580024
I05948     05  PC-MAX-RETRIES          PIC S9(03)  VALUE +5 COMP-3.     00590024
I05948     05  PC-MORE-THAN-MAX-RETRY  PIC S9(03)  VALUE +6 COMP-3.     00590024
                                                                        00610024
       01  PC-PROGRAM-COUNTER.                                          00620024
           05  PC-DISPLAY-CNT          PIC  9(09)  VALUE ZERO.          00640028
                                                                        00650024
      *01  PV-KEY-TEXT-FIELD.                                           00620024
      *    05 PV-KY-TXT-LEN                  PIC S9(4) COMP.                    
      *    05 PV-KY-TXT-TEXT                 PIC X(40).                         
      *                                                                 00890024
       01  PV-KEY-TEXT-FIELD                 PIC X(40).                 00620024
      *                                                                 00620024
       01  PS-PROGRAM-SWITCHES.                                         00900024
           05  PS-EOF-SEQUENCE-SW           PIC  X        VALUE 'N'.    00910024
               88  PS-NO-RECORDS-FOUND                    VALUE 'Y'.    00930024
           05  PS-EOF-CSR-SW                PIC  X        VALUE 'N'.    00910024
               88  PS-MORE-RECORDS                        VALUE 'N'.    00920024
               88  PS-NO-MORE-RECORDS                     VALUE 'Y'.    00930024
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00960024
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00970024
      *                                                                 01030024
      *                                                                 01040024
       01  PROGRAM-VARIABLES.                                           01050024
           10  PV-MAX-TIMESTAMP                PIC  X(26).                      
           10  PV-MAX-TIMESTAMP-LIMIT          PIC  X(26).                      
           05  PV-ONE                          PIC X      VALUE SPACES.         
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01060024
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070024
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01080024
           05  PV-DB2-OPER-ATTEMPTED           PIC  X(50) VALUE SPACES. 01090024
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.   01120024
           05  PV-NULL-IND                 PIC S9(04) COMP VALUE ZERO.  02970000
           05  PV-MAX-SEQUENCE-NBR             PIC   9(9).              02970000
           05  PV-TOTAL-ROW-COUNT              PIC   9(9).              02970000
I05948     05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     01060024
      *                                                                 01130024
       01  SAVE-AREA.                                                   01260024
           05  SV-ADB-MAX-SEQUENCE              PIC  S9(9) COMP.                
           05  SV-TOTAL-ROW-COUNT               PIC  S9(9) COMP.                
      *                                                                 01140024
      *UPT00000 EXTRACT RECORD LAYOUT                                   01140024
       01  PV-EXTRACT-RECORD.                                                   
           10 PV-EXTR-ADB-SEQUENCE         PIC  S9(9) COMP.                     
           10  PV-EXTR-TBL-NM.                                                  
               49  PV-EXTR-TBL-NM-LEN                                           
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-TBL-NM-TEXT                                          
                                                 PIC X(40).                     
           10  PV-EXTR-KY-1-TXT.                                                
               49  PV-EXTR-KY-1-TXT-LEN                                         
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-KY-1-TXT-TEXT                                        
                                                 PIC X(40).                     
           10  PV-EXTR-KY-2-TXT.                                                
               49  PV-EXTR-KY-2-TXT-LEN                                         
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-KY-2-TXT-TEXT                                        
                                                 PIC X(40).                     
           10  PV-EXTR-KY-3-TXT.                                                
               49  PV-EXTR-KY-3-TXT-LEN                                         
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-KY-3-TXT-TEXT                                        
                                                 PIC X(40).                     
           10  PV-EXTR-KY-4-TXT.                                                
               49  PV-EXTR-KY-4-TXT-LEN                                         
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-KY-4-TXT-TEXT                                        
                                                 PIC X(40).                     
           10  PV-EXTR-ADB-SUBJECT.                                             
               49  PV-EXTR-ADB-SUBJECT-LEN                                      
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-ADB-SUBJECT-TEXT                                     
                                                 PIC X(255).                    
           10  PV-EXTR-ADB-SET-SEQUENCE     PIC  S9(9) COMP.                    
           10  PV-EXTR-ADB-TIMESTAMP        PIC   X(26).                        
           10  PV-EXTR-ADB-OPCODE           PIC  S9(9) COMP.                    
           10  PV-EXTR-ADB-UPDATE-ALL       PIC  S9(9) COMP.                    
           10  PV-EXTR-ADB-REF-OBJECT.                                          
               49  PV-EXTR-ADB-REF-OBJECT-LEN                                   
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-ADB-REF-OBJECT-TEXT                                  
                                                 PIC X(64).                     
           10  PV-EXTR-ADB-L-DELIVERY       PIC   X(1).                         
           10  PV-EXTR-ADB-L-CMSEQUENCE     PIC  S9(20)V COMP-3.                
           10  PV-EXTR-ADB-TRACKINGID.                                          
               49  PV-EXTR-ADB-TRACKINGID-LEN                                   
                                                 PIC S9(4) COMP.                
               49  PV-EXTR-ADB-TRACKINGID-TEXT                                  
                                                 PIC X(40).                     
      *                                                                 01150024
       01  PA-PROGRAM-ACCUMULATORS.                                     01150024
           05  PA-RECORD-COUNTS.                                        01160024
               10  PA-OUTPUT-COUNT             PIC  9(9)  VALUE ZEROES. 01170024
               10  PA-TABLE1-COUNT             PIC  9(9)  VALUE ZEROES. 01170024
      *                                                                 01220024
       01  PD-PROGRAM-DISPLAYS.                                         01230024
           05  PD-DEADLOCK-COUNT               PIC  9(04) VALUE 0.      01240024
      *                                                                 01040024
       01  OUTPUT-CONTROL-REC.                                          01050024
           05  OUTPUT-MAX-SEQUENCE             PIC  S9(9) COMP.         01060024
           05  FILLER                          PIC  X(70) VALUE SPACES. 01070024
      *****************************************************************         
      * DCLGEN FOR THE MDM TIBCO SHADOW(1) TABLE - UPT_PRD_MDM_BRG              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE UPT00000                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DCLGEN FOR THE MDM TIBCO SHADOW(2) TABLE - UPT_PRD_MDM_BRG_NEW          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE UPT00002                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************01510024
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01520024
      ******************************************************************01530024
           COPY DPWS004.                                                01540024
      *                                                                 01550024
      ******************************************************************01560024
      *  USED FOR DB2 ERRORS                                            01570024
      ******************************************************************01580024
           EXEC SQL                                                     01590024
               INCLUDE SQLCA                                            01600024
           END-EXEC.                                                    01610024
      *                                                                 01620024
      *                                                                 01800024
      ******************************************************************01810024
      * RECORDS TO BE EXTRACTED FROM SHADOW (1) TABLE                           
      *                                                                         
           EXEC SQL                                                             
               DECLARE MDM_BRIDGE_CSR CURSOR FOR                                
                                                                                
               SELECT MAX(ADB_SEQUENCE)                                         
                          ,TBL_NM                                               
                          ,KY_1_TXT                                             
                          ,KY_2_TXT                                             
                          ,KY_3_TXT                                             
                          ,KY_4_TXT                                             
                          ,''                                                   
                          ,0                                                    
                          ,MAX(ADB_TIMESTAMP)                                   
                          ,ADB_OPCODE                                           
                          ,0                                                    
                          ,''                                                   
                          ,''                                                   
                          ,0                                                    
                          ,''                                                   
                 FROM UPT_PRD_MDM_BRG                                           
                WHERE ADB_SEQUENCE <=  :SV-ADB-MAX-SEQUENCE                     
     **  PROCESS ALL RECORDS EXCEPT TUPC DELETE RECORDS  **                     
                  AND NOT (TBL_NM = 'TUPC' AND                                  
                           ADB_OPCODE = 3)                                      
                GROUP BY  TBL_NM                                                
                         ,KY_1_TXT                                              
                         ,KY_2_TXT                                              
                         ,KY_3_TXT                                              
                         ,KY_4_TXT                                              
                         ,ADB_OPCODE                                            
                 FOR FETCH ONLY                                                 
           END-EXEC.                                                            
      *                                                                 01250024
      *                                                                 01440024
      *                                                                 01450024
      **************************************************************            
      *                                                                 02110024
      *-------------------*                                             02120024
       PROCEDURE DIVISION.                                              02130024
      *-------------------*                                             02140024
                                                                        02150024
       0000-MAIN-MODULE.                                                02160024
                                                                        02170024
           PERFORM 1000-INITIALIZATION.                                 02180024
      *                                                                 02190024
           PERFORM 2000-PROCESS-RECORDS                                 02200024
                UNTIL PS-NO-MORE-RECORDS.                               02210024
      *                                                                 02220024
           PERFORM 8000-EOJ-ROUTINE.                                    02230024
                                                                        02240024
           STOP RUN.                                                    02250024
      *                                                                 02260024
      *                                                                 02270024
      *                                                                 02280024
      ******************************************************************02290024
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02300024
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02310024
      ******************************************************************02320024
      *                                                                 02330024
       1000-INITIALIZATION.                                             02340024
      *                                                                 02350024
           OPEN OUTPUT MDM-SEQUENCE-CONTROL.                            00440024
           OPEN OUTPUT MDM-BRIDGE-FILE.                                 02360024
                                                                        02370024
           DISPLAY ' '                                                  04791037
           DISPLAY '*** PROGRAM IMKUT040 BEGINS ***'                    04791037
           DISPLAY ' '                                                  04791037
                                                                        02370024
           MOVE 'N' TO  PS-EOF-CSR-SW                                           
                        PS-EOF-SEQUENCE-SW.                                     
      *                                                                         
I05948     PERFORM 1500-DETERMINE-LIMIT.                                        
      *                                                                 03960024
I05948     DISPLAY ' CURRENT TIMESTAMP (SYSTEM) : ',                            
I05948                                      PV-MAX-TIMESTAMP.                   
I05948     DISPLAY ' *** MAX TIMESTAMP PROCESSED: ',                            
I05948                                      PV-MAX-TIMESTAMP-LIMIT.             
      *                                                                 03960024
           PERFORM 1100-CHECK-FOR-UPDATES.                              03950037
                                                                                
           IF (PS-NO-RECORDS-FOUND                                              
               OR                                                               
               PV-MAX-SEQUENCE-NBR = 0)                                         
      *                                                                         
                MOVE ZEROES TO PV-TOTAL-ROW-COUNT                               
                MOVE 'Y' TO PS-EOF-CSR-SW                                       
           ELSE                                                                 
                PERFORM 1200-SELECT-MAX-SEQUENCE                                
                PERFORM 1300-WRITE-CONTROL-FILE                         03900024
                PERFORM 1400-PROCESS-CONTROL-COUNTS                     03900024
           END-IF.                                                              
      *                                                                 03900024
      *                                                                 02810024
      ******************************************************************03910024
      *                                                                 03920024
      ******************************************************************03940024
       1100-CHECK-FOR-UPDATES.                                          03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
              SELECT '1'                                                        
                INTO :PV-ONE                                                    
                FROM UPT_PRD_MDM_BRG                                            
I05948          WHERE  ADB_TIMESTAMP  <= :PV-MAX-TIMESTAMP-LIMIT                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
               WHEN -811                                                04790024
                      DISPLAY '*** UPDATES EXIST. PROCESSING CONTINUES' 04791037
               WHEN +100                                                04790024
                      DISPLAY '******* NO UPDATES EXIST ********* '     04791037
                      DISPLAY '******* NO UPDATES EXIST ********* '     04791037
                      DISPLAY '******* NO UPDATES EXIST ********* '     04791037
                      MOVE 'Y' TO PS-EOF-SEQUENCE-SW                            
               WHEN OTHER                                               04910024
                   MOVE '1100-CHECK-FOR-UPDATES'                        04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED          04930037
                   PERFORM 9998-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      *                                                                 03900024
      *                                                                 02810024
      ******************************************************************03910024
      * SELECT THE MAX SEQUENCE NUMBER TO BE PROCESSED                  03920024
      ******************************************************************03940024
       1200-SELECT-MAX-SEQUENCE.                                        03950037
      *                                                                 03960024
I05948     PERFORM VARYING PV-RETRY-COUNT FROM 1 BY 1                   03960024
I05948               UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES              03960024
      *                                                                 03960024
             EXEC SQL                                                   05110024
                SELECT  MAX(ADB_SEQUENCE)                                       
                  INTO  :SV-ADB-MAX-SEQUENCE                                    
                  FROM  UPT_PRD_MDM_BRG                                         
                 WHERE  ADB_TIMESTAMP  <= :PV-MAX-TIMESTAMP-LIMIT               
             END-EXEC                                                           
                                                                                
             EVALUATE SQLCODE                                           04780024
               WHEN 0                                                   04790024
I05948             MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT        04791037
               WHEN +100                                                04790024
                   MOVE 'Y' TO PS-EOF-SEQUENCE-SW                               
I05948             MOVE PC-MORE-THAN-MAX-RETRY TO PV-RETRY-COUNT        04791037
I05948         WHEN -911                                                04791037
I05948         WHEN -913                                                04791037
I05948             CONTINUE                                             04791037
               WHEN OTHER                                               04910024
                   MOVE '1200-SELECT-MAX-SEQUENCE'                      04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED          04930037
                   PERFORM 9998-SQL-ABEND                               04940024
             END-EVALUATE                                               04950024
I05948     END-PERFORM.                                                 03960024
I05948                                                                  03960024
I05948     IF (SQLWARN0 NOT = SPACES  OR                                03960024
I05948        (SQLCODE > 0 AND SQLCODE NOT = +100) OR                   03960024
I05948         SQLCODE < 0)                                             03960024
I05948         DISPLAY 'MAX RETIES EXCEEDED'                            04920037
I05948         DISPLAY 'ABENDING THE PROGRAM'                           04920037
I05948         MOVE '1200-SELECT-MAX-SEQUENCE'                          04920037
I05948           TO PV-PARAGRAPH-NAME                                   04921037
I05948         MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED              04930037
I05948         PERFORM 9998-SQL-ABEND                                   04940024
I05948     END-IF.                                                      03900024
      *                                                                 03900024
                                                                                
      ******************************************************************02820024
      * PROCESS THE CONTROL FILE.                                               
      * THIS CONTAINS THE MAX-SEQUENCE-NBR THAT WILL BE EXTRACTED               
      *                                                                 02830024
      ******************************************************************02840024
       1300-WRITE-CONTROL-FILE.                                         02850024
      *                                                                 02860024
           MOVE SV-ADB-MAX-SEQUENCE TO  PV-MAX-SEQUENCE-NBR             03900024
                                        OUTPUT-MAX-SEQUENCE.            02860024
      *                                                                 02860024
           DISPLAY  '********************************************** '.          
           DISPLAY  'MAX SEQUENCE NUMBER SELECTED : ',                          
                                        PV-MAX-SEQUENCE-NBR                     
           DISPLAY  '********************************************** '.          
      *                                                                 02860024
           WRITE MDM-MAX-SEQUENCE-REC FROM PV-MAX-SEQUENCE-NBR.                 
      *                                                                 02860024
      *                                                                 03900024
      ******************************************************************03910024
      * SELECT THE TOTAL NUMBER OF RECORDS THAT EXIST ON SHADOW 1       03920024
      * ONLY THE 'LAST' UPDATE FOR A SPECIFIC RECORD IN SENT FOR INSERT 03920024
      * TO THE SHADOW 2 TABLE. HOWEVER, ALL RECORDS WILL BE EVENTUALLY  03920024
      * DELETED FOLLOWING THE UPDATES                                   03920024
      ******************************************************************03940024
       1400-PROCESS-CONTROL-COUNTS.                                     03950037
      *                                                                 03960024
           EXEC SQL                                                     05110024
              SELECT COUNT(*)                                                   
                INTO :SV-TOTAL-ROW-COUNT                                        
                 FROM UPT_PRD_MDM_BRG                                           
               WHERE ADB_SEQUENCE <= :SV-ADB-MAX-SEQUENCE                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   MOVE SV-TOTAL-ROW-COUNT TO PV-TOTAL-ROW-COUNT                
               WHEN OTHER                                               04910024
                   MOVE '1400-PROCESS-CONTROL-COUNTS'                   04920037
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED          04930037
                   PERFORM 9998-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                 03900024
      ******************************************************************02820024
      *1500-DETERMINE-LIMIT.                                                    
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *02830024
      ******************************************************************02840024
       1500-DETERMINE-LIMIT.                                                    
      *                                                                         
      *                                                                         
           EXEC SQL                                                     05110024
                SET :PV-MAX-TIMESTAMP        = CURRENT TIMESTAMP                
                   ,:PV-MAX-TIMESTAMP-LIMIT  =                                  
                                       (CURRENT TIMESTAMP - 5 MINUTES)          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                             04780024
               WHEN 0                                                   04790024
                   CONTINUE                                             04790024
               WHEN OTHER                                               04910024
                   MOVE '1500-DETERMINE-LIMIT'                                  
                                      TO PV-PARAGRAPH-NAME              04921037
                   MOVE 'SELECT'      TO PV-DB2-OPER-ATTEMPTED          04930037
                   PERFORM 9998-SQL-ABEND                               04940024
           END-EVALUATE.                                                04950024
      *                                                                         
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************02820024
      * PROCESS THE EXTRACT CURSOR                                      02830024
      *                                                                 02830024
      ******************************************************************02840024
       2000-PROCESS-RECORDS.                                            02850024
      *                                                                 02860024
           PERFORM  5000-OPEN-MDM-BRIDGE-CSR.                           03900024
      *                                                                 03901037
           PERFORM  2100-EXTRACT-PROCESS                                        
               UNTIL  PS-NO-MORE-RECORDS.                                       
                                                                                
           PERFORM 5200-CLOSE-MDM-BRIDGE-CSR.                                   
                                                                                
                                                                                
      ******************************************************************02820024
      * PROCESS THE EXTRACT CURSOR                                      02830024
      *                                                                 02830024
      ******************************************************************02840024
       2100-EXTRACT-PROCESS.                                            02850024
      *                                                                 02860024
           PERFORM 2200-INITIALIZE.                                     02860024
      *                                                                         
           PERFORM 5100-FETCH-MDM-BRIDGE-CSR.                                   
      *                                                                         
           IF PS-MORE-RECORDS                                                   
               PERFORM 2300-FORMAT-EXTRACT                                  0286
               PERFORM 2400-PREPARE-OUTPUT                                  0285
               PERFORM 6000-WRITE-OUTPUT-FILE                               0829
           ELSE                                                                 
               DISPLAY ' **** END OF EXTRACT PROCESS ****'                      
           END-IF.                                                              
      *                                                                 02860024
      ************************************************************      02860024
      *                                                                 02860024
      ************************************************************      02860024
       2200-INITIALIZE.                                                 02860024
      *                                                                 02860024
      *                                                                         
      * INITIALIZE WORKING FIELDS                                               
           MOVE ZEROES TO PV-EXTR-TBL-NM-LEN                            02860024
                          UPT00000-TBL-NM-LEN.                                  
           MOVE SPACES TO PV-EXTR-TBL-NM-TEXT                           02860024
                          UPT00000-TBL-NM-TEXT.                                 
      ** KY-1                                                                   
           MOVE ZEROES TO PV-EXTR-KY-1-TXT-LEN                          02860024
                          UPT00000-KY-1-TXT-LEN.                                
           MOVE SPACES TO PV-EXTR-KY-1-TXT-TEXT                         02860024
                          UPT00000-KY-1-TXT-TEXT.                               
      ** KY-2                                                                   
           MOVE ZEROES TO PV-EXTR-KY-2-TXT-LEN                          02860024
                          UPT00000-KY-2-TXT-LEN.                                
           MOVE SPACES TO PV-EXTR-KY-2-TXT-TEXT                         02860024
                          UPT00000-KY-2-TXT-TEXT.                               
      ** KY-3                                                                   
           MOVE ZEROES TO PV-EXTR-KY-3-TXT-LEN                          02860024
                          UPT00000-KY-3-TXT-LEN.                                
           MOVE SPACES TO PV-EXTR-KY-3-TXT-TEXT                         02860024
                          UPT00000-KY-3-TXT-TEXT.                               
      ** KY-4                                                                   
           MOVE ZEROES TO PV-EXTR-KY-4-TXT-LEN                          02860024
                          UPT00000-KY-4-TXT-LEN.                                
           MOVE SPACES TO PV-EXTR-KY-4-TXT-TEXT                         02860024
                          UPT00000-KY-4-TXT-TEXT.                               
      ** KY-4                                                                   
           MOVE ZEROES TO PV-EXTR-KY-4-TXT-LEN                          02860024
                          UPT00000-KY-4-TXT-LEN.                                
           MOVE SPACES TO PV-EXTR-KY-4-TXT-TEXT                         02860024
                          UPT00000-KY-4-TXT-TEXT.                               
      *                                                                         
      * SUBJECT                                                                 
           MOVE ZEROES TO PV-EXTR-ADB-SUBJECT-LEN                       02860024
                          UPT00000-ADB-SUBJECT-LEN.                     02860024
           MOVE SPACES TO PV-EXTR-ADB-SUBJECT-TEXT                      02860024
                          UPT00000-ADB-SUBJECT-TEXT.                    02860024
      *                                                                         
      * OBJECT                                                                  
           MOVE ZEROES TO PV-EXTR-ADB-REF-OBJECT-LEN                    02860024
                          UPT00000-ADB-REF-OBJECT-LEN.                  02860024
           MOVE SPACES TO PV-EXTR-ADB-REF-OBJECT-TEXT                   02860024
                          UPT00000-ADB-REF-OBJECT-TEXT.                 02860024
      *                                                                         
      * TRACKING ID                                                             
           MOVE ZEROES TO PV-EXTR-ADB-TRACKINGID-LEN                    02860024
                          UPT00000-ADB-TRACKINGID-LEN.                  02860024
           MOVE SPACES TO PV-EXTR-ADB-TRACKINGID-TEXT                   02860024
                          UPT00000-ADB-TRACKINGID-TEXT.                 02860024
      *                                                                 02860024
      ************************************************************      02860024
      *                                                                 02860024
      ************************************************************      02860024
       2300-FORMAT-EXTRACT.                                             02860024
      *                                                                 02860024
      *                                                                         
           MOVE UPT00000-ADB-SET-SEQUENCE TO PV-EXTR-ADB-SET-SEQUENCE.          
           MOVE UPT00000-ADB-TIMESTAMP    TO PV-EXTR-ADB-TIMESTAMP.             
           MOVE UPT00000-ADB-OPCODE       TO PV-EXTR-ADB-OPCODE.                
           MOVE UPT00000-ADB-UPDATE-ALL   TO PV-EXTR-ADB-UPDATE-ALL.            
           MOVE UPT00000-ADB-L-DELIVERY   TO PV-EXTR-ADB-L-DELIVERY.            
           MOVE UPT00000-ADB-L-CMSEQUENCE TO PV-EXTR-ADB-L-CMSEQUENCE.          
           MOVE UPT00000-ADB-SEQUENCE     TO PV-EXTR-ADB-SEQUENCE.              
      *                                                                         
           MOVE UPT00000-TBL-NM-LEN       TO PV-EXTR-TBL-NM-LEN.                
           MOVE UPT00000-TBL-NM-TEXT      TO PV-EXTR-TBL-NM-TEXT.               
      *                                                                         
           MOVE UPT00000-KY-1-TXT-LEN     TO PV-EXTR-KY-1-TXT-LEN.              
           MOVE UPT00000-KY-1-TXT-TEXT    TO PV-EXTR-KY-1-TXT-TEXT.             
      *                                                                         
           MOVE UPT00000-KY-2-TXT-LEN     TO PV-EXTR-KY-2-TXT-LEN.              
           MOVE UPT00000-KY-2-TXT-TEXT    TO PV-EXTR-KY-2-TXT-TEXT.             
      *                                                                         
           MOVE UPT00000-KY-3-TXT-LEN     TO PV-EXTR-KY-3-TXT-LEN.              
           MOVE UPT00000-KY-3-TXT-TEXT    TO PV-EXTR-KY-3-TXT-TEXT.             
      *                                                                         
           MOVE UPT00000-KY-4-TXT-LEN     TO PV-EXTR-KY-4-TXT-LEN.              
           MOVE UPT00000-KY-4-TXT-TEXT    TO PV-EXTR-KY-4-TXT-TEXT.             
      *                                                                         
           MOVE UPT00000-ADB-SUBJECT-LEN  TO PV-EXTR-ADB-SUBJECT-LEN.           
           MOVE UPT00000-ADB-SUBJECT-TEXT TO PV-EXTR-ADB-SUBJECT-TEXT.          
      *                                                                         
           MOVE UPT00000-ADB-REF-OBJECT-LEN                                     
                                       TO PV-EXTR-ADB-REF-OBJECT-LEN.           
           MOVE UPT00000-ADB-REF-OBJECT-TEXT                                    
                                       TO PV-EXTR-ADB-REF-OBJECT-TEXT.          
      *                                                                         
           MOVE UPT00000-ADB-TRACKINGID-LEN                                     
                                       TO PV-EXTR-ADB-TRACKINGID-LEN.           
           MOVE UPT00000-ADB-TRACKINGID-TEXT                                    
                                       TO PV-EXTR-ADB-TRACKINGID-TEXT.          
      *                                                                         
      ******************************************************************02820024
      * PROCESS THE EXTRACT CURSOR                                      02830024
      *                                                                 02830024
      ******************************************************************02840024
       2400-PREPARE-OUTPUT.                                             02850024
      *                                                                 02820024
      *    MOVE PV-EXTRACT-RECORD TO  DCLUPT00002.                              
           MOVE PV-EXTR-ADB-SET-SEQUENCE TO UPT00002-ADB-SET-SEQUENCE.          
           MOVE PV-EXTR-ADB-TIMESTAMP    TO UPT00002-ADB-TIMESTAMP.             
           MOVE PV-EXTR-ADB-OPCODE       TO UPT00002-ADB-OPCODE.                
           MOVE PV-EXTR-ADB-UPDATE-ALL   TO UPT00002-ADB-UPDATE-ALL.            
           MOVE PV-EXTR-ADB-L-DELIVERY   TO UPT00002-ADB-L-DELIVERY.            
           MOVE PV-EXTR-ADB-L-CMSEQUENCE TO UPT00002-ADB-L-CMSEQUENCE.          
           MOVE PV-EXTR-ADB-SEQUENCE     TO UPT00002-ADB-SEQUENCE.              
      *                                                                         
           MOVE PV-EXTR-TBL-NM-LEN      TO UPT00002-TBL-NM-LEN.                 
           MOVE PV-EXTR-TBL-NM-TEXT     TO UPT00002-TBL-NM-TEXT.                
      *                                                                         
           MOVE PV-EXTR-KY-1-TXT-LEN    TO UPT00002-KY-1-TXT-LEN.               
           MOVE PV-EXTR-KY-1-TXT-TEXT   TO UPT00002-KY-1-TXT-TEXT.              
      *                                                                         
           MOVE PV-EXTR-KY-2-TXT-LEN    TO UPT00002-KY-2-TXT-LEN.               
           MOVE PV-EXTR-KY-2-TXT-TEXT   TO UPT00002-KY-2-TXT-TEXT.              
      *                                                                         
           MOVE PV-EXTR-KY-3-TXT-LEN    TO UPT00002-KY-3-TXT-LEN.               
           MOVE PV-EXTR-KY-3-TXT-TEXT   TO UPT00002-KY-3-TXT-TEXT.              
      *                                                                         
           MOVE PV-EXTR-KY-4-TXT-LEN    TO UPT00002-KY-4-TXT-LEN.               
           MOVE PV-EXTR-KY-4-TXT-TEXT   TO UPT00002-KY-4-TXT-TEXT.              
      *                                                                         
           MOVE PV-EXTR-ADB-SUBJECT-LEN  TO UPT00002-ADB-SUBJECT-LEN.           
           MOVE PV-EXTR-ADB-SUBJECT-TEXT TO UPT00002-ADB-SUBJECT-TEXT.          
      *                                                                         
           MOVE PV-EXTR-ADB-REF-OBJECT-LEN                                      
                                       TO UPT00002-ADB-REF-OBJECT-LEN.          
           MOVE PV-EXTR-ADB-REF-OBJECT-TEXT                                     
                                       TO UPT00002-ADB-REF-OBJECT-TEXT.         
      *                                                                         
           MOVE PV-EXTR-ADB-TRACKINGID-LEN                                      
                                       TO UPT00002-ADB-TRACKINGID-LEN.          
           MOVE PV-EXTR-ADB-TRACKINGID-TEXT                                     
                                       TO UPT00002-ADB-TRACKINGID-TEXT.         
      *                                                                 02860024
      *    PERFORM  5100-FETCH-MDM-BRIDGE-CSR.                                  
      ******************************************************************02820024
      * PROCESS THE EXTRACT CURSOR                                      02830024
      *                                                                 02830024
      ******************************************************************02840024
      *2100-EXTRACT-PROCESS.                                            02850024
      *                                                                 02860024
      *                                                                 02860024
      *    MOVE UPT00000-ADB-SEQUENCE     TO UPT00002-ADB-SEQUENCE              
      *    MOVE UPT00000-TBL-NM           TO UPT00002-TBL-NM                    
      *                                                                         
      *    MOVE UPT00000-KY-1-TXT                                               
      *                                                                         
      *    MOVE UPT00000-KY-1-TXT         TO PV-KEY-TEXT-FIELD                  
      *    MOVE PV-KEY-TEXT-FIELD         TO UPT00002-KY-1-TXT                  
      *                                                                         
      *    MOVE UPT00000-KY-1-TXT(1:UPT00000-KY-1-TXT-LEN)                      
      *                                   TO RTRIM(UPT00002-KY-1-TXT)           
      *                                                                         
      *    MOVE UPT00000-KY-2-TXT                                               
      *                                   TO UPT00002-KY-2-TXT                  
      *                                                                         
      *    MOVE UPT00000-KY-3-TXT                                               
      *                                   TO UPT00002-KY-3-TXT                  
      *                                                                         
      *    MOVE UPT00000-KY-4-TXT                                               
      *                                   TO UPT00002-KY-4-TXT                  
      *                                                                         
      *    MOVE UPT00000-ADB-SUBJECT      TO UPT00002-ADB-SUBJECT               
      *    MOVE UPT00000-ADB-SET-SEQUENCE TO UPT00002-ADB-SET-SEQUENCE          
      *    MOVE UPT00000-ADB-TIMESTAMP    TO UPT00002-ADB-TIMESTAMP             
      *    MOVE UPT00000-ADB-OPCODE       TO UPT00002-ADB-OPCODE                
      *    MOVE UPT00000-ADB-UPDATE-ALL   TO UPT00002-ADB-UPDATE-ALL            
      *    MOVE UPT00000-ADB-REF-OBJECT   TO UPT00002-ADB-REF-OBJECT            
      *    MOVE UPT00000-ADB-L-DELIVERY   TO UPT00002-ADB-L-DELIVERY            
      *    MOVE UPT00000-ADB-L-CMSEQUENCE TO UPT00002-ADB-L-CMSEQUENCE          
      *    MOVE UPT00000-ADB-TRACKINGID   TO UPT00002-ADB-TRACKINGID            
      *                                                                         
      *    WRITE MDM-BRIDGE-RECORD FROM  DCLUPT00002.                           
      *                                                                 02860024
      *    PERFORM  5100-FETCH-MDM-BRIDGE-CSR.                                  
      *                                                                 02860024
      ******************************************************************        
      *                                                                 02860024
       5000-OPEN-MDM-BRIDGE-CSR.                                                
                                                                                
           EXEC SQL                                                             
                OPEN MDM_BRIDGE_CSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY ' **   BEGIN CURSOR PROCESSING  **'                  
               WHEN OTHER                                                       
                   MOVE '5000-OPEN-MDM-BRIDGE-CSR'                              
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'OPEN MDM_BRIDGE_CSR' TO PV-DB2-OPER-ATTEMPTED          
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
       5100-FETCH-MDM-BRIDGE-CSR.                                               
                                                                                
           EXEC SQL                                                             
               FETCH MDM_BRIDGE_CSR                                             
                 INTO                                                           
                   :UPT00000-ADB-SEQUENCE                                       
                  ,:UPT00000-TBL-NM                                             
                  ,:UPT00000-KY-1-TXT             :PV-NULL-IND                  
                  ,:UPT00000-KY-2-TXT             :PV-NULL-IND                  
                  ,:UPT00000-KY-3-TXT             :PV-NULL-IND                  
                  ,:UPT00000-KY-4-TXT             :PV-NULL-IND                  
                  ,:UPT00000-ADB-SUBJECT          :PV-NULL-IND                  
                  ,:UPT00000-ADB-SET-SEQUENCE                                   
                  ,:UPT00000-ADB-TIMESTAMP                                      
                  ,:UPT00000-ADB-OPCODE                                         
                  ,:UPT00000-ADB-UPDATE-ALL                                     
                  ,:UPT00000-ADB-REF-OBJECT       :PV-NULL-IND                  
                  ,:UPT00000-ADB-L-DELIVERY                                     
                  ,:UPT00000-ADB-L-CMSEQUENCE                                   
                  ,:UPT00000-ADB-TRACKINGID       :PV-NULL-IND                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO PA-OUTPUT-COUNT                                     
               WHEN SQLCODE = +100                                              
                   SET PS-NO-MORE-RECORDS TO TRUE                               
               WHEN OTHER                                                       
                   MOVE '5100-FETCH-MDM-BRIDGE-CSR' TO PV-PARAGRAPH-NAME        
                   MOVE 'FETCH BRIDGE-CSR '     TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       5200-CLOSE-MDM-BRIDGE-CSR.                                               
                                                                                
           EXEC SQL                                                             
               CLOSE MDM_BRIDGE_CSR                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '5200-CLOSE-MDM-BRIDGE-CSR' TO PV-PARAGRAPH-NAME        
                   MOVE 'CLOSE BRIDGE-CSR '     TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                 03901037
      ******************************************************************08260024
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08270024
      ******************************************************************08280024
       6000-WRITE-OUTPUT-FILE.                                          08290024
      *                                                                         
           WRITE MDM-BRIDGE-RECORD FROM  DCLUPT00002.                           
      *                                                                 02860024
      *                                                                 02860024
      *                                                                 08240024
      *                                                                 08250024
      ******************************************************************08260024
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08270024
      ******************************************************************08280024
       8000-EOJ-ROUTINE.                                                08290024
      *                                                                 08300024
      *                                                                 08300024
           DISPLAY ' ************************************************'. 08300024
           DISPLAY ' SOURCE: SHADOW 1 - TOTAL RECORD COUNT : ',                 
                                                   PV-TOTAL-ROW-COUNT.          
           DISPLAY ' TARGET: SHADOW 2 - EXTRACT FOR UPDATE : ',                 
                                                   PA-OUTPUT-COUNT.             
           DISPLAY ' ************************************************'. 08300024
      *                                                                 08530024
      ******************************************************************08540024
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08550024
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08560024
      ******************************************************************08570024
       9998-SQL-ABEND.                                                  08580024
                                                                        08590024
           MOVE +4013                 TO ABEND-CODE.                    08600024
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            08610024
           DISPLAY '**  ABEND     **'.                                  08620024
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08630024
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08640024
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED .       08650024
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           08660024
                                                                        08670024
           EXEC SQL                                                     08680024
                ROLLBACK                                                08690024
           END-EXEC.                                                    08700024
                                                                        08710024
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08720024
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08730024
                                                                        08740024
           COPY DPPD004.                                                08750024
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08760024
