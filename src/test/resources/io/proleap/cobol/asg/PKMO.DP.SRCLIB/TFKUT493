      ******************************************************************00010017
       IDENTIFICATION DIVISION.                                         00020017
      ******************************************************************00030017
                                                                        00040017
       PROGRAM-ID.             TFKUT493.                                00050017
       AUTHOR.                 JACK KRAMER.                             00060017
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070017
       DATE-WRITTEN            JUNE 97.                                 00080017
       DATE-COMPILED.                                                   00090017
                                                                        00100017
      ******************************************************************00110017
      *  THIS PROGRAM WILL EXTRACT TRANSFER DATA FOR OFFSITE STORAGE    00120017
      * TRANSFERS AND WRITE IT TO AN OUTPUT SEQUENTIAL FILE TO BE PASSED00130017
      * TO THE OFFSITE STORAGE REPORTS.                                 00140017
      ******************************************************************00150017
      *  2/28/2003 - CHRIS ALBRIGHT                                     00151017
      *   PROCESS NONINTELLIGENT SKU.                                   00152017
      ******************************************************************00153017
100404** 10/04/2004 CHANGED BY: RAY GRAF                                00154019
100404**     RECOMPILE DUE TO TFRD040 COPYBOOK CHANGES.                 00155020
      ***************************************************************** 00156019
022812** 02/28/2012 CHANGED BY: CHERI PARMLEY             PRJ#3403      00158021
022812**     RECOMPILE DUE TO TTFITEM TABLE CHANGES.                    00159021
      ***************************************************************** 00159121
                                                                        00160017
      ******************************************************************00170017
       ENVIRONMENT DIVISION.                                            00180017
      ******************************************************************00190017
                                                                        00200017
       CONFIGURATION SECTION.                                           00210017
                                                                        00220017
       SOURCE-COMPUTER.        IBM-3090.                                00230017
       OBJECT-COMPUTER.        IBM-3090.                                00240017
                                                                        00250017
       INPUT-OUTPUT SECTION.                                            00260017
                                                                        00270017
       FILE-CONTROL.                                                    00280017
                                                                        00290017
           SELECT OFFSITE-STORAGE-FILE                                  00291017
               ASSIGN TO OUT01                                          00292017
               FILE STATUS IS PV-FILE-STATUS.                           00293017
      *                                                                 00294017
      ******************************************************************00295017
       DATA DIVISION.                                                   00296017
      ******************************************************************00297017
                                                                        00298017
       FILE SECTION.                                                    00299017
                                                                        00300017
       FD  OFFSITE-STORAGE-FILE                                         00310017
           LABEL RECORDS ARE STANDARD                                   00320017
           RECORDING MODE IS F                                          00330017
           BLOCK CONTAINS 0 RECORDS                                     00340017
           RECORD CONTAINS 60 CHARACTERS                                00350017
           DATA RECORD IS OFFSITE-STORAGE-FILE-RECORD.                  00360017
                                                                        00370017
       01  OFFSITE-STORAGE-FILE-RECORD         PIC  X(60).              00380017
                                                                        00390017
       WORKING-STORAGE SECTION.                                         00400017
                                                                        00410017
      *---------------------------------------------------------------* 00420017
      * OFFSITE STORAGE FILE RECORD LAYOUT                              00430017
      *---------------------------------------------------------------* 00440017
           COPY TFRD040.                                                00450017
                                                                        00460017
       01  PS-PROGRAM-SWITCHES.                                         00470017
           05  PS-MORE-TRANSFER-SW     PIC X(01)    VALUE 'Y'.          00480017
               88  PS-MORE-TRANSFERS                VALUE 'Y'.          00490017
               88  PS-NO-MORE-TRANSFERS             VALUE 'N'.          00500017
                                                                        00510017
       01  PC-PROGRAM-CONSTANTS.                                        00520017
           05  PC-MAX-RETRIES          PIC S9(4)    VALUE +3            00530017
                                       COMP SYNC.                       00540017
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'TFKUT493'.   00550017
           05  PC-OFFSITE-STORAGE      PIC X(02)    VALUE '07'.         00560017
                                                                        00570017
       01  PV-PROGRAM-VARIABLES.                                        00580017
           05  PV-FILE-STATUS          PIC X(02)  VALUE '00'.           00590017
           05  PV-RETRY-COUNT          PIC S9(4)    VALUE ZERO          00600017
                                       COMP SYNC.                       00610017
           05  PV-PARAGRAPH-NAME       PIC X(30)   VALUE SPACE.         00620017
           05  PV-DB2-OPERATION        PIC X(30)   VALUE SPACE.         00630017
      *---------------------------------------------------------------* 00640017
      *    DB2 AREA FOR TTFMSTR (TRANSFER TABLE)                        00650017
      *---------------------------------------------------------------* 00660017
           EXEC SQL                                                     00670017
                INCLUDE TTFMSTR                                         00680017
           END-EXEC.                                                    00690017
      *---------------------------------------------------------------* 00700017
      *    DB2 AREA FOR TTFSTAT (TRANSFER STATUS TABLE)                 00710017
      *---------------------------------------------------------------* 00720017
           EXEC SQL                                                     00730017
                INCLUDE TTFSTAT                                         00740017
           END-EXEC.                                                    00750017
      *---------------------------------------------------------------* 00760017
      *    DB2 AREA FOR TTFITEM (TRANSFER ITEM TABLE)                   00770017
      *---------------------------------------------------------------* 00780017
           EXEC SQL                                                     00790017
                INCLUDE TTFITEM                                         00800017
           END-EXEC.                                                    00810017
      *---------------------------------------------------------------* 00811017
      *    DB2 AREA FOR TSKXREF (NIS CROSS-REFERENCE TABLE)             00812017
      *---------------------------------------------------------------* 00813017
           EXEC SQL                                                     00814017
                INCLUDE TSKXREF                                         00815017
           END-EXEC.                                                    00816017
      *---------------------------------------------------------------* 00820017
      *     CURSOR TO RETRIEVE OFFSITE STORAGE TRANSFERS              * 00830017
      *---------------------------------------------------------------* 00840017
           EXEC SQL                                                     00850017
                DECLARE TRANSFER-CSR CURSOR FOR                         00860017
                SELECT                                                  00870017
                      M.FR_STR_NBR                                      00880017
                     ,M.XFER_SEQ_NBR                                    00890017
022803               ,X.DEPT                                            00921018
022803               ,X.CLASS                                           00922018
022803               ,X.SUBCLASS                                        00923018
                     ,I.SCN_UPC_NBR                                     00930017
                     ,I.XFER_QTY                                        00940017
                     ,I.XFER_PRIC_AMT                                   00950017
                     ,S.XFER_STAT_CDE                                   00960017
022803               ,X.ITM_NBR                                         00961018
                FROM                                                    00970017
                      TTFMSTR M                                         00980017
                     ,TTFSTAT S                                         00990017
                     ,TTFITEM I                                         01000017
022803               ,TSKXREF X                                         01001018
                WHERE M.XFER_DKEY_ID       = I.XFER_DKEY_ID        AND  01010017
                      M.XFER_DKEY_ID       = S.XFER_DKEY_ID        AND  01020017
                      M.XFER_TYP_CDE       = :PC-OFFSITE-STORAGE   AND  01030017
022803                I.ITM_NBR            = X.ITM_NBR             AND  01040018
                      S.XFER_STAT_TMST     =                            01041017
                      (SELECT MAX(XFER_STAT_TMST)                       01050017
                       FROM TTFSTAT                                     01060017
                       WHERE XFER_DKEY_ID  = M.XFER_DKEY_ID)            01070017
           END-EXEC.                                                    01080017
      *---------------------------------------------------------------* 01090017
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                01100017
      *---------------------------------------------------------------* 01110017
           EXEC SQL                                                     01120017
               INCLUDE SQLCA                                            01130017
           END-EXEC.                                                    01140017
      *---------------------------------------------------------------* 01150017
      *  EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING           01160017
      *  SQL CODES FOR DISPLAY                                          01170017
      *---------------------------------------------------------------* 01180017
           COPY SQLCA2.                                                 01190017
                                                                        01200017
       01  ABEND-CODE                  PIC S9(4)    VALUE ZERO          01210017
                                       COMP SYNC.                       01220017
      *                                                                 01230017
      ******************************************************************01240017
       PROCEDURE DIVISION.                                              01250017
      ******************************************************************01260017
       0000-MAINLINE.                                                   01270017
                                                                        01280017
           OPEN OUTPUT OFFSITE-STORAGE-FILE.                            01290017
           PERFORM 1100-OPEN-XFER-CURSOR.                               01300017
           PERFORM 2000-READ-TRANSFERS.                                 01310017
           PERFORM 1200-CLOSE-XFER-CURSOR.                              01320017
                                                                        01330017
           CLOSE OFFSITE-STORAGE-FILE.                                  01340017
           STOP RUN.                                                    01350017
                                                                        01360017
      ******************************************************************01370017
      *  OPEN THE TRANSFER CURSOR.                                      01380017
      ******************************************************************01390017
       1100-OPEN-XFER-CURSOR.                                           01400017
           EXEC SQL                                                     01410017
                OPEN TRANSFER-CSR                                       01420017
           END-EXEC.                                                    01430017
                                                                        01440017
           EVALUATE TRUE                                                01450017
               WHEN SQLCODE = ZERO                                      01460017
                    CONTINUE                                            01470017
               WHEN SQLWARN0 NOT EQUAL SPACE                            01480017
               WHEN SQLCODE NOT EQUAL ZERO                              01490017
                   MOVE '1100-OPEN-XFER-CURSOR' TO PV-PARAGRAPH-NAME    01500017
                   MOVE 'OPEN TRANSFER CURSOR'   TO PV-DB2-OPERATION    01510017
                   PERFORM 9100-SQL-ERROR                               01520017
           END-EVALUATE.                                                01530017
      *************************************************************     01540017
      * CLOSE TRANSFER CURSOR                                           01550017
      *************************************************************     01560017
       1200-CLOSE-XFER-CURSOR.                                          01570017
           EXEC SQL                                                     01580017
                CLOSE TRANSFER-CSR                                      01590017
           END-EXEC.                                                    01600017
      ******************************************************************01610017
      * CONTROL THE PROCESSING OF THE TRANSFER CURSOR                   01620017
      ******************************************************************01630017
       2000-READ-TRANSFERS.                                             01640017
           PERFORM                                                      01650017
               UNTIL (PS-NO-MORE-TRANSFERS)                             01660017
                                                                        01670017
               PERFORM 2100-FETCH-TRANSFER                              01680017
                                                                        01690017
               IF  PS-MORE-TRANSFERS                                    01700017
                   PERFORM 3000-PROCESS-TRANSFER                        01710017
               END-IF                                                   01720017
           END-PERFORM.                                                 01730017
       EJECT                                                            01740017
      *----------------------------------------------------------------*01750017
      *    FETCH THE NEXT TRANSFER.                                     01760017
      *----------------------------------------------------------------*01770017
       2100-FETCH-TRANSFER.                                             01780017
           PERFORM                                                      01790017
               WITH TEST AFTER                                          01800017
               VARYING PV-RETRY-COUNT                                   01810017
               FROM 1 BY 1                                              01820017
               UNTIL PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES         01830017
                  OR SQLCODE = 0                                        01840017
                  OR SQLCODE = +100                                     01850017
               EXEC SQL                                                 01860017
                    FETCH TRANSFER-CSR                                  01870017
                    INTO  :TFMSTR-FR-STR-NBR                            01880017
                         ,:TFMSTR-XFER-SEQ-NBR                          01890017
022803                   ,:SKXREF-DEPT                                  01921018
022803                   ,:SKXREF-CLASS                                 01922018
022803                   ,:SKXREF-SUBCLASS                              01923018
                         ,:TFITEM-SCN-UPC-NBR                           01930017
                         ,:TFITEM-XFER-QTY                              01940017
                         ,:TFITEM-XFER-PRIC-AMT                         01950017
                         ,:TFSTAT-XFER-STAT-CDE                         01960017
022803                   ,:SKXREF-ITM-NBR                               01961018
               END-EXEC                                                 01970017
                                                                        01980017
               EVALUATE TRUE                                            01990017
                   WHEN SQLCODE = ZERO                                  02000017
                   WHEN SQLCODE = -904                                  02010017
                   WHEN SQLCODE = -913                                  02020017
                        CONTINUE                                        02030017
                   WHEN SQLCODE = +100                                  02040017
                        SET PS-NO-MORE-TRANSFERS  TO TRUE               02050017
                   WHEN SQLWARN0 NOT EQUAL SPACE                        02060017
                   WHEN SQLCODE NOT EQUAL ZERO                          02070017
                        MOVE '2100-FETCH-TRANSFER'                      02080017
                                     TO  PV-PARAGRAPH-NAME              02090017
                        MOVE 'FETCH FROM TRANSFER CURSOR'               02100017
                                            TO PV-DB2-OPERATION         02110017
                       PERFORM 9100-SQL-ERROR                           02120017
               END-EVALUATE                                             02130017
           END-PERFORM.                                                 02140017
           IF  PV-RETRY-COUNT GREATER THAN PC-MAX-RETRIES               02150017
               MOVE '2100-FETCH-TRANSFER'   TO  PV-PARAGRAPH-NAME       02160017
               MOVE 'MAX RETRIES FETCHING FROM TRANSFER CURSOR'         02170017
                                            TO PV-DB2-OPERATION         02180017
               PERFORM 9100-SQL-ERROR                                   02190017
           END-IF.                                                      02200017
      *----------------------------------------------------------------*02210017
      *  CONTROL THE PROCESSING OF THE TRANSFER DATA.                  *02220017
      *----------------------------------------------------------------*02230017
       3000-PROCESS-TRANSFER.                                           02240017
           MOVE SPACES                  TO TF040-OFFSITE-STORAGE-RECORD.02250017
           MOVE TFMSTR-FR-STR-NBR       TO TF040-FR-STR-NBR.            02260017
           MOVE TFMSTR-XFER-SEQ-NBR     TO TF040-XFER-SEQ-NBR.          02270017
022803     MOVE SKXREF-DEPT             TO TF040-DEPT.                  02301018
022803     MOVE SKXREF-CLASS            TO TF040-CLASS.                 02302018
022803     MOVE SKXREF-SUBCLASS         TO TF040-SUBCLASS.              02303018
           MOVE TFITEM-SCN-UPC-NBR      TO TF040-SCN-UPC-NBR.           02310017
           MOVE TFITEM-XFER-QTY         TO TF040-XFER-QTY.              02330017
           MOVE TFITEM-XFER-PRIC-AMT    TO TF040-XFER-PRIC-AMT.         02340017
           MOVE TFSTAT-XFER-STAT-CDE    TO TF040-XFER-STAT-CDE.         02350017
022803     MOVE SKXREF-ITM-NBR          TO TF040-ITM-NBR.               02351018
           WRITE OFFSITE-STORAGE-FILE-RECORD                            02360017
                                      FROM TF040-OFFSITE-STORAGE-RECORD.02370017
      ******************************************************************02380017
      *  FORMAT A DB2 ERROR MESSAGE, RELEASE ALL LOCKS, AND ABEND THE  *02390017
      *  PROGRAM.                                                      *02400017
      ******************************************************************02410017
       9100-SQL-ERROR.                                                  02420017
           MOVE SQLCODE TO SQLCODE2.                                    02430017
           MOVE SQLERRD(3) TO SQLERRD3.                                 02440017
           DISPLAY '** ABEND **'.                                       02450017
           DISPLAY 'TFKUT493 - SQL ERROR'.                              02460017
           DISPLAY '       SQLCODE = ' SQLCODE2.                        02470017
           DISPLAY '      SQLWARN0 = ' SQLWARN0.                        02480017
           DISPLAY '      SQLWARN1 = ' SQLWARN1.                        02490017
           DISPLAY '      SQLWARN2 = ' SQLWARN2.                        02500017
           DISPLAY '      SQLWARN3 = ' SQLWARN3.                        02510017
           DISPLAY '      SQLWARN4 = ' SQLWARN4.                        02520017
           DISPLAY '      SQLWARN5 = ' SQLWARN5.                        02530017
           DISPLAY '      SQLWARN6 = ' SQLWARN6.                        02540017
           DISPLAY '      SQLWARN7 = ' SQLWARN7.                        02550017
           DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                        02560017
                                                                        02570017
           MOVE +4013 TO ABEND-CODE.                                    02580017
           CALL 'ILBOABN0' USING ABEND-CODE.                            02590017
