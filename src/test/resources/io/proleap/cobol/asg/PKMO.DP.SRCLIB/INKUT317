       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT317.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  07-20-2021.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     INKUT317 - CLONED FROM INKUT297.                                    
      *                                                                         
      *                THIS PROGRAM HAS BEEN RE-WRITTEN TO PROCESS              
      *                THE MERGED FILE BETWEEN THE PHYSICAL (TSUMINV)           
      *                EXTRACT AND THE CTOFF DATABASE (INT_INV_CTOFF)           
      *                EXTRACT IN THE INRD032 FORMAT AND STRIP OFF THE          
      *                TOP 00100 CANDIDATES. CHANGE INCORPORATES                
      *                CHANGES FOR STORE NUMBER EXPANSION (WR26600)             
      *                PREVIOUS 'CHANGE HISTORY' HAS BEEN DELETED               
      *                                                                         
      *                THIS PROGRAM WILL NOT EXCLUDE RECORDS THAT               
      *                HAVE A SYSTEM PRICE OF 0.  ITEMS THAT HAVE               
      *                0 SYSTEM PRICE ARE EITHER GWP OR CLEARANCE               
      *                PRICE.                                                   
      *                                                                         
      *                CONTROL CARD (INP02) DETERMINES NUMBER OF                
      *                EXCEPTIONS PER STORE WILL BE ON OUTPUT FILE              
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
W33304* W33304   07-20-2021  NEW PROGRAM - THIS WAS COPIED FROM                 
W33304*                      INKUT297.                                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-MERGE-FILE-IN                                           
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CONTROL-FILE-IN                                               
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT STORE-REPORT-FILE-OUT                                         
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-MERGE-FILE-IN                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-MERGE-REC-IN    PIC X(150).                                    
                                                                                
       FD  CONTROL-FILE-IN                                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-REC-IN        PIC X(80).                                     
                                                                                
       FD  STORE-REPORT-FILE-OUT                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-REPORT-REC-OUT   PIC X(150).                                   
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT317'.             
                                                                                
       01  CC-CONTROL-REC.                                                      
           05  CC-CONTROL-COUNT        PIC 9(05)  VALUE ZERO.                   
           05  FILLER                  PIC X(75)  VALUE SPACES.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-MAX-PER-STORE-COUNT  PIC  9(05) VALUE ZERO.                   
           05  PV-STORE-INV-READ       PIC S9(09) VALUE +0  COMP.               
           05  PV-LOCATION-COUNT       PIC S9(09) VALUE +0  COMP.               
           05  PV-DISP-STORE-COUNT     PIC S9(09) VALUE +0  COMP.               
           05  PV-CYCLE-COUNT          PIC S9(09) VALUE +0  COMP.               
           05  PV-FIN-BK-COUNT         PIC S9(09) VALUE +0  COMP.               
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) VALUE +0  COMP.               
           05  PV-STORE-NUMBER         PIC  9(04) VALUE ZEROS.                  
           05  PV-HOLD-STORE           PIC  9(04) VALUE ZEROS.                  
           05  PV-CYCLE-NUMBER         PIC  S9(9) VALUE +0  COMP.               
           05  PV-HOLD-CYCLE           PIC  S9(9) VALUE +0  COMP.               
           05  PV-FIN-BK-DATE          PIC  X(10) VALUE SPACES.                 
           05  PV-HOLD-FIN-BK-DATE     PIC  X(10) VALUE SPACES.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-EOF-SW       PIC  X(01)      VALUE 'N'.               
               88  PS-CONTROL-FILE-EMPTY               VALUE 'Y'.               
           05  PS-STORE-INV-EOF-SW     PIC  X(01)      VALUE 'N'.               
               88  PS-STORE-INV-EOF-YES                VALUE 'Y'.               
               88  PS-STORE-INV-EOF-NO                 VALUE 'N'.               
           05  PS-STORE-BREAK-SW       PIC  X(01)      VALUE 'N'.               
               88  PS-STORE-BREAK-YES                  VALUE 'Y'.               
               88  PS-STORE-BREAK-NO                   VALUE 'N'.               
           05  PS-CYCLE-BREAK-SW       PIC  X(01)      VALUE 'N'.               
               88  PS-CYCLE-BREAK-YES                  VALUE 'Y'.               
               88  PS-CYCLE-BREAK-NO                   VALUE 'N'.               
           05  PS-FIN-BK-BREAK-SW      PIC  X(01)      VALUE 'N'.               
               88  PS-FIN-BK-BREAK-YES                 VALUE 'Y'.               
               88  PS-FIN-BK-BREAK-NO                  VALUE 'N'.               
           05  PS-CHANGE-IN-KEY-SW     PIC  X(01)      VALUE 'N'.               
               88 PS-CHANGE-IN-KEY-YES                 VALUE 'Y'.               
               88 PS-CHANGE-IN-KEY-NO                  VALUE 'N'.               
                                                                                
      /****************************************************************         
      * COPYBOOK - INVENTORY EXCEPTIONS BY STORE EXTRACT                        
      *****************************************************************         
           COPY INRD032.                                                        
                                                                                
      *****************************************************************         
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - WORK AREA          
      ***TO BE USED AGAIN AS THE OUTPUT RECORD LAYOUT AS WELL AS INPUT          
      *****************************************************************         
           COPY INRD032 REPLACING IN032 BY INWRK.                               
                                                                                
      /*****************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0100-MAINLINE-INKUT317.                                                  
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
      *************************************************************             
      *    PROCESS RECORDS UNTIL END OF STORE FILE OR                           
      *    END OF CONTROL RECORD FILE                                           
      *************************************************************             
           PERFORM 1500-PROCESS-CYCLE                                           
               UNTIL PS-STORE-INV-EOF-YES                                       
                  OR PS-CONTROL-FILE-EMPTY.                                     
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      * 1000-INITIALIZATION.                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '*  START OF INKUT317                     *'.                
           DISPLAY '******************************************'.                
                                                                                
           OPEN  INPUT STORE-MERGE-FILE-IN                                      
                 INPUT CONTROL-FILE-IN                                          
                OUTPUT STORE-REPORT-FILE-OUT.                                   
                                                                                
           PERFORM 4000-READ-MERGE-FILE.                                        
           IF PS-STORE-INV-EOF-YES                                              
               DISPLAY '*************************************'                  
               DISPLAY '**  PROGRAM = INKUT317'                                 
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'                     
               DISPLAY '**'                                                     
               DISPLAY '**  NO EXCEPT RECORD TO PROCESS'                        
               DISPLAY '**  INP01'                                              
               DISPLAY '*************************************'                  
           ELSE                                                                 
               ADD 1                   TO PV-DISP-STORE-COUNT                   
               ADD 1                   TO PV-CYCLE-COUNT                        
               ADD 1                   TO PV-FIN-BK-COUNT                       
               MOVE PV-FIN-BK-DATE     TO PV-HOLD-FIN-BK-DATE                   
               MOVE PV-CYCLE-NUMBER    TO PV-HOLD-CYCLE                         
               MOVE PV-STORE-NUMBER    TO PV-HOLD-STORE                         
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-CONTROL-FILE.                                      
           IF PS-CONTROL-FILE-EMPTY                                             
               DISPLAY '*************************************'                  
               DISPLAY '**  PROGRAM = INKUT317'                                 
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'                     
               DISPLAY '**'                                                     
               DISPLAY '**  NO CONTROL RECORD COUNT WAS FOUND'                  
               DISPLAY '**  INP02'                                              
               DISPLAY '*************************************'                  
           END-IF.                                                              
                                                                                
      /*****************************************************************        
      * 1500-PROCESS-CYCLE.                                                     
      *   PROCESS STORES WITHIN FIN-BK / CYCLE                                  
      ******************************************************************        
       1500-PROCESS-CYCLE.                                                      
                                                                                
           PERFORM 2000-PROCESS-STORE-MERGE-RECS                                
               UNTIL PS-STORE-INV-EOF-YES                                       
                  OR PS-FIN-BK-BREAK-YES                                        
                  OR PS-CYCLE-BREAK-YES.                                        
                                                                                
           PERFORM 1550-CHECK-BREAKS.                                           
                                                                                
      /*****************************************************************        
      * 1550-CHECK-BREAKS.                                                      
      *   SET BREAK SWITCHES                                                    
      ******************************************************************        
       1550-CHECK-BREAKS.                                                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-STORE-INV-EOF-YES                                        
                  CONTINUE                                                      
               WHEN PS-FIN-BK-BREAK-YES                                         
                  SET PS-FIN-BK-BREAK-NO  TO TRUE                               
                  SET PS-CYCLE-BREAK-NO   TO TRUE                               
                  SET PS-STORE-BREAK-NO   TO TRUE                               
                  ADD 1                   TO PV-FIN-BK-COUNT                    
                  MOVE PV-FIN-BK-DATE     TO PV-HOLD-FIN-BK-DATE                
                  MOVE PV-CYCLE-NUMBER    TO PV-HOLD-CYCLE                      
                  MOVE PV-STORE-NUMBER    TO PV-HOLD-STORE                      
                  MOVE ZEROES             TO PV-LOCATION-COUNT                  
               WHEN PS-CYCLE-BREAK-YES                                          
                  SET PS-CYCLE-BREAK-NO   TO TRUE                               
                  SET PS-STORE-BREAK-NO   TO TRUE                               
                  ADD 1                   TO PV-CYCLE-COUNT                     
                  MOVE PV-CYCLE-NUMBER    TO PV-HOLD-CYCLE                      
                  MOVE PV-STORE-NUMBER    TO PV-HOLD-STORE                      
                  MOVE ZEROES             TO PV-LOCATION-COUNT                  
               WHEN PS-STORE-BREAK-YES                                          
                  SET PS-STORE-BREAK-NO   TO TRUE                               
                  ADD 1                   TO PV-DISP-STORE-COUNT                
                  MOVE PV-STORE-NUMBER    TO PV-HOLD-STORE                      
                  MOVE ZEROES             TO PV-LOCATION-COUNT                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
      /*****************************************************************        
      * 2000-PROCESS-STORE-MERGE-RECS.                                          
      *   EACH LOCATION IS PROCESSED UNTIL ##### RECORDS ARE WRITTEN            
      ******************************************************************        
       2000-PROCESS-STORE-MERGE-RECS.                                           
                                                                                
           MOVE IN032-STORE-NUMBER   TO PV-STORE-NUMBER                         
                                        PV-HOLD-STORE                           
           MOVE +0                   TO PV-LOCATION-COUNT                       
                                                                                
           SET PS-STORE-BREAK-NO     TO TRUE.                                   
                                                                                
           PERFORM 2100-PROCESS-LOCATION                                        
SR3403*        UNTIL PS-STORE-BREAK-YES.                                        
SR3403         UNTIL PS-STORE-INV-EOF-YES                                       
SR3403            OR PS-STORE-BREAK-YES.                                        
      *                                                                         
      /*****************************************************************        
      * 2100-PROCESS-LOCATION.                                                  
      *   PROCESS EACH STORE FROM THE STORE-SORTED INPUT FILE.                  
      *   BASED ON THE INPUT CONTROL CARD, THE STORE WILL WRITE THE             
      *    SPECIFIC NUMBER OF RECORDS PER STORE (UNLESS THE NUMBER OF           
      *    RECORDS READ IS LESS THAN THE MAX - THEN END THERE)                  
      *   BYPASS RECORDS WHERE THE SYSTEM PRICE = $ 0.00                        
      ******************************************************************        
       2100-PROCESS-LOCATION.                                                   
                                                                                
      ***************************************************************           
      *    TO CHECK WHEN MAXIMUM RECORDS OF STORE HAS BEEN SATISFIED            
      ***************************************************************           
           IF PV-LOCATION-COUNT = PV-MAX-PER-STORE-COUNT                        
               PERFORM 2300-COMPLETE-LOC-PROCESSING                             
           ELSE                                                                 
      ********************************************************                  
      *    PROCESS THE RECORD INFO FOR REPORTING WHEN NO CHANGES                
      ********************************************************                  
              EVALUATE TRUE                                                     
                 WHEN ((PV-FIN-BK-DATE   =  PV-HOLD-FIN-BK-DATE)                
                  AND  (PV-CYCLE-NUMBER  =  PV-HOLD-CYCLE)                      
                  AND  (PV-STORE-NUMBER  =  PV-HOLD-STORE))                     
                    IF IN032-SYSTEM-PRICE > 0                                   
                       MOVE IN032-INV-EXCEPTS-STR-REC  TO                       
                            INWRK-INV-EXCEPTS-STR-REC                           
                                                                                
                       PERFORM 5000-WRITE-EXTRACT-REC                           
                                                                                
                       PERFORM 4000-READ-MERGE-FILE                             
                    ELSE                                                        
                       PERFORM 4000-READ-MERGE-FILE                             
                    END-IF                                                      
                                                                                
      ********************************************************                  
      *    WHENEVER A CHANGE IN FINANCIAL BOOK, CYCLE OR STORE                  
      *    SET A STOP TO PROCESS WITHIN THIS ROUTINE                            
      ********************************************************                  
                 WHEN OTHER                                                     
                    PERFORM 2350-CHECK-COMPARES                                 
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 2300-COMPLETE-LOC-PROCESSING                                            
      *   PROCESS INPUT FILE UNTIL END-OF-FILE OR NEXT LOCATION FOUND           
      *****************************************************************         
       2300-COMPLETE-LOC-PROCESSING.                                            
                                                                                
           MOVE ZERO  TO PV-LOCATION-COUNT.                                     
                                                                                
           EVALUATE TRUE                                                        
               WHEN PS-STORE-INV-EOF-YES                                        
                  CONTINUE                                                      
               WHEN ((PV-FIN-BK-DATE   =  PV-HOLD-FIN-BK-DATE)                  
                AND  (PV-CYCLE-NUMBER  =  PV-HOLD-CYCLE)                        
                AND  (PV-STORE-NUMBER  =  PV-HOLD-STORE))                       
                                                                                
                  PERFORM 2310-PROCESS-REST                                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
           IF PS-STORE-INV-EOF-YES                                              
              SET PS-STORE-BREAK-YES          TO TRUE                           
           ELSE                                                                 
              PERFORM 2350-CHECK-COMPARES                                       
           END-IF.                                                              
                                                                                
       2310-PROCESS-REST.                                                       
           SET PS-CHANGE-IN-KEY-NO TO TRUE.                                     
                                                                                
           PERFORM 2330-READ-UNTIL-CHANGE                                       
              UNTIL PS-CHANGE-IN-KEY-YES.                                       
                                                                                
       2330-READ-UNTIL-CHANGE.                                                  
           PERFORM 4000-READ-MERGE-FILE.                                        
                                                                                
           IF (PV-FIN-BK-DATE   >  PV-HOLD-FIN-BK-DATE) OR                      
              (PV-CYCLE-NUMBER  >  PV-HOLD-CYCLE) OR                            
              (PV-STORE-NUMBER  >  PV-HOLD-STORE) OR                            
              (PS-STORE-INV-EOF-YES)                                            
                 SET PS-CHANGE-IN-KEY-YES TO TRUE                               
           END-IF.                                                              
                                                                                
      /*****************************************************************        
      * 2350-CHECK-COMPARES.                                                    
      * CHECK COMPARE FIELDS.                                                   
      *****************************************************************         
       2350-CHECK-COMPARES.                                                     
                                                                                
           EVALUATE TRUE                                                        
      *       CHANGE IN FINANCIAL BOOK DATE                                     
              WHEN PV-FIN-BK-DATE NOT EQUAL PV-HOLD-FIN-BK-DATE                 
                   SET PS-FIN-BK-BREAK-YES TO TRUE                              
                   SET PS-CYCLE-BREAK-YES  TO TRUE                              
                   SET PS-STORE-BREAK-YES  TO TRUE                              
      *       CHANGE IN CYCLE                                                   
              WHEN PV-CYCLE-NUMBER NOT EQUAL PV-HOLD-CYCLE                      
                   SET PS-CYCLE-BREAK-YES  TO TRUE                              
                   SET PS-STORE-BREAK-YES  TO TRUE                              
                   ADD 1                   TO PV-CYCLE-COUNT                    
      *       CHANGE IN STORE                                                   
              WHEN PV-STORE-NUMBER NOT EQUAL PV-HOLD-STORE                      
                   SET PS-STORE-BREAK-YES  TO TRUE                              
                   ADD 1                   TO PV-DISP-STORE-COUNT               
           END-EVALUATE.                                                        
                                                                                
      /****************************************************************         
      * 4000-READ-MERGE-FILE.                                                   
      * INPUT FILE IS SORTED IN CORRECT ORDER.                                  
      *****************************************************************         
       4000-READ-MERGE-FILE.                                                    
                                                                                
           READ STORE-MERGE-FILE-IN                                             
               INTO IN032-INV-EXCEPTS-STR-REC                                   
             AT END                                                             
                 MOVE 'Y' TO PS-STORE-INV-EOF-SW                                
             NOT AT END                                                         
                 MOVE IN032-FNCL-BKG-DTE TO PV-FIN-BK-DATE                      
                 MOVE IN032-INV-ID       TO PV-CYCLE-NUMBER                     
                 MOVE IN032-STORE-NUMBER TO PV-STORE-NUMBER                     
                 ADD +1                  TO PV-STORE-INV-READ                   
           END-READ.                                                            
                                                                                
      /****************************************************************         
      * 4100-READ THE CONTROL FILE DENOTING MAX RECORDS/LOC TO WRITE            
      *****************************************************************         
       4100-READ-CONTROL-FILE.                                                  
                                                                                
           READ CONTROL-FILE-IN                                                 
               INTO CC-CONTROL-REC                                              
             AT END                                                             
                 MOVE 'Y' TO PS-CONTROL-EOF-SW                                  
             NOT AT END                                                         
                 MOVE CC-CONTROL-COUNT TO PV-MAX-PER-STORE-COUNT                
           END-READ.                                                            
      *                                                                         
      /*****************************************************************        
      * 5000-WRITE-EXTRACT-REC.                                                 
      * WRITE THE TOP EXCEPTION ITEMS. MAX RECORDS/LOC DEFINED BY INP02         
      ******************************************************************        
       5000-WRITE-EXTRACT-REC.                                                  
                                                                                
           WRITE STORE-REPORT-REC-OUT                                           
               FROM INWRK-INV-EXCEPTS-STR-REC                                   
                                                                                
           ADD +1 TO PV-TOTAL-EXTR-WRITTEN.                                     
           ADD +1 TO PV-LOCATION-COUNT.                                         
                                                                                
      /*****************************************************************        
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                            
      ******************************************************************        
       8000-EOJ.                                                                
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** RECORD COUNT SUMMARY'.                                  
           DISPLAY 'MAXIMUM RECORDS CREATED PER LOC = '                         
                 PV-MAX-PER-STORE-COUNT.                                        
           DISPLAY 'INVENTORY EXTRACT RECORDS READ  = '                         
                 PV-STORE-INV-READ.                                             
           DISPLAY 'INVENTORY FINANCIAL BOOK DATE BREAK COUNT = '               
                 PV-FIN-BK-COUNT.                                               
           DISPLAY 'INVENTORY CYCLE BREAK COUNT = '                             
                 PV-CYCLE-COUNT.                                                
           DISPLAY 'INVENTORY STORE BREAK COUNT = '                             
                 PV-DISP-STORE-COUNT.                                           
           DISPLAY 'INVENTORY EXTRACT RECORDS WRITTEN  = '                      
                 PV-TOTAL-EXTR-WRITTEN.                                         
                                                                                
           DISPLAY '******************************************'.                
           DISPLAY '*  END OF INKUT317                       *'.                
           DISPLAY '******************************************'.                
                                                                                
           CLOSE STORE-MERGE-FILE-IN                                            
                 CONTROL-FILE-IN                                                
                 STORE-REPORT-FILE-OUT.                                         
                                                                                
