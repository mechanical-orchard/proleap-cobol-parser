      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             TCKUP080.                                00050000
       AUTHOR.                 DAN PAYNTER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            JUNE 2023.                               00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *                                                                 00120000
      *   ***  COBRAND TRAILING TRANSACTION ACCT INSERT PROGRAM ***     00130000
      *                                                                 00140000
      *  FOR RELEASE 1 OF COBRAND VISA GIFT CARDS (REPLACEMENT TO       00150000
      *  THE ORIGINAL KOHL'S CHARGE CARD THAT SETTLES WITH FISERV),     00160000
      *  THERE WILL BE TRANSACTIONS GENERATED BY CUSTOMER SALES,        00170000
      *  PAYMENTS, PRICE ADJUSTMENTS, AND RETURNS SHORTLY AFTER AN      00180000
      *  ACCOUNT IS CONVERTED FROM FISERV KOHL'S CHARGE TO CAP ONE      00190000
      *  COBRAND VISA THAT WILL COME WITH THE SMALLER FISERV ACCOUNT    00200000
      *  NUMBER, BUT STILL REQUIRE SETTLEMENT TO FLOW TO CAPITAL ONE.   00210000
      *  ANY SUCH TRANSACTIONS WILL REQUIRE EITHER THE TEPCRD OR TEPPAY 00220000
      *  DB2 TABLES TO BE UPDATED TO MARK THE CARD/ACCOUNT AS A         00230000
      *  TRAILING TRANSACTION.  THIS INDICATOR WILL THEN BE USED BY THE 00240000
      *  THE NIGHTLY SETTLEMENT PROCESSES TO DETERMINE WHICH SETTLEMENT 00250000
      *  FILE THE TRANSACTION SHOULD BE ON.  IF THE TRLING_TRN_IND = 'Y'00260000
      *  THEN THE TRANSACTION MUST BE PICKED UP BY THE TCK BATCH PROCESS00270000
      *  AND IF THE TRLING_TRN_IND = 'N', THEN IT SHOULD BE PICKED UP BY00280000
      *  THE ARK (FISERV) BATCH SETTLEMENT PROCESS.                     00290000
      *                                                                 00300000
      *  THIS PROGRAM WILL READ IN A FILE OF CONVERTED ACCOUNTS, APPLY  00301000
      *  PROTEGRITY PROTECTION TO THE APPROPRIATE PORTION OF THE        00302000
      *  ACCOUNT NUMBER AND THEN INSERT THOSE PROTECTED ACCOUNTS INTO   00303000
      *  THE ART_CNV_ACCT_NBR DB2 TABLE. THE INCOMING ACCOUNTS WILL     00303100
      *  BE RECEIVED IN 16 DIGIT FORMATE FOR PLCC CARDS AND 18 DIGITS   00303200
      *  FOR APPLE PAY TOKEN FORMAT.  FOR APPLE PAY, THE FULL 18 NEEDS  00303300
      *  TO BE TOKENIZED AND STORED ON THE TABLE.  FOR PLCC CARDS, ONLY 00303400
      *  THE LAST 10 NEED TO BE USED (WITH '00' APPENDED BEFORE         00303500
      *  TOKENIZATION), THEN THE 00 MUST BE DROPPED PRIOR TO INSERTING  00303600
      *  THOSE 10 DIGITS INTO THE DB2 TABLE.                            00303700
      *                                                                 00304000
      *   INPUT: INP01 - CONTAINS THE PLCC ACCOUNTS AND APPLE PAY       00331000
      *                  ACCOUNT TOKENS.                                00332000
      *                                                                 00333000
      ****************************************************************  00340000
      * CHANGE LOG:                                                     00360000
      * DATE:  2023-05-08                                               00370000
      * PRGMR: DAN PAYNTER                                              00380000
      * WHY:   NEW PROGRAM TO UPDATE TRAILING TRANSACTIONS.             00390000
      * CHANGE: CHG0288838                                              00391000
      ****************************************************************  00400000
      ****************************************************************  00410000
       ENVIRONMENT DIVISION.                                            00420000
      ****************************************************************  00430000
                                                                        00440000
       CONFIGURATION SECTION.                                           00450000
                                                                        00460000
       SOURCE-COMPUTER.        IBM-3090.                                00470000
                                                                        00480000
       OBJECT-COMPUTER.        IBM-3090.                                00490000
                                                                        00500000
       INPUT-OUTPUT SECTION.                                            00510000
                                                                        00520000
       FILE-CONTROL.                                                    00530000
                                                                        00540000
           SELECT ACCT-CONV-FILE ASSIGN TO INP01.                       00560000
                                                                        00570000
      ***************************************************************** 00580000
       DATA DIVISION.                                                   00590000
      ***************************************************************** 00600000
                                                                        00610000
       FILE SECTION.                                                    00620000
                                                                        00630000
       FD  ACCT-CONV-FILE                                               00710000
           RECORDING MODE IS F                                          00720000
           LABEL RECORDS ARE STANDARD                                   00730000
           BLOCK CONTAINS 0 RECORDS                                     00740000
           RECORD CONTAINS 80 CHARACTERS                                00750000
           DATA RECORD IS ACCT-CONV-RECORD.                             00760000
                                                                        00770000
       01  ACCT-CONV-RECORD                PIC X(80).                   00780000
                                                                        00790000
      ***************************************************************** 00800000
       WORKING-STORAGE SECTION.                                         00810000
      ***************************************************************** 00820000
       01  FILLER                            PIC X(50)  VALUE           00830000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUP080 *** '.        00840000
                                                                        00850000
       01  PS-PROGRAM-SWITCHES.                                         01080000
           05  PS-INPUT-CNV-ACCT-EOF-SW     PIC  X(01) VALUE 'N'.       01081000
               88  PS-INPUT-CNV-ACCT-EOF               VALUE 'Y'.       01082000
           05  PS-END-OF-DATE-FILE-SW        PIC  X(01) VALUE 'N'.      01090000
               88  PS-END-OF-DATE-FILE                  VALUE 'Y'.      01100000
                                                                        01170000
       01  PC-CONSTANTS.                                                01180000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE             01190000
                                                      'TCKUP080'.       01200000
           05  PC-DUPLICATE-ROW-FOUND      PIC S9(04) VALUE -803        01230000
                                                      COMP SYNC.        01230100
           05  PC-APPLEPAY-BIN             PIC  X(06) VALUE '600430'.   01241000
           05  PC-KC-BIN                   PIC  X(06) VALUE '639305'.   01243000
                                                                        01244000
       01  PROGRAM-ACCUMULATORS.                                        01245000
           05  PA-READ                     PIC  9(11) VALUE ZEROS.      01246000
           05  PA-INSERTED                 PIC  9(11) VALUE ZEROS.      01247000
           05  PA-DUPE-RECS                PIC  9(11) VALUE ZEROS.      01247100
                                                                        01248000
      ******************************************************************01249000
      *               SYSTEM TIME AND DATE VARIABLES                    01249100
      ******************************************************************01249200
       01  SYS-DATE-TIME.                                               01249300
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01249400
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01249500
                                                                        01249600
       01  ST-BEG-TIME.                                                 01249700
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01249800
           05  FILLER                       PIC  X(01) VALUE ':'.       01249900
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01250000
                                                                        01250100
       01  ST-END-TIME.                                                 01250200
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01250300
           05  FILLER                       PIC  X(01) VALUE ':'.       01250400
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01250500
                                                                        01250600
       01  SD-EDIT-DATE.                                                01250700
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01250800
           05  FILLER                       PIC  X(01) VALUE '-'.       01250900
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01251000
           05  FILLER                       PIC  X(01) VALUE '-'.       01251100
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01251200
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01251300
                                                                        01251400
       01  ABEND-CODE                  PIC S9(04)  VALUE ZEROS          01251500
                                                   COMP SYNC.           01251600
           88  AC-BAD-RECEIVE-DATA                 VALUE +4001.         01251700
           88  AC-BAD-DATA                         VALUE +4008.         01251800
           88  AC-DB2-ERROR                        VALUE +4013.         01251900
           88  AC-DATE-ERROR                       VALUE +4019.         01252000
                                                                        01252400
       01  PV-PROGRAM-VARIABLES.                                        01253000
           05  PV-KC-ACCT-LENGTH           PIC  S9(2)V COMP-3.          01261000
           05  PV-KC-ACCT                  PIC  X(18).                  01261100
           05  PV-KC-SUBSTR-REC6 REDEFINES PV-KC-ACCT.                  01261900
               10  PV-KC-ACCT-FIRST-6      PIC  X(06).                  01262000
               10  PV-KC-ACCT-10           PIC  X(10).                  01262100
               10  PV-KC-ACCT-LAST-2       PIC  X(02).                  01262200
           05  PV-KC-ACCT-SRC-CDE          PIC  X(02).                  01262300
           05  PV-ACCT-FOUND               PIC  X(01).                  01262400
          05 PV-ACCT-NBR-TOKEN            PIC X(18)  VALUE SPACES.      01262500
          05 PV-ACCOUNT-NBR-12.                                         01262600
             10 PV-ACCOUNT-NBR-10         PIC X(10)  VALUE SPACES.      01262700
             10 FILLER                    PIC X(02)  VALUE '00'.        01262800
                                                                        01263000
       01  PV-ACCT-CONV-FILE-REC.                                       01270000
           05  PV-INPUT-CARD-ACCT          PIC X(18).                   01280000
           05  FILLER                      PIC X(62).                   01280200
                                                                        01280300
       01  PV-DB2-VARIABLES.                                            01310000
           05  PV-DB2-PARTN-NBR            PIC S9(04)    COMP           01320000
                                                         VALUE ZERO.    01330000
           05  PV-PREV-PARTN-NBR           PIC S9(04)    COMP           01340000
                                                         VALUE ZERO.    01350000
       01  PV-MISC-FIELDS.                                              01420000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01430000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01440000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01450000
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01460000
           05  PV-EDIT-MASK                PIC  Z,ZZZ,ZZ9.              01470000
                                                                        01480000
      ***************************************************************** 01540000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01550000
      ***************************************************************** 01560000
           COPY DPWS004.                                                01570000
                                                                        01580000
      ***************************************************************** 01590000
      *  DB2 COMMUNICATION AREA.                                        01600000
      ***************************************************************** 01610000
           EXEC SQL                                                     01620000
                INCLUDE SQLCA                                           01630000
           END-EXEC.                                                    01640000
                                                                        01650000
      *---------------------------------------------------------------* 01651000
      * LAYOUT FOR THE PARAMETERS NEEDED TO INVOKE PROTEGRITY MODULE  * 01652000
      *---------------------------------------------------------------* 01653000
           COPY PTYCCSIP.                                               01654000
                                                                        01655000
      *---------------------------------------------------------------* 01656000
      * LAYOUT FOR THE WORKING VARIABLES TO INVOKE PROTEGRITY MODULE  * 01657000
      *---------------------------------------------------------------* 01658000
           COPY DPWS031.                                                01659000
                                                                        01659100
      ***************************************************************** 01660000
      *  DB2 TABLE ART_CNV_ACCT_NBR - CONVERTED PLCC ACCOUNT TABLE      01670000
      ***************************************************************** 01680000
           EXEC SQL                                                     01690000
                INCLUDE ART00003                                        01700000
           END-EXEC.                                                    01710000
                                                                        01720000
      ***************************************************************** 03510000
      * PROCEDURE DIVISION.                                           * 03520000
      ***************************************************************** 03530000
       PROCEDURE DIVISION.                                              03540000
                                                                        03550000
      ***************************************************************** 03560000
      * 0000-MAINLINE-PARAGRAPH                                         03570000
      ***************************************************************** 03580000
       0000-MAINLINE-PARAGRAPH.                                         03590000
                                                                        03600000
           PERFORM 1000-INITIALIZE-PROGRAM.                             03610000
                                                                        03620000
           PERFORM 2100-PROCESS-ACCT                                    03641000
             UNTIL PS-INPUT-CNV-ACCT-EOF.                               03642000
                                                                        03670000
           PERFORM 9999-CONTROLS.                                       03800000
                                                                        03890000
           CLOSE ACCT-CONV-FILE.                                        03900000
           STOP RUN.                                                    03910000
                                                                        03920000
      ***************************************************************** 03930000
      * 1000-INITIALIZE-PROGRAM                                         03940000
      *     PROCESS CONTROL CARD                                        03950000
      ***************************************************************** 03960000
       1000-INITIALIZE-PROGRAM.                                         03970000
                                                                        03980000
           OPEN INPUT  ACCT-CONV-FILE.                                  03990000
                                                                        04010000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04081000
                                                                        04082000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            04083000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            04084000
                                                                        04085000
      ***************************************************************** 04550000
      * 1500-INS-ART-CNV-ACCT-NBR-ROW                                   04560000
      *     INSERT CONVERTED ACCOUNT (TOKENIZED FORMAT)                 04570000
      ***************************************************************** 04580000
       1500-INS-ART-CNV-ACCT-NBR-ROW.                                   04590000
                                                                        04600000
           MOVE PV-ACCT-NBR-TOKEN            TO ART00003-KC-ACCT-NBR.   04601000
                                                                        04602000
           EXEC SQL                                                     04610000
               INSERT INTO ART_CNV_ACCT_NBR                             04620000
                                   ( KC_ACCT_NBR )                      04621000
                                                                        04682000
               VALUES                                                   04683000
                  ( :ART00003-KC-ACCT-NBR )                             04684000
           END-EXEC.                                                    04689500
                                                                        04690000
           EVALUATE TRUE                                                04700000
               WHEN SQLCODE = ZERO                                      04710000
                   ADD +1 TO PA-INSERTED                                04720000
               WHEN SQLCODE = PC-DUPLICATE-ROW-FOUND                    04721000
                   ADD +1 TO PA-DUPE-RECS                               04723000
                   DISPLAY '-803 DUPLICATE ROW INSERT ATTEMPT FOR ',    04724000
                   ART00003-KC-ACCT-NBR, ' ', PV-INPUT-CARD-ACCT        04725000
               WHEN OTHER                                               04730000
                  DISPLAY '**  ABEND  **'                               04740000
                  DISPLAY 'TCKUP080'                                    04750000
                  DISPLAY '1500-INS-ART-CNV-ACCT-NBR-ROW'               04760000
                  DISPLAY 'ABENDING ON INSERT OF ART_CNV_ACCT_NBR'      04770000
                  DISPLAY 'SQLCODE = ' SQLCODE                          04780000
                  DISPLAY '   ACCT = ' ART00003-KC-ACCT-NBR             04790000
                  MOVE '1500-INS-ART-CNV-ACCT-NBR-ROW'                  04800000
                                             TO PV-PARAGRAPH-NAME       04810000
                  MOVE 'ERROR INSERT OF CONVERTED ACCT'                 04820000
                                             TO PV-DB2-OPERATION        04830000
                  PERFORM 9100-SQL-ABEND                                04840000
           END-EVALUATE.                                                04850000
                                                                        04860000
      *1500-INS-ART-CNV-ACCT-NBR-ROW.                                   04870000
                                                                        04880000
      ******************************************************************04881000
      * READ THE CONVERTED ACCOUNT INPUT FILE                           04882000
      ******************************************************************04883000
       2000-READ-CNV-ACCT-INPUT-FILE.                                   04884000
                                                                        04885000
           INITIALIZE PV-ACCT-CONV-FILE-REC.                            04885100
           READ ACCT-CONV-FILE INTO PV-ACCT-CONV-FILE-REC               04886000
             AT END SET PS-INPUT-CNV-ACCT-EOF TO TRUE.                  04887000
                                                                        04888000
           IF PS-INPUT-CNV-ACCT-EOF                                     04889000
               CONTINUE                                                 04889100
           ELSE                                                         04889200
               ADD +1 TO PA-READ                                        04889900
           END-IF.                                                      04890000
                                                                        04900000
      ***************************************************************** 05370000
      * 2100-PROCESS-ACCT.                                              05380000
      *     PROCESS EACH CONVERTED ACCOUNT FROM THE INPUT FILE.         05390000
      ***************************************************************** 05400000
       2100-PROCESS-ACCT.                                               05410000
                                                                        05420000
           PERFORM 2000-READ-CNV-ACCT-INPUT-FILE.                       05430000
           IF PS-INPUT-CNV-ACCT-EOF                                     05430200
               CONTINUE                                                 05430300
           ELSE                                                         05430400
               MOVE PV-INPUT-CARD-ACCT  TO PV-KC-ACCT                   05431000
               PERFORM 7135-TOKEN-ACCTNBR                               05432000
               PERFORM 1500-INS-ART-CNV-ACCT-NBR-ROW                    05432100
           END-IF.                                                      05433000
                                                                        05440000
      ******************************************************************07010100
      * CALLS THE PROTEGRITY MODULE TO TOKENIZE THE ACCOUNT NUMBER.    *07010200
      ******************************************************************07010300
       7135-TOKEN-ACCTNBR.                                              07010400
                                                                        07010500
           INITIALIZE CSI-PARAMETERS                                    07010600
                      DP031-PROTEG-PGM-PARAMETERS.                      07010700
                                                                        07010800
           MOVE 'ACCOUNT-NBR'           TO DP031-FIELD.                 07010900
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                        07011000
           SET DP031-ACCTNBR            TO TRUE.                        07011100
           MOVE PV-INPUT-CARD-ACCT      TO PV-KC-ACCT                   07011200
           IF PV-KC-ACCT-FIRST-6 = PC-APPLEPAY-BIN                      07011300
               MOVE PV-KC-ACCT          TO DP031-CLEAR-TEXT             07011500
               MOVE 18                  TO DP031-INPUT-LEN              07011600
           ELSE                                                         07011700
               MOVE PV-KC-ACCT-10       TO PV-ACCOUNT-NBR-10            07011800
               MOVE PV-ACCOUNT-NBR-12   TO DP031-CLEAR-TEXT             07011900
               MOVE 12                  TO DP031-INPUT-LEN              07012000
           END-IF.                                                      07012100
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                      07012200
           IF PV-KC-ACCT-FIRST-6 = PC-APPLEPAY-BIN                      07012300
               MOVE DP031-CIPHER-TEXT(1:18)                             07012400
                                        TO PV-ACCT-NBR-TOKEN            07012500
           ELSE                                                         07012600
               MOVE DP031-CIPHER-TEXT(1:10)                             07012900
                                        TO PV-ACCT-NBR-TOKEN            07013000
           END-IF.                                                      07013100
                                                                        07013200
                                                                        07013700
      ***************************************************************** 07210000
      * 9100-SQL-ABEND                                                  07220000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07230000
      ***************************************************************** 07240000
       9100-SQL-ABEND.                                                  07250000
           DISPLAY '** ABEND     **'.                                   07260000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07270000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07280000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07290000
           DISPLAY '** CONV ACCT ** = ' ART00003-KC-ACCT-NBR.           07300000
           DISPLAY '** INP  ACCT ** = ' PV-INPUT-CARD-ACCT.             07310000
                                                                        07340000
      ***************************************************************** 07350000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07360000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07370000
      ***************************************************************** 07380000
           COPY DPPD004.                                                07390000
                                                                        07400000
           SET AC-DB2-ERROR TO TRUE.                                    07410000
           CALL 'ILBOABN0' USING ABEND-CODE.                            07420000
                                                                        07430000
      *----------------------------------------------------------------*07431000
      * COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE       *07432000
      * PROTEGRITY MODULE FOR TOKENIZATION                             *07433000
      *----------------------------------------------------------------*07434000
           COPY DPPD031.                                                07436000
                                                                        07436100
      ******************************************************************07438000
      * DISPLAY CONTROLS FROM THE PROGRAM                               07439000
      ******************************************************************07439100
       9999-CONTROLS.                                                   07439200
                                                                        07439300
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 07439400
                                                                        07439500
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            07439600
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            07439700
                                                                        07439800
           DISPLAY '                    '.                              07439900
           DISPLAY '**********************'.                            07440000
           DISPLAY '   TCKUP080 CONTROLS  '.                            07440100
           DISPLAY '**********************'.                            07440200
           DISPLAY 'RUN DATE  = ' SYS-DATE-TIME.                        07440300
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          07440400
           DISPLAY 'END TIME  = ' ST-END-TIME.                          07440500
           DISPLAY '======================'.                            07440600
           DISPLAY 'RECORDS READ    = ' PA-READ.                        07440700
           DISPLAY '                  '.                                07440800
           DISPLAY 'RECORDS INSERTED= ' PA-INSERTED.                    07440900
           DISPLAY '                  '.                                07441000
           DISPLAY 'DUPLICATE RECS  = ' PA-DUPE-RECS.                   07441100
           DISPLAY '                  '.                                07441200
