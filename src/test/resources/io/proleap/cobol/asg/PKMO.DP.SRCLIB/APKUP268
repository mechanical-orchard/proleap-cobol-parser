00001  IDENTIFICATION DIVISION.                                         01/18/96
00002                                                                   APKUP268
00003  PROGRAM-ID.    APKUP268.                                            LV006
00004                                                                   APKUP268
00005  AUTHOR.        CRAIG VOSKUIL - STRATAGEM.                        APKUP268
00006                                                                   APKUP268
00007  DATE-WRITTEN.  JULY 1994.                                        APKUP268
00008                                                                   APKUP268
00009  DATE-COMPILED.                                                   APKUP268
00010                                                                   APKUP268
00011 ******************************************************************APKUP268
00012 *    THE MAIN PURPOSE OF THIS PROGRAM IS TO PURGE RELEASED       *APKUP268
00013 *    PURCHASE ORDERS WHICH HAVE BEEN PROCESSED BY THE OPEN TO    *APKUP268
00014 *    PAY PROCESSING. ANY RELATED PRICE CLAIMS WILL ALSO BE       *APKUP268
00015 *    PURGED.                                                     *APKUP268
00016 *                                                                *APKUP268
00017 *    PROCESSING CONTROL IS BASED ON A FETCH OF THE RELEASED      *APKUP268
00018 *    PURCHASE ORDER TABLE BY THE STATUS CODE EQUAL 'P'. FOR      *APKUP268
00019 *    EACH ROW SELECTED, THE PRICE CLAIM TABLE MUST BE PROCESSED. *APKUP268
00020 *    A CURCOR IS OPENED WHERE THE PURCHASE ORDER AND STORE       *APKUP268
00021 *    NUMBERS ARE EQUAL. THE PRICE CLAIM ROWS ARE DELETED. THERE  *APKUP268
00022 *    MAY NOT BE ANY ROWS SELECTED FOR THE PRICE CLAIM TABLE.     *APKUP268
00023 *    AFTER THE ROWS ARE DELETED, THE RELEASED PURCHASE ORDER     *APKUP268
00024 *    ROW IS DELETED.                                             *APKUP268
00025 *                                                                *APKUP268
00026 *    AT THE END OF THE JOB, TOTALS ARE DISPLAYED.                *APKUP268
00027 ******************************************************************APKUP268
00028 *   DATE               D E S C R I P T I O N                     *APKUP268
00029 *                                                                *APKUP268
00030 *  02/06/95  MARY KING                                           *APKUP268
00031 *            RECOMPILED DUE TO ADDED FIELD ON TPRCCLM            *APKUP268
00032 *                                                                *APKUP268
00033 *  01/17/96  MARY KING                                           *   CL**2
00034 *            ADDED SQLCODE CHECKS AFTER THE DELETE STATEMENTS    *   CL**2
00035 *            EXECUTE. IF THE DELETE WAS UNSUCCESSFUL, DISPLAY    *   CL**2
00036 *            THE KEY TO THE ROW BEING DELETED.                   *   CL**2
00037 ******************************************************************APKUP268
00038      EJECT                                                        APKUP268
00039  ENVIRONMENT DIVISION.                                            APKUP268
00040                                                                   APKUP268
00041  INPUT-OUTPUT SECTION.                                            APKUP268
00042                                                                   APKUP268
00043  FILE-CONTROL.                                                    APKUP268
00044                                                                   APKUP268
00045  DATA DIVISION.                                                   APKUP268
00046                                                                   APKUP268
00047  FILE SECTION.                                                    APKUP268
00048                                                                   APKUP268
00049  EJECT                                                            APKUP268
00050  WORKING-STORAGE SECTION.                                         APKUP268
00051                                                                   APKUP268
00052  01  ABEND-CODE                  PIC S9(04)  COMP SYNC            APKUP268
00053                                              VALUE +0.            APKUP268
00054                                                                   APKUP268
00055  01  WORK-AREAS.                                                  APKUP268
00056      05  WS-TABLE-NAME           PIC X(7).                        APKUP268
00057      05  WS-PARAGRAPH-NAME       PIC X(30).                       APKUP268
00058      05  WS-EDIT-MASK            PIC ZZZ,ZZZ,ZZ9.                 APKUP268
00059                                                                   APKUP268
00060  01  PS-PROGRAM-SWITCHES.                                         APKUP268
00061      05  PS-POSTRR-SWITCH        PIC  X(01)  VALUE 'Y'.           APKUP268
00062          88  PS-MORE-POSTRR                  VALUE 'Y'.           APKUP268
00063          88  PS-NO-MORE-POSTRR               VALUE 'N'.           APKUP268
00064      05  PS-PRCCLM-SWITCH        PIC  X(01)  VALUE 'Y'.           APKUP268
00065          88  PS-MORE-PRCCLM                  VALUE 'Y'.           APKUP268
00066          88  PS-NO-MORE-PRCCLM               VALUE 'N'.           APKUP268
00067                                                                   APKUP268
00068  01  PV-PROGRAM-COUNTS           COMP-3.                          APKUP268
00069      05  PV-POSTRR-COUNT         PIC S9(9)   VALUE ZERO.          APKUP268
00070      05  PV-PRCCLM-COUNT         PIC S9(9)   VALUE ZERO.          APKUP268
00071                                                                   APKUP268
00072  01  ABEND-AREAS.                                                 APKUP268
00073      05  AA-ABEND-LIT            PIC  X(40)  VALUE                APKUP268
00074              '*****       ABEND'.                                 APKUP268
00075      05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                APKUP268
00076              '*****   PROGRAM: APKUP268'.                         APKUP268
00077      05  AA-PARAGRAPH-LIT.                                        APKUP268
00078          10  FILLER              PIC  X(17)  VALUE                APKUP268
00079              '***** PARAGRAPH: '.                                 APKUP268
00080          10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.        APKUP268
00081      05  AA-MESSAGE-LINE.                                         APKUP268
00082          10  FILLER              PIC  X(06)  VALUE '*****'.       APKUP268
00083          10  AA-MESSAGE          PIC  X(127) VALUE SPACES.        APKUP268
00084      05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                APKUP268
00085              '*****    DB2 ERROR'.                                APKUP268
00086      05  AA-DB2-OPERATION-LIT.                                    APKUP268
00087          10  FILLER              PIC  X(17)  VALUE                APKUP268
00088              '***** OPERATION: '.                                 APKUP268
00089          10  AA-DB2-OPERATION    PIC  X(50)  VALUE SPACES.        APKUP268
00090      05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.        APKUP268
00091      EJECT                                                        APKUP268
00092      COPY DPWS004.                                                APKUP268
00093      EJECT                                                        APKUP268
00094 ******************************************************************APKUP268
00095 **   TPOSTRR - RELEASED PO/STORE TABLE                          **APKUP268
00096 ******************************************************************APKUP268
00097      EXEC SQL                                                     APKUP268
00098          INCLUDE TPOSTRR                                          APKUP268
00099      END-EXEC.                                                    APKUP268
00100      EJECT                                                        APKUP268
00101 ******************************************************************APKUP268
00102 **   TPRCCLM - PRICED CLAIM TABLE                               **APKUP268
00103 ******************************************************************APKUP268
00104      EXEC SQL                                                     APKUP268
00105          INCLUDE TPRCCLM                                          APKUP268
00106      END-EXEC.                                                    APKUP268
00107 ******************************************************************APKUP268
00108 **                  DB2 COMMUNICATION AREA  2                   **APKUP268
00109 ******************************************************************APKUP268
00110      COPY SQLCA2.                                                 APKUP268
00111      EJECT                                                        APKUP268
00112 ******************************************************************APKUP268
00113 **   DB2 COMMUNICATION AREA                                     **APKUP268
00114 ******************************************************************APKUP268
00115      EXEC SQL                                                     APKUP268
00116          INCLUDE SQLCA                                            APKUP268
00117      END-EXEC.                                                    APKUP268
00118      EJECT                                                        APKUP268
00119 ******************************************************************APKUP268
00120 **   CURSOR USED TO FETCH THE NEXT RELEASED IN THE TABLE        **APKUP268
00121 ******************************************************************APKUP268
00122      EXEC SQL                                                     APKUP268
00123          DECLARE RELEASED_CURSOR CURSOR FOR                          CL**5
00124              SELECT   PO_NBR                                      APKUP268
00125                      ,STR_NBR                                     APKUP268
00126                      ,PAYT_RLSE_TMST                                 CL**6
00127              FROM     TPOSTRR                                     APKUP268
00128              WHERE    STATUS_CDE = 'P'                            APKUP268
00129      END-EXEC.                                                    APKUP268
00130      EJECT                                                        APKUP268
00131 ******************************************************************APKUP268
00132 **   CURSOR USED TO FETCH THE NEXT PRICE CLAIM TABLE            **APKUP268
00133 ******************************************************************APKUP268
00134      EXEC SQL                                                     APKUP268
00135          DECLARE CLAIMED_CURSOR CURSOR FOR                           CL**5
00136              SELECT   PO_NBR                                      APKUP268
00137                      ,STR_NBR                                     APKUP268
00138                      ,INVC_ID                                     APKUP268
00139                      ,PO_LNE_NBR                                  APKUP268
00140                      ,PRCCLM_SEQ_NBR                              APKUP268
00141              FROM     TPRCCLM                                     APKUP268
00142              WHERE    PO_NBR = :POSTRR-PO-NBR                     APKUP268
00143              AND      STR_NBR = :POSTRR-STR-NBR                   APKUP268
00144              AND      PAYT_RLSE_TMST = :POSTRR-PAYT-RLSE-TMST     APKUP268
00145      END-EXEC.                                                    APKUP268
00146      EJECT                                                        APKUP268
00147  PROCEDURE DIVISION.                                              APKUP268
00148      PERFORM 0000-INITIALIZE.                                     APKUP268
00149                                                                   APKUP268
00150      PERFORM 1000-PROCESS-RELEASED                                APKUP268
00151             UNTIL PS-NO-MORE-POSTRR                               APKUP268
00152                                                                   APKUP268
00153      EXEC SQL                                                     APKUP268
00154          CLOSE RELEASED_CURSOR                                    APKUP268
00155      END-EXEC.                                                    APKUP268
00156                                                                   APKUP268
00157      DISPLAY '****************************************'           APKUP268
00158      DISPLAY '*************** APKUP268 ***************'           APKUP268
00159      DISPLAY '****************************************'           APKUP268
00160      MOVE PV-POSTRR-COUNT        TO WS-EDIT-MASK                  APKUP268
00161      INSPECT WS-EDIT-MASK REPLACING LEADING SPACES                APKUP268
00162          BY '.'                                                   APKUP268
00163      DISPLAY 'RELEASED PO/STORE ROWS ......' WS-EDIT-MASK         APKUP268
00164      MOVE PV-PRCCLM-COUNT        TO WS-EDIT-MASK                  APKUP268
00165      INSPECT WS-EDIT-MASK REPLACING LEADING SPACES                APKUP268
00166          BY '.'                                                   APKUP268
00167      DISPLAY 'PRICE CLAIM ROWS.............' WS-EDIT-MASK         APKUP268
00168      DISPLAY '****************************************'           APKUP268
00169      DISPLAY '****************************************'           APKUP268
00170      DISPLAY '****************************************'           APKUP268
00171                                                                   APKUP268
00172      GOBACK.                                                      APKUP268
00173      EJECT                                                        APKUP268
00174  0000-INITIALIZE.                                                 APKUP268
00175                                                                   APKUP268
00176      EXEC SQL                                                     APKUP268
00177          OPEN RELEASED_CURSOR                                     APKUP268
00178      END-EXEC.                                                    APKUP268
00179                                                                   APKUP268
00180      EVALUATE TRUE                                                APKUP268
00181          WHEN SQLCODE = ZERO                                      APKUP268
00182               CONTINUE                                            APKUP268
00183                                                                   APKUP268
00184          WHEN SQLWARN0 NOT = SPACES                               APKUP268
00185          WHEN SQLCODE  NOT = ZERO                                 APKUP268
00186              MOVE '0000-INITALIZE'        TO AA-PARAGRAPH-NAME    APKUP268
00187              MOVE 'UNSUCCESSFUL OPEN ON RELEASED_CURSOR'          APKUP268
00188                                           TO AA-DB2-OPERATION     APKUP268
00189              MOVE 'TPOSTRR'               TO AA-DB2-TABLE-1       APKUP268
00190              PERFORM 9999-SQL-ERROR                               APKUP268
00191      END-EVALUATE.                                                APKUP268
00192                                                                   APKUP268
00193      PERFORM 9000-FETCH-RELEASED-CURSOR.                          APKUP268
00194      EJECT                                                        APKUP268
00195  1000-PROCESS-RELEASED.                                           APKUP268
00196                                                                   APKUP268
00197      EXEC SQL                                                     APKUP268
00198          OPEN CLAIMED_CURSOR                                      APKUP268
00199      END-EXEC.                                                    APKUP268
00200                                                                   APKUP268
00201      EVALUATE TRUE                                                APKUP268
00202          WHEN SQLCODE = ZERO                                      APKUP268
00203               CONTINUE                                            APKUP268
00204                                                                   APKUP268
00205          WHEN SQLWARN0 NOT = SPACES                               APKUP268
00206          WHEN SQLCODE  NOT = ZERO                                 APKUP268
00207              MOVE '1000-PROCESS-RELEASED' TO AA-PARAGRAPH-NAME    APKUP268
00208              MOVE 'UNSUCCESSFUL OPEN ON CLAIMED_CURSOR'           APKUP268
00209                                           TO AA-DB2-OPERATION     APKUP268
00210              MOVE 'TPRCCLM'               TO AA-DB2-TABLE-1       APKUP268
00211              PERFORM 9999-SQL-ERROR                               APKUP268
00212      END-EVALUATE.                                                APKUP268
00213                                                                   APKUP268
00214      MOVE 'Y'                    TO PS-PRCCLM-SWITCH              APKUP268
00215                                                                   APKUP268
00216      PERFORM 9100-FETCH-CLAIMED-CURSOR.                           APKUP268
00217                                                                   APKUP268
00218      PERFORM 2000-DELETE-CLAIMED                                  APKUP268
00219             UNTIL PS-NO-MORE-PRCCLM.                              APKUP268
00220                                                                   APKUP268
00221      EXEC SQL                                                     APKUP268
00222          CLOSE CLAIMED_CURSOR                                     APKUP268
00223      END-EXEC.                                                    APKUP268
00224                                                                   APKUP268
00225      EVALUATE TRUE                                                APKUP268
00226          WHEN SQLCODE = ZERO                                      APKUP268
00227               CONTINUE                                            APKUP268
00228                                                                   APKUP268
00229          WHEN SQLWARN0 NOT = SPACES                               APKUP268
00230          WHEN SQLCODE  NOT = ZERO                                 APKUP268
00231              MOVE '1000-PROCESS-RELEASED' TO AA-PARAGRAPH-NAME    APKUP268
00232              MOVE 'UNSUCCESSFUL CLOSE ON CLAIMED_CURSOR'          APKUP268
00233                                           TO AA-DB2-OPERATION     APKUP268
00234              MOVE 'TPRCCLM'               TO AA-DB2-TABLE-1       APKUP268
00235              PERFORM 9999-SQL-ERROR                               APKUP268
00236      END-EVALUATE.                                                APKUP268
00237                                                                   APKUP268
00238      EXEC SQL                                                     APKUP268
00239          DELETE FROM TPOSTRR                                      APKUP268
00240              WHERE CURRENT OF RELEASED_CURSOR                     APKUP268
00241      END-EXEC.                                                    APKUP268
00242                                                                   APKUP268
00243      EVALUATE TRUE                                                APKUP268
00244          WHEN SQLCODE = ZERO                                      APKUP268
00245               CONTINUE                                            APKUP268
00246                                                                   APKUP268
00247          WHEN SQLWARN0 NOT = SPACES                               APKUP268
00248          WHEN SQLCODE  NOT = ZERO                                 APKUP268
00249              DISPLAY 'TPOSTRR DELETE FAILED'                      APKUP268
00250              DISPLAY 'PO_NBR         = ' POSTRR-PO-NBR            APKUP268
00251              DISPLAY 'STR_NBR        = ' POSTRR-STR-NBR           APKUP268
00252      END-EVALUATE.                                                APKUP268
00253                                                                   APKUP268
00254      PERFORM 9000-FETCH-RELEASED-CURSOR.                          APKUP268
00255      EJECT                                                        APKUP268
00256  2000-DELETE-CLAIMED.                                             APKUP268
00257                                                                   APKUP268
00258      EXEC SQL                                                     APKUP268
00259          DELETE FROM TPRCCLM                                      APKUP268
00260              WHERE CURRENT OF CLAIMED_CURSOR                      APKUP268
00261      END-EXEC.                                                    APKUP268
00262                                                                   APKUP268
00263      EVALUATE TRUE                                                APKUP268
00264          WHEN SQLCODE = ZERO                                      APKUP268
00265               CONTINUE                                            APKUP268
00266                                                                   APKUP268
00267          WHEN SQLWARN0 NOT = SPACES                               APKUP268
00268          WHEN SQLCODE  NOT = ZERO                                 APKUP268
00269              DISPLAY 'TPRCCLM DELETE FAILED'                      APKUP268
00270              DISPLAY 'PO_NBR         = ' PRCCLM-PO-NBR            APKUP268
00271              DISPLAY 'STR_NBR        = ' PRCCLM-STR-NBR           APKUP268
00272              DISPLAY 'INVC_ID        = ' PRCCLM-INVC-ID           APKUP268
00273              DISPLAY 'PO_LNE_NBR     = ' PRCCLM-PO-LNE-NBR        APKUP268
00274              DISPLAY 'PRCCLM_SEQ_NBR = ' PRCCLM-PRCCLM-SEQ-NBR    APKUP268
00275      END-EVALUATE.                                                APKUP268
00276                                                                   APKUP268
00277      PERFORM 9100-FETCH-CLAIMED-CURSOR.                           APKUP268
00278      EJECT                                                        APKUP268
00279  9000-FETCH-RELEASED-CURSOR.                                      APKUP268
00280                                                                   APKUP268
00281      EXEC SQL                                                     APKUP268
00282         FETCH RELEASED_CURSOR                                     APKUP268
00283             INTO   :POSTRR-PO-NBR                                 APKUP268
00284                   ,:POSTRR-STR-NBR                                APKUP268
00285                   ,:POSTRR-PAYT-RLSE-TMST                            CL**6
00286      END-EXEC.                                                    APKUP268
00287                                                                   APKUP268
00288      EVALUATE TRUE                                                APKUP268
00289          WHEN SQLCODE = ZERO                                      APKUP268
00290              ADD 1 TO PV-POSTRR-COUNT                             APKUP268
00291          WHEN SQLCODE = +100                                      APKUP268
00292              MOVE 'N'            TO PS-POSTRR-SWITCH              APKUP268
00293          WHEN OTHER                                               APKUP268
00294              MOVE '9000-FETCH-RELEASED-CURSOR'                    APKUP268
00295                                           TO AA-PARAGRAPH-NAME    APKUP268
00296              MOVE 'UNSUCCESSFUL FETCH ON RELEASED_CURSOR'         APKUP268
00297                                           TO AA-DB2-OPERATION     APKUP268
00298              MOVE 'TPOSTRR'               TO AA-DB2-TABLE-1       APKUP268
00299              PERFORM 9999-SQL-ERROR                               APKUP268
00300      END-EVALUATE.                                                APKUP268
00301      EJECT                                                        APKUP268
00302  9100-FETCH-CLAIMED-CURSOR.                                       APKUP268
00303                                                                   APKUP268
00304      EXEC SQL                                                     APKUP268
00305         FETCH CLAIMED_CURSOR                                      APKUP268
00306             INTO   :PRCCLM-PO-NBR                                 APKUP268
00307                   ,:PRCCLM-STR-NBR                                APKUP268
00308                   ,:PRCCLM-INVC-ID                                APKUP268
00309                   ,:PRCCLM-PO-LNE-NBR                             APKUP268
00310                   ,:PRCCLM-PRCCLM-SEQ-NBR                         APKUP268
00311      END-EXEC.                                                    APKUP268
00312                                                                   APKUP268
00313      EVALUATE TRUE                                                APKUP268
00314          WHEN SQLCODE = ZERO                                      APKUP268
00315              ADD 1 TO PV-PRCCLM-COUNT                             APKUP268
00316          WHEN SQLCODE = +100                                      APKUP268
00317              MOVE 'N'            TO PS-PRCCLM-SWITCH              APKUP268
00318          WHEN OTHER                                               APKUP268
00319              MOVE '9100-FETCH-CLAIMED-CURSOR'                     APKUP268
00320                                           TO AA-PARAGRAPH-NAME    APKUP268
00321              MOVE 'UNSUCCESSFUL FETCH ON CLAIMED_CURSOR'          APKUP268
00322                                           TO AA-DB2-OPERATION     APKUP268
00323              MOVE 'TPRCCLM'               TO AA-DB2-TABLE-1       APKUP268
00324              PERFORM 9999-SQL-ERROR                               APKUP268
00325      END-EVALUATE.                                                APKUP268
00326      EJECT                                                        APKUP268
00327                                                                   APKUP268
00328  9999-SQL-ERROR.                                                  APKUP268
00329                                                                   APKUP268
00330      DISPLAY AA-ABEND-LIT.                                        APKUP268
00331      DISPLAY AA-DB2-ERROR-LIT.                                    APKUP268
00332      DISPLAY AA-PROGRAM-LIT.                                      APKUP268
00333      DISPLAY AA-PARAGRAPH-LIT.                                    APKUP268
00334      DISPLAY AA-DB2-OPERATION-LIT.                                APKUP268
00335      DISPLAY AA-DB2-TABLE-1.                                      APKUP268
00336      MOVE +4013 TO ABEND-CODE.                                    APKUP268
00337                                                                   APKUP268
00338      COPY DPPD004.                                                APKUP268
00339                                                                   APKUP268
00340      CALL 'ILBOABN0' USING ABEND-CODE.                            APKUP268
00341          EJECT                                                    APKUP268
