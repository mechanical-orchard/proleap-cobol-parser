      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             TXKUT056.                                        
       AUTHOR.                 LENIN JOSEPH - COGNIZANT                         
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JAN 2014.                                        
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                                         
      *  THIS PROGRAM WILL ROLL UP THE TAXABLE AMT AND TAX AMT FOR BOTH         
      *  SFS STORE AND ZIP FILE. THIS PROGRAM ALSO EXTRACTS THE STATE           
      *  CODE AND STORE LOCATION NAME FROM XST_LOC TABLE FOR SFS STORE          
      *  FILE.                                                                  
      *                                                                         
      *   INPUT : INP01 -  SFS STORE INPUT FILE                                 
      *   INPUT : INP02 -  SFS ZIP CODE INPUT FILE                              
      *   INPUT : INP03 -  FISCAL MONTH END DATE                                
      *                                                                         
      *   OUTPUT: OUT01 -  ROLLED UP SFS STORE OUTPUT FILE                      
      *   OUTPUT: OUT02 -  ROLLED UP SFS ZIP CODE OUTPUT FILE                   
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PBI   DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      *  *****   01-29-2014  INITIAL CREATION - COGNIZANT                       
      ******************************************************************        
      *                                                                         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SFS-STR-INPUT                                                 
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT SFS-ZIP-INPUT                                                 
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT POS-DATE-CARD-FILE                                            
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT SFS-STR-OUTPUT                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT SFS-ZIP-OUTPUT                                                
               ASSIGN TO OUT02.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SFS-STR-INPUT                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  SFS-STR-INPUT-REC                     PIC  X(200).                   
                                                                                
       FD  SFS-ZIP-INPUT                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  SFS-ZIP-INPUT-REC                     PIC  X(200).                   
                                                                                
       FD  POS-DATE-CARD-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  POS-DATE-CARD-RECORD                  PIC  X(80).                    
                                                                                
       FD  SFS-STR-OUTPUT                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
                                                                                
       01  SFS-STR-OUTPUT-REC                    PIC  X(80).                    
                                                                                
       FD  SFS-ZIP-OUTPUT                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0  RECORDS.                                           
                                                                                
       01  SFS-ZIP-OUTPUT-REC                    PIC  X(080).                   
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************        
      *          P R O G R A M     A B E N D     C O D E               *        
      ******************************************************************        
       01  ABEND-CODE                            PIC S9(04) COMP.               
           88  MISSING-SFS-STR-INPUT-REC               VALUE +4002.             
           88  AC-CONTROL-CARD-ERROR                   VALUE +4003.             
           88  AC-DB2-ERROR                            VALUE +4013.             
           88  AC-CALENDAR-ROUTINE-ERROR               VALUE +4019.             
                                                                                
      ******************************************************************        
      *              P R O G R A M     C O N S T A N T S               *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                   PIC  X(08) VALUE               
                                                           'TXKUT056'.          
           05  PC-ONE                            PIC  9(01) VALUE 1.            
           05  PC-MINUS-ONE                      PIC S9(01) VALUE -1.           
                                                                                
      ******************************************************************        
      *               P R O G R A M     C O U N T E R S                *        
      ******************************************************************        
       01  PC-PROGRAM-COUNTERS.                                                 
           05  PC-STR-INPUT-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-ZIP-INPUT-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-STR-VALID-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-ZIP-VALID-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-STR-INVLD-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-ZIP-INVLD-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-STR-OUTPT-REC-CNT              PIC  9(05) VALUE 0.            
           05  PC-ZIP-OUTPT-REC-CNT              PIC  9(05) VALUE 0.            
                                                                                
      ******************************************************************        
      *               P R O G R A M     D I S P L A Y S                *        
      ******************************************************************        
       01  PC-PROGRAM-DISPLAYS.                                                 
           05  PC-STR-IN-DIS                     PIC  Z(04)9(01).               
           05  PC-ZIP-IN-DIS                     PIC  Z(04)9(01).               
           05  PC-STR-VAL-DIS                    PIC  Z(04)9(01).               
           05  PC-ZIP-VAL-DIS                    PIC  Z(04)9(01).               
           05  PC-STR-INV-DIS                    PIC  Z(04)9(01).               
           05  PC-ZIP-INV-DIS                    PIC  Z(04)9(01).               
           05  PC-STR-OUT-DIS                    PIC  Z(04)9(01).               
           05  PC-ZIP-OUT-DIS                    PIC  Z(04)9(01).               
      ******************************************************************        
      *          P R O G R A M     E R R O R     M E S S A G E         *        
      ******************************************************************        
                                                                                
      ******************************************************************        
      *              P R O G R A M     L A Y OU T S                    *        
      ******************************************************************        
       01  PL-PROGRAM-LAYOUT.                                                   
           05  PL-OUTSTR-SFS-REC.                                               
               10  PL-STR-NBR                    PIC  9(04).                    
               10  PL-STR-NAME                   PIC  X(10).                    
               10  PL-STR-STATE-CODE             PIC  X(02).                    
               10  PL-STR-TAXABLE-AMT            PIC  S9(7)V99 COMP-3.          
               10  PL-STR-TAX-AMT                PIC  S9(7)V99 COMP-3.          
               10  FILLER                        PIC  X(53).                    
           05  PL-OUTZIP-SFS-REC.                                               
               10  PL-ZIP-CD                     PIC  X(05).                    
               10  PL-ZIP-STATE-CODE             PIC  X(02).                    
               10  PL-ZIP-TAXABLE-AMT            PIC  S9(7)V99 COMP-3.          
               10  PL-ZIP-TAX-AMT                PIC  S9(7)V99 COMP-3.          
               10  FILLER                        PIC  X(63).                    
           05  CC-DATE-CARD.                                                    
               10  CC-DATE-CD-CCYY               PIC  X(04) VALUE               
                                                           SPACES.              
               10  FILLER                        PIC  X(01) VALUE '-'.          
               10  CC-DATE-CD-MM                 PIC  X(02) VALUE               
                                                           SPACES.              
               10  FILLER                        PIC  X(01) VALUE '-'.          
               10  CC-DATE-CD-DD                 PIC  X(02) VALUE               
                                                           SPACES.              
               10  FILLER                        PIC  X(70) VALUE               
                                                           SPACES.              
      ******************************************************************        
      *              P R O G R A M     S W I T C H E S                 *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STR-FLSW                PIC X(01) VALUE 'Y'.           
               88  PS-END-OF-STR-YES                       VALUE 'Y'.           
               88  PS-END-OF-STR-NO                        VALUE 'N'.           
           05  PS-END-OF-ZIP-FLSW                PIC X(01) VALUE 'Y'.           
               88  PS-END-OF-ZIP-YES                       VALUE 'Y'.           
               88  PS-END-OF-ZIP-NO                        VALUE 'N'.           
           05  PS-END-OF-DATE-FILE               PIC X(01) VALUE 'Y'.           
               88  PS-END-OF-DATE-FILE-YES                 VALUE 'Y'.           
               88  PS-END-OF-DATE-FILE-NO                  VALUE 'N'.           
                                                                                
      ******************************************************************        
      *              P R O G R A M     V A R I A B L E S               *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-OPERATION-MSG-1                PIC X(50)                      
                                                    VALUE SPACES.               
           05  PV-OPERATION-MSG-2                PIC X(50)                      
                                                    VALUE SPACES.               
           05  PV-DB2-OPERATION                  PIC X(50)                      
                                                    VALUE SPACES.               
           05  PV-PARAGRAPH-NAME                 PIC X(30)                      
                                                    VALUE SPACES.               
           05  PV-STORE-NBR                      PIC S9(04) COMP                
                                                    VALUE ZERO.                 
           05  PV-PSTIVE-TAXABLE-AMT             PIC S9(07)V99 COMP-3           
                                                    VALUE ZERO.                 
           05  PV-PSTIVE-TAX-AMT                 PIC S9(07)V99 COMP-3           
                                                    VALUE ZERO.                 
           05  PV-NGTIVE-TAXABLE-AMT             PIC S9(07)V99 COMP-3           
                                                    VALUE ZERO.                 
           05  PV-NGTIVE-TAX-AMT                 PIC S9(07)V99 COMP-3           
                                                    VALUE ZERO.                 
           05  PV-ST-CDE                         PIC X(02).                     
           05  PV-CURR-ZIP-NBR                   PIC X(05).                     
           05  PV-PREV-ZIP-NBR                   PIC X(05).                     
           05  PV-CURR-STR-NBR                   PIC 9(04).                     
           05  PV-PREV-STR-NBR                   PIC 9(04).                     
           05  PV-08BYT-CONTROL-DT.                                             
               10 PV-08BYT-CONT-CCYY             PIC X(04).                     
               10 PV-08BYT-CONT-MM               PIC X(02).                     
               10 PV-08BYT-CONT-DD               PIC X(02).                     
           05  PV-COMP-START-DTE.                                               
               10 PV-COMP-START-CCYY             PIC X(04).                     
               10 FILLER                         PIC X(01) VALUE '-'.           
               10 PV-COMP-START-MM               PIC X(02).                     
               10 FILLER                         PIC X(01) VALUE '-'.           
               10 PV-COMP-START-DD               PIC X(02).                     
           05  PV-COMP-END-DTE.                                                 
               10 PV-COMP-END-CCYY               PIC X(04).                     
               10 FILLER                         PIC X(01) VALUE '-'.           
               10 PV-COMP-END-MM                 PIC X(02).                     
               10 FILLER                         PIC X(01) VALUE '-'.           
               10 PV-COMP-END-DD                 PIC X(02).                     
                                                                                
      ******************************************************************        
      *             C O P Y B O O K      L A Y O U T S                 *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------               
      * INPUT RECORD LAYOUT FOR SFS STORE LEVEL TAX  RECORD                     
      *----------------------------------------------------------               
           COPY TXRD006.                                                        
                                                                                
      *----------------------------------------------------------               
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)                     
      *----------------------------------------------------------               
                                                                                
           COPY DPWS500.                                                        
                                                                                
      *----------------------------------------------------------               
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                                    
      *----------------------------------------------------------               
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------               
      *  DB2 COMMUNICATION AREA.                                                
      *----------------------------------------------------------               
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------               
      *  DB2 TABLE XST_LOC.                                                     
      *----------------------------------------------------------               
                                                                                
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *             C U R S O R      D E F I N I T I O N S            *         
      *****************************************************************         
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-PARAGRAPH.                                                 
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PRCSS-STR-INFO      UNTIL PS-END-OF-STR-YES.            
           PERFORM 3000-PRCSS-ZIP-INFO      UNTIL PS-END-OF-ZIP-YES.            
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      *****************************************************************         
      * 1000-INITIALIZE.                                                        
      *                                                                         
      * INITALIZE COUNTER, SWITCHES AND LAYOUTS. OPEN THE FILES                 
      * AND MAKE IT READY FOR FURTHER PROCESSING. CALLS THE DATE ROUTINE        
      * TO GET THE FISCAL MONTH START DATE AND FISCAL MONTH END DATE.           
      *                                                                         
      *                                                                         
      * PARAGRAPH CALLED FROM :- 0000-MAINLINE-PARAGRAPH                        
      * PARAGRAPH CALLED TO   :- 1100-PROCESS-DATE-CARD                         
      *                          8000-READ-INPUT-STR-FILE                       
      *                          9999-ABEND-PROCESS                             
      *****************************************************************         
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  SFS-STR-INPUT                                            
                       SFS-ZIP-INPUT                                            
                       POS-DATE-CARD-FILE                                       
                OUTPUT SFS-STR-OUTPUT                                           
                       SFS-ZIP-OUTPUT.                                          
                                                                                
           INITIALIZE TX006-TAX-AUDIT-RECORD                                    
                      PS-PROGRAM-SWITCHES                                       
                      PL-PROGRAM-LAYOUT                                         
                      PC-PROGRAM-DISPLAYS                                       
                      PC-PROGRAM-COUNTERS                                       
                      PV-PROGRAM-VARIABLES.                                     
                                                                                
           SET PS-END-OF-STR-NO             TO TRUE.                            
           SET PS-END-OF-ZIP-NO             TO TRUE.                            
                                                                                
           PERFORM 1100-PROCESS-DATE-CARD.                                      
           PERFORM 8000-READ-INPUT-STR-FILE.                                    
           IF  PS-END-OF-STR-YES                                                
               MOVE '1000-INITIALIZE'       TO PV-PARAGRAPH-NAME                
               MOVE ' SFS STORE INPUT FILE IS EMPTY '                           
                                            TO PV-OPERATION-MSG-1               
              PERFORM 9999-ABEND-PROCESS                                        
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 1100-PROCESS-DATE-CARD.                                                 
      *                                                                         
      * CALLS THE DATE ROUTINE WITH FISCAL END DATE AND RETRIVES THE            
      * FISCAL MONTH START DATE AND FISCAL MONTH END DATE.                      
      *                                                                         
      * PARAGRAPH CALLED FROM :- 1000-INITIALIZE                                
      * PARAGRAPH CALLED TO   :- 1100-PROCESS-DATE-CARD                         
      *                          8200-READ-CONTROL-CARD                         
      *                          9999-ABEND-PROCESS                             
      *****************************************************************         
       1100-PROCESS-DATE-CARD.                                                  
                                                                                
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                 
           SET PS-END-OF-DATE-FILE-NO       TO TRUE                             
                                                                                
           PERFORM 8200-READ-CONTROL-CARD                                       
                                                                                
           IF  PS-END-OF-DATE-FILE-YES                                          
               MOVE '1100-PROCESS-DATE-CARD'                                    
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'DATE CARD MISSING      '                                   
                                            TO PV-OPERATION-MSG-1               
               PERFORM 9999-ABEND-PROCESS                                       
           END-IF.                                                              
           MOVE CC-DATE-CD-CCYY             TO PV-08BYT-CONT-CCYY.              
           MOVE CC-DATE-CD-MM               TO PV-08BYT-CONT-MM  .              
           MOVE CC-DATE-CD-DD               TO PV-08BYT-CONT-DD  .              
                                                                                
           SET DPG51-FISCAL-454-CALENDAR    TO TRUE.                            
           SET DPG52-LK-DTE-ISO             TO TRUE.                            
                                                                                
           MOVE PV-08BYT-CONTROL-DT         TO DPG52-DB2-ISO-DATE.              
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
                                                   DPG54 DPG55 DPG56.           
           IF  DPG54-SEVERE-ERROR                                               
               DISPLAY '*********************************'                      
               DISPLAY '** ABEND FROM CALL SUB         **'                      
               DISPLAY '** PROGRAM = TXKUT056          **'                      
               DISPLAY '** DATE = ' CC-DATE-CARD '     **'                      
               DISPLAY '*********************************'                      
               DISPLAY DPG54-ERROR-MESSAGE                                      
               SET AC-CALENDAR-ROUTINE-ERROR                                    
                                            TO TRUE                             
               PERFORM 9999-ABEND-PROCESS                                       
           ELSE                                                                 
               IF  DPG54-DATE-INVALID                                           
                   DISPLAY '****************************'                       
                   DISPLAY '** ABEND INVALID END DATE **'                       
                   DISPLAY '** PROGRAM = TXKUT056     **'                       
                   DISPLAY '** DATE = ' CC-DATE-CARD '**'                       
                   DISPLAY '****************************'                       
                   DISPLAY DPG54-ERROR-MESSAGE                                  
                   SET AC-CONTROL-CARD-ERROR                                    
                                            TO TRUE                             
                   PERFORM 9999-ABEND-PROCESS                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DPG56-FIRST-FP-GREG-CC-YR   TO PV-COMP-START-CCYY.              
           MOVE DPG56-FIRST-FP-GREG-CC-MO   TO PV-COMP-START-MM.                
           MOVE DPG56-FIRST-FP-GREG-CC-DY   TO PV-COMP-START-DD.                
           MOVE DPG56-LAST-FP-GREG-CC-YR    TO PV-COMP-END-CCYY.                
           MOVE DPG56-LAST-FP-GREG-CC-MO    TO PV-COMP-END-MM.                  
           MOVE DPG56-LAST-FP-GREG-CC-DY    TO PV-COMP-END-DD.                  
           DISPLAY 'THE STARTING DATE OF FISCAL MONTH :- '                      
                    PV-COMP-START-DTE.                                          
           DISPLAY 'THE ENDING DATE OF FISCAL MONTH   :- '                      
                    PV-COMP-END-DTE.                                            
                                                                                
      *****************************************************************         
      * 2000-PRCSS-STR-INFO.                                                    
      *                                                                         
      * MAIN PROCESSING PARA FOR SFS STORE RECORDS. READS THE SFS STORE         
      * FILE AND CHECKS IF THE PROCESSING DATE IS GREATER THAN FISCAL           
      * MONTH START DATE AND LESSER THAN FISCAL MONTH END DATE. IF THE          
      * ABOVE CHECK IS SATISFIED FURTHER PROCESSING FOR ROLLING THE             
      * TAXABLE AMT AND TAX AMT ON STORE LEVEL BASED ON CREDIT                  
      * INDICATOR IS DONE.                                                      
      *                                                                         
      * PARAGRAPH CALLED FROM :- 0000-MAINLINE-PARAGRAPH                        
      * PARAGRAPH CALLED TO   :- 2100-PRCS-VALID-STR-REC                        
      *                          2200-POPULATE-STR-OUTPUT                       
      *                          8000-READ-INPUT-STR-FILE                       
      *                          8100-READ-INPUT-ZIP-FILE                       
      *                          8300-WRITE-STR-OUTPUT                          
      *                          9999-ABEND-PROCESS                             
      *****************************************************************         
       2000-PRCSS-STR-INFO.                                                     
                                                                                
           IF  TX006-PROCESS-DTE  >=  PV-COMP-START-DTE  AND                    
               TX006-PROCESS-DTE  <=  PV-COMP-END-DTE                           
               PERFORM 2100-PRCS-VALID-STR-REC                                  
               ADD  PC-ONE                  TO PC-STR-VALID-REC-CNT             
           ELSE                                                                 
               ADD  PC-ONE                  TO PC-STR-INVLD-REC-CNT             
           END-IF.                                                              
                                                                                
           PERFORM 8000-READ-INPUT-STR-FILE.                                    
                                                                                
           IF  PS-END-OF-STR-YES                                                
               PERFORM 2200-POPULATE-STR-OUTPUT                                 
               PERFORM 8300-WRITE-STR-OUTPUT                                    
                                                                                
               PERFORM 8100-READ-INPUT-ZIP-FILE                                 
               IF PS-END-OF-ZIP-YES                                             
                  MOVE '1000-INITIALIZE'    TO PV-PARAGRAPH-NAME                
                  MOVE ' SFS ZIP CODE INPUT FILE IS EMPTY '                     
                                            TO PV-OPERATION-MSG-1               
                  PERFORM 9999-ABEND-PROCESS                                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2100-PRCS-VALID-STR-REC.                                                
      *                                                                         
      * IF CURRENT STORE NUMBER IS 'EQUAL' TO PREVIOUS STORE NUMBER,            
      * THEN, ADD THE TAXABLE AMT AND TAX AMT ON STORE LEVEL BASED ON           
      * CREDIT INDICATOR . IF THE CURRENT STORE NUMBER IS 'NOT EQUAL' TO        
      * THEN CONSOLIDATE THE TOTALS FOR PREVIOUS STORE NUMBER AND               
      * WRITE THE OUTPUT FILE. ALSO GET THE STATE CODE AND LOCATION             
      * NAME FOR CURRENT STORE NUMBER FROMM XST_LOC TABLE AND POPULATE          
      * OUTPUT RECORD.                                                          
      *                                                                         
      * PARAGRAPH CALLED FROM :- 2000-PRCSS-STR-INFO                            
      * PARAGRAPH CALLED TO   :- 2200-POPULATE-STR-OUTPUT                       
      *                          2300-PRCS-NEW-STR                              
      *                          5000-COMP-AMT-FLDS                             
      *                          8300-WRITE-STR-OUTPUT                          
      *****************************************************************         
       2100-PRCS-VALID-STR-REC.                                                 
                                                                                
           IF  PV-CURR-STR-NBR  NOT EQUAL  PV-PREV-STR-NBR                      
               IF  PC-STR-VALID-REC-CNT > PC-ONE                                
                   PERFORM 2200-POPULATE-STR-OUTPUT                             
                   PERFORM 8300-WRITE-STR-OUTPUT                                
               END-IF                                                           
                                                                                
               PERFORM 2300-PRCS-NEW-STR                                        
               MOVE PV-CURR-STR-NBR         TO PV-PREV-STR-NBR                  
           ELSE                                                                 
               PERFORM 5000-COMP-AMT-FLDS                                       
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 2200-POPULATE-STR-OUTPUT.                                               
      *                                                                         
      * CONSOLIDATE THE TAXABLE AMOUNT AND TAX AMOUNT AND OUTPUT LAYOUT         
      *                                                                         
      * PARAGRAPH CALLED FROM :- 2000-PRCSS-STR-INFO                            
      *                          2100-PRCS-VALID-STR-REC                        
      * PARAGRAPH CALLED TO   :- NIL                                            
      *****************************************************************         
       2200-POPULATE-STR-OUTPUT.                                                
                                                                                
           MULTIPLY PC-MINUS-ONE            BY  PV-NGTIVE-TAXABLE-AMT.          
           MULTIPLY PC-MINUS-ONE            BY  PV-NGTIVE-TAX-AMT.              
           COMPUTE PL-STR-TAXABLE-AMT       = PV-PSTIVE-TAXABLE-AMT +           
                                              PV-NGTIVE-TAXABLE-AMT.            
           COMPUTE PL-STR-TAX-AMT           = PV-PSTIVE-TAX-AMT +               
                                              PV-NGTIVE-TAX-AMT.                
                                                                                
      *****************************************************************         
      * 2300-PRCS-NEW-STR.                                                      
      *                                                                         
      * INITIALIZE THE WORKING STORAGE AMOUNT VARIABLE AND OUTPUT LAYOUT        
      * MOVE THE NEW STORE NUMBER TO OUTPUT FIELDS AND GET THE STATE            
      * CODE AND LOCATION FROM XST_LOC TABLE AND POPULATE THE OUTPUT            
      * FIELDS.                                                                 
      *                                                                         
      * PARAGRAPH CALLED FROM :- 2100-PRCS-VALID-STR-REC                        
      * PARAGRAPH CALLED TO   :- 5000-COMP-AMT-FLDS                             
      *                          7000-SELECT-STATE                              
      *****************************************************************         
       2300-PRCS-NEW-STR.                                                       
                                                                                
             INITIALIZE PL-OUTSTR-SFS-REC.                                      
             MOVE ZEROS                     TO PV-PSTIVE-TAXABLE-AMT.           
             MOVE ZEROS                     TO PV-PSTIVE-TAX-AMT.               
             MOVE ZEROS                     TO PV-NGTIVE-TAXABLE-AMT.           
             MOVE ZEROS                     TO PV-NGTIVE-TAX-AMT.               
                                                                                
             MOVE TX006-STR-NBR             TO PL-STR-NBR.                      
             PERFORM 5000-COMP-AMT-FLDS.                                        
                                                                                
             PERFORM 7000-SELECT-STATE.                                         
                                                                                
      *****************************************************************         
      * 3000-PRCSS-ZIP-INFO.                                                    
      *                                                                         
      * MAIN PROCESSING PARA FOR SFS ZIP RECORDS. READS THE SFS ZIP CODE        
      * FILE AND CHECKS IF THE PROCESSING DATE IS GREATER THAN FISCAL           
      * MONTH START DATE AND LESSER THAN FISCAL MONTH END DATE. IF THE          
      * ABOVE CHECK IS SATISFIED FURTHER PROCESSING FOR ROLLING THE             
      * TAXABLE AMT AND TAX AMT ON ZIP CODE LEVEL BASED ON CREDIT               
      * INDICATOR IS DONE.                                                      
      *                                                                         
      * PARAGRAPH CALLED FROM :- 0000-MAINLINE-PARAGRAPH                        
      * PARAGRAPH CALLED TO   :- 3100-PRCS-VALID-ZIP-REC                        
      *                          3200-POPULATE-ZIP-OUTPUT                       
      *                          8100-READ-INPUT-ZIP-FILE                       
      *                          8400-WRITE-ZIP-OUTPUT                          
      *                          9999-ABEND-PROCESS                             
      *****************************************************************         
       3000-PRCSS-ZIP-INFO.                                                     
                                                                                
           IF  TX006-PROCESS-DTE >= PV-COMP-START-DTE AND                       
               TX006-PROCESS-DTE <= PV-COMP-END-DTE                             
               PERFORM 3100-PRCS-VALID-ZIP-REC                                  
               ADD  PC-ONE                  TO PC-ZIP-VALID-REC-CNT             
           ELSE                                                                 
               ADD  PC-ONE                  TO PC-ZIP-INVLD-REC-CNT             
           END-IF                                                               
                                                                                
           PERFORM 8100-READ-INPUT-ZIP-FILE.                                    
                                                                                
           IF  PS-END-OF-ZIP-YES                                                
               PERFORM 3200-POPULATE-ZIP-OUTPUT                                 
               PERFORM 8400-WRITE-ZIP-OUTPUT                                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3100-PRCS-VALID-ZIPCODE-REC.                                            
      * IF CURRENT ZIP CODE IS 'EQUAL' TO PREVIOUS ZIP CODE                     
      * THEN ADD THE TAXABLE AMT AND TAX AMT ON ZIP CODE LEVEL BASED ON         
      * CREDIT INDICATOR . IF THE CURRENT ZIP CODE IS 'NOT EQUAL' TO            
      * THEN CONSOLIDATE THE TOTALS FOR PREVIOUS ZIP CODE AND                   
      * WRITE THE OUTPUT FILE. ALSO GET THE STATE CODE AND LOCATION             
      * NAME FOR CURRENT ZIP CODE FROM XST_LOC TABLE AND POPULATE               
      * OUTPUT RECORD.                                                          
      * PARAGRAPH CALLED FROM :- 3000-PRCSS-ZIP-INFO                            
      * PARAGRAPH CALLED TO   :- 3200-POPULATE-ZIP-OUTPUT                       
      *                          3300-PRCS-NEW-ZIP                              
      *                          5000-COMP-AMT-FLDS                             
      *                          8400-WRITE-ZIP-OUTPUT                          
      ******************************************************************        
       3100-PRCS-VALID-ZIP-REC.                                                 
                                                                                
           IF  PV-CURR-ZIP-NBR  NOT EQUAL  PV-PREV-ZIP-NBR                      
               IF  PC-ZIP-VALID-REC-CNT > PC-ONE                                
                   PERFORM 3200-POPULATE-ZIP-OUTPUT                             
                   PERFORM 8400-WRITE-ZIP-OUTPUT                                
               END-IF                                                           
                                                                                
               PERFORM 3300-PRCS-NEW-ZIP                                        
               MOVE PV-CURR-ZIP-NBR         TO PV-PREV-ZIP-NBR                  
           ELSE                                                                 
               PERFORM 5000-COMP-AMT-FLDS                                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3200-POPULATE-ZIP-OUTPUT.                                               
      *                                                                         
      * CONSOLIDATE THE TAXABLE AMOUNT AND TAX AMOUNT AND OUTPUT LAYOUT         
      *                                                                         
      * PARAGRAPH CALLED FROM :- 3000-PRCSS-ZIP-INFO                            
      *                          3100-PRCS-VALID-ZIP-REC                        
      * PARAGRAPH CALLED TO   :- NIL                                            
      *                                                                         
      ******************************************************************        
       3200-POPULATE-ZIP-OUTPUT.                                                
                                                                                
           MULTIPLY PC-MINUS-ONE            BY  PV-NGTIVE-TAXABLE-AMT.          
           MULTIPLY PC-MINUS-ONE            BY  PV-NGTIVE-TAX-AMT.              
           COMPUTE PL-ZIP-TAXABLE-AMT  = PV-PSTIVE-TAXABLE-AMT +                
                                         PV-NGTIVE-TAXABLE-AMT.                 
           COMPUTE PL-ZIP-TAX-AMT      = PV-PSTIVE-TAX-AMT +                    
                                         PV-NGTIVE-TAX-AMT.                     
                                                                                
      ******************************************************************        
      * 3300-PRCS-NEW-ZIP.                                                      
      *                                                                *        
      * INITIALIZE THE WORKING STORAGE AMOUNT VARIABLE AND OUTPUT LAYOUT        
      * MOVE THE NEW ZIP CODE TO OUTPUT FIELDS AND GET THE STATE                
      * CODE AND LOCATION FROM XST_LOC TABLE AND POPULATE THE OUTPUT            
      * FIELDS.                                                                 
      *                                                                         
      * PARAGRAPH CALLED FROM :- 3100-PRCS-VALID-ZIP-REC                        
      * PARAGRAPH CALLED TO   :- 5000-COMP-AMT-FLDS                             
      *                          7000-SELECT-STATE                              
      ******************************************************************        
       3300-PRCS-NEW-ZIP.                                                       
                                                                                
             INITIALIZE PL-OUTZIP-SFS-REC.                                      
             MOVE ZEROS                     TO  PV-PSTIVE-TAXABLE-AMT.          
             MOVE ZEROS                     TO  PV-PSTIVE-TAX-AMT.              
             MOVE ZEROS                     TO  PV-NGTIVE-TAXABLE-AMT.          
             MOVE ZEROS                     TO  PV-NGTIVE-TAX-AMT.              
             MOVE TX006-ZIP-CDE             TO  PL-ZIP-CD.                      
             MOVE TX006-ST-CDE              TO  PL-ZIP-STATE-CODE.              
                                                                                
             PERFORM 5000-COMP-AMT-FLDS.                                        
                                                                                
      ******************************************************************        
      * BASED ON THE CREDIT INDICATOR FIELD MOVING THE TAX AMOUNT AND           
      * TAXABLE AMOUNT TO THE WORKING STORAGE VARIABLE                          
      ******************************************************************        
       5000-COMP-AMT-FLDS.                                                      
                                                                                
             IF  TX006-CREDIT-NO                                                
                 ADD  TX006-TAXABLE-AMT     TO  PV-PSTIVE-TAXABLE-AMT           
                 ADD  TX006-TX-AMT          TO  PV-PSTIVE-TAX-AMT               
             ELSE                                                               
                 ADD  TX006-TAXABLE-AMT     TO  PV-NGTIVE-TAXABLE-AMT           
                 ADD  TX006-TX-AMT          TO  PV-NGTIVE-TAX-AMT               
             END-IF.                                                            
                                                                                
      ******************************************************************        
      * RETRIEVE THE STATE CODE AND LOCATION NUMBER FROM XST_LOC TABLE          
      ******************************************************************        
       7000-SELECT-STATE.                                                       
                                                                                
           MOVE TX006-STR-NBR TO XST00001-LOC-NBR.                              
                                                                                
           EXEC SQL                                                             
             SELECT ST_CDE,                                                     
                    LOC_10_ABBR_NM                                              
              INTO :XST00001-ST-CDE,                                            
                   :XST00001-LOC-10-ABBR-NM                                     
             FROM XST_LOC                                                       
             WHERE LOC_NBR = :XST00001-LOC-NBR                                  
             WITH UR                                                            
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN ZERO                                                         
               MOVE XST00001-ST-CDE         TO  PL-STR-STATE-CODE               
               MOVE XST00001-LOC-10-ABBR-NM TO  PL-STR-NAME                     
              WHEN OTHER                                                        
               MOVE '7000-PARAGRAPH'        TO  PV-PARAGRAPH-NAME               
               MOVE SQLCODE                 TO  PV-DB2-OPERATION                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *     READ INPUT STORE FILE                                               
      * PARAGRAPH CALLED FROM :- 1000-INITIALIZE                                
      *                       :- 2000-PRCSS-STR-INFO                            
      * PARAGRAPH CALLED TO   :- NIL                                            
      ******************************************************************        
       8000-READ-INPUT-STR-FILE.                                                
                                                                                
           READ SFS-STR-INPUT INTO TX006-TAX-AUDIT-RECORD                       
              AT END                                                            
                 SET PS-END-OF-STR-YES      TO TRUE                             
              NOT AT END                                                        
                 SET PS-END-OF-STR-NO       TO TRUE                             
                 MOVE TX006-STR-NBR         TO PV-CURR-STR-NBR                  
                 ADD  PC-ONE                TO PC-STR-INPUT-REC-CNT             
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *     READ INPUT TAX FILE.                                                
      * PARAGRAPH CALLED FROM :- 2000-PRCSS-STR-INFO                            
      *                       :- 3000-PRCSS-ZIP-INFO                            
      * PARAGRAPH CALLED TO   :- NIL                                            
      ******************************************************************        
       8100-READ-INPUT-ZIP-FILE.                                                
                                                                                
           READ SFS-ZIP-INPUT INTO TX006-TAX-AUDIT-RECORD                       
              AT END                                                            
                 SET PS-END-OF-ZIP-YES      TO TRUE                             
              NOT AT END                                                        
                 SET PS-END-OF-ZIP-NO       TO TRUE                             
                 MOVE TX006-ZIP-CDE         TO PV-CURR-ZIP-NBR                  
                 ADD  PC-ONE                TO PC-ZIP-INPUT-REC-CNT             
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *     READ INPUT TAX FILE.                                                
      * PARAGRAPH CALLED FROM :- 1100-PROCESS-DATE-CARD                         
      * PARAGRAPH CALLED TO   :- NIL                                            
      ******************************************************************        
       8200-READ-CONTROL-CARD.                                                  
                                                                                
           READ POS-DATE-CARD-FILE          INTO CC-DATE-CARD                   
               AT END                                                           
                   SET PS-END-OF-DATE-FILE-YES                                  
                                            TO TRUE                             
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *     WRITE STORE NUMBER IN THE OUTPUT RECORD                             
      * PARAGRAPH CALLED FROM :- 2000-PRCSS-STR-INFO                            
      *                       :- 2100-PRCS-VALID-STR-REC                        
      * PARAGRAPH CALLED TO   :- NIL                                            
      ******************************************************************        
       8300-WRITE-STR-OUTPUT.                                                   
                                                                                
           WRITE SFS-STR-OUTPUT-REC         FROM PL-OUTSTR-SFS-REC.             
           ADD  PC-ONE                      TO PC-STR-OUTPT-REC-CNT.            
                                                                                
      ******************************************************************        
      *     WRITE OUT THE ZIP CODE IN THE RECORD                                
      * PARAGRAPH CALLED FROM :- 3000-PRCSS-ZIP-INFO                            
      *                       :- 3100-PRCS-VALID-ZIP-REC                        
      * PARAGRAPH CALLED TO   :- NIL                                            
      ******************************************************************        
       8400-WRITE-ZIP-OUTPUT.                                                   
                                                                                
           WRITE SFS-ZIP-OUTPUT-REC         FROM PL-OUTZIP-SFS-REC.             
           ADD  PC-ONE                      TO PC-ZIP-OUTPT-REC-CNT.            
                                                                                
      ******************************************************************        
      *  END OF PROCESS PARAGRAPH                                               
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           MOVE PC-STR-INPUT-REC-CNT        TO PC-STR-IN-DIS                    
           MOVE PC-ZIP-INPUT-REC-CNT        TO PC-ZIP-IN-DIS                    
           MOVE PC-STR-VALID-REC-CNT        TO PC-STR-VAL-DIS                   
           MOVE PC-ZIP-VALID-REC-CNT        TO PC-ZIP-VAL-DIS                   
           MOVE PC-STR-INVLD-REC-CNT        TO PC-STR-INV-DIS                   
           MOVE PC-ZIP-INVLD-REC-CNT        TO PC-ZIP-INV-DIS                   
           MOVE PC-STR-OUTPT-REC-CNT        TO PC-STR-OUT-DIS                   
           MOVE PC-ZIP-OUTPT-REC-CNT        TO PC-ZIP-OUT-DIS                   
           DISPLAY '**************************************************'         
           DISPLAY '**                 RUN SUMMARY                  **'         
           DISPLAY '**              PROGRAM = TXKUT056              **'         
           DISPLAY '** DATE = ' CC-DATE-CARD '                      **'         
           DISPLAY '**************************************************'         
           DISPLAY '                                 '                          
           DISPLAY 'NO. OF STORE INPUT REC READ      = ' PC-STR-IN-DIS          
           DISPLAY 'NO. OF STORE INPUT REC PROCESSED = ' PC-STR-VAL-DIS         
           DISPLAY 'NO. OF STORE INPUT REC BYPASSED  = ' PC-STR-INV-DIS         
           DISPLAY 'NO. OF STORE OUTPUT REC WRITTEN  = ' PC-STR-OUT-DIS         
           DISPLAY '                                 '                          
           DISPLAY 'NO. OF ZIP CD INPUT REC READ     = ' PC-ZIP-IN-DIS          
           DISPLAY 'NO. OF ZIP CD INPUT REC PROCESSED= ' PC-ZIP-VAL-DIS         
           DISPLAY 'NO. OF ZIP CD INPUT REC BYPASSED = ' PC-ZIP-INV-DIS         
           DISPLAY 'NO. OF ZIP CD OUTPUT REC WRITTEN = ' PC-ZIP-OUT-DIS         
                                                                                
           CLOSE  SFS-STR-INPUT                                                 
                  SFS-ZIP-INPUT                                                 
                  POS-DATE-CARD-FILE                                            
                  SFS-STR-OUTPUT                                                
                  SFS-ZIP-OUTPUT.                                               
                                                                                
      ******************************************************************        
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.        
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
                                                                                
      ******************************************************************        
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED             
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.              
      ******************************************************************        
                                                                                
           COPY DPPD004.                                                        
                                                                                
           SET AC-DB2-ERROR TO TRUE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
       9999-ABEND-PROCESS.                                                      
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                          
      ******************************************************************        
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ********************** END OF PROGRAM ****************************        
