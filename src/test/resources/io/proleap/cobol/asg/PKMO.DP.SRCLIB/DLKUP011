000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.    DLKUP011.                                                 
000400 AUTHOR.        DENNIS STEFFEN.                                           
000500 INSTALLATION.  KOHLS.                                                    
000600 DATE-WRITTEN   AUGUST 2006.                                              
000700 DATE-COMPILED.                                                           
000800                                                                          
000900**********************************************************                
001000*  THIS PROGRAM READS AN INPUT FILE CONTAINING KEYS TO BE                 
001100*  PURGED FROM DLT_P2L_CART_VLDTN TABLE.                                  
001200**********************************************************                
001300                                                                          
001400 ENVIRONMENT DIVISION.                                                    
001500 CONFIGURATION SECTION.                                                   
001600 SOURCE-COMPUTER.        IBM-3090.                                        
001700 OBJECT-COMPUTER.        IBM-3090.                                        
001800 INPUT-OUTPUT SECTION.                                                    
001900                                                                          
002000 FILE-CONTROL.                                                            
002100                                                                          
002200     SELECT PURGE-EXTRACT-FILE                                            
002300         ASSIGN TO INP01.                                                 
002400                                                                          
002500 DATA DIVISION.                                                           
002600 FILE SECTION.                                                            
002700                                                                          
002800 FD  PURGE-EXTRACT-FILE                                                   
002900     RECORDING MODE IS F                                                  
003000     LABEL RECORDS ARE STANDARD                                           
003100     BLOCK CONTAINS 0 RECORDS.                                            
003200                                                                          
003300 01  PURGE-ECXTRACT-RECORD            PIC X(68).                          
003400                                                                          
003500 WORKING-STORAGE SECTION.                                                 
003600                                                                          
003700     COPY DLRD010.                                                        
003800                                                                          
003900     COPY DPWS004.                                                        
004000     COPY SQLCA2.                                                         
004100                                                                          
004200     EXEC SQL                                                             
004300          INCLUDE SQLCA                                                   
004400     END-EXEC.                                                            
004500                                                                          
004600*  DLT_P2L_CART_VLDTN DCLGEN                                              
004700     EXEC SQL                                                             
004800          INCLUDE DLT00010                                                
004900     END-EXEC.                                                            
005000                                                                          
005100*    CHECKPOINT RESTART CONTROLS                                          
005200     EXEC SQL                                                             
005300          INCLUDE TCKRSTCN                                                
005400     END-EXEC.                                                            
005500                                                                          
005600 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
005700                                                   COMP SYNC.             
005800                                                                          
005900 01  DATABASE-KEYS.                                                       
006000     05  DK-USR-ID                   PIC X(08).                           
006100     05  DK-P2L-CART-ID              PIC X(22).                           
           05  DK-TMP-USR-ID               PIC X(08).                           
                                                                                
  6200                                                                          
006300 01  PS-PROGRAM-SWITCHES.                                                 
006400     05  PS-EOF-SW                    PIC  X(01) VALUE 'N'.               
006500         88  PS-EOF                               VALUE 'Y'.              
006600         88  PS-NOT-EOF                           VALUE 'N'.              
006700     05  PS-DELETE-SUCCESSFUL-SW      PIC  X(01)  VALUE 'N'.              
006800         88  PS-DELETE-SUCCESSFUL                 VALUE 'Y'.              
006900         88  PS-DELETE-NOT-SUCCESSFUL             VALUE 'N'.              
007000                                                                          
007100 01  PC-PROGRAM-CONSTANTS.                                                
007200     05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
007300         'DLKUP011'.                                                      
007400     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
007500                                           COMP.                          
007600                                                                          
007700 01  PA-PROGRAM-ACCUMULATORS COMP-3.                                      
007800     05  PA-COMMIT-COUNT             PIC S9(09).                          
007900     05  PA-COMMIT-COUNTER           PIC S9(09).                          
008000     05  PA-COMMIT-TOTAL             PIC S9(09).                          
008100     05  PA-RECS-READ                PIC S9(09).                          
008200     05  PA-VLDTN-DELETES            PIC S9(09).                          
008300     05  PA-VLDTN-DEL-FAIL           PIC S9(09).                          
008400     05  PA-VLDTN-NOT-FOUND          PIC S9(09).                          
008500                                                                          
008600 01  PV-PROGRAM-VARIABLES.                                                
008700     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
008800     05  PV-DB2-OPERATION-ATTEMPTED  PIC X(30) VALUE SPACES.              
008900     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
009000     05  PV-DISPLAY-NBR              PIC ZZZ,ZZZ,ZZ9.                     
009100     05  PV-EVNT-EXPRT-ID            PIC S9(9) COMP                       
009200                                         VALUE ZERO.                      
009300     05  PV-RETRY-COUNTER            PIC S9(03) COMP-3                    
009400                                          VALUE ZERO.                     
009500                                                                          
009600 01  PV-COMMIT-INFO.                                                      
009700     05  FILLER                      PIC X(10) VALUE ' KEY:'.             
009800     05  PV-COMMIT-KEY.                                                   
009900         10  PV-COMMIT-USR-ID        PIC X(08) VALUE SPACE.               
010000         10  FILLER                  PIC X(01) VALUE SPACE.               
010100         10  PV-COMMIT-P2L-CART      PIC X(22) VALUE SPACE.               
010200/                                                                         
010300 PROCEDURE DIVISION.                                                      
010400                                                                          
010500     PERFORM 1000-INITIALIZE.                                             
010600                                                                          
010700     PERFORM 2000-PROCESS-INPUT                                           
010800       UNTIL PS-EOF.                                                      
010900                                                                          
011000     PERFORM 9000-QUIT.                                                   
011100                                                                          
011200     GOBACK.                                                              
011300                                                                          
011400 1000-INITIALIZE.                                                         
011500                                                                          
011600     DISPLAY 'STARTING PROGRAM DLKUP011'                                  
011700                                                                          
011800     OPEN INPUT PURGE-EXTRACT-FILE                                        
011900                                                                          
012000     INITIALIZE PA-PROGRAM-ACCUMULATORS                                   
012100                                                                          
012200     PERFORM 8000-READ-INPUT.                                             
012300                                                                          
012400     IF PS-EOF                                                            
012500         DISPLAY 'NO RECORDS TO PURGE'                                    
012600         DISPLAY 'PROGRAM DLKUP011 IS ENDING NOW'                         
012700         DISPLAY 'GOODBYE!!'                                              
012800     ELSE                                                                 
012900         PERFORM 7000-SELECT-CHKPT-CONTROLS                               
013000     END-IF.                                                              
013100                                                                          
013200 2000-PROCESS-INPUT.                                                      
013300                                                                          
           IF DL010-USR-ID > SPACE                                              
               PERFORM 7100-DELETE                                              
           ELSE                                                                 
               PERFORM 7200-DELETE                                              
           END-IF                                                               
                                                                                
013600     IF PA-COMMIT-COUNTER > TCKRSTCNTL-CKPT-FRQNCY-QTY                    
013700         EXEC SQL                                                         
013800            COMMIT                                                        
013900         END-EXEC                                                         
014000                                                                          
014100         IF SQLCODE = ZERO                                                
014200             ADD +1                   TO PA-COMMIT-TOTAL                  
014300*            MOVE DK-USR-ID           TO PV-COMMIT-USR-ID                 
014400*            MOVE DK-P2L-CART-ID      TO PV-COMMIT-P2L-CART               
014500*            DISPLAY 'COMMIT TAKEN: ' PV-COMMIT-INFO                      
014600             MOVE ZERO                TO PA-COMMIT-COUNTER                
014700         ELSE                                                             
014800             DISPLAY '### COMMIT FAILED ! ###'                            
014900             PERFORM 9100-SQL-ABEND                                       
015000         END-IF                                                           
015100     END-IF.                                                              
015200                                                                          
015300     PERFORM 8000-READ-INPUT.                                             
015400/                                                                         
015500 7000-SELECT-CHKPT-CONTROLS.                                              
015600                                                                          
015700     PERFORM                                                              
015800       WITH TEST AFTER                                                    
015900         VARYING PV-RETRY-COUNTER FROM 1 BY 1                             
016000           UNTIL PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES             
016100             OR  SQLCODE EQUAL ZERO OR +100                               
016200                                                                          
016300         EXEC SQL                                                         
016400             SELECT                                                       
016500                  CKPT_FRQNCY_QTY                                         
016600             INTO                                                         
016700                 :TCKRSTCNTL-CKPT-FRQNCY-QTY                              
016800                                                                          
016900             FROM TCKRSTCNTL                                              
017000             WHERE                                                        
017100                 JOB_NAME = 'DLK100'                                      
017200             AND PLAN_NAME = 'DLKUP011'                                   
017300                                                                          
017400         END-EXEC                                                         
017500                                                                          
017600         EVALUATE TRUE                                                    
017700           WHEN SQLCODE EQUAL ZERO                                        
017800             CONTINUE                                                     
017900           WHEN SQLCODE EQUAL ZERO                                        
018000             MOVE 50             TO TCKRSTCNTL-CKPT-FRQNCY-QTY            
018100           WHEN SQLWARN0 NOT EQUAL SPACE                                  
018200           WHEN SQLCODE  NOT EQUAL +0                                     
018300             MOVE '7000-SELECT-CHKPT-CONTROLS'                            
018400                         TO PV-PARAGRAPH-NAME                             
018500             MOVE 'SELECT FROM TCKRSTCNTL'                                
018600                         TO PV-DB2-OPERATION-ATTEMPTED                    
018700             PERFORM 9100-SQL-ABEND                                       
018800         END-EVALUATE                                                     
018900     END-PERFORM.                                                         
019000                                                                          
019100     IF PV-RETRY-COUNTER GREATER THAN PC-MAX-RETRIES AND                  
019200        (SQLCODE NOT EQUAL +0)                                            
019300         DISPLAY '********************************************'           
019400         DISPLAY '** MAX RETRIES EXCEEDED ATTEMPTING TO     **'           
019500         DISPLAY '** SELECT TCKRSTCNTL                      **'           
019600         DISPLAY '** PARAGRAPH= 7000-SELECT-CHKPT-CONTROLS    '           
019700         DISPLAY '********************************************'           
019800         PERFORM 9100-SQL-ABEND                                           
019900     END-IF.                                                              
020000/                                                                         
      **************************************************************            
      ***  DELETE THE KOHL'S USER ID FROM THE TABLE                             
      **************************************************************            
020100 7100-DELETE.                                                             
020200                                                                          
020300     EXEC SQL                                                             
020400         DELETE                                                           
020500             FROM DLT_P2L_CART_VLDN                                       
020600          WHERE                                                           
020700             USR_ID          =       :DK-USR-ID                           
020800         AND CART_ID         =       :DK-P2L-CART-ID                      
020900     END-EXEC.                                                            
021000                                                                          
021100     EVALUATE TRUE                                                        
021200         WHEN SQLCODE = ZERO                                              
021300             ADD +1           TO PA-VLDTN-DELETES                         
021400             ADD +1           TO PA-COMMIT-COUNTER                        
021500         WHEN SQLCODE = +100                                              
021600             ADD +1           TO PA-VLDTN-NOT-FOUND                       
021700             DISPLAY ' '                                                  
021800             DISPLAY '*******************************************'        
021900             DISPLAY '  CART VLDTN DELETE NOT FOUND (+100)'               
022000             DISPLAY '  USER ID: ' DK-USR-ID                              
022100             DISPLAY '  P2L CART ID: ' DK-P2L-CART-ID                     
022200             DISPLAY '*******************************************'        
022300         WHEN SQLCODE = -904                                              
022400         WHEN SQLCODE = -911                                              
022500         WHEN SQLCODE = -913                                              
022600             ADD +1           TO PA-VLDTN-DEL-FAIL                        
022700             MOVE '7100-DELETE'                                           
022800                         TO PV-PARAGRAPH-NAME                             
022900             MOVE 'DELETE FROM DLT_P2L_CART_VLDTN'                        
023000                         TO PV-DB2-OPERATION-ATTEMPTED                    
023100             PERFORM 9100-SQL-ABEND                                       
023200         WHEN SQLCODE < 0                                                 
023300         WHEN SQLCODE > 0                                                 
023400         WHEN SQLWARN0 = 'W'                                              
023500             MOVE '7100-DELETE'                                           
023600                         TO PV-PARAGRAPH-NAME                             
023700             MOVE 'DELETE FROM DLT_P2L_CART_VLDTN'                        
023800                         TO PV-DB2-OPERATION-ATTEMPTED                    
023900             PERFORM 9100-SQL-ABEND                                       
024000     END-EVALUATE.                                                        
024100                                                                          
      **************************************************************            
      ***  DELETE THE TEMPERARY USER ID FROM THE TALBE                          
      **************************************************************            
020100 7200-DELETE.                                                             
020200                                                                          
020300     EXEC SQL                                                             
020400         DELETE                                                           
020500             FROM DLT_P2L_CART_VLDN                                       
020600          WHERE                                                           
020700             TMP_USR_ID      =       :DK-TMP-USR-ID                       
020800         AND CART_ID         =       :DK-P2L-CART-ID                      
020900     END-EXEC.                                                            
021000                                                                          
021100     EVALUATE TRUE                                                        
021200         WHEN SQLCODE = ZERO                                              
021300             ADD +1           TO PA-VLDTN-DELETES                         
021400             ADD +1           TO PA-COMMIT-COUNTER                        
021500         WHEN SQLCODE = +100                                              
021600             ADD +1           TO PA-VLDTN-NOT-FOUND                       
021700             DISPLAY ' '                                                  
021800             DISPLAY '*******************************************'        
021900             DISPLAY '  CART VLDTN DELETE NOT FOUND (+100)'               
022000             DISPLAY '  USER ID: ' DK-TMP-USR-ID                          
022100             DISPLAY '  P2L CART ID: ' DK-P2L-CART-ID                     
022200             DISPLAY '*******************************************'        
022300         WHEN SQLCODE = -904                                              
022400         WHEN SQLCODE = -911                                              
022500         WHEN SQLCODE = -913                                              
022600             ADD +1           TO PA-VLDTN-DEL-FAIL                        
022700             MOVE '7200-DELETE'                                           
022800                         TO PV-PARAGRAPH-NAME                             
022900             MOVE 'DELETE FROM DLT_P2L_CART_VLDTN'                        
023000                         TO PV-DB2-OPERATION-ATTEMPTED                    
023100             PERFORM 9100-SQL-ABEND                                       
023200         WHEN SQLCODE < 0                                                 
023300         WHEN SQLCODE > 0                                                 
023400         WHEN SQLWARN0 = 'W'                                              
023500             MOVE '7200-DELETE'                                           
023600                         TO PV-PARAGRAPH-NAME                             
023700             MOVE 'DELETE FROM DLT_P2L_CART_VLDTN'                        
023800                         TO PV-DB2-OPERATION-ATTEMPTED                    
023900             PERFORM 9100-SQL-ABEND                                       
024000     END-EVALUATE.                                                        
024100                                                                          
024200 8000-READ-INPUT.                                                         
024300                                                                          
024400     READ PURGE-EXTRACT-FILE                                              
024500         INTO                                                             
024600             DL010-CART-VLDN-PURGE-EXTRACT                                
024700       AT END                                                             
024800         SET PS-EOF                   TO TRUE                             
024900       NOT AT END                                                         
025000         ADD +1                       TO PA-RECS-READ                     
025100         MOVE DL010-USR-ID            TO DK-USR-ID                        
025200         MOVE DL010-P2L-CART-ID       TO DK-P2L-CART-ID                   
               MOVE DL010-TMP-USR-ID        TO DK-TMP-USR-ID                    
025300     END-READ.                                                            
025400                                                                          
025500 9000-QUIT.                                                               
025600                                                                          
025700     CLOSE PURGE-EXTRACT-FILE                                             
025800                                                                          
025900     DISPLAY SPACE.                                                       
026000     DISPLAY '****************************************'.                  
026100     DISPLAY '**    DLKUP011 SUMMARY STATISTICS     **'.                  
026200     DISPLAY '****************************************'.                  
026300     MOVE PA-RECS-READ            TO PV-DISPLAY-COUNT.                    
026400     DISPLAY 'TOTAL RECORDS READ            : '  PV-DISPLAY-COUNT.        
026500                                                                          
026600     MOVE PA-VLDTN-DELETES        TO PV-DISPLAY-COUNT.                    
026700     DISPLAY 'TOTAL VLDTN DELETES           : '  PV-DISPLAY-COUNT.        
026800                                                                          
026900     MOVE PA-COMMIT-TOTAL         TO PV-DISPLAY-COUNT.                    
027000     DISPLAY 'TOTAL COMMITS TAKEN           : '  PV-DISPLAY-COUNT.        
027100                                                                          
027200     MOVE PA-VLDTN-DEL-FAIL       TO PV-DISPLAY-COUNT.                    
027300     DISPLAY 'TOTAL VLDTN DELETES FAILED    : '  PV-DISPLAY-COUNT.        
027400                                                                          
027500     MOVE PA-VLDTN-NOT-FOUND      TO PV-DISPLAY-COUNT.                    
027600     DISPLAY 'TOTAL VLDTN NOT FOUND         : '  PV-DISPLAY-COUNT.        
027700                                                                          
027800     DISPLAY '****************************************'.                  
027900                                                                          
028000******************************************************************        
028100*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
028200*  ERROR OR WARNING CODE OCCURS.                                          
028300******************************************************************        
028400 9100-SQL-ABEND.                                                          
028500                                                                          
028600     DISPLAY '9100-SQL-ABEND'.                                            
028700     DISPLAY '** ABEND     **'.                                           
028800     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
028900     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
029000     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
029100     DISPLAY '***'                                                        
029200     DISPLAY '*** LAST COMMIT TAKEN **' PV-COMMIT-INFO                    
029300     DISPLAY '***'                                                        
029400     DISPLAY '*** COMMITTED UPDATE COUNTS FOLLOW: ***'                    
029500     DISPLAY '***'                                                        
029600     EXEC SQL                                                             
029700          ROLLBACK                                                        
029800     END-EXEC.                                                            
029900******************************************************************        
030000* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
030100* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
030200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
030300* FORMAT.                                                                 
030400******************************************************************        
030500     COPY DPPD004.                                                        
030600                                                                          
030700     MOVE +4013                  TO ABEND-CODE.                           
030800     CALL 'ILBOABN0' USING ABEND-CODE.                                    
