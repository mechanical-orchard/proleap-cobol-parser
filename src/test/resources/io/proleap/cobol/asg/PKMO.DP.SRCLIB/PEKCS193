00001 *-----------------------*                                                 
00002  IDENTIFICATION DIVISION.                                                 
00003 *-----------------------*                                                 
00004  PROGRAM-ID.    PEKCS193.                                                 
00005  AUTHOR.        AMY REILAND.                                              
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00007  DATE-WRITTEN.  08-01-93.                                                 
00008  DATE-COMPILED.                                                           
00009 *----------------------------------------------------------------*        
00010 *    Q193 - IN-STORE DOCUMENT HEADER INQUIRY                     *        
00011 *                                                                *        
00012 *    THIS PROGRAM DISPLAYS THE IN-STORE HEADER INFORMATION FOR   *        
00013 *    A PARTICULAR STORE/DOCUMENT.  KEY INFORMATION MAY EITHER    *        
00014 *    BE PASSED FROM THE CRITERIA SPEC, OR THE ENTRY LIST.        *        
      *----------------------------------------------------------------*        
      * 02/09/01 - JFK -  WR NO. 21963                                 *        
      * ADDED RF CONTINUED CLEARANCE AND RETURN TO VENDOR SHORT DESCR. *        
      *----------------------------------------------------------------*        
      * 02/11/05 - TCM -  WR NO. 27559                                 *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH                                          *        
      *    - REPLACED TSTR000 STORE TABLE CALL(S) WITH STORE TABLE     *        
      *      CALL(S) TO XST_LOC AND ADDED A PV-DUMMY-NBR FIELD FOR     *        
      *      DETERMINING EXISTENCE                                     *        
      *    - CHANGED THE FILLER REDEFINES DP020-VARIABLE-COMMAREA      *        
      *      FILLER LENGTH AND THE APPROPRIATE MNEMONIC LENGTH         *        
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE *        
      *      CALLS                                                     *        
      *    - ADDED THE NEW REASON CODE ("87") FOR                      *        
      *      "COSMETICS TESTERS ONLY" IN-STORE DOCUMENTS               *        
      *----------------------------------------------------------------*        
      * 10/26/05 - TCM -  WR NO. 28048                                 *        
      *    - ADDED THE NEW REASON CODE ("89") FOR                      *        
      *      "MARK OUT-OF-STOCK - SALVAGE" IN-STORE DOCUMENTS          *        
      *----------------------------------------------------------------*        
CICS  *  CHANGED 10/17/2022    BY BARB SCHULTZ    WR: CHG0276658                
CICS  *                                                                         
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL             
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.              
CICS  *-----------------------------------------------------------------        
00017                                                                           
00018                                                                           
00019  ENVIRONMENT DIVISION.                                                    
00020                                                                           
00021                                                                           
00022                                                                           
00023  DATA DIVISION.                                                           
00024                                                                           
00025                                                                           
00026  WORKING-STORAGE SECTION.                                                 
00027                                                                           
00028  01  DK-DB2-KEYS.                                                         
00029      05  DK-CURRENT-DATE                PIC  X(10)  VALUE SPACE.          
00030      05  DK-DOCUMENT-NUMBER             PIC S9(07)  COMP-3                
00031                                                     VALUE ZERO.           
00032      05  DK-OPERATING-COMPANY           PIC  X(02)  VALUE SPACE.          
00033      05  DK-STORE-NUMBER                PIC S9(04)  VALUE ZEROS           
                                                          COMP.                 
00034      05  DK-USERID                      PIC  X(08)  VALUE SPACE.          
00035 *                                                                         
00036  01  PC-PROGRAM-CONSTANTS.                                                
00037      05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'PE193A  '.          
00038      05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'PEKM193 '.          
00039      05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'PEKCS193'.          
00040      05  PC-INTER-APPL-CODES.                                             
00041          10  PC-INTER-APPL-CRIT-SPEC                                      
00042                                   PIC  X(01)  VALUE '1'.                  
00043          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.                  
00044      05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
00045                                               COMP.                       
00046                                                                           
00047      05  PC-TSYMSG-NUMBERS.                                               
00048          10  PC-TSYMSG-00026      PIC  9(05)  VALUE  00026.               
00049          10  PC-TSYMSG-00069      PIC  9(05)  VALUE  00069.               
00050          10  PC-TSYMSG-00070      PIC  9(05)  VALUE  00070.               
00051          10  PC-TSYMSG-00148      PIC  9(05)  VALUE  00148.               
00052          10  PC-TSYMSG-00283      PIC  9(05)  VALUE  00283.               
00053          10  PC-TSYMSG-00507      PIC  9(05)  VALUE  00507.               
00054 *                                                                         
00055  01  PS-PROGRAM-SWITCHES.                                                 
00056      05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
00057          88  PS-ERROR                         VALUE  'Y'.                 
00058          88  PS-NO-ERROR                      VALUE  'N'.                 
00059      05  PS-LIST-END-SW           PIC  X(01)  VALUE  'N'.                 
00060          88  PS-LIST-END                      VALUE  'Y'.                 
00061          88  PS-NO-LIST-END                   VALUE  'N'.                 
00062 *                                                                         
00063  01  PV-PROGRAM-VARIABLES.                                                
00064      05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
00065                                               COMP SYNC.                  
           05  PV-DUMMY-NBR             PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
00066      05  PV-HOLD-DISPLAY-DATE-FMT.                                        
00067          10  PV-HOLD-DISPLAY-DATE-MM                                      
00068                                   PIC  X(02)  VALUE SPACES.               
00069          10  FILLER               PIC  X(01)  VALUE '/'.                  
00070          10  PV-HOLD-DISPLAY-DATE-DD                                      
00071                                   PIC  X(02)  VALUE SPACES.               
00072          10  FILLER               PIC  X(01)  VALUE '/'.                  
00073          10  PV-HOLD-DISPLAY-DATE-YY                                      
00074                                   PIC  X(02)  VALUE SPACES.               
00075      05  PV-HOLD-ISO-DATE-FMT.                                            
00076          10  PV-HOLD-ISO-DATE-CC  PIC  X(02)  VALUE SPACES.               
00077          10  PV-HOLD-ISO-DATE-YY  PIC  X(02)  VALUE SPACES.               
00078          10  FILLER               PIC  X(01)  VALUE SPACES.               
00079          10  PV-HOLD-ISO-DATE-MM  PIC  X(02)  VALUE SPACES.               
00080          10  FILLER               PIC  X(01)  VALUE SPACES.               
00081          10  PV-HOLD-ISO-DATE-DD  PIC  X(02)  VALUE SPACES.               
00082      05  PV-HOLD-TYPE-REASON.                                             
00083          10  PV-HOLD-TYPE-CODE    PIC  X(01)  VALUE SPACES.               
00084          10  PV-HOLD-REASON-CODE.                                         
00085              15  PV-HOLD-REASON-CODE-N                                    
00086                                   PIC  9(02)  VALUE ZEROS.                
00087      05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
00088                                               COMP SYNC.                  
00089      05  PV-TOT-ACT-PR-CHG-AMT    PIC Z,ZZZ,ZZZ.99-.                      
00090 *                                                                         
00091  EJECT                                                                    
00092 *---------------------------------------------------------------**        
00093 *  THE LAYOUT FOR THE TEMPORARY STORAGE QUEUE CONTAINING        **        
00094 *  SELECTED LIST ITEMS TO BE PROCESSED BY THIS PROGRAM.         **        
00095 *---------------------------------------------------------------**        
00096      COPY PEWS067.                                                        
00097 *                                                                         
00098  EJECT                                                                    
00099 *----------------------------------------------------------------*        
00100 *    MAP LAYOUT                                                  *        
00101 *----------------------------------------------------------------*        
00102                                                                           
00103      COPY PEKM193.                                                        
00104 *                                                                         
00105  EJECT                                                                    
00106 *----------------------------------------------------------------*        
00107 *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
00108 *----------------------------------------------------------------*        
00109                                                                           
00110      COPY DPWS015.                                                        
00111 *                                                                         
00112  EJECT                                                                    
00113 *----------------------------------------------------------------*        
00114 *    FUNCTION KEYS COPYBOOK                                      *        
00115 *----------------------------------------------------------------*        
00116                                                                           
00117      COPY DPWS016.                                                        
00118 *                                                                         
00119  EJECT                                                                    
00120 *----------------------------------------------------------------*        
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
00123 *----------------------------------------------------------------*        
00124      COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
00125 *                                                                         
00126  EJECT                                                                    
00127 *----------------------------------------------------------------*        
00128 *    STANDARD COMMAREA.                                          *        
00129 *----------------------------------------------------------------*        
00130                                                                           
00131      COPY DPWS020.                                                        
00132      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
00133                                                                           
00134 *                                                                         
00135  EJECT                                                                    
00136 *----------------------------------------------------------------*        
00137 *    INTER-APPLICATION COMMAREA.                                 *        
00138 *----------------------------------------------------------------*        
00139                                                                           
00140          10  INTER-APPL-COMMAREA  PIC X(200).                             
00141 *----------------------------------------------------------------*        
00142 *    INTER-APPLICATION COMMAREA PASSED FROM THE CRITERIA SPEC    *        
00143 *----------------------------------------------------------------*        
00144      COPY PEWS064.                                                        
00145 *                                                                         
00146  EJECT                                                                    
00147 *----------------------------------------------------------------*        
00148 *    INTER-APPLICATION COMMAREA PASSED FROM THE ENTRY LIST       *        
00149 *----------------------------------------------------------------*        
00150      COPY PEWS066.                                                        
00151 *                                                                         
00152  EJECT                                                                    
00153 *----------------------------------------------------------------*        
00154 *    INTER-APPLICATION COMMAREA TO BE PASSED TO THE DETAIL       *        
00155 *    INQUIRY                                                     *        
00156 *----------------------------------------------------------------*        
00157      COPY PEWS068.                                                        
00158 *                                                                         
00159  EJECT                                                                    
00160 *----------------------------------------------------------------*        
00161 *    SPECIFIC COMMMAREA FOR PEKCS193.                            *        
00162 *----------------------------------------------------------------*        
00163          10  ASC-SPECIFIC-COMMAREA.                                       
00164              15  ASC-INT-APPL-FORMAT-IND   PIC  X(01).                    
00165              15  ASC-STORE-NUMBER          PIC  X(04).                    
                   15  FILLER REDEFINES ASC-STORE-NUMBER.                       
                       20  ASC-STORE-NUMBER-N    PIC  9(04).                    
00166              15  ASC-STORE-NAME            PIC  X(25).                    
00167              15  ASC-DOCUMENT-NUMBER.                                     
00168                  20  ASC-DOCUMENT-NUMBER-N PIC  9(07).                    
00169              15  ASC-STATUS-CODE           PIC  X(02).                    
00170              15  ASC-STATUS-DESC           PIC  X(10).                    
00171              15  ASC-REASON-CODE           PIC  X(03).                    
00172              15  ASC-TYPE-DESC             PIC  X(08).                    
00173              15  ASC-REASON-DESC           PIC  X(12).                    
00174              15  ASC-DEPT-AREA-TEXT        PIC  X(40).                    
00175              15  ASC-ISSUED-DATE           PIC  X(10).                    
00176              15  ASC-ISSUED-ID             PIC  X(08).                    
00177              15  ASC-CANCELLED-DATE        PIC  X(10).                    
00178              15  ASC-CANCELLED-ID          PIC  X(08).                    
00179              15  ASC-ENTERED-DATE          PIC  X(10).                    
00180              15  ASC-ENTERED-ID            PIC  X(08).                    
00181              15  ASC-EFFECTIVE-DATE        PIC  X(10).                    
00182              15  ASC-COMPLETED-NAME        PIC  X(30).                    
00183              15  ASC-TOT-ACT-PR-CHG-AMT    PIC S9(09)V99.                 
00184              15  ASC-HISTORY-SW            PIC  X(01).                    
00185                  88  ASC-HISTORY                       VALUE 'Y'.         
00186                  88  ASC-NOT-HISTORY                   VALUE 'N'.         
00187              15  ASC-USE-SELECTION-QUEUE-IND                              
00188                                            PIC  X(01).                    
00189                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
00190              15  ASC-SEL-QUEUE-NAME        PIC  X(08).                    
00191              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.            
00192              15  ASC-NUMBER-OF-SELECTED-ITEMS                             
00193                                            PIC S9(09)  COMP-3.            
00194              15  ASC-SEL-SUB               PIC S9(04)  COMP.              
00195              15  ASC-SEL-ITEM              PIC S9(04)  COMP.              
00196          10  FILLER                        PIC  X(2626).                  
00197 *                                                                         
00198  EJECT                                                                    
00199 *----------------------------------------------------------------*        
00200 *     PERM PRICE CHANGE CONSTANT/WORKING STORAGE COPYBOOK.       *        
00201 *----------------------------------------------------------------*        
00202                                                                           
00203      COPY PEWS049.                                                        
00204 *                                                                         
00205  EJECT                                                                    
00206 *----------------------------------------------------------------*        
00207 *     DATE PROCESSING COPYBOOK.                                  *        
00208 *----------------------------------------------------------------*        
00209                                                                           
00210      COPY DPWS017.                                                        
00211 *                                                                         
00212  EJECT                                                                    
00213 *----------------------------------------------------------------*        
00214 *    ABEND PROCESSING COPYBOOK.                                  *        
00215 *----------------------------------------------------------------*        
00216                                                                           
00217      COPY DPWS013.                                                        
00218 *                                                                         
00219  EJECT                                                                    
00468 *---------------------------------------------------------------*         
00469 *    SELLING AND CUSTOMER SERVICE LOCATION TABLE                *         
00470 *---------------------------------------------------------------*         
00471      EXEC SQL                                                             
00472           INCLUDE XST00001                                                
00473      END-EXEC.                                                            
00229 *----------------------------------------------------------------*        
00230 *    DB2 AREA FOR TSTRPPC (CURRENT IN-STORE DOCUMENT HEADER      *        
00231 *    TABLE)                                                      *        
00232 *----------------------------------------------------------------*        
00233                                                                           
00234      EXEC SQL                                                             
00235           INCLUDE TSTRPPC                                                 
00236      END-EXEC.                                                            
00237 *                                                                         
00238  EJECT                                                                    
00239 *----------------------------------------------------------------*        
00240 *    DB2 AREA FOR TSTRPPC (HISTORY IN-STORE DOCUMENT HEADER      *        
00241 *    TABLE)                                                      *        
00242 *----------------------------------------------------------------*        
00243                                                                           
00244      EXEC SQL                                                             
00245           INCLUDE TSTRPCH                                                 
00246      END-EXEC.                                                            
00247 *                                                                         
00248  EJECT                                                                    
00249 *----------------------------------------------------------------*        
00250 *    DB2 AREA FOR COMMUNICATIONS                                 *        
00251 *----------------------------------------------------------------*        
00252                                                                           
00253      EXEC SQL                                                             
00254           INCLUDE SQLCA                                                   
00255      END-EXEC.                                                            
00256 *                                                                         
00257  EJECT                                                                    
00258  LINKAGE SECTION.                                                         
00259                                                                           
00260  01  DFHCOMMAREA.                                                         
00261      05  FILLER                         OCCURS  1 TO 4072 TIMES           
00262                                         DEPENDING ON EIBCALEN.            
00263          10  FILLER                     PIC X.                            
00264                                                                           
00265                                                                           
00266  PROCEDURE DIVISION.                                                      
00267 *****************************************************************         
00268 ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
00269 ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
00270 ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
00271 **                                                             **         
00272 ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
00273 ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
00274 *****************************************************************         
00275  0000-MAIN-MODULE.                                                        
00276      INITIALIZE DP030-CICS-API-FIELDS.                                    
00277      MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
00278      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
00279      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
00280      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
00281      MOVE LENGTH OF PE193AI        TO DP030-MAP-LENGTH (1).               
00282      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
00283      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00284 *                                                                         
00285      PERFORM 1000-CONTROL-PROCESSING                                      
00286 *                                                                         
00287      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
00288      PERFORM 0001-CALL-CICS-ARCH-API.                                     
00289                                                                           
00290  0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
00292                                     DFHCOMMAREA                           
00293                                     DP030-CICS-API-FIELDS                 
00294                                     DP020-STANDARD-COMMAREA               
00295                                     PE193AI.                              
00296 *                                                                         
00297      IF  DP030-RC-CALL-SUCCESSFUL                                         
00298          CONTINUE                                                         
00299      ELSE                                                                 
00300          SET DP013-NO-ROLLBACK                                            
00301              DP013-XCTL-DISPLAY-RESTART                                   
00302              DP013-CICS-ABEND      TO TRUE                                
00303          MOVE 'BEFORE 0000-MAIN-MODULE'                                   
00304                                    TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00307          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
00308          PERFORM DP013-0000-PROCESS-ABEND                                 
00309      END-IF.                                                              
00310 *                                                                         
00311  EJECT                                                                    
00312 *----------------------------------------------------------------*        
00313 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
00314 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
00315 *----------------------------------------------------------------*        
00316                                                                           
00317  1000-CONTROL-PROCESSING.                                                 
00318      PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
00319      SET  DP017-EDITED-YMD-DATE-DASH-1                                    
00320           DP017-EDITED-YMD-DATE-DASH-2 TO TRUE.                           
00321      MOVE DP017-EDITED-DATE-YYYYMMDD                                      
00322                                    TO DK-CURRENT-DATE.                    
00323      MOVE DP020-USERID             TO DK-USERID.                          
00324      MOVE DP020-OPERATING-COMPANY  TO DK-OPERATING-COMPANY.               
00325      EVALUATE TRUE                                                        
00326          WHEN DP020-NEXT-ACT-INITIAL                                      
00327               PERFORM 1100-FIND-OBJECT-KEYS                               
00328               IF  PS-ERROR                                                
00329                   CONTINUE                                                
00330               ELSE                                                        
00331                   PERFORM 4000-BUILD-INITIAL-PANEL                        
00332               END-IF                                                      
00333                                                                           
00334          WHEN DP020-NEXT-ACT-READ-MAP                                     
00335              PERFORM 2000-PROCESS-PANEL                                   
00336                                                                           
00337          WHEN DP020-NEXT-ACT-RETURN                                       
00338              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00339                                                                           
00340          WHEN OTHER                                                       
00341              SET DP013-LOGIC-ABEND TO TRUE                                
00342              SET DP013-NO-ROLLBACK TO TRUE                                
00343              MOVE '1000-CONTROL-PROCESSING'                               
00344                                    TO DP013-PARAGRAPH                     
00345              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
00346                                    TO DP013-MESSAGE-TEXT(1)               
00347              MOVE DP020-NEXT-APPL-ACTIVITY                                
00348                                    TO DP013-MESSAGE-TEXT(2)               
00349              PERFORM DP013-0000-PROCESS-ABEND                             
00350      END-EVALUATE.                                                        
00351 *                                                                         
00352  EJECT                                                                    
00353 *----------------------------------------------------------------*        
00354 *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE   **        
00355 *  INTER-APPLICATION FORMAT CODE.                               **        
00356 *----------------------------------------------------------------*        
00357  1100-FIND-OBJECT-KEYS.                                                   
00358      INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
00359      MOVE DP020-INT-APPL-FORMAT-IND                                       
00360                                    TO ASC-INT-APPL-FORMAT-IND.            
00361      EVALUATE DP020-INT-APPL-FORMAT-IND                                   
00362          WHEN PC-INTER-APPL-CRIT-SPEC                                     
00363              IF  PE064-STORE-NUMBER = SPACES                              
00364                  MOVE PC-TSYMSG-00507                                     
00365                                    TO DP020-MSG-NUMBER                    
00366                  SET PS-ERROR                                             
00367                      DP020-MSG-FATAL                                      
00368                      DP020-NEXT-ACT-APPL-ERROR                            
00369                                    TO TRUE                                
00370              ELSE                                                         
00371                  MOVE PE064-STORE-NUMBER                                  
00372                                    TO ASC-STORE-NUMBER                    
00373                                       DK-STORE-NUMBER                     
00374                  MOVE PE064-STORE-NAME                                    
00375                                    TO ASC-STORE-NAME                      
00376                  IF PE064-DOCUMENT-NUMBER = SPACES                        
00377                      MOVE PC-TSYMSG-00026                                 
00378                                    TO DP020-MSG-NUMBER                    
00379                      SET PS-ERROR                                         
00380                          DP020-MSG-FATAL                                  
00381                          DP020-NEXT-ACT-APPL-ERROR                        
00382                                    TO TRUE                                
00383                  ELSE                                                     
00384                      MOVE PE064-DOCUMENT-NUMBER                           
00385                                    TO ASC-DOCUMENT-NUMBER                 
00386                      MOVE ASC-DOCUMENT-NUMBER-N                           
00387                                    TO DK-DOCUMENT-NUMBER                  
00388                      PERFORM 3310-VALIDATE-DOCUMENT                       
00389                  END-IF                                                   
00390              END-IF                                                       
00391                                                                           
00392          WHEN PC-INTER-APPL-LIST                                          
00393              SET ASC-USE-SELECTION-QUEUE TO TRUE                          
00394              MOVE PE066-NUMBER-OF-SELECTED-ITEMS                          
00395                                    TO ASC-NUMBER-OF-SELECTED-ITEMS        
00396              MOVE PE066-NUMBER-OF-TS-ITEMS                                
00397                                    TO ASC-NUMBER-OF-TS-ITEMS              
00398              MOVE PE066-SEL-QUEUE-NAME                                    
00399                                    TO ASC-SEL-QUEUE-NAME                  
00400              PERFORM 1120-PREPARE-LIST-ENTRY                              
00401                                                                           
00402          WHEN OTHER                                                       
00403                  MOVE PC-TSYMSG-00148                                     
00404                                    TO DP020-MSG-NUMBER                    
00405                  SET PS-ERROR                                             
00406                      DP020-MSG-FATAL                                      
00407                      DP020-NEXT-ACT-APPL-ERROR                            
00408                                    TO TRUE                                
00409      END-EVALUATE.                                                        
00410 *                                                                         
00411  EJECT                                                                    
00412 *----------------------------------------------------------------*        
00413 * ENTRY IS FROM A LIST.  GET THE DOCUMENT NUMBER FROM THE TEMP   *        
00414 * STORAGE QUEUE PASSED FROM THE LIST.                            *        
00415 *----------------------------------------------------------------*        
00416  1120-PREPARE-LIST-ENTRY.                                                 
00417      MOVE PE067-MAX-ITEMS          TO ASC-SEL-SUB.                        
00418      MOVE ZERO                     TO ASC-SEL-ITEM.                       
00419      PERFORM 2110-GET-NEXT-DOCUMENT.                                      
00420 *                                                                         
00421  EJECT                                                                    
00422 *----------------------------------------------------------------*        
00423 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
00424 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
00425 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
00426 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
00427 * FUNCTION KEYS CAN GET THIS FAR.                                *        
00428 *----------------------------------------------------------------*        
00429                                                                           
00430  2000-PROCESS-PANEL.                                                      
00431                                                                           
00432      EVALUATE TRUE                                                        
00433                                                                           
00434          WHEN DP020-SRC-AID = DP016-CLEAR                                 
00435              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
00436                                                                           
00437          WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
00438              PERFORM 4000-BUILD-INITIAL-PANEL                             
00439                                                                           
00440          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
00441              IF  ASC-USE-SELECTION-QUEUE                                  
00442                  PERFORM 2110-GET-NEXT-DOCUMENT                           
00443                  IF PS-LIST-END                                           
00444                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
00445                      SET PS-NO-LIST-END TO TRUE                           
00446                  ELSE                                                     
00447                      PERFORM 4000-BUILD-INITIAL-PANEL                     
00448                  END-IF                                                   
00449              ELSE                                                         
00450 *                --- BOTTOM OF LIST ---                                   
00451                  MOVE PC-TSYMSG-00070                                     
00452                                    TO DP020-MSG-NUMBER                    
00453                  SET DP020-MSG-INFORMATIONAL                              
00454                                    TO TRUE                                
00455              END-IF                                                       
00456                                                                           
00457          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
00458              IF  ASC-USE-SELECTION-QUEUE                                  
00459                  PERFORM 2120-GET-PREV-DOCUMENT                           
00460                  IF PS-LIST-END                                           
00461                      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
00462                      SET PS-NO-LIST-END TO TRUE                           
00463                  ELSE                                                     
00464                      PERFORM 4000-BUILD-INITIAL-PANEL                     
00465                  END-IF                                                   
00466              ELSE                                                         
00467 *                --- TOP OF LIST ---                                      
00468                  MOVE PC-TSYMSG-00069                                     
00469                                    TO DP020-MSG-NUMBER                    
00470                  SET DP020-MSG-INFORMATIONAL                              
00471                                    TO TRUE                                
00472              END-IF                                                       
00473                                                                           
00474          WHEN DP020-SRC-AID = DP016-ENTER                                 
00475              CONTINUE                                                     
00476                                                                           
00477          WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                         
00478              IF PS-NO-ERROR                                               
00479                  SET DP030-CONTINUE-GO TO TRUE                            
00480                  PERFORM 2105-PREPARE-INTER-APPL-COMM                     
00481              ELSE                                                         
00482                  SET DP030-OVERRIDE-GO TO TRUE                            
00483              END-IF                                                       
00484                                                                           
00485          WHEN OTHER                                                       
00486              SET DP013-NO-ROLLBACK                                        
00487                  DP013-XCTL-DISPLAY-RESTART                               
00488                  DP013-CICS-ABEND  TO TRUE                                
00489              MOVE '2000-PROCESS-PANEL'                                    
00490                                    TO DP013-PARAGRAPH                     
00491              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
00492                                    TO DP013-MESSAGE-TEXT (1)              
00493              PERFORM DP013-0000-PROCESS-ABEND                             
00494      END-EVALUATE.                                                        
00495 *                                                                         
00496  EJECT                                                                    
00497 *----------------------------------------------------------------*        
00498 *  MOVE DATA NECESSARY TO THE DESTINATION PROGRAM (DETAIL       **        
00499 *  INQUIRY), INTO THE INTER-APPLICATION COMMAREA.               **        
00500 *----------------------------------------------------------------*        
00501  2105-PREPARE-INTER-APPL-COMM.                                            
00502      INITIALIZE PE068-INTER-APPL-COMMAREA.                                
00503      MOVE ASC-STORE-NUMBER-N       TO PE068-STORE-NUMBER.                 
00504      MOVE ASC-STORE-NAME           TO PE068-STORE-NAME.                   
00505      MOVE ASC-DOCUMENT-NUMBER-N    TO PE068-DOCUMENT-NUMBER.              
00506      MOVE ASC-HISTORY-SW           TO PE068-HISTORY-SW.                   
00507 *                                                                         
00508  EJECT                                                                    
00509 *----------------------------------------------------------------*        
00510 *  GET THE DOCUMENT NUMBER (KEY) OF THE NEXT SELECTED           **        
00511 *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.          **        
00512 *----------------------------------------------------------------*        
00513  2110-GET-NEXT-DOCUMENT.                                                  
00514      ADD +1                        TO ASC-SEL-SUB.                        
00515      IF  ASC-SEL-SUB > PE067-MAX-ITEMS                                    
00516          ADD +1                    TO ASC-SEL-ITEM                        
00517          MOVE +1                   TO ASC-SEL-SUB                         
00518      END-IF.                                                              
00519      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
00520          PERFORM 9100-READ-SEL-QUEUE                                      
00521          IF  PE067-ITEM (ASC-SEL-SUB) > ZERO                              
00522              MOVE PE067-STORE-NUMBER (ASC-SEL-SUB)                        
00523                                    TO ASC-STORE-NUMBER                    
                                            XST00001-LOC-NBR                    
00524              MOVE PE067-DOCUMENT-NUMBER (ASC-SEL-SUB)                     
00525                                    TO ASC-DOCUMENT-NUMBER-N               
00526              SET PE067-INQ (ASC-SEL-SUB)                                  
00527                                    TO TRUE                                
00528              PERFORM 9200-REWRITE-LIST-QUEUE                              
00529              PERFORM 3000-GET-STORE-NAME                                  
00530              IF SQLCODE = 0                                               
00531                  MOVE XST00001-LOC-NM                                     
00532                                    TO ASC-STORE-NAME                      
00533              ELSE                                                         
00534                  MOVE SPACES       TO ASC-STORE-NAME                      
00535              END-IF                                                       
00536          ELSE                                                             
00537              SUBTRACT +1 FROM ASC-SEL-SUB                                 
00538 *            --- BOTTOM OF LIST ---                                       
00539              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
00540              SET DP020-MSG-INFORMATIONAL                                  
00541                  PS-LIST-END       TO TRUE                                
00542          END-IF                                                           
00543      ELSE                                                                 
00544          SUBTRACT +1 FROM ASC-SEL-ITEM                                    
00545          MOVE PE067-MAX-ITEMS      TO ASC-SEL-SUB                         
00546          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
00547          SET DP020-MSG-INFORMATIONAL                                      
00548              PS-LIST-END           TO TRUE                                
00549      END-IF.                                                              
00550 *                                                                         
00551  EJECT                                                                    
00552 *----------------------------------------------------------------*        
00553 *  GET THE DOCUMENT NUMBER (KEY) OF THE PREVIOUS SELECTED       **        
00554 *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.          **        
00555 *----------------------------------------------------------------*        
00556  2120-GET-PREV-DOCUMENT.                                                  
00557      SUBTRACT +1 FROM ASC-SEL-SUB.                                        
00558      IF  ASC-SEL-SUB < +1                                                 
00559          SUBTRACT +1 FROM ASC-SEL-ITEM                                    
00560          MOVE PE067-MAX-ITEMS      TO ASC-SEL-SUB                         
00561      END-IF.                                                              
00562 *                                                                         
00563      IF  ASC-SEL-ITEM > ZERO                                              
00564          PERFORM 9100-READ-SEL-QUEUE                                      
00565          MOVE PE067-STORE-NUMBER (ASC-SEL-SUB)                            
00566                                    TO ASC-STORE-NUMBER                    
                                            XST00001-LOC-NBR                    
00567          MOVE PE067-DOCUMENT-NUMBER (ASC-SEL-SUB)                         
00568                                    TO ASC-DOCUMENT-NUMBER-N               
00569          PERFORM 3000-GET-STORE-NAME                                      
00570          IF SQLCODE = 0                                                   
00571              MOVE XST00001-LOC-NM  TO ASC-STORE-NAME                      
00573          ELSE                                                             
00574              MOVE SPACES           TO ASC-STORE-NAME                      
00575          END-IF                                                           
00576      ELSE                                                                 
00577          MOVE +1                   TO ASC-SEL-ITEM                        
00578                                       ASC-SEL-SUB                         
00579 *        --- TOP OF LIST ---                                              
00580          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
00581          SET DP020-MSG-INFORMATIONAL                                      
00582              PS-LIST-END           TO TRUE                                
00583      END-IF.                                                              
00584 *                                                                         
00585  EJECT                                                                    
00586 *----------------------------------------------------------------*        
00587 * GET THE STORE NAME FOR THE ITEMS PASSED FROM THE STORE LIST    *        
00588 *----------------------------------------------------------------*        
00589 *                                                                         
00590  3000-GET-STORE-NAME.                                                     
00591      PERFORM                                                              
00592          WITH TEST AFTER                                                  
00593          VARYING PV-RETRY-COUNTER                                         
00594          FROM 1 BY 1                                                      
00595          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00596                    OR (SQLCODE = ZERO)                                    
00597                    OR (SQLCODE = +100))                                   
00598 *                                                                         
00599          EXEC SQL                                                         
                   SELECT LOC_NM                                                
                   INTO :XST00001-LOC-NM                                        
                   FROM XST_LOC                                                 
                   WHERE LOC_NBR = :XST00001-LOC-NBR                            
00607          END-EXEC                                                         
00608                                                                           
00609          EVALUATE TRUE                                                    
00610              WHEN SQLCODE = ZERO                                          
00611              WHEN SQLCODE = -904                                          
00612              WHEN SQLCODE = -913                                          
00613              WHEN SQLCODE = +100                                          
00614                  CONTINUE                                                 
00615              WHEN SQLWARN0 NOT = SPACES                                   
00616              WHEN SQLCODE < ZERO                                          
00617              WHEN SQLCODE > ZERO                                          
00618                  MOVE '3000-GET-STORE-NAME'                               
00619                                    TO DP013-PARAGRAPH                     
00620                  MOVE 'SELECT THE STORE NAME'                             
00621                                    TO DP013-MESSAGE-TEXT (1)              
00622                  MOVE SQLCA        TO DP013-SQLCA                         
00623                  MOVE 'XST_LOC'    TO DP013-DB2-TABLE-NAME (1)            
00624                  SET DP013-DB2-ABEND                                      
00625                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00626                  PERFORM DP013-0000-PROCESS-ABEND                         
00627          END-EVALUATE                                                     
00628      END-PERFORM.                                                         
00629 *                                                                         
00630      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00631          MOVE '3300-GET-STORE-NAME'                                       
00632                                    TO DP013-PARAGRAPH                     
00633          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
00634                                    TO DP013-MESSAGE-TEXT (1)              
00635          MOVE 'STORE NAME'         TO DP013-MESSAGE-TEXT (2)              
00636          MOVE SQLCA                TO DP013-SQLCA                         
00637          MOVE 'XST_LOC'            TO DP013-DB2-TABLE-NAME (1)            
00638          SET DP013-DB2-ABEND                                              
00639              DP013-XCTL-DISPLAY-RESTART                                   
00640                                    TO TRUE                                
00641          PERFORM DP013-0000-PROCESS-ABEND                                 
00642      END-IF.                                                              
00643 *                                                                         
00644  EJECT                                                                    
00645 *----------------------------------------------------------------*        
00646 * DETERMINE IF THE STORE/DOCUMENT SENT FROM THE CRITERIA SPEC    *        
00647 * IS A VALID STORE/DOCUMENT ON THE CURRENT HEADER TABLE.         *        
00648 *----------------------------------------------------------------*        
00649 *                                                                         
00650  3310-VALIDATE-DOCUMENT.                                                  
00651      PERFORM                                                              
00652          WITH TEST AFTER                                                  
00653          VARYING PV-RETRY-COUNTER                                         
00654          FROM 1 BY 1                                                      
00655          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00656                    OR (SQLCODE = ZERO)                                    
00657                    OR (SQLCODE = +100))                                   
00658 *                                                                         
00659          EXEC SQL                                                         
                   SELECT 1                                                     
                   INTO :PV-DUMMY-NBR                                           
                   FROM TSTRPPC                                                 
                   WHERE DEST_LOC_NBR = :DK-STORE-NUMBER                        
                     AND DOC_NBR      = :DK-DOCUMENT-NUMBER                     
                     AND DOC_TYP_CDE  = :PE049-STORE-DOC-TYPE-CDE               
00669          END-EXEC                                                         
00670                                                                           
00671          EVALUATE TRUE                                                    
00672              WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -811                                          
00673              WHEN SQLCODE = -904                                          
00674              WHEN SQLCODE = -913                                          
00675                  SET ASC-NOT-HISTORY  TO TRUE                             
00676              WHEN SQLCODE = +100                                          
00677                  PERFORM 3320-CHECK-HISTORY                               
00678              WHEN SQLWARN0 NOT = SPACES                                   
00679              WHEN SQLCODE < ZERO                                          
00680              WHEN SQLCODE > ZERO                                          
00681                  MOVE '3310-VALIDATE-DOCUMENT'                            
00682                                    TO DP013-PARAGRAPH                     
00683                  MOVE 'SELECT THE STORE/DOCUMENT FROM THE HEADER'         
00684                                    TO DP013-MESSAGE-TEXT (1)              
00685                  MOVE SQLCA        TO DP013-SQLCA                         
00686                  MOVE 'TSTRPPC'    TO DP013-DB2-TABLE-NAME (1)            
00687                  SET DP013-DB2-ABEND                                      
00688                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00689                  PERFORM DP013-0000-PROCESS-ABEND                         
00690          END-EVALUATE                                                     
00691      END-PERFORM.                                                         
00692 *                                                                         
00693      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00694          MOVE '3310-VALIDATE-DOCUMENT'                                    
00695                                    TO DP013-PARAGRAPH                     
00696          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
00697                                    TO DP013-MESSAGE-TEXT (1)              
00698          MOVE 'STORE/DOCUMENT FROM TSTRPPC'                               
00699                                    TO DP013-MESSAGE-TEXT (2)              
00700          MOVE SQLCA                TO DP013-SQLCA                         
00701          MOVE 'TSTRPPC'            TO DP013-DB2-TABLE-NAME (1)            
00702          SET DP013-DB2-ABEND                                              
00703              DP013-XCTL-DISPLAY-RESTART                                   
00704                                    TO TRUE                                
00705          PERFORM DP013-0000-PROCESS-ABEND                                 
00706      END-IF.                                                              
00707 *                                                                         
00708  EJECT                                                                    
00709 *----------------------------------------------------------------*        
00710 * SINCE THE STORE/DOCUMENT WAS NOT FOUND ON THE CURRENT FILE,    *        
00711 * CHECK TO SEE IF IT IS ON THE HISTORY HEADER TABLE.             *        
00712 *----------------------------------------------------------------*        
00713 *                                                                         
00714  3320-CHECK-HISTORY.                                                      
00715      PERFORM                                                              
00716          WITH TEST AFTER                                                  
00717          VARYING PV-RETRY-COUNTER                                         
00718          FROM 1 BY 1                                                      
00719          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00720                    OR (SQLCODE = ZERO)                                    
00721                    OR (SQLCODE = +100))                                   
00722 *                                                                         
00723          EXEC SQL                                                         
                   SELECT 1                                                     
                   INTO :PV-DUMMY-NBR                                           
                   FROM TSTRPCH                                                 
                   WHERE DEST_LOC_NBR = :DK-STORE-NUMBER                        
                     AND DOC_NBR      = :DK-DOCUMENT-NUMBER                     
                     AND DOC_TYP_CDE  = :PE049-STORE-DOC-TYPE-CDE               
00733          END-EXEC                                                         
00734                                                                           
00735          EVALUATE TRUE                                                    
00736              WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -811                                          
00737              WHEN SQLCODE = -904                                          
00738              WHEN SQLCODE = -913                                          
00739                  SET ASC-HISTORY      TO TRUE                             
00740              WHEN SQLCODE = +100                                          
00741                  MOVE PC-TSYMSG-00283                                     
00742                                    TO DP020-MSG-NUMBER                    
00743 *    DOCUMENT WAS NOT ISSUED TO THAT LOCATION                             
00744                  SET PS-ERROR                                             
00745                      DP020-MSG-FATAL                                      
00746                      DP020-NEXT-ACT-APPL-ERROR                            
00747                                    TO TRUE                                
00748              WHEN SQLWARN0 NOT = SPACES                                   
00749              WHEN SQLCODE < ZERO                                          
00750              WHEN SQLCODE > ZERO                                          
00751                  MOVE '3320-CHECK-HISTORY'                                
00752                                    TO DP013-PARAGRAPH                     
00753                  MOVE 'SELECT THE STORE/DOCUMENT FROM THE HEADER'         
00754                                    TO DP013-MESSAGE-TEXT (1)              
00755                  MOVE SQLCA        TO DP013-SQLCA                         
00756                  MOVE 'TSTRPCH'    TO DP013-DB2-TABLE-NAME (1)            
00757                  SET DP013-DB2-ABEND                                      
00758                      DP013-XCTL-DISPLAY-RESTART TO TRUE                   
00759                  PERFORM DP013-0000-PROCESS-ABEND                         
00760          END-EVALUATE                                                     
00761      END-PERFORM.                                                         
00762 *                                                                         
00763      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00764          MOVE '3320-CHECK-HISTORY' TO DP013-PARAGRAPH                     
00765          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
00766                                    TO DP013-MESSAGE-TEXT (1)              
00767          MOVE 'STORE/DOCUMENT FROM TSTRPPC'                               
00768                                    TO DP013-MESSAGE-TEXT (2)              
00769          MOVE SQLCA                TO DP013-SQLCA                         
00770          MOVE 'TSTRPCH'            TO DP013-DB2-TABLE-NAME (1)            
00771          SET DP013-DB2-ABEND                                              
00772              DP013-XCTL-DISPLAY-RESTART                                   
00773                                    TO TRUE                                
00774          PERFORM DP013-0000-PROCESS-ABEND                                 
00775      END-IF.                                                              
00776 *                                                                         
00777  EJECT                                                                    
00778 *----------------------------------------------------------------*        
00779 *    VALIDATE THE QUEUE WAS PASSED AND DISPLAY FOR THE FIRST     *        
00780 *    ENTRY.                                                      *        
00781 *----------------------------------------------------------------*        
00782                                                                           
00783  4000-BUILD-INITIAL-PANEL.                                                
00784      SET ASC-NOT-HISTORY                                                  
00785          DP030-SET-CURSOR-APPL-1   TO TRUE.                               
00786      MOVE ASC-STORE-NUMBER-N       TO DK-STORE-NUMBER.                    
00787      MOVE ASC-DOCUMENT-NUMBER-N    TO DK-DOCUMENT-NUMBER.                 
00788      PERFORM 4100-GET-DOCUMENT.                                           
00789      MOVE STRPPC-STATUS-CDE        TO ASC-STATUS-CODE.                    
00790      MOVE ASC-STATUS-CODE          TO                                     
00791                                    PE049-WORKING-INSTR-STATUS-CDE.        
00792      EVALUATE TRUE                                                        
00793          WHEN PE049-IS-AVAILABLE-STAT-CDE                                 
00794              MOVE PE049-AVAILABLE-STATUS-DESC                             
00795                                    TO ASC-STATUS-DESC                     
00796          WHEN PE049-IS-CNTS-ENTER-STAT-CDE                                
00797              MOVE PE049-CNTS-ENTERED-STATUS-DESC                          
00798                                    TO ASC-STATUS-DESC                     
00799          WHEN PE049-IS-CANCELLED-STAT-CDE                                 
00800              MOVE PE049-CANCELLED-STATUS-DESC                             
00801                                    TO ASC-STATUS-DESC                     
00802          WHEN PE049-IS-GENERATED-STAT-CDE                                 
00803              MOVE PE049-GENERATED-STATUS-DESC                             
00804                                    TO ASC-STATUS-DESC                     
00805          WHEN PE049-IS-DELINQUENT-STAT-CDE                                
00806              MOVE PE049-DELINQUENT-STATUS-DESC                            
00807                                    TO ASC-STATUS-DESC                     
00808          WHEN PE049-IS-COMPLETED-STAT-CDE                                 
00809              MOVE PE049-COMPLETE-STATUS-DESC                              
00810                                    TO ASC-STATUS-DESC                     
00811          WHEN PE049-IS-HISTORY-STAT-CDE                                   
00812              MOVE PE049-HISTORY-STATUS-DESC                               
00813                                    TO ASC-STATUS-DESC                     
00814      END-EVALUATE.                                                        
00815      MOVE STRPPC-TYP-CDE           TO PV-HOLD-TYPE-CODE.                  
00816      MOVE STRPPC-RSN-CDE           TO PV-HOLD-REASON-CODE-N.              
00817 *                                                                         
00818      MOVE PV-HOLD-TYPE-CODE        TO PE049-WORKING-TYPE-CDE.             
00819      EVALUATE TRUE                                                        
00820          WHEN PE049-MARKDOWN                                              
00821              MOVE PE049-SHORT-MKDN-DESC1                                  
00822                                    TO ASC-TYPE-DESC                       
00823          WHEN PE049-MARKUP                                                
00824              MOVE PE049-SHORT-MKUP-DESC1                                  
00825                                    TO ASC-TYPE-DESC                       
00826          WHEN PE049-MARKDOWN-CANCEL                                       
00827              MOVE PE049-SHORT-MKDN-CANCEL-DESC                            
00828                                    TO ASC-TYPE-DESC                       
00829          WHEN PE049-MARKUP-CANCEL                                         
00830              MOVE PE049-SHORT-MKUP-CANCEL-DESC                            
00831                                    TO ASC-TYPE-DESC                       
00832          WHEN OTHER                                                       
00833              MOVE SPACES           TO ASC-TYPE-DESC                       
00834      END-EVALUATE.                                                        
00835 *                                                                         
00836      MOVE PV-HOLD-REASON-CODE-N    TO                                     
00837                                    PE049-WORKING-INSTR-RSN-CDE.           
00838      EVALUATE TRUE                                                        
00839          WHEN PE049-IS-ZERO-OUT-RSN                                       
00840              MOVE PE049-IS-SHORT-ZERO-OUT-DESC                            
00841                                    TO ASC-REASON-DESC                     
               WHEN PE049-IS-RF-ZERO-OUT-RSN                                    
                   MOVE PE049-IS-SHORT-RF-ZERO-OUT                              
                                         TO ASC-REASON-DESC                     
               WHEN PE049-IS-RTN-TO-VNDR-RSN                                    
                   MOVE PE049-IS-SHORT-RTN-TO-VNDR                              
                                         TO ASC-REASON-DESC                     
00842          WHEN PE049-IS-CORRECTION-RSN                                     
00843              MOVE PE049-IS-SHORT-CORRECTION-DESC                          
00844                                    TO ASC-REASON-DESC                     
00845          WHEN PE049-IS-BUYER-GEN-MISSED-RSN                               
00846              MOVE PE049-IS-SHORT-BYR-MISSED-DESC                          
00847                                    TO ASC-REASON-DESC                     
00848          WHEN PE049-IS-CUSTOMER-RET-RSN                                   
00849              MOVE PE049-IS-SHORT-CUST-RET-DESC                            
00850                                    TO ASC-REASON-DESC                     
00851          WHEN PE049-IS-KNOWN-THEFT-RSN                                    
00852              MOVE PE049-IS-SHORT-THEFT-DESC                               
00853                                    TO ASC-REASON-DESC                     
00854          WHEN PE049-IS-INSTR-DAMAGE-RSN                                   
00855              MOVE PE049-IS-SHORT-INSTR-DMG-DESC                           
00856                                    TO ASC-REASON-DESC                     
               WHEN PE049-IS-COSMETICS-TESTER-RSN                               
                   MOVE PE049-IS-SHORT-CSMTCS-TST-DESC                          
                                           TO ASC-REASON-DESC                   
               WHEN PE049-IS-MOS-SALVAGE-RSN                                    
                   MOVE PE049-IS-SHORT-MOS-SALVAGE                              
                                           TO ASC-REASON-DESC                   
00857          WHEN PE049-IS-FINANCIAL-ADJ-RSN                                  
00858              MOVE PE049-IS-SHORT-FIN-ADJ-DESC                             
00859                                    TO ASC-REASON-DESC                     
00860          WHEN OTHER                                                       
00861              MOVE SPACES           TO ASC-REASON-DESC                     
00862      END-EVALUATE.                                                        
00863 *                                                                         
00864      IF (PV-HOLD-TYPE-CODE = ZERO                                         
00865       OR PV-HOLD-REASON-CODE = ZERO)                                      
00866          MOVE SPACES               TO ASC-REASON-CODE                     
00867      ELSE                                                                 
00868          MOVE PV-HOLD-TYPE-REASON  TO ASC-REASON-CODE                     
00869      END-IF.                                                              
00870      MOVE STRPPC-DEPT-AREA-DEST    TO ASC-DEPT-AREA-TEXT.                 
00871      MOVE STRPPC-CRE-DTE           TO ASC-ISSUED-DATE.                    
00872      MOVE STRPPC-CRE-ID            TO ASC-ISSUED-ID.                      
00873      MOVE STRPPC-CAN-DTE           TO ASC-CANCELLED-DATE.                 
00874      MOVE STRPPC-CAN-ID            TO ASC-CANCELLED-ID.                   
00875      MOVE STRPPC-ENTR-DTE          TO ASC-ENTERED-DATE.                   
00876      MOVE STRPPC-ENTR-ID           TO ASC-ENTERED-ID.                     
00877      MOVE STRPPC-EFF-DTE           TO ASC-EFFECTIVE-DATE.                 
00878      MOVE STRPPC-PR-CHG-CPL-NAME   TO ASC-COMPLETED-NAME.                 
00879      MOVE STRPPC-TOT-ACT-PR-CHG-AMT                                       
00880                                    TO ASC-TOT-ACT-PR-CHG-AMT.             
00881 *                                                                         
00882      IF  SQLCODE = ZERO                                                   
00883          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
00884      END-IF.                                                              
00885 *                                                                         
00886  EJECT                                                                    
00887 *----------------------------------------------------------------*        
00888 * SELECT A ROW FROM THE DB2 IN-STORE DOCUMENT HEADER TABLE.      *        
00889 *----------------------------------------------------------------*        
00890  4100-GET-DOCUMENT.                                                       
00891      PERFORM                                                              
00892          WITH TEST AFTER                                                  
00893          VARYING PV-RETRY-COUNTER                                         
00894          FROM 1 BY 1                                                      
00895          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00896                    OR (SQLCODE = ZERO)                                    
00897                    OR (SQLCODE = +100))                                   
00898 *                                                                         
00899          EXEC SQL                                                         
00900              SELECT STATUS_CDE,                                           
00901                     TYP_CDE,                                              
00902                     RSN_CDE,                                              
00903                     DEPT_AREA_DEST,                                       
00904                     CRE_DTE,                                              
00905                     CRE_ID,                                               
00906                     CAN_DTE,                                              
00907                     CAN_ID,                                               
00908                     ENTR_DTE,                                             
00909                     ENTR_ID,                                              
00910                     EFF_DTE,                                              
00911                     PR_CHG_CPL_NAME,                                      
00912                     TOT_ACT_PR_CHG_AMT                                    
00913              INTO   :STRPPC-STATUS-CDE,                                   
00914                     :STRPPC-TYP-CDE,                                      
00915                     :STRPPC-RSN-CDE,                                      
00916                     :STRPPC-DEPT-AREA-DEST,                               
00917                     :STRPPC-CRE-DTE,                                      
00918                     :STRPPC-CRE-ID,                                       
00919                     :STRPPC-CAN-DTE,                                      
00920                     :STRPPC-CAN-ID,                                       
00921                     :STRPPC-ENTR-DTE,                                     
00922                     :STRPPC-ENTR-ID,                                      
00923                     :STRPPC-EFF-DTE,                                      
00924                     :STRPPC-PR-CHG-CPL-NAME,                              
00925                     :STRPPC-TOT-ACT-PR-CHG-AMT                            
00926              FROM   TSTRPPC                                               
00928              WHERE (DEST_LOC_NBR = :DK-STORE-NUMBER                       
00929                AND  DOC_NBR      = :DK-DOCUMENT-NUMBER                    
00930                AND  DOC_TYP_CDE  = :PE049-STORE-DOC-TYPE-CDE)             
00931          END-EXEC                                                         
00932                                                                           
00933          EVALUATE TRUE                                                    
00934              WHEN SQLCODE = ZERO                                          
00935              WHEN SQLCODE = -904                                          
00936              WHEN SQLCODE = -913                                          
00937                  SET ASC-NOT-HISTORY  TO TRUE                             
00938              WHEN SQLCODE = +100                                          
00939                  PERFORM 4110-GET-HISTORY-DOCUMENT                        
00940              WHEN SQLWARN0 NOT = SPACES                                   
00941              WHEN SQLCODE < ZERO                                          
00942              WHEN SQLCODE > ZERO                                          
00943                  MOVE '4100-GET-DOCUMENT'                                 
00944                                    TO DP013-PARAGRAPH                     
00945                  MOVE 'SELECT STORE/DOUCMENT NUMBER'                      
00946                                    TO DP013-MESSAGE-TEXT (1)              
00947                  MOVE SQLCA        TO DP013-SQLCA                         
00948                  MOVE 'TSTRPPC'    TO DP013-DB2-TABLE-NAME (1)            
00949                  SET DP013-DB2-ABEND                                      
00950                      DP013-XCTL-DISPLAY-RESTART                           
00951                                    TO TRUE                                
00952                  PERFORM DP013-0000-PROCESS-ABEND                         
00953          END-EVALUATE                                                     
00954      END-PERFORM.                                                         
00955 *                                                                         
00956      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
00957          MOVE '4100-GET-DOCUMENT'                                         
00958                                    TO DP013-PARAGRAPH                     
00959          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
00960                                    TO DP013-MESSAGE-TEXT (1)              
00961          MOVE 'STORE/DOCUMENT NUMBER'                                     
00962                                    TO DP013-MESSAGE-TEXT (2)              
00963          MOVE SQLCA                TO DP013-SQLCA                         
00964          MOVE 'TSTRPPC'            TO DP013-DB2-TABLE-NAME (1)            
00965          SET DP013-DB2-ABEND                                              
00966              DP013-XCTL-DISPLAY-RESTART                                   
00967                                    TO TRUE                                
00968          PERFORM DP013-0000-PROCESS-ABEND                                 
00969      END-IF.                                                              
00970 *                                                                         
00971  EJECT                                                                    
00972 *----------------------------------------------------------------*        
00973 * SELECT A ROW FROM THE DB2 IN-STORE DOCUMENT HISTORY HEADER     *        
00974 * TABLE.                                                         *        
00975 *----------------------------------------------------------------*        
00976  4110-GET-HISTORY-DOCUMENT.                                               
00977      PERFORM                                                              
00978          WITH TEST AFTER                                                  
00979          VARYING PV-RETRY-COUNTER                                         
00980          FROM 1 BY 1                                                      
00981          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
00982                    OR (SQLCODE = ZERO)                                    
00983                    OR (SQLCODE = +100))                                   
00984 *                                                                         
00985          EXEC SQL                                                         
00986              SELECT STATUS_CDE,                                           
00987                     TYP_CDE,                                              
00988                     RSN_CDE,                                              
00989                     DEPT_AREA_DEST,                                       
00990                     CRE_DTE,                                              
00991                     CRE_ID,                                               
00992                     CAN_DTE,                                              
00993                     CAN_ID,                                               
00994                     ENTR_DTE,                                             
00995                     ENTR_ID,                                              
00996                     EFF_DTE,                                              
00997                     PR_CHG_CPL_NAME,                                      
00998                     TOT_ACT_PR_CHG_AMT                                    
00999              INTO   :STRPPC-STATUS-CDE,                                   
01000                     :STRPPC-TYP-CDE,                                      
01001                     :STRPPC-RSN-CDE,                                      
01002                     :STRPPC-DEPT-AREA-DEST,                               
01003                     :STRPPC-CRE-DTE,                                      
01004                     :STRPPC-CRE-ID,                                       
01005                     :STRPPC-CAN-DTE,                                      
01006                     :STRPPC-CAN-ID,                                       
01007                     :STRPPC-ENTR-DTE,                                     
01008                     :STRPPC-ENTR-ID,                                      
01009                     :STRPPC-EFF-DTE,                                      
01010                     :STRPPC-PR-CHG-CPL-NAME,                              
01011                     :STRPPC-TOT-ACT-PR-CHG-AMT                            
01012              FROM   TSTRPCH                                               
01014              WHERE (DEST_LOC_NBR = :DK-STORE-NUMBER                       
01015                AND  DOC_NBR      = :DK-DOCUMENT-NUMBER                    
01016                AND  DOC_TYP_CDE  = :PE049-STORE-DOC-TYPE-CDE)             
01017          END-EXEC                                                         
01018                                                                           
01019          EVALUATE TRUE                                                    
01020              WHEN SQLCODE = ZERO                                          
01021              WHEN SQLCODE = -904                                          
01022              WHEN SQLCODE = -913                                          
01023                  SET ASC-HISTORY  TO TRUE                                 
01024              WHEN SQLWARN0 NOT = SPACES                                   
01025              WHEN SQLCODE < ZERO                                          
01026              WHEN SQLCODE > ZERO                                          
01027                  MOVE '4110-GET-HISTORY-DOCUMENT'                         
01028                                    TO DP013-PARAGRAPH                     
01029                  MOVE 'SELECT STORE/DOUCMENT NUMBER'                      
01030                                    TO DP013-MESSAGE-TEXT (1)              
01031                  MOVE SQLCA        TO DP013-SQLCA                         
01032                  MOVE 'TSTRPCH'    TO DP013-DB2-TABLE-NAME (1)            
01033                  SET DP013-DB2-ABEND                                      
01034                      DP013-XCTL-DISPLAY-RESTART                           
01035                                    TO TRUE                                
01036                  PERFORM DP013-0000-PROCESS-ABEND                         
01037          END-EVALUATE                                                     
01038      END-PERFORM.                                                         
01039 *                                                                         
01040      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
01041          MOVE '4110-GET-HISTORY-DOCUMENT'                                 
01042                                    TO DP013-PARAGRAPH                     
01043          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
01044                                    TO DP013-MESSAGE-TEXT (1)              
01045          MOVE 'STORE/DOCUMENT NUMBER'                                     
01046                                    TO DP013-MESSAGE-TEXT (2)              
01047          MOVE SQLCA                TO DP013-SQLCA                         
01048          MOVE 'TSTRPCH'            TO DP013-DB2-TABLE-NAME (1)            
01049          SET DP013-DB2-ABEND                                              
01050              DP013-XCTL-DISPLAY-RESTART                                   
01051                                    TO TRUE                                
01052          PERFORM DP013-0000-PROCESS-ABEND                                 
01053      END-IF.                                                              
01054 *                                                                         
01055  EJECT                                                                    
01056 *----------------------------------------------------------------*        
01057 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
01058 *----------------------------------------------------------------*        
01059                                                                           
01060  4400-MOVE-COMMAREA-TO-SCREEN.                                            
01061      MOVE ASC-STORE-NUMBER         TO ASTRNO.                             
01062      MOVE ASC-STORE-NAME           TO ASTRNMO.                            
01063      MOVE ASC-DOCUMENT-NUMBER      TO ADOCNO.                             
01064      MOVE ASC-STATUS-CODE          TO ASTATO.                             
01065      MOVE ASC-STATUS-DESC          TO ASTATDO.                            
01066      MOVE ASC-REASON-CODE          TO ARSNO.                              
01067      MOVE ASC-TYPE-DESC            TO ATYPDO.                             
01068      MOVE ASC-REASON-DESC          TO ARSNDO.                             
01069      MOVE ASC-DEPT-AREA-TEXT       TO ADPTARO.                            
01070      MOVE ASC-ISSUED-DATE          TO PV-HOLD-ISO-DATE-FMT.               
01071      MOVE PV-HOLD-ISO-DATE-MM      TO PV-HOLD-DISPLAY-DATE-MM.            
01072      MOVE PV-HOLD-ISO-DATE-DD      TO PV-HOLD-DISPLAY-DATE-DD.            
01073      MOVE PV-HOLD-ISO-DATE-YY      TO PV-HOLD-DISPLAY-DATE-YY.            
01074      MOVE PV-HOLD-DISPLAY-DATE-FMT TO AISSDTO.                            
01075      MOVE ASC-ISSUED-ID            TO AISSIDO.                            
01076                                                                           
01077      IF ASC-CANCELLED-DATE = PE049-DEFAULT-DATE                           
01078          MOVE SPACES               TO ACANDTO                             
01079                                       ACANIDO                             
01080      ELSE                                                                 
01081          MOVE ASC-CANCELLED-DATE   TO PV-HOLD-ISO-DATE-FMT                
01082          MOVE PV-HOLD-ISO-DATE-MM  TO PV-HOLD-DISPLAY-DATE-MM             
01083          MOVE PV-HOLD-ISO-DATE-DD  TO PV-HOLD-DISPLAY-DATE-DD             
01084          MOVE PV-HOLD-ISO-DATE-YY  TO PV-HOLD-DISPLAY-DATE-YY             
01085          MOVE PV-HOLD-DISPLAY-DATE-FMT                                    
01086                                    TO ACANDTO                             
01087          MOVE ASC-CANCELLED-ID     TO ACANIDO                             
01088      END-IF.                                                              
01089                                                                           
01090      IF ASC-ENTERED-DATE = PE049-DEFAULT-DATE                             
01091          MOVE SPACES               TO AENTDTO                             
01092                                       AENTIDO                             
01093      ELSE                                                                 
01094          MOVE ASC-ENTERED-DATE     TO PV-HOLD-ISO-DATE-FMT                
01095          MOVE PV-HOLD-ISO-DATE-MM  TO PV-HOLD-DISPLAY-DATE-MM             
01096          MOVE PV-HOLD-ISO-DATE-DD  TO PV-HOLD-DISPLAY-DATE-DD             
01097          MOVE PV-HOLD-ISO-DATE-YY  TO PV-HOLD-DISPLAY-DATE-YY             
01098          MOVE PV-HOLD-DISPLAY-DATE-FMT                                    
01099                                    TO AENTDTO                             
01100          MOVE ASC-ENTERED-ID       TO AENTIDO                             
01101      END-IF.                                                              
01102                                                                           
01103      IF ASC-EFFECTIVE-DATE = PE049-DEFAULT-DATE                           
01104          MOVE SPACES               TO AEFFDTO                             
01105                                       ACPLNMO                             
01106      ELSE                                                                 
01107          MOVE ASC-EFFECTIVE-DATE   TO PV-HOLD-ISO-DATE-FMT                
01108          MOVE PV-HOLD-ISO-DATE-MM  TO PV-HOLD-DISPLAY-DATE-MM             
01109          MOVE PV-HOLD-ISO-DATE-DD  TO PV-HOLD-DISPLAY-DATE-DD             
01110          MOVE PV-HOLD-ISO-DATE-YY  TO PV-HOLD-DISPLAY-DATE-YY             
01111          MOVE PV-HOLD-DISPLAY-DATE-FMT                                    
01112                                    TO AEFFDTO                             
01113          MOVE ASC-COMPLETED-NAME   TO ACPLNMO                             
01114      END-IF.                                                              
01115                                                                           
01116      MOVE ASC-TOT-ACT-PR-CHG-AMT   TO PV-TOT-ACT-PR-CHG-AMT               
01117      MOVE PV-TOT-ACT-PR-CHG-AMT    TO ATPRCHGO.                           
01118 *                                                                         
01119  EJECT                                                                    
01120 *----------------------------------------------------------------*        
01121 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
01122 *----------------------------------------------------------------*        
01123                                                                           
01124  9100-READ-SEL-QUEUE.                                                     
01125      EXEC CICS                                                            
01126          READQ TS                                                         
01127              QUEUE (ASC-SEL-QUEUE-NAME)                                   
01128              INTO  (PE067-DOCUMENT-SEL-REC)                               
01129              ITEM  (ASC-SEL-ITEM)                                         
01130              RESP  (PV-CICS-RESPONSE)                                     
01131      END-EXEC.                                                            
01132                                                                           
01133      IF  PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
01134          CONTINUE                                                         
01135      ELSE                                                                 
01136          SET DP013-LOGIC-ABEND                                            
01137              DP013-NO-ROLLBACK     TO TRUE                                
01138          MOVE '9100-READ-SEL-QUEUE'                                       
01139                                    TO DP013-PARAGRAPH                     
01140          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
01141                                    TO DP013-MESSAGE-TEXT (1)              
01142          PERFORM DP013-0000-PROCESS-ABEND                                 
01143      END-IF.                                                              
01144 *                                                                         
01145  EJECT                                                                    
01146 *----------------------------------------------------------------*        
01147 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
01148 *----------------------------------------------------------------*        
01149  9200-REWRITE-LIST-QUEUE.                                                 
01150      EXEC CICS                                                            
01151           WRITEQ TS                                                       
01152               QUEUE  (ASC-SEL-QUEUE-NAME)                                 
01153               FROM   (PE067-DOCUMENT-SEL-REC)                             
01154               ITEM   (ASC-SEL-ITEM)                                       
01155               RESP   (PV-CICS-RESPONSE)                                   
01156               REWRITE                                                     
01157      END-EXEC.                                                            
01158                                                                           
01159      IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)                       
01160          SET DP013-CICS-ABEND                                             
01161              DP013-NO-ROLLBACK     TO TRUE                                
01162          MOVE '9200-REWRITE-LIST-QUEUE'                                   
01163                                    TO DP013-PARAGRAPH                     
01164          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
01165                                    TO DP013-MESSAGE-TEXT (1)              
01166          PERFORM DP013-0000-PROCESS-ABEND                                 
01167      END-IF.                                                              
01168 *                                                                         
01169  EJECT                                                                    
01170 *----------------------------------------------------------------*        
01171 *    DATE PROCESSOR MODULE                                       *        
01172 *----------------------------------------------------------------*        
01173                                                                           
01174      COPY DPPD017.                                                        
01175 *                                                                         
01176  EJECT                                                                    
01177 *----------------------------------------------------------------*        
01178 *    ABEND PROCESSOR MODULE                                      *        
01179 *----------------------------------------------------------------*        
01180                                                                           
01181      COPY DPPD013.                                                        
