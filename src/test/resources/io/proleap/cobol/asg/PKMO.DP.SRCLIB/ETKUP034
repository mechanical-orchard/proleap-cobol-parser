00001  ID  DIVISION.                                                    06/13/95
00002  PROGRAM-ID.         ETKUP034.                                    ETKUP034
00003  AUTHOR.             STEVE FLUNKER.                                  LV032
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     ETKUP034
00005  DATE-WRITTEN.       MAY 1995.                                       CL**3
00006  DATE-COMPILED.                                                   ETKUP034
00007                                                                   ETKUP034
00008 ******************************************************************ETKUP034
00009 *  ETKUP034 - PO/UPC RECYCLE UTILITY REVALIDATION                    CL**4
00010 *  WORK REQUEST #  - 3044                                         ETKUP034
00011 ******************************************************************ETKUP034
00012 *  THIS PROGRAM WILL REVALIDATE HELD PURCHASE ORDERS                 CL**2
************************************************************************ETKUP034
JF0403*  DATE  04/22/03                                                         
JF0403*  CHANGE MADE:                                                           
JF0403*   UPDATED PROGRAM SO IT IS COMPLIANT WITH NI-SKU OBJECTIVES.            
JF0403*  MADE BY: JON FIEDLER             WR# : 22777                           
************************************************************************        
JF0304*  DATE  03/22/04                                                         
JF0304*  CHANGE MADE:                                                           
JF0304*   ADD CANCEL DATE TO 855 APPLICATION FILE.                              
JF0304*  MADE BY: JON FIEDLER             WR# : 26812                           
************************************************************************        
************************************************************************        
WG0610*  DATE  06/10/10                                                         
WG0610*  CHANGE MADE:                                                           
WG0610*   ADD PARENT-ENT-ID AND FACTORY-ENT-ID.                                 
WG0610*  MADE BY: WADE GROVE              WR# : 36565                           
************************************************************************        
00014 *                                                                 ETKUP034
00015  ENVIRONMENT DIVISION.                                            ETKUP034
00016 *                                                                 ETKUP034
00017  CONFIGURATION SECTION.                                           ETKUP034
00018 *                                                                 ETKUP034
00019  SOURCE-COMPUTER.        IBM-3090.                                ETKUP034
00020  OBJECT-COMPUTER.        IBM-3090.                                ETKUP034
00021 *                                                                 ETKUP034
00022  INPUT-OUTPUT SECTION.                                            ETKUP034
00023  FILE-CONTROL.                                                    ETKUP034
00024      SELECT  PO-INPUT-FILE                                        ETKUP034
00025          ASSIGN TO INP01.                                         ETKUP034
00026                                                                   ETKUP034
00027      SELECT  PO-STATUS-FILE                                       ETKUP034
00028          ASSIGN TO OUT01.                                         ETKUP034
00029                                                                   ETKUP034
00030  DATA DIVISION.                                                   ETKUP034
00031  FILE SECTION.                                                    ETKUP034
00032                                                                   ETKUP034
00033  FD  PO-INPUT-FILE                                                ETKUP034
00034      LABEL RECORDS ARE STANDARD                                   ETKUP034
00035      RECORDING MODE IS F                                          ETKUP034
00036      BLOCK CONTAINS 0 RECORDS                                     ETKUP034
00037      RECORD CONTAINS 384 CHARACTERS                                  CL**4
00038      DATA RECORD IS PO-INPUT-RECORD.                              ETKUP034
00039                                                                   ETKUP034
00040  01  PO-INPUT-RECORD              PIC  X(384).                       CL**9
00041                                                                   ETKUP034
00042  FD  PO-STATUS-FILE                                               ETKUP034
00043      LABEL RECORDS ARE STANDARD                                   ETKUP034
00044      RECORDING MODE IS F                                          ETKUP034
00045      BLOCK CONTAINS 0 RECORDS                                     ETKUP034
00046      RECORD CONTAINS 385 CHARACTERS                                  CL**4
00047      DATA RECORD IS PO-INPUT-RECORD.                              ETKUP034
00048                                                                   ETKUP034
00049  01  PO-STATUS-RECORD             PIC  X(385).                       CL*15
00050                                                                   ETKUP034
00051  WORKING-STORAGE SECTION.                                         ETKUP034
00052 *                                                                 ETKUP034
00053  01  WS-WORKING-STORAGE.                                          ETKUP034
00054      05  WS-NOSKU.                                                ETKUP034
00055          10  FILLER                 PIC X(05) VALUE 'NOSKU'.      ETKUP034
00056          10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.         ETKUP034
00057                                                                   ETKUP034
00058      05  WS-HEADER-SAVE-AREA        PIC X(385) VALUE SPACES.         CL*12
00059      05  WS-HEADER-RECORD.                                           CL*12
00060          10  WS-DB-KEY-HEADER-REC   PIC X(34) VALUE SPACES.          CL*12
00061          10  WS-PO-NBR              PIC 9(09) VALUE ZERO.            CL*12
00062          10  WS-SORT-SEQ-NBR        PIC 9(06) VALUE ZERO.            CL*12
00063          10  WS-PO-STATUS           PIC X(01) VALUE SPACE.           CL*20
00064          10  WS-PO-DEPT-NBR         PIC X(03) VALUE SPACES.          CL*28
00065          10  WS-DEPT-STATUS         PIC X(01) VALUE SPACE.           CL*20
00066          10  WS-LINE-STATUS         PIC X(01) VALUE SPACE.           CL*21
00067          10  FILLER                 PIC X(17) VALUE SPACES.          CL*21
00068          10  FILLER                 PIC X(01) VALUE '1'.             CL*21
JF0304         10  FILLER                 PIC X(02) VALUE SPACES.          CL*21
JF0304         10  WS-CANCEL-DATE         PIC X(06) VALUE SPACES.          CL*12
00070          10  WS-SHIP-DATE           PIC X(06) VALUE SPACES.          CL*12
00071          10  WS-TP-ID-QUAL          PIC X(02) VALUE SPACES.          CL*12
00072          10  WS-TP-ID               PIC X(15) VALUE SPACES.          CL*12
00073          10  FILLER                 PIC X(01) VALUE SPACE.           CL*21
00072          10  WS-ISA-CNTL-NBR        PIC 9(09) VALUE ZEROS.           CL*12
00072          10  WS-GS-CNTL-NBR         PIC 9(09) VALUE ZEROS.           CL*12
00072          10  WS-ST-CNTL-NBR         PIC X(09) VALUE SPACES.          CL*12
00072          10  WS-TIMESTAMP           PIC X(26) VALUE SPACES.          CL*12
00072          10  WS-SESSION-NBR         PIC 9(06) VALUE ZEROS.           CL*12
SP0408         10  WS-LC-IND              PIC X(02) VALUE SPACES.          CL*12
WG0610         10  WS-PARENT-ID           PIC 9(10).                            
WG0610         10  WS-FACTORY-ID          PIC 9(10).                            
WG0610         10  FILLER                 PIC X(198) VALUE SPACES.              
00074          10  WS-UPDATE-FLAG         PIC X(01) VALUE SPACES.          CL*12
00075      05  WS-ASSIGNED-DEPT           PIC X(03) VALUE ZEROES.          CL*29
00076      05  WS-DEPT-NBR                PIC X(03) VALUE ZEROES.          CL*30
JF0403     05  WS-SAVE-DEPT1              PIC X(03) VALUE SPACES           CL*30
JF0403                                              JUSTIFIED RIGHT.            
JF0403     05  WS-SAVE-DEPT2              PIC X(03) VALUE SPACES           CL*30
JF0403                                              JUSTIFIED RIGHT.            
JF0403     05  WS-SAVE-SKU                PIC X(08) VALUE SPACES.          CL*30
JF0403     05  WS-PO003-I-SKU             PIC S9(8)V COMP-3.                    
00077      05  WS-PO003-H-PO-NBR          PIC S9(9) COMP.                  CL*16
00078      05  WS-PO003-I-UPC             PIC S9(15)V COMP-3.              CL*10
00079 *                                                                 ETKUP034
00080  01  PV-PROGRAM-VARIABLES.                                        ETKUP034
00081      05  PV-PARAGRAPH-NAME       PIC  X(30)   VALUE SPACES.       ETKUP034
00082      05  PV-DB2-OPER-ATTEMPTED   PIC  X(30)   VALUE SPACES.       ETKUP034
00083      05  PV-SQL-SUB              PIC S9(03)   VALUE +0            ETKUP034
00084                                               COMP.               ETKUP034
00085      05  PV-CURRENT-REC-NBR      PIC  9(06)   VALUE ZERO.         ETKUP034
00086      05  PV-RECS-THIS-PO         PIC  9(06)   VALUE ZERO.         ETKUP034
00087      05  PV-SORT-SEQ-NBR         PIC  9(06)   VALUE ZERO.         ETKUP034
00088 *                                                                 ETKUP034
00089  01  PC-PROGRAM-CONSTANTS.                                        ETKUP034
00090      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03           ETKUP034
00091                                               COMP.               ETKUP034
00092 *                                                                 ETKUP034
00093  01  PS-PROGRAM-SWITCHES.                                         ETKUP034
00094      05  PS-END-OF-FILE             PIC X     VALUE 'N'.          ETKUP034
00095          88  PS-EOF                           VALUE 'Y'.          ETKUP034
00096      05  PS-FIRST-HEADER            PIC X     VALUE 'Y'.          ETKUP034
00097          88  FIRST-HEADER                     VALUE 'Y'.          ETKUP034
00098          88  PAST-FIRST-HEADER                VALUE 'N'.          ETKUP034
00099      05  PS-REC-HAS-NOT-CHANGED     PIC X     VALUE 'N'.             CL*12
00100          88  REC-HAS-NOT-CHANGED              VALUE 'N'.             CL*12
00101          88  REC-HAS-CHANGED                  VALUE 'Y'.             CL*12
00102      05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.          ETKUP034
00103          88  ONE-DEPT                         VALUE 'Y'.          ETKUP034
00104          88  MULTIPLE-DEPTS                   VALUE 'N'.          ETKUP034
00105      05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.          ETKUP034
00106          88  ALL-GOOD-UPCS                    VALUE 'Y'.          ETKUP034
00107          88  NOT-ALL-GOOD-UPCS                VALUE 'N'.          ETKUP034
00108 *                                                                 ETKUP034
00109 ***************************************************************** ETKUP034
00110 *****  DATABASE EXTRACT FILE RECORD LAYOUT FOR HELD DOCUMENTS        CL*12
00111 ***************************************************************** ETKUP034
00112 *                                                                 ETKUP034
00113      COPY PORD003.                                                   CL*32
00114      EJECT                                                        ETKUP034
00115 *                                                                 ETKUP034
00116 ******************************************************************ETKUP034
00117 *  SQL ABEND WORKING STORAGE                                      ETKUP034
00118 ******************************************************************ETKUP034
00119      COPY DPWS004.                                                ETKUP034
00120      EJECT                                                        ETKUP034
00121 *                                                                 ETKUP034
00122 ***************************************************************** ETKUP034
00123 *****  DB2 P.O. DOCS ON HOLD DB                                      CL*28
00124 ***************************************************************** ETKUP034
00125 *                                                                    CL**4
00126      EXEC SQL                                                     ETKUP034
00127          INCLUDE TTRPOHL                                             CL*28
00128      END-EXEC.                                                    ETKUP034
00129 *                                                                    CL*28
JF0403*****************************************************************    CL*28
JF0403*****  DB2 SKU CROSS REFERENCE TABLE DCLGEN                          CL*28
JF0403*****************************************************************    CL*28
JF0403*                                                                    CL*28
JF0403     EXEC SQL                                                        CL*28
JF0403         INCLUDE TSKXREF                                             CL*28
JF0403     END-EXEC.                                                       CL*28
JF0403*                                                                 ETKUP034
00138 ***************************************************************** ETKUP034
00139 *****  DB2 PO TABLE DCLGEN                                        ETKUP034
00140 ***************************************************************** ETKUP034
00141 *                                                                    CL**4
00142      EXEC SQL                                                     ETKUP034
00143          INCLUDE TPURCHS                                          ETKUP034
00144      END-EXEC.                                                    ETKUP034
00145 *                                                                 ETKUP034
00146 ***************************************************************** ETKUP034
00147 *****  DB2 UPC / SKU TABLE DCLGEN                                 ETKUP034
00148 ***************************************************************** ETKUP034
00149 *                                                                    CL**4
00150      EXEC SQL                                                     ETKUP034
00151          INCLUDE TUPCSKU                                          ETKUP034
00152      END-EXEC.                                                    ETKUP034
00153 *                                                                 ETKUP034
00154 ***************************************************************** ETKUP034
00155 * DB2 COMMUNICATIONS AREA                                         ETKUP034
00156 ***************************************************************** ETKUP034
00157                                                                   ETKUP034
00158      EXEC SQL                                                     ETKUP034
00159          INCLUDE SQLCA                                            ETKUP034
00160      END-EXEC.                                                    ETKUP034
00161 *                                                                 ETKUP034
00162      COPY SQLCA2.                                                 ETKUP034
00163                                                                   ETKUP034
00164  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              ETKUP034
00165                                           COMP SYNC.              ETKUP034
00166      EJECT                                                        ETKUP034
00167  LINKAGE SECTION.                                                 ETKUP034
00168 *                                                                 ETKUP034
00169  PROCEDURE DIVISION.                                              ETKUP034
00170 *                                                                 ETKUP034
00171 ******************************************************************ETKUP034
00172 * MAIN-LINE LOGIC.                                                ETKUP034
00173 ******************************************************************ETKUP034
00174  A100-MAIN-MODULE.                                                ETKUP034
00175 *                                                                 ETKUP034
00176      OPEN INPUT  PO-INPUT-FILE.                                   ETKUP034
00177      OPEN OUTPUT PO-STATUS-FILE.                                  ETKUP034
00178 *                                                                 ETKUP034
00179      PERFORM Z100-READ-INPUT-FILE.                                ETKUP034
00180      SET NO-DB-ACTION TO TRUE.                                       CL*17
00181 *                                                                 ETKUP034
00182      SET FIRST-HEADER TO TRUE.                                    ETKUP034
00183 *                                                                 ETKUP034
00184      PERFORM B100-PROCESS-PO-FILE                                 ETKUP034
00185              UNTIL PS-EOF.                                        ETKUP034
00186 *                                                                 ETKUP034
00187      PERFORM B200-WRAPUP-PO-FILE.                                 ETKUP034
00188 *                                                                 ETKUP034
00189      CLOSE PO-INPUT-FILE                                          ETKUP034
00190            PO-STATUS-FILE.                                        ETKUP034
00191      STOP RUN.                                                    ETKUP034
00192 *                                                                 ETKUP034
00193 ******************************************************************ETKUP034
00194 *  PROCESS ALL PO RECORDS FROM INPUT FILE.                        ETKUP034
00195 ******************************************************************ETKUP034
00196  B100-PROCESS-PO-FILE.                                            ETKUP034
00197 *                                                                 ETKUP034
00198       IF PO003-HEADER-RECORD                                         CL*10
00199 * MOVE NEW HEADER TO SAVE AREA                                    ETKUP034
00200          MOVE PO003-EDI855-RECORD                                    CL*12
00201               TO WS-HEADER-SAVE-AREA                              ETKUP034
00202                                                                   ETKUP034
00203          IF FIRST-HEADER                                          ETKUP034
00204             SET PAST-FIRST-HEADER TO TRUE                         ETKUP034
00205          ELSE                                                     ETKUP034
00206 * MOVE OLD HEADER TO PO003 HEADER TO WRAP UP THAT PO                 CL*19
00207             MOVE WS-HEADER-RECORD                                 ETKUP034
00208                  TO PO003-EDI855-RECORD                              CL*12
00209                                                                      CL*12
00210             SET REC-HAS-NOT-CHANGED TO TRUE                          CL*14
00211             COMPUTE PV-SORT-SEQ-NBR =                                CL*12
00212                 PV-CURRENT-REC-NBR - PV-RECS-THIS-PO                 CL*12
00213                                                                      CL*12
00214             MOVE PV-SORT-SEQ-NBR TO PO003-H-SORT-SEQ-NBR             CL*12
00215 *                                                                 ETKUP034
00216             IF WS-DEPT-NBR = SPACES                               ETKUP034
00217                CONTINUE                                              CL*12
00218             ELSE                                                  ETKUP034
00219                IF ONE-DEPT                                        ETKUP034
00220                   IF PO003-MULTIPLE-DEPTS OR                         CL*12
00221                      PO003-NO-DEPT                                   CL*12
00222                      SET PO003-ONE-DEPT  TO TRUE                     CL*12
00223                      SET REC-HAS-CHANGED TO TRUE                     CL*12
00224                      MOVE WS-DEPT-NBR TO PO003-H-DEPT-NBR            CL*12
00225                   END-IF                                             CL*12
00226                ELSE                                                  CL*12
00227                   IF PO003-NO-DEPT OR                                CL*12
00228                      PO003-ONE-DEPT                                  CL*12
00229                      SET PO003-MULTIPLE-DEPTS TO TRUE                CL*12
00230                      SET REC-HAS-CHANGED TO TRUE                     CL*12
00231                      MOVE SPACES TO PO003-H-DEPT-NBR                 CL*12
00232                   END-IF                                             CL*12
00233                END-IF                                             ETKUP034
00234             END-IF                                                ETKUP034
00235                                                                   ETKUP034
00236             IF ALL-GOOD-UPCS                                      ETKUP034
00237                IF PO003-BAD-UPCS                                     CL*12
00238                   SET PO003-GOOD-UPCS TO TRUE                        CL*12
00239                   SET REC-HAS-CHANGED TO TRUE                        CL*12
00240                END-IF                                                CL*12
00241             END-IF                                                   CL*12
00242             MOVE WS-PO-NBR TO WS-PO003-H-PO-NBR                      CL*10
00243             PERFORM C100-VERIFY-PO-NBR                            ETKUP034
00244 *                                                                    CL*12
00245             IF (SQLCODE = +000 OR -811) AND WS-LC-IND NOT = 'LC'  ETKUP034
00246                IF PO003-NEW-PO-BAD                                   CL*12
00247                   SET PO003-DUPLICATE-PO TO TRUE                     CL*12
00248                   SET REC-HAS-CHANGED TO TRUE                        CL*12
00249                END-IF                                                CL*12
00250             ELSE                                                  ETKUP034
00251 *                                                                    CL*12
00252                IF PO003-DUPLICATE-PO                                 CL*12
00253                   SET REC-HAS-CHANGED TO TRUE                        CL*12
00254                   IF PO003-ONE-DEPT AND                              CL*12
00255                      PO003-GOOD-UPCS                                 CL*12
00256                      SET PO003-NEW-PO-GOOD TO TRUE                   CL*12
00257                   ELSE                                               CL*12
00258                      SET PO003-NEW-PO-BAD TO TRUE                    CL*12
00259                   END-IF                                             CL*12
00260                ELSE                                                  CL*12
00261                   IF PO003-ONE-DEPT AND                              CL*12
00262                      PO003-GOOD-UPCS                                 CL*12
00263                      SET PO003-NEW-PO-GOOD TO TRUE                   CL*12
00264                      SET REC-HAS-CHANGED TO TRUE                     CL*12
00265                   END-IF                                             CL*12
00266                END-IF                                                CL*12
00267             END-IF                                                ETKUP034
00268 *                                                                    CL*12
00269             IF REC-HAS-CHANGED                                       CL*12
00270                SET UPDATE-DB-RECORD TO TRUE                          CL*17
00271             ELSE                                                     CL*12
00272                SET NO-DB-ACTION TO TRUE                              CL*17
00273             END-IF                                                   CL*12
00274                                                                   ETKUP034
00275             PERFORM Z200-WRITE-OUTPUT-FILE                        ETKUP034
00276                                                                   ETKUP034
00277          END-IF                                                   ETKUP034
00278                                                                   ETKUP034
00279 * MOVE NEW SAVED HEADER BACK INTO PO003                              CL*19
00280          MOVE WS-HEADER-SAVE-AREA                                 ETKUP034
00281                  TO PO003-EDI855-RECORD                              CL*12
00282                                                                      CL**8
00283          MOVE SPACES              TO WS-DEPT-NBR                     CL*20
00284          SET ALL-GOOD-UPCS        TO TRUE                            CL*20
00285          SET ONE-DEPT             TO TRUE                            CL*20
00286          MOVE 0                   TO PV-RECS-THIS-PO                 CL*20
00287          ADD  1                   TO PV-CURRENT-REC-NBR              CL*20
00288          MOVE PO003-H-DEPT-NBR    TO WS-PO-DEPT-NBR                  CL*28
00289          MOVE PO003-DB-KEY        TO WS-DB-KEY-HEADER-REC            CL*20
00290          MOVE PO003-H-PO-NBR      TO WS-PO-NBR                       CL*20
00291          MOVE PO003-H-PO-STATUS   TO WS-PO-STATUS                    CL*20
00292          MOVE PO003-H-DEPT-STATUS TO WS-DEPT-STATUS                  CL*20
00293          MOVE PO003-H-LINE-STATUS TO WS-LINE-STATUS                  CL*21
JF0304         MOVE PO003-CANCEL-DATE   TO WS-CANCEL-DATE                  CL*20
00294          MOVE PO003-SHIP-DATE     TO WS-SHIP-DATE                    CL*20
00295          MOVE PO003-H-INTERCHANGE-ID-QUAL                            CL*20
00296                                   TO WS-TP-ID-QUAL                   CL*20
00297          MOVE PO003-H-INTERCHANGE-ID                                 CL*20
00298                                   TO WS-TP-ID                        CL*20
00297          MOVE PO003-H-ISA-CNTL-NBR                                   CL*20
00298                                   TO WS-ISA-CNTL-NBR                 CL*20
00297          MOVE PO003-H-GS-CNTL-NBR                                    CL*20
00298                                   TO WS-GS-CNTL-NBR                  CL*20
00297          MOVE PO003-H-ST-CNTL-NBR                                    CL*20
00298                                   TO WS-ST-CNTL-NBR                  CL*20
00297          MOVE PO003-H-TIMESTAMP                                      CL*20
00298                                   TO WS-TIMESTAMP                    CL*20
00297          MOVE PO003-H-SESSION-NBR                                    CL*20
00298                                   TO WS-SESSION-NBR                  CL*20
SP0408         MOVE PO003-H-LC-IND      TO WS-LC-IND                       CL*20
00299 *                                                                    CL*20
WG0610         MOVE PO003-H-PARENT-ENT-ID       TO WS-PARENT-ID                 
WG0610         MOVE PO003-H-FACTORY-ENT-ID      TO WS-FACTORY-ID                
00300          PERFORM Z300-FIND-ASSIGNED-DEPT                             CL*28
00301                                                                      CL*28
00302       END-IF.                                                     ETKUP034
00303                                                                      CL*28
00304 *                                                                 ETKUP034
00305       IF PO003-ITEM-RECORD                                           CL*10
00306          SET REC-HAS-NOT-CHANGED TO TRUE                             CL*12
JF0403         MOVE PO003-I-SKU TO WS-PO003-I-SKU                               
JF0403         PERFORM C200-GET-DEPT-FOR-SKU                                    
JF0403         IF SQLCODE = 0                                                   
JF0403            MOVE SKXREF-DEPT TO WS-SAVE-DEPT1                             
JF0403         END-IF                                                           
00307          IF PO003-I-UPC NOT = SPACES                                 CL*10
JF0403            IF PO003-I-SKU(1:5) = 'NOSKU'                                 
JF0403            OR WS-SAVE-DEPT1 NOT = WS-ASSIGNED-DEPT                  CL*12
00310 *                                                                 ETKUP034
00311                MOVE PO003-I-UPC TO WS-PO003-I-UPC                    CL*12
00312                PERFORM C150-VERIFY-UPC-SKU                           CL*12
00313 *                                                                 ETKUP034
00314                IF SQLCODE = +000                                     CL*25
JF0403                  MOVE SKXREF-SKU TO WS-SAVE-SKU                          
JF0403                  MOVE SKXREF-DEPT TO WS-SAVE-DEPT2                       
JF0403                  IF WS-SAVE-SKU NOT = PO003-I-SKU                   CL*25
JF0403                     MOVE WS-SAVE-SKU TO PO003-I-SKU                 CL*25
00319                      SET REC-HAS-CHANGED TO TRUE                     CL*25
00320                   END-IF                                             CL*25
00321 *                                                                    CL*12
00322                   IF WS-DEPT-NBR = SPACES                            CL*25
JF0403                     MOVE WS-SAVE-DEPT2 TO WS-DEPT-NBR               CL*25
00324                   ELSE                                               CL*25
JF0403                     IF WS-SAVE-DEPT2 = WS-DEPT-NBR                  CL*25
00326                         CONTINUE                                     CL*25
00327                      ELSE                                            CL*25
00328                         SET MULTIPLE-DEPTS TO TRUE                   CL*25
00329                      END-IF                                          CL*25
00330                   END-IF                                             CL*25
00331                ELSE                                                  CL*25
00332                   IF SQLCODE = +100                                  CL*25
00333                      SET NOT-ALL-GOOD-UPCS TO TRUE                   CL*25
00334                   ELSE                                               CL*25
00335                      DISPLAY 'SQLCODE: ' SQLCODE                     CL*25
00336                      MOVE 'B100-PROCESS-PO-FILE'                     CL*25
00337                         TO PV-PARAGRAPH-NAME                         CL*25
00338                      MOVE 'FIND UPC AND SKU'                         CL*25
00339                         TO PV-DB2-OPER-ATTEMPTED                     CL*25
00340                      PERFORM Z900-SQL-ABEND                          CL*25
00341                   END-IF                                             CL*25
00342                END-IF                                                CL*25
00343             ELSE                                                     CL*25
00344                IF WS-DEPT-NBR = SPACES                               CL*25
JF0403                  MOVE WS-SAVE-DEPT1 TO WS-DEPT-NBR                  CL*25
00346                ELSE                                                  CL*25
JF0403                  IF WS-SAVE-DEPT1 = WS-DEPT-NBR                     CL*25
00348                      CONTINUE                                        CL*25
00349                   ELSE                                               CL*25
00350                      SET MULTIPLE-DEPTS TO TRUE                      CL*25
00351                   END-IF                                             CL*25
00352                END-IF                                                CL*25
00353             END-IF                                                   CL*25
00354          ELSE                                                        CL*12
00355             IF WS-DEPT-NBR = SPACES                                  CL*12
JF0403               MOVE WS-SAVE-DEPT1 TO WS-DEPT-NBR                     CL*12
00357             ELSE                                                     CL*12
JF0403               IF WS-SAVE-DEPT1 = WS-DEPT-NBR                        CL*12
00359                   CONTINUE                                           CL*12
00360                ELSE                                                  CL*12
00361                   SET MULTIPLE-DEPTS TO TRUE                         CL*12
00362                END-IF                                                CL*12
00363             END-IF                                                   CL*12
00364          END-IF                                                      CL*12
00365          IF REC-HAS-CHANGED                                          CL*12
00366             SET UPDATE-DB-RECORD TO TRUE                             CL*17
00367          ELSE                                                        CL*12
00368             SET NO-DB-ACTION TO TRUE                                 CL*17
00369          END-IF                                                      CL*12
00370 *                                                                 ETKUP034
00371          ADD 1 TO PV-RECS-THIS-PO                                 ETKUP034
00372          ADD 1 TO PV-CURRENT-REC-NBR                              ETKUP034
00373          MOVE PV-CURRENT-REC-NBR TO PO003-I-SORT-SEQ-NBR             CL*10
00374          PERFORM Z200-WRITE-OUTPUT-FILE                           ETKUP034
00375       END-IF.                                                     ETKUP034
00376 *                                                                 ETKUP034
00377       IF PO003-ALLOC-RECORD                                          CL*10
00378          SET NO-DB-ACTION TO TRUE                                    CL*17
00379          ADD 1 TO PV-RECS-THIS-PO                                 ETKUP034
00380          ADD 1 TO PV-CURRENT-REC-NBR                              ETKUP034
00381          MOVE PV-CURRENT-REC-NBR TO PO003-A-SORT-SEQ-NBR             CL*10
00382          PERFORM Z200-WRITE-OUTPUT-FILE                           ETKUP034
00383       END-IF.                                                     ETKUP034
00384 *                                                                 ETKUP034
00385       PERFORM Z100-READ-INPUT-FILE.                               ETKUP034
00386       SET NO-DB-ACTION TO TRUE.                                      CL*17
00387 *                                                                 ETKUP034
00388 ******************************************************************ETKUP034
00389 *  PROCESS ALL PO RECORDS FROM INPUT FILE.                        ETKUP034
00390 ******************************************************************ETKUP034
00391  B200-WRAPUP-PO-FILE.                                             ETKUP034
00392 *                                                                 ETKUP034
00393 * MOVE OLD HEADER TO PO003 HEADER TO WRAP UP THAT PO                 CL*19
00394       MOVE WS-HEADER-RECORD                                          CL*12
00395            TO PO003-EDI855-RECORD.                                   CL*12
00396                                                                      CL*12
00397       SET REC-HAS-NOT-CHANGED TO TRUE.                               CL*14
00398       COMPUTE PV-SORT-SEQ-NBR =                                      CL*12
00399           PV-CURRENT-REC-NBR - PV-RECS-THIS-PO.                      CL*12
00400                                                                      CL*12
00401       MOVE PV-SORT-SEQ-NBR TO PO003-H-SORT-SEQ-NBR.                  CL*12
00402 *                                                                    CL*12
00403       IF WS-DEPT-NBR = SPACES                                        CL*12
00404          CONTINUE                                                    CL*12
00405       ELSE                                                           CL*12
00406          IF ONE-DEPT                                                 CL*12
00407             IF PO003-MULTIPLE-DEPTS OR                               CL*12
00408                PO003-NO-DEPT                                         CL*12
00409                SET PO003-ONE-DEPT  TO TRUE                           CL*12
00410                SET REC-HAS-CHANGED TO TRUE                           CL*12
00411                MOVE WS-DEPT-NBR TO PO003-H-DEPT-NBR                  CL*12
00412             END-IF                                                   CL*12
00413          ELSE                                                        CL*12
00414             IF PO003-NO-DEPT OR                                      CL*12
00415                PO003-ONE-DEPT                                        CL*12
00416                SET PO003-MULTIPLE-DEPTS TO TRUE                      CL*12
00417                SET REC-HAS-CHANGED TO TRUE                           CL*12
00418                MOVE SPACES TO PO003-H-DEPT-NBR                       CL*12
00419             END-IF                                                   CL*12
00420          END-IF                                                      CL*12
00421       END-IF.                                                        CL*12
00422                                                                      CL*12
00423       IF ALL-GOOD-UPCS                                               CL*12
00424          IF PO003-BAD-UPCS                                           CL*12
00425             SET PO003-GOOD-UPCS TO TRUE                              CL*12
00426             SET REC-HAS-CHANGED TO TRUE                              CL*12
00427          END-IF                                                      CL*12
00428       END-IF.                                                        CL*12
00429       MOVE WS-PO-NBR TO WS-PO003-H-PO-NBR.                           CL*12
00430       PERFORM C100-VERIFY-PO-NBR.                                    CL*12
00431 *                                                                    CL*12
00432       IF (SQLCODE = +000 OR -811) AND WS-LC-IND NOT = 'LC'           CL*12
00433          IF PO003-NEW-PO-BAD                                         CL*12
00434             SET PO003-DUPLICATE-PO TO TRUE                           CL*12
00435             SET REC-HAS-CHANGED TO TRUE                              CL*12
00436          END-IF                                                      CL*12
00437       ELSE                                                           CL*12
00438          IF PO003-DUPLICATE-PO                                       CL*12
00439             SET REC-HAS-CHANGED TO TRUE                              CL*12
00440             IF PO003-ONE-DEPT AND                                    CL*12
00441                PO003-GOOD-UPCS                                       CL*12
00442                SET PO003-NEW-PO-GOOD TO TRUE                         CL*12
00443             ELSE                                                     CL*12
00444                SET PO003-NEW-PO-BAD TO TRUE                          CL*12
00445             END-IF                                                   CL*12
00446          ELSE                                                        CL*12
00447             IF PO003-ONE-DEPT AND                                    CL*12
00448                PO003-GOOD-UPCS                                       CL*12
00449                SET PO003-NEW-PO-GOOD TO TRUE                         CL*12
00450                SET REC-HAS-CHANGED TO TRUE                           CL*12
00451             END-IF                                                   CL*12
00452          END-IF                                                      CL*12
00453       END-IF.                                                        CL*12
00454       IF REC-HAS-CHANGED                                             CL*12
00455          SET UPDATE-DB-RECORD TO TRUE                                CL*17
00456       ELSE                                                           CL*12
00457          SET NO-DB-ACTION TO TRUE                                    CL*17
00458       END-IF.                                                        CL*12
00459 *                                                                    CL*12
00460       PERFORM Z200-WRITE-OUTPUT-FILE.                                CL*12
00461 *                                                                 ETKUP034
00462 ******************************************************************ETKUP034
00463 *  VERIFY THAT THE UPC AND SKU ARE ON THE APPROPRIATE TABLES      ETKUP034
00464 ******************************************************************ETKUP034
00465  C100-VERIFY-PO-NBR.                                              ETKUP034
00466 *                                                                 ETKUP034
00467      PERFORM                                                      ETKUP034
00468          WITH TEST AFTER                                          ETKUP034
00469          VARYING PV-SQL-SUB                                       ETKUP034
00470          FROM 1 BY 1                                              ETKUP034
00471          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUP034
00472                    OR SQLCODE = 0                                 ETKUP034
00473                    OR SQLCODE = +100                              ETKUP034
00474                    OR SQLCODE = -811)                             ETKUP034
00475 *                                                                 ETKUP034
00476          EXEC SQL                                                 ETKUP034
00477              SELECT A.PO_NBR                                      ETKUP034
00478              INTO :PURCHS-PO-NBR                                  ETKUP034
00479              FROM TPURCHS A                                       ETKUP034
00480              WHERE A.PO_NBR = :WS-PO003-H-PO-NBR                     CL*10
00481          END-EXEC                                                 ETKUP034
00482 *                                                                 ETKUP034
00483          EVALUATE TRUE                                            ETKUP034
00484              WHEN SQLCODE = -904                                  ETKUP034
00485              WHEN SQLCODE = -911                                  ETKUP034
00486              WHEN SQLCODE = -811                                  ETKUP034
00487              WHEN SQLCODE = 100                                   ETKUP034
00488              WHEN SQLCODE = 0                                     ETKUP034
00489                  CONTINUE                                         ETKUP034
00490              WHEN SQLWARN NOT = SPACES                            ETKUP034
00491              WHEN SQLCODE NOT = ZERO                              ETKUP034
00492                  MOVE 'C100-VERIFY-PO-NBR'                        ETKUP034
00493                                TO  PV-PARAGRAPH-NAME              ETKUP034
00494                  MOVE 'VERIFY PO NUMBER'                          ETKUP034
00495                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP034
00496                  PERFORM Z900-SQL-ABEND                           ETKUP034
00497          END-EVALUATE                                             ETKUP034
00498      END-PERFORM.                                                 ETKUP034
00499 *                                                                 ETKUP034
00500      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUP034
00501          MOVE 'C100-VERIFY-PO-NBR'                                ETKUP034
00502                                TO  PV-PARAGRAPH-NAME              ETKUP034
00503          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUP034
00504                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP034
00505          PERFORM Z900-SQL-ABEND                                   ETKUP034
00506      END-IF.                                                      ETKUP034
00507 *                                                                 ETKUP034
00508 ******************************************************************ETKUP034
00509 *  VERIFY THAT THE UPC AND SKU ARE ON THE APPROPRIATE TABLES      ETKUP034
00510 ******************************************************************ETKUP034
00511  C150-VERIFY-UPC-SKU.                                             ETKUP034
00512 *                                                                 ETKUP034
00513      PERFORM                                                      ETKUP034
00514          WITH TEST AFTER                                          ETKUP034
00515          VARYING PV-SQL-SUB                                       ETKUP034
00516          FROM 1 BY 1                                              ETKUP034
00517          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUP034
00518                    OR SQLCODE = 0                                 ETKUP034
00519                    OR SQLCODE = +100)                             ETKUP034
00520 *                                                                 ETKUP034
00521          EXEC SQL                                                 ETKUP034
JF0403             SELECT A.SKU                                         ETKUP034
JF0403                  , A.DEPT                                        ETKUP034
JF0403             INTO :SKXREF-SKU                                     ETKUP034
JF0403                , :SKXREF-DEPT                                    ETKUP034
JF0403             FROM TSKXREF A                                       ETKUP034
JF0403                , TUPCSKU B                                               
00529              WHERE B.UPC_NBR  = :WS-PO003-I-UPC                      CL*10
JF0403               AND A.ITM_NBR = B.ITEM_NBR                         ETKUP034
00531          END-EXEC                                                 ETKUP034
00532 *                                                                 ETKUP034
00533          EVALUATE TRUE                                            ETKUP034
00534              WHEN SQLCODE = -904                                  ETKUP034
00535              WHEN SQLCODE = -911                                  ETKUP034
00536              WHEN SQLCODE = 100                                   ETKUP034
00537              WHEN SQLCODE = 0                                     ETKUP034
00538                  CONTINUE                                         ETKUP034
00539              WHEN SQLWARN NOT = SPACES                            ETKUP034
00540              WHEN SQLCODE NOT = ZERO                              ETKUP034
00541                  MOVE 'C150-VERIFY-UPC-SKU'                       ETKUP034
00542                                TO  PV-PARAGRAPH-NAME              ETKUP034
00543                  MOVE 'VERIFY UPCS AND SKUS'                      ETKUP034
00544                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP034
00545                  PERFORM Z900-SQL-ABEND                           ETKUP034
00546          END-EVALUATE                                             ETKUP034
00547      END-PERFORM.                                                 ETKUP034
00548 *                                                                 ETKUP034
00549      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUP034
00550          MOVE 'C150-VERIFY-UPC-SKU'                               ETKUP034
00551                                TO  PV-PARAGRAPH-NAME              ETKUP034
00552          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUP034
00553                                TO  PV-DB2-OPER-ATTEMPTED          ETKUP034
00554          PERFORM Z900-SQL-ABEND                                   ETKUP034
00555      END-IF.                                                      ETKUP034
00556 *                                                                 ETKUP034
JF0403******************************************************************   CL*47
JF0403*  GET DEPARTMENT FOR THE SPECIFIED SKU                              CL*47
JF0403******************************************************************   CL*47
JF0403 C200-GET-DEPT-FOR-SKU.                                              CL*47
JF0403*                                                                    CL*47
JF0403     PERFORM                                                         CL*47
JF0403         WITH TEST AFTER                                             CL*47
JF0403         VARYING PV-SQL-SUB                                          CL*47
JF0403         FROM 1 BY 1                                                 CL*47
JF0403         UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*47
JF0403                   OR SQLCODE = 0                                    CL*47
JF0403                   OR SQLCODE = +100)                                CL*47
JF0403*                                                                    CL*47
JF0403         EXEC SQL                                                    CL**1
JF0403             SELECT A.DEPT                                           CL*28
JF0403             INTO :SKXREF-DEPT                                       CL*21
JF0403             FROM TSKXREF A                                          CL*17
JF0403             WHERE SKU = :WS-PO003-I-SKU                             CL*54
JF0403         END-EXEC                                                    CL**1
JF0403*                                                                    CL**1
JF0403         EVALUATE TRUE                                               CL**1
JF0403             WHEN SQLCODE = -904                                     CL**1
JF0403             WHEN SQLCODE = -911                                     CL**1
JF0403             WHEN SQLCODE = 100                                      CL**1
JF0403             WHEN SQLCODE = 0                                        CL**1
JF0403                 CONTINUE                                            CL**1
JF0403             WHEN SQLWARN NOT = SPACES                               CL**1
JF0403             WHEN SQLCODE NOT = ZERO                                 CL**1
JF0403                 MOVE 'C200-GET-DEPT-FOR-SKU'                        CL*47
JF0403                               TO  PV-PARAGRAPH-NAME                 CL**1
JF0403                 MOVE 'GET DEPT FOR SKU'                             CL*17
JF0403                               TO  PV-DB2-OPER-ATTEMPTED             CL**1
JF0403                 PERFORM Z900-SQL-ABEND                              CL**1
JF0403         END-EVALUATE                                                CL**1
JF0403     END-PERFORM.                                                    CL**1
JF0403*                                                                    CL**1
JF0403     IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL**1
JF0403         MOVE 'C200-GET-DEPT-FOR-SKU'                                CL*47
JF0403                               TO  PV-PARAGRAPH-NAME                 CL**1
JF0403         MOVE 'RETRIES EXCEEDED'                                     CL*17
JF0403                               TO  PV-DB2-OPER-ATTEMPTED             CL**1
JF0403         PERFORM Z900-SQL-ABEND                                      CL**1
JF0403     END-IF.                                                         CL**1
JF0403*                                                                    CL*20
00557 ******************************************************************ETKUP034
00558 * Z100-READ-INPUT-FILE                                            ETKUP034
00559 ******************************************************************ETKUP034
00560  Z100-READ-INPUT-FILE.                                            ETKUP034
00561 *                                                                 ETKUP034
00562      READ PO-INPUT-FILE                                              CL**4
00563           INTO PO003-EDI855-384-REC                                  CL**9
00564             AT END SET PS-EOF TO TRUE.                            ETKUP034
00565 *                                                                    CL**6
00566 ******************************************************************   CL**6
00567 * Z200-WRITE-OUTPUT-FILE                                             CL**6
00568 ******************************************************************   CL**6
00569  Z200-WRITE-OUTPUT-FILE.                                             CL**6
00570 *                                                                    CL**6
00571      WRITE PO-STATUS-RECORD                                          CL*11
00572           FROM PO003-EDI855-RECORD.                                  CL*11
00573 *                                                                 ETKUP034
00574 ******************************************************************   CL*28
00575 *  FIND THE DEPT # ASSIGNED TO THIS P.O.                             CL*28
00576 ******************************************************************   CL*28
00577  Z300-FIND-ASSIGNED-DEPT.                                            CL*28
00578 *                                                                    CL*28
00579      PERFORM                                                         CL*28
00580          WITH TEST AFTER                                             CL*28
00581          VARYING PV-SQL-SUB                                          CL*28
00582          FROM 1 BY 1                                                 CL*28
00583          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                          CL*28
00584                    OR SQLCODE = 0                                    CL*28
00585                    OR SQLCODE = +100)                                CL*28
00586 *                                                                    CL*28
00587          EXEC SQL                                                    CL*28
00588              SELECT DEPT_NBR                                         CL*28
00589              INTO :WS-ASSIGNED-DEPT                                  CL*28
00590              FROM TTRPOHL                                            CL*28
00591              WHERE TP_DKEY       = :PO003-TP-DKEY      AND           CL*28
00592                    TP_DKEY_TMST  = :PO003-TP-DKEY-TMST               CL*28
00593          END-EXEC                                                    CL*28
00594 *                                                                    CL*28
00595          EVALUATE TRUE                                               CL*28
00596              WHEN SQLCODE = -904                                     CL*28
00597              WHEN SQLCODE = -911                                     CL*28
00598              WHEN SQLCODE = 0                                        CL*28
00599                  CONTINUE                                            CL*28
00600              WHEN SQLWARN NOT = SPACES                               CL*28
00601              WHEN SQLCODE NOT = ZERO                                 CL*28
00602                  MOVE 'Z300-FIND-ASSIGNED-DEPT'                      CL*28
00603                                TO  PV-PARAGRAPH-NAME                 CL*28
00604                  MOVE 'FIND P.O. DEPT'                               CL*28
00605                                TO  PV-DB2-OPER-ATTEMPTED             CL*28
00606                  PERFORM Z900-SQL-ABEND                              CL*28
00607          END-EVALUATE                                                CL*28
00608      END-PERFORM.                                                    CL*28
00609 *                                                                    CL*28
00610      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                           CL*28
00611          MOVE 'Z300-FIND-ASSIGNED-DEPT'                              CL*28
00612                                TO  PV-PARAGRAPH-NAME                 CL*28
00613          MOVE 'VERIFY RETRIES EXCEEDED'                              CL*28
00614                                TO  PV-DB2-OPER-ATTEMPTED             CL*28
00615          PERFORM Z900-SQL-ABEND                                      CL*28
00616      END-IF.                                                         CL*28
00617 *                                                                    CL*28
00618 ******************************************************************ETKUP034
00619 *  STANDARD DB2 ERROR ABEND ROUTINE                               ETKUP034
00620 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             ETKUP034
00621 ******************************************************************ETKUP034
00622  Z900-SQL-ABEND.                                                  ETKUP034
00623                                                                   ETKUP034
00624      MOVE +4013                 TO ABEND-CODE.                    ETKUP034
00625      DISPLAY '**  ABEND     **'.                                  ETKUP034
00626      DISPLAY '**  PROGRAM   **  =  ETKUP034'.                     ETKUP034
00627      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            ETKUP034
00628      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        ETKUP034
00629                                                                   ETKUP034
00630 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         ETKUP034
00631 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        ETKUP034
00632                                                                   ETKUP034
00633      COPY DPPD004.                                                ETKUP034
00634                                                                   ETKUP034
00635      CALL 'ILBOABN0'  USING ABEND-CODE.                           ETKUP034
