       IDENTIFICATION DIVISION.                                         00020000
                                                                                
       PROGRAM-ID.             PMKRP114.                                00040055
       AUTHOR.                 KELLY LARUM.                             00050000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060000
       DATE-WRITTEN.           AUGUST 2003.                             00070000
       DATE-COMPILED.                                                   00080000
                                                                                
      ************************************************************      00090000
      *          PMKRP114 - SALES REULTS                                00100000
      ************************************************************      00090000
      * THIS PROGRAM WILL PRODUCE A SALES RESULT SUMMARY REPORT         00102100
      * FOR THE FISCAL MONTH.                                           00102100
      ************************************************************      00090000
                                                                                
      ************************************************************      00090000
      * CHANGE LOG:                                                     00103600
      ************************************************************      00090000
      * DATE: 3/9/2009                                                  00103800
      * WR/IR#: WR36158                                                 00103900
      * PROGRAMMER: DARYL STEWART                                       00104000
      * DESCRIPTION: FIXED SORT ORDER DUE TO ORACLE 10G UPGRADE         00104100
      ************************************************************      00090000
      * DATE:         07/30/2018                                                
      * WR/IR#:       CHG0204126                                                
      * PROGRAMMER:   COGNIZANT                                                 
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE                     
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                            00105000
                                                                                
       CONFIGURATION SECTION.                                           00107000
       SOURCE-COMPUTER. IBM-3090.                                       00108000
       OBJECT-COMPUTER. IBM-3090.                                       00109000
                                                                        00110000
       INPUT-OUTPUT SECTION.                                            00120000
                                                                        00130000
       FILE-CONTROL.                                                    00140000
                                                                        00150000
           SELECT CONTROL-CARD-FILE                                     00184030
                  ASSIGN TO INP01.                                      00185030
                                                                                
070941     SELECT ORACLE-LOGON-FILE                                             
070941            ASSIGN TO ORALOGON.                                           
                                                                        00186030
           SELECT SALES-RESULTS-REPORT                                  00190007
                  ASSIGN TO RPT01.                                      00200000
                                                                        00210000
       DATA DIVISION.                                                   00230000
                                                                                
       FILE SECTION.                                                    00250000
                                                                                
       FD  CONTROL-CARD-FILE                                            00327030
           RECORDING MODE F                                             00328030
           BLOCK CONTAINS 0 RECORDS                                     00329030
           LABEL RECORDS ARE OMITTED.                                   00329130
       01  CONTROL-CARD-RECORD              PIC X(80).                  00329230
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                        00329330
       FD  SALES-RESULTS-REPORT                                         00330000
           RECORDING MODE F                                             00340000
           BLOCK CONTAINS 0 RECORDS                                     00350000
           LABEL RECORDS ARE OMITTED.                                   00360000
       01  SALES-RESULTS-REPORT-LINE        PIC X(200).                 00370000
                                                                        00380000
       WORKING-STORAGE SECTION.                                         00390000
                                                                                
      ***********************************************************       01090000
      * CD- CONTROL DATES (DATES ACCEPTED INTO THE PROGRAM)             00410000
      ***********************************************************       01090000
       01  CC-CONTROL-CARD.                                                     
           05  CC-FISCAL-END-DATE.                                              
               10  CC-FISCAL-END-DATE-CCYY         PIC 9(04).                   
               10  CC-FISCAL-END-DATE-MM           PIC 9(02).                   
               10  CC-FISCAL-END-DATE-DD           PIC 9(02).                   
           05  FILLER                       PIC X(72).                          
                                                                                
       01  CD-CPU-DATE.                                                         
           05  CD-CPU-YR                    PIC 9(02)      VALUE ZERO.          
           05  CD-CPU-MO                    PIC 9(02)      VALUE ZERO.          
           05  CD-CPU-DY                    PIC 9(02)      VALUE ZERO.          
                                                                                
       01  CD-CPU-DATE-GREG.                                                    
           05  CD-CPU-MM                    PIC 9(02)      VALUE ZERO.          
           05  CD-CPU-DD                    PIC 9(02)      VALUE ZERO.          
           05  CD-CPU-YY                    PIC 9(02)      VALUE ZERO.          
                                                                                
       01  CD-CPU-DATE-ISO                  PIC X(10)      VALUE SPACES.        
                                                                                
       01  CT-CPU-TIME.                                                         
           05  CT-CPU-HR                    PIC 9(02)      VALUE ZERO.          
           05  CT-CPU-MIN                   PIC 9(02)      VALUE ZERO.          
           05  CT-CPU-SEC                   PIC 9(02)      VALUE ZERO.          
           05  CT-CPU-HSEC                  PIC 9(02)      VALUE ZERO.          
                                                                                
      ***********************************************************       01090000
      * PC- PROGRAM CONSTANTS                                           00460300
      ***********************************************************       01090000
       01  PC-PROGRAM-CONSTANTS.                                        00460500
           05  PC-MAX-LINE-CNT              PIC S9(04) VALUE +78 COMP.  00460600
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC X(08)  VALUE            00500000
070941     05  PC-AUTO-LOGON                PIC X(1)   VALUE '/'.               
070941     05  PC-PROGRAM-NAME              PIC X(08)  VALUE                    
                                                       'PMKRP114'.      00510000
      ***********************************************************       01090000
      * PS- PROGRAM SWITCHES                                            00780000
      ***********************************************************       01090000
       01  PS-PROGRAM-SWITCHES.                                         00800000
070941     05  PS-END-OF-LOGON-FILE-SW      PIC X(01)      VALUE 'N'.           
070941         88  PS-END-OF-LOGON-FILE                    VALUE 'Y'.           
070941     05  PS-ORA-SRC-CTL-DONE-SW       PIC X(01)      VALUE 'N'.           
070941         88  PS-ORA-SRC-EMPTY                        VALUE 'Y'.           
           05  PS-CONTROL-CARD-SW           PIC X(01)      VALUE 'N'.           
               88  PS-CONTROL-CARD-END                     VALUE 'Y'.           
               88  PS-CONTROL-CARD-START                   VALUE 'N'.           
           05  PS-END-OF-REPORT-CSR-SW      PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-REPORT-CSR                    VALUE 'Y'.           
               88  PS-NOT-END-OF-REPORT-CSR                VALUE 'N'.           
           05  PS-NEW-PAGE-SW               PIC X(01)      VALUE 'N'.           
               88  PS-NEW-PAGE                             VALUE 'Y'.           
               88  PS-NOT-NEW-PAGE                         VALUE 'N'.           
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID             PIC X(40).                          
070941         88 CC-AUTO-LOGON                            VALUE '/'.           
070941     05  CC-ORACLE-PASSWORD           PIC X(40).                          
                                                                        01080000
      ***********************************************************       01090000
      * PV- PROGRAM VARIABLES                                           01100000
      ***********************************************************       01090000
       01  PV-PROGRAM-VARIABLES.                                        01120000
070941     05  PV-ORACLE-USERID-LEN         PIC S9(4)  VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PV-ORACLE-PASSWORD-LEN       PIC S9(4)  VALUE ZEROS              
070941                                                 COMP SYNC.               
070941     05  PC-AUTO-LOGON                PIC X(01)  VALUE '/'.               
           05  PV-ADVANCE-LINES             PIC S9(04) VALUE +1 COMP.   01130000
           05  PV-LINE-CNT                  PIC S9(04) VALUE +0 COMP.   01150000
           05  PV-PAGE-CNT                  PIC 9(05)  VALUE 0.         01160000
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACE.     01170000
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.            
           05  PV-OPERATION-MSG-1           PIC X(40)  VALUE SPACES.    01180000
           05  PV-OPERATION-MSG-2           PIC X(40)  VALUE SPACES.    01190000
           05  PV-TABLE-NAME                PIC X(12)  VALUE SPACES.            
           05  PV-CALENDAR-TYPE             PIC X(02)  VALUE 'F2'.              
           05  PV-FISCAL-END-DTE            PIC X(10)  VALUE SPACES.            
           05  PV-FISCAL-END-DTE-RDF REDEFINES PV-FISCAL-END-DTE.               
               10  PV-FISCAL-END-DTE-CCYY   PIC X(04).                          
               10  PV-FISCAL-END-DTE-D1     PIC X(01).                          
               10  PV-FISCAL-END-DTE-MM     PIC X(02).                          
               10  PV-FISCAL-END-DTE-D2     PIC X(01).                          
               10  PV-FISCAL-END-DTE-DD     PIC X(02).                          
           05  PV-FISCAL-BEG-DTE            PIC X(10)  VALUE SPACES.            
           05  PV-SALE-TRAN-DATE            PIC X(10)  VALUE SPACES.            
           05  PV-SALE-TRAN-DATE-SAVED      PIC X(10)  VALUE SPACES.            
           05  PV-AD-EVENT-ID               PIC X(15)  VALUE SPACES.            
           05  PV-AD-EVENT-NAME             PIC X(20)  VALUE SPACES.    03460054
           05  PV-PROMO-ID                  PIC 9(10)  VALUE 0.         03480038
           05  PV-PROMO-NAME                PIC X(20)  VALUE SPACES.    03492034
           05  PV-UNITS-SOLD                PIC S9(9)V99 VALUE 0.       03493000
           05  PV-SALES-VOLUME              PIC S9(9)V99 VALUE 0.       03493254
           05  PV-MARKDOWN                  PIC S9(9)V99 VALUE 0.       03493638
           05  PV-TOTAL-COST                PIC S9(9)V99 VALUE 0.       03493638
           05  PV-CPU-TIME                  PIC 9(08) VALUE 0.                  
           05  PV-RPT-LINE                  PIC X(200) VALUE SPACES.    01819100
                                                                        02214800
      ***********************************************************       01090000
      * ABEND- ABEND VARIABLES                                          02540000
      ***********************************************************       01090000
       01  ABEND-CODE                       PIC S9(04) VALUE +0 COMP.   02560000
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     02570000
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     02580000
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     02590000
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     02600000
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     02610000
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     02620000
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     02630000
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     02640000
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     02650000
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     02680000
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     02690000
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     02700000
                                                                        02710000
      ***********************************************************       01090000
      * HD- HEADER INFO                                                 02770000
      ***********************************************************       01090000
       01  HEADER-01.                                                   02790000
           05  FILLER                       PIC X(77) VALUE SPACES.     02800020
           05  FILLER                       PIC X(47) VALUE             02810003
               'PROMOTION SALES RESULTS SUMMARY -- FISCAL MONTH'.       02820003
           05  FILLER                       PIC X(75) VALUE SPACES.     02840020
                                                                        02850000
       01  HEADER-02.                                                   02960000
           05  FILLER                       PIC X(45)  VALUE            02970000
               '   SALE TRAN DATE       AD EVENT ID          '.         02980019
           05  FILLER                       PIC X(45)  VALUE            02990000
               ' AD EVENT NAME                   PROMO NAME  '.         03000037
           05  FILLER                       PIC X(45)  VALUE            03010037
               '               SALES VOLUME             UNITS'.         03020037
           05  FILLER                       PIC X(45)  VALUE            03030000
               ' SOLD             MARKDOWN $            TOTAL'.         03040037
           05  FILLER                       PIC X(20)  VALUE            03050037
               ' COST               '.                                  03060037
                                                                                
       01  HEADER-03.                                                   03310006
           05  FILLER                       PIC  X(50)     VALUE        03320006
               '  ------------------------------------------------'.    03330006
           05  FILLER                       PIC  X(50)     VALUE        03340006
               '--------------------------------------------------'.    03350006
           05  FILLER                       PIC  X(50)     VALUE        03360006
               '--------------------------------------------------'.    03361006
           05  FILLER                       PIC  X(41)     VALUE        03361106
               '-----------------------------------------'.             03361206
                                                                                
      ***********************************************************       01090000
      * DTL- DETAIL INFO                                                03130000
      ***********************************************************       01090000
                                                                        03362006
       01  DETAIL-01.                                                   03420000
           05  FILLER                       PIC X(03)  VALUE SPACES.    03452038
           05  DTL-SALE-TRAN-DATE           PIC X(10)  VALUE SPACES.    03440000
           05  FILLER                       PIC X(11)  VALUE SPACES.    03452038
           05  DTL-AD-EVENT-ID              PIC X(15)  VALUE SPACES.    03460054
           05  FILLER                       PIC X(07)  VALUE SPACES.    03470038
           05  DTL-AD-EVENT-NAME            PIC X(20)  VALUE SPACES.    03460054
           05  FILLER                       PIC X(12)  VALUE SPACES.    03490034
           05  DTL-PROMO-NAME               PIC X(20)  VALUE SPACES.    03492034
           05  FILLER                       PIC X(05)  VALUE SPACES.    03493138
           05  DTL-SALES-VOLUME             PIC ZZZ,ZZZ,ZZ9.99.         03493000
           05  FILLER                       PIC X(09)  VALUE SPACES.    03493138
           05  DTL-UNITS-SOLD               PIC ZZZ,ZZZ,ZZ9.99.         03493254
           05  FILLER                       PIC X(09)  VALUE SPACES.    03493538
           05  DTL-MARKDOWN                 PIC ZZZ,ZZZ,ZZ9.99.         03493638
           05  FILLER                       PIC X(08)  VALUE SPACES.    03494038
           05  DTL-TOTAL-COST               PIC ZZZ,ZZZ,ZZ9.99.         03500054
           05  FILLER                       PIC X(10)  VALUE SPACES.    03510038
                                                                        03805200
       01  END-OF-REPORT-LINE.                                          02720000
           05  FILLER                       PIC X(86)  VALUE SPACES.    02730000
           05  FILLER                       PIC X(25)  VALUE            02730000
               ' ** END OF REPORT **     '.                             02740000
           05  FILLER                       PIC X(89) VALUE SPACES.     02730000
                                                                        02750000
      ******************************************************************        
      *  DATE VALIDATION CALLING PARAMETER LIST                         03812700
      ******************************************************************        
           COPY DPWS500.                                                03812900
                                                                        03813000
      ******************************************************************        
      *  STANDARD HEADER LINE - KOHLS 200 COL                           03813201
      ******************************************************************        
           COPY DPWS200O.                                               03813400
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *****************************************************************         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *****************************************************************         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ***********************************************************       03813700
       PROCEDURE DIVISION.                                              03813800
      ***********************************************************       03813700
       0000-MAINLINE.                                                   03814000
                                                                        03814100
           PERFORM 1000-INITIALIZATION.                                 03814200
                                                                        03814300
           PERFORM 2000-CREATE-REPORT.                                          
                                                                        03814600
           PERFORM 3000-END-OF-JOB-ROUTINE.                             03814700
                                                                        03814800
           GOBACK.                                                      03814900
                                                                        03840000
      ***********************************************************       03813700
      * 1000-INITIALIZATION.                                            03860000
      ***********************************************************       03813700
       1000-INITIALIZATION.                                             03920000
                                                                        03930000
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM Z100-ABEND                                               
070941     END-IF.                                                              
                                                                                
           OPEN INPUT CONTROL-CARD-FILE                                 03980002
               OUTPUT SALES-RESULTS-REPORT.                             03990000
                                                                        04000000
           ACCEPT CD-CPU-DATE FROM DATE.                                04010000
           MOVE CD-CPU-YR                  TO CD-CPU-YY.                04020000
           MOVE CD-CPU-MO                  TO CD-CPU-MM.                04030000
           MOVE CD-CPU-DY                  TO CD-CPU-DD.                04040000
                                                                        04050000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              04060000
           SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                     04070000
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                     04071000
           SET DPG52-LK-DTE-GREG           TO TRUE.                     04072000
           MOVE CD-CPU-DATE-GREG           TO DPG52-LK-DATE-INPUT.      04072100
           MOVE 'INVALID CONVERT OF CURRENT DATE FROM CPU'              04072200
                                           TO PV-OPERATION-MSG-1.       04072300
                                                                        04072400
           PERFORM 4000-CALENDAR-ROUTINE.                               04072500
                                                                        04072600
           MOVE DPG55-DB2-ISO-DATE-FMT     TO CD-CPU-DATE-ISO.          04072700
                                                                        04072800
           ACCEPT CT-CPU-TIME FROM TIME.                                04072900
           MOVE CT-CPU-HR                  TO DP200O-RUN-HOUR.          04073000
           MOVE CT-CPU-MIN                 TO DP200O-RUN-MINUTE.        04074000
           MOVE CD-CPU-MO                  TO DP200O-RUN-MONTH.         04075000
           MOVE CD-CPU-DY                  TO DP200O-RUN-DAY.           04076000
           MOVE CD-CPU-YR                  TO DP200O-RUN-YEAR.          04077000
070941*    MOVE PC-CURRENT-PROGRAM-NAME    TO DP200O-PROGRAM-NAME.      04078000
           MOVE PC-PROGRAM-NAME            TO DP200O-PROGRAM-NAME.      04078000
                                                                        04078131
           PERFORM 1500-READ-CONTROL-CARD.                              04078231
           PERFORM 1600-GET-FISCAL-BEGIN-DATE.                          04078231
                                                                        04300000
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
                                                                                
      ************************************************************              
      * 1500-READ-CONTROL-CARD.                                         04350002
      ************************************************************              
       1500-READ-CONTROL-CARD.                                          04350002
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
              AT END                                                            
                 SET PS-CONTROL-CARD-END TO TRUE                                
           END-READ.                                                            
                                                                                
           IF PS-CONTROL-CARD-END                                               
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1          
               SET ABEND-4003-CTRL-CARD-ERROR    TO TRUE                        
               PERFORM Z910-ABEND                                               
           ELSE                                                                 
               DISPLAY 'CC-FISCAL-END-DATE = ' CC-FISCAL-END-DATE               
               MOVE  CC-FISCAL-END-DATE-CCYY TO PV-FISCAL-END-DTE-CCYY          
               MOVE  CC-FISCAL-END-DATE-MM   TO PV-FISCAL-END-DTE-MM            
               MOVE  CC-FISCAL-END-DATE-DD   TO PV-FISCAL-END-DTE-DD            
               MOVE '-'                      TO PV-FISCAL-END-DTE-D1            
                                                PV-FISCAL-END-DTE-D2            
                                                                                
           END-IF.                                                              
                                                                                
      ************************************************************      04340000
      * 1600-GET-FISCAL-BEGIN-DATE.                                     04350002
      * USE CALENDAR ROUTINE TO DETERMINE FISCAL MONTH BEGIN DATE               
      * USING THE FISCAL MONTH END DATE PASSED INTO THE PROGRAM                 
      * THROUGH THE CONTROL CARD.                                               
      ************************************************************      04340000
       1600-GET-FISCAL-BEGIN-DATE.                                      04350002
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           MOVE PV-CALENDAR-TYPE            TO DPG51-CALENDAR-TYPE.             
           SET DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                            
                                                                                
           MOVE PV-FISCAL-END-DTE           TO DPG52-LK-DATE-INPUT.             
           SET DPG52-LK-DTE-ISO-FMT         TO TRUE.                            
           MOVE 'INVALID DATE FORMAT IN 1600'                                   
                                            TO PV-OPERATION-MSG-1.              
                                                                                
           PERFORM 4000-CALENDAR-ROUTINE.                               04072500
                                                                                
           MOVE  DPG56-FIRST-FP-DB2-ISO-FMT TO PV-FISCAL-BEG-DTE.               
           DISPLAY 'PV-FISCAL-BEG-DTE: ' PV-FISCAL-BEG-DTE.                     
                                                                                
      ***********************************************************       08031100
      * 2000-CREATE-REPORT.                                             08031200
      ***********************************************************       08031100
       2000-CREATE-REPORT.                                              08031700
                                                                                
           PERFORM 5100-PROCESS-PAGE-HEADER.                            11836206
           PERFORM 2100-DECLARE-REPORT-CSR.                                     
           PERFORM 2200-OPEN-REPORT-CSR.                                        
           PERFORM 2300-FETCH-REPORT-CSR.                                       
                                                                                
           IF NOT PS-END-OF-REPORT-CSR                                          
              PERFORM 2400-PROCESS-REPORT-CSR                                   
                      UNTIL PS-END-OF-REPORT-CSR                                
           END-IF.                                                              
                                                                                
           PERFORM 2500-CLOSE-REPORT-CSR.                                       
                                                                                
      ***********************************************************       08031100
      * 2100-DECLARE-REPORT-CSR.                                        08031200
      ***********************************************************       08031100
       2100-DECLARE-REPORT-CSR.                                         08031700
                                                                                
           EXEC SQL                                                             
                DECLARE REPORTCSR CURSOR FOR                                    
                SELECT                                                          
                      A.TM_DIM_KY_DTE                                           
                     ,B.AD_EVNT_NBR_TXT                                         
                     ,B.AD_EVNT_NM                                              
                     ,B.PROMO_ID                                                
                     ,B.PROMO_NM                                                
                     ,SUM(A.SLS_UNT_QTY)                                        
                     ,SUM(A.SLS_RTL_AMT)                                        
                     ,SUM(A.MKDN_RTL_AMT)                                       
                     ,SUM(A.SLS_COST_AMT)                                       
                 FROM PMT_PROMO_ITM_FCT A                                       
                     ,PMV_PROMO_DIM B                                           
                WHERE A.PROMO_ID         = B.PROMO_ID                           
                  AND A.TM_DIM_KY_DTE >= TO_DATE(:PV-FISCAL-BEG-DTE,            
                                             'YYYY-MM-DD')                      
                  AND A.TM_DIM_KY_DTE <= TO_DATE(:PV-FISCAL-END-DTE,            
                                             'YYYY-MM-DD')                      
                GROUP BY A.TM_DIM_KY_DTE                                        
                        ,B.AD_EVNT_NBR_TXT                                      
                        ,B.AD_EVNT_NM                                           
                        ,B.PROMO_ID                                             
                        ,PROMO_NM                                               
36158           ORDER BY A.TM_DIM_KY_DTE                                        
36158                   ,B.AD_EVNT_NBR_TXT                                      
36158                   ,B.AD_EVNT_NM                                           
36158                   ,B.PROMO_ID                                             
36158                   ,PROMO_NM                                               
           END-EXEC.                                                            
                                                                                
      ***********************************************************       08031100
      * 2200-OPEN-REPORT-CSR.                                           08031200
      ***********************************************************       08031100
       2200-OPEN-REPORT-CSR.                                            08031700
           ACCEPT PV-CPU-TIME FROM TIME.                                        
           DISPLAY ' '.                                                         
           DISPLAY 'START REPORT CSR OPEN: ' PV-CPU-TIME.                       
           SET PS-NOT-END-OF-REPORT-CSR TO TRUE.                                
                                                                                
           EXEC SQL                                                             
               OPEN REPORTCSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = 1403                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2200-OPEN-REPORT-CSR' TO PV-PARAGRAPH-NAME             
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************       08031100
      * 2300-FETCH-REPORT-CSR.                                          08031200
      ***********************************************************       08031100
       2300-FETCH-REPORT-CSR.                                           08031700
                                                                                
           EXEC SQL                                                             
               FETCH REPORTCSR                                                  
                INTO :PV-SALE-TRAN-DATE                                         
                    ,:PV-AD-EVENT-ID                                            
                    ,:PV-AD-EVENT-NAME                                          
                    ,:PV-PROMO-ID                                               
                    ,:PV-PROMO-NAME                                             
                    ,:PV-UNITS-SOLD                                             
                    ,:PV-SALES-VOLUME                                           
                    ,:PV-MARKDOWN                                               
                    ,:PV-TOTAL-COST                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = -1405                                             
                MOVE PV-SALE-TRAN-DATE    TO DTL-SALE-TRAN-DATE                 
                MOVE PV-AD-EVENT-ID       TO DTL-AD-EVENT-ID                    
                MOVE PV-AD-EVENT-NAME     TO DTL-AD-EVENT-NAME                  
                MOVE PV-PROMO-NAME        TO DTL-PROMO-NAME                     
                MOVE PV-UNITS-SOLD        TO DTL-UNITS-SOLD                     
                MOVE PV-SALES-VOLUME      TO DTL-SALES-VOLUME                   
                MOVE PV-MARKDOWN          TO DTL-MARKDOWN                       
                MOVE PV-TOTAL-COST        TO DTL-TOTAL-COST                     
                                                                                
               WHEN SQLCODE = 1403                                              
                   SET PS-END-OF-REPORT-CSR TO TRUE                             
                                                                                
               WHEN OTHER                                                       
                   MOVE '2300-FETCH-REPORT-CSR' TO PV-PARAGRAPH-NAME            
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************       08031100
      * 2400-PROCESS-REPORT-CSR.                                        08031200
      ***********************************************************       08031100
       2400-PROCESS-REPORT-CSR.                                         08031700
                                                                                
           MOVE 1                     TO PV-ADVANCE-LINES.                      
           ADD PV-ADVANCE-LINES       TO PV-LINE-CNT.                   08038006
           PERFORM 5000-CHECK-MAX-LINE-CNT.                             08038106
                                                                                
           IF (PV-SALE-TRAN-DATE = PV-SALE-TRAN-DATE-SAVED)                     
              AND PS-NOT-NEW-PAGE                                               
             MOVE SPACES              TO DTL-SALE-TRAN-DATE             08038206
           ELSE                                                                 
             MOVE 2                   TO PV-ADVANCE-LINES                       
             ADD 1                    TO PV-LINE-CNT                            
           END-IF.                                                              
                                                                                
           MOVE DETAIL-01             TO PV-RPT-LINE.                           
           PERFORM 5300-WRITE-RPT-LINE.                                 08039006
                                                                                
           MOVE PV-SALE-TRAN-DATE     TO PV-SALE-TRAN-DATE-SAVED.               
           PERFORM 2300-FETCH-REPORT-CSR.                                       
                                                                                
      ***********************************************************       08031100
      * 2500-CLOSE-REPORT-CSR.                                          08031200
      ***********************************************************       08031100
       2500-CLOSE-REPORT-CSR.                                           08031700
                                                                                
           EXEC SQL                                                             
               CLOSE REPORTCSR                                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = 1403                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '2500-CLOSE-REPORT-CSR' TO PV-PARAGRAPH-NAME            
                   DISPLAY PV-PARAGRAPH-NAME                                    
                   PERFORM Z100-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ***********************************************************       08031100
      * 3000-END-OF-JOB-ROUTINE.                                        08031200
      ***********************************************************       08031100
       3000-END-OF-JOB-ROUTINE.                                         08031700
                                                                        08031800
           MOVE 2                     TO PV-ADVANCE-LINES.              08037906
           ADD PV-ADVANCE-LINES       TO PV-LINE-CNT.                   08038006
           PERFORM 5000-CHECK-MAX-LINE-CNT.                             08038106
           MOVE END-OF-REPORT-LINE    TO PV-RPT-LINE.                   08038206
           PERFORM 5300-WRITE-RPT-LINE.                                 08039006
                                                                        08040000
           CLOSE CONTROL-CARD-FILE                                      08060131
                 SALES-RESULTS-REPORT.                                  08060202
                                                                                
      ************************************************************      11885900
      * 4000-CALENDAR-ROUTINE.                                          11886000
      *     CALLS THE DATE ROUTINE WITH SPECIFIED PARAMETERS.           11886100
      ************************************************************      11885900
       4000-CALENDAR-ROUTINE.                                           11886300
                                                                        11886400
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52          11886500
                                                   DPG53 DPG54          11886600
                                                   DPG55 DPG56.         11886700
           EVALUATE TRUE                                                11886800
               WHEN DPG54-NO-ERROR                                      11886900
                   CONTINUE                                             11887000
               WHEN DPG54-WARNING-ERROR                                 11887100
                   DISPLAY '** WARNING - PROGRAM '                      11887200
                            PC-PROGRAM-NAME         ' **'               11887300
                   DISPLAY '** 4000-CALENDAR-ROUTINE'                   11887400
                   DISPLAY '** CALL TO DPKUT500 RETURNED WARNING ERROR' 11887500
                   DISPLAY '** DPG54-ERROR-MESSAGE: '                   11887600
                   DISPLAY '** ' DPG54-ERROR-MESSAGE                    11887700
                   DISPLAY '** DPG51: ' DPG51                           11887800
                   DISPLAY '** DPG52: ' DPG52                           11887900
                   DISPLAY '** PLEASE INVESTIGATE FURTHER **'           11888000
               WHEN DPG54-SEVERE-ERROR                                  11888100
                   DISPLAY '** ABEND - PROGRAM '                        11888200
                            PC-PROGRAM-NAME         ' **'               11888300
                   DISPLAY '** 4000-CALENDAR-ROUTINE'                   11888400
                   DISPLAY '** CALL TO DPKUT500 RETURNED SEVERE ERROR'  11888500
                   IF DPG54-CALENDAR-TYPE-INVALID                       11888600
                      DISPLAY '** DPG54-CALENDAR-TYPE-INVALID'          11888700
                      DISPLAY '** DPG51-CALENDAR-TYPE: '                11888800
                               DPG51-CALENDAR-TYPE                      11888900
                   END-IF                                               11889000
                   IF DPG54-DATE-INVALID                                11889100
                      DISPLAY '** DPG54-DATE-INVALID'                   11889200
                      DISPLAY '** DPG52-LK-DATE-INPUT: '                11890000
                               DPG52-LK-DATE-INPUT                      11900000
                   END-IF                                               11910000
                   IF DPG54-INCR-DECR-INVALID                           11920000
                      DISPLAY '** DPG54-INCR-DECR-INVALID'              11930000
                      DISPLAY '** DPG51-INCREMENT-DECREMENT-FLAG: '     11940000
                               DPG51-INCREMENT-DECREMENT-FLAG           11950000
                   END-IF                                               11960000
                   MOVE DPG54-ERROR-MESSAGE                             11970000
                                       TO PV-OPERATION-MSG-2            11980000
                   SET ABEND-4019-DATE-ROUTINE-ERROR TO TRUE            11990000
                   PERFORM Z910-ABEND                                   12000000
           END-EVALUATE.                                                12010000
                                                                        12020000
      ************************************************************      11824700
      * 5000-CHECK-MAX-LINE-CNT.                                         1182480
      *     CHECKS TO SEE IF IT HAS REACHED THE MAXIMUM AMOUNT           1182490
      *     OF LINES FOR THE PAGE. WRITES OUT ALL HEADERS.               1182500
      ************************************************************       1182510
       5000-CHECK-MAX-LINE-CNT.                                          1182520
                                                                         1182530
           IF PV-LINE-CNT > PC-MAX-LINE-CNT                              1182610
               PERFORM 5100-PROCESS-PAGE-HEADER                          1182620
               MOVE 2                  TO PV-ADVANCE-LINES               1182671
               ADD PV-ADVANCE-LINES    TO PV-LINE-CNT                    1182681
           ELSE                                                                 
               SET PS-NOT-NEW-PAGE TO TRUE                                      
           END-IF.                                                       1182690
                                                                         1182730
      ***********************************************************        1183580
      * 5100-PROCESS-PAGE-HEADER.                                        1183590
      *     PROCESS REPORT PAGE HEADERS.                                 1183600
      ***********************************************************        1183580
       5100-PROCESS-PAGE-HEADER.                                        11836206
                                                                                
           MOVE ZEROES                 TO PV-LINE-CNT.                  11836400
           ADD 1                       TO PV-PAGE-CNT.                  11836500
           MOVE PV-PAGE-CNT            TO DP200O-PAGE-NUMBER.           11836600
           MOVE 1                      TO DP200O-REPORT-NUMBER.         11836700
                                                                        11836800
           MOVE DP200O-STANDRD-HEADER-200-COLS                          11836900
                                       TO PV-RPT-LINE.                  11837000
           PERFORM 5200-WRITE-SLS-RPT-NEW-PAGE.                         11837101
           SET PS-NEW-PAGE TO TRUE.                                             
                                                                        11837200
           MOVE HEADER-01              TO PV-RPT-LINE.                  11837300
           MOVE 2                      TO PV-ADVANCE-LINES.             11837400
           ADD PV-ADVANCE-LINES        TO PV-LINE-CNT.                  11837500
           PERFORM 5300-WRITE-RPT-LINE.                                 11837601
                                                                        11837700
           MOVE HEADER-02              TO PV-RPT-LINE.                  11837800
           MOVE 3                      TO PV-ADVANCE-LINES.             11837900
           ADD PV-ADVANCE-LINES        TO PV-LINE-CNT.                  11838000
           PERFORM 5300-WRITE-RPT-LINE.                                 11838101
                                                                        11850000
           MOVE HEADER-03              TO PV-RPT-LINE.                  11837800
           MOVE 1                      TO PV-ADVANCE-LINES.             11837900
           ADD PV-ADVANCE-LINES        TO PV-LINE-CNT.                  11838000
           PERFORM 5300-WRITE-RPT-LINE.                                 11838101
                                                                                
      ***********************************************************        1183580
      * 5200-WRITE-SLS-RPT-NEW-PAGE.                                    11876201
      *     WRITES NEW PAGE.                                            11876300
      ***********************************************************        1183580
       5200-WRITE-SLS-RPT-NEW-PAGE.                                     11876501
                                                                        11876600
           WRITE SALES-RESULTS-REPORT-LINE                              11876707
                 FROM PV-RPT-LINE                                       11876800
                 AFTER ADVANCING PAGE.                                  11876900
                                                                        11877000
           ADD 2            TO PV-LINE-CNT.                             11877100
                                                                        11877200
      ***********************************************************        1183580
      * 5300-WRITE-RPT-LINE.                                            11877701
      *     WRITES OUT A LINE ON THE REPORT.                            11877800
      ***********************************************************        1183580
       5300-WRITE-RPT-LINE.                                             11878001
                                                                        11878100
           WRITE SALES-RESULTS-REPORT-LINE                              11878207
                 FROM PV-RPT-LINE                                       11878300
                 AFTER ADVANCING PV-ADVANCE-LINES.                      11878400
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                        11878800
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *****************************************************************         
      * Z100-ABEND.                                                   *         
      *  - PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE            *         
      *****************************************************************         
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' SQLCODE.                           
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *****************************************************************         
      * Z910-ABEND.                                                     12070000
      *     PERFORMS A NON-SQL-ABEND.                                   12080000
      *****************************************************************         
       Z910-ABEND.                                                      12100000
                                                                        12110000
           DISPLAY '** ABEND           **'.                             12120000
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  12130000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          12130000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        12140000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       12150000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       12160000
                                                                        12170000
           CALL 'ILBOABN0' USING ABEND-CODE.                            12180000
