000100**********************************************************                
000200 IDENTIFICATION DIVISION.                                                 
000300**********************************************************                
000400 PROGRAM-ID.      INKRP010.                                               
000500 AUTHOR.          STEVE FLUNKER.                                          
000600 DATE-WRITTEN.    08/04/94.                                               
000700*                                                                         
000800******************************************************************        
000900* PURPOSE. CREATE REPORT OF ALL INVALID DETAILS IN THE INVENTORY *        
001000*          SYSTEM.                                               *        
001100******************************************************************        
25248 *       DB2 TABLES: TINVPAR, TINVLOC, TINVDTL, XMT_LOC                    
001300*      DB2 CURSORS: INV_PARM                                              
001400*         REPORT #: INKRP010-1                                            
001500*****************************************************************         
0300RM* CHANGED BY:  B. MCASEY          03-23-2000         WR#20839             
0300RM* ADDED CODE TO ADD NEW COLUMN AND ELIMINATE OBTAINING DATA FROM          
0300RM* TINVLOC, AND TO USE A NEW INPUT RECORD FORMAT (80 BYTES).               
      *****************************************************************         
IN2002* CHANGED BY:  S. JACKSON   01-28-2002   WR#22960  (ID:IN2002)            
IN2002* CHANGE CSR TO ADD ACTL_INV_DTE TO SQL AND ORDER CURSOR RECORDS          
IN2002* BY ACTL_INV_DTE AND THEN BY STORE NUBER. ADD ACTL_INV_DTE TO            
IN2002* REPORT.                                                                 
25248 ******************************************************************        
25248 * 25248    10-06-2003  UPC CAPABILITIES                                   
25248 *                      -ROD JANSSEN                                       
W26600* W26600   05-19-2004  PHYSICAL INVENTORY - STORE EXPANSION               
W26600*                      M JOHNSON                                          
W27710* 27710    01-11-2005  S07C ABEND WHEN READING AN EMPTY FILE.             
W27710*                      -ROD JANSSEN                                       
W27710* 26006    02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.         
W27710*                      -ERIN SEARL                                        
28545 * 28545    08/10/2006  DEPARTMENT LEVEL INVENTORY PROJECT                 
28545 *                      JOIN TIMVPAR/TINCNTL                               
28545 *                      B. GRAHAM - STRATAGEM                              
SS0607* INE-SM   06/08/2007  REMOVE ZERO LINE BEFORE TOTAL LINE ON RPT.         
SS0607* DEVELOP              SHARON SZPISZAR - STRATAGEM                        
28545 * W33304   08/18/2008  HILO PROJECT                                       
28545 *                      MOD TINVPAR/TINCNTL                                
28545 *                      P. JAEGER - CODEWORKS                              
W26600******************************************************************        
                                                                                
001600 ENVIRONMENT DIVISION.                                                    
001800 CONFIGURATION SECTION.                                                   
001900 SOURCE-COMPUTER. IBM-3090.                                               
002000 OBJECT-COMPUTER. IBM-3090.                                               
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300 FILE-CONTROL.                                                            
002400                                                                          
002500     SELECT PRINT-FILE              ASSIGN TO RPT01.                      
002600                                                                          
002700     SELECT STORE-SELECT-FILE       ASSIGN TO INP01.                      
002800**********************************************************                
002900 DATA DIVISION.                                                           
003000**********************************************************                
003100 FILE SECTION.                                                            
003200                                                                          
003300 FD  PRINT-FILE                                                           
003400     RECORDING MODE F                                                     
003500     BLOCK CONTAINS 0 RECORDS                                             
003600     RECORD CONTAINS 132 CHARACTERS                                       
003700     LABEL RECORDS ARE OMITTED                                            
003800     DATA RECORD IS PRINT-RECORD.                                         
003900 01  PRINT-RECORD                        PIC X(132).                      
004000                                                                          
004100 FD  STORE-SELECT-FILE                                                    
004200     RECORDING MODE IS F                                                  
004300     BLOCK CONTAINS 0 RECORDS.                                            
0300RM*01  STORE-SELECT-RECORD                 PIC X(03).                       
0300RM 01  STORE-SELECT-RECORD                 PIC X(80).                       
004500/                                                                         
004600 WORKING-STORAGE SECTION.                                                 
004700                                                                          
004800                                                                          
004900 01  CD-CPU-DATE.                                                         
005000     05  CD-CPU-DATE-YY               PIC 9(2)       VALUE ZEROS.         
005100     05  CD-CPU-DATE-MM               PIC 9(2)       VALUE ZEROS.         
005200     05  CD-CPU-DATE-DD               PIC 9(2)       VALUE ZEROS.         
005300                                                                          
005400 01  CT-CPU-TIME.                                                         
005500     05  CD-CPU-TIME-HH               PIC 9(2)       VALUE ZEROS.         
005600     05  CD-CPU-TIME-MM               PIC 9(2)       VALUE ZEROS.         
005700     05  CD-CPU-TIME-SS               PIC 9(2)       VALUE ZEROS.         
005800     05  CD-CPU-TIME-HS               PIC 9(2)       VALUE ZEROS.         
005900                                                                          
006000 01  PA-PROGRAM-ACCUMULATORS.                                             
006100     05  PA-LINE-CNT              PIC S9(3) COMP-3   VALUE +99.           
006200     05  PA-PAGE-CNT              PIC 9(3)  COMP-3   VALUE ZERO.          
006300                                                                          
266000 01  PV-PROGRAM-VARIABLES.                                                
266000     05  PV-LOC-NBR                   PIC 9(04)      VALUE ZEROS.         
266000     05  PV-STR-NBR                   PIC S9(04) COMP VALUE ZERO.         
266000     05  PV-SQL-SUB                   PIC 9(03)      VALUE ZEROS.         
006500                                                                          
006600 01  PC-PROGRAM-CONTROLS.                                                 
006700     05  PC-LINE-MAX                  PIC S9(3) VALUE +48 COMP-3.         
006800     05  PC-NEW-PAGE                  PIC S9(3) VALUE +99 COMP-3.         
006900     05  PC-PROGRAM-NAME              PIC X(08) VALUE 'INKRP010'.         
007000     05  PC-1                         PIC 9(02) VALUE 1.                  
007100     05  PC-IN-INVALID-REC            PIC X     VALUE 'Y'.                
007200     05  PC-IN-INVALID-ITM            PIC X     VALUE 'N'.                
007300     05  PC-MAX-RETRIES               PIC S9(3) VALUE +3 COMP-3.          
007400                                                                          
007500*** FOR SYSTEM TEST:                                                      
007600     05  PC-CURRENT-DATE             PIC X(10) VALUE '2000-01-30'.        
007700                                                                          
007800 01  PS-PROGRAM-SWITCHES.                                                 
007900     05  PS-EOF-PARM-SW               PIC X(01)      VALUE 'N'.           
008000         88  PS-NO-EOF-PARM                          VALUE 'N'.           
008100         88  PS-EOF-PARM                             VALUE 'Y'.           
008200     05  PS-NO-STR-INVLD-DATES-SW     PIC X(01)      VALUE 'N'.           
008300         88  PS-STR-INVLD-DATES                      VALUE 'N'.           
008400         88  PS-NO-INVALIDS                          VALUE 'Y'.           
008500     05  PS-NO-INVALID-TOTALS-SW      PIC X(01)      VALUE 'N'.           
008600         88  PS-INVALID-TOTALS                       VALUE 'N'.           
008700         88  PS-NO-INVALID-TOTALS                    VALUE 'Y'.           
008800     05  PS-NO-INVALID-DETAIL-SW      PIC X(01)      VALUE 'N'.           
008900         88  PS-INVALID-DETAIL                       VALUE 'N'.           
009000         88  PS-NO-INVALID-DETAIL                    VALUE 'Y'.           
009100     05  PS-PROCESS-COMPLETE-SW       PIC X(01)      VALUE 'N'.           
009200         88  PS-PROCESS-COMPLETE                     VALUE 'Y'.           
009300     05  PS-ALL-STORE-SW              PIC X(01)      VALUE 'N'.           
009400         88  PS-ALL-STORES                           VALUE 'Y'.           
009500     05  PS-END-OF-STORES-SW          PIC X(01)      VALUE 'N'.           
009600         88  PS-END-OF-STORES                        VALUE 'Y'.           
009700 01  WS-SSR-STR-RECORD.                                                   
009800     05  SSR-STR-NBR                    PIC X(04).                        
009900         88  SSR-ALL-STORES             VALUE '****'.                     
010000/                                                                         
010100 01  DB-DATABASE-LOAD-AREA.                                               
010200     05  DB-TOT-REM-INVALIDS          PIC S9(9) COMP VALUE ZERO.          
010300     05  DB-TOT-REM-INVALIDS-IND      PIC S9(4) COMP VALUE ZERO.          
010400     05  DB-REM-STR-INVALIDS          PIC S9(9) COMP VALUE ZERO.          
010500     05  DB-REM-STR-INVALIDS-IND      PIC S9(4) COMP VALUE ZERO.          
010600     05  DB-INV-CTL-INVALIDS          PIC S9(9) COMP VALUE ZERO.          
010700     05  DB-INV-ORIG-INVALIDS         PIC S9(9) COMP VALUE ZERO.          
010800     05  DB-INV-CTL-INVALIDS-IND      PIC S9(4) COMP VALUE ZERO.          
0300RM     05  DB-TOT-QTY-SCAN              PIC S9(9) COMP VALUE ZERO.          
0300RM     05  DB-TOT-QTY-SCAN-IND          PIC S9(4) COMP VALUE ZERO.          
0300RM     05  DB-ORIG-INVALIDS             PIC S9(9) COMP VALUE ZERO.          
0300RM     05  DB-ORIG-INVALIDS-IND         PIC S9(4) COMP VALUE ZERO.          
0300RM     05  DB-TOT-REMAIN                PIC S9(9) COMP VALUE ZERO.          
0300RM     05  DB-TOT-REMAIN-IND            PIC S9(4) COMP VALUE ZERO.          
0300RM     05  DB-STR-REMAIN                PIC S9(9) COMP VALUE ZERO.          
0300RM     05  DB-STR-REMAIN-IND            PIC S9(4) COMP VALUE ZERO.          
0300RM     05  DB-TOT-CNT-ZERO-QTY          PIC S9(9) COMP VALUE ZERO.          
0300RM     05  DB-TOT-CNT-ZERO-QTY-IND      PIC S9(4) COMP VALUE ZERO.          
010900                                                                          
011000 01  PW-PROGRAM-WORKAREA.                                                 
011100     05  PW-TOT-REM-INVALIDS          PIC S9(9) COMP VALUE ZERO.          
011200     05  PW-TOT-STORE-INVALIDS        PIC S9(9) COMP VALUE ZERO.          
011300     05  PW-TOT-CONTROL-INVALIDS      PIC S9(9) COMP VALUE ZERO.          
011400     05  PW-TOT-ORIG-INVALIDS         PIC S9(9) COMP-3 VALUE +0.          
011500     05  PW-REM-INVALIDS              PIC S9(9) COMP VALUE ZEROES.        
011600     05  PW-STORE-INVALIDS            PIC S9(9) COMP VALUE ZEROES.        
011700     05  PW-CONTROL-INVALIDS          PIC S9(9) COMP VALUE ZEROES.        
011800     05  PW-ORIG-INVALIDS             PIC S9(9) COMP-3 VALUE +0.          
0300RM     05  PW-STORE-TOT-QTY-SCAN        PIC S9(9) COMP VALUE ZERO.          
0300RM     05  PW-TOT-QTY-SCAN              PIC S9(9) COMP VALUE ZERO.          
011900                                                                          
0300RM**************************************************                        
0300RM*  INWS004 COPYBOOK - INPUT FILE                                          
0300RM**************************************************                        
0300RM                                                                          
012400     COPY INWS004.                                                        
012500                                                                          
012000**************************************************                        
012100*  STANDARD HEADER LINE - KOHLS 132 COL                                   
012200**************************************************                        
012300                                                                          
012400     COPY DPWS132O.                                                       
012500                                                                          
012600 01  PRINT-DATA.                                                          
012700     05  PR-PRINT-H1.                                                     
012800         10 FILLER                    PIC X(51)      VALUE SPACES.        
012900         10 FILLER                    PIC X(30)      VALUE                
013000                               'INVALID DETAIL SUMMARY REPORT'.           
013100         10 FILLER                    PIC X(51)      VALUE SPACES.        
                                                                                
013200     05  PR-PRINT-H2.                                                     
013300         10 FILLER                    PIC X(08)      VALUE SPACES.        
013400         10 FILLER                    PIC X(10)      VALUE                
013500                                      'INVENTORY'.                        
013300         10 FILLER                    PIC X(01)      VALUE SPACES.        
013400         10 FILLER                    PIC X(7)       VALUE                
013500                                      'STORE #'.                          
013600         10 FILLER                    PIC X(5)       VALUE SPACES.        
013700         10 FILLER                    PIC X(10)      VALUE                
013800                                      'STORE NAME'.                       
0300RM*        10 FILLER                    PIC X(14)      VALUE SPACES.        
0300RM         10 FILLER                    PIC X(08)      VALUE SPACES.        
0300RM         10 FILLER                    PIC X(19)      VALUE                
0300RM                                      'ORIGINAL INVALIDS'.                
0300RM*        10 FILLER                    PIC X(11)      VALUE                
0300RM         10 FILLER                    PIC X(08)      VALUE                
014100                                      'ORIGINAL'.                         
IN2002         10 FILLER                    PIC X(03)      VALUE SPACES.        
014200         10 FILLER                    PIC X(15)      VALUE                
014300                                      'TOTAL REMAINING'.                  
014400         10 FILLER                    PIC X(04)      VALUE SPACES.        
014500         10 FILLER                    PIC X(14)      VALUE                
014600                                      'REMAINING     '.                   
0300RM         10 FILLER                    PIC X(01)      VALUE SPACES.        
014800         10 FILLER                    PIC X(19)      VALUE                
014900                                      'REMAINING INVENTORY'.              
015000*        10 FILLER                    PIC X(10)      VALUE SPACES.        
                                                                                
015100     05  PR-PRINT-H3.                                                     
015200         10 FILLER                    PIC X(11)      VALUE SPACES.        
015200         10 FILLER                    PIC X(04)      VALUE                
IN2002                                      'DATE'.                             
015200         10 FILLER                    PIC X(40)      VALUE SPACES.        
0300RM         10 FILLER                    PIC X(13)      VALUE                
0300RM                                      'QUANTITY'.                         
0300RM         10 FILLER                    PIC X(13)      VALUE                
015400                                      'INVALIDS'.                         
015500         10 FILLER                    PIC X(15)      VALUE                
015600                                      '   INVALIDS    '.                  
015800         10 FILLER                    PIC X(18)      VALUE                
015900                                      'STORE INVALIDS    '.               
016100         10 FILLER                    PIC X(19)      VALUE                
016200                                      ' CONTROL INVALIDS  '.              
                                                                                
016400     05  PR-DETAIL-REC.                                                   
016500         10 FILLER                    PIC X(08)      VALUE SPACES.        
016600         10 STORE-ACTL-INV-DTE        PIC X(10)      VALUE SPACES.        
016500         10 FILLER                    PIC X(02)      VALUE SPACES.        
W26600         10 STORE-NUMBER              PIC X(04)      VALUE ZEROES.        
W26600         10 FILLER                    PIC X(03)      VALUE SPACES.        
016800         10 STORE-NAME                PIC X(20)      VALUE SPACES.        
016900         10 FILLER                    PIC X(2)       VALUE SPACES.        
0300RM         10 PR-QTY-SCAN               PIC ZZZ,ZZZ,ZZ9.                    
0300RM         10 FILLER                    PIC X(3)       VALUE SPACES.        
017000         10 PR-ORIG-INVALIDS          PIC Z,ZZZ,ZZ9-.                     
017100         10 FILLER                    PIC X(6)       VALUE SPACES.        
017200         10 PR-REM-INVALIDS           PIC Z,ZZZ,ZZ9- VALUE ZEROES.        
017300*        10 FILLER                    PIC X(15)      VALUE SPACES.        
IN2002         10 FILLER                    PIC X(09)      VALUE SPACES.        
017400         10 PR-STORE-INVALIDS         PIC Z,ZZZ,ZZ9- VALUE ZEROES.        
017500*        10 FILLER                    PIC X(14)      VALUE SPACES.        
IN2002         10 FILLER                    PIC X(08)      VALUE SPACES.        
017600         10 PR-CONTROL-INVALIDS       PIC Z,ZZ9-     VALUE ZEROES.        
0300RM*        10 FILLER                    PIC X(17)      VALUE SPACES.        
                                                                                
017800     05  PR-SUM-REC.                                                      
017900         10 FILLER                    PIC X(12)      VALUE SPACES.        
018000         10 FILLER                    PIC X(13)      VALUE                
018100                                      'GRAND TOTALS:'.                    
018200         10 FILLER                    PIC X(24)      VALUE SPACES.        
0300RM         10 PR-TOT-QTY-SCAN           PIC ZZZ,ZZZ,ZZ9.                    
0400RM         10 FILLER                    PIC X(2)       VALUE SPACES.        
018300         10 PR-TOT-ORIG-INVALIDS      PIC ZZ,ZZZ,ZZ9-.                    
018400         10 FILLER                    PIC X(5)       VALUE SPACES.        
018500         10 PR-TOT-REM-INVALIDS       PIC ZZ,ZZZ,ZZ9-.                    
018600         10 FILLER                    PIC X(08)      VALUE SPACES.        
018700         10 PR-TOT-STORE-INVALIDS     PIC ZZ,ZZZ,ZZ9-.                    
018800         10 FILLER                    PIC X(06)      VALUE SPACES.        
018900         10 PR-TOT-CONTROL-INVALIDS   PIC ZZZ,ZZ9-   VALUE ZEROES.        
0300RM*        10 FILLER                    PIC X(17)      VALUE SPACES.        
                                                                                
019100     05  PR-NO-DETAIL.                                                    
019200         10 FILLER                    PIC X(28)      VALUE SPACES.        
019300         10 FILLER                    PIC X(12)      VALUE                
019400                                      '------------'.                     
019500         10 FILLER                    PIC X(6)       VALUE SPACES.        
019600         10 FILLER                    PIC X(34)      VALUE                
019700                               'THERE WERE NO INVALIDS TO REPORT'.        
019800         10 FILLER                    PIC X(6)       VALUE SPACES.        
019900         10 FILLER                    PIC X(12)      VALUE                
020000                                      '------------'.                     
020100         10 FILLER                    PIC X(24)      VALUE SPACES.        
020200                                                                          
020300 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
020400                                                     COMP SYNC.           
020500     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
020600     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
020700     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
020800     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
020900     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
021000     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
021100     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
021200     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
021300     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
021600     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
021700     88  ABEND-4019-DATE-ROUTINE                     VALUE +4019.         
021800                                                                          
022900*                                                                         
023000* DB2 COMMUNICATION AREA.                                                 
023100*                                                                         
023200     EXEC SQL                                                             
25248          INCLUDE XMT00001                                                 
023400     END-EXEC.                                                            
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
023500*                                                                         
023600     EXEC SQL                                                             
023700         INCLUDE TINVPAR                                                  
023800     END-EXEC.                                                            
023900*                                                                         
0300RM*    EXEC SQL                                                             
0300RM*        INCLUDE TINVLOC                                                  
0300RM*    END-EXEC.                                                            
024300*                                                                         
024400     EXEC SQL                                                             
024500         INCLUDE TINVDTL                                                  
024600     END-EXEC.                                                            
024700*                                                                         
024800     EXEC SQL                                                             
024900         INCLUDE SQLCA                                                    
025000     END-EXEC.                                                            
025100                                                                          
025200*                                                                         
025300* DB2 COMMUNICATION AREA 2                                                
025400*                                                                         
025500* DECLARE DB2 CURSOR INV_PARM                                             
025600                                                                          
025700     EXEC SQL                                                             
025800       DECLARE INV_PARM CURSOR FOR                                        
W26600       SELECT DISTINCT (A.LOC_NBR),                                       
26600                         B.LOC_NM,                                         
026000                        A.SCAN_INV_IND,                                   
IN2002                        A.ACTL_INV_DTE                                    
25248        FROM TINVPAR A, XMT_LOC B                                          
28545            ,TINCNTL C                                                     
026200       WHERE A.LOC_NBR = B.LOC_NBR                                        
W26600       AND A.ACTL_INV_RECD_DTE <= CURRENT DATE                            
W26600       AND CURRENT DATE <= A.ACTL_FIN_BK_DTE                              
28545        AND A.INV_ID = C.INV_ID                                            
28545        AND C.ACTV_IND = 'Y'                                               
W33304       AND A.LOC_INV_STAT_CDE = 'IN'                                      
26600        ORDER BY A.ACTL_INV_DTE, A.LOC_NBR                                 
027100     END-EXEC.                                                            
027200                                                                          
027300     EXEC SQL                                                             
027400       DECLARE INV_PARM2 CURSOR FOR                                       
W26600       SELECT DISTINCT (A.LOC_NBR),                                       
W26600                        B.LOC_NM,                                         
027600                        A.SCAN_INV_IND,                                   
IN2002                        A.ACTL_INV_DTE                                    
25248        FROM TINVPAR A, XMT_LOC B                                          
28545            ,TINCNTL C                                                     
W26600       WHERE A.LOC_NBR = B.LOC_NBR                                        
S26600         AND A.LOC_NBR = :PV-STR-NBR                                      
028400         AND A.ACTL_INV_RECD_DTE <= CURRENT DATE                          
028500         AND CURRENT DATE <= A.ACTL_FIN_BK_DTE                            
28545          AND A.INV_ID = C.INV_ID                                          
28545          AND C.ACTV_IND = 'Y'                                             
W33304         AND A.LOC_INV_STAT_CDE = 'IN'                                    
028600                                                                          
26600        ORDER BY A.ACTL_INV_DTE, A.LOC_NBR                                 
028800     END-EXEC.                                                            
028900                                                                          
029000     COPY SQLCA2.                                                         
029100****************************************************************          
029200 PROCEDURE DIVISION.                                                      
029300****************************************************************          
029400                                                                          
029500 0000-MAINLINE.                                                           
029600                                                                          
029700     PERFORM 1000-INITIALIZE.                                             
029800                                                                          
029900     IF  PS-ALL-STORES                                                    
030000         PERFORM 3030-OPEN-INV-PARM-CURSOR                                
030100         PERFORM 2010-FETCH-INV-PARM-RECORD                               
030200         IF  PS-STR-INVLD-DATES                                           
030300             PERFORM 2000-PROCESS-REPORT                                  
030400                 UNTIL PS-PROCESS-COMPLETE                                
030500         ELSE                                                             
030600             PERFORM 2100-PROCESS-NO-DATA                                 
030700         END-IF                                                           
030800         PERFORM 3010-CLOSE-INV-PARM                                      
030900     ELSE                                                                 
031000         PERFORM 1990-PROCESS-BY-STORE                                    
031100             UNTIL PS-END-OF-STORES                                       
031200     END-IF.                                                              
031300                                                                          
031400     PERFORM 3000-END-PROCESSING.                                         
031500                                                                          
031600     GOBACK.                                                              
031700*0000-EXIT.  EXIT.                                                        
031900                                                                          
032000 1000-INITIALIZE.                                                         
                                                                                
032100     PERFORM 1010-ACCEPT-CURR-DATE-TIME.                                  
032200                                                                          
032300     OPEN OUTPUT PRINT-FILE.                                              
032400     OPEN INPUT STORE-SELECT-FILE.                                        
032500     PERFORM 4000-READ-STORE-SELECT.                                      
0300RM     MOVE IN004-STR-NBR TO SSR-STR-NBR.                                   
           IF SSR-ALL-STORES                                                    
032700         SET PS-ALL-STORES       TO TRUE                                  
W27710     ELSE                                                                 
W27710         MOVE IN004-STR-NBR TO PV-STR-NBR                                 
032800     END-IF.                                                              
032900                                                                          
033000     MOVE PC-1                   TO DP132O-REPORT-NUMBER.                 
033100     MOVE PC-PROGRAM-NAME        TO DP132O-PROGRAM-NAME.                  
033200     PERFORM 7000-PRINT-HEADINGS.                                         
033300                                                                          
033400 1010-ACCEPT-CURR-DATE-TIME.                                              
                                                                                
033500     ACCEPT CT-CPU-TIME FROM TIME.                                        
033600     MOVE CD-CPU-TIME-HH         TO DP132O-RUN-HOUR.                      
033700     MOVE CD-CPU-TIME-MM         TO DP132O-RUN-MINUTE.                    
033800                                                                          
033900     ACCEPT CD-CPU-DATE FROM DATE.                                        
034000                                                                          
034100     DISPLAY 'CURRENT-DATE=>' CD-CPU-DATE.                                
034200                                                                          
034300*** FOR SYSTEM TEST:                                                      
034400*    DISPLAY 'TEST CURRENT DATE: ' PC-CURRENT-DATE.                       
034500                                                                          
034600     MOVE CD-CPU-DATE-MM         TO DP132O-RUN-MONTH.                     
034700     MOVE CD-CPU-DATE-DD         TO DP132O-RUN-DAY.                       
034800     MOVE CD-CPU-DATE-YY         TO DP132O-RUN-YEAR.                      
034900/                                                                         
035000 1990-PROCESS-BY-STORE.                                                   
W26600                                                                          
W26600     MOVE IN004-STR-NUMBER       TO INVDTL-LOC-NBR.                       
035100                                                                          
035200     PERFORM 3030-OPEN-INV-PARM-CURSOR.                                   
035300     PERFORM 2010-FETCH-INV-PARM-RECORD.                                  
035400     IF  SQLCODE EQUAL ZERO                                               
035500         SET PS-NO-EOF-PARM TO TRUE                                       
035600         PERFORM 2000-PROCESS-REPORT                                      
035700     END-IF.                                                              
035800     PERFORM 3010-CLOSE-INV-PARM.                                         
035900     PERFORM 4000-READ-STORE-SELECT.                                      
036000                                                                          
W27710     IF (NOT PS-END-OF-STORES)                                            
W27710         MOVE IN004-STR-NBR TO PV-STR-NBR                                 
W27710     END-IF.                                                              
W27710                                                                          
036100 2000-PROCESS-REPORT.                                                     
036200                                                                          
036300* INITIALIZE SWITCHES FOR EACH STORE LOOP                                 
036400*                                                                         
036500     SET PS-STR-INVLD-DATES TO TRUE.                                      
036600     SET PS-NO-INVALID-TOTALS TO TRUE.                                    
036700                                                                          
036800     IF PS-EOF-PARM                                                       
036900        SET PS-PROCESS-COMPLETE TO TRUE                                   
037000     ELSE                                                                 
037100* NEW STORE                                                               
037200*                                                                         
W26600        MOVE INVPAR-LOC-NBR   TO PV-LOC-NBR                               
W26600        MOVE PV-LOC-NBR       TO STORE-NUMBER                             
25248         MOVE XMT00001-LOC-NM TO STORE-NAME                                
037400        MOVE INVPAR-ACTL-INV-DTE TO STORE-ACTL-INV-DTE                    
037500                                                                          
037600* GET INVALIDS FROM TINVDTL                                               
037700     PERFORM                                                              
037800        WITH TEST AFTER                                                   
037900        VARYING PV-SQL-SUB FROM 1 BY 1                                    
038000        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
038100              SQLCODE = 0 OR                                              
038200              SQLCODE = 100                                               
0300RM*       EXEC SQL                                                          
038500*          SELECT (SUM(TOT_REC_INVLID_QTY)-SUM(TOT_REC_CORR_QTY)),        
038600*                 (SUM(TOT_ITM_INVLID_QTY)-SUM(TOT_ITM_CORR_QTY))         
038700*          INTO :DB-TOT-REM-INVALIDS:DB-REM-STR-INVALIDS-IND,             
038800*               :DB-REM-STR-INVALIDS:DB-TOT-REM-INVALIDS-IND              
038900*          FROM TINVLOC                                                   
039000*          WHERE LOC_NBR = :PV-LOC-NBR                                    
039100*       END-EXEC                                                          
0300RM          EXEC SQL                                                        
0300RM            SELECT SUM(REC_QTY)                                           
0300RM                 , COUNT(*)                                               
0300RM            INTO :DB-TOT-QTY-SCAN  :DB-TOT-QTY-SCAN-IND                   
0300RM                 , :DB-ORIG-INVALIDS :DB-ORIG-INVALIDS-IND                
0300RM            FROM TINVDTL                                                  
W26600            WHERE LOC_NBR = :INVPAR-LOC-NBR                               
0300RM          END-EXEC                                                        
                                                                                
039300        EVALUATE TRUE                                                     
039400            WHEN SQLCODE = ZERO                                           
039500                 CONTINUE                                                 
039600            WHEN SQLCODE = +100                                           
039700                 CONTINUE                                                 
039800            WHEN SQLCODE  > ZERO                                          
039900            WHEN SQLCODE = -911                                           
040000                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
0300RM*                  DISPLAY 'MAX RETRIES EXCEEDED ON TINVLOC'              
0300RM                   DISPLAY 'MAX RETRIES EXCEEDED ON TINVDTL #1'           
040200                   PERFORM 9000-SQL-ERROR                                 
040300                 END-IF                                                   
040400            WHEN SQLWARN0 = 'W'                                           
0300RM*                DISPLAY 'WARNING ON SELECT OF INVLOC'                    
0300RM                 DISPLAY 'WARNING ON SELECT OF INVDTL #1'                 
040600                 PERFORM 8000-SQL-WARNING                                 
040700            WHEN OTHER                                                    
0300RM*                DISPLAY 'ERROR ON SELECT OF INVLOC'                      
0300RM                 DISPLAY 'ERROR ON SELECT OF INVDTL #1'                   
040900                 PERFORM 9000-SQL-ERROR                                   
041000        END-EVALUATE                                                      
041100     END-PERFORM                                                          
                                                                                
0300RM***** COUNT NUMBER OF ROWS WHERE QTY = 0                                  
0300RM     PERFORM                                                              
0300RM        WITH TEST AFTER                                                   
0300RM        VARYING PV-SQL-SUB FROM 1 BY 1                                    
0300RM        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
0300RM              SQLCODE = 0 OR                                              
0300RM              SQLCODE = 100                                               
0300RM          EXEC SQL                                                        
0300RM            SELECT COUNT(*)                                               
0300RM            INTO :DB-TOT-CNT-ZERO-QTY :DB-TOT-CNT-ZERO-QTY-IND            
0300RM            FROM  TINVDTL                                                 
W26600*           WHERE  STORE_NBR = :INVPAR-STORE-NBR                          
W26600            WHERE  LOC_NBR = :INVPAR-LOC-NBR                              
0300RM            AND  REC_QTY = 0                                              
0300RM          END-EXEC                                                        
                                                                                
0300RM        EVALUATE TRUE                                                     
0300RM            WHEN SQLCODE = ZERO                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE = +100                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE > ZERO                                           
0300RM            WHEN SQLCODE = -911                                           
0300RM              IF PV-SQL-SUB > PC-MAX-RETRIES                              
0300RM                 DISPLAY 'MAX RETRIES EXCEEDED ON TINVDTL #2'             
0300RM                 PERFORM 9000-SQL-ERROR                                   
0300RM              END-IF                                                      
0300RM            WHEN SQLWARN0 = 'W'                                           
0300RM                 DISPLAY 'WARNING ON SELECT OF INVDTL #2'                 
0300RM                 PERFORM 8000-SQL-WARNING                                 
0300RM            WHEN OTHER                                                    
0300RM                 DISPLAY 'ERROR ON SELECT OF INVDTL #2'                   
0300RM                 PERFORM 9000-SQL-ERROR                                   
0300RM        END-EVALUATE                                                      
0300RM     END-PERFORM                                                          
0300RM                                                                          
0300RM****** ACCUMULATE TOTAL REMAINING                                         
0300RM     PERFORM                                                              
0300RM        WITH TEST AFTER                                                   
0300RM        VARYING PV-SQL-SUB FROM 1 BY 1                                    
0300RM        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
0300RM              SQLCODE = 0 OR                                              
0300RM              SQLCODE = 100                                               
0300RM          EXEC SQL                                                        
0300RM            SELECT COUNT(*)                                               
0300RM            INTO :DB-TOT-REM-INVALIDS :DB-TOT-REM-INVALIDS-IND            
0300RM            FROM TINVDTL                                                  
W26600*           WHERE STORE_NBR = :INVPAR-STORE-NBR                           
W26600            WHERE LOC_NBR = :INVPAR-LOC-NBR                               
0300RM            AND INVALID_REC_IND = 'Y'                                     
0300RM          END-EXEC                                                        
                                                                                
0300RM        EVALUATE TRUE                                                     
0300RM            WHEN SQLCODE = ZERO                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE = +100                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE  > ZERO                                          
0300RM            WHEN SQLCODE = -911                                           
0300RM                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
0300RM                   DISPLAY 'MAX RETRIES EXCEEDED ON TINVDTL #3'           
0300RM                   PERFORM 9000-SQL-ERROR                                 
0300RM                 END-IF                                                   
0300RM            WHEN SQLWARN0 = 'W'                                           
0300RM                 DISPLAY 'WARNING ON SELECT OF INVDTL #3'                 
0300RM                 PERFORM 8000-SQL-WARNING                                 
0300RM            WHEN OTHER                                                    
0300RM                 DISPLAY 'ERROR ON SELECT OF INVDTL #3'                   
0300RM                 PERFORM 9000-SQL-ERROR                                   
0300RM        END-EVALUATE                                                      
0300RM     END-PERFORM                                                          
                                                                                
0300RM****** ACCUMULATE STORE REMAINING                                         
0300RM     PERFORM                                                              
0300RM        WITH TEST AFTER                                                   
0300RM        VARYING PV-SQL-SUB FROM 1 BY 1                                    
0300RM        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
0300RM              SQLCODE = 0 OR                                              
0300RM              SQLCODE = 100                                               
0300RM          EXEC SQL                                                        
0300RM            SELECT COUNT(*)                                               
0300RM            INTO :DB-REM-STR-INVALIDS :DB-REM-STR-INVALIDS-IND            
0300RM            FROM TINVDTL                                                  
W26600*           WHERE STORE_NBR = :INVPAR-STORE-NBR                           
W26600            WHERE LOC_NBR = :INVPAR-LOC-NBR                               
0300RM            AND INVALID_ITM_IND = 'Y'                                     
0300RM          END-EXEC                                                        
                                                                                
0300RM        EVALUATE TRUE                                                     
0300RM            WHEN SQLCODE = ZERO                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE = +100                                           
0300RM                 CONTINUE                                                 
0300RM            WHEN SQLCODE  > ZERO                                          
0300RM            WHEN SQLCODE = -911                                           
0300RM                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
0300RM                   DISPLAY 'MAX RETRIES EXCEEDED ON TINVDTL #4'           
0300RM                   PERFORM 9000-SQL-ERROR                                 
0300RM                 END-IF                                                   
0300RM            WHEN SQLWARN0 = 'W'                                           
0300RM                 DISPLAY 'WARNING ON SELECT OF INVDTL #4'                 
0300RM                 PERFORM 8000-SQL-WARNING                                 
0300RM            WHEN OTHER                                                    
0300RM                 DISPLAY 'ERROR ON SELECT OF INVDTL #4'                   
0300RM                 PERFORM 9000-SQL-ERROR                                   
0300RM        END-EVALUATE                                                      
0300RM     END-PERFORM                                                          
                                                                                
041200*                                                                         
041300* CHECK FOR NULLS - IF NULLS, FILL TOTALS WITH ZEROS                      
041400*                                                                         
041500     IF DB-REM-STR-INVALIDS-IND < 0                                       
041600        MOVE 0 TO DB-REM-STR-INVALIDS                                     
041700                  DB-REM-STR-INVALIDS-IND                                 
041800     END-IF                                                               
                                                                                
041900     IF DB-TOT-REM-INVALIDS-IND < 0                                       
042000        MOVE 0 TO DB-TOT-REM-INVALIDS                                     
042100                  DB-TOT-REM-INVALIDS-IND                                 
042200     END-IF                                                               
                                                                                
0300RM     IF DB-TOT-QTY-SCAN-IND < 0                                           
0300RM        MOVE 0 TO DB-TOT-QTY-SCAN                                         
0300RM                  DB-TOT-QTY-SCAN-IND                                     
0300RM     END-IF.                                                              
                                                                                
0300RM     IF DB-ORIG-INVALIDS-IND < 0                                          
0300RM        MOVE 0 TO DB-ORIG-INVALIDS                                        
0300RM                  DB-ORIG-INVALIDS-IND                                    
0300RM     END-IF.                                                              
042300*                                                                         
042400*  RETURN ABSOLUTE VALUE OF SUM FIELDS                                    
042500*                                                                         
042600*    IF DB-REM-STR-INVALIDS < 0                                           
042700*       MULTIPLY DB-REM-STR-INVALIDS BY -1                                
042800*                GIVING DB-REM-STR-INVALIDS                               
042900*    END-IF                                                               
043000*    IF DB-TOT-REM-INVALIDS < 0                                           
043100*       MULTIPLY DB-TOT-REM-INVALIDS BY -1                                
043200*                GIVING DB-TOT-REM-INVALIDS                               
043300*    END-IF                                                               
043400*                                                                         
043500*  GET INVENTORY CONTROL INVALIDS FROM TINVDTL                            
043600*                                                                         
043700*  DOES THE STORE SCAN? IF SO FETCH NUMBER OF INV CNTL INVALIDS           
043800*  ???? IS THIS EDIT NEEDED ANYMORE????                                   
043900     IF INVPAR-SCAN-INV-IND = 'Y'                                         
044000         PERFORM 2030-FETCH-INV-DTL-RECORD                                
044100         PERFORM 2040-COUNT-ORIG-INVALIDS                                 
044200     ELSE                                                                 
0300RM         MOVE DB-TOT-REM-INVALIDS TO DB-INV-CTL-INVALIDS                  
044400                                    DB-INV-ORIG-INVALIDS                  
0300RM         MOVE 0 TO DB-REM-STR-INVALIDS                                    
044600     END-IF                                                               
044700*                                                                         
044800*  ADD INVALIDS FROM BOTH FETCHES TO TOTALS                               
044900*                                                                         
0300RM     ADD DB-TOT-CNT-ZERO-QTY TO  DB-TOT-QTY-SCAN.                         
0300RM     ADD DB-TOT-QTY-SCAN     TO  PW-STORE-TOT-QTY-SCAN                    
0300RM                                 PW-TOT-QTY-SCAN                          
045000     ADD DB-TOT-REM-INVALIDS TO  PW-REM-INVALIDS                          
045100                                 PW-TOT-REM-INVALIDS                      
045200     ADD DB-REM-STR-INVALIDS TO  PW-STORE-INVALIDS                        
045300                                 PW-TOT-STORE-INVALIDS                    
045400     ADD DB-INV-CTL-INVALIDS TO  PW-CONTROL-INVALIDS                      
045500                                 PW-TOT-CONTROL-INVALIDS                  
045700     ADD DB-INV-ORIG-INVALIDS TO PW-ORIG-INVALIDS                         
045800                                 PW-TOT-ORIG-INVALIDS                     
045900*                                                                         
046000* PRINT INFORMATION ON THE REPORT                                         
046100*                                                                         
046200     PERFORM 5000-PRINT-DETAIL-LINE.                                      
046300*                                                                         
046400* INITIALIZE TOTALS FOR NEXT STORE                                        
046500*                                                                         
046600     INITIALIZE PR-REM-INVALIDS                                           
046700                PR-STORE-INVALIDS                                         
046800                PR-CONTROL-INVALIDS                                       
046900                DB-TOT-REM-INVALIDS                                       
047000                DB-REM-STR-INVALIDS                                       
0300RM                DB-TOT-QTY-SCAN                                           
0300RM                DB-TOT-CNT-ZERO-QTY                                       
047100                DB-INV-CTL-INVALIDS.                                      
                                                                                
047200     MOVE SPACES TO STORE-NUMBER                                          
047300                    STORE-NAME                                            
IN2002                    STORE-ACTL-INV-DTE.                                   
                                                                                
047400     IF  PS-ALL-STORES                                                    
047500         PERFORM 2010-FETCH-INV-PARM-RECORD                               
047600     END-IF.                                                              
047700                                                                          
047800 2010-FETCH-INV-PARM-RECORD.                                              
                                                                                
047900     PERFORM                                                              
048000        WITH TEST AFTER                                                   
048100        VARYING PV-SQL-SUB FROM 1 BY 1                                    
048200        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
048300              SQLCODE = 0 OR                                              
048400              SQLCODE = 100                                               
048500                                                                          
048600        IF  PS-ALL-STORES                                                 
048700            EXEC SQL                                                      
048800                FETCH INV_PARM                                            
26600                 INTO :INVPAR-LOC-NBR,                                     
26600                      :XMT00001-LOC-NM,                                    
049000                     :INVPAR-SCAN-INV-IND,                                
IN2002                     :INVPAR-ACTL-INV-DTE                                 
049100            END-EXEC                                                      
049200        ELSE                                                              
049300            EXEC SQL                                                      
049400                FETCH INV_PARM2                                           
W26600                INTO :INVPAR-LOC-NBR,                                     
W26600                     :XMT00001-LOC-NM,                                    
049600                     :INVPAR-SCAN-INV-IND,                                
IN2002                     :INVPAR-ACTL-INV-DTE                                 
049700            END-EXEC                                                      
049800        END-IF                                                            
049900                                                                          
050000        EVALUATE TRUE                                                     
050100            WHEN SQLCODE = ZERO                                           
050200                 CONTINUE                                                 
050300            WHEN SQLCODE = +100                                           
050400                 MOVE 'Y' TO PS-NO-STR-INVLD-DATES-SW                     
050500                 MOVE 'Y' TO PS-EOF-PARM-SW                               
SS0607                 MOVE 'Y' TO PS-PROCESS-COMPLETE-SW                       
050600            WHEN SQLCODE  = -911                                          
050700                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
050800                    DISPLAY 'MAX RETRIES EXCEEDED ON INV_PARM'            
050900                    PERFORM 9000-SQL-ERROR                                
051000                 END-IF                                                   
051100            WHEN SQLCODE  > ZERO                                          
051200            WHEN SQLWARN0 = 'W'                                           
051300                 DISPLAY 'WARNING ON FETCH OF INV_PARM'                   
051400                 PERFORM 8000-SQL-WARNING                                 
051500            WHEN OTHER                                                    
051600                 DISPLAY 'ERROR ON FETCH OF INV_PARM'                     
051700                 PERFORM 9000-SQL-ERROR                                   
051800        END-EVALUATE                                                      
051900     END-PERFORM.                                                         
052000                                                                          
052100 2030-FETCH-INV-DTL-RECORD.                                               
                                                                                
052200     PERFORM                                                              
052300        WITH TEST AFTER                                                   
052400        VARYING PV-SQL-SUB FROM 1 BY 1                                    
052500        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
052600              SQLCODE = 0 OR                                              
052700              SQLCODE = 100                                               
052800                                                                          
052900        EXEC SQL                                                          
053000            SELECT COUNT(*)                                               
053100            INTO :DB-INV-CTL-INVALIDS:DB-INV-CTL-INVALIDS-IND             
053200            FROM TINVDTL                                                  
W26600*           WHERE STORE_NBR = :INVPAR-STORE-NBR AND                       
W26600            WHERE LOC_NBR = :INVPAR-LOC-NBR AND                           
053400                   INVALID_REC_IND = 'Y' AND                              
053500                  (INVALID_ITM_IND = 'N' OR REC_QTY = 0)                  
053600        END-EXEC                                                          
053700                                                                          
053800        EVALUATE TRUE                                                     
053900            WHEN SQLCODE = ZERO                                           
054000                 CONTINUE                                                 
054100            WHEN SQLCODE = +100                                           
054200                 CONTINUE                                                 
054300            WHEN SQLCODE  > ZERO                                          
054400            WHEN SQLCODE  = -911                                          
054500                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
054600                    DISPLAY 'MAX RETRIES EXCEEDED ON TINVDTL'             
054700                    PERFORM 9000-SQL-ERROR                                
054800                 END-IF                                                   
054900            WHEN SQLWARN0 = 'W'                                           
055000                 DISPLAY 'WARNING ON SELECT OF TINVDTL'                   
055100                 PERFORM 8000-SQL-WARNING                                 
055200            WHEN OTHER                                                    
055300                 DISPLAY 'ERROR ON SELECT OF TINVDTL'                     
055400                 PERFORM 9000-SQL-ERROR                                   
055500        END-EVALUATE                                                      
055600     END-PERFORM.                                                         
055700*                                                                         
055800* CHECK FOR NULLS                                                         
055900*                                                                         
056000     IF DB-INV-CTL-INVALIDS-IND < 0                                       
056100        MOVE 0 TO DB-INV-CTL-INVALIDS.                                    
056200                                                                          
056300     EVALUATE TRUE                                                        
056400         WHEN SQLCODE = ZERO                                              
056500              CONTINUE                                                    
056600         WHEN SQLCODE = +100                                              
056700              CONTINUE                                                    
056800         WHEN SQLCODE  > ZERO                                             
056900         WHEN SQLWARN0 = 'W'                                              
057000              DISPLAY 'WARNING ON SELECT OF TINVDTL'                      
057100              PERFORM 8000-SQL-WARNING                                    
057200         WHEN OTHER                                                       
057300              DISPLAY 'ERROR ON SELECT OF TINVDTL'                        
057400              PERFORM 9000-SQL-ERROR                                      
057500     END-EVALUATE.                                                        
057600/                                                                         
057700 2040-COUNT-ORIG-INVALIDS.                                                
057800                                                                          
057900     MOVE ZERO        TO  DB-INV-ORIG-INVALIDS.                           
058000     PERFORM                                                              
058100        WITH TEST AFTER                                                   
058200        VARYING PV-SQL-SUB FROM 1 BY 1                                    
058300        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
058400              SQLCODE = 0 OR 100 OR -305                                  
058500                                                                          
058600        EXEC SQL                                                          
058700            SELECT COUNT(*)                                               
058800            INTO :DB-INV-ORIG-INVALIDS                                    
058900            FROM TINVDTL                                                  
W26600*           WHERE STORE_NBR = :INVPAR-STORE-NBR                           
W26600            WHERE LOC_NBR = :INVPAR-LOC-NBR                               
059100        END-EXEC                                                          
059200                                                                          
059300        EVALUATE TRUE                                                     
059400            WHEN SQLCODE = ZERO                                           
059500            WHEN SQLCODE = -305                                           
059600            WHEN SQLCODE = +100                                           
059700                 CONTINUE                                                 
059800            WHEN SQLCODE  > ZERO                                          
059900            WHEN SQLCODE  = -911                                          
060000            WHEN SQLCODE  = -904                                          
060100                 IF PV-SQL-SUB > PC-MAX-RETRIES                           
060200                    DISPLAY 'MAX RETRIES EXCEEDED TINVDTL: 2040'          
060300                    PERFORM 9000-SQL-ERROR                                
060400                 END-IF                                                   
060500            WHEN SQLWARN0 = 'W'                                           
060600                 DISPLAY 'WARNING ON SELECT OF TINVDTL: 2040'             
060700                 PERFORM 8000-SQL-WARNING                                 
060800            WHEN OTHER                                                    
060900                 DISPLAY 'ERROR ON SELECT OF TINVDTL: 2040'               
061000                 PERFORM 9000-SQL-ERROR                                   
061100        END-EVALUATE                                                      
061200     END-PERFORM.                                                         
061300/                                                                         
061400 2100-PROCESS-NO-DATA.                                                    
                                                                                
061500     WRITE PRINT-RECORD                                                   
061600         FROM PR-NO-DETAIL                                                
061700         AFTER ADVANCING 2 LINES.                                         
061800                                                                          
061900 3000-END-PROCESSING.                                                     
                                                                                
0300RM     MOVE PW-TOT-QTY-SCAN         TO PR-TOT-QTY-SCAN.                     
062000     MOVE PW-TOT-REM-INVALIDS     TO PR-TOT-REM-INVALIDS.                 
062100     MOVE PW-TOT-STORE-INVALIDS   TO PR-TOT-STORE-INVALIDS.               
062200     MOVE PW-TOT-CONTROL-INVALIDS TO PR-TOT-CONTROL-INVALIDS.             
062300     MOVE PW-TOT-ORIG-INVALIDS    TO PR-TOT-ORIG-INVALIDS.                
062400                                                                          
062500     WRITE PRINT-RECORD                                                   
062600         FROM PR-SUM-REC                                                  
062700         AFTER ADVANCING 3 LINES.                                         
062800                                                                          
062900     CLOSE PRINT-FILE.                                                    
063000     CLOSE STORE-SELECT-FILE.                                             
063100                                                                          
063200 3010-CLOSE-INV-PARM.                                                     
                                                                                
063300     IF  PS-ALL-STORES                                                    
063400         EXEC SQL                                                         
063500             CLOSE INV_PARM                                               
063600         END-EXEC                                                         
063700     ELSE                                                                 
063800         EXEC SQL                                                         
063900             CLOSE INV_PARM2                                              
064000         END-EXEC                                                         
064100     END-IF.                                                              
064200                                                                          
064300     EVALUATE TRUE                                                        
064400         WHEN SQLCODE = ZERO                                              
064500              CONTINUE                                                    
064600         WHEN SQLCODE  > ZERO                                             
064700         WHEN SQLWARN0 = 'W'                                              
064800              DISPLAY 'WARNING ON CLOSE OF INV_PARM'                      
064900              PERFORM 8000-SQL-WARNING                                    
065000         WHEN OTHER                                                       
065100              DISPLAY 'ERROR ON CLOSE OF INV_PARM'                        
065200              PERFORM 9000-SQL-ERROR                                      
065300     END-EVALUATE.                                                        
065400/                                                                         
065500 3030-OPEN-INV-PARM-CURSOR.                                               
                                                                                
065600     PERFORM                                                              
065700        WITH TEST AFTER                                                   
065800        VARYING PV-SQL-SUB FROM 1 BY 1                                    
065900        UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
066000              SQLCODE = 0 OR                                              
066100              SQLCODE = 100                                               
066200                                                                          
066300        IF  PS-ALL-STORES                                                 
066400            EXEC SQL                                                      
066500                OPEN INV_PARM                                             
066600            END-EXEC                                                      
066700        ELSE                                                              
066800            EXEC SQL                                                      
066900                OPEN INV_PARM2                                            
067000            END-EXEC                                                      
067100        END-IF                                                            
067200                                                                          
067300        EVALUATE TRUE                                                     
067400            WHEN SQLCODE = ZERO                                           
067500                 CONTINUE                                                 
067600            WHEN SQLCODE  = +100                                          
067700                 MOVE 'Y' TO PS-NO-STR-INVLD-DATES-SW                     
067800            WHEN SQLCODE = -911                                           
067900              IF PV-SQL-SUB > PC-MAX-RETRIES                              
068000                DISPLAY 'MAX RETRIES EXCEEDED ON OPEN OF INV_PARM'        
068100                PERFORM 9000-SQL-ERROR                                    
068200              END-IF                                                      
068300            WHEN SQLCODE > ZERO                                           
068400            WHEN SQLWARN0 = 'W'                                           
068500                 DISPLAY 'WARNING ON OPEN OF INV_PARM'                    
068600                 PERFORM 8000-SQL-WARNING                                 
068700            WHEN OTHER                                                    
068800                 DISPLAY 'ERROR ON OPEN OF INV_PARM'                      
068900                 PERFORM 9000-SQL-ERROR                                   
069000        END-EVALUATE                                                      
069100     END-PERFORM.                                                         
069200/                                                                         
069300 4000-READ-STORE-SELECT.                                                  
                                                                                
0300RM*    READ STORE-SELECT-FILE INTO WS-SSR-STR-RECORD AT END                 
0300RM     READ STORE-SELECT-FILE                                               
0300RM          INTO IN004-INVENTORY-COUNT-RECORD                               
0300RM      AT END                                                              
               SET PS-END-OF-STORES TO TRUE                                     
W27710*     NOT AT END                                                          
W27710*        MOVE IN004-STR-NBR TO PV-STR-NBR                                 
069600     END-READ.                                                            
069700                                                                          
069800 5000-PRINT-DETAIL-LINE.                                                  
                                                                                
069900     ADD 1 TO PA-LINE-CNT.                                                
070000                                                                          
070100     IF PA-LINE-CNT > PC-LINE-MAX                                         
070200        PERFORM 7000-PRINT-HEADINGS.                                      
070300                                                                          
0300RM     MOVE PW-STORE-TOT-QTY-SCAN TO PR-QTY-SCAN.                           
070400     MOVE PW-REM-INVALIDS       TO PR-REM-INVALIDS.                       
070500     MOVE PW-STORE-INVALIDS     TO PR-STORE-INVALIDS.                     
070600     MOVE PW-CONTROL-INVALIDS   TO PR-CONTROL-INVALIDS.                   
070700     MOVE PW-ORIG-INVALIDS      TO PR-ORIG-INVALIDS.                      
070800                                                                          
070900     WRITE PRINT-RECORD                                                   
071000         FROM PR-DETAIL-REC                                               
071100         AFTER ADVANCING 1 LINE.                                          
071200                                                                          
071300     MOVE ZEROES TO PW-REM-INVALIDS                                       
071400                    PW-STORE-INVALIDS                                     
071500                    PW-ORIG-INVALIDS                                      
0300RM                    PW-STORE-TOT-QTY-SCAN                                 
071600                    PW-CONTROL-INVALIDS.                                  
071700                                                                          
071800 7000-PRINT-HEADINGS.                                                     
                                                                                
071900     ADD 1 TO PA-PAGE-CNT.                                                
072000     MOVE PA-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
072100     MOVE '9' TO PA-LINE-CNT.                                             
072200                                                                          
072300     WRITE PRINT-RECORD                                                   
072400         FROM DP132O-STANDRD-HEADER-132-COLS                              
072500         AFTER ADVANCING PAGE.                                            
072600                                                                          
072700     WRITE PRINT-RECORD                                                   
072800         FROM PR-PRINT-H1                                                 
072900         AFTER ADVANCING 3 LINES.                                         
073000                                                                          
073100     WRITE PRINT-RECORD                                                   
073200         FROM PR-PRINT-H2                                                 
073300         AFTER ADVANCING 3 LINES.                                         
073400                                                                          
073500     WRITE PRINT-RECORD                                                   
073600         FROM PR-PRINT-H3                                                 
073700         AFTER ADVANCING 1 LINE.                                          
073800                                                                          
073900     MOVE SPACES TO PRINT-RECORD.                                         
074000     WRITE PRINT-RECORD                                                   
074100         AFTER ADVANCING 1 LINE.                                          
074200                                                                          
074300 8000-SQL-WARNING.                                                        
                                                                                
074400      MOVE SQLCODE               TO SQLCODE2.                             
074500      MOVE SQLERRD(3)            TO SQLERRD3.                             
074600                                                                          
074700      DISPLAY '** ABEND **       ** = SQLWARNING'.                        
074800      DISPLAY '** PROGRAM        ** = INKRP010'.                          
074900      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
075000      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                         
075100      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                         
075200      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                         
075300      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                         
075400      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                         
075500      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                         
075600      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                         
075700      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                         
075800      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                         
075900                                                                          
076000      SET ABEND-4013-DB2-ERROR   TO TRUE                                  
076100      CALL 'ILBOABN0' USING ABEND-CODE.                                   
076200                                                                          
076300 9000-SQL-ERROR.                                                          
                                                                                
076400      MOVE SQLCODE               TO SQLCODE2.                             
076500      MOVE SQLERRD(3)            TO SQLERRD3.                             
076600                                                                          
076700      DISPLAY '** ABEND **       ** = SQLERROR'.                          
076800      DISPLAY '** PROGRAM        ** = INKRP010'.                          
076900      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                         
077000      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                         
077100      DISPLAY '** SQLERRM        ** = ' SQLERRM.                          
077200      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                         
077300                                                                          
077400      SET ABEND-4013-DB2-ERROR   TO TRUE.                                 
077500      CALL 'ILBOABN0' USING ABEND-CODE.                                   
