/* REXX */                                                                      
/* DEKRX002 - Write out a spool file, up to the maximum number of     */        
/*      allowed characters, including starting and ending lines       */        
/*      identifying the workload object name.  Truncate the spool     */        
/*      line length at 131 characters (it should already be folded    */        
/*      during the preparation before the spool file transfer         */        
/*      process) for proper retention in the sysout capture facility. */        
/*                                                                    */        
/*      I/O errors will cause the job to fail, which will be running  */        
/*      under job name of the spool retrieval workload object.        */        
/*                                                                    */        
/*      ** This iteration will not know the user, when originating    */        
/*         from non-production and will not know the application or   */        
/*         application generation, and platform (server name) no      */        
/*         longer needed to be specified in the process.              */        
                                                                                
ARG WOBNAME                                                                     
                                                                                
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' DEKRX002 - '                                                        
Maximum_spool_records = 10000                                                   
                                                                                
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
WOBNAME = STRIP(WOBNAME)                                                        
                                                                                
Spool_header_message = "### START OF <<"WOBNAME">> SPOOL ###"                   
Spool_trailer_message = "### END OF <<"WOBNAME">> SPOOL ###"                    
                                                                                
IF STRIP(WOBNAME) = '' THEN                                                     
DO                                                                              
  SAY CURRPGM 'WORKLOAD OBJECT NAME WAS NOT PASSED AS AN ARGUMENT'              
  EXIT 4                                                                        
END                                                                             
                                                                                
"EXECIO" Maximum_spool_records+1 "DISKR INP01 (FINIS"                           
                                                                                
SELECT                                                                          
                                                                                
  /* Spool file contains less than or equal to the maximum number of */         
  /* records                                                         */         
                                                                                
  WHEN RC = '2' THEN                                                            
    DO                                                                          
      SAY Spool_header_message                                                  
                                                                                
      DO QUEUED()                                                               
        PARSE PULL SPOOL_LINE                                                   
        SAY LEFT(SPOOL_LINE,131)                                                
      END                                                                       
                                                                                
      SAY Spool_trailer_message                                                 
    END                                                                         
                                                                                
  /* Spool file exceeds the maximum number of records                */         
                                                                                
  WHEN RC = 0 THEN                                                              
    DO                                                                          
      SAY                                                                       
      SAY CENTER("",70,'=')                                                     
      SAY CENTER(" SPOOL FILE EXCEEDS" Maximum_spool_records,                   
                "RECORDS ",70,'=')                                              
      SAY CENTER(" THE FULL LISTING IS AVAILABLE IN THE SPOOL FILE ON",         
                "THE SERVER",70,'=')                                            
      SAY CENTER(" BELOW ARE THE FIRST" Maximum_spool_records+1,                
                "RECORDS ",70,'=')                                              
      SAY CENTER("",70,'=')                                                     
      SAY                                                                       
      SAY Spool_header_message                                                  
                                                                                
      /* Free the file, since the full file was not read  */                    
      'FREE FILE(INP01)'                                                        
                                                                                
      DO QUEUED()                                                               
        PARSE PULL SPOOL_LINE                                                   
        SAY LEFT(SPOOL_LINE,131)                                                
      END                                                                       
                                                                                
      SAY Spool_trailer_message "(Partial Listing)"                             
      SAY                                                                       
      SAY CENTER("",70,'=')                                                     
      SAY CENTER(" SPOOL FILE EXCEEDS" Maximum_spool_records,                   
                "RECORDS ",70,'=')                                              
      SAY CENTER(" THE FULL LISTING IS AVAILABLE IN THE SPOOL FILE ON",         
                "THE SERVER",70,'=')                                            
      SAY CENTER(" ABOVE ARE THE FIRST" Maximum_spool_records+1,                
                "RECORDS ",70,'=')                                              
      SAY CENTER("",70,'=')                                                     
      SAY                                                                       
    END                                                                         
                                                                                
  /* Spool file read or I/O error                                    */         
                                                                                
  OTHERWISE                                                                     
    DO                                                                          
      SAY CURRPGM 'SPOOL FILE COULD NOT BE ALLOCATED'                           
      EXIT 16                                                                   
    END                                                                         
END                                                                             
                                                                                
EXIT                                                                            
