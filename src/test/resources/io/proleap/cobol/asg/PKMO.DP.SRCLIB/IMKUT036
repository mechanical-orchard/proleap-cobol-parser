      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             IMKUT036.                                00050000
       AUTHOR.                 BOB MCASEY.                              00060001
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            5/01/2023.                               00080001
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      *                                                                 00110000
      *-----------------------------------------------------------------00120000
      *    SELECT COLOR DATA FROM TCOLOR TABLE FOR UPCS                 00130001
      *-----------------------------------------------------------------00140000
      *  THE PROGRAM IS A REWRITE OF IMKUT030 TO SELECT THE COLOR       00150001
      *  DATA FROM THE TCOLOR TABLE. INPUT IS SORTED IN COLOR CODE      00160001
      *  ORDER TO IMPROVE PERFORMANCE BY LIMITING THE NUMBER OF SELECTS 00160101
      *  ON THE TCOLOR TABLE.                                           00160201
      *-----------------------------------------------------------------00160400
      *    CHG          DATE        DESCRIPTION                         00160500
      *    --------   --------     ---------------------------------    00160600
      *-----------------------------------------------------------------00161200
                                                                        00161300
      ******************************************************************00161400
       ENVIRONMENT DIVISION.                                            00161500
      ******************************************************************00161600
                                                                        00161700
       CONFIGURATION SECTION.                                           00161800
                                                                        00161900
       SOURCE-COMPUTER.        IBM-3090.                                00162000
       OBJECT-COMPUTER.        IBM-3090.                                00163000
                                                                        00164000
       INPUT-OUTPUT SECTION.                                            00165000
                                                                        00166000
       FILE-CONTROL.                                                    00167000
                                                                        00168000
           SELECT UPC-UNLOAD-IN                                         00169000
               ASSIGN TO INP01.                                         00170000
                                                                        00180000
                                                                        00201000
           SELECT UPC-EXTRACT-OUT                                       00202000
               ASSIGN TO OUT01.                                         00203000
                                                                        00204000
                                                                        00205000
      ******************************************************************00206000
       DATA DIVISION.                                                   00207000
      ******************************************************************00208000
                                                                        00209000
       FILE SECTION.                                                    00210000
                                                                        00220000
       FD  UPC-UNLOAD-IN                                                00230000
           RECORDING MODE IS F                                          00240000
           BLOCK CONTAINS 0 RECORDS                                     00250000
           LABEL RECORDS ARE STANDARD.                                  00260000
       01  UPC-UNLOAD-IN-REC                  PIC X(250).               00270000
                                                                        00280000
       FD  UPC-EXTRACT-OUT                                              00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           LABEL RECORDS ARE STANDARD.                                  00390000
       01  UPC-EXTRACT-OUT-REC                PIC X(250).               00400000
                                                                        00410000
      ******************************************************************00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00450000
                                                          COMP SYNC.    00460000
       01  PC-PROGRAM-CONSTANTS.                                        00470000
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUT036'. 00480000
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2.         00490000
                                                                        00500000
       01  PS-PROGRAM-SWITCHES.                                         00510000
           05  PS-TUPC-UNLOAD-DONE-SW         PIC X(01)    VALUE 'N'.   00511000
               88  PS-TUPC-UNLOAD-DONE                     VALUE 'Y'.   00512000
               88  PS-TUPC-UNLOAD-NOT-DONE                 VALUE 'N'.   00513000
           05  PS-SQL-ERROR-SW                PIC X(01)    VALUE 'N'.   00517000
               88  PS-SQL-ERROR                            VALUE 'Y'.   00518000
               88  PS-NO-SQL-ERROR                         VALUE 'N'.   00519000
                                                                        00520000
       01  PV-PROGRAM-VARIABLES.                                        00530000
           05  PV-UPC-RECS-WRITTEN            PIC S9(09) VALUE +0  COMP.00540000
           05  PV-UPC-RECS-READ               PIC S9(09) VALUE +0  COMP.00550000
           05  PV-TCLOR-SELECT-CTR            PIC S9(09) VALUE +0  COMP.00560000
           05  PV-PARAGRAPH-NAME              PIC  X(30)  VALUE SPACE.  00580000
           05  PV-DB2-OPERATION               PIC  X(30)  VALUE SPACE.  00590000
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE ZERO    00600000
                                                          COMP SYNC.    00610000
           05  PV-INTR-UPC-ID-TEN             PIC S9(10) VALUE +0 COMP. 00611000
           05  PV-DISPLAY-INTERNAL            PIC  9(10) VALUE ZERO.    00612000
           05  PV-PREV-NRF-CLOR-CDE           PIC  X(03) VALUE SPACE.   00613000
           05  PV-PREV-NRF-CLOR-DESC     PIC  X(10) VALUE SPACE.        00614002
           05  PV-PREV-SHRT-CLOR-DESC    PIC  X(04) VALUE SPACE.        00615002
                                                                        00670200
                                                                        00670300
      *--------------------------------------------------               00670400
      *  COPYBOOK FOR ITEM DOMAIN UPC EXTRACT OUTPUT FILE.              00671000
      *--------------------------------------------------               00680000
           COPY IMRD055.                                                00690000
                                                                        00700000
      *--------------------------------------------------               00710000
      *  COLOR TABLE                                                    00720000
      *--------------------------------------------------               00730000
           EXEC SQL                                                     00740000
               INCLUDE TCOLOR                                           00750000
           END-EXEC.                                                    00760000
                                                                        00840000
      *--------------------------------------------------               00850000
      *  COMMUNICATION AREA FOR DB2                                     00860000
      *--------------------------------------------------               00870000
           EXEC SQL                                                     00880000
               INCLUDE SQLCA                                            00890000
           END-EXEC.                                                    00900000
                                                                        00910000
      *--------------------------------------------------               00920000
      *  DB2 SQL ERROR ROUTINE                                          00930000
      *--------------------------------------------------               00940000
           COPY DPWS004.                                                00950000
                                                                        00960000
       PROCEDURE DIVISION.                                              00970000
      *-----------------------------------------------------------------00980000
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                          00990000
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. 01000000
      *-----------------------------------------------------------------01010000
       0000-MAINLINE.                                                   01020000
                                                                        01030000
           PERFORM 0100-HOUSEKEEPING.                                   01040000
                                                                        01050000
           PERFORM 1000-PROCESS-INPUT-FILES                             01060000
               UNTIL PS-TUPC-UNLOAD-DONE.                               01070000
                                                                        01090000
           PERFORM 9000-EOJ.                                            01100000
           GOBACK.                                                      01110000
                                                                        01120000
      *-----------------------------------------------------------------01130000
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE              01140000
      *-----------------------------------------------------------------01150000
       0100-HOUSEKEEPING.                                               01160000
                                                                        01170000
           OPEN INPUT UPC-UNLOAD-IN                                     01180000
                OUTPUT UPC-EXTRACT-OUT.                                 01200000
                                                                        01210000
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01220000
           IF PS-TUPC-UNLOAD-DONE                                       01240000
              DISPLAY '*** '                                            01250000
              DISPLAY '*** IMKUT036 - NO INPUT UPC RECS FOUND'          01260000
              DISPLAY '*** '                                            01270000
              MOVE +4003 TO ABEND-CODE                                  01280000
              PERFORM 9999-ABEND                                        01290000
           END-IF.                                                      01300000
           MOVE HIGH-VALUES TO PV-PREV-NRF-CLOR-CDE.                    01301000
                                                                        01310000
                                                                        01361400
      *-----------------------------------------------------------------01361500
      *  PROCESS ALL INPUT RECORDS AND FORMAT ITEM UPC DOMAIN EXTRACT   01361600
      *  RECORDS UNTIL END OF INPUT FILE.                               01361700
      *-----------------------------------------------------------------01361800
       1000-PROCESS-INPUT-FILES.                                        01361902
      *                                                                 01362000
           IF IM055-NRF-CLOR-CDE  =  PV-PREV-NRF-CLOR-CDE               01364100
               MOVE PV-PREV-NRF-CLOR-DESC TO                            01364302
                    IM055-NRF-CLOR-DESC                                 01364400
               MOVE PV-PREV-SHRT-CLOR-DESC TO                           01364502
                    IM055-SHRT-CLOR-DESC                                01364600
           ELSE                                                         01364700
**TEMP*    DISPLAY 'IM055-NRF-CLOR-CDE  ' IM055-NRF-CLOR-CDE            01364803
               PERFORM 1400-GET-COLOR-DESCS                             01364900
               MOVE COLOR-NRF-CLOR-DESC  TO IM055-NRF-CLOR-DESC         01365300
                                            PV-PREV-NRF-CLOR-DESC       01365402
               MOVE COLOR-SHRT-CLOR-DESC TO IM055-SHRT-CLOR-DESC        01365500
                                           PV-PREV-SHRT-CLOR-DESC       01365602
               MOVE IM055-NRF-CLOR-CDE  TO PV-PREV-NRF-CLOR-CDE         01365700
           END-IF.                                                      01365800
                                                                        01366000
      ***  WRITE UPC OUTPUT RECORD                                      01366100
           PERFORM 7000-WRITE-UPC-OUTPUT-REC.                           01366200
                                                                        01366300
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01367000
                                                                        01370800
      *-----------------------------------------------------------------02150000
      * GET THE COLOR DESCS FROM TCOLOR BASED ON THE NRF COLOR CODE     02160000
      * FROM THE INPUT FILE.                                            02170000
      *-----------------------------------------------------------------02180000
       1400-GET-COLOR-DESCS.                                            02190000
                                                                        02200000
           INITIALIZE DCLTCOLOR.                                        02210000
                                                                        02220000
           PERFORM WITH TEST AFTER                                      02230000
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02240000
                 UNTIL SQLCODE = +0                                     02250000
                    OR SQLCODE = +100                                   02260000
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02270000
               EXEC SQL                                                 02280000
                   SELECT NRF_CLOR_DESC                                 02290000
                         ,SHRT_CLOR_DESC                                02300000
                     INTO :COLOR-NRF-CLOR-DESC                          02310000
                         ,:COLOR-SHRT-CLOR-DESC                         02320000
                     FROM TCOLOR                                        02330000
                    WHERE NRF_CLOR_CDE = :IM055-NRF-CLOR-CDE            02340000
                    WITH UR                                             02350000
               END-EXEC                                                 02360000
                                                                        02370000
               EVALUATE TRUE                                            02380000
                   WHEN SQLCODE = -904                                  02390000
                   WHEN SQLCODE = -913                                  02400000
                       CONTINUE                                         02410000
                   WHEN SQLCODE = ZERO                                  02420000
                       ADD 1 TO PV-TCLOR-SELECT-CTR                     02421000
                   WHEN SQLCODE = +100                                  02440000
                       MOVE SPACES TO COLOR-NRF-CLOR-DESC               02450000
                       MOVE SPACES TO COLOR-SHRT-CLOR-DESC              02460000
                   WHEN OTHER                                           02470000
                       DISPLAY '** '                                    02471000
                               PC-PGM-NAME                              02471100
                               ' - INTR UPC ID: '                       02471200
                               PV-DISPLAY-INTERNAL                      02471300
                       MOVE '1400-GET-COLOR-DESCS' TO PV-PARAGRAPH-NAME 02471400
                       MOVE 'SELECT FROM TCOLOR'   TO PV-DB2-OPERATION  02471500
                       PERFORM 9998-SQL-ERROR                           02471600
               END-EVALUATE                                             02471700
           END-PERFORM.                                                 02471800
                                                                        02471900
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02472000
               DISPLAY '** '                                            02472100
                       PC-PGM-NAME                                      02472200
                       ' - INTR UPC ID: '                               02472300
                       PV-DISPLAY-INTERNAL                              02472400
               MOVE '1400-GET-COLOR-DESCS' TO PV-PARAGRAPH-NAME         02472500
               MOVE 'SELECT FROM TCOLOR'   TO PV-DB2-OPERATION          02472600
               PERFORM 9998-SQL-ERROR                                   02472700
           END-IF.                                                      02472800
                                                                        02472900
      *-----------------------------------------------------------------02473000
      * READ THE UPC UNLOAD INPUT FILE                                  02474000
      *-----------------------------------------------------------------02475000
       6000-READ-UPC-UNLOAD-FILE.                                       02476000
                                                                        02477000
           INITIALIZE IM055-ITEM-UPC-DOMAIN-REC.                        02478000
                                                                        02479000
           READ UPC-UNLOAD-IN INTO IM055-ITEM-UPC-DOMAIN-REC            02480000
              AT END                                                    02490000
                 SET PS-TUPC-UNLOAD-DONE TO TRUE                        02500000
              NOT AT END                                                02510000
                 ADD +1   TO PV-UPC-RECS-READ.                          02520000
                                                                        02530000
                                                                        02650000
      *-----------------------------------------------------------------02660000
      * WRITE THE UPC ITEM DOMAIN OUTPUT FILE.                          02670000
      *-----------------------------------------------------------------02680000
       7000-WRITE-UPC-OUTPUT-REC.                                       02690000
                                                                        02700000
           WRITE UPC-EXTRACT-OUT-REC FROM   IM055-ITEM-UPC-DOMAIN-REC.  02710000
                                                                        02720000
           ADD +1 TO PV-UPC-RECS-WRITTEN.                               02730000
                                                                        02740000
      *-----------------------------------------------------------------02880000
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.    02890000
      *-----------------------------------------------------------------02900000
       9000-EOJ.                                                        02910000
                                                                        02920000
           DISPLAY '============================================='.     02972000
           DISPLAY ' '.                                                 02973000
           DISPLAY ' ******  IMKUT036 PROCESSING SUMMARY  ******  '.    02974000
           DISPLAY ' '.                                                 02974100
           DISPLAY '    UPC RECORDS READ         = '                    02974200
                 PV-UPC-RECS-READ.                                      02974300
           DISPLAY '    COLOR ROW SELECTED       = '                    02974400
                 PV-TCLOR-SELECT-CTR.                                   02974500
           DISPLAY '    UPC RECORDS WRITTEN      = '                    02974600
                 PV-UPC-RECS-WRITTEN.                                   02974700
           DISPLAY ' '.                                                 02974800
           DISPLAY '============================================='.     02975100
                                                                        02975200
                                                                        02975300
           CLOSE UPC-UNLOAD-IN                                          02975400
                 UPC-EXTRACT-OUT.                                       02975600
                                                                        02975700
      *-----------------------------------------------------------------03070000
      * 9800-SQL-ABEND - DISPLAY DB2 ABEND INFO                         03080000
      *-----------------------------------------------------------------03090000
       9998-SQL-ERROR.                                                  03100000
                                                                        03110000
           SET PS-SQL-ERROR TO TRUE.                                    03120000
                                                                        03130000
           DISPLAY '** '                                                03140000
                   PC-PGM-NAME                                          03150000
                   ' ABEND IN PARAGRAPH '                               03160000
                   PV-PARAGRAPH-NAME.                                   03170000
           DISPLAY '** '                                                03180000
                   PC-PGM-NAME                                          03190000
                   ' OPERATION = ' PV-DB2-OPERATION.                    03200000
                                                                        03210000
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     03220000
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               03230000
           COPY DPPD004.                                                03240000
                                                                        03250000
           EXEC SQL                                                     03260000
               ROLLBACK                                                 03270000
           END-EXEC.                                                    03280000
                                                                        03290000
           MOVE +4013 TO ABEND-CODE                                     03300000
           PERFORM 9999-ABEND.                                          03310000
                                                                        03320000
      *-----------------------------------------------------------------03330000
      * 9999-ABEND - FORCES PROGRAM ABEND WITH RETURN CODE              03340000
      *-----------------------------------------------------------------03350000
       9999-ABEND.                                                      03360000
                                                                        03370000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03380000
                                                                        03390000
