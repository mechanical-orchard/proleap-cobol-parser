       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT101.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  08/12/99.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS PROGRAM REFORMATS THE UNIX 852 FILE SO IT CAN BE USED    *        
      *  BY APPLICATION INTEGRATOR.                                             
      ******************************************************************        
JF0202* 02-28-2002 JON FIEDLER     JF0202                WR# : 22794            
JF0202* ENHANCED CODE TO PROCESS COLBY 852'S, INSTEAD OF SENDING UPC            
JF0202* NBR COLBY WILL RECEIVE THE STYLE NUMBER, STYLE DESCRIPTION,             
JF0202* COLOR #, COLOR DESCRIPTION, SIZE CODE, AND SIZE DESCRIPTION ON          
JF0202* THE ITEM RECORD.                                                        
************************************************************************        
JF0305*  DATE  02/17/2005                                                       
JF0305*  CHANGE MADE:                                                           
JF0305*   UPDATED PROGRAM FOR STORE EXPANSION TO FIVE DIGITS.                   
JF0305*  MADE BY: JON FIEDLER             WR# : WR26913                         
293408******************************************************************        
293408*  DATE  09/12/2023                                                       
293408*  CHANGE MADE:                                                           
293408*   UPDATED PROGRAM TO SEND CORRECT RETAIL UNIT PRICE FOR CLAIRES.        
293408*  MADE BY: SRINIVASAN SIVARAJ      WR# : CHG0293408                      
293408******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS ET005-EDI-SALES-EXTRACT-REC.                          
                                                                                
           COPY ETRD005.                                                        
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
JF0305     RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           COPY ETRD019.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
JF0202 01  PROGRAM-CONSTANTS.                                                   
JF0202     05  PC-COLBY-ENT-ID       PIC 9(10)      VALUE 10593.                
293408     05  PC-CLAIRE-ENT-ID      PIC 9(10)      VALUE 232413.               
                                                                                
       01  WS-DATE-MMDDYY            PIC X(6)      VALUE SPACES.                
       01  WS-OLD-DATE REDEFINES WS-DATE-MMDDYY.                                
           05  WS-OLD-DATE-MM        PIC X(2).                                  
           05  WS-OLD-DATE-DD        PIC X(2).                                  
           05  WS-OLD-DATE-YY        PIC X(2).                                  
                                                                                
       01  WS-DATE-CCYYMMDD          PIC X(8)      VALUE SPACES.                
       01  WS-NEW-DATE REDEFINES WS-DATE-CCYYMMDD.                              
           05  WS-NEW-DATE-CC        PIC X(2).                                  
           05  WS-NEW-DATE-YY        PIC X(2).                                  
           05  WS-NEW-DATE-MM        PIC X(2).                                  
           05  WS-NEW-DATE-DD        PIC X(2).                                  
                                                                                
       01  PREV-DEPARTMENT           PIC X(3)      VALUE SPACES.                
       01  PREV-ENT-ID               PIC 9(10)     VALUE ZERO.                  
                                                                                
       01  WS-HOLD-UNIT-PRICE        PIC S9(9)V99  VALUE ZERO.                  
                                                                                
       01  SUB                       PIC 9(5)      VALUE ZERO.                  
       01  ET019-SUB                 PIC 9(3)      VALUE ZERO.                  
                                                                                
       01  WS-OUTPUT-LOOP-TABLE.                                                
JF0305     05  WS-STORE-TABLE OCCURS 10000 TIMES.                               
JF0305         10  WS-TBL-QS-STORE   PIC X(5).                                  
               10  WS-TBL-QTY-SOLD   PIC S9(7).                                 
JF0305         10  WS-TBL-QA-STORE   PIC X(5).                                  
               10  WS-TBL-QTY-AVAIL  PIC S9(7).                                 
                                                                                
JF0202 01  PROGRAM-VARIABLES.                                                   
JF0202     05  PV-PARAGRAPH-NAME     PIC X(30).                                 
JF0202     05  PV-DB2-OPER-ATTEMPTED PIC X(30).                                 
JF0202     05  PV-UPC-CODE           PIC  S9(15)V COMP-3.                       
SP1104                                                                          
SP1104     05  PV-CHAR-UPC-CODE      PIC X(15).                                 
SP1104     05  FILLER REDEFINES PV-CHAR-UPC-CODE.                               
SP1104         10  FILLER            PIC X(2).                                  
SP1104         10  PV-EAN            PIC X(1).                                  
SP1104         10  FILLER            PIC X(12).                                 
                                                                                
       01  END-OF-INPUT-FILE-SW      PIC X(1)      VALUE 'N'.                   
           88  END-OF-INPUT-FILE                   VALUE 'Y'.                   
                                                                                
JF0202 01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
JF0202     88 ABEND-4013                         VALUE +4013.                   
JF0202                                                                          
JF0202*****************************************************************         
JF0202*    SQL ABEND ROUTINE WORKING STORAGE                          *         
JF0202*****************************************************************         
JF0202     COPY DPWS004.                                                        
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        INCLUDE TUPC                                                      
JF0202     END-EXEC.                                                            
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        INCLUDE TMDARVS                                                   
JF0202     END-EXEC.                                                            
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        INCLUDE TVNDSTL                                                   
JF0202     END-EXEC.                                                            
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        INCLUDE TSIZE                                                     
JF0202     END-EXEC.                                                            
293408                                                                          
293408     EXEC SQL                                                             
293408        INCLUDE TUPCPLS                                                   
293408     END-EXEC.                                                            
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        INCLUDE SQLCA                                                     
JF0202     END-EXEC.                                                            
JF0202                                                                          
JF0202     COPY SQLCA2.                                                         
JF0202                                                                          
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           INITIALIZE WS-OUTPUT-LOOP-TABLE.                                     
                                                                                
           PERFORM 1000-REFORMAT-FILE                                           
              THRU 1000-REFORMAT-FILE-EXIT                                      
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           PERFORM 5000-QS-DETAIL                                               
              THRU 5000-QS-DETAIL-EXIT.                                         
           PERFORM 6000-QA-DETAIL                                               
              THRU 6000-QA-DETAIL-EXIT.                                         
                                                                                
           CLOSE INPUT-FILE.                                                    
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-REFORMAT-FILE.                                                      
                                                                                
           IF PREV-DEPARTMENT NOT = ET005-DEPT OR                               
              PREV-ENT-ID NOT = ET005-ENT-ID                                    
                 PERFORM 5000-QS-DETAIL                                         
                    THRU 5000-QS-DETAIL-EXIT                                    
                 PERFORM 6000-QA-DETAIL                                         
                    THRU 6000-QA-DETAIL-EXIT                                    
                 MOVE ZERO TO SUB                                               
                 INITIALIZE WS-OUTPUT-LOOP-TABLE                                
                 PERFORM 2000-HEADER-RECORD                                     
                    THRU 2000-HEADER-RECORD-EXIT                                
                 MOVE ET005-ENT-ID TO PREV-ENT-ID                               
                 MOVE ET005-DEPT TO PREV-DEPARTMENT.                            
                                                                                
           IF ET005-RECORD-TYPE = '2'                                           
              PERFORM 5000-QS-DETAIL                                            
                 THRU 5000-QS-DETAIL-EXIT                                       
              PERFORM 6000-QA-DETAIL                                            
                 THRU 6000-QA-DETAIL-EXIT                                       
              MOVE ZERO TO SUB                                                  
              INITIALIZE WS-OUTPUT-LOOP-TABLE                                   
              PERFORM 3000-ITEM-RECORD                                          
                 THRU 3000-ITEM-RECORD-EXIT.                                    
                                                                                
           IF ET005-RECORD-TYPE = '1'                                           
              ADD 1 TO SUB                                                      
              PERFORM 4000-DETAIL-RECORD                                        
                 THRU 4000-DETAIL-RECORD-EXIT.                                  
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-REFORMAT-FILE-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2000-HEADER-RECORD.                                                      
                                                                                
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           MOVE '01' TO ET019-RECORD-TYPE.                                      
           MOVE ET005-ENT-ID TO ET019-ENT-ID.                                   
                                                                                
           IF ET005-DAILY-SALES                                                 
              MOVE ET005-CURRENT-DATE TO WS-DATE-MMDDYY                         
           ELSE                                                                 
              MOVE ET005-WEEK-START-DATE TO WS-DATE-MMDDYY.                     
           PERFORM 7000-REFORMAT-DATE                                           
              THRU 7000-REFORMAT-DATE-EXIT.                                     
           MOVE WS-DATE-CCYYMMDD TO ET019-START-DATE.                           
                                                                                
           IF ET005-DAILY-SALES                                                 
              MOVE SPACES TO ET019-END-DATE                                     
           ELSE                                                                 
              MOVE ET005-WEEK-END-DATE TO WS-DATE-MMDDYY                        
              PERFORM 7000-REFORMAT-DATE                                        
                 THRU 7000-REFORMAT-DATE-EXIT                                   
              MOVE WS-DATE-CCYYMMDD TO ET019-END-DATE.                          
                                                                                
           MOVE ET005-DEPT TO ET019-DEPARTMENT.                                 
                                                                                
           WRITE ET019-PRODUCT-ACTIVITY-RECORD.                                 
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
       2000-HEADER-RECORD-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       3000-ITEM-RECORD.                                                        
                                                                                
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           MOVE '02' TO ET019-RECORD-TYPE.                                      
SP1104     MOVE ET005-UPC-CODE TO PV-CHAR-UPC-CODE.                             
SP1104     IF PV-EAN = '0'                                                      
SP1104        MOVE 'UP' TO ET019-UPC-TYPE                                       
SP1104     ELSE                                                                 
SP1104        MOVE 'EN' TO ET019-UPC-TYPE.                                      
           MOVE ET005-UPC-CODE TO ET019-UPC-CODE.                               
           MOVE ET005-UPC-CODE TO PV-UPC-CODE.                                  
                                                                                
293408     IF ET005-ENT-ID = PC-CLAIRE-ENT-ID                                   
293408        PERFORM 3600-GET-CLAIRE-ITEM-REC                                  
293408           THRU 3600-GET-CLAIRE-ITEM-REC-EXIT                             
293408     ELSE                                                                 
293408        MOVE ET005-UNIT-PRICE-ALL TO WS-HOLD-UNIT-PRICE                   
293408     END-IF.                                                              
                                                                                
JF0202     IF ET005-ENT-ID = PC-COLBY-ENT-ID                                    
JF0202        PERFORM 3500-GET-COLBY-ITEM-REC                                   
JF0202           THRU 3500-GET-COLBY-ITEM-REC-EXIT                              
JF0202        IF SQLCODE = +000                                                 
JF0202           INSPECT MDARVS-VS-DESC REPLACING ALL '*' BY '-'                
JF0202           INSPECT MDARVS-VS-DESC REPLACING ALL '¼' BY ' '                
JF0202           MOVE MDARVS-VS-DESC      TO ET019-VS-DESC                      
JF0202                                                                          
JF0202           MOVE VNDSTL-VS-NBR       TO ET019-VS-NBR                       
JF0202                                                                          
JF0202           IF UPC-NRF-CLOR-CDE = ZERO                                     
JF0202              MOVE 'NA' TO ET019-NRF-CLOR-CDE                             
JF0202           ELSE                                                           
JF0202              MOVE UPC-NRF-CLOR-CDE TO ET019-NRF-CLOR-CDE                 
JF0202           END-IF                                                         
JF0202                                                                          
JF0202           IF UPC-CSTM-CLOR-DESC = SPACES                                 
JF0202              MOVE 'NONE' TO ET019-CSTM-CLOR-DESC                         
JF0202           ELSE                                                           
JF0202              MOVE UPC-CSTM-CLOR-DESC TO ET019-CSTM-CLOR-DESC             
JF0202           END-IF                                                         
JF0202                                                                          
JF0202           IF SIZE-KOHLS-SIZE-CDE = ZERO                                  
JF0202              MOVE 'NA' TO ET019-KOHLS-SIZE-CDE                           
JF0202           ELSE                                                           
JF0202              MOVE SIZE-KOHLS-SIZE-CDE TO ET019-KOHLS-SIZE-CDE            
JF0202           END-IF                                                         
JF0202                                                                          
JF0202           IF SIZE-KOHLS-DESC = SPACES                                    
JF0202              MOVE 'NONE' TO SIZE-KOHLS-DESC                              
JF0202           ELSE                                                           
JF0202              IF SIZE-KOHLS-DESC = '0'                                    
JF0202                 MOVE 'ZERO' TO SIZE-KOHLS-DESC                           
JF0202              END-IF                                                      
JF0202           END-IF                                                         
JF0202                                                                          
JF0202           MOVE SIZE-KOHLS-DESC TO ET019-KOHLS-DESC                       
JF0202        ELSE                                                              
JF0202           DISPLAY '**COLBY INFORMATION NOT FOUND**'                      
JF0202           DISPLAY 'UPC NBR  = ' ET005-UPC-CODE                           
JF0202           DISPLAY ' '                                                    
JF0202        END-IF                                                            
JF0202     END-IF.                                                              
                                                                                
           WRITE ET019-PRODUCT-ACTIVITY-RECORD.                                 
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
       3000-ITEM-RECORD-EXIT.                                                   
           EXIT.                                                                
                                                                                
JF0202 3500-GET-COLBY-ITEM-REC.                                                 
JF0202                                                                          
JF0202     EXEC SQL                                                             
JF0202        SELECT VS_NBR                                                     
JF0202              ,VS_DESC                                                    
JF0202              ,NRF_CLOR_CDE                                               
JF0202              ,CSTM_CLOR_DESC                                             
JF0202              ,S.KOHLS_SIZE_CDE                                           
JF0202              ,KOHLS_DESC                                                 
JF0202        INTO   :VNDSTL-VS-NBR                                             
JF0202              ,:MDARVS-VS-DESC                                            
JF0202              ,:UPC-NRF-CLOR-CDE                                          
JF0202              ,:UPC-CSTM-CLOR-DESC                                        
JF0202              ,:SIZE-KOHLS-SIZE-CDE                                       
JF0202              ,:SIZE-KOHLS-DESC                                           
JF0202        FROM   TUPC    U                                                  
JF0202              ,TMDARVS M                                                  
JF0202              ,TVNDSTL V                                                  
JF0202              ,TSIZE   S                                                  
JF0202        WHERE  U.UPC_NBR = :PV-UPC-CODE                                   
JF0202          AND  U.SUB_CL_MDSEAR_ID = M.SUB_CL_MDSEAR_ID                    
JF0202          AND  U.VS_ID = M.VS_ID                                          
JF0202          AND  M.VS_ID = V.VS_ID                                          
JF0202          AND  U.KOHLS_SIZE_CDE = S.KOHLS_SIZE_CDE                        
JF0202          AND  U.EFF_END_DTE IS NULL                                      
JF0202     END-EXEC.                                                            
JF0202                                                                          
JF0202     IF SQLCODE = +000 OR SQLCODE = +100                                  
JF0202        CONTINUE                                                          
JF0202     ELSE                                                                 
JF0202        MOVE '3500-GET-COLBY-ITEM-REC' TO PV-PARAGRAPH-NAME               
JF0202        MOVE 'SELECT COLBY ITEM REC' TO PV-DB2-OPER-ATTEMPTED             
JF0202        PERFORM 9999-SQL-ERROR.                                           
JF0202                                                                          
JF0202 3500-GET-COLBY-ITEM-REC-EXIT.                                            
JF0202     EXIT.                                                                
                                                                                
293408 3600-GET-CLAIRE-ITEM-REC.                                                
293408                                                                          
293408     EXEC SQL                                                             
293408        SELECT UNT_RTL_AMT                                                
293408        INTO   :UPCPLS-UNT-RTL-AMT                                        
293408        FROM   TUPCPLS A                                                  
293408              ,TUPC    B                                                  
293408              ,TSKXREF C                                                  
293408        WHERE  B.UPC_NBR = :PV-UPC-CODE                                   
293408          AND  A.ITM_NBR = C.ITM_NBR                                      
293408          AND  B.INTR_UPC_ID = C.INTR_UPC_ID                              
293408          AND  A.EFF_END_TMST IS NULL                                     
293408     END-EXEC.                                                            
293408                                                                          
293408     IF SQLCODE = +000                                                    
293408        MOVE UPCPLS-UNT-RTL-AMT TO WS-HOLD-UNIT-PRICE                     
293408     ELSE                                                                 
293408        MOVE '3600-GET-CLAIRE-ITEM-REC' TO PV-PARAGRAPH-NAME              
293408        MOVE '3600-GET-CLAIRE-ITEM-REC-EXIT'                              
293408                                          TO PV-DB2-OPER-ATTEMPTED        
293408        PERFORM 9999-SQL-ERROR.                                           
293408                                                                          
293408 3600-GET-CLAIRE-ITEM-REC-EXIT.                                           
293408     EXIT.                                                                
293408                                                                          
       4000-DETAIL-RECORD.                                                      
                                                                                
           MOVE ET005-STORE-NUMBER TO WS-TBL-QS-STORE(SUB).                     
           MOVE ET005-UNITS-SOLD-ALL TO WS-TBL-QTY-SOLD(SUB).                   
                                                                                
           IF ET005-ONHAND                                                      
              MOVE ET005-STORE-NUMBER TO WS-TBL-QA-STORE(SUB)                   
              MOVE ET005-ONHAND-QUANTITY TO WS-TBL-QTY-AVAIL(SUB).              
                                                                                
       4000-DETAIL-RECORD-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       5000-QS-DETAIL.                                                          
                                                                                
           IF WS-TBL-QS-STORE(1) NOT = SPACES                                   
              MOVE '03' TO ET019-RECORD-TYPE                                    
              MOVE 'QS' TO ET019-DETAIL-TYPE                                    
              WRITE ET019-PRODUCT-ACTIVITY-RECORD                               
              MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD                      
              MOVE '04' TO ET019-RECORD-TYPE                                    
              MOVE WS-HOLD-UNIT-PRICE TO ET019-UNIT-PRICE                       
              WRITE ET019-PRODUCT-ACTIVITY-RECORD                               
              MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                     
                                                                                
           MOVE ZERO TO ET019-SUB.                                              
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           PERFORM VARYING SUB FROM 1 BY 1                                      
              UNTIL WS-TBL-QS-STORE(SUB) = SPACES                               
                                                                                
              ADD 1 TO ET019-SUB                                                
                                                                                
              MOVE WS-TBL-QS-STORE(SUB) TO ET019-STORE(ET019-SUB)               
              MOVE WS-TBL-QTY-SOLD(SUB) TO ET019-QUANTITY(ET019-SUB)            
                                                                                
              IF ET019-SUB = 10                                                 
                 MOVE '05' TO ET019-RECORD-TYPE                                 
                 WRITE ET019-PRODUCT-ACTIVITY-RECORD                            
                 MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD                   
                 MOVE ZERO TO ET019-SUB                                         
              END-IF                                                            
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF ET019-SUB > ZERO                                                  
              PERFORM UNTIL ET019-SUB > 9                                       
                 ADD 1 TO ET019-SUB                                             
                 MOVE ZERO TO ET019-QUANTITY(ET019-SUB)                         
              END-PERFORM                                                       
              MOVE '05' TO ET019-RECORD-TYPE                                    
              WRITE ET019-PRODUCT-ACTIVITY-RECORD                               
              MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                     
                                                                                
       5000-QS-DETAIL-EXIT.                                                     
           EXIT.                                                                
                                                                                
                                                                                
       6000-QA-DETAIL.                                                          
                                                                                
           IF WS-TBL-QA-STORE(1) NOT = SPACES                                   
              MOVE '03' TO ET019-RECORD-TYPE                                    
              MOVE 'QA' TO ET019-DETAIL-TYPE                                    
              WRITE ET019-PRODUCT-ACTIVITY-RECORD                               
              MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                     
                                                                                
           MOVE ZERO TO ET019-SUB.                                              
           MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                        
                                                                                
           PERFORM VARYING SUB FROM 1 BY 1                                      
              UNTIL WS-TBL-QA-STORE(SUB) = SPACES                               
                                                                                
              ADD 1 TO ET019-SUB                                                
                                                                                
              MOVE WS-TBL-QA-STORE(SUB) TO ET019-STORE(ET019-SUB)               
              MOVE WS-TBL-QTY-AVAIL(SUB) TO ET019-QUANTITY(ET019-SUB)           
                                                                                
              IF ET019-SUB = 10                                                 
                 MOVE '05' TO ET019-RECORD-TYPE                                 
                 WRITE ET019-PRODUCT-ACTIVITY-RECORD                            
                 MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD                   
                 MOVE ZERO TO ET019-SUB                                         
              END-IF                                                            
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF ET019-SUB > ZERO                                                  
              PERFORM UNTIL ET019-SUB > 9                                       
                 ADD 1 TO ET019-SUB                                             
                 MOVE ZERO TO ET019-QUANTITY(ET019-SUB)                         
              END-PERFORM                                                       
              MOVE '05' TO ET019-RECORD-TYPE                                    
              WRITE ET019-PRODUCT-ACTIVITY-RECORD                               
              MOVE SPACES TO ET019-PRODUCT-ACTIVITY-RECORD.                     
                                                                                
       6000-QA-DETAIL-EXIT.                                                     
           EXIT.                                                                
                                                                                
                                                                                
       7000-REFORMAT-DATE.                                                      
                                                                                
           IF WS-OLD-DATE-YY > 80                                               
              MOVE '19' TO WS-NEW-DATE-CC                                       
           ELSE                                                                 
              MOVE '20' TO WS-NEW-DATE-CC.                                      
                                                                                
           MOVE WS-OLD-DATE-YY TO WS-NEW-DATE-YY.                               
           MOVE WS-OLD-DATE-MM TO WS-NEW-DATE-MM.                               
           MOVE WS-OLD-DATE-DD TO WS-NEW-DATE-DD.                               
                                                                                
       7000-REFORMAT-DATE-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       8000-READ-INPUT-FILE.                                                    
                                                                        ETKUT004
           READ INPUT-FILE                                              ETKUT004
              AT END                                                    ETKUT004
                 SET END-OF-INPUT-FILE TO TRUE.                         ETKUT004
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
JF0202 9999-SQL-ERROR.                                                          
JF0202                                                                          
JF0202     SET ABEND-4013 TO TRUE.                                              
JF0202                                                                          
JF0202     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
JF0202     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
JF0202                                                                          
JF0202     COPY DPPD004.                                                        
JF0202                                                                          
JF0202     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
