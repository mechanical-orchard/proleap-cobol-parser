      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.             PMKRP145.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           12/06/2010.                                      
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * REMARKS                                                        *        
      * -------                                                        *        
      *                                                                *        
      * TITLE        - FUTURE RETAIL PRICE CHANGE REPORT                        
      *                                                                *        
      * DESCRIPTION  - THIS PROGRAM  WILL GENERATE THE REPORT IN THE            
      *                REQUIRED FORMAT FROM THE SORTED OUTPUT OF THE   *        
      *                EXTRACT PROGRAM PMKUT642                        *        
      *                                                                *        
      *  CHANGE HISTORY                                                *        
      ******************************************************************        
      * WR NUMBER     PROGRAMMER       DATE                            *        
      * ---------------------------------------------------------------*        
      * WR39399       COGNIZANT        12/06/2010                               
      * ORIGINAL VERSION                                               *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN                       TO INP01                            
               FILE STATUS                  IS INPUT-FILE-PS.                   
                                                                                
           SELECT PROMO-EXCEPTION-RPT                                           
               ASSIGN                       TO RPT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 373 CHARACTERS                                       
           DATA RECORD IS IN-INPUT-RECORD.                                      
       01  IN-INPUT-RECORD                  PIC  X(373).                        
                                                                                
       FD  PROMO-EXCEPTION-RPT                                                  
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                      PIC  X(200).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                    PIC S9(04) COMP VALUE +0.              
           88  ABEND-4001-WRITE-ERROR                VALUE +4001.               
           88  ABEND-4002-READ-ERROR                 VALUE +4002.               
           88  ABEND-4003-CTRL-CARD-ERROR            VALUE +4003.               
           88  ABEND-4004-TABLE-ERROR                VALUE +4004.               
           88  ABEND-4005-OPEN-ERROR                 VALUE +4005.               
           88  ABEND-4006-CLOSE-ERROR                VALUE +4006.               
           88  ABEND-4007-SEQUENCE-ERROR             VALUE +4007.               
           88  ABEND-4008-DATA-ERROR                 VALUE +4008.               
           88  ABEND-4009-KEY-DATA-ERROR             VALUE +4009.               
           88  ABEND-4013-SQL-ERROR                  VALUE +4013.               
           88  ABEND-4014-ORACLE-LOGON-ERROR         VALUE +4014.               
           88  ABEND-4019-DATE-ROUTINE-ERROR         VALUE +4019.               
           88  ABEND-4444-FORCED-ABEND               VALUE +4444.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORDS-READ              PIC S9(10)   VALUE ZERO             
                                                         COMP-3.                
           05  PA-RECORDS-WROTE             PIC S9(10)   VALUE ZERO             
                                                         COMP-3.                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)   VALUE                  
                                                         'PMKRP145'.            
           05  PC-MAX-LINE-CNT              PIC S9(04)   COMP                   
                                                         VALUE +80.             
           05  PC-SPACE                     PIC  X(01)   VALUE SPACES.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-CNT                  PIC  9(05)   VALUE ZERO.            
           05  PV-ADVANCE-CNT               PIC S9(04)   VALUE ZERO             
                                                         COMP.                  
           05  PV-LINE-CNT                  PIC S9(04)   VALUE +0               
                                                         COMP.                  
           05  PV-ADVANCE-LINES             PIC S9(04)   VALUE +1               
                                                         COMP.                  
           05  PV-PARAGRAPH-NAME            PIC  X(30)   VALUE SPACES.          
           05  PV-OPERATION-MSG-1           PIC  X(40)   VALUE SPACES.          
           05  PV-OPERATION-MSG-2           PIC  X(40)   VALUE SPACES.          
           05  PV-DETAL-NULL-LINE           PIC  X(200)  VALUE SPACES.          
           05  PV-CURRENT-DATE              PIC  X(08)   VALUE SPACES.          
           05  PV-PRIC-CHG-RET              PIC  Z,ZZZ,ZZ9.99.                  
           05  PV-CURR-RET                  PIC  Z,ZZZ,ZZ9.99.                  
                                                                                
           05  PV-CURRENT-DATE-FMT.                                             
               10  PV-CURRENT-MONTH         PIC  9(02)   VALUE ZERO.            
               10  PV-FILLER                PIC  X(01)   VALUE "/".             
               10  PV-CURRENT-DAY           PIC  9(02)   VALUE ZERO.            
               10  PV-FILLER                PIC  X(01)   VALUE "/".             
               10  PV-CURRENT-YYYY          PIC  9(04)   VALUE ZERO.            
                                                                                
           05  PV-CURRENT-TIME.                                                 
               10  PV-CURRENT-HOUR          PIC  9(02)   VALUE ZERO.            
               10  PV-CURRENT-MINUTE        PIC  9(02)   VALUE ZERO.            
               10  FILLER                   PIC  9(04)   VALUE ZERO.            
                                                                                
           05  PV-HOLD-RECORD.                                                  
               10  PV-HOLD-GMA-NBR          PIC  9(04)   VALUE ZERO.            
               10  PV-HOLD-GMM-FRST-NM      PIC  X(20)   VALUE SPACES.          
               10  PV-HOLD-GMM-LAST-NM      PIC  X(30)   VALUE SPACES.          
               10  PV-HOLD-DMA-NBR          PIC  9(04)   VALUE ZERO.            
               10  PV-HOLD-DMM-FRST-NM      PIC  X(20)   VALUE SPACES.          
               10  PV-HOLD-DMM-LAST-NM      PIC  X(30)   VALUE SPACES.          
               10  PV-HOLD-BYR-NBR          PIC  9(04)   VALUE ZERO.            
               10  PV-HOLD-BYR-FRST-NM      PIC  X(20)   VALUE SPACES.          
               10  PV-HOLD-BYR-LAST-NM      PIC  X(30)   VALUE SPACES.          
               10  PV-HOLD-DEPT-NBR         PIC  9(04)   VALUE ZERO.            
               10  PV-HOLD-DEPT-NM          PIC  X(20)   VALUE SPACES.          
               10  PV-HOLD-SUB-CL-NBR       PIC  9(04)   VALUE ZERO.            
               10  PV-HOLD-GP-CDE           PIC  X(03)   VALUE SPACES.          
               10  PV-HOLD-SKU-NBR          PIC  9(08)   VALUE ZERO.            
               10  PV-HOLD-SKU-DESC         PIC  X(30)   VALUE SPACES.          
               10  PV-HOLD-PRCH-TYP-CDE     PIC  X(03)   VALUE SPACES.          
               10  PV-HOLD-EFF-DTE          PIC  X(10)   VALUE SPACES.          
               10  PV-HOLD-NW-LNE-AMT       PIC  9(07)V99 VALUE 0.              
               10  PV-HOLD-CURR-LNE-AMT     PIC  9(07)V99 VALUE 0.              
               10  PV-HOLD-PROMO-ID         PIC  9(10)   VALUE ZERO.            
               10  PV-HOLD-PROMO-NM         PIC  X(40)   VALUE SPACES.          
               10  PV-HOLD-PROMO-ITM-DESC   PIC  X(31)   VALUE SPACES.          
               10  PV-HOLD-PROMO-PT-AMT     PIC  9(07)V99 VALUE 0.              
               10  PV-HOLD-TIER-PRIC-IND    PIC  X(01)   VALUE SPACES.          
                                                                                
      *****************************************************************         
      *    FILE VARIABLES FOR REPORT RUN DATES                        *         
      *****************************************************************         
       01  FV-REP-RUN-DT-REC.                                                   
           05  FV-HDR-FLG                PIC  9(04)  VALUE ZERO.                
           05  FILLER                    PIC  X(01)  VALUE SPACES.              
           05  FV-REP-RUN-STRT-DT        PIC  X(10).                            
           05  FILLER                    PIC  X(01)  VALUE SPACES.              
           05  FV-REP-RUN-END-DT         PIC  X(10).                            
           05  FILLER                    PIC  X(347) VALUE SPACES.              
                                                                                
       01  END-OF-REPORT-LINE.                                                  
           05  FILLER                       PIC  X(90)   VALUE SPACES.          
           05  FILLER                       PIC  X(20)   VALUE                  
               '** END OF REPORT ** '.                                          
           05  FILLER                       PIC  X(90)   VALUE SPACES.          
                                                                                
       01  DETAIL-NO-DATA.                                                      
           05  FILLER                       PIC  X(88)   VALUE SPACES.          
           05  FILLER                       PIC  X(24)   VALUE                  
               'NO DATA FOUND TO PROCESS'.                                      
           05  FILLER                       PIC  X(88)   VALUE SPACES.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-FIRST-INPUT-SW            PIC  X(01)   VALUE 'Y'.             
               88  PS-NOT-FIRST-INPUT                    VALUE 'N'.             
               88  PS-FIRST-INPUT                        VALUE 'Y'.             
           05  PS-FILE-CHK-SW               PIC  X(01)   VALUE 'N'.             
               88  NOT-EOF-FILE                          VALUE 'N'.             
               88  EOF-FILE                              VALUE 'Y'.             
           05  PS-SAME-SKU-SW               PIC  X(01)   VALUE 'Y'.             
               88  PS-NOT-SAME-SKU                       VALUE 'N'.             
               88  PS-SAME-SKU                           VALUE 'Y'.             
                                                                                
       01  OTHER-DATA-VARIABLES.                                                
           05  INPUT-FILE-PS                PIC  X(02)   VALUE SPACES.          
                                                                                
      *****************************************************************         
      *  COPYBOOKS                                                    *         
      *****************************************************************         
      ** INPUT COPYBOOK                                                         
           COPY PMRD169.                                                        
                                                                                
      ** STANDARD HEADER LINE - KOHLS 200 COL                                   
           COPY DPWS200O.                                                       
                                                                                
      ** OUTPUT REPORT COPYBOOK                                                 
           COPY PMRD170.                                                        
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      *        MAINLINE ROUTINE                                       *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-ROUTINE                                         
             UNTIL EOF-FILE.                                                    
                                                                                
           PERFORM B300-EXIT-RTN.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      *        INITIALIZE ROUTINE                                     *         
      *****************************************************************         
       B100-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT PROMO-EXCEPTION-RPT.                                     
                                                                                
           ACCEPT PV-CURRENT-DATE         FROM DATE YYYYMMDD.                   
           MOVE PV-CURRENT-DATE(3:2)        TO DP200O-RUN-YEAR                  
           MOVE PV-CURRENT-DATE(1:4)        TO PV-CURRENT-YYYY.                 
           MOVE PV-CURRENT-DATE(5:2)        TO DP200O-RUN-MONTH                 
                                               PV-CURRENT-MONTH.                
           MOVE PV-CURRENT-DATE(7:2)        TO DP200O-RUN-DAY                   
                                               PV-CURRENT-DAY.                  
           ACCEPT PV-CURRENT-TIME         FROM TIME.                            
           MOVE PV-CURRENT-HOUR             TO DP200O-RUN-HOUR.                 
           MOVE PV-CURRENT-MINUTE           TO DP200O-RUN-MINUTE.               
           MOVE PC-CURRENT-PROGRAM-NAME     TO DP200O-PROGRAM-NAME.             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' ** ' PC-CURRENT-PROGRAM-NAME ' **'.                        
                                                                                
           MOVE  0                          TO PV-PAGE-CNT.                     
           MOVE +85                         TO PV-LINE-CNT.                     
                                                                                
           PERFORM X100-READ-HEADER-RECORD.                                     
                                                                                
           PERFORM X150-READ-INPUT-RECORD.                                      
                                                                                
           INITIALIZE                          PV-HOLD-RECORD.                  
           MOVE PM169-PROMO-PRIC-CHGS       TO PV-HOLD-RECORD.                  
                                                                                
      *****************************************************************         
      * B200-PROCESS-ROUTINE                                          *         
      *****************************************************************         
       B200-PROCESS-ROUTINE.                                                    
                                                                                
           IF  PM169-GMA-NBR         = PV-HOLD-GMA-NBR                          
           AND PM169-DMA-NBR         = PV-HOLD-DMA-NBR                          
           AND PM169-BYR-NBR         = PV-HOLD-BYR-NBR                          
           AND PM169-DEPT-NBR        = PV-HOLD-DEPT-NBR                         
               IF  PM169-SKU-NBR     = PV-HOLD-SKU-NBR                          
                   SET PS-SAME-SKU          TO TRUE                             
               ELSE                                                             
                   SET PS-NOT-SAME-SKU      TO TRUE                             
               END-IF                                                           
           ELSE                                                                 
              MOVE +85                      TO PV-LINE-CNT                      
           END-IF.                                                              
                                                                                
           INITIALIZE                          PV-HOLD-RECORD.                  
           MOVE PM169-PROMO-PRIC-CHGS       TO PV-HOLD-RECORD.                  
           PERFORM X400-LOAD-READ-RECORD.                                       
           PERFORM H300-PRINT-DETAIL-LINE.                                      
           PERFORM X150-READ-INPUT-RECORD.                                      
                                                                                
      *****************************************************************         
      *    CLOSE FILES AND DISPLAY COUNTS                             *         
      *****************************************************************         
       B300-EXIT-RTN.                                                           
                                                                                
           PERFORM W450-WRITE-END-OF-RPT-LINE.                                  
                                                                                
           CLOSE INPUT-FILE                                                     
                 PROMO-EXCEPTION-RPT.                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' RECORDS READ ..... ' PA-RECORDS-READ.                      
           DISPLAY ' RECORDS WRITTEN .. ' PA-RECORDS-WROTE.                     
           DISPLAY ' '.                                                         
                                                                                
      *****************************************************************         
      *   PROCESS REPORT PAGE HEADERS                                 *         
      *****************************************************************         
       H100-PROCESS-PAGE-HEADER.                                                
                                                                                
           MOVE ZERO                   TO PV-LINE-CNT.                          
           ADD  1                      TO PV-PAGE-CNT.                          
           MOVE PV-PAGE-CNT            TO DP200O-PAGE-NUMBER.                   
                                                                                
           MOVE 1                      TO DP200O-REPORT-NUMBER.                 
           MOVE DP200O-STANDRD-HEADER-200-COLS                                  
                                       TO REPORT-LINE.                          
                                                                                
           WRITE REPORT-LINE         FROM DP200O-STANDRD-HEADER-200-COLS        
                                    AFTER ADVANCING PAGE.                       
                                                                                
           WRITE REPORT-LINE         FROM HEADER-LINE-1                         
                                    AFTER ADVANCING 2.                          
                                                                                
           WRITE REPORT-LINE         FROM HEADER-LINE-2                         
                                    AFTER ADVANCING 2.                          
                                                                                
           MOVE PV-CURRENT-DATE-FMT    TO HL3-DATE-VAL.                         
           WRITE REPORT-LINE         FROM HEADER-LINE-3                         
                                    AFTER ADVANCING 2.                          
                                                                                
           WRITE REPORT-LINE         FROM PV-DETAL-NULL-LINE                    
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM HEADER-LINE-4                         
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM HEADER-LINE-5                         
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM PV-DETAL-NULL-LINE                    
                                    AFTER ADVANCING 1.                          
                                                                                
           INITIALIZE GMM-LINE                                                  
           MOVE PV-HOLD-GMA-NBR        TO GMM-NBR                               
           STRING PV-HOLD-GMM-FRST-NM  DELIMITED BY SPACE                       
                  ' '                  DELIMITED BY SIZE                        
                  PV-HOLD-GMM-LAST-NM  DELIMITED BY SPACE                       
                                       INTO GMM-NAME.                           
           WRITE REPORT-LINE         FROM GMM-LINE                              
                                    AFTER ADVANCING 1.                          
                                                                                
           INITIALIZE DMM-LINE                                                  
           MOVE PV-HOLD-DMA-NBR        TO DMM-NBR                               
           STRING PV-HOLD-DMM-FRST-NM  DELIMITED BY SPACE                       
                  ' '                  DELIMITED BY SIZE                        
                  PV-HOLD-DMM-LAST-NM  DELIMITED BY SPACE                       
                                       INTO DMM-NAME.                           
           WRITE REPORT-LINE         FROM DMM-LINE                              
                                    AFTER ADVANCING 1.                          
                                                                                
           INITIALIZE BUYER-LINE                                                
           MOVE PV-HOLD-BYR-NBR        TO BYR-NBR                               
           STRING PV-HOLD-BYR-FRST-NM  DELIMITED BY SPACE                       
                  ' '                  DELIMITED BY SIZE                        
                  PV-HOLD-BYR-LAST-NM  DELIMITED BY SPACE                       
                                       INTO BYR-NAME.                           
           WRITE REPORT-LINE         FROM BUYER-LINE                            
                                    AFTER ADVANCING 1.                          
                                                                                
           INITIALIZE DEPT-LINE                                                 
           MOVE PV-HOLD-DEPT-NBR       TO DEPT-NBR                              
           MOVE PV-HOLD-DEPT-NM        TO DEPT-NAME                             
           WRITE REPORT-LINE         FROM DEPT-LINE                             
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM PV-DETAL-NULL-LINE                    
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM DETAIL-HEAD1                          
                                    AFTER ADVANCING 2.                          
                                                                                
           WRITE REPORT-LINE         FROM DETAIL-HEAD2                          
                                    AFTER ADVANCING 1.                          
                                                                                
           WRITE REPORT-LINE         FROM DETAIL-HEAD-DASH                      
                                    AFTER ADVANCING 1.                          
                                                                                
           MOVE SPACES                 TO PV-DETAL-NULL-LINE                    
           WRITE REPORT-LINE         FROM PV-DETAL-NULL-LINE                    
                                    AFTER ADVANCING 1.                          
                                                                                
           ADD +21                     TO PV-LINE-CNT.                          
                                                                                
      *****************************************************************         
      *     PRINT OUT THE DETAIL LINE                                 *         
      *****************************************************************         
       H300-PRINT-DETAIL-LINE.                                                  
                                                                                
           PERFORM W300-WRITE-DETAIL-LINE.                                      
                                                                                
           ADD 1                       TO PA-RECORDS-WROTE.                     
           MOVE SPACES                 TO DETAIL-LINE.                          
                                                                                
      *****************************************************************         
      *     WRITE DETAIL LINE                                         *         
      *****************************************************************         
       W300-WRITE-DETAIL-LINE.                                                  
                                                                                
           IF (PV-LINE-CNT + 3) > PC-MAX-LINE-CNT                               
               MOVE PV-HOLD-SUB-CL-NBR TO DTL-SCL-NBR                           
               MOVE PV-HOLD-GP-CDE     TO DTL-GRP-CDE                           
               MOVE PV-HOLD-SKU-NBR    TO DTL-SKU-NBR                           
               MOVE PV-HOLD-SKU-DESC   TO DTL-SKU-DESC                          
               MOVE PV-HOLD-PRCH-TYP-CDE                                        
                                       TO DTL-PRIC-CHG-TYP                      
               MOVE PV-HOLD-EFF-DTE    TO DTL-PRIC-CHG-EFF-DT                   
               MOVE PV-HOLD-NW-LNE-AMT TO PV-PRIC-CHG-RET                       
               MOVE PV-PRIC-CHG-RET    TO DTL-PRIC-CHG-RET                      
               MOVE PV-HOLD-CURR-LNE-AMT                                        
                                       TO PV-CURR-RET                           
               MOVE PV-CURR-RET        TO DTL-CURR-RET                          
               PERFORM H100-PROCESS-PAGE-HEADER                                 
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE         FROM DETAIL-LINE                           
                                    AFTER 1.                                    
                                                                                
           ADD +1                      TO PV-LINE-CNT.                          
                                                                                
      *****************************************************************         
      *     WRITE END OF REPORT LINE                                  *         
      *****************************************************************         
       W450-WRITE-END-OF-RPT-LINE.                                              
                                                                                
           IF  PV-LINE-CNT >= 75                                                
               PERFORM H100-PROCESS-PAGE-HEADER                                 
               MOVE 3                  TO PV-ADVANCE-LINES                      
           ELSE                                                                 
               SUBTRACT 5            FROM PC-MAX-LINE-CNT                       
                                   GIVING PV-ADVANCE-CNT                        
               SUBTRACT PV-LINE-CNT  FROM PV-ADVANCE-CNT                        
                                   GIVING PV-ADVANCE-CNT                        
               MOVE PV-ADVANCE-CNT     TO PV-ADVANCE-LINES                      
                                                                                
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE         FROM END-OF-REPORT-LINE                    
                                    AFTER ADVANCING PV-ADVANCE-LINES.           
                                                                                
      *****************************************************************         
       X100-READ-HEADER-RECORD.                                                 
      *****************************************************************         
                                                                                
           READ INPUT-FILE INTO FV-REP-RUN-DT-REC                               
           END-READ                                                             
                                                                                
           DISPLAY ' FILE STATUS . ' INPUT-FILE-PS                              
                                                                                
           EVALUATE INPUT-FILE-PS                                               
           WHEN '00'                                                            
                MOVE FV-REP-RUN-STRT-DT     TO HL4-BEG-DT                       
                MOVE FV-REP-RUN-END-DT      TO HL5-END-DT                       
           WHEN OTHER                                                           
                DISPLAY '** INVALID INPUT FILE **'                              
                MOVE 'PMKRP145'             TO PC-CURRENT-PROGRAM-NAME          
                MOVE 'X100-READ-HEADER-RECORD'                                  
                                            TO PV-PARAGRAPH-NAME                
                MOVE 'READ                       '                              
                                            TO PV-OPERATION-MSG-1               
                MOVE 'INVALID HEADER RECORD..... '                              
                                            TO PV-OPERATION-MSG-2               
                PERFORM Z100-ABEND                                              
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
       X150-READ-INPUT-RECORD.                                                  
      *****************************************************************         
                                                                                
           READ INPUT-FILE NEXT RECORD INTO PM169-PROMO-PRIC-CHGS               
              AT END                                                            
                 SET EOF-FILE          TO TRUE                                  
           END-READ.                                                            
                                                                                
           IF  EOF-FILE                                                         
               IF  PS-FIRST-INPUT                                               
                   DISPLAY '** NO INPUT RECORDS TO PROCESS **'                  
                   PERFORM X200-PROCESS-NO-RECORDS                              
               END-IF                                                           
           ELSE                                                                 
              SET PS-NOT-FIRST-INPUT   TO TRUE                                  
              ADD 1                    TO PA-RECORDS-READ                       
           END-IF.                                                              
      *****************************************************************         
       X200-PROCESS-NO-RECORDS.                                                 
      *****************************************************************         
                                                                                
           MOVE '0000'                 TO GMM-NBR.                              
           MOVE SPACES                 TO GMM-NAME                              
                                                                                
           MOVE '0000'                 TO DMM-NBR.                              
           MOVE SPACES                 TO DMM-NAME                              
                                                                                
           MOVE '0000'                 TO BYR-NBR.                              
           MOVE SPACES                 TO BYR-NAME                              
                                                                                
           MOVE '0000'                 TO DEPT-NBR                              
           MOVE SPACES                 TO DEPT-NAME.                            
           MOVE SPACES                 TO PV-DETAL-NULL-LINE                    
                                                                                
           PERFORM  H100-PROCESS-PAGE-HEADER.                                   
           WRITE REPORT-LINE         FROM DETAIL-NO-DATA                        
                                    AFTER ADVANCING 2.                          
                                                                                
           ADD   +3                   TO PV-LINE-CNT.                           
      *****************************************************************         
       X400-LOAD-READ-RECORD.                                                   
      *****************************************************************         
                                                                                
           MOVE PV-HOLD-PROMO-NM            TO DTL-PROMO-NM.                    
           MOVE PV-HOLD-PROMO-ITM-DESC      TO DTL-PROMO-ITM-DESC.              
           MOVE PV-HOLD-PROMO-PT-AMT        TO DTL-PROMO-PRIC-AMT.              
           MOVE PV-HOLD-TIER-PRIC-IND       TO DTL-TIER-PRIC-IND.               
                                                                                
           IF  PS-SAME-SKU                                                      
               INITIALIZE                      DTL-GRP-CDE                      
                                               DTL-SKU-DESC                     
                                               DTL-PRIC-CHG-TYP                 
                                               DTL-PRIC-CHG-EFF-DT              
                                               DTL-SCL-NBR                      
                                               DTL-SKU-NBR                      
                                               DTL-PRIC-CHG-RET                 
                                               DTL-CURR-RET                     
           ELSE                                                                 
               MOVE PV-HOLD-SUB-CL-NBR      TO DTL-SCL-NBR                      
               MOVE PV-HOLD-GP-CDE          TO DTL-GRP-CDE                      
               MOVE PV-HOLD-SKU-NBR         TO DTL-SKU-NBR                      
               MOVE PV-HOLD-SKU-DESC        TO DTL-SKU-DESC                     
               MOVE PV-HOLD-PRCH-TYP-CDE    TO DTL-PRIC-CHG-TYP                 
               MOVE PV-HOLD-EFF-DTE         TO DTL-PRIC-CHG-EFF-DT              
               MOVE PV-HOLD-NW-LNE-AMT      TO PV-PRIC-CHG-RET                  
               MOVE PV-PRIC-CHG-RET         TO DTL-PRIC-CHG-RET                 
               MOVE PV-HOLD-CURR-LNE-AMT    TO PV-CURR-RET                      
               MOVE PV-CURR-RET             TO DTL-CURR-RET                     
           END-IF.                                                              
                                                                                
      ************************************************************              
      * Z100-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      ************************************************************              
       Z100-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
