       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    IMKUT210.                                                 
       AUTHOR.        BOB MCASEY.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/16/2001.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * STYLE DELETE MATCH/MERGE                                                
      * THIS PROGRAM USES INP01 AS THE DRIVER AND ATTEMPTS TO MATCH THE         
      * SKU COUNTS BETWEEN INP01 AND INP02.  INP01 IS SORT-SUMMED BY            
      * STYLE AND CONTAINS A COUNTER OF SKUS WITHIN THE STYLE.  INP02 IS        
      * THE SAME.  INP02 CONTAINS ALL SKUS THAT PASSED THE EDITS IN THE         
      * PRIOR PROGRAMS WITHIN IMK025 (IMKUT200 & IMKUT205).  WHEN A             
      * MATCH IS FOUND ON THE STYLE IDS BETWEEN INP01 AND INP02, THE            
      * SKU COUNT IS CHECKED.  IF THE SKU COUNTS MATCH, FILE INP03 (THE         
      * DETAIL SKUS) IS READ TO FIND THE STYLE ID AND ALL RECORDS FROM          
      * THAT FILE ARE WRITTEN OUT TO THE VALID OUTPUT FILE.  THE ENT_ID         
      * IS REQUIRED ON THE OUTPUT.  THE FIRST RECORD FROM THE THAT FILE         
      * WILL BE USED TO OBTAIN THE ENT_ID AND ALL SUBSEQUENT RECORDS FOR        
      * THAT STYLE WILL USE THE SAME ENT_ID.  COUNTS ARE MAINTAINED FOR         
      * RECORDS READ, MATCHED, NOT MATCHED AND WRITTEN.                         
      ******************************************************************        
001700*  CHANGED:                                                      *        
001700*                                                                *        
      *    WR/PITS#     DATE        DESCRIPTION                        *        
      *    --------   ----------   ---------------------------------   *        
0702RM*    WR24363    12-09-2002    MADE CHANGES TO WORKING STOREAGE   *        
0702RM*                             VERSIONS OF IMRD020.               *        
0702RM*    WR29925    04-27-2006    FIXED DIFFERENCES OF ID AND SU     *        
      *                             AND IMRD020.                                
004900*----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STYLE-UNLD-FILE                                               
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT ITEM-DELETE-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT ITEM-DELETE-DETAIL-FILE                                       
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT VALID-STYLE-DELETE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
      ****************************************************************          
       DATA DIVISION.                                                           
      ****************************************************************          
       FILE SECTION.                                                            
                                                                                
       FD  STYLE-UNLD-FILE                                                      
           RECORDING MODE IS V.                                                 
       01  STYLE-UNLD-REC           PIC X(40).                                  
                                                                                
       FD  ITEM-DELETE-FILE                                                     
           RECORDING MODE IS F.                                                 
       01  ITEM-DELETE-REC          PIC X(100).                                 
                                                                                
       FD  ITEM-DELETE-DETAIL-FILE                                              
           RECORDING MODE IS F.                                                 
       01  ITEM-DELETE-DETAIL-REC   PIC X(100).                                 
                                                                                
       FD  VALID-STYLE-DELETE                                                   
           RECORDING MODE IS F.                                                 
       01  VALID-STYLE-REC          PIC X(100).                                 
                                                                                
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
      ******************************************************************        
      *  ABEND VARIABLE                                                         
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)  VALUE ZEROES                 
                                                    COMP SYNC.                  
                                                                                
      ****************************************************************          
      *  PROGRAM ACCUMULATORS                                                   
      ****************************************************************          
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-MATCH-KEY-CNT        PIC  9(08)  VALUE 0.                     
           05  PA-STYLE-UNLOAD-CNT     PIC  9(08)  VALUE 0.                     
           05  PA-ITEM-DELETE-CNT      PIC  9(08)  VALUE 0.                     
           05  PA-ITEM-DELETE-DTL-CNT  PIC  9(08)  VALUE 0.                     
           05  PA-VALID-ITEM-DEL-CNT   PIC  9(08)  VALUE 0.                     
           05  PA-STYLE-DELETE-CNT     PIC  9(08)  VALUE 0.                     
                                                                                
      ****************************************************************          
      *  PROGRAM CONSTANTS                                                      
      ****************************************************************          
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'IMKUT210'.            
                                                                                
      ******************************************************************        
      *  PROGRAM SWITCHES                                                       
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STYL-UNLD-SW  PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-STYL-UNLD             VALUE 'Y'.                   
           05  PS-END-OF-ITEM-DEL-SW   PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-ITEM-DEL              VALUE 'Y'.                   
           05  PS-END-OF-ITEM-DTL-SW   PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-ITEM-DTL              VALUE 'Y'.                   
                                                                                
      ******************************************************************        
      *  PROGRAM VARIABLES                                                      
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30)     VALUE SPACES.             
           05  PV-DB2-OPER-ATTEMPTED   PIC  X(30)     VALUE SPACES.             
           05  PV-CURR-DATE            PIC  X(10)     VALUE SPACES.             
                                                                                
      ****************************************************************          
      *  SAVE AREA                                                              
      ****************************************************************          
       01  SA-SAVE-AREA.                                                        
           05  SA-SUB-CL-MDSEAR-ID PIC S9(06)    COMP-3                         
                                                 VALUE ZERO.                    
           05  SA-VS-ID            PIC S9(09)    COMP                           
                                                 VALUE ZERO.                    
      ****************************************************************          
      *  STYLE UNLOAD DATA LAYOUT - ALL RECORDS                                 
      ****************************************************************          
       01  SU-STYLE-UNLD-DATA.                                                  
           05  SU-KEY.                                                          
               10  SU-SUB-CL-MDSEAR-ID PIC S9(06)    COMP-3                     
                                                     VALUE ZERO.                
               10  SU-VS-ID            PIC S9(09)    COMP                       
                                                     VALUE ZERO.                
           05  SU-INTR-UPC-ID          PIC S9(09)    COMP                       
                                                     VALUE ZERO.                
           05  SU-ITM-NBR              PIC S9(15)    COMP-3                     
                                                     VALUE ZERO.                
           05  SU-SKU                  PIC S9(08)V   COMP-3                     
                                                     VALUE ZERO.                
           05  SU-SKU-STAT             PIC X(02).                               
           05  SU-SKU-CNTR             PIC 9(08)     COMP                       
                                                     VALUE ZERO.                
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  SU-TWEBITM-IND          PIC X(01).                               
           05  SU-RMS-STYL-ID          PIC S9(08)    COMP-3                     
                                                     VALUE ZERO.                
           05  SU-ENT-ID               PIC S9(9)     COMP                       
                                                     VALUE ZERO.                
           05  SU-LAST-SALES-DTE       PIC X(08)     VALUE SPACES.              
           05  SU-LAST-RECEIPT-DTE     PIC X(08)     VALUE SPACES.              
           05  SU-VS-NBR               PIC X(20)     VALUE SPACES.              
1202RM     05  FILLER                  PIC X(21)     VALUE SPACES.              
                                                                                
      ****************************************************************          
      *  ITEM DELETE DATA LAYOUT - RECORDS ELIGIBLE FOR DELETION                
      ****************************************************************          
       01  ID-ITEM-DELETE-DATA.                                                 
           05  ID-KEY.                                                          
               10 ID-SUB-CL-MDSEAR-ID  PIC S9(06)    COMP-3                     
                                                     VALUE ZERO.                
               10 ID-VS-ID             PIC S9(09)    COMP                       
                                                     VALUE ZERO.                
           05  ID-INTR-UPC-ID          PIC S9(09)    COMP                       
                                                     VALUE ZERO.                
           05  ID-ITM-NBR              PIC S9(15)    COMP-3                     
                                                     VALUE ZERO.                
1202RM     05  ID-SKU                  PIC S9(08)V   COMP-3                     
1202RM                                               VALUE ZERO.                
           05  ID-SKU-STAT             PIC X(02).                               
           05  ID-SKU-CNTR             PIC 9(08)     COMP                       
                                                     VALUE ZERO.                
           05  FILLER                  PIC X(02)     VALUE SPACES.              
           05  ID-TWEBITM-IND          PIC X(01).                               
           05  ID-RMS-STYL-ID          PIC S9(08)    COMP-3                     
                                                     VALUE ZERO.                
           05  ID-ENT-ID               PIC S9(9)     COMP                       
                                                     VALUE ZERO.                
           05  ID-LAST-SALES-DTE       PIC X(08)     VALUE SPACES.              
           05  ID-LAST-RECEIPT-DTE     PIC X(08)     VALUE SPACES.              
           05  ID-VS-NBR               PIC X(20)     VALUE SPACES.              
1202RM     05  FILLER                  PIC X(21)     VALUE SPACES.              
                                                                                
      ****************************************************************          
      *  STYLE DELETE COPYBOOK                                                  
      ****************************************************************          
           COPY IMRD020.                                                        
                                                                                
      *****************************************************************         
      * TVNDSTL DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
      *                                                                         
00541      EXEC SQL                                                             
00542           INCLUDE SQLCA                                                   
00543      END-EXEC.                                                            
00545                                                                           
00203 ******************************************************************        
00204 *  DB2 ERROR MESSAGES FORMAT AREA.                                        
00205 ******************************************************************        
00206      COPY DPWS004.                                                        
00207                                                                           
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-IMKUT210.                                                           
      ******************************************************************        
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 3000-MAIN-PROCESS                                            
                   UNTIL  PS-END-OF-STYL-UNLD                                   
                     AND  PS-END-OF-ITEM-DEL.                                   
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       1000-INITIALIZE.                                                         
      ******************************************************************        
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '** PROGRAM :       ' PC-PROGRAM-NAME                        
           DISPLAY '************************************************'.          
                                                                                
           OPEN INPUT  STYLE-UNLD-FILE                                          
                       ITEM-DELETE-FILE                                         
                       ITEM-DELETE-DETAIL-FILE                                  
                OUTPUT VALID-STYLE-DELETE.                                      
                                                                                
           PERFORM 6000-READ-STYLE-UNLOAD-FILE.                                 
           PERFORM 6100-READ-ITEM-DELETE-FILE.                                  
           PERFORM 6200-READ-ITEM-DELETE-DETAIL.                                
      *****************************************************************         
       3000-MAIN-PROCESS.                                                       
      *****************************************************************         
                                                                                
           MOVE '*3000-MAIN-PROCESS.          ' TO PV-PARAGRAPH-NAME.           
                                                                                
           IF SU-KEY > ID-KEY                                                   
**TEMP*        DISPLAY ' SU-KEY > ID-KEY '                                      
**TEMP*        SU-SUB-CL-MDSEAR-ID ' ' ID-SUB-CL-MDSEAR-ID                      
**TEMP*        ' ' SU-VS-ID ' ' ID-VS-ID                                        
               PERFORM 6100-READ-ITEM-DELETE-FILE                               
           ELSE                                                                 
               IF SU-KEY <  ID-KEY                                              
**TEMP*            DISPLAY ' SU-KEY < ID-KEY '                                  
**TEMP*            SU-SUB-CL-MDSEAR-ID ' ' ID-SUB-CL-MDSEAR-ID                  
**TEMP*            ' ' SU-VS-ID ' ' ID-VS-ID                                    
                   PERFORM 6000-READ-STYLE-UNLOAD-FILE                          
               ELSE                                                             
                   IF SU-KEY = ID-KEY                                           
                       ADD 1 TO PA-MATCH-KEY-CNT                                
**TEMP*                DISPLAY ' MATCHING KEYS: SUMMED & ITEM DEL '             
**TEMP*                SU-SUB-CL-MDSEAR-ID ' ' SU-VS-ID ' '                     
                       IF SU-SKU-CNTR = ID-SKU-CNTR                             
**TEMP*                    DISPLAY ' MATCHING SKU COUNTS '                      
**TEMP*                    SU-SKU-CNTR ' ' ID-SKU-CNTR                          
                           PERFORM 3200-PROCESS-STYLE-DELETE                    
**TEMP*                ELSE                                                     
**TEMP*                    DISPLAY 'SKU COUNTS DONT MATCH: '                    
**TEMP*                    SU-SKU-CNTR ' ' ID-SKU-CNTR                          
                       END-IF                                                   
                       PERFORM 6000-READ-STYLE-UNLOAD-FILE                      
                       PERFORM 6100-READ-ITEM-DELETE-FILE                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************MWKUT028
       3200-PROCESS-STYLE-DELETE.                                               
      ******************************************************************MWKUT028
           MOVE '*3200-PROCESS-STYLE-DELETE. *' TO PV-PARAGRAPH-NAME.           
                                                                                
           IF ((IM020-SUB-CL-MDSEAR-ID = ID-SUB-CL-MDSEAR-ID)                   
               AND                                                              
               (IM020-VS-ID            = ID-VS-ID))                             
              CONTINUE                                                          
           ELSE                                                                 
               PERFORM 6200-READ-ITEM-DELETE-DETAIL                             
                   UNTIL                                                        
                ((IM020-SUB-CL-MDSEAR-ID = ID-SUB-CL-MDSEAR-ID)                 
                 AND                                                            
                 (IM020-VS-ID            = ID-VS-ID))                           
                 OR                                                             
                (IM020-SUB-CL-MDSEAR-ID > ID-SUB-CL-MDSEAR-ID)                  
                 OR PS-END-OF-ITEM-DTL                                          
           END-IF.                                                              
                                                                                
           IF (IM020-SUB-CL-MDSEAR-ID > ID-SUB-CL-MDSEAR-ID)                    
                OR                                                              
             ((IM020-SUB-CL-MDSEAR-ID = ID-SUB-CL-MDSEAR-ID)                    
                AND                                                             
              (IM020-VS-ID            > ID-VS-ID))                              
                OR PS-END-OF-ITEM-DTL                                           
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 3400-WRITE-N-READ                                        
                  UNTIL                                                         
                    ((IM020-SUB-CL-MDSEAR-ID NOT = ID-SUB-CL-MDSEAR-ID)         
                     OR                                                         
                     (IM020-VS-ID            NOT = ID-VS-ID)                    
                     OR PS-END-OF-ITEM-DTL)                                     
           END-IF.                                                              
                                                                                
       3400-WRITE-N-READ.                                                       
                                                                                
           MOVE '*3400-WRITE-N-READ.         *' TO PV-PARAGRAPH-NAME.           
                                                                                
           IF (IM020-SUB-CL-MDSEAR-ID NOT = SA-SUB-CL-MDSEAR-ID)                
               OR                                                               
              (IM020-VS-ID            NOT = SA-VS-ID)                           
               MOVE IM020-SUB-CL-MDSEAR-ID TO SA-SUB-CL-MDSEAR-ID               
               MOVE IM020-VS-ID            TO SA-VS-ID                          
               PERFORM 4000-SELECT-ENT-ID                                       
               ADD 1 TO PA-STYLE-DELETE-CNT                                     
           END-IF.                                                              
           PERFORM 8000-WRITE-VALID-DELETES.                                    
           PERFORM 6200-READ-ITEM-DELETE-DETAIL.                                
                                                                                
       4000-SELECT-ENT-ID.                                                      
                                                                                
           MOVE '*4000-SELECT-ENT-ID.        *' TO PV-PARAGRAPH-NAME.           
                                                                                
           EXEC SQL                                                             
              SELECT  ENT_ID                                                    
                   ,  VS_NBR                                                    
                INTO :VNDSTL-ENT-ID                                             
                  ,  :VNDSTL-VS-NBR                                             
                FROM TVNDSTL                                                    
               WHERE VS_ID  = :IM020-VS-ID                                      
                 AND ((EFF_BEG_DTE <= CURRENT DATE)                             
                 AND  (EFF_END_DTE >= CURRENT DATE                              
                    OR EFF_END_DTE IS NULL))                                    
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                 CONTINUE                                                       
               WHEN SQLCODE = +100                                              
                 DISPLAY ' ENT ID NOT FOUND : '                                 
                       IM020-VS-ID                                              
               WHEN OTHER                                                       
                 PERFORM 9980-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************MWKUT028
       6000-READ-STYLE-UNLOAD-FILE.                                             
      ******************************************************************MWKUT028
                                                                                
           MOVE '*6000-READ-STYLE-UNLOAD-FILE.' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ STYLE-UNLD-FILE                                                 
               INTO SU-STYLE-UNLD-DATA                                          
               AT END                                                           
                   SET PS-END-OF-STYL-UNLD TO TRUE                              
                   MOVE HIGH-VALUES        TO SU-KEY                            
                   DISPLAY '#*#*# STYLE UNLD KEY IS HIGH VALUES'                
           END-READ.                                                            
                                                                                
           IF NOT PS-END-OF-STYL-UNLD                                           
              ADD 1 TO PA-STYLE-UNLOAD-CNT                                      
**TEMP*        DISPLAY '    6000-READ-STYLE ' SU-SUB-CL-MDSEAR-ID ' '           
**TEMP*           SU-VS-ID                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************MWKUT028
       6100-READ-ITEM-DELETE-FILE.                                              
      ******************************************************************MWKUT028
                                                                                
           MOVE '*6100-READ-ITEM-DELETE-FILE. ' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ ITEM-DELETE-FILE                                                
               INTO ID-ITEM-DELETE-DATA                                         
               AT END                                                           
                   SET PS-END-OF-ITEM-DEL TO TRUE                               
                   MOVE HIGH-VALUES       TO ID-KEY                             
                   DISPLAY '#*#*# ITEM DELETE KEY IS HIGH VALUES '              
           END-READ.                                                            
                                                                                
           IF NOT PS-END-OF-ITEM-DEL                                            
              ADD 1 TO PA-ITEM-DELETE-CNT                                       
**TEMP*        DISPLAY '    6100-READ-ITEM ' ID-SUB-CL-MDSEAR-ID ' '            
**TEMP*           ID-VS-ID                                                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************MWKUT028
       6200-READ-ITEM-DELETE-DETAIL.                                            
      ******************************************************************MWKUT028
                                                                                
           MOVE '*6200-READ-ITEM-DELETE-DETAIL' TO PV-PARAGRAPH-NAME.           
                                                                                
           READ ITEM-DELETE-DETAIL-FILE                                         
               INTO IM020-STYLE-DELETE-REC                                      
               AT END                                                           
                   SET PS-END-OF-ITEM-DTL TO TRUE                               
                   DISPLAY '#*#*# ID KEY IS HIGH VALUES #(#&*#&'                
           END-READ.                                                            
                                                                                
           IF NOT PS-END-OF-ITEM-DTL                                            
              ADD 1 TO PA-ITEM-DELETE-DTL-CNT                                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
       8000-WRITE-VALID-DELETES.                                                
      *****************************************************************         
                                                                                
           MOVE '*8000-WRITE-VALID-DELETES.   ' TO PV-PARAGRAPH-NAME.           
                                                                                
**TEMP*    IF SU-SKU-CNTR > 1                                                   
**TEMP*        DISPLAY '    WRITTEN OUT ' IM020-SUB-CL-MDSEAR-ID ' '            
**TEMP*           IM020-VS-ID                                                   
**TEMP*    END-IF.                                                              
                                                                                
           MOVE VNDSTL-ENT-ID        TO IM020-ENT-ID.                           
           MOVE VNDSTL-VS-NBR        TO IM020-VS-NBR.                           
           WRITE VALID-STYLE-REC   FROM IM020-STYLE-DELETE-REC.                 
           ADD 1 TO PA-VALID-ITEM-DEL-CNT.                                      
                                                                                
      *****************************************************************         
       9000-END-OF-JOB.                                                         
      *****************************************************************         
                                                                                
           MOVE '*9000-END-OF-JOB.            ' TO PV-PARAGRAPH-NAME.           
                                                                                
           CLOSE STYLE-UNLD-FILE                                                
                 ITEM-DELETE-FILE                                               
                 ITEM-DELETE-DETAIL-FILE                                        
                 VALID-STYLE-DELETE.                                            
                                                                                
           DISPLAY '************************************************'.          
           DISPLAY '******* IMKUT210 PROGRAM SUMMARY ******* '.                 
           DISPLAY 'STYLE UNLOAD RECORDS READ          : '                      
                                 PA-STYLE-UNLOAD-CNT.                           
           DISPLAY 'ITEM DELETE RECORDS READ           : '                      
                                 PA-ITEM-DELETE-CNT.                            
           DISPLAY 'ITEM DELETE DETAIL RECORDS READ    : '                      
                                 PA-ITEM-DELETE-DTL-CNT.                        
           DISPLAY 'ITEM DELETE DETAIL MATCHED KEYS    : '                      
                                 PA-MATCH-KEY-CNT      .                        
           DISPLAY 'ITEM DELETE DETAIL RECORDS WRITTEN : '                      
                                 PA-VALID-ITEM-DEL-CNT.                         
           DISPLAY 'NUMBER OF STYLES TO BE DELETED     : '                      
                                 PA-STYLE-DELETE-CNT.                           
           DISPLAY '******* END OF IMKUT210 **************** '.                 
                                                                                
      *                                                                         
       9980-SQL-ERROR.                                                          
      ******************************************************************        
      * ABEND ROUTINE FOR BAD SQL CALLS                                *        
      ******************************************************************        
           DISPLAY '** ABEND **          = SQL ERROR'.                          
           DISPLAY '** PROGRAM **        = IMKUT200'.                           
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.                 
      *                                                                         
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
             EXEC SQL                                                           
                COMMIT                                                          
             END-EXEC.                                                          
      *                                                                         
           MOVE +4013                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      *****************  END  OF  IMKUT210  ****************************        
      ******************************************************************        
