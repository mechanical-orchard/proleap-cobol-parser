/* REXX */                                                                      
/* RMKRX015 - Prepare Infopac copy add and delete transactions.       */        
/*      Match/merge candidate copy requirements from xref with        */        
/*      extracts from the Infopac distribution file copy records.     */        
/*      Create copy add records and report detail lines when a new    */        
/*      copy requirement (based on the report id, hierarchy code,     */        
/*      distribution key and recipient id) is found on the xref       */        
/*      file.  Create copy delete records and report detail lines     */        
/*      when an existing copy definition is no longer found on the    */        
/*      xref file.  On matching keys, and change in recipient should  */        
/*      create a delete of the existing copy requirement and an add   */        
/*      of the new copy requirement.  With the recipient now in the   */        
/*      compound key, this should be an obsolete requirement.  Force  */        
/*      the copy requirements on all adds to be zero to force an opt  */        
/*      in for physical copies, but display the requirement as one    */        
/*      copy for reporting requirements.  Read through the xref file  */        
/*      when getting the next copy requirement to detect any          */        
/*      duplicates with that key, and echo duplicate detail to sysout.*/        
/**********************************************************************/        
ARG IPACRGN                                                                     
INFOPAC_REGION = STRIP(IPACRGN)                                                 
UPPER INFOPAC_REGION                                                            
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX015 - '                                                        
CURRENT_PROGRAM_NAME = 'RMKRX015'                                               
XREF_FILE_END_OF_FILE = 'Y'                                                     
XREF_FILE_NOT_END_OF_FILE = 'N'                                                 
COPY_FILE_END_OF_FILE = 'Y'                                                     
COPY_FILE_NOT_END_OF_FILE = 'N'                                                 
EOF_REPORT_ID = '9999999999'                                                    
EOF_HIERARCHY_CODE = '999999999999999999999999999999'                           
EOF_DISTRIB_KEY = '99999999999'                                                 
EOF_RECIPIENT_ID = '9999999999'                                                 
ADD_TRANSACTION = 'A'                                                           
DELETE_TRANSACTION = 'D'                                                        
TRAN_COPY_HEADER = '##COPY        '                                             
TRAN_COPY_CONTINUATION = '              '                                       
CC_NEW_PAGE = '1'                                                               
CC_NEXT_LINE = ' '                                                              
CC_DOUBLE_SPACE = '0'                                                           
REPORT_NUMBER = '1'                                                             
COMPLETE_REPORT_COPY_TYPE = '3'                                                 
SELECTIVE_DIST_COPY_TYPE = '4'                                                  
DEFAULT_STORE_USER_DISTRIBUTION = 'MBS(STORE__'                                 
PAGE_SIZE = 60                                                                  
KOHLS_BANNER = 'K O H L S   D E P A R T M E N T   S T O R E S'                  
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE                                       
COPY_FILE_EOF = COPY_FILE_NOT_END_OF_FILE                                       
XREF_KEY = LEFT(' ',61)                                                         
COPY_KEY = LEFT(' ',61)                                                         
LAST_REPORT_ID = LEFT(' ',10)                                                   
REPORT_PAGE_NUMBER = 0                                                          
REPORT_CC = CC_NEW_PAGE                                                         
REPORT_LINE_COUNT = PAGE_SIZE                                                   
CURRENT_DATE_STANDARD = DATE('S')                                               
CURRENT_DATE = SUBSTR(CURRENT_DATE_STANDARD,5,2) || '/' ||,                     
          SUBSTR(CURRENT_DATE_STANDARD,7,2) || '/' ||,                          
          SUBSTR(CURRENT_DATE_STANDARD,1,4)                                     
CURRENT_TIME = TIME()                                                           
TITLE_LINE = '  ' || CURRENT_DATE || '  ' ||,                                   
          CURRENT_TIME ||  '                      ' ||,                         
          KOHLS_BANNER || '                     ' ||,                           
          CURRENT_PROGRAM_NAME || '-'                                           
REGION_HEADER = ,                                                               
          LEFT(' ',45) ||,                                                      
          'INFOPAC REPORT DISTRIBUTION SYSTEM - ' || INFOPAC_REGION             
REPORT_HEADER = ,                                                               
          LEFT(' ',55) || 'MBS UPDATE TRANSACTIONS'                             
REPORT_COLUMN_HEADER = ,                                                        
          LEFT('REPORT #',11) ||,                                               
          LEFT('ADD/DEL',9) ||,                                                 
          LEFT('COPY TYPE',11) ||,                                              
          LEFT('HIERARCHY CODE',30) ||,                                         
          LEFT('RPT GROUP',11) ||,                                              
          LEFT('RECIPIENT',11) ||,                                              
          LEFT('COPIES',8) ||,                                                  
          LEFT('COMMENT',20)                                                    
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM 'INFOPAC REGION =' INFOPAC_REGION                                   
"EXECIO * DISKR INP03  (STEM RPTPRT. FINIS)"                                    
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP03 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
REPORT_PRINT_LIST = ' '                                                         
DO I=1 TO RPTPRT.0                                                              
  STORE_REPORT_PRINT_LIST = STORE_REPORT_PRINT_LIST LEFT(RPTPRT.I,8)            
END                                                                             
CALL READ_XREF_RECORD                                                           
CALL READ_COPY_RECORD                                                           
DO WHILE ((XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE) |,                        
        (COPY_FILE_EOF = COPY_FILE_NOT_END_OF_FILE))                            
    SELECT                                                                      
        WHEN (XREF_KEY < COPY_KEY) THEN DO                                      
            TRANSACTION_TYPE = ADD_TRANSACTION                                  
            TRAN_REPORT_GROUP = XREF_REPORT_GROUP                               
            TRAN_RECIPIENT_ID = XREF_RECIPIENT_ID                               
            IF (STRIP(XREF_HIERARCHY_CODE) = '') THEN                           
                TRAN_COPY_TYPE = COMPLETE_REPORT_COPY_TYPE                      
            ELSE                                                                
                TRAN_COPY_TYPE = SELECTIVE_DIST_COPY_TYPE                       
            TRAN_REPORT_ID = XREF_REPORT_ID                                     
            TRAN_HIERARCHY_CODE = XREF_HIERARCHY_CODE                           
            TRAN_COPY_COMMENTS = XREF_REPORT_NUMBER || ' MBS(' ||,              
                      XREF_DISTRIB_KEY || ')'                                   
            TRAN_COPY_COMMENTS = LEFT(TRAN_COPY_COMMENTS,60)                    
/*                                                                    */        
/*  Force the copy requirements on all adds to be zero to force an    */        
/*  opt in for physical copies                                        */        
/*                                                                    */        
            TRAN_NUMBER_OF_COPIES = '00'                                        
            IF WORDPOS(STRIP(TRAN_REPORT_ID),STORE_REPORT_PRINT_LIST),          
                                                           > 0  THEN DO         
                TRAN_DISTRIBUTION_COMMENTS =,                                   
                          SUBSTR(TRAN_COPY_COMMENTS,12,11)                      
                IF (TRAN_DISTRIBUTION_COMMENTS =,                               
                          DEFAULT_STORE_USER_DISTRIBUTION) THEN DO              
                    TRAN_NUMBER_OF_COPIES = '01'                                
                END                                                             
            END                                                                 
            REPORT_LINE_DETAIL = ,                                              
                      LEFT(' ',4) || TRANSACTION_TYPE ||,                       
                      LEFT(' ',9) || TRAN_COPY_TYPE ||,                         
                      LEFT(' ',6) || TRAN_HIERARCHY_CODE ||,                    
                      LEFT(' ',2) || TRAN_REPORT_GROUP ||,                      
                      LEFT(' ',5) || TRAN_RECIPIENT_ID ||,                      
                      LEFT(' ',3) || TRAN_NUMBER_OF_COPIES ||,                  
                      LEFT(' ',4) || TRAN_COPY_COMMENTS                         
            CALL CHECK_REPORT_LINE                                              
            CALL CREATE_COPY_ADD_TRANS                                          
            CALL GET_NEXT_XREF_COPY                                             
        END                                                                     
        WHEN (XREF_KEY > COPY_KEY) THEN DO                                      
            TRANSACTION_TYPE = DELETE_TRANSACTION                               
            TRAN_REPORT_GROUP = COPY_REPORT_GROUP                               
            TRAN_RECIPIENT_ID = COPY_RECIPIENT_ID                               
            IF (STRIP(COPY_HIERARCHY_CODE) = '') THEN                           
                TRAN_COPY_TYPE = COMPLETE_REPORT_COPY_TYPE                      
            ELSE                                                                
                TRAN_COPY_TYPE = SELECTIVE_DIST_COPY_TYPE                       
            TRAN_REPORT_ID = COPY_REPORT_ID                                     
            TRAN_HIERARCHY_CODE = COPY_HIERARCHY_CODE                           
            TRAN_COPY_COMMENTS = LEFT(' ',60)                                   
            TRAN_NUMBER_OF_COPIES = '  '                                        
            REPORT_LINE_DETAIL = ,                                              
                      LEFT(' ',4) || TRANSACTION_TYPE ||,                       
                      LEFT(' ',9) || TRAN_COPY_TYPE ||,                         
                      LEFT(' ',6) || TRAN_HIERARCHY_CODE ||,                    
                      LEFT(' ',2) || TRAN_REPORT_GROUP ||,                      
                      LEFT(' ',5) || TRAN_RECIPIENT_ID ||,                      
            CALL CHECK_REPORT_LINE                                              
            CALL CREATE_COPY_DEL_TRANS                                          
            CALL READ_COPY_RECORD                                               
        END                                                                     
        OTHERWISE DO                                                            
            IF ((STRIP(COPY_REPORT_ID) ^= ' ') &,                               
                    (STRIP(XREF_RECIPIENT_ID) ^= ' ') &,                        
                    (XREF_RECIPIENT_ID ^= COPY_RECIPIENT_ID)) THEN DO           
                TRANSACTION_TYPE = DELETE_TRANSACTION                           
                TRAN_REPORT_GROUP = COPY_REPORT_GROUP                           
                TRAN_RECIPIENT_ID = COPY_RECIPIENT_ID                           
                TRAN_COPY_TYPE = COPY_COPY_TYPE                                 
                TRAN_REPORT_ID = COPY_REPORT_ID                                 
                TRAN_HIERARCHY_CODE = COPY_HIERARCHY_CODE                       
                TRAN_COPY_COMMENTS = LEFT(' ',60)                               
                TRAN_NUMBER_OF_COPIES = COPY_NUMBER_OF_COPIES                   
                REPORT_LINE_DETAIL = ,                                          
                          LEFT(' ',4) || TRANSACTION_TYPE ||,                   
                          LEFT(' ',9) || TRAN_COPY_TYPE ||,                     
                          LEFT(' ',6) || TRAN_HIERARCHY_CODE ||,                
                          LEFT(' ',2) || TRAN_REPORT_GROUP ||,                  
                          LEFT(' ',5) || TRAN_RECIPIENT_ID ||,                  
                CALL CHECK_REPORT_LINE                                          
                CALL CREATE_COPY_DEL_TRANS                                      
                                                                                
                TRANSACTION_TYPE = ADD_TRANSACTION                              
                TRAN_RECIPIENT_ID = XREF_RECIPIENT_ID                           
                IF (STRIP(COPY_HIERARCHY_CODE) = '') THEN                       
                    TRAN_COPY_TYPE = COMPLETE_REPORT_COPY_TYPE                  
                ELSE                                                            
                    TRAN_COPY_TYPE = SELECTIVE_DIST_COPY_TYPE                   
                TRAN_COPY_COMMENTS = XREF_REPORT_NUMBER || ' MBS(' ||,          
                          XREF_DISTRIB_KEY || ')'                               
                TRAN_COPY_COMMENTS = LEFT(TRAN_COPY_COMMENTS,60)                
/*                                                                    */        
/*  Force the copy requirements on all adds to be zero to force an    */        
/*  opt in for physical copies                                        */        
/*                                                                    */        
                TRAN_NUMBER_OF_COPIES = '00'                                    
                IF WORDPOS(STRIP(TRAN_REPORT_ID),,                              
                                  STORE_REPORT_PRINT_LIST) > 0  THEN DO         
                    TRAN_DISTRIBUTION_COMMENTS =,                               
                              SUBSTR(TRAN_COPY_COMMENTS,12,11)                  
                    IF (TRAN_DISTRIBUTION_COMMENTS =,                           
                              DEFAULT_STORE_USER_DISTRIBUTION) THEN DO          
                        TRAN_NUMBER_OF_COPIES = '01'                            
                    END                                                         
                END                                                             
                REPORT_LINE_DETAIL = ,                                          
                          LEFT(' ',4) || TRANSACTION_TYPE ||,                   
                          LEFT(' ',9) || TRAN_COPY_TYPE ||,                     
                          LEFT(' ',6) || TRAN_HIERARCHY_CODE ||,                
                          LEFT(' ',2) || TRAN_REPORT_GROUP ||,                  
                          LEFT(' ',5) || TRAN_RECIPIENT_ID ||,                  
                          LEFT(' ',3) || TRAN_NUMBER_OF_COPIES ||,              
                          LEFT(' ',4) || TRAN_COPY_COMMENTS                     
                CALL CHECK_REPORT_LINE                                          
                CALL CREATE_COPY_ADD_TRANS                                      
            END                                                                 
            CALL GET_NEXT_XREF_COPY                                             
            CALL READ_COPY_RECORD                                               
        END                                                                     
    END                                                                         
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
"EXECIO 0 DISKR OUT02 (FINIS"                                                   
"EXECIO 0 DISKR RPT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* GET_NEXT_XREF_COPY                                                */         
/*                                                                   */         
/* Get the next copy by full key on the XREF file.  Display any      */         
/* duplicates detected.                                              */         
/* ----------------------------------------------------------------- */         
GET_NEXT_XREF_COPY:                                                             
    CURRENT_XREF_KEY = XREF_KEY                                                 
    CALL READ_XREF_RECORD                                                       
    DO WHILE ((XREF_FILE_EOF = XREF_FILE_NOT_END_OF_FILE) &,                    
            (XREF_KEY = CURRENT_XREF_KEY))                                      
        SAY CURRPGM 'DUP XREF COPY - ' ||,                                      
                  'RPT ID:' || XREF_REPORT_ID ||,                               
                  'HIER: ' || XREF_HIERARCHY_CODE ||,                           
                  'KEY: ' || XREF_DISTRIB_KEY ||,                               
                  'RECIP: ' || XREF_RECIPIENT_ID ||,                            
                  'RPT NBR: ' || XREF_REPORT_NUMBER                             
        CALL READ_XREF_RECORD                                                   
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_XREF_RECORD                                                  */         
/*                                                                   */         
/* Read a copy extract record built from the Summary XREF file.      */         
/* ----------------------------------------------------------------- */         
READ_XREF_RECORD:                                                               
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        XREF_FILE_EOF = XREF_FILE_END_OF_FILE                                   
        XREF_REPORT_ID = EOF_REPORT_ID                                          
        XREF_HIERARCHY_CODE = EOF_HIERARCHY_CODE                                
        XREF_DISTRIB_KEY = EOF_DISTRIB_KEY                                      
        XREF_RECIPIENT_ID = EOF_RECIPIENT_ID                                    
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP01_RECORD                                                 
        XREF_REPORT_ID = SUBSTR(INP01_RECORD,3,10)                              
        XREF_REPORT_NUMBER = SUBSTR(INP01_RECORD,15,10)                         
        XREF_DISTRIB_KEY = SUBSTR(INP01_RECORD,30,11)                           
        XREF_REPORT_GROUP = SUBSTR(INP01_RECORD,45,4)                           
        XREF_HIERARCHY_CODE = SUBSTR(INP01_RECORD,50,30)                        
        XREF_RECIPIENT_ID = SUBSTR(INP01_RECORD,80,10)                          
    END                                                                         
    XREF_KEY = XREF_REPORT_ID || XREF_HIERARCHY_CODE ||,                        
              XREF_DISTRIB_KEY || XREF_RECIPIENT_ID                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_COPY_RECORD                                                  */         
/*                                                                   */         
/* Read an Infopac copy extract record.                              */         
/* ----------------------------------------------------------------- */         
READ_COPY_RECORD:                                                               
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        COPY_FILE_EOF = COPY_FILE_END_OF_FILE                                   
        COPY_REPORT_ID = EOF_REPORT_ID                                          
        COPY_HIERARCHY_CODE = EOF_HIERARCHY_CODE                                
        COPY_DISTRIB_KEY = EOF_DISTRIB_KEY                                      
        COPY_RECIPIENT_ID = EOF_RECIPIENT_ID                                    
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP02_RECORD                                                 
        COPY_COPY_TYPE = SUBSTR(INP02_RECORD,1,1)                               
        COPY_REPORT_ID = SUBSTR(INP02_RECORD,2,10)                              
        COPY_HIERARCHY_CODE = SUBSTR(INP02_RECORD,12,30)                        
        COPY_REPORT_GROUP = SUBSTR(INP02_RECORD,42,4)                           
        COPY_RECIPIENT_ID = SUBSTR(INP02_RECORD,46,10)                          
        COPY_NUMBER_OF_COPIES = SUBSTR(INP02_RECORD,56,2)                       
        COPY_COPY_COMMENTS = SUBSTR(INP02_RECORD,58,60)                         
        COPY_DISTRIB_KEY = SUBSTR(INP02_RECORD,119,11)                          
    END                                                                         
    COPY_KEY = COPY_REPORT_ID || COPY_HIERARCHY_CODE ||,                        
              COPY_DISTRIB_KEY || COPY_RECIPIENT_ID                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_COPY_DEL_TRANS:                                            */         
/*                                                                   */         
/* Create the records to support a single copy delete transaction.   */         
/* ----------------------------------------------------------------- */         
CREATE_COPY_DEL_TRANS:                                                          
    INFOPAC_TRAN_RECORD = TRAN_COPY_HEADER ||,                                  
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_REPORT_GROUP || ' ' || TRAN_RECIPIENT_ID || ' ' ||,          
              TRAN_COPY_TYPE || ' ' || TRAN_REPORT_ID || ' ' ||,                
              TRAN_HIERARCHY_CODE                                               
    CALL WRITE_COPY_DEL_TRAN_RECORD                                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_COPY_DEL_TRAN_RECORD                                        */         
/*                                                                   */         
/* Write an Infopac copy delete transaction record.                  */         
/* ----------------------------------------------------------------- */         
WRITE_COPY_DEL_TRAN_RECORD:                                                     
    OUT01_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_COPY_ADD_TRANS:                                            */         
/*                                                                   */         
/* Create the records to support a single copy add transaction.      */         
/* ----------------------------------------------------------------- */         
CREATE_COPY_ADD_TRANS:                                                          
    INFOPAC_TRAN_RECORD = TRAN_COPY_HEADER ||,                                  
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_REPORT_GROUP || ' ' || TRAN_RECIPIENT_ID || ' ' ||,          
              TRAN_COPY_TYPE || ' ' || TRAN_REPORT_ID || ' ' ||,                
              TRAN_HIERARCHY_CODE                                               
    CALL WRITE_COPY_ADD_TRAN_RECORD                                             
    INFOPAC_TRAN_RECORD = TRAN_COPY_CONTINUATION ||,                            
              '02 ' || TRAN_NUMBER_OF_COPIES || '        ' ||,                  
              '5000       Y'                                                    
    CALL WRITE_COPY_ADD_TRAN_RECORD                                             
    INFOPAC_TRAN_RECORD = TRAN_COPY_CONTINUATION ||,                            
              '03 ' || TRAN_COPY_COMMENTS                                       
    CALL WRITE_COPY_ADD_TRAN_RECORD                                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_COPY_ADD_TRAN_RECORD                                        */         
/*                                                                   */         
/* Write an Infopac copy add transaction record.                     */         
/* ----------------------------------------------------------------- */         
WRITE_COPY_ADD_TRAN_RECORD:                                                     
    OUT02_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT02_RECORD                                                           
    "EXECIO 1 DISKW OUT02"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CHECK_REPORT_LINE                                                 */         
/*                                                                   */         
/* Check pagination and write the next Infopac copy report line.     */         
/* Force page breaks at change in report id, and do not repeat the   */         
/* report id on subsequent lines, except at the start of             */         
/* continuation pages.                                               */         
/* ----------------------------------------------------------------- */         
CHECK_REPORT_LINE:                                                              
    IF (TRAN_REPORT_ID ^= LAST_REPORT_ID) THEN                                  
        REPORT_LINE_COUNT = PAGE_SIZE                                           
    REPORT_LINE_COUNT = REPORT_LINE_COUNT + 1                                   
    IF (REPORT_LINE_COUNT > PAGE_SIZE) THEN DO                                  
        REPORT_LINE_SAVE = REPORT_LINE_DETAIL                                   
        REPORT_PAGE_NUMBER = REPORT_PAGE_NUMBER + 1                             
        REPORT_CC = CC_NEW_PAGE                                                 
        REPORT_LINE = TITLE_LINE || REPORT_NUMBER ||,                           
                  '  PAGE ' || RIGHT(REPORT_PAGE_NUMBER,5)                      
        CALL WRITE_REPORT_LINE                                                  
        REPORT_CC = CC_NEXT_LINE                                                
        REPORT_LINE = REGION_HEADER                                             
        CALL WRITE_REPORT_LINE                                                  
        REPORT_LINE = REPORT_HEADER                                             
        CALL WRITE_REPORT_LINE                                                  
        REPORT_CC = CC_DOUBLE_SPACE                                             
        REPORT_LINE = REPORT_COLUMN_HEADER                                      
        CALL WRITE_REPORT_LINE                                                  
        REPORT_LINE_DETAIL = REPORT_LINE_SAVE                                   
        REPORT_LINE_COUNT = 7                                                   
        REPORT_CC = CC_DOUBLE_SPACE                                             
        LAST_REPORT_ID = LEFT(' ',10)                                           
    END                                                                         
    IF (TRAN_REPORT_ID ^= LAST_REPORT_ID) THEN                                  
        REPORT_LINE = TRAN_REPORT_ID || REPORT_LINE_DETAIL                      
    ELSE                                                                        
        REPORT_LINE = LEFT(' ',10) || REPORT_LINE_DETAIL                        
    CALL WRITE_REPORT_LINE                                                      
    REPORT_CC = CC_NEXT_LINE                                                    
    LAST_REPORT_ID = TRAN_REPORT_ID                                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_REPORT_LINE                                                 */         
/*                                                                   */         
/* Write an Infopac copy report line.                                */         
/* ----------------------------------------------------------------- */         
WRITE_REPORT_LINE:                                                              
    RPT01_RECORD = REPORT_CC || LEFT(REPORT_LINE,132)                           
    PUSH RPT01_RECORD                                                           
    "EXECIO 1 DISKW RPT01"                                                      
    RETURN                                                                      
