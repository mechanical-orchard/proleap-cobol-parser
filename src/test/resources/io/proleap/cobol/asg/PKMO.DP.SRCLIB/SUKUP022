      ***************************************************************** 00010013
       IDENTIFICATION DIVISION.                                         00020013
      ***************************************************************** 00030013
       PROGRAM-ID.             SUKUP022.                                00040013
       AUTHOR.                 PATRICK BURKE.                           00050013
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060013
       DATE-WRITTEN.           JUNE 2006.                               00070013
       DATE-COMPILED.                                                   00080013
      *                                                                 00090013
      ***************************************************************** 00100013
      * THIS PROGRAM WILL BE USED TO DELETE MULITPLE ENTRIES WITH THE   00110013
      * SAME TRIP_OCRNC_ID IN TABLE SUT_TRIP_DTL_SUM.                   00120013
      ***************************************************************** 00130013
       ENVIRONMENT DIVISION.                                            00140013
      ***************************************************************** 00150013
       CONFIGURATION SECTION.                                           00160013
       SOURCE-COMPUTER.                        IBM-3090.                00170013
       OBJECT-COMPUTER.                        IBM-3090.                00180013
       INPUT-OUTPUT SECTION.                                            00190013
       FILE-CONTROL.                                                    00200013
                                                                        00210013
           SELECT EXTRACT-FILE                                          00220013
               ASSIGN TO INP01.                                         00230013
      /                                                                 00240013
      ***************************************************************** 00250013
       DATA DIVISION.                                                   00260013
      ***************************************************************** 00270013
       FILE SECTION.                                                    00280013
                                                                        00290013
       FD  EXTRACT-FILE                                                 00300013
           RECORDING MODE IS F                                          00310013
           LABEL RECORDS ARE STANDARD                                   00320013
           BLOCK CONTAINS 0 RECORDS.                                    00330013
       01  EXTRACT-RECORD                   PIC X(150).                 00340013
      /                                                                 00350013
      ***************************************************************** 00360013
       WORKING-STORAGE SECTION.                                         00370013
      ***************************************************************** 00380013
                                                                        00390013
       01  PS-SWITCHES.                                                 00400013
           05  PS-END-OF-INPUT-FILE-SW      PIC X(01) VALUE 'N'.        00410013
               88  PS-END-OF-INPUT                    VALUE 'Y'.        00420013
               88  PS-NOT-END-OF-INPUT                VALUE 'N'.        00430013
                                                                        00440013
       01  PV-VARIABLES.                                                00450013
           05  PV-READ-COUNT                PIC S9(7) COMP-3            00460013
                                                      VALUE ZERO.       00470013
           05  PV-REC-READ-QTY              PIC S9(7) COMP-3            00480013
                                                      VALUE ZERO.       00490013
           05  PV-WRITE-COUNT               PIC S9(7) COMP-3            00500013
                                                      VALUE ZERO.       00510013
           05  PV-PAGE-COUNT                PIC S9(3) COMP-3            00520013
                                                      VALUE ZERO.       00530013
           05  PV-LINE-COUNT                PIC S9(04) VALUE ZERO       00540013
                                                       COMP SYNC.       00550013
           05  PV-DB2-FUNCTION              PIC X(30) VALUE SPACES.     00560013
           05  PV-PARAGRAPH                 PIC X(30) VALUE SPACES.     00570013
           05  PV-DISP-NUMBER               PIC Z,ZZZ,ZZ9.              00580013
                                                                        00590013
           05  PV-RUN-DATE                  PIC X(10) VALUE SPACES.     00600013
           05  PV-SAVE-START-DATE           PIC X(10) VALUE SPACES.     00610013
           05  PV-ERROR-MESSAGE-1           PIC X(60)  VALUE SPACES.    00620013
           05  PV-ERROR-MESSAGE-2           PIC X(60)  VALUE SPACES.    00630013
           05  PV-DB2-YEAR                  PIC S9(08) COMP             00640013
                                                VALUE ZERO.             00650013
           05  PV-DB2-MONTH                 PIC S9(08) COMP             00660013
                                                VALUE ZERO.             00670013
           05  PV-DB2-DAY                   PIC S9(08) COMP             00680013
                                                VALUE ZERO.             00690013
           05  PV-DB2-HOUR                  PIC S9(08) COMP             00700013
                                                VALUE ZERO.             00710013
           05  PV-DB2-MINUTE                PIC S9(08) COMP             00720013
                                                VALUE ZERO.             00730013
           05  PV-DB2-SECOND                PIC S9(08) COMP             00740013
                                                VALUE ZERO.             00750013
                                                                        00760013
           05  PV-DISPLAY-COUNT             PIC ZZZ,ZZZ,ZZ9.            00770013
           05  PV-COMMIT-COUNT              PIC S9(09)    VALUE +0      00780013
                                                         COMP SYNC.     00790013
           05  PV-COMMIT-COUNTER            PIC 9(03)     VALUE ZERO.   00800013
           05  PV-DELETE-95-COUNTER         PIC S9(09) COMP-3           00810014
                                                    VALUE ZERO.         00820013
           05  PV-DELETE-114-COUNTER        PIC S9(09) COMP-3           00821014
                                                    VALUE ZERO.         00822014
           05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZZ,ZZ9.        00830013
                                                                        00840013
           05  PV-DELETE-SUT00095           PIC S9(09)    VALUE ZERO    00850013
                                                         COMP SYNC.     00860013
           05  PV-DELETE-SUT00114           PIC S9(09)    VALUE ZERO    00861014
                                                         COMP SYNC.     00862014
           05  PV-NOT-FOUND-SUT00095        PIC S9(09)    VALUE +0      00870013
                                                         COMP SYNC.     00880013
           05  PV-NOT-FOUND-SUT00114        PIC S9(09)    VALUE +0      00881014
                                                         COMP SYNC.     00882014
           05  PV-TRIP-STAT-CDE-SU          PIC S9(09)    VALUE ZERO    00890013
                                                         COMP SYNC.     00900013
           05  PV-TRIP-STAT-CDE-AR          PIC S9(09)    VALUE ZERO    00910013
                                                         COMP SYNC.     00920013
           05  PV-TRIP-STAT-CDE-DR          PIC S9(09)    VALUE ZERO    00930013
                                                         COMP SYNC.     00940013
           05  PV-CURR-TMST                 PIC X(26) VALUE SPACES.     00950013
           05  PV-CURR-FUNC-TMST            PIC X(26) VALUE SPACES.     00960013
           05  PV-FUNC-QTY                  PIC S9(09) COMP.            00970013
                                                                        00980013
                                                                        00990013
      ******************************************************************01000013
       01  PV-ABEND-FIELDS.                                             01010013
           05  PV-ABEND-CODE                PIC S9(04) VALUE +0         01020013
                                                       COMP SYNC.       01030013
           05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'SUKUP022'.01040013
           05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.    01050013
           05  PV-ABEND-OPERATION           PIC  X(51) VALUE SPACES.    01060013
           05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.    01070013
           05  PV-ABEND-STRUCTURE-2         PIC X(8)   VALUE SPACES.    01080013
           05  PV-ABEND-STRUCTURE-3         PIC X(8)   VALUE SPACES.    01090013
           05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.    01100013
           05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.    01110013
           05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.    01120013
      /                                                                 01130013
       01  PC-CONSTANTS.                                                01140013
           05  PC-CURRENT-PROGRAM-NAME      PIC X(08) VALUE 'SUKUP022'. 01150013
           05  PC-ROW-NOT-FOUND             PIC S9(04)    VALUE +100    01160013
                                                         COMP SYNC.     01170013
           05  PC-COMMIT-HOLD               PIC 9(3)      VALUE 200.    01180013
      /                                                                 01190013
      ******************************************************************01200013
      *  COPYBOOK FOR UPLOAD TRANSACTION CODES AND ERROR CODES.         01210013
      ******************************************************************01220013
           COPY PCWS208.                                                01230013
      *                                                                 01240013
      ******************************************************************01250013
      *  COPYBOOK USED INPUT FILE                                       01260013
      ******************************************************************01270013
           COPY SURD072.                                                01280013
      *                                                                 01290013
      ******************************************************************01300013
      *  COPYBOOK USED BY MBS SYSTEM                                    01310013
      ******************************************************************01320013
           COPY DPWS003.                                                01330013
      *                                                                 01340013
      ******************************************************************01350013
      *  WORKING STORAGE AREA FOR NAME REFORMAT DPPD024                 01360013
      ******************************************************************01370013
           COPY DPWS024.                                                01380013
      /                                                                 01390013
      ******************************************************************01400013
      *  DB2 FORMATTED MESSAGE AREA                                     01410013
      ******************************************************************01420013
           COPY DPWS004.                                                01430013
      /                                                                 01440013
      ******************************************************************01450013
      * CORPORATE DATE ROUTINES                                         01460013
      ******************************************************************01470013
           COPY DPWS500.                                                01480013
      /                                                                 01490013
      ******************************************************************01500013
      *   DB2 SQL COMMUNICATION AREA                                    01510013
      ******************************************************************01520013
           EXEC SQL                                                     01530013
               INCLUDE SQLCA                                            01540013
           END-EXEC.                                                    01550013
      *                                                                 01560013
           COPY SQLCA2.                                                 01570013
      *                                                                 01580013
      ******************************************************************01590013
      *   TRIP DETAIL SUM TABLE (SUT_TRIP_DTL_SUM)                      01600013
      ******************************************************************01610013
      *                                                                 01620013
           EXEC SQL                                                     01630013
               INCLUDE SUT00095                                         01640013
           END-EXEC.                                                    01650013
                                                                        01650113
      ******************************************************************01651013
      *   TRIP DETAIL SUM TABLE (SUT_TRIP_PRCHG)                        01652013
      ******************************************************************01653013
      *                                                                 01654013
           EXEC SQL                                                     01655013
               INCLUDE SUT00114                                         01656013
           END-EXEC.                                                    01657013
      *                                                                 01660013
      ******************************************************************01670013
       PROCEDURE DIVISION.                                              01680013
      ******************************************************************01690013
       0000-MAINLINE.                                                   01700013
                                                                        01710013
           PERFORM 1000-INITIALIZE.                                     01720013
                                                                        01730013
           PERFORM 2000-PROCESS-EXTRACT                                 01740013
               UNTIL PS-END-OF-INPUT.                                   01750013
                                                                        01760013
           PERFORM 9000-EOJ.                                            01770013
                                                                        01780013
            GOBACK.                                                     01790013
                                                                        01800013
      ******************************************************************01810013
      *  INITIALIZATION TASKS.                                          01820013
      *    -- OPEN INPUT FILE                                           01830013
      *    -- READ FILE                                                 01840013
      ******************************************************************01850013
       1000-INITIALIZE.                                                 01860013
                                                                        01870013
           OPEN INPUT  EXTRACT-FILE.                                    01880013
                                                                        01890013
           PERFORM 2100-READ-EXTRACT.                                   01900013
           MOVE SU072-CURR-TMST      TO PV-CURR-TMST.                   01910013
           MOVE SU072-CURR-FUNC-TMST TO PV-CURR-FUNC-TMST               01920013
           MOVE SU072-FUNC-QTY       TO PV-FUNC-QTY.                    01930013
      /                                                                 01940013
      ******************************************************************01950013
       2000-PROCESS-EXTRACT.                                            01960013
      ******************************************************************01970013
      *                                                                 01980013
           PERFORM 8100-DELETE-TRIP-DTL.                                01990014
                                                                        02000013
           PERFORM 8300-DELETE-TRIP-PRCHG.                              02001014
                                                                        02002014
           PERFORM 2200-COMMIT-DELETE.                                  02010014
                                                                        02020013
           PERFORM 2100-READ-EXTRACT.                                   02030013
      *                                                                 02040013
      ******************************************************************02050013
      *  READ NEXT INPUT FILE RECORD.                                   02060013
      ******************************************************************02070013
       2100-READ-EXTRACT.                                               02080013
                                                                        02090013
           READ EXTRACT-FILE INTO SU072-EXTRACT-RECORD                  02100013
              AT END                                                    02110013
                 SET PS-END-OF-INPUT TO TRUE                            02120013
              NOT AT END                                                02130013
                 ADD +1                   TO PV-READ-COUNT              02140013
           END-READ.                                                    02150013
      *                                                                 02160013
      ******************************************************************02170013
      * COMMIT THE DELETE FROM ERROR TABLE                              02180013
      ******************************************************************02190013
       2200-COMMIT-DELETE.                                              02200013
                                                                        02210013
            EXEC SQL                                                    02220013
               COMMIT                                                   02230013
            END-EXEC                                                    02240013
                                                                        02250013
            ADD +1                          TO PV-COMMIT-COUNT.         02260013
            MOVE +0 TO PV-COMMIT-COUNTER.                               02270013
                                                                        02280013
      *                                                                 02290013
      ******************************************************************02300013
      * DELETE THE ROWS FROM THE SUM_TRIP_DTL_SUM TABLE                 02310013
      ******************************************************************02320013
       8100-DELETE-TRIP-DTL.                                            02330013
                                                                        02340013
           MOVE SU072-TRIP-OCRNC-ID      TO SUT00095-TRIP-OCRNC-ID.     02350013
                                                                        02360013
           EXEC SQL                                                     02370013
               DELETE                                                   02380013
                   FROM  SUT_TRIP_DTL_SUM                               02390013
                   WHERE TRIP_OCRNC_ID   = :SUT00095-TRIP-OCRNC-ID      02400013
           END-EXEC.                                                    02410013
                                                                        02420013
           EVALUATE TRUE                                                02430013
               WHEN SQLCODE = 0                                         02440013
                   PERFORM 8200-TRIP-STAT-CDE                           02450013
                   ADD +1           TO PV-DELETE-SUT00095               02460013
                   ADD SQLERRD(3)   TO PV-DELETE-95-COUNTER             02470014
               WHEN SQLCODE = +100                                      02480013
                   ADD +1           TO PV-NOT-FOUND-SUT00095            02490013
               WHEN SQLCODE < 0                                         02500013
               WHEN SQLCODE > 0                                         02510013
               WHEN SQLWARN0 = 'W'                                      02520013
                   MOVE '8100-DELETE-TRIP-DTL'                          02530013
                               TO PV-PARAGRAPH                          02540013
                   MOVE 'DELETE OF SUT00095'                            02550013
                               TO PV-DB2-FUNCTION                       02560013
                   PERFORM 9200-SQL-ABEND                               02570013
           END-EVALUATE.                                                02580013
      /                                                                 02592013
      ******************************************************************02600013
      * COUNT TYPES OF TRIP STAT CDES DELETED                           02610013
      ******************************************************************02620013
       8200-TRIP-STAT-CDE.                                              02630013
                                                                        02640013
           EVALUATE TRUE                                                02650013
               WHEN SU072-TRIP-STAT-CDE  = 'SU'                         02660013
                   ADD +1           TO PV-TRIP-STAT-CDE-SU              02670013
                                                                        02680013
               WHEN SU072-TRIP-STAT-CDE  = 'AR'                         02690013
                   ADD +1           TO PV-TRIP-STAT-CDE-AR              02700013
                                                                        02710013
               WHEN SU072-TRIP-STAT-CDE  = 'DR'                         02720013
                   ADD +1           TO PV-TRIP-STAT-CDE-DR              02730013
                                                                        02740013
           END-EVALUATE.                                                02750013
      *                                                                 02751014
      ******************************************************************02752014
      * DELETE THE ROWS FROM THE SUT_TRIP_PRCHG TABLE                   02753014
      ******************************************************************02754014
       8300-DELETE-TRIP-PRCHG.                                          02755014
                                                                        02756014
           MOVE SU072-OTBND-TRIP-ID      TO SUT00114-OTBND-TRIP-ID.     02757014
                                                                        02758014
           EXEC SQL                                                     02759014
               DELETE                                                   02759114
                   FROM  SUT_TRIP_PRCHG                                 02759214
                   WHERE OTBND_TRIP_ID   = :SUT00114-OTBND-TRIP-ID      02759314
           END-EXEC.                                                    02759414
                                                                        02759514
           EVALUATE TRUE                                                02759614
               WHEN SQLCODE = 0                                         02759714
                   ADD +1                TO PV-DELETE-SUT00114          02759814
                   ADD SQLERRD(3)        TO PV-DELETE-114-COUNTER       02759914
               WHEN SQLCODE = +100                                      02760014
                   ADD +1                TO PV-NOT-FOUND-SUT00114       02760114
               WHEN SQLCODE < 0                                         02760214
               WHEN SQLCODE > 0                                         02760314
               WHEN SQLWARN0 = 'W'                                      02760414
                   MOVE '8300-DELETE-TRIP-PRCHG'                        02760514
                                         TO PV-PARAGRAPH                02760614
                   MOVE 'DELETE OF SUT00114'                            02760714
                                         TO PV-DB2-FUNCTION             02760814
                   PERFORM 9200-SQL-ABEND                               02760914
           END-EVALUATE.                                                02761014
      /                                                                 02762013
      ******************************************************************02770013
      *CLOSE INPUT FILE.                                                02780013
      ******************************************************************02790013
       9000-EOJ.                                                        02800013
      *                                                                 02810013
           CLOSE EXTRACT-FILE.                                          02820013
                                                                        02830013
           MOVE PV-DELETE-95-COUNTER        TO PV-DISP-NBR              02840014
           DISPLAY 'SUT_TRIP_DTL_SUM DELETES   = '  PV-DISP-NBR.        02850013
                                                                        02860013
           MOVE PV-DELETE-SUT00095  TO PV-DISPLAY-COUNT.                02870013
           DISPLAY 'SUT_TRIP_SUM TRIP OCRNCS = ' PV-DISPLAY-COUNT.      02880013
                                                                        02880114
           MOVE PV-NOT-FOUND-SUT00095 TO PV-DISPLAY-COUNT.              02880214
           DISPLAY 'SUT_TRIP_SUM TRIP OCRNC NOT FOUND'                  02880314
                                         PV-DISPLAY-COUNT.              02880414
                                                                        02881014
           MOVE PV-DELETE-114-COUNTER       TO PV-DISP-NBR              02882014
           DISPLAY 'SUT_TRIP_PRCHG DELETES   = '  PV-DISP-NBR.          02883014
                                                                        02884014
           MOVE PV-DELETE-SUT00114  TO PV-DISPLAY-COUNT.                02885014
           DISPLAY 'SUT_TRIP_PRCHG TRIP OCRNCS = ' PV-DISPLAY-COUNT.    02886014
                                                                        02887014
           MOVE PV-NOT-FOUND-SUT00114 TO PV-DISPLAY-COUNT.              02888014
           DISPLAY 'SUT_TRIP_PRCHG TRIP OTBND NOT FOUND'                02889014
                                         PV-DISPLAY-COUNT.              02890014
                                                                        02930013
           DISPLAY 'CURRENT TIMESTAMP               ' PV-CURR-TMST.     02940013
           DISPLAY 'CURRENT TIMESTAMP LESS FUNC QTY ' PV-CURR-FUNC-TMST.02950013
           DISPLAY 'NUMBER OF DAYS ON PARM TABLE    ' PV-FUNC-QTY.      02960013
                                                                        02970013
           MOVE PV-TRIP-STAT-CDE-SU      TO PV-DISPLAY-COUNT.           02980013
           DISPLAY 'SU TRIP STAT CODES ' PV-DISPLAY-COUNT.              02990013
           MOVE PV-TRIP-STAT-CDE-AR      TO PV-DISPLAY-COUNT.           03000013
           DISPLAY 'AR TRIP STAT CODES ' PV-DISPLAY-COUNT.              03010013
           MOVE PV-TRIP-STAT-CDE-DR      TO PV-DISPLAY-COUNT.           03020013
           DISPLAY 'DR TRIP STAT CODES ' PV-DISPLAY-COUNT.              03030013
                                                                        03040013
           MOVE PV-COMMIT-COUNT  TO PV-DISPLAY-COUNT.                   03050013
           DISPLAY 'TOTAL COMMITS TAKEN        ' PV-DISPLAY-COUNT.      03060013
      /                                                                 03070013
      ******************************************************************03080013
       9200-SQL-ABEND.                                                  03090013
      ******************************************************************03100013
                                                                        03110013
           MOVE SQLCODE                     TO SQLCODE2.                03120013
           MOVE SQLERRD(3)                  TO SQLERRD3.                03130013
                                                                        03140013
           DISPLAY '** ABEND **         ** = SQLERROR'.                 03150013
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        03160013
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                   03170013
           DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                03180013
           DISPLAY '*** MESSAGE     = ' PV-DB2-OPERATION-MESSAGE-1.     03190013
           DISPLAY '***             = ' PV-DB2-OPERATION-MESSAGE-2.     03200013
           DISPLAY '***             = ' PV-DB2-OPERATION-MESSAGE-3.     03210013
                                                                        03220013
           DISPLAY '** SQLCODE          ** = ' SQLCODE2.                03230013
           DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                03240013
           DISPLAY '** SQLERRM          ** = ' SQLERRM.                 03250013
           DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                03260013
                                                                        03270013
                                                                        03280013
           MOVE +4013                       TO PV-ABEND-CODE.           03290013
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         03300013
                                                                        03310013
