       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUP135.                                        
       AUTHOR.                 PATTY JAEGER.                                    
       INSTALLATION.           KOHLS.                                           
       DATE-WRITTEN.           NOVEMBER 2008.                                   
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                                         
      *    THIS PROGRAM PROCESSES INFORMATION FROM JOB MLK140.                  
      *    IT WILL QUERY THE TINVPAR TABLE FOR STORE DATA MATCHING              
      *    THE INPUT FILE WHERE THE FINANCIAL INVENTORY STATUS IS TC            
      *    , THE STORE INVENTORY STATUS IS ACTIVE, AND THE FINANCIAL            
      *    BOOK DATE HAS NOT BEEN SET YET.   WHEN THIS DATA                     
      *    IS FOUND, THE FINANCIAL BOOK STATUS WILL BE UPDATED                  
      *    TO TP AND THE ACTUAL FINANCIAL BOOK DATE WILL BE                     
      *    SET TO THE CURRENT DATE.                                             
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
      * ------- ---------- ------------ --------------------------              
      * W33304              NEW  - PATTY JAEGER                                 
      * 11/25/2008          CHANGED BY:                                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INVEN-ADJ-FILE             ASSIGN TO INP01.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INVEN-ADJ-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 90 CHARACTERS                                        
           DATA RECORD IS INV-ADJ-REC.                                          
       01  INV-ADJ-REC                 PIC X(90).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00760000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00770000
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)   VALUE SPACE.         
           05  PV-TOTAL-RTL-ADJ-NUM            PIC S9(11)V99                    
                                                           VALUE ZERO.          
           05  PV-TOTAL-RTL-ADJ-DIS            PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                            VALUE ZERO.         
           05  PV-TOTAL-EST-RTL-ADJ-NUM        PIC S9(11)V99                    
                                                            VALUE ZERO.         
           05  PV-TOTAL-EST-RTL-ADJ-DIS        PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                            VALUE ZERO.         
           05  PV-RETRY-COUNT                  PIC S9(4)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-SKIP-ERROR-CNT               PIC S9(9)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-UPDATED-CNT                  PIC S9(9)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-COMMIT-CNT-CHK               PIC S9(9)                        
                                               COMP SYNC   VALUE +0.            
           05  PV-STR-NBR                      PIC 9(05).                       
           05  FILLER REDEFINES PV-STR-NBR.                                     
               10  PV-STR-NBR-X                PIC X(05).                       
           05  PV-SQLCODE                      PIC S9(04) VALUE ZERO.           
                                                                                
           05  PV-OPN-FSCL-MTH-END-DTE.                                         
               10  PV-OPN-FSCL-FSCL-YYCC.                                       
                   15 PV-OPN-FSCL-FSCL-CC      PIC X(02)   VALUE SPACE.         
                   15 PV-OPN-FSCL-FSCL-YY      PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-MM         PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-DD         PIC X(02)   VALUE SPACE.         
                                                                                
           05  PV-CURRENT-DATE.                                                 
               10 PV-CURRENT-DATE-CCYY   PIC X(04)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-MM    PIC X(02)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-DD    PIC X(02)   VALUE SPACE.               
                                                                                
           05  PV-DB2-CURRENT-DATE       PIC X(10).                             
           05  PV-HOLD-STORE-NBR         PIC X(05)  VALUE SPACE.        INKUT119
           05  PV-STORE-NBR-ALPHA        PIC X(05)  VALUE SPACES.               
           05  PV-STORE-NBR-NUM REDEFINES PV-STORE-NBR-ALPHA                    
                                         PIC 9(5).                              
           05  PV-STORE-NBR-COMP         PIC S9(4) COMP.                        
           05  PV-BOOK-ADJ-FSCL-PER      PIC X(02).                     INKUT119
           05  PV-DB2-FIN-BOOK-DATE      PIC X(10).                     INKUT119
           05  PV-SQL-SUB                PIC S9(4)  COMP.               INKUT119
           05  PV-INV-ID                 PIC ZZZZ9.                             
           05  PV-CYCLE-DE-ACT-REC.                                             
               10  FILLER                PIC  X(16) VALUE                       
                                                     'INVENTORY CYCLE '.        
               10  PV-CYCLE-INV-ID       PIC ZZZZ9  VALUE ZERO.                 
               10  FILLER                PIC  X(13) VALUE                       
                                                     ' DE-ACTIVATED'.           
               10  FILLER                PIC  X(46) VALUE SPACES.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC  X(08)  VALUE 'INKUP135'.  00387000
           05  PC-MAX-DEADLOCKS          PIC S9(03)  VALUE +3.          00389000
           05  PC-MAX-RETRIES            PIC S9(4)   COMP VALUE +3.     INKUT119
           05  PC-9S-DATE                PIC X(10)   VALUE '9999-09-09'.INKUT119
           05  PC-INKUP135               PIC X(08)   VALUE 'INKUP135'.  INKUT119
           05  PC-5161                   PIC 9(09)   VALUE 5161.        INKUT119
           05  PC-2230                   PIC 9(05)   VALUE 2230.        INKUT119
           05  PC-TRANS-PROCESSED        PIC X(02)   VALUE 'TP'.        INKUT119
           05  PC-TRANS-CREATED          PIC X(02)   VALUE 'TC'.        INKUT119
           05  PC-EMPTY-DATE             PIC X(10)   VALUE '9999-09-09'.INKUT119
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00400000
           05  PE-MSG-01                       PIC X(50) VALUE          00510000
                'PROGRAM ABENDED WHILE TRYING TO LOOKUP A STORE   '.    00520000
           05  PE-MSG-05                       PIC X(50) VALUE          00510000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00520000
           05  PE-MSG-06                       PIC X(50) VALUE          00530000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED '.    00540000
                                                                                
       01  ABEND-CODE                    PIC S9(04)   VALUE +0.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INV-ADJ-EOF-SW         PIC X(01)     VALUE 'N'.               
               88  PS-INV-ADJ-EOF                      VALUE 'Y'.               
           05  PS-FOUND-TINVPAR-SW       PIC X(01)     VALUE 'N'.               
               88  PS-FOUND-TINVPAR-NO                 VALUE 'N'.               
               88  PS-FOUND-TINVPAR-YES                VALUE 'Y'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ                    PIC 9(09) VALUE ZERO.            
           05  PA-COMMIT-COUNT                 PIC 9(09)  VALUE ZEROES.         
           05  PA-RECS-BYPASSED                PIC 9(09) VALUE ZERO.            
           05  PA-ERROR-RECS                   PIC 9(09) VALUE ZERO.            
           05  PA-ROWS-UPDATED                 PIC 9(09) VALUE ZERO.            
                                                                                
                                                                        01650000
      ******************************************************************        
      *    INPUT - INVENTORY ADJUSTMENT RECORD                                  
      ******************************************************************        
           COPY MLRD024.                                                        
                                                                                
      ***************************************************************           
      *  ERROR MESSAGE AREA FOR DB2                                             
      ***************************************************************           
           COPY DPWS004.                                                        
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * TINCNTL DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 INVENTORY PARAMETERS TABLE.                      
      ***************************************************************           
           EXEC SQL                                                     INKUT119
                INCLUDE TINVPAR                                         INKUT119
           END-EXEC.                                                    INKUT119
                                                                                
                                                                        01462000
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-INKUP135.                                                           
                                                                                
           OPEN INPUT INVEN-ADJ-FILE.                                           
                                                                                
           PERFORM 1000-INIT-PROCESS.                                           
                                                                                
           PERFORM 4000-READ-INV-ADJ-FILE.                                      
                                                                                
           PERFORM 2000-PROCESS-INVEN-ADJ-RECORDS                               
               UNTIL PS-INV-ADJ-EOF.                                            
                                                                                
      ********************************************                              
      *    DO ONE LAST COMMIT IF THERE ARE RECS THAT HAVE BEEN UPDATED          
      ********************************************                              
           IF PV-COMMIT-CNT-CHK > 0                                             
              MOVE 6  TO PV-COMMIT-CNT-CHK                              05940000
              PERFORM 7800-COMMIT-CHANGES                               05930000
           END-IF.                                                              
                                                                                
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
           DISPLAY '* TOTAL RECORDS READ      : ' PA-RECS-READ.                 
           DISPLAY '* TINVPAR ROWS UPDATED    : ' PA-ROWS-UPDATED.              
           DISPLAY '* COMMITS DONE            : ' PA-COMMIT-COUNT.      06030000
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
                                                                                
           CLOSE INVEN-ADJ-FILE.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   SET UP THE CURRENT DATE FOR USE LATER                                 
      ******************************************************************        
       1000-INIT-PROCESS.                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:4) TO PV-CURRENT-DATE-CCYY.            
           MOVE FUNCTION CURRENT-DATE (5:2) TO PV-CURRENT-DATE-MM.              
           MOVE FUNCTION CURRENT-DATE (7:2) TO PV-CURRENT-DATE-DD.              
                                                                        02140000
      ******************************************************************        
      *  THIS PARAGRAPH WRITES ADJUSTMENT RECORDS THAT WILL BE USED             
      *  IN THE FINAL BOOKING OF THE INVENTORY.  A TRANSACTION CODE             
      *  OF "8" FLAGS THE RECORD AS AN INVENTORY ADJUSTMENT RECORD.             
      *  THE BOOKING MONTH IS THE MONTH THAT THESE ADJUSTMENTS ARE TO           
      *  BE APPLIED.                                                            
      ******************************************************************        
       2000-PROCESS-INVEN-ADJ-RECORDS.                                          
               PERFORM 3000-LOOKUP-ACTIVE-INV.                                  
               PERFORM 4000-READ-INV-ADJ-FILE.                                  
                                                                                
      ******************************************************************        
      * QUERY FOR TINVPAR INFO                                                  
      ******************************************************************        
       3000-LOOKUP-ACTIVE-INV.                                                  
                                                                                
           MOVE '* 3000-LOOKUP-ACTIVE-INV *' TO PV-PARAGRAPH-NAME.      06180000
                                                                                
           MOVE ML024-ADJ-STORE-NBR TO PV-STORE-NBR-ALPHA.                      
           MOVE PV-STORE-NBR-NUM    TO PV-STORE-NBR-COMP.                       
           MOVE PV-STORE-NBR-COMP   TO INVPAR-LOC-NBR.                          
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
               EXEC SQL                                                         
                   SELECT A.INV_ID                                              
                           , INV_NM                                             
                           , LOC_NBR                                            
                           , ACTL_INV_DTE                                       
                           , ACTL_FIN_BK_DTE                                    
                           , SCHD_PHY_CNT_CDE                                   
                           , FINCL_INV_STAT_CDE                                 
                     INTO    :INVPAR-INV-ID                                     
                           , :INCNTL-INV-NM                                     
                           , :INVPAR-LOC-NBR                                    
                           , :INVPAR-ACTL-INV-DTE                               
                           , :INVPAR-ACTL-FIN-BK-DTE                            
                           , :INVPAR-SCHD-PHY-CNT-CDE                           
                           , :INVPAR-FINCL-INV-STAT-CDE                         
                   FROM TINVPAR A                                               
                       ,TINCNTL B                                               
                   WHERE LOC_NBR            = :INVPAR-LOC-NBR                   
                     AND ACTL_FIN_BK_DTE    = '9999-09-09'                      
                     AND FINCL_INV_STAT_CDE = 'TC'                              
                     AND LOC_INV_STAT_CDE   = 'IN'                              
                     AND ACTV_IND           = 'Y'                               
                     AND A.INV_ID           = B.INV_ID                          
                   WITH UR                                                      
               END-EXEC                                                     INKU
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                      PERFORM 5000-UPDATE-INVPAR-TABLE                          
                      PERFORM 7800-COMMIT-CHANGES                       05930000
                      ADD 1 TO PA-ROWS-UPDATED                                  
                   WHEN SQLCODE        = +100                                   
                      MOVE PE-MSG-01      TO PV-OPERATION-ATTEMPTED     06170000
                      DISPLAY 'INVPAR NOT FOUND FOR '                           
                      DISPLAY 'STORE NUMBER......', ML024-ADJ-STORE-NBR         
********              PERFORM 9950-SQL-ERROR                            06190000
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                     IF PV-SQL-SUB > PC-MAX-RETRIES                             
                       DISPLAY 'MAX RETRIES EXCEEDED ON COUNT INV_PARM'         
                       PERFORM 9950-SQL-ERROR                           06190000
                     END-IF                                                     
                   WHEN OTHER                                                   
                      DISPLAY '3000-LOOKUP-ACTIVE-INV'                          
                      DISPLAY 'ERROR LOOKING UP INVPAR'                         
                      DISPLAY 'STORE NUMBER......', ML024-ADJ-STORE-NBR         
                      PERFORM 9950-SQL-ERROR                            06190000
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
       4000-READ-INV-ADJ-FILE.                                                  
                                                                                
           READ INVEN-ADJ-FILE INTO ML024-ADJ-RECORD                            
               AT END                                                           
                   SET PS-INV-ADJ-EOF    TO TRUE.                               
                                                                                
           IF PS-INV-ADJ-EOF                                                    
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-RECS-READ                                            
           END-IF.                                                              
                                                                                
      ***************************************************************           
      *    UPDATE INVPAR ACTIVE REC WITH STATUS AND DATE                        
      ***************************************************************           
       5000-UPDATE-INVPAR-TABLE.                                                
                                                                                
           MOVE '5000-UPDATE-INVPAR-TABLE'                              INKUT119
                                    TO  PV-PARAGRAPH-NAME.              INKUT119
                                                                                
           MOVE PV-CURRENT-DATE     TO PV-DB2-CURRENT-DATE.                     
                                                                                
           PERFORM                                                              
              WITH TEST AFTER                                                   
              VARYING PV-SQL-SUB FROM 1 BY 1                                    
              UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                              
                    SQLCODE = 0 OR                                              
                    SQLCODE = 100                                               
                                                                                
              EXEC SQL                                                          
                                                                                
                  UPDATE TINVPAR                                                
                  SET   ACTL_FIN_BK_DTE       = :PV-DB2-CURRENT-DATE            
                      , FINCL_INV_STAT_CDE    = :PC-TRANS-PROCESSED             
                      , CHG_TMST              =  CURRENT TIMESTAMP      INKUT119
                      , CHG_ID_NBR            = :PC-INKUP135            INKUT119
                  WHERE INV_ID                = :INVPAR-INV-ID                  
                    AND LOC_NBR               = :PV-STORE-NBR-COMP              
                    AND ACTL_FIN_BK_DTE       = :PC-EMPTY-DATE                  
                    AND FINCL_INV_STAT_CDE    = :PC-TRANS-CREATED               
              END-EXEC                                                     INKUT
                                                                                
              EVALUATE TRUE                                                INKUT
                  WHEN SQLCODE = 0                                         INKUT
                      CONTINUE                                                  
                  WHEN SQLCODE = -904                                      INKUT
                  WHEN SQLCODE = -914                                      INKUT
                       IF  PV-SQL-SUB > PC-MAX-RETRIES                  INKUT119
                          MOVE 'TINVPAR UPDATE RETRIES EXCEEDED'        INKUT119
                                     TO  PV-DB2-OPERATION-ATTEMPTED     INKUT119
                          PERFORM 9950-SQL-ERROR                        INKUT119
                       END-IF                                           INKUT119
                  WHEN OTHER                                               INKUT
                       DISPLAY '5000-UPDATE-INVPAR-TABLE'                       
                       DISPLAY 'STORE NUMBER.... ' ML024-ADJ-STORE-NBR          
                       DISPLAY 'CYCLE ID ....... ' INVPAR-INV-ID                
                       DISPLAY 'UPDATE NOT SUCCESSFUL'                          
                       MOVE 'UPDATE TINVPAR'                               INKUT
                                        TO  PV-DB2-OPERATION-ATTEMPTED     INKUT
                       PERFORM 9950-SQL-ERROR                              INKUT
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                        INKUT119
                                                                        INKUT119
      ******************************************************************05870000
      * 7800-COMMIT-CHANGES.                                            05880000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      05890000
      *            TAKE A COMMIT.                                       05900000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    05910000
      ******************************************************************05920000
       7800-COMMIT-CHANGES.                                             05930000
                                                                                
           MOVE '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME.                 
                                                                                
           IF PV-COMMIT-CNT-CHK > 5                                     05940000
              PERFORM                                                           
                 WITH TEST AFTER                                                
                 VARYING PV-SQL-SUB FROM 1 BY 1                                 
                 UNTIL PV-SQL-SUB > PC-MAX-RETRIES OR                           
                       SQLCODE = 0 OR                                           
                       SQLCODE = 100                                            
                                                                                
                 EXEC SQL                                                     06
                     COMMIT                                                   06
                 END-EXEC                                                     06
                                                                        06130000
                 EVALUATE TRUE                                                  
                    WHEN SQLCODE = 0                                            
                       ADD +1 TO PA-COMMIT-COUNT                        06030000
                       MOVE 0 TO PV-COMMIT-CNT-CHK                              
                    WHEN SQLCODE = -904                                         
                    WHEN SQLCODE = -911                                         
                     IF PV-SQL-SUB > PC-MAX-RETRIES                             
                       DISPLAY 'MAX RETRIES EXCEEDED ON COUNT INV_PARM'         
                       PERFORM 9950-SQL-ERROR                                061
                     END-IF                                                     
                    WHEN OTHER                                                  
                       MOVE PE-MSG-05         TO PV-OPERATION-ATTEMPTED      061
                       PERFORM 9950-SQL-ERROR                                061
                 END-EVALUATE                                                   
              END-PERFORM                                                       
           ELSE                                                                 
              ADD 1 TO PV-COMMIT-CNT-CHK                                        
           END-IF.                                                      05940000
                                                                                
                                                                        06220000
       9950-SQL-ERROR.                                                          
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **     ** = SQLERROR'.                             
           DISPLAY '** PROGRAM      ** = INKUP135'.                             
           DISPLAY '** PARAGRAPH    ** = ' PV-PARAGRAPH-NAME.                   
           DISPLAY '** OPERATION    ** = ' PV-DB2-OPERATION-ATTEMPTED.          
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
       EJECT                                                                    
