000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT033.                                        
000400 AUTHOR.                 DAN HINTZ.                                       
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING ITEM DATA                   
001200*  INFORMATION FOR COMPLIANCE NETWORK                                     
001400*                                                                         
001400*                                                                         
002800 ENVIRONMENT DIVISION.                                                    
002900                                                                          
003000 CONFIGURATION SECTION.                                                   
003100                                                                          
003200 SOURCE-COMPUTER.                        IBM-3090.                        
003300 OBJECT-COMPUTER.                        IBM-3090.                        
003400                                                                          
003500 INPUT-OUTPUT SECTION.                                                    
003600                                                                          
003700 FILE-CONTROL.                                                            
003800                                                                          
003900     SELECT  VL-ITEM-FILE                                                 
004000         ASSIGN TO OUT01.                                                 
004100                                                                          
003900     SELECT  VL-MARKER-FILE                                               
004000         ASSIGN TO OUT02.                                                 
004100                                                                          
004500******************************************************************        
004600 DATA DIVISION.                                                           
004700******************************************************************        
004800                                                                          
004900 FILE SECTION.                                                            
005000*                                                                         
005100 FD  VL-ITEM-FILE                                                         
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  VL-ITEM-RECORD                   PIC  X(278).                        
005600*                                                                         
005100 FD  VL-MARKER-FILE                                                       
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  VL-MARKER-RECORD                 PIC  X(78).                         
005600*                                                                         
006300*                                                                         
006500 WORKING-STORAGE SECTION.                                                 
000250*****************************************************************         
000260*    INPUT CONTROL CARD                                                   
000270*****************************************************************         
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
013800*****************************************************************         
013800*    PROGRAM CONSTANTS                                                    
013800*****************************************************************         
013600 01  PC-CONSTANTS.                                                        
013700     05  PC-CURRENT-PGM-VLKUT033      PIC  X(08) VALUE 'VLKUT033'.        
013700     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
013700                                                 COMP SYNC.               
013800                                                                          
013800*****************************************************************         
013800*    PROGRAM SWITCHES                                                     
013800*****************************************************************         
013900 01  PS-SWITCHES.                                                         
014000     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
014100         88  PS-END-CSR                          VALUE 'Y'.               
014100         88  PS-NOT-END-CSR                      VALUE 'N'.               
014000     05  PS-EDI-ORD-SWITCH            PIC  X(01) VALUE 'N'.               
014100         88  PS-HAVE-EDI                         VALUE 'Y'.               
014100         88  PS-NO-EDI                           VALUE 'N'.               
014000     05  PS-INTERNAL-UPC-SWITCH       PIC  X(01) VALUE 'N'.               
014100         88  PS-HAVE-INTERNAL                    VALUE 'Y'.               
014100         88  PS-NO-INTERNAL                      VALUE 'N'.               
014200                                                                          
013800*****************************************************************         
013800*    PROGRAM VARIABLES                                                    
013800*****************************************************************         
008000 01  PV-VARIABLES.                                                        
013700     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
006800                                                 COMP SYNC.               
006700     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
006800                                                 COMP SYNC.               
011000     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
010800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-1             PIC  X(20) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-2             PIC  X(20) VALUE SPACES.            
010900     05  PV-ABEND-TABLE-3             PIC  X(20) VALUE SPACES.            
019710     05  PV-CURRENT-TIMESTAMP         PIC  X(26) VALUE SPACES.            
019720     05  PV-COMMIT-TIMESTAMP          PIC  X(26) VALUE SPACES.            
010900     05  PV-ERROR-MSG                 PIC  X(60) VALUE SPACES.            
009700     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
009700     05  PV-UPC-NBR-X.                                                    
009700         10  PV-UPC-NBR               PIC  9(15) VALUE ZERO.              
009700     05  PV-ENT-ID-X.                                                     
009700         10  PV-ENT-ID                PIC  9(09) VALUE ZERO.              
009700     05  PV-SKU-NBR-X.                                                    
009700         10  PV-SKU-NBR               PIC  9(08) VALUE ZERO.              
009700     05  PV-STYL-ID-X.                                                    
009700         10  PV-STYL-ID               PIC  9(08) VALUE ZERO.              
009700     05  PV-SIZE-CDE-X.                                                   
009700         10  PV-SIZE-CDE              PIC  9(09) VALUE ZERO.              
009700     05  PV-DEPT-NBR-X.                                                   
009700         10  PV-DEPT-NBR              PIC  9(04) VALUE ZERO.              
009700     05  PV-MAJ-CL-NBR-X.                                                 
009700         10  PV-MAJ-CL-NBR            PIC  9(04) VALUE ZERO.              
009700     05  PV-SUB-CL-NBR-X.                                                 
009700         10  PV-SUB-CL-NBR            PIC  9(04) VALUE ZERO.              
009000     05  PV-RUN-DATE-CN               PIC  X(08) VALUE SPACES.            
010100     05  PV-RUN-TIME-CN               PIC  X(06) VALUE SPACES.            
010500***** FILE NAME CONSISTS OF RUN-DATE-CN_ITEM.DAT                          
010600*****     EXAMPLE: 20050317_ITEM.DAT                                      
010700     05  PV-FILE-NAME.                                                    
010800         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
010900         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
011000         10  PV-ITEM-DAT              PIC  X(08) VALUE SPACES.            
010100     05  PV-PREV-SKU                  PIC  9(08) VALUE ZEROS.             
010100     05  PV-INTERNAL-UPC-CNT          PIC  9(03) VALUE ZEROS.             
010100     05  PV-HOLD-INTERNAL-REC.                                            
DH             10  FILLER                   PIC  X(270) VALUE SPACES.           
010100         10  PV-HOLD-PRIM-UPC-FL      PIC  X(001) VALUE SPACES.           
DH             10  FILLER                   PIC  X(001) VALUE SPACES.           
VML            10  FILLER                   PIC  X(006) VALUE SPACES.           
011100                                                                          
014600                                                                          
015400***  MARKER FILE LAYOUT                                                   
014700     COPY VLRD029.                                                        
014800                                                                          
015400***  ITEM FILE LAYOUT                                                     
014700     COPY VLRD033.                                                        
014800                                                                          
015400***  DATE ROUTINE COPYBOOK                                                
014700     COPY DPWS500.                                                        
014800                                                                          
015400***  DB2 ABEND COPYBOOK                                                   
014700     COPY DPWS004.                                                        
014800                                                                          
014900     EXEC SQL                                                             
015000         INCLUDE SQLCA                                                    
015100     END-EXEC.                                                            
015200                                                                          
015300     COPY SQLCA2.                                                         
015400                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR VENDOR LOGISTICS DOMAIN SKU TABLE               *         
034500*    (XLT_SKU)                                                  *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE XLT00003                                                 
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR VENDOR LOGISTICS DOMAIN VENDOR STYLE TABLE      *         
034500*    (XLT_VND_STYL)                                             *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE XLT00004                                                 
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR VENDOR LOGISTICS DOMAIN UPC TABLE               *         
034500*    (XLT_UPC)                                                  *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE XLT00006                                                 
016500     END-EXEC.                                                            
016600                                                                          
034300*---------------------------------------------------------------*         
034400*    DCLGEN FOR TUPC TABLE                                      *         
034500*    (TUPC)                                                     *         
034600*---------------------------------------------------------------*         
016300     EXEC SQL                                                             
016400         INCLUDE TUPC                                                     
016500     END-EXEC.                                                            
016600                                                                          
014310******************************************************************        
014320** COBOL DECLARATION AND DCLGEN MEMBER FOR THE CHECKPOINT       **        
014321** RESTART CONTROL TABLE.  THIS PROGRAM IS NOT WRITTEN USING    **        
014322** THE CHECKPOINT RESTART MODULE.  WE ARE USING THIS TABLE      **        
014323** TO DYNAMICALLY DETERMINE HOW OFTEN COMMITS ARE TO BE TAKEN.  **        
014330******************************************************************        
014340     EXEC SQL                                                             
014350          INCLUDE TCKRSTCN                                                
014360     END-EXEC.                                                            
016600                                                                          
016600                                                                          
016600***  EXTRACT CURSOR                                                       
016600     EXEC SQL                                                             
016600         DECLARE EXTRACT-CSR CURSOR WITH HOLD FOR                         
016600         SELECT                                                           
                      A.INTR_UPC_ID                                             
                    , A.STYL_ID                                                 
                    , A.SKU_NBR                                                 
                    , A.SKU_STAT_CDE                                            
                    , A.SKU_STAT_DESC                                           
                    , A.KOHLS_SIZE_CDE                                          
                    , A.SIZE_DESC                                               
                    , A.NRF_CLOR_CDE                                            
                    , A.CSTM_CLOR_DESC                                          
                    , B.UPC_TYP_CDE                                             
                    , B.EDI_ORD_IND                                             
                    , B.UPC_NBR                                                 
                    , B.PREPK_IND                                               
                    , C.DEPT_NBR                                                
                    , C.MAJ_CL_NBR                                              
                    , C.SUB_CL_NBR                                              
                    , C.VS_NBR                                                  
                    , C.ENT_ID                                                  
                    , C.VS_DESC                                                 
                    , E.UNT_COST_1ST_AMT                                        
016600           FROM                                                           
                      XLT_SKU      A                                            
                    , XLT_UPC      B                                            
                    , XLT_VND_STYL C                                            
                    , TUPC         D                                            
                    , TUPC         E                                            
                 WHERE                                                          
                      A.INTR_UPC_ID = B.INTR_UPC_ID                             
                  AND A.INTR_UPC_ID = D.INTR_UPC_ID                             
                  AND B.UPC_NBR     = D.UPC_NBR                                 
                  AND D.EFF_END_DTE IS NULL                                     
                  AND E.UPC_ID      = D.INTR_UPC_ID                             
                  AND E.EFF_END_DTE IS NULL                                     
                  AND A.STYL_ID     = C.STYL_ID                                 
              ORDER BY                                                          
                      A.SKU_NBR                                                 
                    , B.UPC_NBR                                                 
              WITH UR                                                           
016600     END-EXEC.                                                            
016600                                                                          
016700 LINKAGE SECTION.                                                         
016800                                                                          
016900 PROCEDURE DIVISION.                                                      
017000*****************************************************************         
017200*  MAINLINE LOGIC FOR PROGRAM                                             
017000*****************************************************************         
017100 0000-MAINLINE.                                                           
017500     PERFORM 1000-INITIALIZE.                                             
017600                                                                          
017700     PERFORM 2000-PROCESS                                                 
017800         UNTIL PS-END-CSR.                                                
017900                                                                          
018100     PERFORM 9000-EOJ.                                                    
018200                                                                          
018200     STOP RUN.                                                            
018200                                                                          
017000*****************************************************************         
017200*  INITIALIZATION PARAGRAPH                                               
017000*****************************************************************         
018600 1000-INITIALIZE.                                                         
020200     OPEN OUTPUT VL-ITEM-FILE                                             
JB                     VL-MARKER-FILE.                                          
019500                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME-CN.                   
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME-CN.                     
019500                                                                          
033710     PERFORM 1200-FETCH-COMMIT-TIMESTAMP.                                 
033500                                                                          
020300     PERFORM 7000-OPEN-EXTRACT-CURSOR.                                    
020400     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
022400     MOVE XLT00003-SKU-NBR TO PV-PREV-SKU.                                
022400                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1100-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT033 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
000169                                                                          
038700******************************************************************        
038800*  FETCH THE TIMESTAMP THAT WILL BE USED TO DETERMINE WHEN A              
038900*  COMMIT IS TAKEN.  THE TIME INTERVAL IS DETERMINED BY A VALUE           
039000*  ON THE TCKRSTCNTL TABLE.                                               
039100******************************************************************        
039200 1200-FETCH-COMMIT-TIMESTAMP.                                             
039210                                                                          
000971     PERFORM                                                              
000972         WITH TEST AFTER                                                  
000973         VARYING PV-RETRY-COUNT                                           
000974         FROM 1 BY 1                                                      
000975         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
000976                   OR (SQLCODE = ZERO))                                   
039210                                                                          
039300         EXEC SQL                                                         
039400             SELECT (CURRENT TIMESTAMP +                                  
                           CKPT_FRQNCY_QTY SECONDS)                             
039500               INTO  :PV-COMMIT-TIMESTAMP                                 
039600               FROM  TCKRSTCNTL                                           
039700              WHERE  PLAN_NAME = :PC-CURRENT-PGM-VLKUT033                 
039710         END-EXEC                                                         
039800                                                                          
039810         EVALUATE TRUE                                                    
000982             WHEN SQLCODE = +0                                            
000983             WHEN SQLCODE = -904                                          
000984             WHEN SQLCODE = -911                                          
000985             WHEN SQLCODE = -913                                          
039830                 CONTINUE                                                 
039870             WHEN SQLWARN0 NOT = SPACES                                   
039880             WHEN SQLCODE < ZERO                                          
039890             WHEN SQLCODE > ZERO                                          
001000                 DISPLAY '** ABEND **'                                    
001010                 DISPLAY '** PROGRAM = VLKUT033 **'                       
001011                 DISPLAY '** PARAGRAPH = '                                
                               '1200-FETCH-COMMIT-TIMESTAMP'                    
001012                 MOVE 'FETCH COMMIT TIMESTAMP'                            
001013                                 TO PV-ERROR-MSG                          
001014                 MOVE 'TCKRSTCNTL'                                        
                                       TO PV-ABEND-TABLE-1                      
001015                 PERFORM 9200-SQL-ERROR                                   
039897         END-EVALUATE                                                     
000997     END-PERFORM.                                                         
000998                                                                          
000999     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
001000         DISPLAY '** ABEND **'                                            
001010         DISPLAY '** PROGRAM = VLKUT033 **'                               
001011         DISPLAY '** PARAGRAPH =  1200-FETCH-COMMIT-TIMESTAMP **'         
001012         MOVE 'FETCH COMMIT TIMESTAMP RETRIES ENCOUNTERED'                
001013                                      TO PV-ERROR-MSG                     
001014         MOVE 'TCKRSTCNTL'            TO PV-ABEND-TABLE-1                 
001015         PERFORM 9200-SQL-ERROR                                           
001016     END-IF.                                                              
041600                                                                          
017000*****************************************************************         
017200* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD                        
017000*****************************************************************         
024200 2000-PROCESS.                                                            
002800     MOVE 'A'                     TO VL033-ACD-INDICATOR.                 
000011     MOVE XLT00003-INTR-UPC-ID    TO VL033-INTR-UPC-ID.                   
000011     MOVE XLT00003-SKU-NBR        TO PV-SKU-NBR.                          
000011     MOVE PV-SKU-NBR-X            TO VL033-SKU-NBR.                       
000012     MOVE XLT00006-UPC-NBR        TO PV-UPC-NBR.                          
000012     MOVE PV-UPC-NBR-X            TO VL033-UPC-NBR.                       
000012     MOVE XLT00003-STYL-ID        TO PV-STYL-ID.                          
000012     MOVE PV-STYL-ID-X            TO VL033-STYL-ID.                       
000013     MOVE XLT00004-VS-DESC        TO VL033-STYL-DESC.                     
000013     MOVE XLT00003-NRF-CLOR-CDE   TO VL033-NRF-CLOR-CDE.                  
000013     MOVE XLT00003-CSTM-CLOR-DESC TO VL033-CSTM-CLOR-DESC.                
000013     MOVE XLT00003-KOHLS-SIZE-CDE TO PV-SIZE-CDE.                         
000013     MOVE PV-SIZE-CDE-X           TO VL033-KOHLS-SIZE-CDE.                
000013     MOVE XLT00003-SIZE-DESC      TO VL033-SIZE-DESC.                     
000013     MOVE UPC-UNT-COST-1ST-AMT    TO VL033-UNT-COST-1ST-AMT.              
022400                                                                          
017000*** IF WE HAVE A 13-DIGIT UPC, WE NEED TO SET THE UPC TYPE TO             
017000*** 'E' FOR EAN TYPE UPC.  OTHERWISE, IT IS BEING SET TO 'U'.             
000014     IF  XLT00006-UPC-NBR > 999999999999                                  
000014         MOVE 'E'                 TO VL033-UPC-TYPE                       
000014     ELSE                                                                 
000014         MOVE 'U'                 TO VL033-UPC-TYPE                       
000014     END-IF.                                                              
000014     MOVE XLT00006-PREPK-IND      TO VL033-PREPK-IND.                     
000014     MOVE XLT00004-VS-NBR         TO VL033-VS-NBR.                        
000014     MOVE SPACES                  TO VL033-BUY-UM                         
000014                                     VL033-SELL-UM                        
000014                                     VL033-SKU-LEAD-TIME.                 
000014     MOVE 'A'                     TO VL033-STATUS-FLAG.                   
000014     MOVE SPACES                  TO VL033-PRO-NMFC.                      
000014     MOVE XLT00004-DEPT-NBR       TO PV-DEPT-NBR.                         
000014     MOVE PV-DEPT-NBR-X           TO VL033-DEPT-NBR.                      
000014     MOVE XLT00004-MAJ-CL-NBR     TO PV-MAJ-CL-NBR.                       
000014     MOVE PV-MAJ-CL-NBR-X         TO VL033-MAJ-CL-NBR.                    
000014     MOVE XLT00004-SUB-CL-NBR     TO PV-SUB-CL-NBR.                       
000014     MOVE PV-SUB-CL-NBR-X         TO VL033-SUB-CL-NBR.                    
000014     MOVE XLT00003-SKU-STAT-CDE   TO VL033-SKU-STAT-CDE.                  
000014     MOVE XLT00004-ENT-ID         TO PV-ENT-ID.                           
000014     MOVE PV-ENT-ID-X             TO VL033-ENT-ID.                        
000014     MOVE XLT00003-SKU-STAT-DESC  TO VL033-SKU-STAT-DESC.                 
000014     MOVE XLT00006-EDI-ORD-IND    TO VL033-PRIMARY-UPC-FL.                
002800     MOVE 'X'                     TO VL033-ANCHOR-CONSTANT.               
002800                                                                          
002800***** AN INDICATOR NEEDS TO BE SENT TO RCMS TO SHOW WHICH UPC IS          
002800***** THE PRIMARY UPC. KOHLS CAN CHANGE OR DELETE A UPC AT ANY            
002800***** TIME.  THIS CAN RESULT IN A UPC NOT BEING FOUND WHEN                
002800***** TRYING TO CREATE A VIOLATION IN RCMS FOR A PARTICULAR               
002800***** SKU/UPC,RCMS WILL CREATE A POST-PROCESS TO UPDATE ANY ACTIVE        
002800***** PO'S WHERE THE PRIMARY UPC HAS CHANGED.                             
002800***** BEFORE WRITING OUT A RECORD WE NEED TO DETERMINE                    
002800***** WHAT TYPE OF UPC IT IS AND IF THE EDI_IND IS 'Y' OR 'N'. IF         
002800***** WE GET AN INTERNAL UPC BEFORE A VENDOR ONE THAT'S PRIMARY           
002800***** WE HOLD THE INTERNAL REC IN A HOLD AREA UNTIL WE GET A              
002800***** DIFFERENT SKU.  IF THE PV-EDI-ORD-SWITCH HAS NOT BEEN SET           
002800***** TO 'Y', A 'Y' IS MOVED INTO THE HELD INTERNAL RECORD BEFORE         
002800***** IT IS WRITTEN OUT.  THERE ARE VARIOUS SITUATIONS POSSIBLE:          
002800*****                                                                     
002800***** 1) ONLY AN INTERNAL UPC WHICH MAY OR MAY NOT HAVE THE               
002800*****    EDI-ORD-IND SET 'Y'                                              
002800***** 2) A VENDOR & AN INTERNAL UPC & THE VENDOR IS SET TO 'Y'            
002800***** 3) A VENDOR & AN INTERNAL UPC & THE INTERNAL IS SET TO 'Y'          
002800***** 4) A VENDOR & INTERNAL UPC & NEITHER IS SET TO 'Y'                  
002800***** 5) A VENDOR, INTERNAL AND A RENUMBERED SKU.  THE RENUMBERED         
002800*****    SKU SHOULD NEVER BE SET TO BE A PRIMARY UPC. THE PRIMARY         
002800*****    UPC MUST EITHER BE A VENDOR OR INTERNAL ONE. THERE CAN           
002800*****    ONLY BE 1 UPC MARKED AS PRIMARY.                                 
002800                                                                          
002800     IF XLT00003-SKU-NBR = PV-PREV-SKU AND PS-NO-EDI                      
002800         PERFORM 2050-CHK-EDI-UPC                                         
002800     ELSE                                                                 
002800         IF XLT00003-SKU-NBR = PV-PREV-SKU AND PS-HAVE-EDI                
002800             PERFORM 8000-WRITE-OUTPUT                                    
002800         END-IF                                                           
002800     END-IF.                                                              
002800                                                                          
002800     IF XLT00003-SKU-NBR NOT = PV-PREV-SKU                                
002800         MOVE XLT00003-SKU-NBR TO PV-PREV-SKU                             
002800         IF PV-INTERNAL-UPC-CNT > 0                                       
002800             PERFORM 2075-INTERNAL-REC                                    
002800         END-IF                                                           
002800         SET        PS-NO-EDI                                             
002800                    PS-NO-INTERNAL  TO TRUE                               
002800         INITIALIZE PV-INTERNAL-UPC-CNT                                   
002800                    PV-HOLD-INTERNAL-REC                                  
002800         PERFORM 2050-CHK-EDI-UPC                                         
002800     END-IF.                                                              
002800                                                                          
026000                                                                          
043910     EXEC SQL                                                             
043920         SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                    
043930     END-EXEC.                                                            
043940*                                                                         
043950     IF  SQLCODE = +0                                                     
043960         IF  PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                   
043970             PERFORM 7600-COMMIT-AND-RELEASE-LOCKS                        
043980             PERFORM 1200-FETCH-COMMIT-TIMESTAMP                          
043990         END-IF                                                           
043991     ELSE                                                                 
000989         DISPLAY '** ABEND **'                                            
000990         DISPLAY '** PROGRAM = VLKUT033 **'                               
000991         DISPLAY '** PARAGRAPH =  2000-PROCESS **'                        
000993         MOVE 'ERROR FETCHING CURRENT TIMESTAMP'                          
                                       TO PV-ERROR-MSG                          
000994         MOVE SPACES             TO PV-ABEND-TABLE-1                      
000995         PERFORM 9200-SQL-ERROR                                           
043998     END-IF.                                                              
043999                                                                          
026200     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
021700                                                                          
017000*****************************************************************         
017200* CHECK THE EDI INDICATOR AND THE UPC TYPE                                
017000*****************************************************************         
024200 2050-CHK-EDI-UPC.                                                        
041700     IF XLT00006-UPC-TYP-CDE = 'V'                                        
041700         IF XLT00006-EDI-ORD-IND = 'Y'                                    
041700             SET PS-HAVE-EDI TO TRUE                                      
041700         ELSE                                                             
041700             IF PS-HAVE-EDI                                               
041700                 CONTINUE                                                 
041700             ELSE                                                         
041700                 SET PS-NO-EDI TO TRUE                                    
041700             END-IF                                                       
041700         END-IF                                                           
041700         PERFORM 8000-WRITE-OUTPUT                                        
041700     ELSE                                                                 
041700***********INTERNAL UPC                                                   
041700         IF XLT00006-UPC-TYP-CDE = 'I'                                    
041700             IF PV-INTERNAL-UPC-CNT > 0                                   
041700                 CONTINUE                                                 
041700             ELSE                                                         
041700                ADD 1 TO PV-INTERNAL-UPC-CNT                              
041700                SET PS-HAVE-INTERNAL TO TRUE                              
041700                MOVE VL033-ITEM-RECORD TO PV-HOLD-INTERNAL-REC            
041700             END-IF                                                       
041700             IF XLT00006-EDI-ORD-IND = 'Y'                                
041700                 SET PS-HAVE-EDI TO TRUE                                  
041700             ELSE                                                         
041700                 IF PS-HAVE-EDI                                           
041700                     CONTINUE                                             
041700                 ELSE                                                     
041700                     SET PS-NO-EDI TO TRUE                                
041700                 END-IF                                                   
041700             END-IF                                                       
041700         ELSE                                                             
041700***********THIS IS A RENUMBERED SKU, JUST WRITE IT OUT                    
041700             IF XLT00006-UPC-TYP-CDE = 'R'                                
041700                 PERFORM 8000-WRITE-OUTPUT                                
041700             END-IF                                                       
041700         END-IF                                                           
041700     END-IF.                                                              
041700                                                                          
021700                                                                          
017000*****************************************************************         
017200* HAVE INTERNAL UPC RECORD TO WRITE                                       
017000*****************************************************************         
024200 2075-INTERNAL-REC.                                                       
041700     IF PS-HAVE-INTERNAL AND PS-NO-EDI                                    
041700         MOVE 'Y' TO PV-HOLD-PRIM-UPC-FL                                  
041700         PERFORM 8100-WRITE-INTERNAL-REC                                  
041700     ELSE                                                                 
041700         PERFORM 8100-WRITE-INTERNAL-REC                                  
041700     END-IF.                                                              
042800                                                                          
026400******************************************************************        
026600* OPEN CURSOR                                                             
026700******************************************************************        
060600 7000-OPEN-EXTRACT-CURSOR.                                                
061000                                                                          
000971     PERFORM                                                              
000972         WITH TEST AFTER                                                  
000973         VARYING PV-RETRY-COUNT                                           
000974         FROM 1 BY 1                                                      
000975         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
000976                   OR (SQLCODE = ZERO))                                   
060700         EXEC SQL                                                         
060800              OPEN EXTRACT-CSR                                            
060900         END-EXEC                                                         
061000                                                                          
061100         EVALUATE TRUE                                                    
061300             WHEN SQLCODE = +0                                            
001036             WHEN SQLCODE = -904                                          
001037             WHEN SQLCODE = -911                                          
001038             WHEN SQLCODE = -913                                          
061300                 CONTINUE                                                 
061200             WHEN SQLWARN0 NOT EQUAL SPACE                                
061300             WHEN SQLCODE NOT EQUAL ZERO                                  
061400                 DISPLAY '** ABEND **'                                    
061500                 DISPLAY '** PROGRAM = VLKUT033 **'                       
061600                 DISPLAY '** PARAGRAPH = '                                
                               '7000-OPEN-EXTRACT-CURSOR **'                    
118726                 MOVE 'OPEN CURSOR'   TO PV-ERROR-MSG                     
118726                 MOVE 'XLT_SKU'       TO PV-ABEND-TABLE-1                 
118726                 MOVE 'XLT_UPC'       TO PV-ABEND-TABLE-2                 
118726                 MOVE 'XLT_VND_STYL'  TO PV-ABEND-TABLE-3                 
061700                 PERFORM 9200-SQL-ERROR                                   
061800         END-EVALUATE                                                     
000997     END-PERFORM.                                                         
000998                                                                          
000999     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
061400         DISPLAY '** ABEND **'                                            
061500         DISPLAY '** PROGRAM = VLKUT033 **'                               
061600         DISPLAY '** PARAGRAPH = 7000-OPEN-EXTRACT-CURSOR **'             
118726         MOVE 'OPEN CURSOR'           TO PV-ERROR-MSG                     
118726         MOVE 'XLT_SKU'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'XLT_UPC'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'XLT_VND_STYL'          TO PV-ABEND-TABLE-3                 
061700         PERFORM 9200-SQL-ERROR                                           
001016     END-IF.                                                              
001017                                                                          
061900                                                                          
062000******************************************************************        
062100* CLOSE CURSOR                                                            
062200******************************************************************        
062300 7100-CLOSE-EXTRACT-CURSOR.                                               
001023                                                                          
001024     PERFORM                                                              
001025         WITH TEST AFTER                                                  
001026         VARYING PV-RETRY-COUNT                                           
001027         FROM 1 BY 1                                                      
001028         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
001029                   OR (SQLCODE = ZERO))                                   
062400         EXEC SQL                                                         
062500             CLOSE EXTRACT-CSR                                            
062600         END-EXEC                                                         
062700                                                                          
062800         EVALUATE TRUE                                                    
061300             WHEN SQLCODE = +0                                            
001036             WHEN SQLCODE = -904                                          
001037             WHEN SQLCODE = -911                                          
001038             WHEN SQLCODE = -913                                          
061300                 CONTINUE                                                 
062900             WHEN SQLWARN0 NOT EQUAL SPACE                                
063000             WHEN SQLCODE NOT EQUAL ZERO                                  
063100             DISPLAY '** ABEND **'                                        
063200             DISPLAY '** PROGRAM = VLKUT033 **'                           
063300             DISPLAY '** PARAGRAPH = '                                    
                           '7100-CLOSE-EXTRACT-CURSOR**'                        
118726             MOVE 'CLOSE CURSOR'      TO PV-ERROR-MSG                     
118726             MOVE 'XLT_SKU'           TO PV-ABEND-TABLE-1                 
118726             MOVE 'XLT_UPC'           TO PV-ABEND-TABLE-2                 
118726             MOVE 'XLT_VND_STYL'      TO PV-ABEND-TABLE-3                 
063400             PERFORM 9200-SQL-ERROR                                       
063500         END-EVALUATE                                                     
001050     END-PERFORM.                                                         
001051                                                                          
001052     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
063100         DISPLAY '** ABEND **'                                            
063200         DISPLAY '** PROGRAM = VLKUT033 **'                               
063300         DISPLAY '** PARAGRAPH = 7100-CLOSE-EXTRACT-CURSOR**'             
118726         MOVE 'CLOSE CURSOR'          TO PV-ERROR-MSG                     
118726         MOVE 'XLT_SKU'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'XLT_UPC'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'XLT_VND_STYL'          TO PV-ABEND-TABLE-3                 
063400         PERFORM 9200-SQL-ERROR                                           
001072     END-IF.                                                              
001073                                                                          
063600                                                                          
063700******************************************************************        
063800*  FETCH CURSOR                                                           
063900******************************************************************        
064000 7200-FETCH-EXTRACT-CURSOR.                                               
063600                                                                          
001078     PERFORM                                                              
001079         WITH TEST AFTER                                                  
001080         VARYING PV-RETRY-COUNT                                           
001081         FROM 1 BY 1                                                      
001082         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
001083                   OR (SQLCODE = ZERO) OR (SQLCODE = +100))               
064100         EXEC SQL                                                         
064200              FETCH EXTRACT-CSR                                           
064400              INTO                                                        
                          :XLT00003-INTR-UPC-ID                                 
                        , :XLT00003-STYL-ID                                     
                        , :XLT00003-SKU-NBR                                     
                        , :XLT00003-SKU-STAT-CDE                                
                        , :XLT00003-SKU-STAT-DESC                               
                        , :XLT00003-KOHLS-SIZE-CDE                              
                        , :XLT00003-SIZE-DESC                                   
                        , :XLT00003-NRF-CLOR-CDE                                
                        , :XLT00003-CSTM-CLOR-DESC                              
                        , :XLT00006-UPC-TYP-CDE                                 
                        , :XLT00006-EDI-ORD-IND                                 
                        , :XLT00006-UPC-NBR                                     
                        , :XLT00006-PREPK-IND                                   
                        , :XLT00004-DEPT-NBR                                    
                        , :XLT00004-MAJ-CL-NBR                                  
                        , :XLT00004-SUB-CL-NBR                                  
                        , :XLT00004-VS-NBR                                      
                        , :XLT00004-ENT-ID                                      
                        , :XLT00004-VS-DESC                                     
                        , :UPC-UNT-COST-1ST-AMT                                 
065100         END-EXEC                                                         
065200                                                                          
065300         EVALUATE TRUE                                                    
065400             WHEN SQLCODE = ZERO                                          
001036             WHEN SQLCODE = -904                                          
001037             WHEN SQLCODE = -911                                          
001038             WHEN SQLCODE = -913                                          
065500                 CONTINUE                                                 
065600             WHEN SQLCODE = +100                                          
065700                 SET PS-END-CSR       TO TRUE                             
065800             WHEN SQLWARN0 NOT EQUAL SPACE                                
065900             WHEN SQLCODE NOT EQUAL ZERO                                  
066000                 DISPLAY '** ABEND **'                                    
066100                 DISPLAY '** PROGRAM = VLKUT033 **'                       
066200                 DISPLAY '** PARAGRAPH = '                                
066210                         '7200-FETCH-EXTRACT-CURSOR**'                    
066220                 MOVE 'FETCH CURSOR'  TO PV-ERROR-MSG                     
066230                 DISPLAY '** SKU NUMBER: ' XLT00003-SKU-NBR               
066240                         '   UPC NUMBER: ' XLT00006-UPC-NBR               
066250                 DISPLAY '** RECORDS WRITTEN: ' PV-RECS-WRITTEN           
066260                 MOVE 'XLT_SKU'       TO PV-ABEND-TABLE-1                 
066270                 MOVE 'XLT_UPC'       TO PV-ABEND-TABLE-2                 
066280                 MOVE 'XLT_VND_STYL'  TO PV-ABEND-TABLE-3                 
066300                 PERFORM 9200-SQL-ERROR                                   
066400         END-EVALUATE                                                     
001161     END-PERFORM.                                                         
001163                                                                          
001164     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
066000         DISPLAY '** ABEND **'                                            
066100         DISPLAY '** PROGRAM = VLKUT033 **'                               
066200         DISPLAY '** PARAGRAPH = 7200-FETCH-EXTRACT-CURSOR**'             
118726         MOVE 'FETCH CURSOR MAX RETRIES' TO PV-ERROR-MSG                  
066230         DISPLAY '** SKU NUMBER: ' XLT00003-SKU-NBR                       
066240                 '   UPC NUMBER: ' XLT00006-UPC-NBR                       
066250         DISPLAY '** RECORDS WRITTEN: ' PV-RECS-WRITTEN                   
118726         MOVE 'XLT_SKU'               TO PV-ABEND-TABLE-1                 
118726         MOVE 'XLT_UPC'               TO PV-ABEND-TABLE-2                 
118726         MOVE 'XLT_VND_STYL'          TO PV-ABEND-TABLE-3                 
066300         PERFORM 9200-SQL-ERROR                                           
001176     END-IF.                                                              
001177                                                                          
066500                                                                          
044534******************************************************************        
044535*  ISSUE A DB2 COMMIT TO RELEASE THE READ LOCKS BEING HELD ON THE         
044535*  TABLES.                                                                
044536******************************************************************        
044537 7600-COMMIT-AND-RELEASE-LOCKS.                                           
044538                                                                          
044540     EXEC SQL                                                             
044550          COMMIT                                                          
044564     END-EXEC.                                                            
044565                                                                          
044566     EVALUATE TRUE                                                        
044567         WHEN SQLCODE = ZERO                                              
044569             CONTINUE                                                     
044580         WHEN SQLWARN0 NOT = SPACES                                       
044581         WHEN SQLCODE < ZERO                                              
044582         WHEN SQLCODE > ZERO                                              
001103             DISPLAY '** ABEND **'                                        
001110             DISPLAY '** PROGRAM = VLKUT033 **'                           
001120             DISPLAY '** PARAGRAPH = '                                    
001121                     '7600-COMMIT-AND-RELEASE-LOCKS **'                   
001130             MOVE 'FETCH CURSOR'      TO PV-ERROR-MSG                     
118726             MOVE 'XLT_SKU'           TO PV-ABEND-TABLE-1                 
118726             MOVE 'XLT_UPC'           TO PV-ABEND-TABLE-2                 
118726             MOVE 'XLT_VND_STYL'      TO PV-ABEND-TABLE-3                 
001150             PERFORM 9200-SQL-ERROR                                       
044590     END-EVALUATE.                                                        
044591                                                                          
044592                                                                          
051200*****************************************************************         
051200*  WRITE OUTPUT FILE                                                      
051200*****************************************************************         
050800 8000-WRITE-OUTPUT.                                                       
050900     WRITE VL-ITEM-RECORD FROM VL033-ITEM-RECORD.                         
051000                                                                          
051100     ADD 1                TO PV-RECS-WRITTEN.                             
002800                                                                          
002800                                                                          
051200*****************************************************************         
051200*  WRITE INTERNAL UPC RECORD                                              
051200*****************************************************************         
050800 8100-WRITE-INTERNAL-REC.                                                 
050900     WRITE VL-ITEM-RECORD FROM PV-HOLD-INTERNAL-REC.                      
051000                                                                          
051100     ADD 1                TO PV-RECS-WRITTEN.                             
002800                                                                          
002800                                                                          
035800*****************************************************************         
035900*  CREATE MARKER FILE                                                     
036000*****************************************************************         
036100 8500-CREATE-MARKER.                                                      
036200     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
036300     MOVE 'ITEM.DAT'      TO PV-ITEM-DAT.                                 
036400     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
036500     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
036600     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
036700     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
036800     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
036900                                                                          
037000     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
037100                                                                          
002800                                                                          
051200*****************************************************************         
051200*  END OF JOB PROCESSING                                                  
051200*****************************************************************         
051400 9000-EOJ.                                                                
020300     IF PV-INTERNAL-UPC-CNT > 0                                           
020300         PERFORM 2075-INTERNAL-REC                                        
020300     ELSE                                                                 
020300         PERFORM 8000-WRITE-OUTPUT                                        
020300     END-IF.                                                              
051800                                                                          
020300     PERFORM 7100-CLOSE-EXTRACT-CURSOR.                                   
051800                                                                          
051800     DISPLAY 'RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.                    
051800                                                                          
020300     PERFORM 8500-CREATE-MARKER.                                          
051800                                                                          
051600     CLOSE VL-ITEM-FILE                                                   
051600           VL-MARKER-FILE.                                                
002800                                                                          
052500                                                                          
051200*****************************************************************         
051200*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
051200*****************************************************************         
052600 9200-SQL-ERROR.                                                          
052700     CLOSE VL-ITEM-FILE                                                   
JB               VL-MARKER-FILE.                                                
052900                                                                          
053000     MOVE SQLCODE                     TO SQLCODE2.                        
053100     MOVE SQLERRD(3)                  TO SQLERRD3.                        
053200                                                                          
053300     DISPLAY '** ABEND **         ** = SQLERROR'.                         
053400     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT033.                
053500     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
053600     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-1.                       
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-2.                       
118726     DISPLAY '**           ** = ' PV-ABEND-TABLE-3.                       
053700     DISPLAY '**           ** = ' PV-ERROR-MSG                            
053800                                                                          
053900     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
054000     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
054100     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
054200     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
054300                                                                          
054400     COPY DPPD004.                                                        
054500                                                                          
054600     MOVE +4013                       TO PV-ABEND-CODE.                   
054700     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
054800                                                                          
054800*************  END OF VLKUT033  ********************************          
