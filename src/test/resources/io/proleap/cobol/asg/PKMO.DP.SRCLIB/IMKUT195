      ******************************************************************00010094
       IDENTIFICATION DIVISION.                                         00020094
      ******************************************************************00030094
      *                                                                 00040094
       PROGRAM-ID.    IMKUT195.                                         00050094
       AUTHOR.        D. THEEL.                                         00060094
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00070094
       DATE-WRITTEN.  APRIL, 2006                                       00080094
       DATE-COMPILED.                                                   00090094
      *                                                                 00100094
      ******************************************************************00110094
      *   THE INPUT FILE IS ALL SKUS THAT ARE ACTIVE SORTED BY INTERNAL*00120094
      *   UPC ID.  ALL SKUS THAT HAVE THE IM020-TWEBITM-IND = N WILL   *00130094
      *   BE WRITTEN TO THE OUTPUT1 FILE. THOSE WITH THE IM020-TWEBITM-*00140094
      *   IND = Y WILL BE CHECKED TO SEE TO SEE IF THE FOLLOWING ARE   *00150094
      *   TRUE:                                                        *00160094
      *   1. SKU IS IN CLEARANCE STATUS                                *00170094
      *   2. SKU HAS BEEN IN CLEARANCE STATUS FOR MORE THAN 1 YEAR     *00180094
      *   3. SKU IS NOT DIRECT SHIP                                    *00180095
      *   IF ALL THE ABOVE ARE TRUE THE RECORD WILL BE WRITTEN TO      *00180096
      *   OUTPUT 1, ELSE OUTPUT2.                                       00180097
      ******************************************************************00190094
      *  CHANGED:                                                      *00200094
      *                                                                *00210094
      *    WR/PITS#     DATE        DESCRIPTION                        *00220094
      *    --------   ----------   ---------------------------------   *00230094
      *    WR29925    04-10-2006    INITIAL IMPLEMENTATION.            *00250094
      ******************************************************************00272094
       ENVIRONMENT DIVISION.                                            00280094
      ******************************************************************00290094
      *                                                                 00300094
       CONFIGURATION SECTION.                                           00310094
       SOURCE-COMPUTER.    IBM-3090.                                    00320094
       OBJECT-COMPUTER.    IBM-3090.                                    00330094
      *                                                                 00340094
       INPUT-OUTPUT SECTION.                                            00350094
       FILE-CONTROL.                                                    00360094
      *                                                                 00370094
           SELECT STYLE-INPUT-FILE                                      00380094
               ASSIGN TO INP01.                                         00390094
      *                                                                 00420094
           SELECT STYLE-OUTPUT-FILE                                     00430094
               ASSIGN TO OUT01.                                         00440094
           SELECT INVALID-STYL-FILE                                     00450094
               ASSIGN TO OUT02.                                         00460094
      *                                                                 00470094
      ******************************************************************00480094
       DATA DIVISION.                                                   00490094
      ******************************************************************00500094
      *                                                                 00510094
       FILE SECTION.                                                    00520094
      *                                                                 00530094
       FD  STYLE-INPUT-FILE                                             00540094
           RECORDING MODE IS V                                          00550094
           BLOCK CONTAINS 0 RECORDS.                                    00560094
       01  SD-INPUT-RECORD         PIC X(40).                           00570094
      *                                                                 00580094
       FD  STYLE-OUTPUT-FILE                                            00640094
           RECORDING MODE IS F                                          00650094
           BLOCK CONTAINS 0 RECORDS.                                    00660094
       01  SD-OUTPUT-RECORD        PIC X(100).                          00670094
      *                                                                 00680094
       FD  INVALID-STYL-FILE                                            00690094
           RECORDING MODE IS F                                          00700094
           BLOCK CONTAINS 0 RECORDS.                                    00710094
       01  SD-INVALID-REC          PIC X(100).                          00720094
      *                                                                 00730094
       WORKING-STORAGE SECTION.                                         00740094
      ******************************************************************00750094
      *  INPUT/OUTPUT FILE RECORD LAYOUT                                00760094
      ******************************************************************00770094
           COPY IMRD020.                                                00780094
      ******************************************************************00790094
      *  OUTPUT FILE INVALID RECORD LAYOUT                              00800094
      ******************************************************************00810094
           COPY IMRD021.                                                00820094
      *                                                                 00890094
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00900094
                                                         COMP SYNC.     00910094
      *                                                                 00920094
       01  PA-PROGRAM-ACCUMULATORS.                                     01120094
           05  PA-WEBITM-FAILS             PIC S9(7)    VALUE ZERO      01141099
                                                        COMP-3.         01142099
           05  PA-WEBITM-PASSES            PIC S9(7)    VALUE ZERO      01142100
                                                        COMP-3.         01142101
           05  PA-WEBITM-COUNT             PIC S9(7)    VALUE ZERO      01142102
                                                        COMP-3.         01142103
           05  PA-WEBITM-CLR-CT            PIC S9(7)    VALUE ZERO      01142104
                                                        COMP-3.         01142105
           05  PA-WRITE-COUNT              PIC S9(7)    VALUE ZERO      01190094
                                                        COMP-3.         01200094
           05  PA-READ-COUNT               PIC 9(07)    VALUE ZERO      01210099
                                                        COMP-3.         01220099
           05  PA-COUNT                    PIC S9(7)    VALUE ZERO      01230094
                                                        COMP-3.         01240094
           05  PA-BYPASSED-SKU-COUNT       PIC S9(7)    VALUE ZERO      01250094
                                                        COMP-3.         01260094
           05  PA-WRITTEN-STYLE-COUNT      PIC S9(7)    VALUE ZERO      01270094
                                                        COMP-3.         01280094
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0         01390094
                                                        COMP SYNC.      01400094
      *                                                                 01410094
       01  PC-PROGRAM-CONSTANTS.                                        01420094
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          01430094
               'IMKUT195'.                                              01440094
           05  PC-MAX-RETRY-COUNT          PIC S9(04)    VALUE 3        01530094
                                                       COMP SYNC.       01540094
           05  PC-MORE-THAN-MAX-RETRY      PIC S9(04)    VALUE 4        01550094
                                                       COMP SYNC.       01560094
      *                                                                 01570094
       01  PS-PROGRAM-SWITCHES.                                         01580094
           05  PS-END-OF-INPUT-FILE-SW     PIC X(01)  VALUE 'N'.        01610094
               88  PS-END-OF-INPUT-FILE               VALUE 'Y'.        01620094
      *                                                                 01760094
                                                                        02010094
       01  PW-PROGRAM-WORKAREA.                                         02020094
           05  PW-PARA-NAME                PIC X(35).                   02030094
           05  PW-INTR-COMPTEN             PIC S9(10) COMP VALUE ZERO.  02040094
           05  PW-INTR-NUMERIC             PIC 9(10) VALUE ZERO.        02040095
      *                                                                 02100094
           COPY SQLCA2.                                                 02110094
      *                                                                 02120094
      ******************************************************************02130094
      *  TWEBITM                                                        02140094
      ******************************************************************02150094
      *                                                                 02160094
           EXEC SQL                                                     02170094
                INCLUDE TWEBITM                                         02180094
           END-EXEC.                                                    02190094
      *                                                                 02200094
      ******************************************************************02210094
      *  TUPCPLS                                                        02220094
      ******************************************************************02230094
      *                                                                 02240094
           EXEC SQL                                                     02250094
                INCLUDE TUPCPLS                                         02260094
           END-EXEC.                                                    02270094
      *                                                                 02280094
                                                                        02679599
      *                                                                 02680094
           EXEC SQL                                                     02690094
                INCLUDE SQLCA                                           02700094
           END-EXEC.                                                    02710094
                                                                        02720094
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
           EJECT                                                        02780094
      *                                                                 02790094
      ******************************************************************02800094
       PROCEDURE DIVISION.                                              02810094
      ******************************************************************02820094
      *                                                                 02830094
           EJECT                                                        02840094
       0000-IMKUT195.                                                   02850094
      ******************************************************************02860094
      * MAIN PROCESSING ROUTINE                                        *02870094
      ******************************************************************02880094
                                                                        02890094
           PERFORM 1000-HOUSEKEEPING.                                   02900094
      *                                                                 02910094
           PERFORM 2000-PROCESS-DELETIONS                               02920094
             UNTIL PS-END-OF-INPUT-FILE.                                02930094
      *                                                                 02940094
           PERFORM 1500-END-OF-JOB.                                     02950094
           GOBACK.                                                      02960094
      *                                                                 02970094
                                                                        02980094
       1000-HOUSEKEEPING.                                               02990094
                                                                        03000094
           MOVE '*1000-HOUSEKEEPING.              *' TO PW-PARA-NAME.   03010094
                                                                        03020094
           OPEN INPUT  STYLE-INPUT-FILE                                 03030094
                OUTPUT INVALID-STYL-FILE                                03050094
                       STYLE-OUTPUT-FILE.                               03060094
                                                                        03070094
           PERFORM 6000-READ-SKU-DELETES.                               03080094
           IF PS-END-OF-INPUT-FILE                                      03090094
               DISPLAY '********** IMKUT195 ***********'                03100094
               DISPLAY 'SKU DELETES INPUT FILE IS EMPTY'                03110094
               DISPLAY '*******************************'                03120094
           END-IF.                                                      03130094
                                                                        03190094
                                                                        03680094
       1500-END-OF-JOB.                                                 03690094
                                                                        03700094
      ******************************************************************03710094
      *  CLOSE FILES AND DISPLAY COUNTS                                *03720094
      ******************************************************************03730094
                                                                        03740094
           CLOSE STYLE-INPUT-FILE                                       03750094
                 INVALID-STYL-FILE                                      03770094
                 STYLE-OUTPUT-FILE.                                     03780094
                                                                        03790094
           DISPLAY '***************************'.                       03800094
           DISPLAY 'IMKUT195   STATS'                                   03810094
           DISPLAY '# OF RECS READ             : = ' PA-READ-COUNT.     03820094
           DISPLAY '# OF WEBITM SKUS           : = ' PA-WEBITM-COUNT.   03820095
           DISPLAY '# OF WEBITM CLEARANCE SKUS : = ' PA-WEBITM-CLR-CT.  03820096
           DISPLAY '# OF CLR WEBITM SQL PASSES : = ' PA-WEBITM-PASSES.  03830099
           DISPLAY '# OF CLR WEBITM SQL FAILS  : = ' PA-WEBITM-FAILS.   03840094
           DISPLAY '# OF INV RECS WRITTEN  : = ' PA-BYPASSED-SKU-COUNT. 03880094
           DISPLAY '# OF VALID RECS WRITTEN: = ' PA-WRITE-COUNT.        03890094
           DISPLAY '***************************'.                       03890095
                                                                        03900094
                                                                        03900095
      ******************************************************************03900096
      *  WRITE ALL SKUS THAT ARE NOT ON THE WEB TO THE OUTPUT1 FILE.   *03900097
      *  THIS FILE WILL CONTINUE ON THRU THE REST OF THE BATCH STYLE   *03900098
      *  DELETE EDIT CHECKS IN JOB IMK025.                             *03900099
      *  IF SKU IS ON THE WEB AND IN CLEARANCE STATUS - GO CHECK       *03900100
      *  AGAINST TWEBITM AND TUPCPLS TO SEE IF SKU HAS BEEN IN         *03900101
      *  CLEARANCE STATUS FOR MORE THAN ONE YEAR AND NOT A DIRECT SHIP *03900102
      *  SKU. IF SKU FAILS ABOVE CHECK, WRITE TO OUTPUT2 BYPASS FILE.  *03900110
      *  IF SKU PASSES ABOVE CHECK, WRITE TO OUTPUT1 FILE.             *03900111
      *  IF SKU IS ON THE WEB IN ANY STATUS OTHER THAN CLEARANCE,      *03900120
      *  WRITE TO OUTPUT2 BYPASS FILE.                                 *03900130
      ******************************************************************03900200
       2000-PROCESS-DELETIONS.                                          03910094
           MOVE '*2000-PROCESS-DELETIONS.         *' TO PW-PARA-NAME.   03920094
           IF IM020-TWEBITM-IND = 'N'                                   03920095
               PERFORM 8000-WRITE-SKU-DELETE-FILE                       03920097
           ELSE                                                         03920098
               ADD +1 TO PA-WEBITM-COUNT                                03920102
               IF IM020-SKU-STAT = '30'                                 03920103
                   ADD +1 TO PA-WEBITM-CLR-CT                           03920104
                   PERFORM 3000-CK-WEBITM                               03920105
               ELSE                                                     03920114
                   PERFORM 8200-WRITE-BYPASSED-SKU                      03920115
               END-IF                                                   03920120
           END-IF.                                                      03920500
                                                                        04310094
           PERFORM 6000-READ-SKU-DELETES.                               04320094
                                                                        04330094
       3000-CK-WEBITM.                                                  04330095
           MOVE '*3000-CK-WEBITM.                 *' TO PW-PARA-NAME.   04330096
      ******************************************************************04330097
      * SELECT FROM TWEBITM AND TUPCPLS TO SEE IF SKU HAS BEEN IN      *04330098
      *  CLEARANCE STATUS FOR MORE THAN ONE YEAR AND NOT A DIRECT      *04330099
      *  SHIP SKU.                                                     *04330100
      ******************************************************************04330101
           MOVE ZERO TO PA-COUNT.                                       04330104
           PERFORM VARYING PA-RETRY-COUNT FROM 1 BY 1                   04330106
             UNTIL PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                  04330107
             EXEC SQL                                                   04330110
                 SELECT 1                                               04330111
                   INTO :PA-COUNT                                       04330112
                 FROM TWEBITM A                                         04330113
                     ,TUPCPLS B                                         04330114
                 WHERE A.INTR_UPC_ID = :IM020-INTR-UPC-ID               04330115
                   AND A.SHIP_GP_SEQ_NBR NOT IN (13)                    04330116
                   AND A.EFF_BEG_TMST < CURRENT TIMESTAMP               04330117
                   AND (A.EFF_END_TMST IS NULL                          04330118
                      OR A.EFF_END_TMST > CURRENT TIMESTAMP)            04330119
                   AND B.UPC_ID = A.INTR_UPC_ID                         04330120
                   AND B.STR_NBR = SMALLINT(0)                          04330121
                   AND B.ORIG_CLR_DTE < (CURRENT DATE - 365 DAYS)       04330122
                   AND B.EFF_BEG_TMST < CURRENT TIMESTAMP               04330123
                   AND (B.EFF_END_TMST IS NULL                          04330124
                      OR B.EFF_END_TMST > CURRENT TIMESTAMP)            04330125
                 FETCH FIRST 1 ROWS ONLY                                04330126
             END-EXEC                                                   04330800
                                                                        04330900
             EVALUATE TRUE                                              04331000
                 WHEN SQLCODE = +0                                      04331100
                     ADD +1 TO PA-WEBITM-PASSES                         04331101
                     PERFORM 8000-WRITE-SKU-DELETE-FILE                 04331102
                     MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT      04331103
                 WHEN SQLCODE = +100                                    04331300
                     ADD +1 TO PA-WEBITM-FAILS                          04331310
                     PERFORM 8200-WRITE-BYPASSED-SKU                    04331320
                     MOVE PC-MORE-THAN-MAX-RETRY TO PA-RETRY-COUNT      04331330
                 WHEN SQLCODE = -911                                    04331800
                 WHEN SQLCODE = -913                                    04332200
                     CONTINUE                                           04332300
                 WHEN OTHER                                             04332400
                     PERFORM 9980-SQL-ERROR                             04332500
             END-EVALUATE                                               04332600
           END-PERFORM.                                                 04332700
                                                                        06750094
      *                                                                 06760094
       6000-READ-SKU-DELETES.                                           06770094
           MOVE '*6000-READ-SKU-DELETES.          *' TO PW-PARA-NAME.   06780094
      ******************************************************************06790094
      * READ SKU DELETES FILE.                                         *06800094
      ******************************************************************06810094
           READ STYLE-INPUT-FILE INTO IM020-STYLE-DELETE-REC            06820094
               AT END                                                   06830094
                      SET PS-END-OF-INPUT-FILE TO TRUE.                 06840094
                                                                        06850094
           IF PS-END-OF-INPUT-FILE                                      06860094
               CONTINUE                                                 06870094
           ELSE                                                         06880094
               ADD 1 TO PA-READ-COUNT                                   07100099
           END-IF.                                                      07110094
                                                                        07120094
                                                                        07290094
       8000-WRITE-SKU-DELETE-FILE.                                      07300094
           MOVE '*8000-WRITE-SKU-DELETE-FILE.   *' TO PW-PARA-NAME.     07310094
      ******************************************************************07320094
      * WRITE STYLE DELETE RECORDS                                     *07330094
      ******************************************************************07340094
           INITIALIZE IM020-ENT-ID.                                     07350094
           INITIALIZE IM020-RMS-STYL-ID.                                07360094
           WRITE SD-OUTPUT-RECORD         FROM IM020-STYLE-DELETE-REC.  07370094
           ADD +1 TO PA-WRITE-COUNT.                                    07380094
      *                                                                 07390094
       8200-WRITE-BYPASSED-SKU.                                         07400094
           MOVE '*8200-WRITE-BYPASSED-SKU.      *' TO PW-PARA-NAME.     07410094
      ******************************************************************07420094
      * WRITE BYPASSED STYLE DELETE RECORDS                            *07430094
      ******************************************************************07440094
           INITIALIZE IM021-STYLE-DELETE-INV-REC.                       07450094
           MOVE IM020-SUB-CL-MDSEAR-ID    TO IM021-SUB-CL-MDSEAR-ID.    07470095
           MOVE IM020-VS-ID               TO IM021-VS-ID.               07470096
           MOVE IM020-INTR-UPC-ID         TO IM021-INTR-UPC-ID.         07470097
           MOVE IM020-ITM-NBR             TO IM021-ITM-NBR.             07470098
           MOVE IM020-SKU                 TO IM021-SKU.                 07470099
           MOVE IM020-SKU-STAT            TO IM021-SKU-STAT.            07470100
           MOVE IM020-SKU-CNTR            TO IM021-SKU-CNTR.            07470200
           MOVE IM020-TWEBITM-IND         TO IM021-TWEBITM-IND.         07470300
           WRITE SD-INVALID-REC FROM      IM021-STYLE-DELETE-INV-REC.   07480094
           ADD +1 TO PA-BYPASSED-SKU-COUNT.                             07480095
      *                                                                 07490094
       9980-SQL-ERROR.                                                  07500094
      ******************************************************************07510094
      * ABEND ROUTINE FOR BAD SQL CALLS                                *07520094
      ******************************************************************07530094
           DISPLAY '** ABEND **          = SQL ERROR'.                  07540094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07550094
           DISPLAY '** PARAGRAPH **      = ' PW-PARA-NAME.              07560094
      *                                                                 07570094
      ******************************************************************07580094
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    07590094
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         07600094
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     07610094
      * FORMAT.                                                         07620094
      ******************************************************************07630094
           COPY DPPD004.                                                07640094
                                                                        07650094
             EXEC SQL                                                   07660094
                COMMIT                                                  07670094
             END-EXEC.                                                  07680094
      *                                                                 07690094
           MOVE +4013                      TO ABEND-CODE.               07700094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07710094
                                                                        07720094
       9990-ERROR.                                                      07730094
      ******************************************************************07740094
      * ABEND ROUTINE MISC ERRORS                                      *07750094
      ******************************************************************07760094
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           07770094
           DISPLAY '** PARAGRAPH **      = ' PW-PARA-NAME.              07780094
           MOVE +4016                    TO ABEND-CODE.                 07790094
           CALL 'ILBOABN0' USING ABEND-CODE.                            07800094
