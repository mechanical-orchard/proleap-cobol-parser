       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT033.                                         MAKUT033
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      *  THE PURPOSE OF THIS PROGRAM IS TO CREATE OH/OO RECORDS THAT   *      32
      *  WILL BE LOADED INTO THE TSKUINRU ORACLE TABLE.  THE INVENTORY *      32
      *  LOAD FILE CONTAINS A RECORD FOR EACH DPT/STORE COMBINATION.   *      32
      *  ALONG WITH THE DPT/STORE, THE RECORD CONTAINS THE OH AND OO   *      32
      *  QUANTITIES FOR THAT DPT/STORE.                                *      32
      *                                                                *      32
      *  THIS PROGRAM DETERMINES THE CORRESPONDING ITEM CLUSTER AND    *      32
      *  AND ORG UNIT ID'S FOR EACH DPT/STORE RECORD.  IT THEN CREATES *      32
      *  AN OUTPUT RECORD CONTAINING THE ITEM/ORG CLUSTER ID'S ALONG   *      32
      *  WITH THE OH/OO QUANTITIES.                                    *      32
      ******************************************************************      32
      *  WR25478 - LINDA GRIESBACH                                     *      32
      *            STORE NBR EXPANSION                                 *      32
      ******************************************************************      32
220045*  03/15/2019 - CHG0220045 - JIM HUNTER                          *      32
220045*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
220045*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
220045*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
220045******************************************************************      32
                                                                        MAKUT033
       ENVIRONMENT DIVISION.                                            MAKUT033
       INPUT-OUTPUT SECTION.                                            MAKUT033
       FILE-CONTROL.                                                    MAKUT033
                                                                        MAKUT033
           SELECT DEPT-STORE-INVENTORY-FILE                             MAKUT033
               ASSIGN TO INP01.                                         MAKUT033
                                                                        MAKUT033
           SELECT ORG-CLUSTR-FILE                                       MAKUT033
               ASSIGN TO INP02.                                         MAKUT033
                                                                        MAKUT033
           SELECT ITEM-CLUSTR-FILE                                      MAKUT033
               ASSIGN TO INP03.                                         MAKUT033
                                                                        MAKUT033
           SELECT INVENTORY-LOAD-FILE                                   MAKUT033
               ASSIGN TO OUT01.                                         MAKUT033
                                                                        MAKUT033
       DATA DIVISION.                                                   MAKUT033
       FILE SECTION.                                                    MAKUT033
       FD  DEPT-STORE-INVENTORY-FILE                                    MAKUT033
           RECORDING MODE IS F                                          MAKUT033
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT033
       01  DEPT-STORE-INVENTORY-RECORD PIC X(109).                         CL**5
                                                                        MAKUT033
       FD  ORG-CLUSTR-FILE                                              MAKUT033
           RECORDING MODE IS F                                          MAKUT033
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT033
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT033
       FD  ITEM-CLUSTR-FILE                                             MAKUT033
           RECORDING MODE IS F                                          MAKUT033
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT033
       01  ITEM-CLUSTR-RECORD          PIC X(13).                          CL**5
                                                                        MAKUT033
       FD  INVENTORY-LOAD-FILE                                          MAKUT033
           RECORDING MODE IS F                                          MAKUT033
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT033
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT033
                                                                        MAKUT033
       WORKING-STORAGE SECTION.                                         MAKUT033
                                                                        MAKUT033
       01  PROGRAM-VARIABLES.                                           MAKUT033
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT033
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT033
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT033
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT033
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
           05  PV-PREV-ITEM-CLUSTR-DEPT                                         
                                       PIC X(10)  VALUE SPACES.                 
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT033
           05  PS-DEPT-STORE-INV-EOF-SW                                 MAKUT033
                                       PIC X(1)   VALUE 'N'.            MAKUT033
               88 PS-DEPT-STORE-INV-EOF           VALUE 'Y'.            MAKUT033
               88 PS-DEPT-STORE-INV-NOT-EOF       VALUE 'N'.            MAKUT033
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT033
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT033
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT033
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT033
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT033
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT033
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT033
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT033
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT033
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT033
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT033
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT033
           05  PS-EMPTY-ORG-CLUSTR-FILE-SW                              MAKUT033
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ORG-CLUSTR-FILE        VALUE 'Y'.            MAKUT033
               88 PS-NOT-EMPTY-ORG-CLUSTR-FILE    VALUE 'N'.            MAKUT033
           05  PS-EMPTY-ITEM-CLUSTR-FILE-SW                             MAKUT033
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ITEM-CLUSTR-FILE       VALUE 'Y'.            MAKUT033
               88 PS-NOT-EMPTY-ITEM-CLUSTR-FILE   VALUE 'N'.            MAKUT033
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT033
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT033
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT033
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT033
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT033
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT033
                                                                        MAKUT033
       01  ORG-CLUSTR-TABLE.                                                    
220045*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
220045     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT033'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT033
           05  FILLER                  PIC X(23)                        MAKUT033
                   VALUE '*** PROGRAM MAKUT033 - '.                     MAKUT033
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT033
                                                                        MAKUT033
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT033
           05  FILLER                  PIC X(37)                        MAKUT033
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT033
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT033
           05  INF-LM-SKU              PIC X(10)  VALUE SPACES.                 
                                                                        MAKUT033
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT033
           05  FILLER                  PIC X(36)                        MAKUT033
                   VALUE 'DEPT STORE INVENTORY RECORDS READ = '.        MAKUT033
           05  RSLM1-SKU-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT033
                                                                        MAKUT033
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT033
           05  FILLER                  PIC X(33)                        MAKUT033
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT033
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT033
                                                                        MAKUT033
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT033
           05  FILLER                  PIC X(43)                        MAKUT033
                   VALUE 'NUMBER OF DEPT STORE INVENTORY RECORDS DROP'. MAKUT033
           05  FILLER                  PIC X(40)                        MAKUT033
                   VALUE 'PED DUE TO NO MATCHING ITEM CLUSTER ID= '.    MAKUT033
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT033
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT033
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT033
           05  FILLER                  PIC X(39)                        MAKUT033
                   VALUE 'NUMBER OF DEPT STORE INVENTORY RECORDS '.     MAKUT033
           05  FILLER                  PIC X(38)                        MAKUT033
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT033
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT033
                                                                        MAKUT033
       01  WS-ORG-CLUSTR-RECORD.                                                
           05  WS-ORG-CLUSTR-STORE     PIC X(4).                                
           05  WS-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT033
       01  WS-ITEM-CLUSTR-RECORD.                                               
           05  WS-ITEM-CLUSTR-DEPT     PIC X(3).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                                
      * FIELDS FOR THE DEPT STORE INVENTORY EXTRACT FILE                MAKUT033
           COPY MARD015.                                                MAKUT033
                                                                        MAKUT033
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT033
           COPY MARD016.                                                MAKUT033
                                                                        MAKUT033
                                                                        MAKUT033
       EJECT                                                            MAKUT033
       PROCEDURE DIVISION.                                              MAKUT033
                                                                        MAKUT033
       0000-MAINLINE.                                                   MAKUT033
                                                                        MAKUT033
           PERFORM 1000-INITIALIZE                                      MAKUT033
              THRU 1000-EXIT.                                                   
                                                                        MAKUT033
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT033
              THRU 2000-EXIT                                                    
              UNTIL PS-DEPT-STORE-INV-EOF.                                      
                                                                        MAKUT033
           PERFORM 3000-CLOSE                                           MAKUT033
              THRU 3000-EXIT.                                                   
                                                                        MAKUT033
           DISPLAY 'MAKUT033 - SUCCESSFUL END'.                         MAKUT033
           MOVE ZERO TO RETURN-CODE.                                    MAKUT033
           GOBACK.                                                      MAKUT033
                                                                        MAKUT033
       0000-EXIT.                                                       MAKUT033
           EXIT.                                                                
                                                                        MAKUT033
                                                                        MAKUT033
       1000-INITIALIZE.                                                 MAKUT033
                                                                        MAKUT033
           OPEN INPUT  DEPT-STORE-INVENTORY-FILE                        MAKUT033
                       ORG-CLUSTR-FILE                                  MAKUT033
                       ITEM-CLUSTR-FILE                                 MAKUT033
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT033
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-DEPT-STORE-INV-FILE                                
              THRU 9998-EXIT.                                                   
                                                                        MAKUT033
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT033
           READ ORG-CLUSTR-FILE                                                 
               INTO WS-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT033
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF WS-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND WS-ORG-CLUSTR-STORE > ZERO                                     
               MOVE WS-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE WS-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT033
                                                                        MAKUT033
       2000-CREATE-OH-OO-FILE.                                          MAKUT033
                                                                        MAKUT033
      * THE INPUT FILES ARE SORTED IN ASCENDING DEPT SEQUENCE           MAKUT033
                                                                                
           IF MA015-ITEM-NAME(1:3) = WS-ITEM-CLUSTR-DEPT                        
             OR MA015-ITEM-NAME(1:3) < WS-ITEM-CLUSTR-DEPT                      
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                        MAKUT033
           IF MA015-ITEM-NAME(1:3) = WS-ITEM-CLUSTR-DEPT                        
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE WS-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
               IF MA015-ITEM-NAME(1:3) NOT =                                    
                                      PV-PREV-ITEM-CLUSTR-DEPT                  
                   MOVE MA015-ITEM-NAME(1:3)                                    
                                   TO PV-PREV-ITEM-CLUSTR-DEPT                  
                                      INF-LM-ITEM-NAME                          
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
                   DISPLAY LOG-MESSAGE UPON SYSOUT                              
               END-IF                                                           
               ADD +1 TO PV-NO-ITEM-COUNT                                       
           END-IF.                                                              
                                                                                
           PERFORM 9998-READ-DEPT-STORE-INV-FILE                                
              THRU 9998-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE DEPT-STORE-INVENTORY-FILE                                      
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-SKU-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT033
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT033
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT033
                                                                        MAKUT033
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT033
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT033
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT033
                                                                        MAKUT033
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT033
                                                                        MAKUT033
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT033
           READ ITEM-CLUSTR-FILE                                                
               INTO WS-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO WS-ITEM-CLUSTR-DEPT                   
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT033
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT033
           IF MA015-ITEM-NAME(1:3) = WS-ITEM-CLUSTR-DEPT                        
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT033
           IF MA015-ITEM-NAME(1:3) < WS-ITEM-CLUSTR-DEPT                        
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT033
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-DEPT-STORE-INV-FILE.                                           
                                                                        MAKUT033
           READ DEPT-STORE-INVENTORY-FILE                               MAKUT033
               INTO MA015-ROLLUP-INVENTORY                              MAKUT033
                   AT END                                               MAKUT033
                       SET PS-DEPT-STORE-INV-EOF TO TRUE                MAKUT033
                   NOT AT END                                           MAKUT033
                       ADD +1 TO PV-INV-READ-COUNT                      MAKUT033
           END-READ.                                                            
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT033
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT033
                                                                        MAKUT033
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT033
                                                                        MAKUT033
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT033
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
