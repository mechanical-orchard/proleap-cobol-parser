       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT287.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  05-31-2001.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     THIS PROCESS HAS BEEN RE-WRITTEN (AS OF 09/01/2004)                 
      *                                                                         
      ***   ONE OF THE PRIMARY TABLES (TSKST) WAS REPLACED BY ANOTHER           
      *     (INT00000 - INT_INV_CTOFF) TO RELY LESS ON FS DIRECTLY.             
      *     MUCH OF THE UNDERLYING LOGIC REMAINS WITHIN PROGRAM, BUT            
      *     SUBSTANTIAL CHANGES TO THE OVERALL FLOW HAVE BEEN MADE.             
      ***   STORE NUMBER EXPANSION CHANGES ALSO INCORPORATED                    
      *                                                                         
      *                                                                         
      *CHANGE HISTORY:                                                          
W26600* W26600   09-01-2004  NEW EXCEPTION REPORTING MERGE PROCESS.             
W26600*                      (CREATION) -SCOTT JACKSON                          
W27710* W27710   01-24-2005  REMOVE ROGUE DISPLAYS IN INVENTORY JOBS.           
W27710*                      -ROD JANSSEN                                       
W27808* W27808   07-14-2005  MANIFESTING CHANGES & EXPAND CTOFF LRECL           
W27808*                      -SCOTT JACKSON                                     
W29908* W29908   01-16-2006  BYPASS CLEARANCE AND GWP MERCHANDISE.              
W29908*                      -ROD JANSSEN                                       
W28545* W28545   04-28-2006  CREATE SECOND FILE TO CONTAIN ALL                  
W28545*                      INFORMATION BEFORE THE BYPASS OF                   
W28545*                      CLEARENCE AND GWP MERCHANDISE.                     
W28545*                      -B. GRAHAM - STRATAGEM                             
W28545* W28545   06-27-2006  FIX INITIALIZATION                                 
W28545*                      FOR FILE ABOVE                                     
W28545*                      -B. GRAHAM - STRATAGEM                             
W34894* W34894   05-30-2008  REWRITE FOR PERFORMANCE IMPROVEMENTS.              
W34894*                      REMOVED SECOND FILE AND TUPCPLS CALLS.             
W34894*                      ADDED CALL TO INT_SKU_STYL_DLT FOR DELETED         
W34894*                      SKUS.                                              
W34894*                      -PAUL KEBER                                        
      * INC000000453695                                                         
      *          07-23-2008  THIS PROGRAM PROCESSES TWO SKU/LOCATION            
      *                      INPUT FILES, OUTPUTING A MERGED FILE USED          
      *                      FOR REPORTING IN INK326.  THE INPUT                
      *                      PHYSICAL COUNT DATA FILE HAS SKU/LOCATIONS         
      *                      ASSOCIATED TO THEIR APPROPRIATE                    
      *                      DEPARTMENT/CLASS/SUB CLASS ON IT.  THE             
      *                      INPUT CUTOFF TABLE FILE HAS ONLY SKU/              
      *                      LOCATIONS ON IT.  A RECORD IS READ FROM            
      *                      EACH FILE.  LOCATIONS ARE COMPARED FOR FILE        
      *                      PROCESSING ORDER.  WHEN THE PHYSICAL COUNT         
      *                      DATA FILE LOCATION IS <= TO THE CUTOFF             
      *                      TABLE FILE LOCATION, AND THE ASSOCIATED            
      *                      PHYSICAL COUNT DATA FILE SKU DOES NOT              
      *                      EQUAL THE PREVIOUSLY SAVED SKU, A                  
      *                      SELECT IS DONE ON TSKXREF TO RETURN THE            
      *                      INTERNAL UPC ID ASSOCIATED WITH THE SKU.           
      *                      BECAUSE THE PHYSICAL COUNT DATA FILE               
      *                      HAS DEPARTMENT/CLASS/SUB CLASS ASSOCIATED          
      *                      INFORMATION, IT WAS ASSUMED THAT THE SQL           
      *                      WOULD NOT HAVE TO RETURN THIS INFORMATION.         
      *                      IF THE CUTOFF TABLE DATA FILE LOCATION IS          
      *                      < THE PHYSICAL COUNT DATA FILE LOCATION,           
      *                      AND THE ASSOCIATED CUTOFF TABLE DATA FILE          
      *                      SKU DOES NOT EQUAL THE PREVIOUSLY SAVED            
      *                      SKU, A SELECT IS DONE ON TSKXREF TO RETURN         
      *                      THE INTERNAL UPC ID, DEPTARTMENT, CLASS,           
      *                      SUB CLASS ASSOCIATED WITH THE SKU.  THUS,          
      *                      IF A ROW IS FIRST PROCESSED FROM THE               
      *                      PHYSICAL COUNT DATA FILE, THEN FROM                
      *                      MATCHING PHYSICAL COUNT DATA FILE, CUTOFF          
      *                      DATA FILE ROWS, AND FINALLY FROM THE               
      *                      THE REMAINING CUTOFF DATA FILE ROWS, IT            
      *                      WAS ASSUMED DEPARTMENT/CLASS/SUB CLASS             
      *                      INFORMATION WOULD BE PRESENT FROM THE              
      *                      INITIAL, WHEN THE SKU CHANGED, TSKXREF             
      *                      CALL.  BECAUSE THIS, AND ONLY, CALL TO             
      *                      TSKXREF FOR THE SKU RETURNED ONLY THE              
      *                      INTERNAL UPC ID, OUTPUT ROWS WERE BEING            
      *                      CREATED WITH INCORRECT                             
      *                      DEPARTMENT/CLASS/SUB CLASS INFORMATION.            
      *                      TO CORRECT THIS PROBLEM                            
      *                      3100-SELECT-TSKXREF PERFORMS (RETURNING            
      *                      ONLY INTERNAL UPC ID INFORMATION) WERE             
      *                      REPLACED WITH 3200-GET-SKU-ATTRIBUTES              
      *                      PERFORMS (RETURNING INTERNAL UPC ID,               
      *                      DEPARTMENT, CLASS, SUB CLASS INFORMATION).         
      *                      - TODD C. MICHALEK                                 
W33304* W33304   08-20-2008  HILO CHANGES.                                      
W33304*                      -PATTY JAEGER - CODEWORKS                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-PHYCNT-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CTOFF-UNLOAD-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT MERGE-FILE-OUT                                                
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STORE-PHYCNT-FILE                                                    
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-PHYCNT-REC      PIC X(100).                                    
                                                                                
       FD  CTOFF-UNLOAD-FILE                                                    
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W27808 01  CTOFF-UNLOAD-REC      PIC X(23).                                     
                                                                                
       FD  MERGE-FILE-OUT                                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MERGE-REC-OUT         PIC X(100).                                    
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE               PIC S9(04) VALUE ZERO COMP SYNC.            
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AREA-HEADER          PIC  X(06)     VALUE '000000'.           
           05  PC-DEFAULT-LOC          PIC S9(04)     VALUE +0 COMP.            
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2 COMP.            
           05  PC-NEG-VALUE            PIC S9(01)     VALUE -1.                 
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-STORE-INV-READ       PIC S9(09) VALUE +0  COMP.               
           05  PV-CTOFF-UNLOAD-READ    PIC S9(09) VALUE +0  COMP.               
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) VALUE +0  COMP.               
           05  PV-INTSKUSD-ROWS-READ   PIC S9(09) VALUE +0  COMP.               
           05  PV-TSKXREF-ROWS-READ    PIC S9(09) VALUE +0  COMP.               
           05  PV-SKUS-NOT-FOUND       PIC S9(09) VALUE +0  COMP.               
           05  PV-SQL-SUB              PIC S9(04) VALUE +0  COMP.       INKUT102
           05  PV-STORE-NUMBER         PIC 9(04)  VALUE ZERO.                   
           05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.                 
           05  PV-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT287'.             
           05  PV-ABEND-MESSAGE        PIC X(50)  VALUE SPACE.                  
           05  PV-CUTOFF-INTRANSIT-QTY PIC S9(09) COMP VALUE ZERO.              
           05  PV-STR-NDX              PIC S9(04) COMP   VALUE ZEROS.           
           05  PV-MAX-SKU              PIC S9(08)V COMP-3                       
                                                   VALUE +99999999.             
W33304     05  PV-VARIANCE-QTY         PIC S9(09)     COMP-3   VALUE +0.        
W33304     05  PV-ABSOLUTE-VAR-QTY     PIC S9(09)     COMP-3   VALUE +0.        
           05  PV-HOLD-VARIABLES.                                               
               10  PV-PHYSICAL-QTY      PIC S9(09)    VALUE +0  COMP.           
               10  PV-CUTOFF-QTY        PIC S9(09)    VALUE +0  COMP.           
               10  PV-INTRANSIT-QTY     PIC S9(09)    VALUE +0  COMP.           
               10  PV-POST-FLUSH-QTY    PIC S9(09)    VALUE +0  COMP.           
W27808         10  PV-STR-EXPTED-QTY    PIC S9(09)    VALUE +0  COMP.           
W27808     05  PV-CALC-VARIABLES.                                               
W27808         10  PV-STR-EXPTED        PIC S9(09)    VALUE +0  COMP.           
W27808         10  PV-STR-EXPECTED      PIC S9(09)    VALUE +0  COMP.           
           05  PV-SAVE-VARIABLES.                                               
               10  PV-SAVE-LOC          PIC S9(4)      COMP    VALUE +0.        
               10  PV-SAVE-SKU          PIC S9(8)V     COMP-3  VALUE +0.        
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-STORE-INV-DONE       PIC  X(01)      VALUE 'N'.               
               88  PS-STORE-INV-IS-DONE                VALUE 'Y'.               
               88  PS-STORE-INV-IS-NOT-DONE            VALUE 'N'.               
           05  PS-CTOFF-UNLOAD-DONE    PIC  X(01)      VALUE 'N'.               
               88  PS-CTOFF-UNLOAD-IS-DONE             VALUE 'Y'.               
               88  PS-CTOFF-UNLOAD-IS-NOT-DONE         VALUE 'N'.               
           05  PS-TINVPAR-DONE-SW         PIC X(01)    VALUE 'N'.               
               88  TINVPAR-PROCESS-COMPLETE            VALUE 'Y'.               
           05  PS-ITEM-FOUND-SW           PIC X(01)    VALUE 'N'.               
               88  PS-ITEM-FOUND                       VALUE 'Y'.               
               88  PS-NOT-ITEM-FOUND                   VALUE 'N'.               
                                                                                
       01  WS-TINVPAR-TABLE.                                                    
           05  WS-TINVPAR-TABLE-ELEMENTS   OCCURS 9999 TIMES.                   
               10  WS-MFSTNG-IND    PIC X(01)  VALUE SPACES.                    
                                                                                
       01  WS-EDIT-STORE-ALPH       PIC X(04)  VALUE ZEROS.                     
       01  WS-EDIT-STORE-NUM REDEFINES                                          
           WS-EDIT-STORE-ALPH       PIC 9(04).                                  
                                                                                
      *****************************************************************         
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - IN/OUT             
      *****************************************************************         
           COPY INRD024.                                                        
                                                                                
      *****************************************************************         
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - WORK AREA          
      ***TO BE USED AGAIN AS THE OUTPUT RECORD LAYOUT AS WELL AS INPUT          
      *****************************************************************         
           COPY INRD024 REPLACING IN024 BY INWRK.                               
                                                                                
      *****************************************************************         
      * INPUT CTOFF ONHAND / INTRANSIT ONHAND UNLOAD LAYOUT                     
      *****************************************************************         
           COPY INRD139.                                                        
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      * TSKXREF DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
33304 ******************************************************************01320000
33304 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
33304 ******************************************************************01340000
33304      EXEC SQL                                                     01350000
33304          INCLUDE TINCNTL                                          01360000
33304      END-EXEC.                                                    01370000
33304                                                                   01380000
      *****************************************************************         
      * INT_SKU_STYL_DLT DCLGEN                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE INT00003                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TINVPAR CURSOR.                                                         
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TINVPAR_CURSOR CURSOR FOR                                  
W33304         SELECT A.LOC_NBR,                                                
W33304                A.MFSTNG_IND                                              
W33304         FROM TINVPAR A                                                   
W33304             ,TINCNTL B                                                   
W33304         WHERE A.ACTL_FIN_BK_DTE = '9999-09-09'                           
W33304            AND A.LOC_INV_STAT_CDE = 'IN'                                 
W33304            AND A.INV_ID = B.INV_ID                                       
W33304            AND B.ACTV_IND = 'Y'                                          
               FOR FETCH ONLY                                                   
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * SQL ERROR HANDLING.                                                     
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      /*****************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0100-MAINLINE-INKUT287.                                                  
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-STORE-INV-RECS                                  
               UNTIL PS-STORE-INV-IS-DONE                                       
                 AND PS-CTOFF-UNLOAD-IS-DONE.                                   
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      *  OPEN FILES AND READ THE FIRST RECORD OF EACH INPUT FILE.               
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  STORE-PHYCNT-FILE                                        
                       CTOFF-UNLOAD-FILE                                        
W28545          OUTPUT MERGE-FILE-OUT.                                          
                                                                                
      *** LOAD THE TINVPAR TABLE....                                            
           INITIALIZE      WS-TINVPAR-TABLE.                                    
           MOVE ZEROS      TO PV-STR-NDX.                                       
           MOVE 'N'        TO PS-TINVPAR-DONE-SW.                               
           PERFORM 8200-OPEN-TINVPAR.                                           
           PERFORM 8300-LOAD-TINVPAR-TABLE                                      
               UNTIL TINVPAR-PROCESS-COMPLETE  OR                               
                     PV-STR-NDX IS GREATER THAN 9999.                           
           PERFORM 8400-CLOSE-TINVPAR.                                          
                                                                                
           PERFORM 5000-READ-STORE-INV.                                         
           PERFORM 5100-READ-CTOFF-UNLOAD.                                      
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY '*  START OF INKUT287                          *'.           
           DISPLAY '***********************************************'.           
      *                                                                         
      ******************************************************************        
      *   A RECORD HAS BEEN READ FROM EACH OF THE INPUT FILES.                  
      * - THE FIRST RECORD FROM THE PHYSICAL FILE IS HEADER RECORD.             
      *   COMPARE THIS TO THE INT_INV_CTOFF RECORD FOR THE SAME LOC/STR.        
      * - HEADER RECORD WILL CONTAIN TOTAL ONHAND.                              
      *                                                                         
      * - ADD CUTOFF INFO TO THE HEADER RECORD.                                 
      * - WRITE EACH INDIVIDUAL RECORD.                                         
      *                                                                         
      * - 2100-PROCESS-BOTH-FILES (LOGIC)                                       
      * - EACH UNIQUE STORE/SKU ROW WILL BE PRECEEDED BY A HEADER REC           
      *   CONTAINING UNIT TOTAL FOR ALL UNITS REGARDLESS OF AREA. ONCE          
      *   HEADER AND CTOFF ROW ARE PROCESSED, EACH SKU/AREA MUST BE             
      *   PROCESSED BEFORE ANOTHER CTOFF RECORD IS READ FOR A MATCH.            
      ******************************************************************        
       2000-PROCESS-STORE-INV-RECS.                                             
                                                                                
           IF IN024-SKU = IN139-SKU                                             
               IF IN024-STORE-NUMBER = IN139-LOC-NBR                            
                   PERFORM 2100-PROCESS-BOTH-FILES                              
                       UNTIL IN024-SKU > IN139-SKU                              
                          OR IN024-STORE-NUMBER > IN139-LOC-NBR                 
                          OR PS-STORE-INV-IS-DONE                               
                                                                                
                   IF PS-CTOFF-UNLOAD-IS-NOT-DONE                               
                       PERFORM 5100-READ-CTOFF-UNLOAD                           
                   END-IF                                                       
               ELSE                                                             
                   IF IN024-STORE-NUMBER < IN139-LOC-NBR                        
                       PERFORM 2200-PROCESS-PHYSICAL-FILE                       
                         UNTIL IN024-SKU > IN139-SKU                            
                            OR IN024-STORE-NUMBER >= IN139-LOC-NBR              
                            OR PS-STORE-INV-IS-DONE                             
                   ELSE                                                         
                       IF IN024-STORE-NUMBER > IN139-LOC-NBR                    
                           PERFORM 2300-PROCESS-CUTOFF-FILE                     
                             UNTIL IN139-SKU > IN024-SKU                        
                                OR IN139-LOC-NBR >= IN024-STORE-NUMBER          
                                OR PS-CTOFF-UNLOAD-IS-DONE                      
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               IF IN024-SKU < IN139-SKU                                         
                   PERFORM 2200-PROCESS-PHYSICAL-FILE                           
                     UNTIL IN024-SKU >= IN139-SKU                               
                        OR PS-STORE-INV-IS-DONE                                 
               ELSE                                                             
                   PERFORM 2300-PROCESS-CUTOFF-FILE                             
                     UNTIL IN139-SKU >= IN024-SKU                               
                        OR PS-CTOFF-UNLOAD-IS-DONE                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *                                                                         
      ******************************************************************        
      * 2100-PROCESS-BOTH-FILES.                                                
      *  *  SKU WAS FOUND ON BOTH THE PHYSICAL AND CUTOFF FILES                 
      *  *  MOVE ALL KNOWN DATA TO WORKING OUTPUT LAYOUT (INWRK-...)            
      *  *  RETRIEVE THE SYSTEM PRICE AND CALCULATE UNIT VARIANCE               
      ******************************************************************        
       2100-PROCESS-BOTH-FILES.                                                 
                                                                                
           INITIALIZE INWRK-INV-EXCEPTS-STR-REC.                        INRD024 
                                                                        INRD024 
           SET  INWRK-FOUND-IN-BOTH      TO TRUE.                       INRD024 
                                                                                
           MOVE IN024-STORE-NUMBER         TO INWRK-STORE-NUMBER.       INRD024 
           MOVE IN024-SKU                  TO INWRK-SKU                 INRD024 
                                              SKXREF-SKU.                       
           MOVE IN024-PHYSICAL-DATA        TO INWRK-PHYSICAL-DATA.      INRD024 
           MOVE IN024-QUANTITY             TO PV-PHYSICAL-QTY.          INRD024 
                                                                        INRD024 
      *    INCLUDE CTOFF QUANTITIES WITH THE HEADER RECORD (AREA 000000)INRD024 
           MOVE IN139-CTOFF-OH-QTY         TO INWRK-INV-CUTOFF-OH-QTY   INRD024 
                                              PV-CUTOFF-QTY.            INRD024 
           MOVE IN139-IN-TRNST-CTOFF-QTY   TO INWRK-CUTOFF-INTRANSIT    INRD024 
                                              PV-INTRANSIT-QTY.         INRD024 
           MOVE IN139-POST-FSH-RCPT-QTY    TO INWRK-POST-FL-AFT-REC     INRD024 
                                              PV-POST-FLUSH-QTY.        INRD024 
                                                                        INRD024 
W27808     COMPUTE PV-STR-EXPECTED =                                            
W27808         (IN139-STR-EXPTED-CTOFF-QTY - IN139-IN-TRNST-CTOFF-QTY).         
W27808     MOVE PV-STR-EXPECTED            TO INWRK-CUTOFF-STR-EXPECT           
W27808                                        PV-STR-EXPTED-QTY.                
                                                                        INRD024 
           MOVE IN024-SKU-ATTRIBUTES       TO INWRK-SKU-ATTRIBUTES.             
           MOVE IN024-STORE-NUMBER         TO PV-STR-NDX.                       
           MOVE WS-MFSTNG-IND (PV-STR-NDX) TO INWRK-MFSTNG-IND.         INRD024 
           PERFORM 4100-CALC-UNIT-VARIANCE.                                     
           MOVE PV-VARIANCE-QTY          TO INWRK-VARIANCE-QTY.                 
           MOVE PV-ABSOLUTE-VAR-QTY      TO INWRK-ABSOLUTE-VAR-QTY.             
                                                                                
      *    GET THE INTERNAL UPC ID FOR THE SKU                                  
           IF IN024-SKU = PV-SAVE-SKU                                           
               MOVE SKXREF-INTR-UPC-ID     TO INWRK-INTR-UPC-ID         INRD024 
           ELSE                                                                 
               PERFORM 3200-GET-SKU-ATTRIBUTES                                  
               IF PS-ITEM-FOUND                                                 
                   MOVE SKXREF-INTR-UPC-ID TO INWRK-INTR-UPC-ID         INRD024 
               ELSE                                                             
                   MOVE +0                 TO INWRK-INTR-UPC-ID         INRD024 
                                              SKXREF-INTR-UPC-ID                
                   DISPLAY '** PHYSICAL INV SKU NOT FOUND: ' IN024-SKU          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
           IF PS-STORE-INV-IS-NOT-DONE                                          
               PERFORM 5000-READ-STORE-INV                                      
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 2200-PROCESS-PHYSICAL-FILE.                                             
      *  *  SKU WAS FOUND ONLY ON THE PHYSICAL EXTRACT FROM TSUMINV             
      *  *  MOVE ALL KNOWN DATA TO WORKING OUTPUT LAYOUT (INWRK-...)            
      *  *  RETRIEVE THE SYSTEM PRICE AND CALCULATE UNIT VARIANCE               
      ******************************************************************        
       2200-PROCESS-PHYSICAL-FILE.                                              
                                                                                
           INITIALIZE INWRK-INV-EXCEPTS-STR-REC.                        INRD024 
                                                                        INRD024 
           SET  INWRK-PHYSICAL-ONLY        TO TRUE.                     INRD024 
                                                                                
           MOVE IN024-STORE-NUMBER         TO INWRK-STORE-NUMBER.       INRD024 
           MOVE IN024-SKU                  TO INWRK-SKU                 INRD024 
                                              SKXREF-SKU.                       
           MOVE IN024-PHYSICAL-DATA        TO INWRK-PHYSICAL-DATA.      INRD024 
                                                                        INRD024 
      *    IF NON-HEADER RECORD, MOVE ZEROES TO THE CUTOFF FIELDS       INRD024 
           MOVE ZEROES                     TO INWRK-INV-CUTOFF-OH-QTY   INRD024 
                                              INWRK-POST-FL-AFT-REC     INRD024 
                                              INWRK-CUTOFF-INTRANSIT    INRD024 
W27808                                        INWRK-CUTOFF-STR-EXPECT.  INRD024 
                                                                        INRD024 
           MOVE IN024-SKU-ATTRIBUTES       TO INWRK-SKU-ATTRIBUTES.             
           MOVE IN024-STORE-NUMBER         TO PV-STR-NDX.                       
           MOVE WS-MFSTNG-IND (PV-STR-NDX) TO INWRK-MFSTNG-IND.         INRD024 
                                                                                
           MOVE IN024-QUANTITY             TO PV-PHYSICAL-QTY                   
           MOVE ZEROES                     TO PV-CUTOFF-QTY                     
                                              PV-INTRANSIT-QTY                  
                                              PV-POST-FLUSH-QTY                 
W27808                                        PV-STR-EXPTED-QTY.                
                                                                                
           PERFORM 4100-CALC-UNIT-VARIANCE.                                     
           MOVE PV-VARIANCE-QTY            TO INWRK-VARIANCE-QTY.               
           MOVE PV-ABSOLUTE-VAR-QTY        TO INWRK-ABSOLUTE-VAR-QTY.           
                                                                                
      *    GET THE INTERNAL UPC ID FOR THE SKU                                  
           IF IN024-SKU = PV-SAVE-SKU                                           
               MOVE SKXREF-INTR-UPC-ID     TO INWRK-INTR-UPC-ID         INRD024 
           ELSE                                                                 
               PERFORM 3200-GET-SKU-ATTRIBUTES                                  
               IF PS-ITEM-FOUND                                                 
                   MOVE SKXREF-INTR-UPC-ID TO INWRK-INTR-UPC-ID         INRD024 
               ELSE                                                             
                   MOVE +0                 TO INWRK-INTR-UPC-ID         INRD024 
                                              SKXREF-INTR-UPC-ID                
                   DISPLAY '** PHYSICAL INV SKU NOT FOUND: ' IN024-SKU          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
           IF PS-STORE-INV-IS-NOT-DONE                                          
               PERFORM 5000-READ-STORE-INV                                      
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      * 2300-PROCESS-CUTOFF-FILE.                                               
      *  *  SKU WAS FOUND ONLY ON THE CUTOFF EXTRACT FROM INT_INV_CTOFF         
      *  *  MOVE ALL KNOWN DATA TO WORKING OUTPUT LAYOUT (INWRK-...)            
      *  *  RETRIEVE THE SYSTEM PRICE AND CALCULATE UNIT VARIANCE               
      ******************************************************************        
       2300-PROCESS-CUTOFF-FILE.                                                
                                                                                
           INITIALIZE INWRK-INV-EXCEPTS-STR-REC.                        INRD024 
                                                                        INRD024 
           SET  INWRK-CTOFF-DB-ONLY        TO TRUE.                     INRD024 
                                                                                
           MOVE IN139-LOC-NBR              TO INWRK-STORE-NUMBER.       INRD024 
           MOVE IN139-SKU                  TO INWRK-SKU                 INRD024 
                                              SKXREF-SKU.               INRD024 
                                                                        INRD024 
      *    MOVE ZEROES TO THE PHYSICAL FIELDS IF SKU NOT INVENTORIED    INRD024 
           MOVE ZEROES                     TO INWRK-AREA-NUMBER         INRD024 
                                              INWRK-QUANTITY            INRD024 
                                              INWRK-PRICE               INRD024 
                                              INWRK-SKU-STR-TOT-EXTD.   INRD024 
                                                                        INRD024 
      *    INCLUDE CTOFF QUANTITIES WITH A CUTOFF DB RECORD ONLY        INRD024 
           MOVE IN139-CTOFF-OH-QTY         TO INWRK-INV-CUTOFF-OH-QTY.  INRD024 
           MOVE IN139-IN-TRNST-CTOFF-QTY   TO INWRK-CUTOFF-INTRANSIT.   INRD024 
           MOVE IN139-POST-FSH-RCPT-QTY    TO INWRK-POST-FL-AFT-REC.    INRD024 
           COMPUTE PV-STR-EXPTED =                                      INRD024 
               (IN139-STR-EXPTED-CTOFF-QTY - IN139-IN-TRNST-CTOFF-QTY). INRD024 
           MOVE PV-STR-EXPTED              TO INWRK-CUTOFF-STR-EXPECT.  INRD024 
                                                                        INRD024 
           MOVE IN139-LOC-NBR              TO PV-STR-NDX.                       
           MOVE WS-MFSTNG-IND (PV-STR-NDX) TO INWRK-MFSTNG-IND.         INRD024 
                                                                                
           MOVE ZEROES                     TO PV-PHYSICAL-QTY.                  
           MOVE IN139-CTOFF-OH-QTY         TO PV-CUTOFF-QTY.                    
           MOVE IN139-IN-TRNST-CTOFF-QTY   TO PV-INTRANSIT-QTY.                 
           MOVE IN139-POST-FSH-RCPT-QTY    TO PV-POST-FLUSH-QTY.                
                                                                                
W27808     COMPUTE PV-STR-EXPECTED =                                            
W27808         (IN139-STR-EXPTED-CTOFF-QTY - IN139-IN-TRNST-CTOFF-QTY).         
W27808     MOVE PV-STR-EXPECTED TO PV-STR-EXPTED-QTY.                           
                                                                                
           PERFORM 4100-CALC-UNIT-VARIANCE.                                     
           MOVE PV-VARIANCE-QTY            TO INWRK-VARIANCE-QTY.               
           MOVE PV-ABSOLUTE-VAR-QTY        TO INWRK-ABSOLUTE-VAR-QTY.           
                                                                                
      *    GET THE INTERNAL UPC ID FOR THE SKU                                  
           IF IN139-SKU = PV-SAVE-SKU                                           
               MOVE SKXREF-INTR-UPC-ID     TO INWRK-INTR-UPC-ID         INRD024 
               MOVE SKXREF-DEPT            TO INWRK-DEPT                        
               MOVE SKXREF-CLASS           TO INWRK-CLASS                       
               MOVE SKXREF-SUBCLASS        TO INWRK-SUB-CLASS                   
           ELSE                                                                 
               PERFORM 3200-GET-SKU-ATTRIBUTES                                  
               IF PS-ITEM-FOUND                                                 
                   MOVE SKXREF-INTR-UPC-ID TO INWRK-INTR-UPC-ID         INRD024 
                   MOVE SKXREF-DEPT        TO INWRK-DEPT                        
                   MOVE SKXREF-CLASS       TO INWRK-CLASS                       
                   MOVE SKXREF-SUBCLASS    TO INWRK-SUB-CLASS                   
               ELSE                                                             
                   MOVE +0                 TO INWRK-INTR-UPC-ID         INRD024 
                                              SKXREF-INTR-UPC-ID                
                   MOVE ZERO               TO INWRK-DEPT                INRD024 
                                              SKXREF-DEPT                       
                                              INWRK-CLASS                       
                                              SKXREF-CLASS                      
                                              INWRK-SUB-CLASS                   
                                              SKXREF-SUBCLASS                   
                   DISPLAY '** STORE CUTOFF SKU NOT FOUND: ' IN139-SKU          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-EXTRACT-REC.                                      
                                                                                
           IF PS-CTOFF-UNLOAD-IS-NOT-DONE                                       
               PERFORM 5100-READ-CTOFF-UNLOAD                                   
           END-IF.                                                              
                                                                                
      ******************************************************************INKUT102
      *  SELECT SKU ATTRIBUTES FROM TSKXREF TABLE FOR OUTPUT DATA               
      ******************************************************************INKUT102
       3200-GET-SKU-ATTRIBUTES.                                                 
                                                                        INKUT102
           MOVE SKXREF-SKU              TO PV-SAVE-SKU.                         
                                                                        INKUT102
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
                 UNTIL SQLCODE = ZERO                                           
                    OR SQLCODE = +100                                           
                    OR PV-SQL-SUB > PC-MAX-RETRIES                              
               EXEC SQL                                                         
                 SELECT  INTR_UPC_ID                                            
                        ,DEPT                                                   
                        ,CLASS                                                  
                        ,SUBCLASS                                               
                   INTO  :SKXREF-INTR-UPC-ID                                    
                        ,:SKXREF-DEPT                                           
                        ,:SKXREF-CLASS                                          
                        ,:SKXREF-SUBCLASS                                       
                   FROM  TSKXREF                                                
                   WHERE SKU  = :SKXREF-SKU                                     
W27710             WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-ITEM-FOUND TO TRUE                                
                       ADD +1 TO PV-TSKXREF-ROWS-READ                           
                   WHEN SQLCODE = +100                                          
                       PERFORM 3300-GET-DELETED-SKU-ATTRS                       
                       IF PS-ITEM-FOUND                                         
                           MOVE INT00003-INTR-UPC-ID TO                         
                                 SKXREF-INTR-UPC-ID                             
                           MOVE INT00003-DEPT-NBR    TO SKXREF-DEPT             
                           MOVE INT00003-MAJ-CL-NBR  TO SKXREF-CLASS            
                           MOVE INT00003-SUB-CL-NBR  TO SKXREF-SUBCLASS         
                       END-IF                                                   
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY 'SKU   ' SKXREF-SKU                              
                       MOVE '3200-GET-SKU-ATTRIBUTES'                           
                                          TO PV-PARAGRAPH-NAME                  
                       MOVE 'SELECT TSKXREF SKU ATTRIBUTES'                     
                                          TO PV-DB2-OPER-ATTEMPTED              
                       PERFORM 9000-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               DISPLAY 'SKU   ' SKXREF-SKU                                      
               MOVE '3200-GET-SKU-ATTRIBUTES'                                   
                                          TO PV-PARAGRAPH-NAME                  
               MOVE 'SELECT TSKXREF SKU ATTRIBUTES'                             
                                          TO PV-DB2-OPER-ATTEMPTED              
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************INKUT102
      *  SELECT SKU ATTRIBUTES FROM INT_SKU_STYL_DLT FOR OUTPUT DATA            
      ******************************************************************INKUT102
       3300-GET-DELETED-SKU-ATTRS.                                              
                                                                        INKUT102
           MOVE SKXREF-SKU              TO INT00003-SKU-NBR.                    
                                                                        INKUT102
           PERFORM WITH TEST AFTER                                              
               VARYING PV-SQL-SUB FROM 1 BY 1                                   
                 UNTIL SQLCODE = ZERO                                           
                    OR SQLCODE = +100                                           
                    OR PV-SQL-SUB > PC-MAX-RETRIES                              
W33304         EXEC SQL                                                         
W33304           SELECT  A.INTR_UPC_ID                                          
W33304                  ,A.DEPT_NBR                                             
W33304                  ,A.MAJ_CL_NBR                                           
W33304                  ,A.SUB_CL_NBR                                           
W33304           INTO    :INT00003-INTR-UPC-ID                                  
W33304                  ,:INT00003-DEPT-NBR                                     
W33304                  ,:INT00003-MAJ-CL-NBR                                   
W33304                  ,:INT00003-SUB-CL-NBR                                   
W33304           FROM   INT_SKU_STYL_DLT A                                      
W33304           WHERE  A.SKU_NBR      = :INT00003-SKU-NBR                      
W33304             AND  A.LOC_NBR      = SMALLINT(0)                            
W33304             AND  A.INTR_UPC_ID  = A.UPC_ID                               
W33304             AND  A.CRE_TMST     =                                        
W33304               (SELECT MAX(B.CRE_TMST)                                    
W33304                FROM  INT_SKU_STYL_DLT B                                  
W33304                  WHERE  B.SKU_NBR      = :INT00003-SKU-NBR               
W33304                  AND    B.LOC_NBR      = SMALLINT(0)                     
W33304                  AND    B.INTR_UPC_ID  = B.UPC_ID)                       
W27710           WITH UR                                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                       SET PS-ITEM-FOUND TO TRUE                                
                       ADD +1 TO PV-INTSKUSD-ROWS-READ                          
                   WHEN SQLCODE = +100                                          
                       SET PS-NOT-ITEM-FOUND TO TRUE                            
                       ADD +1 TO PV-SKUS-NOT-FOUND                              
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       DISPLAY 'SKU   ' SKXREF-SKU                              
                       MOVE '3300-GET-DELETED-SKU-ATTRS'                        
                                          TO PV-PARAGRAPH-NAME                  
                       MOVE 'SELECT INT_SKU_STYL_DLT'                           
                                          TO  PV-DB2-OPER-ATTEMPTED             
                       PERFORM 9000-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               DISPLAY 'SKU   ' SKXREF-SKU                                      
               MOVE '3300-GET-DELETED-SKU-ATTRS'                                
                                          TO PV-PARAGRAPH-NAME                  
               MOVE 'SELECT INT_SKU_STYL_DLT'                                   
                                          TO PV-DB2-OPER-ATTEMPTED              
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CALCULATE THE DIFFERENCE BETWEEN THE PHYSICAL INVENTORY COUNT          
      *  AND THE SYSTEM RECORD ON-HAND                                          
      ******************************************************************        
       4100-CALC-UNIT-VARIANCE.                                                 
                                                                                
           IF IN024-AREA-NUMBER EQUAL PC-AREA-HEADER                            
             OR INWRK-CTOFF-DB-ONLY                                             
               IF  WS-MFSTNG-IND (PV-STR-NDX) = 'Y'                             
                                                                                
                   COMPUTE PV-VARIANCE-QTY = (PV-PHYSICAL-QTY                   
                                           - (PV-CUTOFF-QTY -                   
                                             (PV-STR-EXPTED-QTY                 
                                           -  PV-INTRANSIT-QTY)))               
               ELSE                                                             
                   IF  WS-MFSTNG-IND (PV-STR-NDX) = 'N'                         
                                                                                
                       COMPUTE PV-VARIANCE-QTY                                  
                                           = (PV-PHYSICAL-QTY                   
                                           - (PV-CUTOFF-QTY -                   
                                              PV-POST-FLUSH-QTY))               
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF PV-VARIANCE-QTY < 0                                           
                   COMPUTE PV-ABSOLUTE-VAR-QTY = (PV-VARIANCE-QTY               
                                               *  PC-NEG-VALUE)                 
               ELSE                                                             
                  MOVE PV-VARIANCE-QTY TO PV-ABSOLUTE-VAR-QTY                   
               END-IF                                                           
           ELSE                                                                 
               MOVE ZEROES TO PV-VARIANCE-QTY                                   
                              PV-ABSOLUTE-VAR-QTY                               
           END-IF.                                                              
                                                                                
      *                                                                         
      *****************************************************************         
      * 5000-READ-STORE-INV.  READ A TSUMINV PROD UNLOAD FILE RECORD.           
      *****************************************************************         
       5000-READ-STORE-INV.                                                     
                                                                                
           READ STORE-PHYCNT-FILE INTO IN024-INV-EXCEPTS-STR-REC                
             AT END                                                             
                 MOVE 'Y' TO PS-STORE-INV-DONE                                  
                 MOVE PV-MAX-SKU TO IN024-SKU                                   
             NOT AT END                                                         
                 ADD +1   TO PV-STORE-INV-READ                                  
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * 5100-READ-CTOFF-UNLOAD. READ A TMNSCLS TEST UNLOAD FILE RECORD.         
      *****************************************************************         
       5100-READ-CTOFF-UNLOAD.                                                  
                                                                                
           READ CTOFF-UNLOAD-FILE                                               
             INTO INRD139-CUTOFF-REC                                            
               AT END                                                           
                   MOVE 'Y' TO PS-CTOFF-UNLOAD-DONE                             
                   MOVE PV-MAX-SKU TO IN139-SKU                                 
               NOT AT END  ADD 1 TO PV-CTOFF-UNLOAD-READ                        
           END-READ.                                                            
                                                                                
      *                                                                         
      ******************************************************************        
      * 6000-WRITE-EXTRACT-REC.                                                 
      *  CREATE A STORE INVENTORY EXCEPTIONS OUTPUT RECORD CONTAINING           
      *  MATCHING CTOFF INFORMATION.                                            
      ******************************************************************        
       6000-WRITE-EXTRACT-REC.                                                  
                                                                                
           WRITE MERGE-REC-OUT                                                  
               FROM INWRK-INV-EXCEPTS-STR-REC.                                  
                                                                                
           ADD +1 TO PV-TOTAL-EXTR-WRITTEN.                                     
                                                                                
      ******************************************************************        
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                            
      ******************************************************************        
       8000-EOJ.                                                                
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*** INKUT287 RECORD COUNT SUMMARY'.                         
           DISPLAY 'INVENTORY EXTRACT RECORDS READ    = '                       
                 PV-STORE-INV-READ.                                             
           DISPLAY 'CTOFF UNLOAD RECORDS READ         = '                       
                 PV-CTOFF-UNLOAD-READ.                                          
           DISPLAY 'INVENTORY EXTRACT RECORDS WRITTEN = '                       
                 PV-TOTAL-EXTR-WRITTEN.                                         
           DISPLAY 'TSKXREF ROWS SELECTED             = '                       
                 PV-TSKXREF-ROWS-READ.                                          
           DISPLAY 'INT_SKU_STYL_DLT ROWS SELECTED    = '                       
                 PV-INTSKUSD-ROWS-READ.                                         
           DISPLAY 'SKUS NOT FOUND                    = '                       
                 PV-SKUS-NOT-FOUND.                                             
                                                                                
           DISPLAY '***********************************************'.           
           DISPLAY '*  END OF INKUT287                            *'.           
           DISPLAY '***********************************************'.           
                                                                                
           CLOSE STORE-PHYCNT-FILE                                              
                 CTOFF-UNLOAD-FILE                                              
W28545           MERGE-FILE-OUT.                                                
                                                                                
      *****************************************************************         
      * 8200-OPEN-TINVPAR.                                                      
      *****************************************************************         
       8200-OPEN-TINVPAR.                                                       
      *                                                                         
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8200-OPEN-TINVPAR' TO PV-PARAGRAPH-NAME                     
              MOVE 'OPEN CURSOR'       TO PV-DB2-OPER-ATTEMPTED                 
              MOVE 'Y'                 TO PS-TINVPAR-DONE-SW                    
              PERFORM 9000-SQL-ABEND                                            
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * 8300-LOAD-TINVPAR-TABLE.                                                
      *****************************************************************         
       8300-LOAD-TINVPAR-TABLE.                                                 
      *                                                                         
           EXEC SQL                                                             
               FETCH TINVPAR_CURSOR                                             
               INTO :INVPAR-LOC-NBR,                                            
                    :INVPAR-MFSTNG-IND                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               MOVE INVPAR-LOC-NBR      TO PV-STR-NDX                           
               MOVE INVPAR-MFSTNG-IND   TO WS-MFSTNG-IND   (PV-STR-NDX)         
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE 'Y' TO PS-TINVPAR-DONE-SW                                   
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '8300-LOAD-TINVPAR-TABLE' TO PV-PARAGRAPH-NAME              
               MOVE 'LOAD TABLE'        TO PV-DB2-OPER-ATTEMPTED                
               MOVE 'Y'        TO PS-TINVPAR-DONE-SW                            
               PERFORM 9000-SQL-ABEND                                           
           END-EVALUATE.                                                        
      *                                                                         
      *****************************************************************         
      * 8400-CLOSE-TINVPAR.                                                     
      *****************************************************************         
       8400-CLOSE-TINVPAR.                                                      
      *                                                                         
           EXEC SQL                                                             
               CLOSE TINVPAR_CURSOR                                             
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '8400-CLOSE-TINVPAR' TO PV-PARAGRAPH-NAME                    
              MOVE 'CLOSE CURSOR'       TO PV-DB2-OPER-ATTEMPTED                
              MOVE 'Y'                  TO PS-TINVPAR-DONE-SW                   
              PERFORM 9000-SQL-ABEND                                            
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 9000-SQL-ABEND.  STANDARD DB2 ERROR ABEND ROUTINE                       
      *                  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PBLM.        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
