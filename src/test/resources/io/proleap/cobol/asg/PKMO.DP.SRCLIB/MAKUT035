       IDENTIFICATION DIVISION.                                         12/18/95
       PROGRAM-ID.    MAKUT035.                                         MAKUT035
       AUTHOR.        LINDA GRIESBACH.                                     LV005
      ******************************************************************      32
      * THE PURPOSE OF THIS PROGRAM IS TO CREATE OH/OO RECORDS THAT    *      32
      * WILL BE LOADED INTO THE TSKUINRU ORACLE TABLE.  THE INVENTORY  *      32
      * LOAD FILE CONTAINS A RECORD FOR EACH SCL/STORE COMBINATION.    *      32
      * ALONG WITH THE SCL/STORE, THE RECORD CONTAINS THE OH AND OO    *      32
      * QUANTITIES FOR THAT SCL/STORE.                                 *      32
      *                                                                *      32
      * THIS PROGRAM DETERMINES THE CORRESPONDING ITEM CLUSTER AND     *      32
      * AND ORG UNIT ID'S FOR EACH SCL/STORE RECORD.  IT THEN CREATES  *      32
      * AN OUTPUT RECORD CONTAINING ITEM/ORG CLUSTER ID'S ALONG WITH   *      32
      * THE OH/OO QUANTITIES.                                          *      32
      ******************************************************************      32
      *  WR25478 - LINDA GRIESBACH                                     *      32
      *            STORE NBR EXPANSION                                 *      32
      ******************************************************************      32
221500*  04/08/2019 - CHG0221500 - JIM ARENDT                          *      32
221500*            MAINFRAME REFACTORING - INCREASING OCCURS OF        *      32
221500*            STORE TABLE (ORG-CLUSTR-ID) FROM 2000 TO 9999       *      32
221500*            TO ALLOW FOR 6666 (STOREFRONT) AND TEST LAB STORES. *      32
221500******************************************************************      32
                                                                        MAKUT035
       ENVIRONMENT DIVISION.                                            MAKUT035
       INPUT-OUTPUT SECTION.                                            MAKUT035
       FILE-CONTROL.                                                    MAKUT035
                                                                        MAKUT035
           SELECT SCL-STORE-INVENTORY-FILE                              MAKUT035
               ASSIGN TO INP01.                                         MAKUT035
                                                                        MAKUT035
           SELECT ORG-CLUSTR-FILE                                       MAKUT035
               ASSIGN TO INP02.                                         MAKUT035
                                                                        MAKUT035
           SELECT ITEM-CLUSTR-FILE                                      MAKUT035
               ASSIGN TO INP03.                                         MAKUT035
                                                                        MAKUT035
           SELECT INVENTORY-LOAD-FILE                                   MAKUT035
               ASSIGN TO OUT01.                                         MAKUT035
                                                                        MAKUT035
       DATA DIVISION.                                                   MAKUT035
       FILE SECTION.                                                    MAKUT035
       FD  SCL-STORE-INVENTORY-FILE                                     MAKUT035
           RECORDING MODE IS F                                          MAKUT035
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT035
       01  SCL-STORE-INVENTORY-RECORD  PIC X(109).                         CL**5
                                                                        MAKUT035
       FD  ORG-CLUSTR-FILE                                              MAKUT035
           RECORDING MODE IS F                                          MAKUT035
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT035
       01  ORG-CLUSTR-RECORD           PIC X(13).                          CL**5
                                                                        MAKUT035
       FD  ITEM-CLUSTR-FILE                                             MAKUT035
           RECORDING MODE IS F                                          MAKUT035
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT035
       01  ITEM-CLUSTR-RECORD          PIC X(16).                          CL**5
                                                                        MAKUT035
       FD  INVENTORY-LOAD-FILE                                          MAKUT035
           RECORDING MODE IS F                                          MAKUT035
           BLOCK CONTAINS 0 RECORDS.                                    MAKUT035
       01  INVENTORY-LOAD-RECORD       PIC X(41).                          CL**5
                                                                        MAKUT035
                                                                        MAKUT035
       WORKING-STORAGE SECTION.                                         MAKUT035
                                                                        MAKUT035
       01  PROGRAM-VARIABLES.                                           MAKUT035
           05  PV-INV-READ-COUNT       PIC S9(13) VALUE +0 COMP.        MAKUT035
           05  PV-INV-WRITTEN-COUNT    PIC S9(13) VALUE +0 COMP.        MAKUT035
           05  PV-NO-ITEM-COUNT        PIC S9(9)  VALUE +0 COMP.        MAKUT035
           05  PV-NO-ORG-COUNT         PIC S9(9)  VALUE +0 COMP.        MAKUT035
           05  PV-SUB                  PIC S9(4)  COMP-3.                       
           05  PV-ABEND-CODE           PIC S9(4)  VALUE +0 COMP.                
           05  PV-PREV-ITEM-CLUSTR-SCL PIC X(10)  VALUE SPACES.                 
                                                                                
       01  PROGRAM-SWITCHES.                                            MAKUT035
           05  PS-SCL-STORE-INV-EOF-SW PIC X(1)   VALUE 'N'.            MAKUT035
               88 PS-SCL-STORE-INV-EOF            VALUE 'Y'.            MAKUT035
               88 PS-SCL-STORE-INV-NOT-EOF        VALUE 'N'.            MAKUT035
           05  PS-ORG-CLUSTR-EOF-SW    PIC X(1)   VALUE 'N'.            MAKUT035
               88 PS-ORG-CLUSTR-EOF               VALUE 'Y'.            MAKUT035
               88 PS-ORG-CLUSTR-NOT-EOF           VALUE 'N'.            MAKUT035
           05  PS-ITEM-CLUSTR-EOF-SW   PIC X(1)   VALUE 'N'.            MAKUT035
               88 PS-ITEM-CLUSTR-EOF              VALUE 'Y'.            MAKUT035
               88 PS-ITEM-CLUSTR-NOT-EOF          VALUE 'N'.            MAKUT035
           05  PS-FIRST-ORG-CLUSTR-SW  PIC X(1)   VALUE 'Y'.            MAKUT035
               88 PS-FIRST-ORG-CLUSTR             VALUE 'Y'.            MAKUT035
               88 PS-NOT-FIRST-ORG-CLUSTR         VALUE 'N'.            MAKUT035
           05  PS-FIRST-ITEM-CLUSTR-SW PIC X(1)   VALUE 'Y'.            MAKUT035
               88 PS-FIRST-ITEM-CLUSTR            VALUE 'Y'.            MAKUT035
               88 PS-NOT-FIRST-ITEM-CLUSTR        VALUE 'N'.            MAKUT035
           05  PS-EMPTY-ORG-CLUSTR-FILE-SW                              MAKUT035
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ORG-CLUSTR-FILE        VALUE 'Y'.            MAKUT035
               88 PS-NOT-EMPTY-ORG-CLUSTR-FILE    VALUE 'N'.            MAKUT035
           05  PS-EMPTY-ITEM-CLUSTR-FILE-SW                             MAKUT035
                                       PIC X(1)   VALUE 'N'.                    
               88 PS-EMPTY-ITEM-CLUSTR-FILE       VALUE 'Y'.            MAKUT035
               88 PS-NOT-EMPTY-ITEM-CLUSTR-FILE   VALUE 'N'.            MAKUT035
           05  PS-ITEM-CLUSTR-MATCH-SW PIC X(1)   VALUE 'N'.            MAKUT035
               88 PS-ITEM-CLUSTR-MATCH            VALUE 'Y'.            MAKUT035
               88 PS-ITEM-CLUSTR-NO-MATCH         VALUE 'N'.            MAKUT035
           05  PS-ITEM-CLUSTR-FILE-SW  PIC X(1)   VALUE 'Y'.            MAKUT035
               88 PS-ITEM-CLUSTR-IN-FILE          VALUE 'Y'.            MAKUT035
               88 PS-ITEM-CLUSTR-NOT-IN-FILE      VALUE 'N'.            MAKUT035
                                                                        MAKUT035
       01  ORG-CLUSTR-TABLE.                                                    
221500*    05  ORG-CLUSTR-ID           OCCURS 2000 TIMES                        
221500     05  ORG-CLUSTR-ID           OCCURS 9999 TIMES                        
                                       PIC 9(9).                                
                                                                                
       01  ERROR-MESSAGE.                                                       
           05  FILLER                  PIC X(29)                                
                  VALUE '*** ERROR IN PROGRAM MAKUT035'.                        
           05  FILLER                  PIC X(14)                                
                  VALUE '; ERROR CODE ='.                                       
           05  EM-ERROR-CODE           PIC --------9                            
                                                  VALUE ZERO.                   
           05  FILLER                  PIC X(15)                                
                  VALUE '; ERROR DESC ='.                                       
           05  EM-ERROR-DESC           PIC X(65)  VALUE SPACES.                 
                                                                                
       01  LOG-MESSAGE.                                                 MAKUT035
           05  FILLER                  PIC X(23)                        MAKUT035
                   VALUE '*** PROGRAM MAKUT035 - '.                     MAKUT035
           05  LM-MESSAGE              PIC X(109) VALUE SPACES.         MAKUT035
                                                                        MAKUT035
       01  ITEM-NOT-FOUND-LOG-MSG.                                      MAKUT035
           05  FILLER                  PIC X(37)                        MAKUT035
                   VALUE 'ITEM CLUSTER ROW NOT FOUND FOR = '.           MAKUT035
           05  INF-LM-ITEM-NAME        PIC X(20)  VALUE SPACES.         MAKUT035
           05  INF-LM-SCL              PIC X(10)  VALUE SPACES.                 
                                                                        MAKUT035
       01  RUN-SUMMARY-LOG-MSG1.                                        MAKUT035
           05  FILLER                  PIC X(35)                        MAKUT035
                   VALUE 'SCL STORE INVENTORY RECORDS READ = '.         MAKUT035
           05  RSLM1-SCL-STOR-INV-RECS PIC Z(12)9 VALUE ZERO.           MAKUT035
                                                                        MAKUT035
       01  RUN-SUMMARY-LOG-MSG2.                                        MAKUT035
           05  FILLER                  PIC X(33)                        MAKUT035
                   VALUE 'INVENTORY LOAD RECORDS WRITTEN = '.           MAKUT035
           05  RSLM2-INV-LOAD-RECS     PIC Z(12)9 VALUE ZERO.           MAKUT035
                                                                        MAKUT035
       01  RUN-SUMMARY-LOG-MSG3.                                        MAKUT035
           05  FILLER                  PIC X(43)                        MAKUT035
                   VALUE 'NUMBER OF SCL STORE INVENTORY RECORDS DROPP'. MAKUT035
           05  FILLER                  PIC X(39)                        MAKUT035
                   VALUE 'ED DUE TO NO MATCHING ITEM CLUSTER ID= '.     MAKUT035
           05  RSLM3-EXTRACT-NO-ITEM-RECS                               MAKUT035
                                       PIC Z(8)9   VALUE ZERO.                  
                                                                        MAKUT035
       01  RUN-SUMMARY-LOG-MSG4.                                        MAKUT035
           05  FILLER                  PIC X(38)                        MAKUT035
                   VALUE 'NUMBER OF SCL STORE INVENTORY RECORDS '.      MAKUT035
           05  FILLER                  PIC X(38)                        MAKUT035
                   VALUE 'DROPPED DUE TO NO MATCHING ORG UNIT = '.      MAKUT035
           05  RSLM4-STORE-NO-ORG-RECS PIC Z(8)9   VALUE ZERO.          MAKUT035
                                                                        MAKUT035
       01  WS-ORG-CLUSTR-RECORD.                                                
           05  WS-ORG-CLUSTR-STORE     PIC X(4).                                
           05  WS-ORG-CLUSTR-ID        PIC 9(9).                                
                                                                        MAKUT035
       01  WS-ITEM-CLUSTR-RECORD.                                               
           05  WS-ITEM-CLUSTR-SCL      PIC X(6).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ITEM-CLUSTR-ID       PIC 9(9).                                
                                                                                
      * FIELDS FOR THE SCL STORE INVENTORY EXTRACT FILE                 MAKUT035
           COPY MARD015.                                                MAKUT035
                                                                        MAKUT035
      * FIELDS FOR THE INVENTORY LOAD FILE                              MAKUT035
           COPY MARD016.                                                MAKUT035
                                                                        MAKUT035
                                                                        MAKUT035
       EJECT                                                            MAKUT035
       PROCEDURE DIVISION.                                              MAKUT035
                                                                        MAKUT035
       0000-MAINLINE.                                                   MAKUT035
                                                                        MAKUT035
           PERFORM 1000-INITIALIZE                                      MAKUT035
              THRU 1000-EXIT.                                                   
                                                                        MAKUT035
           PERFORM 2000-CREATE-OH-OO-FILE                               MAKUT035
              THRU 2000-EXIT                                                    
              UNTIL PS-SCL-STORE-INV-EOF.                                       
                                                                        MAKUT035
           PERFORM 3000-CLOSE                                           MAKUT035
              THRU 3000-EXIT.                                                   
                                                                        MAKUT035
           DISPLAY 'MAKUT035 - SUCCESSFUL END'.                         MAKUT035
           MOVE ZERO TO RETURN-CODE.                                    MAKUT035
           GOBACK.                                                      MAKUT035
                                                                        MAKUT035
       0000-EXIT.                                                       MAKUT035
           EXIT.                                                                
                                                                        MAKUT035
                                                                        MAKUT035
       1000-INITIALIZE.                                                 MAKUT035
                                                                        MAKUT035
           OPEN INPUT  SCL-STORE-INVENTORY-FILE                         MAKUT035
                       ORG-CLUSTR-FILE                                  MAKUT035
                       ITEM-CLUSTR-FILE                                 MAKUT035
                OUTPUT INVENTORY-LOAD-FILE.                                     
                                                                        MAKUT035
           INITIALIZE ORG-CLUSTR-TABLE.                                         
                                                                                
           PERFORM 1100-LOAD-ORG-CLUSTR-FILE                                    
              THRU 1100-EXIT                                                    
             UNTIL PS-ORG-CLUSTR-EOF.                                           
                                                                                
           PERFORM 9997-READ-ITEM-CLUSTR-FILE                                   
              THRU 9997-EXIT.                                                   
                                                                                
           PERFORM 9998-READ-SCL-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                        MAKUT035
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1100-LOAD-ORG-CLUSTR-FILE.                                               
                                                                        MAKUT035
           READ ORG-CLUSTR-FILE                                                 
               INTO WS-ORG-CLUSTR-RECORD                                        
                   AT END                                                       
                       IF PS-FIRST-ORG-CLUSTR                                   
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ORG CLUSTER TABLE'                       
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           SET PS-ORG-CLUSTR-EOF TO TRUE                        
                           GO TO 1100-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT035
           IF PS-FIRST-ORG-CLUSTR                                               
               SET PS-NOT-FIRST-ORG-CLUSTR TO TRUE                              
           END-IF.                                                              
                                                                                
           IF WS-ORG-CLUSTR-STORE IS NUMERIC                                    
             AND WS-ORG-CLUSTR-STORE > ZERO                                     
               MOVE WS-ORG-CLUSTR-STORE TO PV-SUB                               
               MOVE WS-ORG-CLUSTR-ID    TO ORG-CLUSTR-ID (PV-SUB)               
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT035
                                                                        MAKUT035
       2000-CREATE-OH-OO-FILE.                                          MAKUT035
                                                                        MAKUT035
      * THE INPUT FILE IS SORTED BY SCL/STORE -                         MAKUT035
      * IT MAY CONTAIN THE SAME SCL FOR MULITPLE STORES -               MAKUT035
      * SO MULTIPLE INPUT RECORDS COULD BE BYPASSED                     MAKUT035
      * IF THE SCL IS NOT IN THE ITEM-CLUSTR-FILE                               
                                                                                
           IF MA015-ITEM-NAME(1:6) = WS-ITEM-CLUSTR-SCL                         
             OR MA015-ITEM-NAME(1:6) < WS-ITEM-CLUSTR-SCL                       
             OR PS-ITEM-CLUSTR-EOF                                              
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PS-ITEM-CLUSTR-NO-MATCH TO TRUE                              
               SET PS-ITEM-CLUSTR-IN-FILE  TO TRUE                              
               PERFORM 9997-READ-ITEM-CLUSTR-FILE                               
                  THRU 9997-EXIT                                                
                 UNTIL PS-ITEM-CLUSTR-MATCH                                     
                    OR PS-ITEM-CLUSTR-NOT-IN-FILE                               
                    OR PS-ITEM-CLUSTR-EOF                                       
           END-IF.                                                              
                                                                        MAKUT035
           IF MA015-ITEM-NAME(1:6) = WS-ITEM-CLUSTR-SCL                         
               MOVE MA015-STORE TO PV-SUB                                       
               MOVE ORG-CLUSTR-ID (PV-SUB)                                      
                                TO MA016-ORG-UNIT-CLUSTER-ID                    
               IF MA016-ORG-UNIT-CLUSTER-ID NOT = ZERO                          
                   MOVE WS-ITEM-CLUSTR-ID TO MA016-ITEM-CLUSTER-ID              
                   MOVE MA015-ONHAND-QTY  TO MA016-ONHAND-QTY                   
                   MOVE MA015-ONORD-QTY   TO MA016-ONORD-QTY                    
                                                                                
                   WRITE INVENTORY-LOAD-RECORD                                  
                       FROM MA016-INVENTORY-LOAD                                
                   ADD +1 TO PV-INV-WRITTEN-COUNT                               
               ELSE                                                             
                   ADD +1 TO PV-NO-ORG-COUNT                                    
               END-IF                                                           
           ELSE                                                                 
               IF MA015-ITEM-NAME(1:6) NOT =                                    
                                      PV-PREV-ITEM-CLUSTR-SCL                   
                   MOVE MA015-ITEM-NAME(1:6)                                    
                                   TO PV-PREV-ITEM-CLUSTR-SCL                   
                                      INF-LM-ITEM-NAME                          
                   MOVE ITEM-NOT-FOUND-LOG-MSG TO LM-MESSAGE                    
                   DISPLAY LOG-MESSAGE UPON SYSOUT                              
               END-IF                                                           
               ADD +1 TO PV-NO-ITEM-COUNT                                       
           END-IF.                                                              
                                                                                
           PERFORM 9998-READ-SCL-STORE-INV-FILE                                 
              THRU 9998-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-CLOSE.                                                              
                                                                                
           CLOSE SCL-STORE-INVENTORY-FILE                                       
                 ORG-CLUSTR-FILE                                                
                 ITEM-CLUSTR-FILE                                               
                 INVENTORY-LOAD-FILE.                                           
                                                                                
           MOVE PV-INV-READ-COUNT       TO RSLM1-SCL-STOR-INV-RECS.             
           MOVE RUN-SUMMARY-LOG-MSG1    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-INV-WRITTEN-COUNT    TO RSLM2-INV-LOAD-RECS.                 
           MOVE RUN-SUMMARY-LOG-MSG2    TO LM-MESSAGE.                          
           DISPLAY LOG-MESSAGE UPON SYSOUT.                                     
                                                                                
           MOVE PV-NO-ITEM-COUNT        TO RSLM3-EXTRACT-NO-ITEM-RECS.  MAKUT035
           MOVE RUN-SUMMARY-LOG-MSG3    TO LM-MESSAGE.                  MAKUT035
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT035
                                                                        MAKUT035
           MOVE PV-NO-ORG-COUNT         TO RSLM4-STORE-NO-ORG-RECS.     MAKUT035
           MOVE RUN-SUMMARY-LOG-MSG4    TO LM-MESSAGE.                  MAKUT035
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT035
                                                                        MAKUT035
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        MAKUT035
                                                                        MAKUT035
       9997-READ-ITEM-CLUSTR-FILE.                                              
                                                                        MAKUT035
           READ ITEM-CLUSTR-FILE                                                
               INTO WS-ITEM-CLUSTR-RECORD                                       
                   AT END                                                       
                       IF PS-FIRST-ITEM-CLUSTR                                  
                           MOVE -1003 TO EM-ERROR-CODE                          
                                         PV-ABEND-CODE                          
                           MOVE 'EMPTY ITEM CLUSTER TABLE'                      
                                      TO EM-ERROR-DESC                          
                           PERFORM 9999-ABEND                                   
                              THRU 9999-EXIT                                    
                       ELSE                                                     
                           MOVE SPACES TO WS-ITEM-CLUSTR-SCL                    
                           SET PS-ITEM-CLUSTR-EOF TO TRUE                       
                           GO TO 9997-EXIT                                      
                       END-IF                                                   
           END-READ.                                                            
                                                                        MAKUT035
           IF PS-FIRST-ITEM-CLUSTR                                              
               SET PS-NOT-FIRST-ITEM-CLUSTR TO TRUE                             
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT035
           IF MA015-ITEM-NAME(1:6) = WS-ITEM-CLUSTR-SCL                         
               SET PS-ITEM-CLUSTR-MATCH TO TRUE                                 
               GO TO 9997-EXIT                                                  
           END-IF.                                                              
                                                                        MAKUT035
           IF MA015-ITEM-NAME(1:6) < WS-ITEM-CLUSTR-SCL                         
               SET PS-ITEM-CLUSTR-NOT-IN-FILE TO TRUE                           
           END-IF.                                                              
                                                                        MAKUT035
       9997-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9998-READ-SCL-STORE-INV-FILE.                                            
                                                                        MAKUT035
           READ SCL-STORE-INVENTORY-FILE                                MAKUT035
               INTO MA015-ROLLUP-INVENTORY                              MAKUT035
                   AT END                                               MAKUT035
                       SET PS-SCL-STORE-INV-EOF TO TRUE                 MAKUT035
                   NOT AT END                                           MAKUT035
                       ADD +1 TO PV-INV-READ-COUNT                      MAKUT035
           END-READ.                                                            
                                                                                
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                        MAKUT035
           MOVE ERROR-MESSAGE TO LOG-MESSAGE.                           MAKUT035
                                                                        MAKUT035
           DISPLAY LOG-MESSAGE UPON SYSOUT.                             MAKUT035
                                                                        MAKUT035
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                         MAKUT035
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
