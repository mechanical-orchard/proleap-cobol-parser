000100 TITLE 'UPDATE PCT-TRN-CLSN-BAL TABLE - IT QTY    '.                      
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    PCKUP992.                                                 
000400 AUTHOR.        DENISE HAHN.                                              
000500 DATE-WRITTEN.  JANUARY, 2015.                                            
000600                                                                          
000610******************************************************************        
000621*                                                                *        
000622* THIS PROGRAM WILL USE THE EXTRACT FILE CREATED BY PCKUT987 OF  *        
000630* OLD IT FROM PCT_TRN_CLSN_DTL THAT HAVE NOT PURGED FORM THE     *        
000640* TABLES.  WILL UPDATE PCT_TRN_CLSN_BAL IT QUANTITIES            *        
000651*                                                                *        
000660******************************************************************        
000700                                                                          
000800 ENVIRONMENT DIVISION.                                                    
000900 INPUT-OUTPUT SECTION.                                                    
001000 FILE-CONTROL.                                                            
001300                                                                          
001310     SELECT IN-TRANSIT-EXTRACT-FILE                                       
001320         ASSIGN TO INP01.                                                 
001330                                                                          
001400 DATA DIVISION.                                                           
001500 FILE SECTION.                                                            
001600                                                                          
002410 FD  IN-TRANSIT-EXTRACT-FILE                                              
002420     RECORDING MODE IS F                                                  
002430     LABEL RECORDS ARE STANDARD                                           
002440     BLOCK CONTAINS 0 RECORDS.                                            
002450                                                                          
002460 01  IN-TRANSIT-RECORD                   PIC  X(50).                      
002470                                                                          
002500 WORKING-STORAGE SECTION.                                                 
002600                                                                          
003000 01  ABEND-CODE                          PIC S9(04)  VALUE ZERO           
003100                                                     COMP SYNC.           
003200 01  PV-PROGRAM-SWITCHES.                                                 
003300     05  PS-EOF-INPUT-SW                 PIC  X(01)  VALUE 'N'.           
003301         88 PS-EOF-INPUT                             VALUE 'Y'.           
003302         88 PS-EOF-INPUT-NO                          VALUE 'N'.           
003303                                                                          
003304 01  PV-PROGRAM-CONSTANTS.                                                
003305     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE 'PCKUP992'.          
003308                                                                          
003310 01  PV-PROGRAM-VARIABLES.                                                
003320     05  PV-ABEND-PARAGRAPH              PIC  X(30)  VALUE SPACE.         
003400     05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(30)  VALUE SPACE.         
003410     05  PV-SQL-CODE                     PIC -999.                        
003500                                                                          
004300     05 PV-COMMIT-COUNT                  PIC S9(09)  COMP-3               
004301                                                     VALUE ZERO.          
004302     05 PV-EXTRACT-COUNT                 PIC S9(09)  COMP-3               
004303                                                     VALUE ZERO.          
004310     05 PV-NO-UPDATE-COUNT               PIC S9(09)  COMP-3               
004311                                                     VALUE ZERO.          
004312     05 PV-UPDATE-COUNT                  PIC S9(09)  COMP-3               
004313                                                     VALUE ZERO.          
004320     05 PV-NOT-FND-COUNT                 PIC S9(09)  COMP-3               
004400                                                     VALUE ZERO.          
004401     05 PV-TOTAL-SELECT                  PIC S9(09)  COMP-3               
004402                                                     VALUE ZERO.          
004403     05 PV-SELECT-COUNT                 PIC S9(09)  COMP.                 
004500                                                                          
004900     05 PV-DISP-SQL                      PIC -999.                        
004901     05 PV-DISP-EXTR-CNT                 PIC ZZZ,ZZZ,ZZ9.                 
004902     05 PV-DISP-UPDATE-CNT               PIC ZZZ,ZZZ,ZZ9.                 
004903     05 PV-DISP-NO-UPD-CNT               PIC ZZZ,ZZZ,ZZ9.                 
004904     05 PV-DISP-NOTFND-CNT               PIC ZZZ,ZZZ,ZZ9.                 
004905     05 PV-DISP-BEFORE-QTY               PIC ZZZ,ZZZ,ZZ9-.                
004906     05 PV-DISP-AFTER-QTY                PIC ZZZ,ZZZ,ZZ9-.                
004907                                                                          
004910     05 PV-PURGE-DAYS                    PIC S9(9) COMP.                  
004920     05 PV-CURRENT-DATE                  PIC X(10).                       
004930     05 PV-PURGE-DATE                    PIC X(10).                       
005000                                                                          
005100*                                                                         
010620* IN-TRANSIT BALANCE TABLE                                                
010630*                                                                         
010640     EXEC SQL                                                             
010650          INCLUDE PCT00014                                                
010660     END-EXEC.                                                            
010670                                                                          
010680*                                                                         
010690* IN-TRANSIT DETAIL TABLE                                                 
010700*                                                                         
010800     EXEC SQL                                                             
010900          INCLUDE PCT00015                                                
011000     END-EXEC.                                                            
011100                                                                          
011320     COPY DPWS004.                                                        
011321                                                                          
011322     COPY PCRD080.                                                        
011323                                                                          
011330     COPY DPWS004.                                                        
011400                                                                          
011500     EXEC SQL                                                             
011600         INCLUDE SQLCA                                                    
011700     END-EXEC.                                                            
011800     COPY SQLCA2.                                                         
011900/                                                                         
012000 PROCEDURE DIVISION.                                                      
012100                                                                          
012200     PERFORM 1000-INITIALIZE.                                             
012300                                                                          
012400     PERFORM 2000-PROCESS                                                 
012500             UNTIL PS-EOF-INPUT.                                          
012600                                                                          
012710     PERFORM 9000-QUIT.                                                   
012800                                                                          
012900     GOBACK.                                                              
013000                                                                          
013100 1000-INITIALIZE.                                                         
013200                                                                          
013201     OPEN INPUT IN-TRANSIT-EXTRACT-FILE.                                  
013203                                                                          
013214     DISPLAY ' '.                                                         
013215     DISPLAY '*****************************************'.                 
013216     DISPLAY '*                                       *'.                 
013217     DISPLAY '* PCKUP992 - DELETE PCT_TRN_CLSN_DTL    *'.                 
013218     DISPLAY '*                                       *'.                 
013230                                                                          
014610     PERFORM 7000-READ-INPUT.                                             
014650                                                                          
014651     DISPLAY '* PURGE DATE:  '  PC080-PURGE-DATE.                         
014652     DISPLAY '*                                       *'.                 
014653     DISPLAY '*                                       *'.                 
014660                                                                          
014700 2000-PROCESS.                                                            
014710                                                                          
015110     PERFORM 8000-SELECT-IT-BAL.                                          
015120     PERFORM 8020-SELECT-IT-DTL.                                          
015121     IF  PCT00015-TRN-CLSN-PRCG-QTY = PCT00014-TRN-CLSN-QTY               
015122         ADD  1      TO   PV-NO-UPDATE-COUNT                              
015123     ELSE                                                                 
015130         PERFORM 8030-UPDATE-IT-BAL                                       
015140     END-IF.                                                              
017823                                                                          
017824     PERFORM 7000-READ-INPUT.                                             
017825                                                                          
017826                                                                          
017827 7000-READ-INPUT.                                                         
017828                                                                          
017829      READ IN-TRANSIT-EXTRACT-FILE                                        
017830      INTO PC080-INTRNST-PURGE-REC                                        
017831        AT END                                                            
017832           SET PS-EOF-INPUT TO TRUE                                       
017833        NOT AT END                                                        
017834           ADD  1  TO  PV-EXTRACT-COUNT                                   
017836      END-READ.                                                           
017850                                                                          
017900                                                                          
018000 8000-SELECT-IT-BAL.                                                      
033211                                                                          
033212      MOVE ZEROES                TO PCT00014-INTR-UPC-ID                  
033213                                    PCT00014-LOC-NBR.                     
033214      MOVE PC080-INTR-UPC-ID-KEY TO PCT00014-INTR-UPC-ID.                 
033215      MOVE PC080-LOC-NBR         TO PCT00014-LOC-NBR.                     
033216                                                                          
033217      EXEC SQL                                                            
033218           SELECT INTR_UPC_ID                                             
033219                , LOC_NBR                                                 
033220                , TRN_CLSN_CDE                                            
033221                , TRN_CLSN_QTY                                            
033222                , LAST_UPD_DTE                                            
033223             INTO :PCT00014-INTR-UPC-ID                                   
033224                , :PCT00014-LOC-NBR                                       
033225                , :PCT00014-TRN-CLSN-CDE                                  
033226                , :PCT00014-TRN-CLSN-QTY                                  
033227                , :PCT00014-LAST-UPD-DTE                                  
033228             FROM PCT_TRN_CLSN_BAL                                        
033229           WHERE INTR_UPC_ID        =  :PCT00014-INTR-UPC-ID              
033230             AND LOC_NBR            =  :PCT00014-LOC-NBR                  
033231             AND TRN_CLSN_CDE       =  :PC080-TRN-CLSN-CDE                
033232           WITH UR                                                        
033233      END-EXEC.                                                           
033234                                                                          
033242                                                                          
033243     EVALUATE TRUE                                                        
033244         WHEN SQLCODE = ZERO                                              
033246              CONTINUE                                                    
033260                                                                          
033280         WHEN SQLCODE = +100                                              
033281              ADD  1                     TO PV-NOT-FND-COUNT              
033282             DISPLAY 'NOT FOUND - '                                       
033283                     ' UPC ID:  ' PC080-INTR-UPC-ID                       
033284                     ' LOC NBR: ' PC080-LOC-NBR                           
033285                     ' CODE:    ' PC080-TRN-CLSN-CDE                      
033286                     ' DATE:    ' PC080-PURGE-DATE                        
033287                                                                          
033288         WHEN OTHER                                                       
033289             MOVE SQLCODE                TO PV-DISP-SQL                   
033290             DISPLAY '**************************************'             
033291             DISPLAY 'ERROR ISSUED ON                       '             
033292             DISPLAY ' SELECT INTRANSIT BALANCE             '             
033293             DISPLAY 'SQLCODE: ' PV-DISP-SQL                              
033294             DISPLAY 'UPC ID.: ' PC080-INTR-UPC-ID                        
033295             DISPLAY 'LOC NBR: ' PC080-LOC-NBR                            
033296             DISPLAY 'CODE...: ' PC080-TRN-CLSN-CDE                       
033297             DISPLAY 'DATE...: ' PC080-PURGE-DATE                         
033298             DISPLAY '**************************************'             
033299             PERFORM 9100-SQL-ABEND                                       
033300                                                                          
033301     END-EVALUATE.                                                        
033302                                                                          
033303 8010-COMMIT.                                                             
033304                                                                          
033305      EXEC SQL                                                            
033306         COMMIT                                                           
033307      END-EXEC.                                                           
033310                                                                          
033311     MOVE PV-EXTRACT-COUNT            TO PV-DISP-EXTR-CNT.                
033312     MOVE PV-UPDATE-COUNT             TO PV-DISP-UPDATE-CNT.              
033313     MOVE PV-NO-UPDATE-COUNT          TO PV-DISP-NO-UPD-CNT.              
033315     MOVE PV-NOT-FND-COUNT            TO PV-DISP-NOTFND-CNT.              
033317                                                                          
033318     DISPLAY '  * EXTRACT: '    PV-DISP-EXTR-CNT                          
033319             '  * UPDATE: '     PV-DISP-UPDATE-CNT                        
033320             '  * NO UPD: '     PV-DISP-NO-UPD-CNT                        
033321             '  * NOT FOUND: '  PV-DISP-NOTFND-CNT.                       
033323                                                                          
033330 8020-SELECT-IT-DTL.                                                      
033331                                                                          
033332      MOVE ZEROES            TO PCT00015-INTR-UPC-ID                      
033333                                PCT00015-LOC-NBR.                         
033334      MOVE PC080-INTR-UPC-ID-KEY TO PCT00015-INTR-UPC-ID.                 
033335      MOVE PC080-LOC-NBR         TO PCT00015-LOC-NBR.                     
033336                                                                          
033337      EXEC SQL                                                            
033338           SELECT SUM(TRN_CLSN_PRCG_QTY)                                  
033339             INTO :PCT00015-TRN-CLSN-PRCG-QTY                             
033340             FROM PCT_TRN_CLSN_DTL                                        
033341           WHERE INTR_UPC_ID        =  :PCT00015-INTR-UPC-ID              
033342             AND LOC_NBR            =  :PCT00015-LOC-NBR                  
033343             AND TRN_CLSN_CDE       =  :PC080-TRN-CLSN-CDE                
033344             AND NOT TRN_TYP_CDE IN ('NS','UM','MM','AM','SM')            
033345             AND DATE(IN_TRNST_TRN_TMST)                                  
033346                                    >  :PC080-PURGE-DATE                  
033347           WITH UR                                                        
033348      END-EXEC.                                                           
033349                                                                          
033356                                                                          
033357     EVALUATE TRUE                                                        
033358         WHEN SQLCODE = ZERO                                              
033359              CONTINUE                                                    
033368         WHEN SQLCODE = +100                                              
033369         WHEN SQLCODE = -305                                              
033370              MOVE ZEROES  TO  PCT00015-TRN-CLSN-PRCG-QTY                 
033371                                                                          
033372         WHEN OTHER                                                       
033373             MOVE SQLCODE                TO PV-DISP-SQL                   
033374             DISPLAY '**************************************'             
033375             DISPLAY 'ERROR ISSUED ON                       '             
033376             DISPLAY ' SUM INTRNSIT DETAIL                  '             
033377             DISPLAY 'SQLCODE: ' PV-DISP-SQL                              
033378             DISPLAY 'UPC ID.: ' PC080-INTR-UPC-ID                        
033379             DISPLAY 'LOC NBR: ' PC080-LOC-NBR                            
033380             DISPLAY 'CODE...: ' PC080-TRN-CLSN-CDE                       
033381             DISPLAY 'DATE...: ' PC080-INTR-UPC-ID                        
033382             DISPLAY '**************************************'             
033383             PERFORM 9100-SQL-ABEND                                       
033384                                                                          
033385     END-EVALUATE.                                                        
033386                                                                          
033387                                                                          
033388 8030-UPDATE-IT-BAL.                                                      
033389                                                                          
033390      MOVE ZEROES            TO PCT00015-INTR-UPC-ID                      
033391                                PCT00015-LOC-NBR.                         
033392      MOVE PC080-INTR-UPC-ID-KEY TO PCT00015-INTR-UPC-ID.                 
033393      MOVE PC080-LOC-NBR         TO PCT00015-LOC-NBR.                     
033394                                                                          
033395      MOVE PCT00014-TRN-CLSN-QTY      TO PV-DISP-BEFORE-QTY               
033396      MOVE PCT00015-TRN-CLSN-PRCG-QTY TO PV-DISP-AFTER-QTY                
033397      DISPLAY '  INTR-UPC-ID: '  PC080-INTR-UPC-ID                        
033398              '  LOC-NBR: '      PC080-LOC-NBR                            
033399              '  CODE: '         PC080-TRN-CLSN-CDE                       
033400              '  BEFORE: '       PV-DISP-BEFORE-QTY                       
033401              '  AFTER: '        PV-DISP-AFTER-QTY.                       
033402                                                                          
033403                                                                          
033404      EXEC SQL                                                            
033405           UPDATE PCT_TRN_CLSN_BAL                                        
033406              SET TRN_CLSN_QTY    =  :PCT00015-TRN-CLSN-PRCG-QTY          
033407           WHERE INTR_UPC_ID        =  :PCT00015-INTR-UPC-ID              
033408             AND LOC_NBR            =  :PCT00015-LOC-NBR                  
033409             AND TRN_CLSN_CDE       =  :PC080-TRN-CLSN-CDE                
033412      END-EXEC.                                                           
033413                                                                          
033414     EVALUATE TRUE                                                        
033415         WHEN SQLCODE = ZERO                                              
033417              ADD  1                     TO PV-COMMIT-COUNT               
033418              ADD  1                     TO PV-UPDATE-COUNT               
033419                                                                          
033420              IF  PV-COMMIT-COUNT > 10                                    
033421                  PERFORM 8010-COMMIT                                     
033422                  MOVE ZEROES  TO  PV-COMMIT-COUNT                        
033423              END-IF                                                      
033424                                                                          
033425         WHEN SQLCODE = +100                                              
033426              ADD  1                     TO PV-NOT-FND-COUNT              
033427                                                                          
033428         WHEN OTHER                                                       
033429             MOVE SQLCODE                TO PV-DISP-SQL                   
033430             DISPLAY '**************************************'             
033431             DISPLAY 'ERROR ISSUED ON                       '             
033432             DISPLAY ' UPDATE INTRANSIT BALANCE             '             
033433             DISPLAY 'SQLCODE: ' PV-DISP-SQL                              
033434             DISPLAY 'UPC ID.: ' PC080-INTR-UPC-ID                        
033435             DISPLAY 'LOC NBR: ' PC080-LOC-NBR                            
033436             DISPLAY 'CODE...: ' PC080-TRN-CLSN-CDE                       
033437             DISPLAY 'DATE...: ' PC080-INTR-UPC-ID                        
033438             DISPLAY '**************************************'             
033439             PERFORM 9100-SQL-ABEND                                       
033440                                                                          
033441     END-EVALUATE.                                                        
033442                                                                          
033443                                                                          
033444                                                                          
033450 9000-QUIT.                                                               
033500                                                                          
033600     MOVE PV-EXTRACT-COUNT            TO PV-DISP-EXTR-CNT.                
033800     MOVE PV-UPDATE-COUNT             TO PV-DISP-UPDATE-CNT.              
033810     MOVE PV-NO-UPDATE-COUNT          TO PV-DISP-NO-UPD-CNT.              
033900     MOVE PV-NOT-FND-COUNT            TO PV-DISP-NOTFND-CNT.              
034004                                                                          
034020     DISPLAY '*                                       *'.                 
034021     DISPLAY '* EXTRACT-COUNT..: ' PV-DISP-EXTR-CNT.                      
034022     DISPLAY '* UPDATE COUNT...: ' PV-DISP-UPDATE-CNT.                    
034023     DISPLAY '* NO UPD COUNT...: ' PV-DISP-NO-UPD-CNT.                    
034026     DISPLAY '* NOT FOUND COUNT: ' PV-DISP-NOTFND-CNT.                    
034028     DISPLAY '*                                       *'.                 
034030     DISPLAY '*****************************************'.                 
034031                                                                          
034040     CLOSE IN-TRANSIT-EXTRACT-FILE.                                       
035100                                                                          
035200 9100-SQL-ABEND.                                                          
035300******************************************************************        
035400*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
035500*  ERROR OR WARNING CODE OCCURS.                                          
035600******************************************************************        
035700     DISPLAY '9100-SQL-ABEND'.                                            
035800     DISPLAY '** ABEND     **'.                                           
035900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
036500     DISPLAY '**              ** = '                                      
036600     MOVE SQLCODE TO PV-SQL-CODE.                                         
036700     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
036800     MOVE +4013 TO ABEND-CODE.                                            
036900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
