00001 ******************************************************************11/17/95
00002  IDENTIFICATION DIVISION.                                         ARKUT050
00003 ******************************************************************   LV001
00004                                                                   ARKUT050
00005  PROGRAM-ID.             ARKUT050.                                ARKUT050
00006  AUTHOR.                 TAMARA KOLTUN.                           ARKUT050
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                 ARKUT050
00008  DATE-WRITTEN            5-15-95.                                 ARKUT050
00009  DATE-COMPILED.                                                   ARKUT050
00010                                                                   ARKUT050
00011 *-----------------------------------------------------------------ARKUT050
00012 * THIS SUBPROGRAM IS USED BY ARKUT025 TO TRANSLATE A CHECK AMOUNT ARKUT050
00013 * INTO WORDS. THIS WORDS AMOUNT WILL BE PRINTED ON THE CREDIT *   ARKUT050
00014 * REFUND CHECKS.                                                  ARKUT050
00015 *-----------------------------------------------------------------ARKUT050
00016                                                                   ARKUT050
00017 ******************************************************************ARKUT050
00018  ENVIRONMENT DIVISION.                                            ARKUT050
00019 ******************************************************************ARKUT050
00020                                                                   ARKUT050
00021  INPUT-OUTPUT SECTION.                                            ARKUT050
00022                                                                   ARKUT050
00023  FILE-CONTROL.                                                    ARKUT050
00024                                                                   ARKUT050
00025 ******************************************************************ARKUT050
00026  DATA DIVISION.                                                   ARKUT050
00027 ******************************************************************ARKUT050
00028                                                                   ARKUT050
00029  FILE SECTION.                                                    ARKUT050
00030                                                                   ARKUT050
00031                                                                   ARKUT050
00032 *------------------------------------------------------*          ARKUT050
00033  WORKING-STORAGE SECTION.                                         ARKUT050
00034 *------------------------------------------------------*          ARKUT050
00035 ***                                                               ARKUT050
00036  01 WS-CHECK-AMT-IN-WORDS-AREA.                                   ARKUT050
00037     05 WS-UNITS-WORDS-TBL                     PIC X(45) VALUE     ARKUT050
00038                   'ONE  TWO  THREEFOUR FIVE SIX  SEVENEIGHTNINE '.ARKUT050
00039     05 WS-UNITS-WORDS-TBL-RED REDEFINES WS-UNITS-WORDS-TBL.       ARKUT050
00040        10 WS-UNITS-WORD         OCCURS 9      PIC X(05).          ARKUT050
00041     05 WS-UNITS-WORD-LENGTH                   PIC 9(09) VALUE     ARKUT050
00042                    335443554.                                     ARKUT050
00043     05 WS-UNITS-WORD-LENGTH-RED REDEFINES WS-UNITS-WORD-LENGTH.   ARKUT050
00044        10 WS-UNITS-LENGTH-BYTE  OCCURS 9      PIC 9(01).          ARKUT050
00045 ***                                                               ARKUT050
00046     05 WS-TEENS-WORDS-TBL                     PIC X(81) VALUE     ARKUT050
00047      'ELEVEN   TWELVE   THIRTEEN FOURTEEN FIFTEEN  SIXTEEN  SEVENTARKUT050
00048 -    'EENEIGHTEEN NINETEEN '.                                     ARKUT050
00049     05 WS-TEENS-WORDS-TBL-RED REDEFINES WS-TEENS-WORDS-TBL.       ARKUT050
00050        10 WS-TEENS-WORD         OCCURS 9      PIC X(09).          ARKUT050
00051     05 WS-TEENS-WORD-LENGTH                   PIC 9(09) VALUE     ARKUT050
00052                    668877988.                                     ARKUT050
00053     05 WS-TEENS-WORD-LENGTH-RED REDEFINES WS-TEENS-WORD-LENGTH.   ARKUT050
00054        10 WS-TEENS-LENGTH-BYTE  OCCURS 9      PIC 9(01).          ARKUT050
00055 ***                                                               ARKUT050
00056     05 WS-TENS-WORDS-TBL                      PIC X(63) VALUE     ARKUT050
00057      'TEN    TWENTY THIRTY FORTY  FIFTY  SIXTY  SEVENTYEIGHTY NINEARKUT050
00058 -    'TY '.                                                       ARKUT050
00059     05 WS-TENS-WORDS-TBL-RED REDEFINES WS-TENS-WORDS-TBL.         ARKUT050
00060        10 WS-TENS-WORD          OCCURS 9      PIC X(07).          ARKUT050
00061     05 WS-TENS-WORD-LENGTH                    PIC 9(09) VALUE     ARKUT050
00062                    366655766.                                     ARKUT050
00063     05 WS-TENS-WORD-LENGTH-RED REDEFINES WS-TENS-WORD-LENGTH.     ARKUT050
00064        10 WS-TENS-LENGTH-BYTE   OCCURS 9      PIC 9(01).          ARKUT050
00065 ***                                                               ARKUT050
00066     05 WS-AMT-IN-WORDS-LINE-1                 PIC X(80).          ARKUT050
00067     05 WS-AMT-IN-WORDS-LINE-2                 PIC X(80).          ARKUT050
00068     05 WS-AMT-IN-WORDS-LINE-RED REDEFINES WS-AMT-IN-WORDS-LINE-2. ARKUT050
00069        10 WS-AMT-IN-WORDS-LINE-BYTE OCCURS 80 PIC X(01).          ARKUT050
00070     05 WS-LINE-LENGTH                         PIC 9(02) VALUE 80. ARKUT050
00071 ***                                                               ARKUT050
00072     05 WS-AMT-IN-WORDS-COUNTERS.                                  ARKUT050
00073        10 WS-SUB1                    PIC S9(04) COMP.             ARKUT050
00074        10 WS-SUB2                    PIC S9(04) COMP.             ARKUT050
00075        10 WS-SUM-SUB                 PIC S9(04) COMP VALUE 0.     ARKUT050
00076        10 WS-TIMES                   PIC S9(04) COMP.             ARKUT050
00077     05 WS-CHECK-AMT-NBR              PIC  9(05)V9(02).            ARKUT050
00078     05 WS-CHECK-AMT-1-RED REDEFINES WS-CHECK-AMT-NBR.             ARKUT050
00079        10 WS-CHECK-AMT-BYTE OCCURS 7 PIC  9(01).                  ARKUT050
00080     05 WS-CHECK-AMT-2-RED REDEFINES WS-CHECK-AMT-NBR.             ARKUT050
00081        10 WS-THOU                    PIC  9(02).                  ARKUT050
00082        10 WS-UNITS                   PIC  9(03).                  ARKUT050
00083        10 WS-CENTS                   PIC  9(02).                  ARKUT050
00084     05 WS-HOLD                       PIC  X(18).                  ARKUT050
00085     05 WS-HOLD-RED REDEFINES WS-HOLD.                             ARKUT050
00086        10 WS-HOLD-BYTE    OCCURS 18  PIC  X(01).                  ARKUT050
00087     05 WS-HOLD-LENGTH                PIC 9(02) VALUE 18.          ARKUT050
00088     05 WS-CLASS.                                                  ARKUT050
00089        10 WS-THOUSAND                PIC X(08) VALUE 'THOUSAND'.  ARKUT050
00090        10 WS-HUNDRED                 PIC X(07) VALUE 'HUNDRED'.   ARKUT050
00091     05 WS-AND                        PIC X(03) VALUE 'AND'.       ARKUT050
00092     05 WS-CLASS-DESC-LENGTH.                                      ARKUT050
00093        10 WS-THOUSAND-LENGTH         PIC 9(01) VALUE 8.           ARKUT050
00094        10 WS-HUNDRED-LENGTH          PIC 9(01) VALUE 7.           ARKUT050
00095     05 WS-AND-LENGTH                 PIC 9(01) VALUE 3.           ARKUT050
00096 ***                                                               ARKUT050
00097  01 PS-PROGRAM-SWITCHES.                                          ARKUT050
00098     05 PS-FINISH-JOB-IND                PIC  X(01) VALUE ' '.     ARKUT050
00099        88 FINISH-JOB                               VALUE 'F'.     ARKUT050
00100        88 DO-TRANSLATION                           VALUE 'T'.     ARKUT050
00101     05 PS-USE-WORDS-IND                 PIC  X(02) VALUE '  '.    ARKUT050
00102        88 RESET-IND                                VALUE '  '.    ARKUT050
00103        88 USE-UNITS-WORDS                          VALUE 'UN'.    ARKUT050
00104        88 USE-TEENS-WORDS                          VALUE 'TE'.    ARKUT050
00105        88 USE-TENS-WORDS                           VALUE 'TN'.    ARKUT050
00106 ****                                                              ARKUT050
00107  01 CNT-CONTROL-PARAMETERS.                                       ARKUT050
00108     05  CNT-CHECK-AMT             PIC S9(5)V99     VALUE ZERO.    ARKUT050
00109                                                                   ARKUT050
00110  LINKAGE SECTION.                                                 ARKUT050
00111 *-----------------*                                               ARKUT050
00112                                                                   ARKUT050
00113  01 CA-COMMON-AREA.                                               ARKUT050
00114     05 CA-REFUND-AMT              PIC 9(05)V9(02).                ARKUT050
00115     05 CA-REFUND-IN-WORDS         PIC X(80).                      ARKUT050
00116                                                                   ARKUT050
00117 *------------------------------------------------------*          ARKUT050
00118  PROCEDURE DIVISION                                               ARKUT050
00119         USING  CA-COMMON-AREA.                                    ARKUT050
00120 *------------------------------------------------------*          ARKUT050
00121  0000-MAIN.                                                       ARKUT050
00122      PERFORM 0001-INITIALIZATION     THRU 0001-EXIT.              ARKUT050
00123                                                                   ARKUT050
00124      IF WS-CHECK-AMT-NBR = 0                                      ARKUT050
00125         GO TO 0000-EXIT                                           ARKUT050
00126      END-IF.                                                      ARKUT050
00127                                                                   ARKUT050
00128      PERFORM 1000-BEGIN-TRANSLATION   THRU 1000-EXIT.             ARKUT050
00129                                                                   ARKUT050
00130      SET FINISH-JOB      TO TRUE.                                 ARKUT050
00131      COMPUTE WS-TIMES = WS-LINE-LENGTH - WS-SUM-SUB               ARKUT050
00132      PERFORM  9900-LOOP  THRU 9900-EXIT.                          ARKUT050
00133                                                                   ARKUT050
00134      MOVE WS-AMT-IN-WORDS-LINE-2  TO CA-REFUND-IN-WORDS.          ARKUT050
00135                                                                   ARKUT050
00136  0000-EXIT.  EXIT PROGRAM.                                        ARKUT050
00137                                                                   ARKUT050
00138  0001-INITIALIZATION.                                             ARKUT050
00139                                                                   ARKUT050
00140      SET  DO-TRANSLATION TO TRUE.                                 ARKUT050
00141      SET  RESET-IND      TO TRUE.                                 ARKUT050
00142      MOVE 0              TO WS-SUM-SUB.                           ARKUT050
00143                                                                   ARKUT050
00144      MOVE CA-REFUND-AMT  TO WS-CHECK-AMT-NBR.                     ARKUT050
00145                                                                   ARKUT050
00146      MOVE SPACE          TO WS-AMT-IN-WORDS-LINE-1                ARKUT050
00147                             WS-AMT-IN-WORDS-LINE-2                ARKUT050
00148                             WS-HOLD.                              ARKUT050
00149  0001-EXIT.  EXIT.                                                ARKUT050
00150                                                                   ARKUT050
00151  1000-BEGIN-TRANSLATION.                                          ARKUT050
00152                                                                   ARKUT050
00153      IF ( WS-THOU NOT = 0)                                        ARKUT050
00154         PERFORM 1200-THOU-ROUTINE    THRU 1200-EXIT               ARKUT050
00155         PERFORM 1300-UNITS-ROUTINE   THRU 1300-EXIT               ARKUT050
00156         PERFORM 1400-CENTS-ROUTINE   THRU 1400-EXIT               ARKUT050
00157      END-IF                                                       ARKUT050
00158                                                                   ARKUT050
00159      IF ( WS-THOU = 0 AND WS-UNITS NOT = 0)                       ARKUT050
00160         PERFORM 1300-UNITS-ROUTINE   THRU 1300-EXIT               ARKUT050
00161         PERFORM 1400-CENTS-ROUTINE   THRU 1400-EXIT               ARKUT050
00162      END-IF                                                       ARKUT050
00163                                                                   ARKUT050
00164      IF ( WS-THOU = 0 AND WS-UNITS = 0)                           ARKUT050
00165         PERFORM 1400-CENTS-ROUTINE   THRU 1400-EXIT               ARKUT050
00166      END-IF.                                                      ARKUT050
00167  1000-EXIT.  EXIT.                                                ARKUT050
00168                                                                   ARKUT050
00169                                                                   ARKUT050
00170  1200-THOU-ROUTINE.                                               ARKUT050
00171      PERFORM 1220-TEN-THOUSANDS        THRU 1220-EXIT.            ARKUT050
00172      PERFORM 1230-THOUSANDS            THRU 1230-EXIT.            ARKUT050
00173                                                                   ARKUT050
00174      MOVE SPACE         TO WS-HOLD.                               ARKUT050
00175      MOVE WS-THOUSAND   TO WS-HOLD.                               ARKUT050
00176      COMPUTE  WS-TIMES = WS-THOUSAND-LENGTH + 1.                  ARKUT050
00177      PERFORM 9900-LOOP          THRU 9900-LOOP.                   ARKUT050
00178  1200-EXIT.  EXIT.                                                ARKUT050
00179                                                                   ARKUT050
00180  1220-TEN-THOUSANDS.                                              ARKUT050
00181      IF WS-CHECK-AMT-BYTE (1) = 0                                 ARKUT050
00182         SET USE-UNITS-WORDS       TO TRUE                         ARKUT050
00183         GO TO 1220-EXIT                                           ARKUT050
00184      END-IF.                                                      ARKUT050
00185                                                                   ARKUT050
00186      IF WS-CHECK-AMT-BYTE (1) = 1                                 ARKUT050
00187         IF WS-CHECK-AMT-BYTE (2) > 0                              ARKUT050
00188           SET USE-TEENS-WORDS       TO TRUE                       ARKUT050
00189           GO TO 1220-EXIT                                         ARKUT050
00190          END-IF                                                   ARKUT050
00191      END-IF.                                                      ARKUT050
00192                                                                   ARKUT050
00193      IF WS-CHECK-AMT-BYTE (1) > 1  OR                             ARKUT050
00194         (WS-CHECK-AMT-BYTE (1) = 1 AND WS-CHECK-AMT-BYTE (2) = 0) ARKUT050
00195           SET USE-TENS-WORDS         TO TRUE                      ARKUT050
00196           MOVE WS-CHECK-AMT-BYTE (1) TO WS-SUB1                   ARKUT050
00197           PERFORM  9800-MOVES        THRU 9800-EXIT               ARKUT050
00198           SET USE-UNITS-WORDS        TO TRUE                      ARKUT050
00199      END-IF.                                                      ARKUT050
00200                                                                   ARKUT050
00201  1220-EXIT.  EXIT.                                                ARKUT050
00202                                                                   ARKUT050
00203  1230-THOUSANDS.                                                  ARKUT050
00204      IF WS-CHECK-AMT-BYTE (2) = 0                                 ARKUT050
00205         GO TO 1230-EXIT                                           ARKUT050
00206      END-IF.                                                      ARKUT050
00207                                                                   ARKUT050
00208      MOVE WS-CHECK-AMT-BYTE (2)     TO WS-SUB1.                   ARKUT050
00209      PERFORM  9800-MOVES          THRU 9800-EXIT.                 ARKUT050
00210      SET RESET-IND                  TO TRUE.                      ARKUT050
00211  1230-EXIT.  EXIT.                                                ARKUT050
00212                                                                   ARKUT050
00213  1300-UNITS-ROUTINE.                                              ARKUT050
00214      PERFORM 1310-HUNDREDS                THRU 1310-EXIT          ARKUT050
00215      PERFORM 1320-TEN-HUNDREDS            THRU 1320-EXIT          ARKUT050
00216      PERFORM 1330-ONES                    THRU 1330-EXIT.         ARKUT050
00217  1300-EXIT.  EXIT.                                                ARKUT050
00218                                                                   ARKUT050
00219  1310-HUNDREDS.                                                   ARKUT050
00220      IF WS-CHECK-AMT-BYTE (3) = 0                                 ARKUT050
00221         GO TO 1310-EXIT                                           ARKUT050
00222      END-IF.                                                      ARKUT050
00223                                                                   ARKUT050
00224      SET USE-UNITS-WORDS          TO TRUE                         ARKUT050
00225      MOVE WS-CHECK-AMT-BYTE (3)   TO WS-SUB1                      ARKUT050
00226      PERFORM  9800-MOVES        THRU 9800-EXIT                    ARKUT050
00227      SET RESET-IND                TO TRUE.                        ARKUT050
00228                                                                   ARKUT050
00229      MOVE SPACE         TO WS-HOLD.                               ARKUT050
00230      MOVE WS-HUNDRED    TO WS-HOLD.                               ARKUT050
00231      COMPUTE WS-TIMES = WS-HUNDRED-LENGTH + 1                     ARKUT050
00232                                                                   ARKUT050
00233      PERFORM 9900-LOOP          THRU 9900-LOOP.                   ARKUT050
00234                                                                   ARKUT050
00235  1310-EXIT.  EXIT.                                                ARKUT050
00236                                                                   ARKUT050
00237  1320-TEN-HUNDREDS.                                               ARKUT050
00238      IF WS-CHECK-AMT-BYTE (4) = 0                                 ARKUT050
00239         SET USE-UNITS-WORDS   TO TRUE                             ARKUT050
00240         GO TO 1320-EXIT                                           ARKUT050
00241      END-IF.                                                      ARKUT050
00242                                                                   ARKUT050
00243      IF WS-CHECK-AMT-BYTE (4) = 1                                 ARKUT050
00244         IF WS-CHECK-AMT-BYTE (5) > 0                              ARKUT050
00245            SET USE-TEENS-WORDS  TO TRUE                           ARKUT050
00246            GO TO 1320-EXIT                                        ARKUT050
00247         END-IF                                                    ARKUT050
00248      END-IF.                                                      ARKUT050
00249                                                                   ARKUT050
00250                                                                   ARKUT050
00251      IF WS-CHECK-AMT-BYTE (4) > 1 OR                              ARKUT050
00252         (WS-CHECK-AMT-BYTE (4) = 1 AND WS-CHECK-AMT-BYTE (5) = 0) ARKUT050
00253         SET USE-TENS-WORDS           TO TRUE                      ARKUT050
00254         MOVE WS-CHECK-AMT-BYTE (4)   TO WS-SUB1                   ARKUT050
00255         PERFORM  9800-MOVES        THRU 9800-EXIT                 ARKUT050
00256         SET  USE-UNITS-WORDS         TO TRUE                      ARKUT050
00257      END-IF.                                                      ARKUT050
00258                                                                   ARKUT050
00259  1320-EXIT.  EXIT.                                                ARKUT050
00260                                                                   ARKUT050
00261  1330-ONES.                                                       ARKUT050
00262      IF WS-CHECK-AMT-BYTE (5) = 0                                 ARKUT050
00263         GO TO 1330-EXIT                                           ARKUT050
00264      END-IF.                                                      ARKUT050
00265                                                                   ARKUT050
00266      MOVE WS-CHECK-AMT-BYTE (5)   TO WS-SUB1                      ARKUT050
00267      PERFORM  9800-MOVES        THRU 9800-EXIT                    ARKUT050
00268      SET RESET-IND                TO TRUE.                        ARKUT050
00269  1330-EXIT.  EXIT.                                                ARKUT050
00270                                                                   ARKUT050
00271  1400-CENTS-ROUTINE.                                              ARKUT050
00272      MOVE SPACE      TO WS-HOLD                                   ARKUT050
00273      IF WS-THOU > 0  OR  WS-UNITS > 0                             ARKUT050
00274         STRING WS-AND                                             ARKUT050
00275                ' '                                                ARKUT050
00276                WS-CENTS                                           ARKUT050
00277                '/100 DOLLARS'                                     ARKUT050
00278             DELIMITED BY SIZE                                     ARKUT050
00279             INTO WS-HOLD                                          ARKUT050
00280         END-STRING                                                ARKUT050
00281      ELSE                                                         ARKUT050
00282         STRING WS-CENTS                                           ARKUT050
00283                '/100 DOLLARS'                                     ARKUT050
00284             DELIMITED BY SIZE                                     ARKUT050
00285             INTO WS-HOLD                                          ARKUT050
00286         END-STRING                                                ARKUT050
00287      END-IF                                                       ARKUT050
00288      MOVE WS-HOLD-LENGTH    TO   WS-TIMES.                        ARKUT050
00289      PERFORM 9900-LOOP       THRU 9900-LOOP.                      ARKUT050
00290  1400-EXIT.  EXIT.                                                ARKUT050
00291                                                                   ARKUT050
00292  9800-MOVES.                                                      ARKUT050
00293      IF USE-UNITS-WORDS                                           ARKUT050
00294         MOVE WS-UNITS-WORD (WS-SUB1)        TO WS-HOLD            ARKUT050
00295         MOVE WS-UNITS-LENGTH-BYTE (WS-SUB1) TO WS-SUB2            ARKUT050
00296      END-IF                                                       ARKUT050
00297                                                                   ARKUT050
00298      IF USE-TEENS-WORDS                                           ARKUT050
00299         MOVE WS-TEENS-WORD (WS-SUB1)        TO WS-HOLD            ARKUT050
00300         MOVE WS-TEENS-LENGTH-BYTE (WS-SUB1) TO WS-SUB2            ARKUT050
00301      END-IF                                                       ARKUT050
00302                                                                   ARKUT050
00303      IF USE-TENS-WORDS                                            ARKUT050
00304         MOVE WS-TENS-WORD (WS-SUB1)        TO WS-HOLD             ARKUT050
00305         MOVE WS-TENS-LENGTH-BYTE (WS-SUB1) TO WS-SUB2             ARKUT050
00306      END-IF                                                       ARKUT050
00307                                                                   ARKUT050
00308      COMPUTE WS-TIMES = WS-SUB2 + 1.                              ARKUT050
00309                                                                   ARKUT050
00310      PERFORM 9900-LOOP          THRU 9900-LOOP.                   ARKUT050
00311  9800-EXIT.  EXIT.                                                ARKUT050
00312                                                                   ARKUT050
00313  9900-LOOP.                                                       ARKUT050
00314      MOVE 0        TO WS-SUB1                                     ARKUT050
00315      PERFORM WS-TIMES TIMES                                       ARKUT050
00316        ADD 1       TO WS-SUM-SUB                                  ARKUT050
00317        ADD 1       TO WS-SUB1                                     ARKUT050
00318        IF FINISH-JOB                                              ARKUT050
00319           MOVE '*'               TO WS-AMT-IN-WORDS-LINE-BYTE     ARKUT050
00320                                              (WS-SUM-SUB)         ARKUT050
00321        ELSE                                                       ARKUT050
00322           MOVE WS-HOLD-BYTE(WS-SUB1)                              ARKUT050
00323                                  TO WS-AMT-IN-WORDS-LINE-BYTE     ARKUT050
00324                                              (WS-SUM-SUB)         ARKUT050
00325        END-IF                                                     ARKUT050
00326      END-PERFORM.                                                 ARKUT050
00327  9900-EXIT.  EXIT.                                                ARKUT050
