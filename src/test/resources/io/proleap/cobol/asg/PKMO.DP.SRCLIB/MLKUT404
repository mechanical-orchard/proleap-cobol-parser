000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.              MLKUT404.                                       
000400 AUTHOR.                  TESS BESTE.                                     
000500 INSTALLATION.            KOHLS DEPARTMENT STORES.                        
000600 DATE-WRITTEN.            04/15/98.                                       
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*              DEPARTMENT MERCHANDISE LEDGER EXTRACT.                     
001100*                                                                         
001200* THIS PROGRAM WILL EXTRACT FROM THE MONTH-TO-DATE DEPARTMENT             
001300* LEDGER TABLE, THE DATA TO BE REPORTED ON THE DEPARTMENT LEDGER          
001400* REPORT.                                                                 
001500*                                                                         
001600* DATA IS EXTRACTED FOR THE OPEN FISCAL YEAR FROM THE BEGINNING OF        
001700* THE YEAR THROUGH THE MAX POSTING MONTH. IF THE MAX POSTING DATE         
001800* HAS CROSSED OVER INTO THE NEXT FISCAL YEAR, THE EXTRACT ENDS            
001900* WITH THE POSTING MONTH INSTEAD OF THE MAX POSTING MONTH.                
002000*                                                                         
002100* A CONTROL CARD MAY BE USED TO OVERRIDE THE USE OF THE MAX               
002200* POSTING DATE. IF A CONTOL CARD IS USED, DATA WILL BE EXTRACTED          
002300* FROM THE BEGINNING OF THE FISCAL YEAR (INTO WHICH THE CONTROL           
002400* CARD DATE FALLS) THROUGH THE CONTROL CARD DATE.                         
002500*                                                                         
002600* THE EXTRACT IS DRIVEN BY A CURSOR OF DEPARTMENTS THAT WERE              
002700* ACTIVE ON MBS FOR MERCHANDISE LEDGER AS OF THE END OF THE               
002800* OPEN FISCAL MONTH PLUS ANY OTHER DEPARTMENTS THAT HAVE                  
002900* ACTIVITY ON TMNDPT DURING THE PERIOD BEING REPORTED.                    
003000*                                                                         
003100* FOR EACH DEPARTMENT, THE PROGRAM  EXTRACTS THE BEGINNING                
003200* INVENTORY AT COST AND RETAIL FOR THE FISCAL YEAR, AND                   
003300* ALL COST AND RETAIL AMOUNTS FOR EACH MONTH.                             
      *                                                                         
      *                                                                         
      * JBF 06/30/00 -CHANGED THE PROGRAM TO USE THE POSTING MONTH              
      *               INSTEAD OF THE OPEN FISCAL MONTH WHEN THE                 
      *               FISCAL YEARS ARE NOT THE SAME.                            
W26603******************************************************************        
W26603* CHG ID   DATE        DESCRIPTION                               *        
W26603* ------   ----------  ----------------------------------------- *        
W26603* W26603   03-17-2004  STORE EXPANSION.  EXPANDED LRECL OF OUT01 *        
W26603*                      FILE BECAUSE OF CHANGES TO MLRD102.       *        
W26603*                      ADD 'WITH UR' TO DB2 SELECTS.             *        
W26603*                      - RONNA MCDONALD                          *        
003400******************************************************************        
003500                                                                          
003600                                                                          
003700                                                                          
003800 ENVIRONMENT DIVISION.                                                    
003900                                                                          
004000                                                                          
004100 CONFIGURATION SECTION.                                                   
004200                                                                          
004300 SOURCE-COMPUTER. IBM-3090.                                               
004400 OBJECT-COMPUTER. IBM-3090.                                               
004500                                                                          
004600                                                                          
004700 INPUT-OUTPUT SECTION.                                                    
004800                                                                          
004900 FILE-CONTROL.                                                            
005000                                                                          
005100     SELECT CONTROL-CARD-FILE                                             
005200         ASSIGN TO INP01.                                                 
005300                                                                          
005400     SELECT LEDGER-EXTRACT-DATE-FILE                                      
005500         ASSIGN TO OUT01.                                                 
005600                                                                          
005700     SELECT LEDGER-EXTRACT-FILE                                           
005800         ASSIGN TO OUT02.                                                 
005900                                                                          
006000                                                                          
006100                                                                          
006200 DATA DIVISION.                                                           
006300                                                                          
006400                                                                          
006500 FILE SECTION.                                                            
006600                                                                          
006700 FD  CONTROL-CARD-FILE                                                    
006800     RECORDING MODE IS F                                                  
006900     LABEL RECORDS ARE STANDARD                                           
007000     BLOCK CONTAINS 0 RECORDS.                                            
007100                                                                          
007200 01  CONTROL-CARD-RECORD              PIC X(80).                          
007300                                                                          
007400 FD  LEDGER-EXTRACT-DATE-FILE                                             
007500     RECORDING MODE IS F                                                  
007600     LABEL RECORDS ARE STANDARD                                           
007700     BLOCK CONTAINS 0 RECORDS.                                            
007800                                                                          
007900 01  LEDGER-EXTRACT-DATE-RECORD       PIC X(80).                          
008000                                                                          
008100 FD  LEDGER-EXTRACT-FILE                                                  
008200     RECORDING MODE IS F                                                  
008300     LABEL RECORDS ARE STANDARD                                           
008400     BLOCK CONTAINS 0 RECORDS.                                            
008500                                                                          
W26603 01  LEDGER-EXTRACT-RECORD            PIC X(1288).                        
008700                                                                          
008800                                                                          
008900 WORKING-STORAGE SECTION.                                                 
009000                                                                          
009100****************************************************************          
009200* RECORD LAYOUT FOR LEDGER EXTRACT DATE RECORD                            
009300****************************************************************          
009400                                                                          
009500     COPY MLRD114.                                                        
009600                                                                          
009700****************************************************************          
009800* RECORD LAYOUT FOR LEDGER EXTRACT RECORD                                 
009900****************************************************************          
010000                                                                          
010100     COPY MLRD102.                                                        
010200                                                                          
010300****************************************************************          
010400*  ERROR MESSAGE AREA FOR DB2                                             
010500****************************************************************          
010600                                                                          
010700     COPY DPWS004.                                                        
010800                                                                          
011300 01  CC-CONTROL-CARD.                                                     
011400     05  CC-EXTRACT-DTE               PIC X(10).                          
011500     05  FILLER                       PIC X(70).                          
011600                                                                          
011700                                                                          
011800 01  PC-PROGRAM-CONSTANTS.                                                
011900     05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.              
012000                                                                          
012100 01  PS-PROGRAM-SWITCHES.                                                 
012200     05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.               
012300         88  PS-END-OF-CONTROL-CARDS             VALUE 'Y'.               
012400     05  PS-DATE-FOUND-SW             PIC X      VALUE 'N'.               
012500         88  PS-DATE-FOUND                       VALUE 'Y'.               
012600         88  PS-DATE-NOT-FOUND                   VALUE 'N'.               
012700     05  PS-END-OF-DEPT-NBR-CSR-SW    PIC X      VALUE 'N'.               
012800         88  PS-END-OF-DEPT-NBR-CSR              VALUE 'Y'.               
012900     05  PS-END-OF-LEDGER-CSR-SW      PIC X      VALUE 'N'.               
013000         88  PS-END-OF-LEDGER-CSR                VALUE 'Y'.               
013100         88  PS-NOT-END-OF-LEDGER-CSR            VALUE 'N'.               
013200     05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.               
013300         88  PS-MBS-INFO-FOUND                   VALUE 'Y'.               
013400         88  PS-MBS-INFO-NOT-FOUND               VALUE 'N'.               
013500                                                                          
013600 01  PV-PROGRAM-VARIABLES.                                                
013700     05  PV-FSCL-YR-NBR               PIC X(04)  VALUE SPACE.             
013800     05  PV-FSCL-YR-BEG-DTE           PIC X(10)  VALUE SPACE.             
013900     05  PV-PREV-FSCL-YR-END-DTE      PIC X(10)  VALUE SPACE.             
014000                                                                          
014100     05  PV-DB2-OPER-ATTEMPTED        PIC X(30).                          
014200     05  PV-PARAGRAPH-NAME            PIC X(30).                          
014300 01  SS-SUBSCRIPTS.                                                       
014400     05  SS-FSCL-MN                   PIC S9(04) COMP SYNC                
014500                                                 VALUE ZERO.              
014600 01  ABEND-CODE                       PIC S9(04) COMP                     
014700                                                 VALUE ZERO.              
014800                                                                          
014900****************************************************************          
015000* LEDGER MONTH-TO-DATE DEPARTMENT TABLE                                   
015100****************************************************************          
015200                                                                          
015300     EXEC SQL                                                             
015400         INCLUDE TMNDPT                                                   
015500     END-EXEC.                                                            
015600                                                                          
015700****************************************************************          
015800* MERCHANDISE LEDGER CONTROL TABLE                                        
015900****************************************************************          
016000                                                                          
016100     EXEC SQL                                                             
016200        INCLUDE TMLCNTL                                                   
016300     END-EXEC.                                                            
016400                                                                          
016500****************************************************************          
016600* DATE ATTRIBUTE TABLE                                                    
016700****************************************************************          
016800                                                                          
016900     EXEC SQL                                                             
017000         INCLUDE TDTATTR                                                  
017100     END-EXEC.                                                            
017200                                                                          
017300****************************************************************          
017400* MBS MERCHANDISE AREA TABLE                                              
017500****************************************************************          
017600                                                                          
017700     EXEC SQL                                                             
017800         INCLUDE TMDSEAR                                                  
017900     END-EXEC.                                                            
018000                                                                          
018100****************************************************************          
018200* MBS APPLICATION AVAILABILITY TABLE                                      
018300****************************************************************          
018400                                                                          
018500     EXEC SQL                                                             
018600         INCLUDE TAPLAVL                                                  
018700     END-EXEC.                                                            
018800                                                                          
018900****************************************************************          
019000*  COMMUNICATION AREAS FOR DB2                                            
019100****************************************************************          
019200                                                                          
019300     EXEC SQL                                                             
019400         INCLUDE SQLCA                                                    
019500     END-EXEC.                                                            
019600                                                                          
019700****************************************************************          
019800*  DEPARTMENT NUMBER CUROSR                                               
019900****************************************************************          
020000                                                                          
020100     EXEC SQL                                                             
020200        DECLARE DEPT-NBR-CSR CURSOR FOR                                   
020300            SELECT                                                        
020400                DEPT_NBR                                                  
020500            FROM                                                          
020600                TMDSEAR A                                                 
020700               ,TAPLAVL B                                                 
020800                                                                          
020900            WHERE A.OPERCO_NBR     = :PC-OPERCO-NBR                       
021000              AND A.MDSELV_CDE     = 'DPT'                                
021100              AND :ML114-EXTRACT-DTE                                      
021200                   BETWEEN A.STRT_DTE                                     
021300                       AND A.END_DTE                                      
021400              AND A.MDSEAR_DKEY    =  B.MDSEAR_DKEY                       
021500              AND B.APLSYS_CDE     = 'ML'                                 
021600              AND :ML114-EXTRACT-DTE                                      
021700                   BETWEEN B.MDSEAR_STRT_DTE                              
021800                       AND B.MDSEAR_END_DTE                               
022000                                                                          
022100        UNION                                                             
022200                                                                          
022300            SELECT DISTINCT                                               
022400                DEPT_NBR                                                  
022500            FROM                                                          
022600                TMNDPT                                                    
022700            WHERE FSCL_MN_END_DTE                                         
022800                      BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
022900                          AND :ML114-EXTRACT-DTE                          
022910                                                                          
023000        FOR FETCH ONLY                                                    
W26603        WITH UR                                                           
023100     END-EXEC.                                                            
023200                                                                          
023300****************************************************************          
023400*  LEDGER CURSOR                                                          
023500****************************************************************          
023600                                                                          
023700     EXEC SQL                                                             
023800        DECLARE LEDGER-CSR CURSOR FOR                                     
023900            SELECT                                                        
024000                FSCL_MN_NBR                                               
024100               ,FSCL_MN_END_DTE                                           
024200               ,SLS_RTL_AMT                                               
024300               ,MKDN_RTL_AMT                                              
024400               ,MKUP_RTL_AMT                                              
024500               ,XFER_IN_RTL_AMT                                           
024600               ,XFER_OUT_RTL_AMT                                          
024700               ,RCPT_RTL_AMT                                              
024800               ,RCPT_NET_COST_AMT                                         
024900               ,PADJ_RTL_AMT                                              
025000               ,PADJ_NET_COST_AMT                                         
025100               ,INV_ADJ_RTL_AMT                                           
025200               ,SHNK_RTL_AMT                                              
025300               ,END_INV_RTL_AMT                                           
025400               ,END_INV_COST_AMT                                          
025500               ,SLS_COST_AMT                                              
025600               ,GRO_MRGN_AMT                                              
025700            FROM                                                          
025800                TMNDPT                                                    
025900            WHERE FSCL_MN_END_DTE                                         
026000                      BETWEEN :PV-PREV-FSCL-YR-END-DTE                    
026100                          AND :ML114-EXTRACT-DTE                          
026200              AND DEPT_NBR = :MDSEAR-DEPT-NBR                             
026300            FOR FETCH ONLY                                                
W26603        WITH UR                                                           
026400     END-EXEC.                                                            
026500                                                                          
026600                                                                          
026700 LINKAGE SECTION.                                                         
026800                                                                          
026900                                                                          
027000                                                                          
027100 PROCEDURE DIVISION.                                                      
027200                                                                          
027300                                                                          
027400 1000-MAIN-MODULE.                                                        
027500                                                                          
027600     OPEN INPUT  CONTROL-CARD-FILE                                        
027700          OUTPUT LEDGER-EXTRACT-DATE-FILE                                 
027800                 LEDGER-EXTRACT-FILE.                                     
027900                                                                          
028000     PERFORM 1150-SET-EXTRACT-DTE.                                        
028100                                                                          
028200     PERFORM 8200-OPEN-DEPT-NBR-CSR.                                      
028300                                                                          
028400     PERFORM 8300-FETCH-DEPT-NBR-CSR.                                     
028500                                                                          
028600     PERFORM 2000-EXTRACT-DEPT-DATA                                       
028700         UNTIL PS-END-OF-DEPT-NBR-CSR.                                    
028800                                                                          
028900     PERFORM 8400-CLOSE-DEPT-NBR-CSR.                                     
029000                                                                          
029100     CLOSE CONTROL-CARD-FILE                                              
029200           LEDGER-EXTRACT-DATE-FILE                                       
029300           LEDGER-EXTRACT-FILE.                                           
029400                                                                          
029500     STOP RUN.                                                            
029600                                                                          
029700                                                                          
029800 1150-SET-EXTRACT-DTE.                                                    
029900                                                                          
030000     PERFORM 7000-READ-CONTROL-CARD-FILE.                                 
030100                                                                          
030200     IF PS-END-OF-CONTROL-CARDS                                           
030300     OR CC-EXTRACT-DTE = SPACE                                            
030400         PERFORM 1170-USE-ML-CNTL-DTES                                    
030500     ELSE                                                                 
030600         DISPLAY '** INPUT CONTROL CARD: ' CC-CONTROL-CARD                
030700         PERFORM 1160-USE-CONTROL-CARD-DTE                                
030800     END-IF.                                                              
030900                                                                          
031000     DISPLAY '** SELECTED EXTRACT DATE: ' ML114-EXTRACT-DTE.              
031100                                                                          
031200     PERFORM  7100-WRITE-LEDGER-EXTRACT-DATE.                             
031300                                                                          
031400*    GET THE MONTH-END DATE FOR THE LAST MONTH OF THE PREVIOUS            
031500*    FISCAL YEAR.  THIS DATE WILL BE USED AS THE STARTING DATE            
031600*    FOR EXTRACTING THE MONTHLY DATA SO THAT THE BEGINNING                
031700*    INVENTORIES FOR THE OPEN FISCAL YEAR CAN BE DETERMINED.              
031800                                                                          
031900     MOVE PV-FSCL-YR-BEG-DTE  TO DTATTR-KY-DTE.                           
032000                                                                          
032100     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
032200                                                                          
032300     IF PS-DATE-NOT-FOUND                                                 
032400         DISPLAY '** ABEND **'                                            
032500         DISPLAY '** PROGRAM MLKUT404 **'                                 
032600         DISPLAY '** PARAGRAPH 1150-SET-EXTRACT-DTE'                      
032700         DISPLAY '** PREV FSCL YR BEG DTE IS NOT A VALID DATE.'           
032800         MOVE +4008 TO ABEND-CODE                                         
032900         PERFORM 9999-ABEND                                               
033000     END-IF.                                                              
033100                                                                          
033200     MOVE DTATTR-PFM-END-DTE TO PV-PREV-FSCL-YR-END-DTE.                  
033300                                                                          
033400                                                                          
033500 1160-USE-CONTROL-CARD-DTE.                                               
033600                                                                          
033700     DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'               
033800                                                                          
033900     MOVE CC-EXTRACT-DTE TO DTATTR-KY-DTE.                                
034000                                                                          
034100     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
034200                                                                          
034300     IF  PS-DATE-FOUND                                                    
034400     AND DTATTR-KY-DTE = DTATTR-FSCL-MN-END-DTE                           
034500         MOVE DTATTR-FSCL-MN-END-DTE TO ML114-EXTRACT-DTE                 
034600         MOVE DTATTR-FSCL-MN-NBR     TO ML114-EXTRACT-MN-NBR              
034700         MOVE DTATTR-FSCL-YR-BEG-DTE TO PV-FSCL-YR-BEG-DTE                
034800     ELSE                                                                 
034900         DISPLAY '** ABEND **'                                            
035000         DISPLAY '** PROGRAM MLKUT404 **'                                 
035100         DISPLAY '** PARAGRAPH 1160-USE-CONTROL-CARD-DTE'                 
035200         DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'                
035300         DISPLAY '** MONTH-END DATE. '                                    
035400         MOVE +4003 TO ABEND-CODE                                         
035500         PERFORM 9999-ABEND                                               
035600     END-IF.                                                              
035700                                                                          
035800                                                                          
035900 1170-USE-ML-CNTL-DTES.                                                   
036000                                                                          
036100     PERFORM 8000-GET-ML-CNTL-DTES.                                       
                                                                                
036300     DISPLAY                                                              
036400         '** POSTING MONTH DATE: '     MLCNTL-PSTG-FSCL-MN-DTE.           
036500     DISPLAY                                                              
036600         '** MAX POSTING MONTH DATE: ' MLCNTL-MXPSTG-FSCL-MN-DTE.         
036700                                                                          
036800     MOVE MLCNTL-MXPSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                     
036900                                                                          
037000     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
037100                                                                          
037200     IF PS-DATE-NOT-FOUND                                                 
037300         DISPLAY '** ABEND **'                                            
037400         DISPLAY '** PROGRAM MLKUT404 **'                                 
037500         DISPLAY '** PARAGRAPH 1160-USE-ML-CNTL-DTE'                      
037600         DISPLAY '** MAX POSTING DATE IS NOT A VALID DATE.'               
037700         MOVE +4008 TO ABEND-CODE                                         
037800         PERFORM 9999-ABEND                                               
037900     END-IF.                                                              
038000                                                                          
038100     MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE.                 
038200     MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR.              
038300     MOVE DTATTR-FSCL-YR-NBR        TO PV-FSCL-YR-NBR.                    
038400     MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE.                
038500                                                                          
038600     MOVE MLCNTL-PSTG-FSCL-MN-DTE TO DTATTR-KY-DTE.                       
038700                                                                          
038800     PERFORM 8050-GET-DATE-ATTRIBUTES.                                    
038900                                                                          
039000     IF PS-DATE-NOT-FOUND                                                 
039100         DISPLAY '** ABEND **'                                            
039200         DISPLAY '** PROGRAM MLKUT404 **'                                 
039300         DISPLAY '** PARAGRAPH 1160-USE-ML-CNTL-DTE'                      
039400         DISPLAY                                                          
               '** POSTING FISCAL MONTH DATE IS NOT A VALID DATE.'              
039500         MOVE +4008 TO ABEND-CODE                                         
039600         PERFORM 9999-ABEND                                               
039700     END-IF.                                                              
039800                                                                          
039900     IF DTATTR-FSCL-YR-NBR NOT = PV-FSCL-YR-NBR                           
040000         DISPLAY '** USING POSTING MONTH FOR EXTRACT DATE.'               
040100         MOVE DTATTR-FSCL-MN-END-DTE    TO ML114-EXTRACT-DTE              
040200         MOVE DTATTR-FSCL-MN-NBR        TO ML114-EXTRACT-MN-NBR           
040300         MOVE DTATTR-FSCL-YR-BEG-DTE    TO PV-FSCL-YR-BEG-DTE             
040400     ELSE                                                                 
040500         DISPLAY '** USING MAX POSTING MONTH FOR EXTRACT DATE.'           
040600     END-IF.                                                              
040700                                                                          
040800                                                                          
040900 2000-EXTRACT-DEPT-DATA.                                                  
041000                                                                          
041100     INITIALIZE ML102-LEDGER-EXTRACT-REC.                                 
041200                                                                          
041300     PERFORM 2050-GET-MBS-INFO.                                           
041400                                                                          
041500     SET PS-NOT-END-OF-LEDGER-CSR TO TRUE.                                
041600     PERFORM 8500-OPEN-LEDGER-CSR.                                        
041700                                                                          
041800     PERFORM 8600-FETCH-LEDGER-CSR.                                       
041900                                                                          
042000     PERFORM 3000-EXTRACT-MONTHS-FOR-DEPT                                 
042100         UNTIL PS-END-OF-LEDGER-CSR.                                      
042200                                                                          
042300     PERFORM 8700-CLOSE-LEDGER-CSR.                                       
042400                                                                          
042500     PERFORM 7200-WRITE-LEDGER-EXTRACT.                                   
042600     PERFORM 8300-FETCH-DEPT-NBR-CSR.                                     
042700                                                                          
042800                                                                          
042900 2050-GET-MBS-INFO.                                                       
043000                                                                          
043100*****************************************************************         
043200* GET THE MBS INFO THAT WAS EFFECTIVE FOR THE MOST RECENT PERIOD          
043300* BEING REPORTED. IF THE DEPARTMENT WAS NOT ACTIVE AT THAT TIME,          
043400* GET THE MOST RECENT MBS INFO FOR THE DEPARTMENT.                        
043500*****************************************************************         
043600                                                                          
043700     PERFORM 8060-GET-EFFECTIVE-MBS-INFO.                                 
043800                                                                          
043900     IF PS-MBS-INFO-NOT-FOUND                                             
044000         PERFORM 8070-GET-LATEST-MBS-INFO                                 
044100     END-IF.                                                              
044200                                                                          
044300     MOVE MDSEAR-DEPT-NBR     TO ML102-DEPT-NBR.                          
044400                                                                          
044500     IF PS-MBS-INFO-FOUND                                                 
044600         MOVE MDSEAR-GMA-NBR  TO ML102-GMA-NBR                            
044700         MOVE MDSEAR-DMA-NBR  TO ML102-DMA-NBR                            
044800         MOVE MDSEAR-BYR-NBR  TO ML102-BYR-NBR                            
044900     END-IF.                                                              
045000                                                                          
045100                                                                          
045200 3000-EXTRACT-MONTHS-FOR-DEPT.                                            
045300                                                                          
045400     IF MNDPT-FSCL-MN-END-DTE = PV-PREV-FSCL-YR-END-DTE                   
045500         MOVE MNDPT-END-INV-COST-AMT                                      
045600                          TO ML102-BEG-INV-COST-AMT                       
045700         MOVE MNDPT-END-INV-RTL-AMT                                       
045800                          TO ML102-BEG-INV-RTL-AMT                        
045900     ELSE                                                                 
046000         MOVE MNDPT-FSCL-MN-NBR                                           
046100                          TO SS-FSCL-MN                                   
046200         MOVE MNDPT-SLS-RTL-AMT                                           
046300                          TO ML102-SLS-RTL-AMT       (SS-FSCL-MN)         
046400         MOVE MNDPT-MKDN-RTL-AMT                                          
046500                          TO ML102-MKDN-RTL-AMT      (SS-FSCL-MN)         
046600         MOVE MNDPT-MKUP-RTL-AMT                                          
046700                          TO ML102-MKUP-RTL-AMT      (SS-FSCL-MN)         
046800         MOVE MNDPT-XFER-IN-RTL-AMT                                       
046900                          TO ML102-XFER-IN-RTL-AMT   (SS-FSCL-MN)         
047000         MOVE MNDPT-XFER-OUT-RTL-AMT                                      
047100                          TO ML102-XFER-OUT-RTL-AMT  (SS-FSCL-MN)         
047200         MOVE MNDPT-RCPT-RTL-AMT                                          
047300                          TO ML102-RCPT-RTL-AMT      (SS-FSCL-MN)         
047400         MOVE MNDPT-RCPT-NET-COST-AMT                                     
047500                          TO ML102-RCPT-NET-COST-AMT (SS-FSCL-MN)         
047600         MOVE MNDPT-PADJ-RTL-AMT                                          
047700                          TO ML102-PADJ-RTL-AMT      (SS-FSCL-MN)         
047800         MOVE MNDPT-PADJ-NET-COST-AMT                                     
047900                          TO ML102-PADJ-NET-COST-AMT (SS-FSCL-MN)         
048000         MOVE MNDPT-INV-ADJ-RTL-AMT                                       
048100                          TO ML102-INV-ADJ-RTL-AMT   (SS-FSCL-MN)         
048200         MOVE MNDPT-SHNK-RTL-AMT                                          
048300                          TO ML102-SHNK-RTL-AMT      (SS-FSCL-MN)         
048400         MOVE MNDPT-END-INV-RTL-AMT                                       
048500                          TO ML102-END-INV-RTL-AMT   (SS-FSCL-MN)         
048600         MOVE MNDPT-END-INV-COST-AMT                                      
048700                          TO ML102-END-INV-COST-AMT  (SS-FSCL-MN)         
048800         MOVE MNDPT-SLS-COST-AMT                                          
048900                          TO ML102-SLS-COST-AMT      (SS-FSCL-MN)         
049000         MOVE MNDPT-GRO-MRGN-AMT                                          
049100                          TO ML102-GRO-MRGN-AMT      (SS-FSCL-MN)         
049200     END-IF.                                                              
049300                                                                          
049400     PERFORM 8600-FETCH-LEDGER-CSR.                                       
049500                                                                          
049600                                                                          
049700 7000-READ-CONTROL-CARD-FILE.                                             
049800                                                                          
049900     READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
050000         AT END SET PS-END-OF-CONTROL-CARDS TO TRUE.                      
050100                                                                          
050200                                                                          
050300 7100-WRITE-LEDGER-EXTRACT-DATE.                                          
050400                                                                          
050500     WRITE LEDGER-EXTRACT-DATE-RECORD                                     
050600         FROM ML114-LEDGER-EXTRACT-DATE-REC.                              
050700                                                                          
050800                                                                          
050900 7200-WRITE-LEDGER-EXTRACT.                                               
051000                                                                          
051100     WRITE LEDGER-EXTRACT-RECORD                                          
051200         FROM ML102-LEDGER-EXTRACT-REC.                                   
051300                                                                          
051400                                                                          
051500 8000-GET-ML-CNTL-DTES.                                                   
051600                                                                          
051700     EXEC SQL                                                             
051800          SELECT                                                          
051900              PSTG_FSCL_MN_DTE                                            
052000             ,MXPSTG_FSCL_MN_DTE                                          
052100          INTO                                                            
052200              :MLCNTL-PSTG-FSCL-MN-DTE                                    
052300             ,:MLCNTL-MXPSTG-FSCL-MN-DTE                                  
052400          FROM                                                            
052500              TMLCNTL                                                     
W26603        WITH UR                                                           
052600     END-EXEC.                                                            
052700                                                                          
052800     EVALUATE TRUE                                                        
052900         WHEN SQLCODE = ZERO                                              
053000             CONTINUE                                                     
053100         WHEN OTHER                                                       
053200             MOVE '8000-GET-ML-CNTL-DTES' TO PV-PARAGRAPH-NAME            
053300             MOVE 'SELECT ROW FROM TMLCNTL'                               
053400                                          TO PV-DB2-OPER-ATTEMPTED        
053500             PERFORM 9998-SQL-ABEND                                       
053600     END-EVALUATE.                                                        
053700                                                                          
053800                                                                          
053900 8050-GET-DATE-ATTRIBUTES.                                                
054000                                                                          
054100     EXEC SQL                                                             
054200          SELECT                                                          
054300              FSCL_YR_NBR                                                 
054400             ,FSCL_MN_NBR                                                 
054500             ,FSCL_MN_END_DTE                                             
054600             ,FSCL_YR_BEG_DTE                                             
054700             ,PFM_END_DTE                                                 
054800          INTO                                                            
054900              :DTATTR-FSCL-YR-NBR                                         
055000             ,:DTATTR-FSCL-MN-NBR                                         
055100             ,:DTATTR-FSCL-MN-END-DTE                                     
055200             ,:DTATTR-FSCL-YR-BEG-DTE                                     
055300             ,:DTATTR-PFM-END-DTE                                         
055400          FROM                                                            
055500              TDTATTR                                                     
055600          WHERE                                                           
055700              KY_DTE = :DTATTR-KY-DTE                                     
W26603        WITH UR                                                           
055800     END-EXEC.                                                            
055900                                                                          
056000     EVALUATE TRUE                                                        
056100         WHEN SQLCODE = ZERO                                              
056200             SET PS-DATE-FOUND     TO TRUE                                
056300         WHEN SQLCODE = +100                                              
056400             SET PS-DATE-NOT-FOUND TO TRUE                                
056500         WHEN OTHER                                                       
056600             DISPLAY '** DTATTR-KY-DTE: ' DTATTR-KY-DTE                   
056700             MOVE    '8050-GET-DATE-ATTRIBUTES'                           
056800                                          TO PV-PARAGRAPH-NAME            
056900             MOVE    'SELECT ROW FROM TDTATTR'                            
057000                                          TO PV-DB2-OPER-ATTEMPTED        
057100             PERFORM 9998-SQL-ABEND                                       
057200     END-EVALUATE.                                                        
057300                                                                          
057400                                                                          
057500 8060-GET-EFFECTIVE-MBS-INFO.                                             
057600                                                                          
057700******************************************************************        
057800* GET THE MBS INFO FROM THE EFFECTIVE ROW FOR THE DEPARTMENT.             
057900******************************************************************        
058000                                                                          
058100     EXEC SQL                                                             
058200          SELECT                                                          
058300              GMA_NBR                                                     
058400             ,DMA_NBR                                                     
058500             ,BYR_NBR                                                     
058600          INTO                                                            
058700              :MDSEAR-GMA-NBR                                             
058800             ,:MDSEAR-DMA-NBR                                             
058900             ,:MDSEAR-BYR-NBR                                             
059000          FROM                                                            
059100              TMDSEAR                                                     
059200          WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
059300            AND MDSELV_CDE  = 'DPT'                                       
059400            AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
059500            AND :ML114-EXTRACT-DTE                                        
059600                 BETWEEN STRT_DTE                                         
059700                     AND END_DTE                                          
W26603        WITH UR                                                           
059800     END-EXEC.                                                            
059900                                                                          
060000     EVALUATE TRUE                                                        
060100         WHEN SQLCODE = ZERO                                              
060200             SET PS-MBS-INFO-FOUND TO TRUE                                
060300         WHEN SQLCODE = +100                                              
060400             SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
060500         WHEN OTHER                                                       
060600             MOVE '8060-GET-EFFECTIVE-MBS-INFO'                           
060700                                          TO PV-PARAGRAPH-NAME            
060800             MOVE 'SELECT ROW FROM TMDSEAR'                               
060900                                          TO PV-DB2-OPER-ATTEMPTED        
061000             PERFORM 9998-SQL-ABEND                                       
061100     END-EVALUATE.                                                        
061200                                                                          
061300                                                                          
061400 8070-GET-LATEST-MBS-INFO.                                                
061500                                                                          
061600******************************************************************        
061700* GET THE MBS INFO FROM THE MOST RECENT ROW FOR THE DEPARTMENT.           
061800******************************************************************        
061900                                                                          
062000     EXEC SQL                                                             
062100          SELECT                                                          
062200              GMA_NBR                                                     
062300             ,DMA_NBR                                                     
062400             ,BYR_NBR                                                     
062500          INTO                                                            
062600              :MDSEAR-GMA-NBR                                             
062700             ,:MDSEAR-DMA-NBR                                             
062800             ,:MDSEAR-BYR-NBR                                             
062900          FROM                                                            
063000              TMDSEAR                                                     
063100          WHERE OPERCO_NBR  = :PC-OPERCO-NBR                              
063200            AND MDSELV_CDE  = 'DPT'                                       
063300            AND DEPT_NBR    = :MDSEAR-DEPT-NBR                            
063400            AND END_DTE     =                                             
063500                  (SELECT MAX (END_DTE)                                   
063600                   FROM   TMDSEAR                                         
063700                   WHERE  OPERCO_NBR = :PC-OPERCO-NBR                     
063800                     AND  MDSELV_CDE = 'DPT'                              
063900                     AND  DEPT_NBR   = :MDSEAR-DEPT-NBR)                  
W26603        WITH UR                                                           
064000     END-EXEC.                                                            
064100                                                                          
064200     EVALUATE TRUE                                                        
064300         WHEN SQLCODE = ZERO                                              
064400             SET PS-MBS-INFO-FOUND TO TRUE                                
064500         WHEN SQLCODE = +100                                              
064600             SET PS-MBS-INFO-NOT-FOUND TO TRUE                            
064700         WHEN OTHER                                                       
064800             MOVE '8070-GET-LATEST-MBS-INFO'                              
064900                                          TO PV-PARAGRAPH-NAME            
065000             MOVE 'SELECT ROW FROM TMDSEAR'                               
065100                                          TO PV-DB2-OPER-ATTEMPTED        
065200             PERFORM 9998-SQL-ABEND                                       
065300     END-EVALUATE.                                                        
065400                                                                          
065500                                                                          
065600 8200-OPEN-DEPT-NBR-CSR.                                                  
065700                                                                          
065800     EXEC SQL                                                             
065900          OPEN DEPT-NBR-CSR                                               
066000     END-EXEC.                                                            
066100                                                                          
066200     EVALUATE TRUE                                                        
066300         WHEN SQLCODE = ZERO                                              
066400             CONTINUE                                                     
066500         WHEN OTHER                                                       
066600             MOVE '8200-OPEN-DEPT-NBR-CSR'                                
066700                                          TO PV-PARAGRAPH-NAME            
066800             MOVE 'OPEN DEPT-NBR-CSR'     TO PV-DB2-OPER-ATTEMPTED        
066900             PERFORM 9998-SQL-ABEND                                       
067000     END-EVALUATE.                                                        
067100                                                                          
067200                                                                          
067300 8300-FETCH-DEPT-NBR-CSR.                                                 
067400                                                                          
067500     EXEC SQL                                                             
067600         FETCH                                                            
067700             DEPT-NBR-CSR                                                 
067800         INTO                                                             
067900             :MDSEAR-DEPT-NBR                                             
068000     END-EXEC.                                                            
068100                                                                          
068200     EVALUATE TRUE                                                        
068300         WHEN SQLCODE = ZERO                                              
068400             CONTINUE                                                     
068500         WHEN SQLCODE = +100                                              
068600             SET PS-END-OF-DEPT-NBR-CSR TO TRUE                           
068700         WHEN OTHER                                                       
068800             MOVE '8300-FETCH-DEPT-NBR-CSR'                               
068900                                          TO PV-PARAGRAPH-NAME            
069000             MOVE 'FETCH DEPT-NBR-CSR'    TO PV-DB2-OPER-ATTEMPTED        
069100             PERFORM 9998-SQL-ABEND                                       
069200     END-EVALUATE.                                                        
069300                                                                          
069400                                                                          
069500 8400-CLOSE-DEPT-NBR-CSR.                                                 
069600                                                                          
069700     EXEC SQL                                                             
069800         CLOSE DEPT-NBR-CSR                                               
069900     END-EXEC.                                                            
070000                                                                          
070100     EVALUATE TRUE                                                        
070200         WHEN SQLCODE = ZERO                                              
070300             CONTINUE                                                     
070400         WHEN OTHER                                                       
070500             MOVE '8400-CLOSE-DEPT-NBR-CSR'                               
070600                                          TO PV-PARAGRAPH-NAME            
070700             MOVE 'CLOSE DEPT-NBR-CSR'    TO PV-DB2-OPER-ATTEMPTED        
070800             PERFORM 9998-SQL-ABEND                                       
070900     END-EVALUATE.                                                        
071000                                                                          
071100                                                                          
071200 8500-OPEN-LEDGER-CSR.                                                    
071300                                                                          
071400     EXEC SQL                                                             
071500          OPEN LEDGER-CSR                                                 
071600     END-EXEC.                                                            
071700                                                                          
071800     EVALUATE TRUE                                                        
071900         WHEN SQLCODE = ZERO                                              
072000             CONTINUE                                                     
072100         WHEN OTHER                                                       
072200             MOVE '8500-OPEN-LEDGER-CSR'                                  
072300                                      TO PV-PARAGRAPH-NAME                
072400             MOVE 'OPEN LEDGER-CSR'   TO PV-DB2-OPER-ATTEMPTED            
072500             PERFORM 9998-SQL-ABEND                                       
072600     END-EVALUATE.                                                        
072700                                                                          
072800                                                                          
072900 8600-FETCH-LEDGER-CSR.                                                   
073000                                                                          
073100     EXEC SQL                                                             
073200         FETCH                                                            
073300             LEDGER-CSR                                                   
073400         INTO                                                             
073500            :MNDPT-FSCL-MN-NBR                                            
073600           ,:MNDPT-FSCL-MN-END-DTE                                        
073700           ,:MNDPT-SLS-RTL-AMT                                            
073800           ,:MNDPT-MKDN-RTL-AMT                                           
073900           ,:MNDPT-MKUP-RTL-AMT                                           
074000           ,:MNDPT-XFER-IN-RTL-AMT                                        
074100           ,:MNDPT-XFER-OUT-RTL-AMT                                       
074200           ,:MNDPT-RCPT-RTL-AMT                                           
074300           ,:MNDPT-RCPT-NET-COST-AMT                                      
074400           ,:MNDPT-PADJ-RTL-AMT                                           
074500           ,:MNDPT-PADJ-NET-COST-AMT                                      
074600           ,:MNDPT-INV-ADJ-RTL-AMT                                        
074700           ,:MNDPT-SHNK-RTL-AMT                                           
074800           ,:MNDPT-END-INV-RTL-AMT                                        
074900           ,:MNDPT-END-INV-COST-AMT                                       
075000           ,:MNDPT-SLS-COST-AMT                                           
075100           ,:MNDPT-GRO-MRGN-AMT                                           
075200     END-EXEC.                                                            
075300                                                                          
075400     EVALUATE TRUE                                                        
075500         WHEN SQLCODE = ZERO                                              
075600             CONTINUE                                                     
075700         WHEN SQLCODE = +100                                              
075800             SET PS-END-OF-LEDGER-CSR TO TRUE                             
075900         WHEN OTHER                                                       
076000             MOVE '8600-FETCH-LEDGER-CSR' TO PV-PARAGRAPH-NAME            
076100             MOVE 'FETCH LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
076200             PERFORM 9998-SQL-ABEND                                       
076300     END-EVALUATE.                                                        
076400                                                                          
076500                                                                          
076600 8700-CLOSE-LEDGER-CSR.                                                   
076700                                                                          
076800     EXEC SQL                                                             
076900         CLOSE LEDGER-CSR                                                 
077000     END-EXEC.                                                            
077100                                                                          
077200     EVALUATE TRUE                                                        
077300         WHEN SQLCODE = ZERO                                              
077400             CONTINUE                                                     
077500         WHEN OTHER                                                       
077600             MOVE '8700-CLOSE-LEDGER-CSR' TO PV-PARAGRAPH-NAME            
077700             MOVE 'CLOSE LEDGER-CSR'      TO PV-DB2-OPER-ATTEMPTED        
077800             PERFORM 9998-SQL-ABEND                                       
077900     END-EVALUATE.                                                        
078000                                                                          
078100                                                                          
078200 9998-SQL-ABEND.                                                          
078300                                                                          
078400     MOVE +4013 TO ABEND-CODE.                                            
078500     DISPLAY ' '.                                                         
078600     DISPLAY '** ABEND **         ** = SQLERROR'.                         
078700     DISPLAY '** PROGRAM          ** = MLKUT404'.                         
078800     DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
078900     DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
079000                                                                          
079100*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
079200*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
079300     COPY DPPD004.                                                        
079400                                                                          
079500     PERFORM 9999-ABEND.                                                  
079600                                                                          
079700                                                                          
079800 9999-ABEND.                                                              
079900                                                                          
080000     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
