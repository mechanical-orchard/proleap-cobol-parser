       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT209.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030099
       DATE-WRITTEN.  10-06-2006.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:   THE PROGRAM IS DESIGNED TO PERFORM LEVEL I DATA       00080000
      *         SMOOTHING CUM MKUP PERCENT CALCULATIONS.....            00090000
      *             (SEE THE FLOW SECTION FOR MORE DETAILS)                     
      *                                                                 00100000
      *CHANGE HISTORY:                                                  00110000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00120000
      * ------- ---------- ----------- -------------------------------- 00130000
      *         10-06-2006 ROD JANSSEN CREATION.....                    00140000
C28582* 28582   01-10-2007 ROD JANSSEN GROSS MARGIN $ REFINEMENT CHANGE         
I28582* 28582   03-19-2007 ROD JANSSEN ADD TEST ID FOR SMOOTHING LEVEL          
      ******************************************************************00150000
                                                                        00160000
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.        IBM-3090.                                00190000
       OBJECT-COMPUTER.        IBM-3090.                                00200000
       INPUT-OUTPUT SECTION.                                            00210000
       FILE-CONTROL.                                                    00220000
                                                                        00230000
           SELECT REQUEST-CNTL-FILE                                     00240010
               ASSIGN TO INP01.                                         00250010
                                                                        00260010
           SELECT DTL-LDGR-FILE-IN                                      00270000
               ASSIGN TO INP02.                                         00280000
                                                                        00290000
I28582     SELECT SMOOTH-VERBIAGE-IN                                    00270000
I28582         ASSIGN TO INP03.                                         00280000
I28582                                                                  00290000
           SELECT LDGR-SMOOTH-FILE-OUT                                  00300000
               ASSIGN TO OUT01.                                         00310000
                                                                        00320000
       DATA DIVISION.                                                   00360000
       FILE SECTION.                                                    00370000
                                                                        00380000
       FD  REQUEST-CNTL-FILE                                            00390010
           RECORDING MODE IS F                                          00400010
           LABEL RECORDS ARE STANDARD                                   00410010
           BLOCK CONTAINS 0 RECORDS.                                    00420010
       01  REQUEST-CNTL-REC                PIC X(080).                  00430010
                                                                        00440010
       FD  DTL-LDGR-FILE-IN                                             00450000
           LABEL RECORDS ARE OMITTED                                    00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS.                                    00480000
       01  DTL-LDGR-FILE-IN-REC            PIC X(400).                  00490005
                                                                        00500000
I28582 FD  SMOOTH-VERBIAGE-IN                                           00450000
I28582     LABEL RECORDS ARE OMITTED                                    00460000
I28582     RECORDING MODE IS F                                          00470000
I28582     BLOCK CONTAINS 0 RECORDS.                                    00480000
I28582 01  SMOOTH-VERBIAGE-REC             PIC X(080).                  00490005
I28582                                                                  00500000
       FD  LDGR-SMOOTH-FILE-OUT                                         00510000
           LABEL RECORDS ARE OMITTED                                    00520000
           RECORDING MODE IS F                                          00530000
           BLOCK CONTAINS 0 RECORDS.                                    00540000
       01  LDGR-SMOOTH-LVL1-REC            PIC X(400).                  00550000
                                                                        00560000
      /*****************************************************************00630000
       WORKING-STORAGE SECTION.                                         00640000
      ******************************************************************00650000
       01  FILLER                          PIC X(40)       VALUE        00660010
           '*** REPORT REQUEST CONTROL REC ***'.                        00670010
                                                                        00680010
       01  RQ-CNTL-TYPE-AREA.                                           00690010
           05  RQ-CNTL-TYPE-IND            PIC  X(01)      VALUE SPACE. 00700010
               88  RQ-COMMENTS                             VALUE '*'.   00710010
           05  RQ-CNTL-TYPE-SW             PIC  X(08)      VALUE SPACES.00720010
               88  RQ-CNTL-TYPE-WEEKLY                     VALUE        00730010
                   'WEEKLY  '.                                          00740010
               88  RQ-CNTL-TYPE-MONTHLY                    VALUE        00750010
                   'MONTHLY '.                                          00760010
           05  FILLER                      PIC  X(01)      VALUE SPACE. 00770010
           05  RQ-CNTL-TYPE-DATE           PIC  X(10)      VALUE SPACES.00780010
           05  FILLER REDEFINES RQ-CNTL-TYPE-DATE.                      00790010
               10  RQ-CNTL-TYPE-DATE-CCYY  PIC  X(04).                  00800010
               10  FILLER                  PIC  X(01).                  00810010
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-1      VALUE '-'.   00820010
               10  RQ-CNTL-TYPE-DATE-MM    PIC  X(02).                  00830010
               10  FILLER                  PIC  X(01).                  00840010
                   88  RQ-CNTL-TYPE-DATE-VALID-DASH-2      VALUE '-'.   00850010
               10  RQ-CNTL-TYPE-DATE-DD    PIC  X(02).                  00860010
           05  FILLER                      PIC  X(01)      VALUE SPACE. 00870092
           05  RQ-CNTL-TABLE-NAME          PIC  X(18)      VALUE SPACES.00880092
           05  FILLER                      PIC  X(41)      VALUE SPACES.00890092
                                                                        00900010
I28582 01  CC-SMOOTH-VERBIAGE-AREA.                                     00690010
I28582     05  CC-SMOOTH-LVL-IND           PIC  X(01)      VALUE SPACE. 00700010
I28582         88  LEVEL-I-SMOOTH                          VALUE '1'.   00710010
I28582         88  LEVEL-II-SMOOTH                         VALUE '2'.   00710010
I28582         88  LEVEL-III-SMOOTH                        VALUE '3'.   00710010
I28582         88  LEVEL-IV-SMOOTH                         VALUE '4'.   00710010
I28582     05  CC-SEPERATOR                PIC  X(03)      VALUE SPACES.00720010
I28582     05  CC-DESCRIPTION              PIC  X(76)      VALUE SPACES.00720010
I28582                                                                  00780010
       01  ABEND-CODE                     PIC S9(04) COMP VALUE ZEROS.  00910000
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 00920000
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 00930000
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 00940000
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 00950000
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 00960000
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 00970000
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 00980000
           88  ABEND-4008-DATA-ERROR                       VALUE +4008. 00990000
           88  ABEND-4009-KEY-DATA-ERROR                   VALUE +4009. 01000000
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012. 01010000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 01020000
           88  ABEND-4019-DATE-ROUTINE-ERROR               VALUE +4019. 01030000
           88  ABEND-4444-FORCED-ABEND                     VALUE +4444. 01040000
                                                                        01050000
       01  PC-PROGRAM-CONSTANTS.                                        01060000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'MLKUT209'.     01070000
                                                                        01080000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01090000
           05  PE-MSG-02                       PIC X(30) VALUE          01100000
                'DB2 SELECT ATTEMPTED         '.                        01110000
           05  PE-MSG-03                       PIC X(30) VALUE          01120000
                'ATTEMPTING TO OPEN DB2 CURSOR'.                        01130000
           05  PE-MSG-05                       PIC X(30) VALUE          01140000
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                        01150000
                                                                        01160000
       01  PV-PROGRAM-VARIABLES.                                        01170004
           05  PV-DTL-LDGR-READ        PIC S9(15) COMP-3 VALUE ZEROS.   01180004
           05  PV-WRITE-RECS           PIC S9(15) COMP-3 VALUE ZEROS.   01190004
           05  PV-REQUEST-COUNT        PIC S9(15) COMP-3 VALUE ZEROS.   01200018
           05  PV-DISPLAY-AMOUNT  PIC ZZZZZZZZZZZZZZZZZ9    VALUE ZEROS.01210004
           05  PV-DISPLAY-AMOUNT2 PIC ZZZZZZZZZZZZZZZZZ9.99 VALUE ZEROS.01220004
           05  PV-SAVE-KEY.                                             01230004
               10  PV-SV-PARTN-NBR     PIC S9(4) COMP   VALUE ZEROES.   01240004
               10  PV-SV-FSCL-PERIOD   PIC X(02)        VALUE SPACES.   01250004
               10  PV-SV-FSCL-END-DTE  PIC X(10)        VALUE SPACES.   01260004
               10  PV-SV-DEPT-NBR      PIC S9(4) COMP-3 VALUE ZEROES.   01270004
               10  PV-SV-MAJ-CL-NBR    PIC S9(4) COMP-3 VALUE ZEROES.   01280004
               10  PV-SV-SUB-CL-NBR    PIC S9(4) COMP-3 VALUE ZEROES.   01290004
               10  PV-SV-LOC-NBR       PIC S9(4) COMP   VALUE ZEROES.   01300004
               10  PV-SV-ENT-ID        PIC S9(9) COMP   VALUE ZEROES.   01310004
               10  PV-SV-PARTN-GROUP   PIC S9(4) COMP   VALUE ZEROES.   01320004
               10  PV-SV-LBL-SEQ-NBR   PIC S9(9) COMP   VALUE ZEROES.   01330004
               10  PV-SV-ML-LWST-ID    PIC S9(9) COMP   VALUE ZEROES.   01340004
               10  FILLER              PIC X(09)        VALUE SPACES.   01350004
           05  PV-WORK-AMOUNT          PIC S9(11)V99 COMP-3 VALUE +0.   01360004
           05  PV-WORK-AMOUNT2         PIC S9(11)V99 COMP-3 VALUE +0.   01370004
           05  PV-WORK-CMU-PCT         PIC S9(04)V9(7) COMP-3 VALUE +0. 01380007
           05  PV-OPERATION-ATTEMPTED  PIC X(30)       VALUE SPACES.    01390004
           05  PV-PARAGRAPH-NAME       PIC X(30)       VALUE SPACES.    01400004
           05  PV-HOLD-CURR-SLS-COST-AMT      PIC S9(11)V99 VALUE +0    01125035
                                                      COMP-3.           01840004
                                                                        01840004
       01  PS-PROGRAM-SWITCHES.                                         01850000
I28582     05  PS-SMOOTH-VERB-DONE-SW    PIC X(01)   VALUE 'N'.         01860000
I28582         88  PS-SMOOTH-VERB-DONE               VALUE 'Y'.         01870000
           05  PS-PROCESS-COMPLETE-SW    PIC X(01)   VALUE 'N'.         01860000
               88  PS-PROCESS-COMPLETE               VALUE 'Y'.         01870000
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.   01880012
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.   01890012
               88  NOT-END-OF-REQUEST-CNTL                 VALUE 'N'.   01900012
                                                                        01910000
                                                                        01940099
      /*****************************************************************01967892
      *    MLRD070 - ML'S COMMON RECALC EXTRACT                         01967995
      ******************************************************************01968000
           COPY MLRD070.                                                01969000
                                                                        01970000
      /*****************************************************************01980000
      *FLOW: THE PROGRAM IS DESIGNED TO PERFORM LEVEL I DATA            00080000
      *      SMOOTHING CUM MKUP PERCENT CALCULATIONS.....               00090000
      *                                                                         
      *      BREAKS OCCUR AT THE DEPT LEVEL.                                    
      *                                                                         
      *      THE SMOOTHING CANDIDATE FILE IS READ SEQUENTIALLY. BASED           
      *      ON THE DEFINED TARGET CMU PERCENT, THE END INV COST 1 IS           
      *      CALCULATED. THE LEVEL I COST ADJUSTMENTS IS CALCULATED             
      *      FROM THE DIFFERENCE BETWEEN THE ORIG EIC AND THE EIC1.             
      *      THE NEW SALES COST AND ADJUSTED GROSS MARGIN ARE CALCULATED        
      *      AS WELL.                                                           
      *                                                                         
      *      SEE THE JOBS, MLK199/MLK209/MLK211, TO DETERMINE WHICH             
      *      SMOOTHING CANDIDATES GO THROUGH LEVEL I SMOOTHING.                 
      *                                                                         
      *      INPUTS:                                                            
      *           INP01 - THE FISCAL PERIOD (MLK190)                            
      *                   (&PSQENV..&SYSID..RECALC.FISCAL.PERIOD.&TBL)          
      *           INP02 - LEVEL I SMOOTHING CANDIDATE EXTRACT                   
      *                   (MLK199/MLK209/MLK211)                                
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH00&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH01&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH02&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH03&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH04&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH05&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH06&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH07&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH08&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTH09&RUNLTR)         
I28582*           INP03 - LEVEL I SMOOTHING VERBIAGE CONTROL CARD               
      *                                                                         
      *      OUTPUT:                                                            
      *           OUT01 - LEVEL I SMOOTHED CANDIDATE EXTRACT                    
      *                   (MLK199/MLK209/MLK211)                                
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD0&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD1&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD2&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD3&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD4&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD5&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD6&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD7&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD8&RUNLTR)         
      *                   (&PSQENV..&SYSID..&TBL..LEVELI.SMTHD9&RUNLTR)         
      *                                                                         
      ******************************************************************02010000
      ******************************************************************02020000
       PROCEDURE DIVISION.                                              02030000
      ******************************************************************02040000
                                                                        02050000
       0000-MAINLINE-MLKUT209.                                          02060000
                                                                        02070000
           PERFORM 1000-INITIALIZATION.                                 02080000
                                                                        02090000
           PERFORM 2000-LVLI-SMOOTH-MAIN-LOOP                           02100001
               UNTIL PS-PROCESS-COMPLETE.                               02110000
                                                                        02120000
           PERFORM 9000-END-OF-JOB-TASKS.                               02130000
                                                                        02140000
       9999-EXIT-PROGRAM.                                               02150000
           STOP RUN.                                                    02160000
                                                                        02170000
      /*****************************************************************02180000
      * 1000-INITIALIZATION.                                            02190000
      ******************************************************************02200000
       1000-INITIALIZATION.                                             02210000
                                                                        02220000
           DISPLAY '******************************************'.        02230000
           DISPLAY '*  START OF ', PC-PROGRAM-NAME                      02240000
           DISPLAY '******************************************'.        02250000
           DISPLAY ' '.                                                 02260000
                                                                        02270000
           OPEN INPUT  REQUEST-CNTL-FILE                                02280010
                INPUT  DTL-LDGR-FILE-IN                                 02290010
I28582          INPUT  SMOOTH-VERBIAGE-IN                               02290010
                OUTPUT LDGR-SMOOTH-FILE-OUT.                            02310000
                                                                        02320000
           MOVE 'N'            TO PS-PROCESS-COMPLETE-SW.               02330000
                                                                        02340000
           PERFORM 7000-READ-DTL-LDGR-FILE.                             02350000
           IF PS-PROCESS-COMPLETE                                       02360000
               DISPLAY '*******************************************'    02370000
               DISPLAY '*** ' PC-PROGRAM-NAME ' - NOTHING TO PROCESS'   02380000
               DISPLAY '*******************************************'    02390000
               DISPLAY ' '                                              02400000
           ELSE                                                         02410019
               DISPLAY '********************************************'   02420019
               DISPLAY '*** CONTROL/FISCAL DATES BEING PROCESSED.'      02430019
               DISPLAY ' '                                              02440020
               PERFORM 1200-PROCESS-RQ-CNTL-TYPE                        02450019
                                                                        02460019
               IF PV-REQUEST-COUNT > ZEROES                             02470019
                   IF PV-REQUEST-COUNT > +1                             02480019
                       DISPLAY ' ** MULTIPLE REQUEST CONTROL: '         02490019
                               PV-REQUEST-COUNT                         02500019
                       DISPLAY ' ** USE THE LAST REQUEST'               02510019
                   END-IF                                               02520019
                   DISPLAY ' *** TABLE BEING SMOOTHED ',                02530019
                            RQ-CNTL-TABLE-NAME                          02540019
               ELSE                                                     02550019
                   DISPLAY ' *** NO CONTROL/FISCAL DATE TO PROCESS.'    02560019
                   MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME      02570019
                   SET LOGICAL-ISSUE-ABEND   TO TRUE                    02580019
                   PERFORM 9999-ABEND                                   02590019
               END-IF                                                   02600019
               DISPLAY '********************************************'   02610022
               DISPLAY                                                  02620022
                  '*** LEVEL I SMOOTHING CALCULATIONS BEGIN FOR...'     02630023
               DISPLAY '*** TYPE - ', ML070-OUTLYER-CANDI-IND,          02640022
                       ' CANDIDATES'                                    02650022
               DISPLAY '********************************************'   02660022
               DISPLAY ' '                                              02670022
I28582         DISPLAY '********************************************'   02660022
I28582         PERFORM 7200-READ-SMOOTH-VERBIAGE                        02350000
I28582         IF PS-SMOOTH-VERB-DONE                                   02360000
I28582             DISPLAY ' *** NO SMOOTH VERBIAGE IND FOUND.'         02560019
I28582             MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME      02570019
I28582             SET LOGICAL-ISSUE-ABEND   TO TRUE                    02580019
I28582             PERFORM 9999-ABEND                                   02590019
I28582         ELSE                                                             
I28582         IF (NOT LEVEL-I-SMOOTH)                                          
I28582             DISPLAY ' *** INVALID SMOOTH VERBIAGE IND.'          02560019
I28582             MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME      02570019
I28582             SET LOGICAL-ISSUE-ABEND   TO TRUE                    02580019
I28582             PERFORM 9999-ABEND                                   02590019
I28582         ELSE                                                             
I28582             DISPLAY CC-SMOOTH-VERBIAGE-AREA                      02530019
I28582         DISPLAY '********************************************'   02660022
I28582         DISPLAY ' '                                              02670022
I28582         END-IF                                                   02680000
               END-IF.                                                  02680000
                                                                        02690000
      ******************************************************************02700010
      * PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES           02710010
      ******************************************************************02720010
       1200-PROCESS-RQ-CNTL-TYPE.                                       02730010
                                                                        02740010
           DISPLAY 'EXTRACT REQUESTED:'.                                02750010
           DISPLAY '  ----+----1----+----2----+----3----+----4'.        02760010
           PERFORM 7300-READ-REQUEST-CNTL.                              02770093
                                                                        02780010
           MOVE ZEROES             TO PV-REQUEST-COUNT.                 02790010
                                                                        02800010
           PERFORM                                                      02810010
               UNTIL END-OF-REQUEST-CNTL                                02820010
                                                                        02830010
               DISPLAY '  ' RQ-CNTL-TYPE-AREA                           02840010
               IF RQ-COMMENTS                                           02850010
                   CONTINUE                                             02860010
               ELSE                                                     02870010
                   IF RQ-CNTL-TYPE-DATE-CCYY IS NUMERIC                 02880010
                       AND RQ-CNTL-TYPE-DATE-MM IS NUMERIC              02890010
                       AND RQ-CNTL-TYPE-DATE-DD IS NUMERIC              02900010
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-1               02910010
                       AND RQ-CNTL-TYPE-DATE-VALID-DASH-2               02920010
                       ADD +1                  TO PV-REQUEST-COUNT      02930010
                       DISPLAY ' ** TYPE: ' RQ-CNTL-TYPE-SW             02940010
                               ', DATE: ' RQ-CNTL-TYPE-DATE             02950010
                   END-IF                                               02960010
               END-IF                                                   02970010
                                                                        02980010
               PERFORM 7300-READ-REQUEST-CNTL                           02990093
                                                                        03000010
           END-PERFORM.                                                 03010010
                                                                        03020010
      /*****************************************************************03030000
      * 2000-LVLI-SMOOTH-MAIN-LOOP.                                     03040001
      ******************************************************************03050000
       2000-LVLI-SMOOTH-MAIN-LOOP.                                      03060001
                                                                        03070000
           IF ML070-DEPT-NBR   NOT = PV-SV-DEPT-NBR                     03080012
               DISPLAY ' PROCESSING DEPT/DATE ' ML070-DEPT-NBR          03090010
                       '/' ML070-FSCL-END-DTE                           03100010
                       ' ... ' FUNCTION CURRENT-DATE                    03110010
               MOVE ML070-FSCL-END-DTE  TO PV-SV-FSCL-END-DTE           03120018
               MOVE ML070-DEPT-NBR      TO PV-SV-DEPT-NBR               03130018
               MOVE ML070-MAJ-CL-NBR    TO PV-SV-MAJ-CL-NBR             03140018
               MOVE ML070-SUB-CL-NBR    TO PV-SV-SUB-CL-NBR             03150018
               MOVE ML070-LOC-NBR       TO PV-SV-LOC-NBR                03160018
               MOVE ML070-ENT-ID        TO PV-SV-ENT-ID                 03170018
               MOVE ML070-LBL-SEQ-NBR   TO PV-SV-LBL-SEQ-NBR            03180018
               MOVE ML070-ML-LWST-ID    TO PV-SV-ML-LWST-ID             03190018
           END-IF.                                                      03200010
                                                                        03210010
      *** CALCULATE THE OVERRIDE ENDING INVENTORY COST.                 03363021
           PERFORM 3000-CALC-OVERRIDE-EIC.                              03370006
                                                                        03380006
      *** CALCULATE LEVEL I SMOOTHED NUMBERS.....                       03419588
           PERFORM 3100-CALC-SMOOTH-NUMBERS.                            03430092
                                                                        03440006
      *** WRITE RECORD(S).....                                          03666088
           PERFORM 7100-WRITE-EXTRACT-REC.                              03670005
                                                                        03680005
      *** READ NEXT RECORD....                                          03701088
           PERFORM 7000-READ-DTL-LDGR-FILE.                             03710000
                                                                        03720000
      ***************************************************************** 03730006
      * 3000-CALC-OVERRIDE-EIC.                                         03740006
      *   RECALCULATE THE EIC BASED ON THE OVERRIDE CMU PERCENT COMING  03750006
      * FROM THE LEVEL I DATA SMOOTH CANDIDATE SELECTION PROCESS.       03760006
      ***************************************************************** 03770004
       3000-CALC-OVERRIDE-EIC.                                          03780006
                                                                        03790004
           COMPUTE PV-WORK-AMOUNT ROUNDED                               03850064
                               = ((100 - ML070-TGT-CUM-MKUP-PCT)        03860064
                               *  ML070-CURR-END-INV-RTL-AMT)           03870064
                               / 100.                                   03880064
                                                                        03890064
      ***************************************************************** 04230006
      * 3100-CALC-SMOOTH-NUMBERS.                                       04240092
      ***************************************************************** 04260006
       3100-CALC-SMOOTH-NUMBERS.                                        04270092
                                                                        04280006
                                                                        04950014
           MOVE PV-WORK-AMOUNT       TO  ML070-END-INV-COST-AMT1.       04821092
                                                                        05030035
           COMPUTE ML070-INV-COST-ADJ1 =                                05040035
                       ML070-END-INV-COST-AMT1                          05050035
                     - ML070-CURR-END-INV-COST-AMT.                     05041035
                                                                        05060035
           MOVE ML070-CURR-SLS-COST-AMT TO PV-HOLD-CURR-SLS-COST-AMT.   05070035
                                                                        05070135
           COMPUTE ML070-CURR-SLS-COST-AMT =                            05071035
                       PV-HOLD-CURR-SLS-COST-AMT                        05080035
                     - ML070-INV-COST-ADJ1.                             05090035
                                                                        05100035
           COMPUTE ML070-ADJ-GRO-MRGN-AMT =                             05110035
                       ML070-CURR-SLS-RTL-AMT                           05120035
                     - ML070-CURR-SLS-COST-AMT.                         05121035
                                                                        04832006
I28582     IF ML070-INV-COST-ADJ1 IS EQUAL TO ZEROS                             
I28582       MOVE SPACE             TO ML070-LVL1-SMOOTH-IND            00700010
I28582     ELSE                                                                 
I28582       MOVE CC-SMOOTH-LVL-IND TO ML070-LVL1-SMOOTH-IND            00700010
I28582     END-IF.                                                              
I28582                                                                  06055093
      /**************************************************************** 05800006
      * 7000-READ-DTL-LDGR-FILE.                                        05810093
      ***************************************************************** 05820006
       7000-READ-DTL-LDGR-FILE.                                         05830006
                                                                        05840006
           READ DTL-LDGR-FILE-IN NEXT                                   05850000
               INTO ML070-COMMON-RECALC-EXTRACT                         05860016
                   AT END      MOVE 'Y' TO PS-PROCESS-COMPLETE-SW       05870016
                   NOT AT END  ADD 1 TO PV-DTL-LDGR-READ                05880016
           END-READ.                                                    05890000
                                                                        05900000
      ******************************************************************05910000
      * 7100-WRITE-EXTRACT-REC.                                         05920000
      ******************************************************************05930000
       7100-WRITE-EXTRACT-REC.                                          05940000
                                                                        05950000
           WRITE LDGR-SMOOTH-LVL1-REC FROM ML070-COMMON-RECALC-EXTRACT. 05960015
           ADD 1                      TO PV-WRITE-RECS.                 05970000
                                                                        05980000
I28582******************************************************************06051093
I28582* 7200-READ-SMOOTH-VERBIAGE.                                      06052093
I28582******************************************************************06053093
I28582 7200-READ-SMOOTH-VERBIAGE.                                       06054093
I28582                                                                  06055093
I28582     READ SMOOTH-VERBIAGE-IN INTO CC-SMOOTH-VERBIAGE-AREA         06056093
I28582         AT END SET PS-SMOOTH-VERB-DONE TO TRUE.                  06057093
I28582                                                                  06058093
      ******************************************************************06051093
      * 7300-READ-REQUEST-CNTL.                                         06052093
      ******************************************************************06053093
       7300-READ-REQUEST-CNTL.                                          06054093
                                                                        06055093
           READ REQUEST-CNTL-FILE INTO RQ-CNTL-TYPE-AREA                06056093
               AT END SET END-OF-REQUEST-CNTL TO TRUE.                  06057093
                                                                        06058093
      /*****************************************************************06060000
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.        06070000
      ******************************************************************06080000
       9000-END-OF-JOB-TASKS.                                           06090000
                                                                        06100000
           DISPLAY '******************************************'.        06110000
           DISPLAY '** RECORD COUNT SUMMARY **'.                        06120000
                                                                        06130000
           MOVE PV-DTL-LDGR-READ        TO PV-DISPLAY-AMOUNT.           06140000
           DISPLAY '* READ - DETAIL LEDGER RECORDS : '                  06150000
                                           PV-DISPLAY-AMOUNT.           06160000
           MOVE PV-WRITE-RECS           TO PV-DISPLAY-AMOUNT.           06170000
           DISPLAY '* WRITE - TOTAL RECORDS WRITTEN: '                  06180000
                                           PV-DISPLAY-AMOUNT.           06190000
           DISPLAY '******************************************'.        06200000
                                                                        06210000
           DISPLAY ' '.                                                 06220000
           DISPLAY '******************************************'.        06230000
           DISPLAY '*  END OF ' PC-PROGRAM-NAME                         06240000
           DISPLAY '******************************************'.        06250000
                                                                        06260000
           CLOSE DTL-LDGR-FILE-IN                                       06270003
                 REQUEST-CNTL-FILE                                      06280010
I28582           SMOOTH-VERBIAGE-IN                                     02290010
                 LDGR-SMOOTH-FILE-OUT.                                  06300000
                                                                        06310000
      ******************************************************************06320000
      * 9999-ABEND.                                                     06330000
      *   THIS ROUTINE FORCES AN ABEND                                  06340000
      ******************************************************************06350000
       9999-ABEND.                                                      06360000
                                                                        06370000
           DISPLAY '**  ABEND     **'.                                  06380000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              06390000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            06400000
           DISPLAY '**'                                                 06410000
           DISPLAY '**'                                                 06420000
           DISPLAY '**********************************'                 06430000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           06440000
                                                                        06450000
