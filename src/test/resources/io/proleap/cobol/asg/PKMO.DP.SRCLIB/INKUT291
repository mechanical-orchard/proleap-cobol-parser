       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUT291.                                        
       AUTHOR.                 D. THEEL.                                        
       INSTALLATION.           KOHLS.                                           
       DATE-WRITTEN.           11-19-08.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *     EXTRACT DATA FOR CREATING CUMULATIVE REPORT.               *        
      ******************************************************************        
      * THIS PROGRAM EXTRACTS DATA FOR CREATING A CUMULATIVE REPORT    *        
      * THAT NEEDS TO BREAK AND TOTAL BY CYCLE.  THE OUPUT FILE FROM   *        
      * THIS PROGRAM WILL BE USED IN THE NEW REPORT PROGRAM INKRP291.  *        
      * THE INPUT FILE IS THE STORE EXPECTED EXTRACT SUMMED TO THE     *        
      * ADJUSTMENT/STORE LEVEL.                                        *        
      *                                                                *        
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES         *        
      * ------- ---------- ------------ -------------------------------*        
      * W33304  11-19-2008 D. THEEL      NEW PROGRAM CREATED AS PART   *        
      *                                  OF HOLDING INVENTORY LEDGER   *        
      *                                  OPEN - HILO                   *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT STR-EXPECTED-IN-FILE       ASSIGN TO INP01.                   
           SELECT STR-EXPECTED-OUT-FILE      ASSIGN TO OUT01.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  STR-EXPECTED-IN-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
                                                                                
       01  STR-EXPECTED-IN-RECORD          PIC  X(100).                         
                                                                                
       FD  STR-EXPECTED-OUT-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 75 CHARACTERS.                                       
                                                                                
       01  STR-EXPECTED-OUT-RECORD         PIC  X(75).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *    CONSTANTS ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC  X(08)   VALUE 'INKUT291'.         
           05  PC-MAX-RETRIES            PIC S9(04)   VALUE +3                  
                                                      COMP SYNC.                
                                                                                
      ******************************************************************        
      *    VARIABLES ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-STORE-NBR                 PIC S9(04) COMP VALUE ZERO.         
           05  PV-OPER-ATTEMPTED            PIC X(30)    VALUE SPACES.          
           05  PV-PARAGRAPH-NAME            PIC X(30)    VALUE SPACES.          
           05  PV-RETRY-COUNT               PIC S9(04)   VALUE ZERO.            
                                                                                
      ******************************************************************        
      *    PROGRAM SWITCHES ARE DECLARED HERE                          *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-INPUT-FILE-SW             PIC X(01)    VALUE 'N'.             
               88  PS-INPUT-EOF-YES                      VALUE 'Y'.             
               88  PS-INPUT-EOF-NO                       VALUE 'N'.             
           05  PS-STORE-VALID-SW            PIC X(01)    VALUE 'N'.             
               88  PS-STORE-VALID                        VALUE 'Y'.             
               88  PS-STORE-NOT-VALID                    VALUE 'N'.             
                                                                                
      ******************************************************************        
      *    ACCUMULATORS ARE DECLARED HERE                              *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECS-CNT            PIC 9(09)    VALUE ZEROES.          
           05  PA-SELECT-CNT                PIC 9(09)    VALUE ZEROES.          
           05  PA-OUT-WRITE-CNT             PIC 9(09)    VALUE ZEROES.          
                                                                                
      ******************************************************************        
      *    ABEND CODE DECLARATION                                      *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04)   VALUE +0               
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      * INPUT FILE COPY MEMBER                                                  
      ******************************************************************        
           COPY INRD137.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE COPY MEMBER                                                 
      ******************************************************************        
           COPY INRD237.                                                        
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINVPAR                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINCNTL                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    USED FOR SQL COMMUNICATION AREA.                            *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                               *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  PROGRAM PROCESSING STARTS HERE                                *        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-PROCESS-FILE                                            
              UNTIL PS-INPUT-EOF-YES.                                           
           PERFORM 5000-END-OF-JOB.                                             
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   INITIALIZATION PARA                                          *        
      ******************************************************************        
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  STR-EXPECTED-IN-FILE                                     
                OUTPUT STR-EXPECTED-OUT-FILE.                                   
           INITIALIZE  IN137-STR-EXPTED-RECORD                                  
                       IN237-STR-EXPTED-RECORD.                                 
                                                                                
           SET PS-INPUT-EOF-NO              TO   TRUE.                          
                                                                                
           INITIALIZE DCLTINVPAR                                                
                      DCLTINCNTL.                                               
                                                                                
           PERFORM 3000-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      *   MAIN PROCESSING PARA                                         *        
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           MOVE IN137-STORE-NBR-9         TO PV-STORE-NBR.                      
           PERFORM 2100-SELECT-SORT-DATA.                                       
           IF PS-STORE-VALID                                                    
               MOVE INVPAR-INV-ID           TO IN237-INV-ID                     
               MOVE INCNTL-OPN-FSCL-MN-DTE  TO IN237-IN-OPN-FSCL-MN-DTE         
               MOVE INCNTL-OPN-FSCL-MN-NBR  TO IN237-IN-OPN-FSCL-MN-NBR         
               MOVE INVPAR-FINCL-INV-STAT-CDE TO                                
                                             IN237-FINCL-INV-STAT-CDE           
               MOVE IN137-STORE-NBR-9       TO IN237-STORE-NBR-9                
               MOVE IN137-ADJ-DATE          TO IN237-ADJ-DATE                   
               MOVE IN137-ADJ-QTY           TO IN237-ADJ-QTY                    
               MOVE IN137-EXTD-RTL-AMT      TO IN237-EXTD-RTL-AMT               
               PERFORM 4000-WRITE-OUTPUT                                        
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-INPUT-FILE.                                        
                                                                                
      ******************************************************************        
      *   SELECTS THE SORT DATA FROM TABLES                            *        
      ******************************************************************        
       2100-SELECT-SORT-DATA.                                                   
           MOVE 'N' TO PS-STORE-VALID-SW.                                       
           MOVE PV-STORE-NBR TO INVPAR-LOC-NBR.                                 
           MOVE IN137-ADJ-DATE TO INVPAR-PLND-INV-DTE.                          
           MOVE ZERO TO PV-RETRY-COUNT.                                         
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
           EXEC SQL                                                             
               SELECT A.INV_ID,                                                 
                      A.FINCL_INV_STAT_CDE,                                     
                      B.OPN_FSCL_MN_DTE,                                        
                      B.OPN_FSCL_MN_NBR                                         
                 INTO :INVPAR-INV-ID,                                           
                      :INVPAR-FINCL-INV-STAT-CDE,                               
                      :INCNTL-OPN-FSCL-MN-DTE,                                  
                      :INCNTL-OPN-FSCL-MN-NBR                                   
                 FROM TINVPAR A,                                                
                      TINCNTL B                                                 
                WHERE A.LOC_NBR = :INVPAR-LOC-NBR                               
                  AND A.LOC_INV_STAT_CDE = 'IN'                                 
                  AND A.ACTL_FIN_BK_DTE = '9999-09-09'                          
                  AND A.PLND_INV_DTE = :INVPAR-PLND-INV-DTE                     
                  AND A.SCHD_PHY_CNT_CDE IN ('PI','NS','CL')                    
                  AND A.MFSTNG_IND = 'Y'                                        
                  AND A.INV_ID = B.INV_ID                                       
                  AND B.ACTV_IND = 'Y'                                          
                 WITH UR                                                        
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                    ADD 1                TO PA-SELECT-CNT                       
                    SET PS-STORE-VALID   TO TRUE                                
               WHEN +100                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   MOVE '2100-SELECT-SORT-DATA'                                 
                                            TO PV-PARAGRAPH-NAME                
                   MOVE 'ERROR IN SELECT - SORT_DATA'                           
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE '2100-SELECT-SORT-DATA' TO PV-PARAGRAPH-NAME                
               MOVE 'TINVPAR MAX RETRIES EXCEEDED'                              
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   READS INPUT FILE                                             *        
      ******************************************************************        
       3000-READ-INPUT-FILE.                                                    
                                                                                
           READ STR-EXPECTED-IN-FILE                                            
               INTO IN137-STR-EXPTED-RECORD                                     
           AT END                                                               
               SET PS-INPUT-EOF-YES TO TRUE                                     
           NOT AT END                                                           
               ADD 1                        TO PA-INPUT-RECS-CNT                
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *   WRITES INTO THE OUTPUT FILE                                  *        
      ******************************************************************        
       4000-WRITE-OUTPUT.                                                       
                                                                                
           WRITE STR-EXPECTED-OUT-RECORD FROM IN237-STR-EXPTED-RECORD.          
           ADD 1                            TO   PA-OUT-WRITE-CNT.              
                                                                                
      ******************************************************************        
      *   END OF JOB PROCESSING                                        *        
      ******************************************************************        
       5000-END-OF-JOB.                                                         
                                                                                
           DISPLAY 'NUMBER OF RECORDS READ FROM INPUT FILE : '                  
                   PA-INPUT-RECS-CNT.                                           
           DISPLAY 'NUMBER OF TINVPAR/TINCNTL SELECTS : '                       
                   PA-SELECT-CNT.                                               
           DISPLAY 'NUMBER OF RECORDS WRITTEN TO OUTPUT FILE : '                
                   PA-OUT-WRITE-CNT.                                            
                                                                                
           CLOSE STR-EXPECTED-IN-FILE                                           
                 STR-EXPECTED-OUT-FILE.                                         
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                           *        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = '  PC-PROGRAM-NAME.                     
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED.                   
           DISPLAY '**  PARAGRAPH **  = '  PV-PARAGRAPH-NAME.                   
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                  *        
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           MOVE +4013                       TO ABEND-CODE.                      
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
      *                 END OF PROGRAM INKUT291                        *        
      ******************************************************************        
