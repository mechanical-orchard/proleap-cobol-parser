      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.      MOKUT616.                                               
       AUTHOR.          JOHN BOLDT, STRATAGEM.                                  
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    MARCH 2006.                                             
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WAS VALIDATE DATES ON MOT_STGG_STR_STYL_PRICG BY           
      * DEPT.                                                                   
      * THIS IS A COPIED AND MODIFIED VERSION OF PROGRAM PEKUT610.              
      ******************************************************************        
      **  HISTORY LOG:                                                 *        
      ******************************************************************        
      * DATE:         03/24/2006                                       *        
      * WR/IR#:       WR29858                                          *        
      * PROGRAMMER:   JOHN BOLDT, STRATAGEM.                           *        
      * DESCRIPTION:  INITIAL CREATION.                                *        
      ******************************************************************        
      * DATE:         06/14/2006                                       *        
      * WR/IR#:       P000890369                                       *        
      * PROGRAMMER:   DARYL WOELFEL                                    *        
      * DESCRIPTION:  REMOVED OLD "ALTER SESSION COST=5" STATEMENT, AS *        
      *               IT WAS CAUSING THE SQL TO TAKE A BAD PATH.       *        
      ******************************************************************        
      * DATE:         10/25/2007                                       *        
      * WR/IR#:       WR32984                                          *        
      * PROGRAMMER:   LINDA FRIESS                                              
      * DESCRIPTION:  CHANGE VALID PROCESS INSTANCE TO BE 1 THRU 8.    *        
      ******************************************************************        
      * DATE:         08/27/2008                                       *        
      * WR/IR#:       INC000000497852                                  *        
      * PROGRAMMER:   DAVE METZGER                                              
      * DESCRIPTION:  CHANGE VALID PROCESS INSTANCE TO BE 1 THRU 12.   *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT PROCESS-INSTANCE-FILE                                         
                  ASSIGN                    TO INP01.                           
                                                                                
           SELECT PRCS-IND-FILE                                                 
                  ASSIGN                    TO INP02.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
               ASSIGN                       TO ORALOGON.                        
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
      *    PROCESS INSTANCE WILL CONTAIN WHICH PARALLEL JOBFLOW IS              
      *    RUNNING THAT DETERMINES THE DEPARTMENTS TO PROCESS                   
       FD  PROCESS-INSTANCE-FILE                                                
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PROCESS-INSTANCE-RECORD.                              
       01  PROCESS-INSTANCE-RECORD         PIC  X(80).                          
                                                                                
       FD  PRCS-IND-FILE                                                        
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PRCS-IND-RECORD.                                      
       01  PRCS-IND-RECORD                 PIC  X(80).                          
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                                    
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AUTO-LOGON               PIC X(1)    VALUE '/'.               
           05  PC-PROGRAM-NAME             PIC  X(08)  VALUE                    
                                                       'MOKUT616'.              
           05  PC-HISTORICAL               PIC  X(01)  VALUE 'H'.               
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                                    
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-TABLE-NAME               PIC  X(30)  VALUE SPACES.            
           05  PV-SQL-CODE                 PIC  9(09)  VALUE 0.                 
           05  PV-PARAGRAPH-NAME           PIC  X(40)  VALUE SPACES.            
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACES.            
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.            
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.            
           05  PV-CURRENT-TIME             PIC  X(08)  VALUE SPACES.            
           05  PV-PRCS-IND                 PIC  X(01)  VALUE SPACES.            
           05  PV-DEPT-NBR                 PIC S9(04)  VALUE +0  COMP-3.        
                                                                                
      ******************************************************************        
      * PA- PROGRAM ACCUMULATORS                                                
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-CNT-DEPTS                PIC  9(09)  VALUE 0.                 
                                                                                
      ******************************************************************        
      * CC- CONTROL CARDS INPUT.                                                
      ******************************************************************        
       01  CC-PROCESS-INSTANCE-REC.                                             
           05  CC-PROCESS-INSTANCE         PIC  9(02)  VALUE 0.                 
           05  FILLER                      PIC  X(78)  VALUE SPACES.            
                                                                                
       01  CC-CONTROL-CARD-PRCS-IND.                                            
           05  CC-PRCS-IND                 PIC  X(01)  VALUE SPACES.            
           05  FILLER                      PIC  X(79)  VALUE SPACES.            
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW     PIC X(01)   VALUE 'N'.               
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
           05  PS-ORA-SRC-CTL-DONE-SW  PIC  X(01)      VALUE 'N'.               
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-END-OF-PROC-INST-FILE-SW PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-PROC-INST-FILE            VALUE 'Y'.               
               88  PS-NOT-END-OF-PROC-INST-FILE        VALUE 'N'.               
                                                                                
           05  PS-END-OF-PRCS-IND-FILE-SW  PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-PRCS-IND-FILE             VALUE 'Y'.               
               88  PS-NOT-END-OF-PRCS-IND-FILE         VALUE 'N'.               
                                                                                
           05  PS-PROCESS-INSTANCE-SW      PIC  9(02)  VALUE 0.                 
               88  PS-PROCESS-INSTANCE-VALID           VALUE 1 THRU 12.         
                                                                                
           05  PS-END-OF-DEPTS-CSR-SW      PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-DEPTS-CSR                 VALUE 'Y'.               
               88  PS-NOT-END-OF-DEPTS-CSR             VALUE 'N'.               
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID            PIC X(40).                           
               88 CC-AUTO-LOGON                        VALUE '/'.               
           05  CC-ORACLE-PASSWORD          PIC X(40).                           
                                                                                
      ******************************************************************        
      * AC ABEND CODES                                                 *        
      ******************************************************************        
                                                                                
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.          
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
      *    COPY MOWS700.                                                        
                                                                                
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
                                                                                
       01  PC-ENT01-TYPE-CDE               PIC  X(02)  VALUE '01'.              
       01  PV-ORACLE-ID                    PIC  X(01)  VALUE '/'.               
                                                                                
       01  USERNAME                        PIC  X(10)  VARYING.                 
       01  PASSWD                          PIC  X(10)  VARYING.                 
                                                                                
       01  ORACLE-RETURN                   PIC  X(200) VALUE SPACES.            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
       EJECT                                                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * MAIN PROCESSING                                                         
      ******************************************************************        
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM C200-OPEN-DEPTS-CSR.                                         
           PERFORM C300-FETCH-DEPTS-CSR.                                        
                                                                                
           PERFORM C100-CONTROL                                                 
             UNTIL PS-END-OF-DEPTS-CSR.                                         
                                                                                
           PERFORM C400-CLOSE-DEPTS-CSR.                                        
           PERFORM B900-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * OPEN FILES                                                              
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM 7000-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000         PERFORM 9900-ORACLE-ABEND                                        
021200     END-IF.                                                              
                                                                                
           OPEN INPUT PROCESS-INSTANCE-FILE                                     
                      PRCS-IND-FILE.                                            
                                                                                
           PERFORM B300-READ-PROC-INST-FILE.                                    
           PERFORM B400-READ-PRCS-FILE.                                         
                                                                                
           PERFORM B800-DECLARE-DEPTS-CSR.                                      
                                                                                
                                                                                
      ******************************************************************        
      * READS THE PROCESS INSTANCE THAT ACQUIRES THE DEPTS NEEDED.              
      ******************************************************************        
       B300-READ-PROC-INST-FILE.                                                
                                                                                
           READ PROCESS-INSTANCE-FILE INTO CC-PROCESS-INSTANCE-REC              
                AT END SET PS-END-OF-PROC-INST-FILE TO TRUE.                    
                                                                                
           IF PS-NOT-END-OF-PROC-INST-FILE                                      
              DISPLAY 'PROC INST ' CC-PROCESS-INSTANCE                          
              MOVE CC-PROCESS-INSTANCE TO PS-PROCESS-INSTANCE-SW                
              IF PS-PROCESS-INSTANCE-VALID                                      
                  NEXT SENTENCE                                                 
              ELSE                                                              
                  MOVE 4003                       TO ABEND-CODE                 
                  MOVE 'B300-READ-PROC-INST-FILE' TO PV-PARAGRAPH-NAME          
                  MOVE 'INVALID PROCESS INSTANCE' TO PV-OPERATION-MSG           
                  PERFORM 9999-APPLICATION-ABEND                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * READS THE PROCESS INDICATOR FILE.                                       
      ******************************************************************        
       B400-READ-PRCS-FILE.                                                     
                                                                                
           READ PRCS-IND-FILE INTO CC-CONTROL-CARD-PRCS-IND                     
                AT END SET PS-END-OF-PRCS-IND-FILE TO TRUE.                     
                                                                                
           IF PS-NOT-END-OF-PRCS-IND-FILE                                       
               MOVE CC-PRCS-IND TO PV-PRCS-IND                                  
               DISPLAY 'HISTORICAL OR INCREMENTAL IND: ' CC-PRCS-IND            
           ELSE                                                                 
               MOVE 4003                       TO ABEND-CODE                    
               MOVE 'B400-READ-PRCS-IND-FILE'  TO PV-PARAGRAPH-NAME             
               MOVE 'EMPTY HIST/INCR IND FILE' TO PV-OPERATION-MSG              
               PERFORM 9999-APPLICATION-ABEND                                   
           END-IF.                                                              
                                                                                
                                                                                
      *--------------------------------------------------------------           
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT              
      * LOGS ON TO ORACLE.                                                      
      *---------------------------------------------------------------          
      *    COPY MOPD700.                                                        
                                                                                
      ******************************************************************        
      * NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
      * OTHERWISE I GET A ORA-3113 ERROR                                        
      ******************************************************************        
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET CURSOR_SHARING = 'EXACT'                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *---------------------------------------------------------------          
      * DECLARE THE DEPTS CURSOR.                                               
      *---------------------------------------------------------------          
       B800-DECLARE-DEPTS-CSR.                                                  
                                                                                
           EXEC SQL                                                             
             DECLARE DEPTSCSR CURSOR FOR                                        
               SELECT PRC_CNTL_VAL_NBR                                          
                 FROM MOTW_PRC_CNTL                                             
                WHERE PRC_CNTL_INSTN_NBR = :CC-PROCESS-INSTANCE                 
                  AND ACTV_IND           = 'Y'                                  
                  AND PRC_CNTL_STAT_CDE  = 'P'                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * CLEAN UP                                                                
      ******************************************************************        
       B900-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE PROCESS-INSTANCE-FILE                                          
                 PRCS-IND-FILE.                                                 
                                                                                
           DISPLAY ' '                                                          
           DISPLAY 'TOTAL DEPARTMENTS PROCESSED: ' PA-CNT-DEPTS                 
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      * CONTROL PROCESSING BASED ON DEPARTMENT NUMBERS.                         
      ******************************************************************        
       C100-CONTROL.                                                            
                                                                                
           PERFORM 8000-GET-CURRENT-TIME.                                       
           IF PV-PRCS-IND EQUAL PC-HISTORICAL                                   
               PERFORM D200-PROCESS-DEPT                                        
           END-IF.                                                              
                                                                                
           PERFORM D500-UPDATE-DEPT-COMPLETE.                                   
           PERFORM C300-FETCH-DEPTS-CSR.                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * OPENS THE PROCESS CONTROL TABLE CURSOR.                                 
      *----------------------------------------------------------               
       C200-OPEN-DEPTS-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               OPEN DEPTSCSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-DEPTS-CSR TO TRUE                         
               WHEN OTHER                                                       
                    SET ABEND-4013-DB2-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE 'C200-OPEN-DEPTS-CSR'                                  
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON DEPTSCSR'                               
                                      TO PV-OPERATION-MSG                       
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * FETCHES THE PROCESS CONTROL TABLE CURSOR.                               
      *----------------------------------------------------------               
       C300-FETCH-DEPTS-CSR.                                                    
                                                                                
           EXEC SQL                                                             
             FETCH DEPTSCSR                                                     
               INTO :PV-DEPT-NBR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    ADD 1 TO PA-CNT-DEPTS                                       
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-DEPTS-CSR TO TRUE                             
               WHEN OTHER                                                       
                    MOVE SQLCODE      TO PV-SQL-CODE                            
                    SET ABEND-4013-DB2-ERROR TO TRUE                            
                    MOVE 'C300-FETCH-DEPTS-CSR'                                 
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON DEPTSCSR'                              
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * CLOSE THE PROCESS CONTROL TABLE CURSOR.                                 
      *----------------------------------------------------------               
       C400-CLOSE-DEPTS-CSR.                                                    
                                                                                
           EXEC SQL                                                             
               CLOSE DEPTSCSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-DB2-ERROR TO TRUE                            
                    MOVE 'C400-CLOSE-DEPTS-CSR'                                 
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON DEPTSCSR'                              
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS EACH DEPARTMENT                                                 
      ******************************************************************        
       D200-PROCESS-DEPT.                                                       
                                                                                
           EXEC SQL EXECUTE                                                     
             BEGIN                                                              
               K_MO01.K_MOPG_SAS_UTILITIES.K_MOSP_GAP_CHECK(                    
               :PV-DEPT-NBR                                                     
               );                                                               
             END;                                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 DISPLAY 'FINISHED GAP CHECK DEPT: ' PV-DEPT-NBR                
             WHEN OTHER                                                         
                 MOVE SQLCODE                    TO PV-SQL-CODE                 
                 MOVE 'MOT_STGG_STR_STYL_PRICG'  TO PV-TABLE-NAME               
                 MOVE 'D200-PROCESS-DEPTS' TO PV-PARAGRAPH-NAME                 
                 PERFORM 9900-ORACLE-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * MARK PROCESS CONTROL TABLE COMPLETE FOR A DEPARTMENT.                   
      ******************************************************************        
       D500-UPDATE-DEPT-COMPLETE.                                               
                                                                                
           EXEC SQL EXECUTE                                                     
             BEGIN                                                              
               K_MO01.K_MOPG_SAS_UTILITIES.K_MOSP_CNTL_DEPT_COMPLETE(           
               :PV-DEPT-NBR);                                                   
             END;                                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 DISPLAY 'DEPARTMENT PROCESSING COMPLETE: ' PV-DEPT-NBR         
             WHEN OTHER                                                         
                 MOVE SQLCODE                     TO PV-SQL-CODE                
                 MOVE 'MOTW_PRC_CNTL'             TO PV-TABLE-NAME              
                 MOVE 'D500-UPDATE-DEPT-COMPLETE' TO PV-PARAGRAPH-NAME          
                 PERFORM 9900-ORACLE-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
034503****************************************************************          
034504** READ THE ORACLE LOGON CONTROL CARD.                        **          
034505****************************************************************          
034506 7000-READ-ORACLE-LOGON.                                                  
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
034600*                                                                         
                                                                                
      ******************************************************************        
       8000-GET-CURRENT-TIME.                                                   
      ******************************************************************        
                                                                                
            MOVE SPACES TO PV-CURRENT-TIME.                                     
                                                                                
           EXEC SQL                                                             
              SELECT                                                            
                 TO_CHAR(SYSDATE,'HH24:MI:SS')                                  
              INTO :PV-CURRENT-TIME                                             
              FROM DUAL                                                         
            END-EXEC.                                                           
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 DISPLAY 'CURRENT TIME: ' PV-CURRENT-TIME                       
             WHEN OTHER                                                         
                 MOVE SQLCODE                    TO PV-SQL-CODE                 
                 MOVE 'DUAL'                      TO PV-TABLE-NAME              
                 MOVE '8000-GET-CURRENT-TIME   ' TO PV-PARAGRAPH-NAME           
                 PERFORM 9900-ORACLE-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9999-APPLICATION-ABEND.                                                 
      *     PERFORM AN ABEND IF AN ERROR IS DETECTED.                           
      *----------------------------------------------------------               
       9999-APPLICATION-ABEND.                                                  
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = '.                                   
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
