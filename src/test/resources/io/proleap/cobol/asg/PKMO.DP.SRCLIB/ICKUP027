       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.      ICKUP027.                                       00030011
       AUTHOR.          COGNIZANT.  .                                   00040000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00050000
       DATE-WRITTEN.    05/31/10.                                       00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ************************************************************      00090000
      *  MODULE TYPE:   COBOL2 DB2 BATCH.                               00100000
      *                                                                 00110000
      *  UPDATE OF THE ICT_LOC_CNT_SUM TABLE                            00120000
      *                                                                 00130000
      *========================== CHANGE LOG ===========================00140000
      *  AUTH        BY            DATE          DESCRIPTION            00150023
      *=================================================================00160000
      * WR#####     COGNIZANT    05-31-10        NEW PROGRAM. CREATED   00170023
      *                                          PART OF STORE ENHANCEM-00180012
      *                                          ENTS PROJECT. CLARITY  00181012
      *                                          ID: 3132C              00182012
260107* CHG0260107  JIM HUNTER   08-16-21        ADD COPYBOOK DPWS991   00183023
260107*                                          TO CHANGE CALL TO      00184023
260107*                                          DPKUT901 FROM STATIC   00184123
260107*                                          TO DYNAMIC.            00185023
      ******************************************************************00190000
                                                                        00200000
       ENVIRONMENT DIVISION.                                            00210000
       CONFIGURATION SECTION.                                           00220000
                                                                        00230000
       SOURCE-COMPUTER.    IBM-3090.                                    00240000
       OBJECT-COMPUTER.    IBM-3090.                                    00250000
                                                                        00260000
       INPUT-OUTPUT SECTION.                                            00270000
       FILE-CONTROL.                                                    00280000
                                                                        00290000
       DATA DIVISION.                                                   00300000
       FILE SECTION.                                                    00310000
                                                                        00320000
                                                                        00330000
      ***************************************************************** 00340000
      *                       WORKING STORAGE                           00350000
      ***************************************************************** 00360000
       WORKING-STORAGE SECTION.                                         00370000
                                                                        00380000
       01  W-START                          PIC  X(40)                  00390000
           VALUE 'ICKUP027 - WORKING STORAGE BEGINS HERE'.              00400013
                                                                        00410000
                                                                        00420000
      ***************************************************************** 00430000
      *                         SWITCHES                                00440000
      ***************************************************************** 00450000
       01  PROGRAM-SWITCHES.                                            00460000
           05  PS-RESTART-REQUIRED-SW       PIC  X(01) VALUE 'N'.       00470000
               88  PS-RESTART-REQUIRED                 VALUE 'Y'.       00480000
               88  PS-RESTART-NOT-REQUIRED             VALUE 'N'.       00490000
                                                                        00500000
      ******************************************************************00510000
      *                       ACCUMULATORS                              00520000
      ******************************************************************00530000
       01  PROGRAM-ACCUM.                                               00540000
           05  PA-READ                      PIC  9(11) VALUE ZEROS.     00550000
           05  PA-CKPT                      PIC  9(11) VALUE ZEROS.     00560000
                                                                        00570000
      ******************************************************************00580000
      *                           CONSTANTS                             00590000
      ******************************************************************00600000
       01  PROGRAM-CONSTANTS.                                           00610000
           05  PC-MAX-RETRIES               PIC S9(04) VALUE +3         00620000
                                                       COMP SYNC.       00630000
           05  PC-MAX-DEADLOCKS             PIC S9(04) VALUE +3         00640000
                                                       COMP SYNC.       00650000
                                                                        00660000
           05  PC-CKPT-JOB-NAME             PIC  X(08) VALUE 'ICK027  '.00670000
           05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'ICKUP027'.00680013
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'ICKUP027'.00690013
                                                                        00700000
           05  PC-TRAN-PRES-FUNC-CODES.                                 00710000
               10  PC-TRAN-PRES-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   00720000
               10  PC-TRAN-PRES-FUNC-TRANS  PIC  X(05) VALUE 'TRANS'.   00730000
               10  PC-TRAN-PRES-FUNC-RSTRT  PIC  X(05) VALUE 'RSTRT'.   00740000
               10  PC-TRAN-PRES-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   00750000
                                                                        00760000
           05  PC-MINUS-ONE                 PIC S9(01) VALUE -1.        00770000
           05  PC-SYSTEM                    PIC  X(08) VALUE 'SYSTEM  '.00780000
                                                                        00790000
           05  PC-NOT-NULL                  PIC S9(04) VALUE +0.        00800000
           05  PC-NULL-DATA                 PIC S9(04) VALUE -1.        00810000
                                                                        00820000
      ******************************************************************00830000
      *                          VARIABLES                              00840000
      ******************************************************************00850000
       01  PROGRAM-VARIABLES.                                           00860000
           05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      00870000
           05  PV-DEADLOCK-COUNTER          PIC  9(03) VALUE ZERO.      00880000
           05  PV-RETRY-CNT                 PIC  9(03) VALUE ZERO.      00890000
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     00900000
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.     00910000
           05  PV-OPERATION-MSG             PIC  X(35) VALUE SPACE.     00920000
           05  PV-RET-CODE                  PIC  9(04) VALUE ZEROS.     00930000
                                                                        00940000
      ***************************************************************** 00950000
      *                      NULL INDICATORS                            00960000
      ***************************************************************** 00970000
       01  PROGRAM-NULL-INDICATORS.                                     00980000
      ** INDICATOR VALUE OF -1 INDICATES VALUE IS NULL, +0              00990000
      ** INDICATES NON-NULL VALUE                                       01000000
                                                                        01010000
           05  PN-IND                       PIC S9(04) VALUE +0  COMP.  01020000
               88  DB2-DATA-NULL                       VALUE -1.        01030000
                                                                        01040000
           05  PN-DEPT-NBR                  PIC S9(04) VALUE +0  COMP.  01050000
           05  PN-MJCL-NBR                  PIC S9(04) VALUE +0  COMP.  01060000
           05  PN-SBCL-NBR                  PIC S9(04) VALUE +0  COMP.  01070000
           05  PN-STYL-NBR                  PIC S9(04) VALUE +0  COMP.  01080000
                                                                        01090000
      ******************************************************************01100000
      *               SYSTEM TIME AND DATE VARIABLES                    01110000
      ******************************************************************01120000
       01  SYS-DATE-TIME.                                               01130000
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    01140000
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    01150000
                                                                        01160000
       01  ST-BEG-TIME.                                                 01170000
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     01180000
           05  FILLER                       PIC  X(01) VALUE ':'.       01190000
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     01200000
                                                                        01210000
       01  ST-END-TIME.                                                 01220000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     01230000
           05  FILLER                       PIC  X(01) VALUE ':'.       01240000
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     01250000
                                                                        01260000
       01  SD-EDIT-DATE.                                                01270000
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     01280000
           05  FILLER                       PIC  X(01) VALUE '-'.       01290000
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     01300000
           05  FILLER                       PIC  X(01) VALUE '-'.       01310000
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     01320000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     01330000
                                                                        01340000
      ******************************************************************01350000
      *                        ERROR HANDLING                           01360000
      ******************************************************************01370000
       01  ABEND-CODE                       PIC S9(04) VALUE ZEROS      01380000
                                                       COMP SYNC.       01390000
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.     01400000
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     01410000
                                                                        01420000
                                                                        01430000
      ******************************************************************01440000
      *  UPDATE RECORD                                                  01450000
      ******************************************************************01460000
           COPY ICRD048.                                                01470000
                                                                        01480000
      ******************************************************************01490000
      *  DB2 FORMATTED ERROR MESSAGE                                    01500000
      ******************************************************************01510000
           COPY DPWS004.                                                01520000
                                                                        01530000
      ******************************************************************01540000
      *  SQL COMMUNICATIONS WORK AREA                                   01550000
      ******************************************************************01560000
           COPY SQLCA2.                                                 01570000
                                                                        01580000
      ******************************************************************01590000
      *  COMMUNICATIONS AREA FOR DB2                                    01600000
      ******************************************************************01610000
           EXEC SQL                                                     01620000
               INCLUDE SQLCA                                            01630000
           END-EXEC.                                                    01640000
                                                                        01650000
      ******************************************************************01660000
      *  DCLGEN FOR ICT_LOC_CNT_SUM TABLE                               01670000
      ******************************************************************01680000
           EXEC SQL                                                     01690000
                INCLUDE ICT00012                                        01700000
           END-EXEC.                                                    01710000
                                                                        01720000
                                                                        01730000
      ******************************************************************01740000
      *  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA             01750000
      ******************************************************************01760000
260107     COPY DPWS991.                                                01770023
                                                                        01770123
           COPY DPLS001.                                                01771023
           05  WORK-STORAGE-PASSED.                                     01780000
      ******************************************************************01790000
      *PA - PROGRAM ACCUMULATORS                                        01800000
      ******************************************************************01810000
               10  PROGRAM-ACCUMULATORS.                                01820000
                   15  PA-ROWS-INSERT   PIC S9(11)           VALUE ZERO 01830000
                                                             COMP-3.    01840000
                   15  PA-ROWS-DELETE   PIC S9(11)           VALUE ZERO 01841000
                                                             COMP-3.    01842000
                                                                        01850000
       01  FILLER                          PIC X(4096)  VALUE SPACE.    01860000
                                                                        01870000
                                                                        01880000
                                                                        01890000
       PROCEDURE DIVISION.                                              01900000
      ******************************************************************01910000
      * MAINLINE-PROCESSING                                             01920000
      ******************************************************************01930000
                                                                        01940000
           PERFORM A100-INITIALIZE.                                     01950000
                                                                        01960000
           PERFORM B100-PROCESS-RECORDS                                 01970000
             UNTIL DP001-PREP-TRANS-EOF.                                01980000
                                                                        01990000
           PERFORM Z990-EOJ-ROUTINE.                                    02000000
                                                                        02010000
           GOBACK.                                                      02020000
                                                                        02030000
                                                                        02040000
      ******************************************************************02050000
      * PREFORMS TASK INITIATION                                        02060000
      ******************************************************************02070000
       A100-INITIALIZE.                                                 02080000
                                                                        02090000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02100000
                                                                        02110000
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            02120000
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            02130000
                                                                        02140000
           MOVE SYS-DATE (1:2) TO SD-CENT.                              02150000
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02160000
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02170000
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02180000
                                                                        02190000
           MOVE PC-TRAN-PRES-FUNC-OPEN TO DP001-FUNC-CODE.              02200000
                                                                        02210000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02220000
                                                                        02230000
                                                                        02240000
      ******************************************************************02250000
      * PROCESS RECORDS                                                 02260000
      ******************************************************************02270000
       B100-PROCESS-RECORDS.                                            02280000
                                                                        02290000
           SET PS-RESTART-NOT-REQUIRED TO TRUE.                         02300000
                                                                        02310000
           PERFORM C100-INSERT-RECS.                                    02320000
                                                                        02330000
      *----------------------------------------------------------------*02340000
      *  IF A RESTART IS REQUIRED, ISSUE A RESTART CALL RATHER THAN A  *02350000
      *  TRANS CALL.  THIS WILL ALLOW PROCESSING TO RESUME WITH THE    *02360000
      *  FIRST TRANSACTION AFTER THE LAST COMMIT.                      *02370000
      *----------------------------------------------------------------*02380000
           IF  PS-RESTART-REQUIRED                                      02390000
               MOVE PC-TRAN-PRES-FUNC-RSTRT TO DP001-FUNC-CODE          02400000
           ELSE                                                         02410000
               MOVE PC-TRAN-PRES-FUNC-TRANS TO DP001-FUNC-CODE          02420000
           END-IF.                                                      02430000
                                                                        02440000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      02450000
                                                                        02460000
           ADD 1 TO PA-READ.                                            02470000
                                                                        02480000
                                                                        02490000
      ******************************************************************02500000
      * INSERT PROCESS                                                  02510000
      ******************************************************************02520000
       C100-INSERT-RECS.                                                02530000
                                                                        02540000
           PERFORM C200-POPULATE-DCLGEN.                                02550000
           PERFORM C300-SET-NULL-IND.                                   02560000
           PERFORM C350-DELETE-DATA.                                    02570000
           PERFORM C400-INSERT-DATA.                                    02580000
                                                                        02590000
                                                                        02600000
      ******************************************************************02610000
      * POPULATE THE ICT00012 DCLGEN                                    02620000
      ******************************************************************02630000
       C200-POPULATE-DCLGEN.                                            02640000
                                                                        02650000
           MOVE IC048-LOC-NBR            TO ICT00012-LOC-NBR.           02660000
           MOVE IC048-CNT-STUP-ID        TO ICT00012-CNT-STUP-ID.       02670000
           MOVE IC048-LAST-UPD-TMST      TO ICT00012-LAST-UPD-TMST.     02680000
           MOVE IC048-CNT-SEQ-NBR        TO ICT00012-CNT-SEQ-NBR.       02690000
           MOVE IC048-LOC-EXPTED-UNT-QTY TO ICT00012-LOC-EXPTED-UNT-QTY.02700000
           MOVE IC048-KYD-UNT-QTY        TO ICT00012-KYD-UNT-QTY.       02710000
           MOVE IC048-DEPT-NBR           TO ICT00012-DEPT-NBR.          02720000
           MOVE IC048-MAJ-CL-NBR         TO ICT00012-MAJ-CL-NBR.        02730000
           MOVE IC048-SUB-CL-NBR         TO ICT00012-SUB-CL-NBR.        02740000
           MOVE IC048-STYL-ID            TO ICT00012-STYL-ID.           02750000
           MOVE IC048-EXEC-TMST          TO ICT00012-EXEC-TMST.         02760000
                                                                        02770000
                                                                        02780000
      ******************************************************************02790000
      * SET THE NULL INDICATORS                                         02800000
      ******************************************************************02810000
       C300-SET-NULL-IND.                                               02820000
                                                                        02830000
           IF  ICT00012-DEPT-NBR   = ZEROS                              02840000
               MOVE PC-NULL-DATA TO PN-DEPT-NBR                         02850000
           ELSE                                                         02860000
               MOVE PC-NOT-NULL  TO PN-DEPT-NBR                         02870000
           END-IF.                                                      02880000
           IF  ICT00012-MAJ-CL-NBR = ZEROS                              02890000
               MOVE PC-NULL-DATA TO PN-MJCL-NBR                         02900000
           ELSE                                                         02910000
               MOVE PC-NOT-NULL  TO PN-MJCL-NBR                         02920000
           END-IF.                                                      02930000
           IF  ICT00012-SUB-CL-NBR = ZEROS                              02940000
               MOVE PC-NULL-DATA TO PN-SBCL-NBR                         02950000
           ELSE                                                         02960000
               MOVE PC-NOT-NULL  TO PN-SBCL-NBR                         02970000
           END-IF.                                                      02980000
           IF  ICT00012-STYL-ID    = ZEROS                              02990000
               MOVE PC-NULL-DATA TO PN-STYL-NBR                         03000000
           ELSE                                                         03010000
               MOVE PC-NOT-NULL  TO PN-STYL-NBR                         03020000
           END-IF.                                                      03030000
                                                                        03040000
      ******************************************************************03040200
      * DELETE ICT_LOC_CNT_SUM TABLE                                    03040300
      ******************************************************************03040400
       C350-DELETE-DATA.                                                03041000
                                                                        03041100
           EXEC SQL                                                     03042117
             DELETE FROM ICT_LOC_CNT_SUM                                03042217
             WHERE LOC_NBR        = :ICT00012-LOC-NBR                   03043000
             AND CNT_STUP_ID      = :ICT00012-CNT-STUP-ID               03044000
             AND CNT_SEQ_NBR      = :ICT00012-CNT-SEQ-NBR               03045000
             AND LOC_EXPTED_UNT_QTY  = 0                                03046000
             AND KYD_UNT_QTY         = 0                                03046122
             AND COALESCE(DEPT_NBR,0)                                   03046222
                                  = :ICT00012-DEPT-NBR                  03046322
             AND COALESCE(MAJ_CL_NBR,0)                                 03047019
                                  = :ICT00012-MAJ-CL-NBR                03047119
             AND COALESCE(SUB_CL_NBR,0)                                 03047218
                                  = :ICT00012-SUB-CL-NBR                03047318
             AND COALESCE(STYL_ID,0)                                    03047418
                                  = :ICT00012-STYL-ID                   03047518
           END-EXEC.                                                    03048000
                                                                        03048100
           EVALUATE TRUE                                                03049000
                  WHEN SQLCODE = 0                                      03049100
                      ADD SQLERRD(3) TO PA-ROWS-DELETE                  03049200
                                                                        03049300
                  WHEN SQLCODE = +100                                   03049400
                      DISPLAY 'NOT FOUND - ' ICT00012-CNT-STUP-ID       03049500
                                       ' - ' ICT00012-LOC-NBR           03049600
                                       ' - ' ICT00012-CNT-SEQ-NBR       03049700
                                       ' - ' ICT00012-DEPT-NBR          03049800
                                       ' - ' ICT00012-MAJ-CL-NBR        03049915
                                       ' - ' ICT00012-SUB-CL-NBR        03050015
                                       ' - ' ICT00012-STYL-ID           03050115
                                                                        03050215
                  WHEN OTHER                                            03051000
                      DISPLAY '          '                              03052000
                      DISPLAY 'LOC-NBR = ' ICT00012-LOC-NBR             03053000
                      DISPLAY 'STUP-ID = ' ICT00012-CNT-STUP-ID         03054000
                      DISPLAY 'UPD-TMST= ' ICT00012-LAST-UPD-TMST       03055000
                      DISPLAY 'SEQ-NBR = ' ICT00012-CNT-SEQ-NBR         03056000
                      DISPLAY 'DEPT-NBR= ' ICT00012-DEPT-NBR            03057000
                      MOVE 'C350-DELETE-DATA'       TO                  03058000
                            PV-PARAGRAPH-NAME                           03058100
                      MOVE 'ICT_LOC_CNT_SUM DELETE' TO                  03059000
                            PV-DB2-OPER-ATTEMPTED                       03059100
                      PERFORM Z200-SQL-ABEND                            03059200
           END-EVALUATE.                                                03059300
                                                                        03059400
      ******************************************************************03060000
      * INSERT ICT_LOC_CNT_SUM TABLE                                    03070000
      ******************************************************************03080000
       C400-INSERT-DATA.                                                03090000
                                                                        03100000
           EXEC SQL                                                     03110000
             INSERT INTO ICT_LOC_CNT_SUM                                03120000
                     (LOC_NBR                                           03130000
                     ,CNT_STUP_ID                                       03140000
                     ,LAST_UPD_TMST                                     03150000
                     ,CNT_SEQ_NBR                                       03160000
                     ,LOC_EXPTED_UNT_QTY                                03170000
                     ,KYD_UNT_QTY                                       03180000
                     ,DEPT_NBR                                          03190000
                     ,MAJ_CL_NBR                                        03200000
                     ,SUB_CL_NBR                                        03210000
                     ,STYL_ID                                           03220000
                     ,EXEC_TMST)                                        03230000
           VALUES                                                       03240000
                   (:ICT00012-LOC-NBR                                   03250000
                   ,:ICT00012-CNT-STUP-ID                               03260000
                   ,:ICT00012-LAST-UPD-TMST                             03270000
                   ,:ICT00012-CNT-SEQ-NBR                               03280000
                   ,:ICT00012-LOC-EXPTED-UNT-QTY                        03290000
                   ,:ICT00012-KYD-UNT-QTY                               03300000
                   ,:ICT00012-DEPT-NBR    :PN-DEPT-NBR                  03310000
                   ,:ICT00012-MAJ-CL-NBR  :PN-MJCL-NBR                  03320000
                   ,:ICT00012-SUB-CL-NBR  :PN-SBCL-NBR                  03330000
                   ,:ICT00012-STYL-ID     :PN-STYL-NBR                  03340000
                   ,:ICT00012-EXEC-TMST)                                03350000
           END-EXEC.                                                    03360000
                                                                        03370000
                                                                        03380000
           EVALUATE TRUE                                                03390000
              WHEN SQLCODE = 0                                          03400000
                  ADD 1 TO PA-ROWS-INSERT                               03410000
                                                                        03420000
              WHEN OTHER                                                03430000
                  DISPLAY '          '                                  03440000
                  DISPLAY 'LOC-NBR = ' ICT00012-LOC-NBR                 03450000
                  DISPLAY 'STUP-ID = ' ICT00012-CNT-STUP-ID             03460000
                                                                        03461000
                  DISPLAY 'UPD-TMST= ' ICT00012-LAST-UPD-TMST           03470000
                  DISPLAY 'SEQ-NBR = ' ICT00012-CNT-SEQ-NBR             03480000
                  DISPLAY 'UNT-QTY = ' ICT00012-LOC-EXPTED-UNT-QTY      03490000
                  DISPLAY 'KYD-QTY = ' ICT00012-KYD-UNT-QTY             03500000
                  DISPLAY 'DEPT-NBR= ' ICT00012-DEPT-NBR                03510000
                  DISPLAY 'MAJ-CL  = ' ICT00012-MAJ-CL-NBR              03520000
                  DISPLAY 'SUB-CL  = ' ICT00012-SUB-CL-NBR              03530000
                  DISPLAY 'STYL-ID = ' ICT00012-STYL-ID                 03540000
                  DISPLAY 'EXEC TM = ' ICT00012-EXEC-TMST               03550000
                                                                        03560000
                  MOVE 'C400-INSERT-DATA'       TO PV-PARAGRAPH-NAME    03570000
                  MOVE 'ICT_LOC_CNT_SUM INSERT' TO PV-DB2-OPER-ATTEMPTED03580000
                                                                        03590000
                  PERFORM Z200-SQL-ABEND                                03600000
           END-EVALUATE.                                                03610000
                                                                        03620000
                                                                        03630000
      ******************************************************************03640000
      * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      03650000
      * PRESENTATION MODULE                                             03660000
      ******************************************************************03670000
       X100-BUILD-COMM-AREA-TRAN-PRES.                                  03680000
                                                                        03690000
           MOVE LENGTH OF                                               03700000
                WORK-STORAGE-PASSED TO DP001-WORK-STRG-LENGTH.          03710000
           MOVE PC-CKPT-JOB-NAME    TO DP001-JOB-NAME.                  03720000
           MOVE PC-CKPT-PLAN-NAME   TO DP001-PLAN-NAME.                 03730000
                                                                        03740000
           PERFORM X200-CALL-TRANS-PRES-MODULE.                         03750000
                                                                        03760000
           MOVE DP001-TRANS-RECORD  TO IC048-UPDT-REC.                  03770000
                                                                        03780000
           IF  DP001-CKPT-TAKEN                                         03790000
               ADD 1 TO PA-CKPT                                         03800000
                                                                        03810000
               INITIALIZE PV-DEADLOCK-COUNTER                           03820000
           END-IF.                                                      03830000
                                                                        03840000
                                                                        03850000
      ******************************************************************03860000
      * VERIFY THE RETURN CODE ON THE CALLED MODULE                     03870000
      ******************************************************************03880000
       X200-CALL-TRANS-PRES-MODULE.                                     03890000
                                                                        03900000
           PERFORM Z900-CALL-TRANS-PRES-MODULE.                         03910000
                                                                        03920000
           IF  DP001-GOOD-RET-CODE                                      03930000
           OR  DP001-CKPT-TAKEN                                         03940000
           OR  DP001-RESTART                                            03950000
               CONTINUE                                                 03960000
                                                                        03970000
           ELSE                                                         03980000
               MOVE 'X200-CALL-TRANS-PRES-MODULE' TO PV-PARAGRAPH-NAME  03990000
               MOVE '** BAD CALL TO TRANS PREP MODULE **'               04000000
                                                  TO PV-OPERATION-MSG   04010000
               MOVE DP001-RET-CODE                TO PV-RET-CODE        04020000
                                                                        04030000
               SET ABEND-4444-FORCED-ABEND TO TRUE                      04040000
                                                                        04050000
               PERFORM Z100-ABEND                                       04060000
           END-IF.                                                      04070000
                                                                        04080000
                                                                        04090000
      ******************************************************************04100000
      * NON-DB2 ABEND                                                   04110000
      ******************************************************************04120000
       Z100-ABEND.                                                      04130000
                                                                        04140000
           PERFORM Z999-CONTROLS.                                       04150000
                                                                        04160000
           DISPLAY '                     '.                             04170000
           DISPLAY '** ABEND           **'.                             04180000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          04190000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        04200000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         04210000
           DISPLAY '** RETURN CODE     ** = ' PV-RET-CODE.              04220000
                                                                        04230000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04240000
                                                                        04250000
                                                                        04260000
      ******************************************************************04270000
      * ABEND THE PROGRAM IN THE EVENT THAT A SQL ERROR OR WARNING      04280000
      * CODE OCCURS.                                                    04290000
      ******************************************************************04300000
       Z200-SQL-ABEND.                                                  04310000
                                                                        04320000
           PERFORM Z999-CONTROLS.                                       04330000
                                                                        04340000
           DISPLAY '** ABEND     **'.                                   04350000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                04360000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              04370000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.          04380000
                                                                        04390000
      ******************************************************************04400000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    04410000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         04420000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     04430000
      * FORMAT.                                                         04440000
      ******************************************************************04450000
           COPY DPPD004.                                                04460000
                                                                        04470000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04480000
                                                                        04490000
           CALL 'ILBOABN0' USING ABEND-CODE.                            04500000
                                                                        04510000
                                                                        04520000
      ******************************************************************04530000
      * PREFORMS TASK TERMINATION PROCESSING                            04540000
      ******************************************************************04550000
       Z990-EOJ-ROUTINE.                                                04560000
                                                                        04570000
           MOVE PC-TRAN-PRES-FUNC-CLOSE TO DP001-FUNC-CODE.             04580000
                                                                        04590000
           PERFORM X100-BUILD-COMM-AREA-TRAN-PRES.                      04600000
                                                                        04610000
           PERFORM Z999-CONTROLS.                                       04620000
                                                                        04630000
                                                                        04640000
      ******************************************************************04650000
      * DISPLAY CONTROLS FROM THE PROGRAM                               04660000
      ******************************************************************04670000
       Z999-CONTROLS.                                                   04680000
                                                                        04690000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04700000
                                                                        04710000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04720000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04730000
                                                                        04740000
           DISPLAY '                    '.                              04750000
           DISPLAY '**********************'.                            04760000
           DISPLAY '   ICKUP027 CONTROLS  '.                            04770013
           DISPLAY '**********************'.                            04780000
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         04790000
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          04800000
           DISPLAY 'END TIME  = ' ST-END-TIME.                          04810000
           DISPLAY '======================'.                            04820000
           DISPLAY 'RECORDS READ   = ' PA-READ.                         04830000
           DISPLAY 'CHECKPOINTS    = ' PA-CKPT.                         04840000
           DISPLAY '                '.                                  04850000
           DISPLAY 'ROWS UPDATE  = ' PA-ROWS-INSERT.                    04860000
           DISPLAY 'ROWS DELETE  = ' PA-ROWS-DELETE.                    04861000
           DISPLAY '                '.                                  04870000
                                                                        04880000
      ******************************************************************04890000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    04900000
      * MENTS THAT ARE REQUIRED TO INVOKE THE TRANSACTION PRESENTATION  04910000
      * MODULE (DPKUT901) OF THE DB2 CHECKPOINT RESTART SYSTEM.         04920000
      ******************************************************************04930000
           COPY DPPD001.                                                04940000
                                                                        04950000
                                                                        04960000
