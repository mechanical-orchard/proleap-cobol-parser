      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    FSKUT032.                                                 
       AUTHOR.        M JOHNSON.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-2004.                                                  
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * PROGRAM OBJECTIVE:                                                      
      * ------------------                                                      
      * SEQUENTIALLY READ A FILE CONTAINING ITEM LOCATION INFORMATION           
      * AND WRITE OUT A FILE THAT CONTAINS THE ORIGINAL ITEM LOCATION           
      * INFORMATION AND THE QTY ON ORDER FROM THE TSKSTOO DB2 TABLE             
      * IF THE DATA IS FOUND ON THE TSKSTOO TABLE                               
      *     COMPUTE THE ON ORDER AMOUNT                                         
      *     WRITE OUT THE UPDATED INFORMATION                                   
      * ELSE                                                                    
      *     DO NOT WRITE OUT A RECORD                                           
      * END IF                                                                  
      *                                                                         
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      * --------------                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * WR27494 09-01-2004 M JOHNSON   ORIGINAL IMPLEMENTATION                  
      *                                FALL04 PERFORMANCE TUNING                
      *                                                                         
      * WR27494 09-17-2004 M JOHNSON   THE INPUT RECORD LAYOUT WAS              
      *                                CHANGED AND READ INTO FSRD066.           
      *                                THE INPUT RECORD LENGTH WAS              
      *                                REDUCED AND ALSO ADDES MOVES             
      *                                FROM THE INPUT TO THE OUTPUT.            
      * WR27494 10-17-2004 B FORD      REMOVED INTERMEDIATE RECORD COUNT        
      *                                DISPLAYS AND UN-NEEDED FIELDS.           
      *                                ADDED RTL-CHG-DTE AND STAT-CHG-DT        
      *                                AND CHANGED LRECLS.                      
KP1005* WR29725 10-27-2005 K PETERSON  CHANGES REQUIRED FOR MARKDOWN            
KP1005*                                OPTIMIZATION.  INCREASED LENGTH          
KP1005*                                OF INPUT FILE.                           
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
      ******************************************************************        
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
      ******************************************************************        
       INPUT-OUTPUT SECTION.                                                    
                                                                                
      ******************************************************************        
       FILE-CONTROL.                                                            
                                                                                
           SELECT FS-CLEAR-PRC-FILE                                             
             ASSIGN TO INP01.                                                   
                                                                                
           SELECT FS-CLEAR-PRC-W-ONORDR-FILE                                    
             ASSIGN TO OUT01.                                                   
                                                                                
      /*****************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  FS-CLEAR-PRC-FILE                                                    
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
KP1005 01  FS-CLEAR-PRC-DTL            PIC  X(71).                              
                                                                                
       FD  FS-CLEAR-PRC-W-ONORDR-FILE                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  FS-CLEAR-PRC-W-ONORDR-DTL   PIC  X(250).                             
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EDIT-ERROR-SW        PIC  X(01)          VALUE 'N'.           
               88  PS-EDIT-ERROR                           VALUE 'Y'.           
           05  PS-CLEAR-PRC-EOF-SW     PIC  X(01)          VALUE 'N'.           
               88  PS-CLEAR-PRC-EOF                        VALUE 'Y'.           
           05  PS-TSKSTOO-FND-SW       PIC  X(01)          VALUE 'N'.           
               88  PS-TSKSTOO-FND                          VALUE 'Y'.           
                                                                                
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME         PIC  X(08)          VALUE                
               'FSKUT032'.                                                      
           05  PC-MAX-RETRIES          PIC S9(04)          VALUE +3             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
       01  PT-PROGRAM-TOTALS.                                                   
           05  PT-CLEAR-PRC-RCD-READ   PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-TSKSTOO-RCD-FND      PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-TSKSTOO-RCD-NOT-FND  PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-FS-CLR-PRC-WOO-WRTTN PIC S9(09)          VALUE +0             
                                                           COMP-3.              
           05  PT-DISPLAY-CNTR         PIC S9(09)          VALUE +0             
                                                           COMP-3.              
               88  PT-DISPLAY-CNTR-CNT                     VALUE                
                   5000000.                                                     
      *            5000.                                                        
                                                                                
      ******************************************************************        
       01  NI-NULL-INDICATORS.                                                  
           05  NI-ONORD-UNT-QTY        PIC S9(04)          VALUE +0             
                                                           COMP.                
                                                                                
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)          VALUE +0             
                                                           COMP SYNC.           
                                                                                
      ******************************************************************        
      *01  SDT-SYSTEM-DATE-TIME.                                                
      *    05  SDT-YYYYMMDD            PIC  X(10)          VALUE SPACE.         
      *    05  FILLER REDEFINES SDT-YYYYMMDD.                                   
      *        10  SDT-YYYYMMDD-CCYY.                                           
      *            15  SDT-YYYYMMDD-CC PIC  X(02).                              
      *            15  SDT-YYYYMMDD-YY PIC  X(02).                              
      *        10  FILLER              PIC  X(01).                              
      *        10  SDT-YYYYMMDD-MM     PIC  X(02).                              
      *        10  FILLER              PIC  X(01).                              
      *        10  SDT-YYYYMMDD-DD     PIC  X(02).                              
      *    05  SDT-HHMMSS              PIC  X(08)          VALUE SPACE.         
      *    05  FILLER REDEFINES SDT-HHMMSS.                                     
      *        10  SDT-HHMMSS-HH       PIC  X(02).                              
      *        10  FILLER              PIC  X(01).                              
      *        10  SDT-HHMMSS-MM       PIC  X(02).                              
      *        10  FILLER              PIC  X(01).                              
      *        10  SDT-HHMMSS-SS       PIC  X(02).                              
      *                                                                         
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL-SUB              PIC  9(03)          VALUE ZEROS.         
           05  PV-RETRY-COUNT          PIC  9(03)          VALUE ZEROS.         
           05  PV-PARAGRAPH-NAME       PIC  X(30)          VALUE SPACE.         
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC  X(30)          VALUE SPACE.         
                                                                                
      ******************************************************************        
      *  INVENTORY MANAGEMENT CLEARANCE ITEM INFO - INPUT RECORD                
      ******************************************************************        
           COPY FSRD066.                                                        
                                                                                
      ******************************************************************        
      *  FASHION SALES CLEARANCE ITEM INFO - OUTPUT RECORD                      
      ******************************************************************        
           COPY FSRD065.                                                        
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF SKU STORE ON ORDER              
      *  DB2 TABLE                                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKSTOO                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN LAYOUT AND COBOL DECLARATION OF LOCATION DB2 TABLE              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                            
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *  DB2 FORMATTED ERROR MESSAGE                                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
       0000-CLEAR-PRC-ONORDR-QTY.                                               
                                                                                
      *    PERFORM 0100-INITIALIZE.                                             
                                                                                
           PERFORM 8000-OPEN-FILES.                                             
                                                                                
           PERFORM 8100-READ-CLEAR-PRC-RCD.                                     
                                                                                
           PERFORM 1000-PROCESS-CLEAR-PRC-ITM                                   
               UNTIL PS-CLEAR-PRC-EOF                                           
               OR PS-EDIT-ERROR.                                                
                                                                                
           PERFORM 8900-CLOSE-FILES.                                            
                                                                                
           PERFORM 0900-DISPLAY-PGM-TOTALS.                                     
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * 0100-INITIALIZE.                                                        
      ******************************************************************        
      *0100-INITIALIZE.                                                         
      *                                                                         
      *    PERFORM 7600-SELECT-DATE-TIME.                                       
      *                                                                         
      *    DISPLAY 'FSKUT032 RUN DATE: ' SDT-YYYYMMDD ' - '                     
      *       SDT-HHMMSS.                                                       
      *                                                                         
      ******************************************************************        
      *    PERFORM 0900-DISPLAY-PGM-TOTALS.                                     
      ******************************************************************        
       0900-DISPLAY-PGM-TOTALS.                                                 
                                                                                
           DISPLAY '** FSKUT032 - PROGRAM TOTALS **'.                           
           DISPLAY '** FSKUT032 - ' PT-CLEAR-PRC-RCD-READ                       
               ' NUMBER RECORDS READ **'.                                       
           DISPLAY '** FSKUT032 - ' PT-TSKSTOO-RCD-FND                          
               ' TSKSTOO ROWS FND **'.                                          
           DISPLAY '** FSKUT032 - ' PT-TSKSTOO-RCD-NOT-FND                      
               ' TSKSTOO ROWS NOT FND **'.                                      
           DISPLAY '** FSKUT032 - ' PT-FS-CLR-PRC-WOO-WRTTN                     
               ' NUMBER RECORDS WRITTEN **'.                                    
                                                                                
      /                                                                         
      ******************************************************************        
      * 1000-PROCESS-CLEAR-PRC-ITM                                              
      ******************************************************************        
       1000-PROCESS-CLEAR-PRC-ITM.                                              
                                                                                
           PERFORM 7000-SELECT-TSKSTOO-ROW.                                     
           IF PS-TSKSTOO-FND                                                    
               PERFORM 2000-FMT-FS-CLR-PRC-WOO-RCD                              
           ELSE                                                                 
               MOVE ZEROS              TO FS065-ONORD-QTY                       
                                          FS065-ONORD-AMT                       
               ADD +1                  TO PT-TSKSTOO-RCD-NOT-FND                
           END-IF.                                                              
                                                                                
           PERFORM 8100-READ-CLEAR-PRC-RCD.                                     
                                                                                
      /                                                                         
      ******************************************************************        
       2000-FMT-FS-CLR-PRC-WOO-RCD.                                             
                                                                                
           INITIALIZE                  FS065-RCP-SKU-LIST-REC.                  
           MOVE FS066-ITM-NBR          TO FS065-ITM-NBR.                        
           MOVE FS066-STR-LVL-STAT-CDE TO FS065-STR-LVL-STAT-CDE.               
           MOVE FS066-SUM-LVL-CDE      TO FS065-SUM-LVL-CDE.                    
           MOVE FS066-SKU              TO FS065-SKU.                            
           MOVE FS066-INTR-UPC-ID      TO FS065-INTR-UPC-ID.                    
           MOVE FS066-UNT-RTL-AMT      TO FS065-UNT-RTL-AMT.                    
           MOVE FS066-STR-NBR          TO FS065-STR-NBR.                        
           MOVE FS066-UNT-RTL-CHG-DTE  TO FS065-UNT-RTL-CHG-DTE.                
           MOVE FS066-STAT-CHG-DTE     TO FS065-STAT-CHG-DTE.                   
                                                                                
           MOVE SKSTOO-ONORD-UNT-QTY   TO FS065-ONORD-QTY.                      
           COMPUTE FS065-ONORD-AMT =                                            
             FS065-ONORD-QTY * FS065-UNT-RTL-AMT.                               
                                                                                
           EVALUATE TRUE                                                        
             WHEN FS066-TICKET-COLOR = 'W'                                      
               MOVE 'WHITE'            TO FS065-TICKET-COLOR                    
             WHEN FS066-TICKET-COLOR = 'Y'                                      
               MOVE 'YELLOW'           TO FS065-TICKET-COLOR                    
             WHEN FS066-TICKET-COLOR = 'R'                                      
               MOVE 'RED'              TO FS065-TICKET-COLOR                    
             WHEN OTHER                                                         
               MOVE 'UNKNOWN'          TO FS065-TICKET-COLOR                    
           END-EVALUATE.                                                        
                                                                                
           MOVE FS066-SKU-LVL-STAT-CDE TO FS065-SKU-LVL-STAT-CDE.               
           ADD +1                      TO PT-TSKSTOO-RCD-FND.                   
           PERFORM 8200-WRITE-FS-CLR-PRC-WOO-RCD.                               
                                                                                
      /                                                                         
      ******************************************************************        
      * 7000-SELECT-TSKSTOO-ROW.                                                
      ******************************************************************        
       7000-SELECT-TSKSTOO-ROW.                                                 
                                                                                
           INITIALIZE                  PS-TSKSTOO-FND-SW                        
                                                                                
           PERFORM                                                              
             WITH TEST AFTER                                                    
             VARYING PV-SQL-SUB                                                 
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
               OR SQLCODE = 0                                                   
               OR SQLCODE = +100)                                               
                                                                                
             EXEC SQL                                                           
               SELECT ITM_NBR,                                                  
                      STR_NBR,                                                  
                      SUM(ONORD_UNT_QTY)                                        
               INTO :SKSTOO-ITM-NBR,                                            
                    :SKSTOO-STR-NBR,                                            
                    :SKSTOO-ONORD-UNT-QTY:NI-ONORD-UNT-QTY                      
               FROM TSKSTOO                                                     
               GROUP BY ITM_NBR, STR_NBR                                        
               HAVING ITM_NBR = :FS066-ITM-NBR                                  
               AND STR_NBR = :FS066-STR-NBR                                     
               WITH UR                                                          
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = 0                                                 
                 SET PS-TSKSTOO-FND    TO TRUE                                  
               WHEN SQLCODE = +100                                              
                 CONTINUE                                                       
               WHEN SQLCODE = -904                                              
                 CONTINUE                                                       
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < 0                                                 
               WHEN SQLCODE > 0                                                 
                 DISPLAY 'FSKUT032 - INVALID ITEM NMBR ' FS065-ITM-NBR          
                 DISPLAY 'FSKUT032 - INVALID LOC NMR ' FS065-STR-NBR            
                 MOVE '7000-SELECT-TSKSTOO-ROW'                                 
                                       TO PV-PARAGRAPH-NAME                     
                 MOVE 'SELECT TSKSTOO TABLE'                                    
                                       TO PV-DB2-OPERATION-ATTEMPTED            
                 PERFORM 9900-SQL-ABEND                                         
             END-EVALUATE                                                       
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE '7000-SELECT-TSKSTOO-ROW'                                   
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED'                                      
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7600-SELECT-DATE-TIME.                                                  
      ******************************************************************        
      *7600-SELECT-DATE-TIME.                                                   
      *                                                                         
      *    EXEC SQL                                                             
      *      SELECT CURRENT DATE,                                               
      *             CURRENT TIME                                                
      *      INTO :SDT-YYYYMMDD,                                                
      *            :SDT-HHMMSS                                                  
      *      FROM SYSIBM.SYSDUMMY1                                              
      *    END-EXEC.                                                            
                                                                                
      /*****************************************************************        
      * 8000-OPEN-FILES.                                                        
      ******************************************************************        
       8000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT FS-CLEAR-PRC-FILE.                                        
           OPEN OUTPUT FS-CLEAR-PRC-W-ONORDR-FILE.                              
                                                                                
      ******************************************************************        
      * 8100-READ-CLEAR-PRC-RCD                                                 
      ******************************************************************        
       8100-READ-CLEAR-PRC-RCD.                                                 
                                                                                
           READ FS-CLEAR-PRC-FILE                                               
             INTO FS066-RCP-SKU-LIST-REC                                        
               AT END                                                           
                 SET PS-CLEAR-PRC-EOF  TO TRUE.                                 
                                                                                
           IF NOT PS-CLEAR-PRC-EOF                                              
             ADD +1                    TO PT-CLEAR-PRC-RCD-READ                 
      *                                   PT-DISPLAY-CNTR                       
      *      IF PT-DISPLAY-CNTR-CNT                                             
      *        PERFORM 7600-SELECT-DATE-TIME                                    
      *        DISPLAY 'FSKUT032 - ' SDT-YYYYMMDD ' - '                         
      *          SDT-HHMMSS ' NBR RCDS READ '                                   
      *          PT-CLEAR-PRC-RCD-READ ' ITEM NBR '                             
      *          FS065-ITM-NBR ' LOC NBR ' FS065-STR-NBR                        
      *        MOVE +0                  TO PT-DISPLAY-CNTR                      
      *      END-IF                                                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 8200-WRITE-FS-CLR-PRC-WOO-RCD                                           
      ******************************************************************        
       8200-WRITE-FS-CLR-PRC-WOO-RCD.                                           
                                                                                
           WRITE FS-CLEAR-PRC-W-ONORDR-DTL                                      
             FROM FS065-RCP-SKU-LIST-REC.                                       
                                                                                
           ADD +1                      TO PT-FS-CLR-PRC-WOO-WRTTN.              
                                                                                
      ******************************************************************        
      * 8900-CLOSE-FILES.                                                       
      ******************************************************************        
       8900-CLOSE-FILES.                                                        
                                                                                
           CLOSE FS-CLEAR-PRC-FILE                                              
                 FS-CLEAR-PRC-W-ONORDR-FILE.                                    
                                                                                
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE                               
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = FSKUT032'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
           PERFORM 0900-DISPLAY-PGM-TOTALS.                                     
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0'             USING ABEND-CODE.                        
                                                                                
