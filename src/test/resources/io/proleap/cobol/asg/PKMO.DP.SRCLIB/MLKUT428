       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    MLKUT428.                                         00020001
       AUTHOR.        DAVE METZGER.                                     00030001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040001
       DATE-WRITTEN.  12-13-99.                                         00050001
       DATE-COMPILED.                                                   00060001
      ******************************************************************00070001
      *                                                                 00080001
      * MLKUT428 - DESCRIPTION.............                             00090001
      *                                                                 00100001
      * THIS PROGRAM WILL CREATE THE DATA INTEGRITY EXTRACT FILE FOR    00110001
      * TMNMCLS.                                                        00120001
      *                                                                 00130001
      *CHANGE HISTORY:                                                  00131007
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00132007
      * ------- ---------- ----------- -------------------------------- 00133007
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION     00134007
      ******************************************************************00140001
      *                                                                 00150001
       ENVIRONMENT DIVISION.                                            00160001
      *                                                                 00170001
       CONFIGURATION SECTION.                                           00180001
      *                                                                 00190001
       SOURCE-COMPUTER.        IBM-3090.                                00200001
       OBJECT-COMPUTER.        IBM-3090.                                00210001
      *                                                                 00220001
       INPUT-OUTPUT SECTION.                                            00230001
       FILE-CONTROL.                                                    00240001
                                                                        00250001
           SELECT DATA-INTEGRITY-CTL-FILE                               00260001
               ASSIGN TO INP01.                                         00270001
                                                                        00280001
           SELECT DATA-INTEGRITY-EXTR-FILE                              00290001
               ASSIGN TO OUT01.                                         00300001
                                                                        00310001
      ******************************************************************00320001
       DATA DIVISION.                                                   00330001
      ******************************************************************00340001
                                                                        00350001
       FILE SECTION.                                                    00360001
                                                                        00370001
       FD  DATA-INTEGRITY-CTL-FILE                                      00380001
           LABEL RECORDS ARE STANDARD                                   00390001
           RECORDING MODE IS F                                          00400001
           BLOCK CONTAINS 0 RECORDS.                                    00410001
       01  DATA-INTEGRITY-CTL-REC      PIC X(80).                       00420001
                                                                        00430001
       FD  DATA-INTEGRITY-EXTR-FILE                                     00440001
           RECORDING MODE IS F                                          00450001
           LABEL RECORDS ARE STANDARD                                   00460001
           BLOCK CONTAINS 0 RECORDS.                                    00470001
                                                                        00480001
W26603 01  DATA-INTEGRITY-EXTR-REC     PIC X(120).                      00490007
                                                                        00500001
      *                                                                 00510001
      ******************************************************************00520001
       WORKING-STORAGE SECTION.                                         00530001
      ******************************************************************00540001
                                                                        00550001
       01  PS-PROGRAM-SWITCHES.                                         00560001
           05  PS-TMNMCLS-DONE         PIC X       VALUE 'N'.           00570001
               88  PS-TMNMCLS-IS-DONE              VALUE 'Y'.           00580001
               88  PS-TMNMCLS-IS-NOT-DONE          VALUE 'N'.           00590001
                                                                        00600001
       01  PC-PROGRAM-CONSTANTS.                                        00610001
           05  PC-SUB-CL-NBR-ZEROS     PIC  X(02)  VALUE '00'.          00610102
                                                                        00611001
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00612001
           05  PA-TMNMCLS-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00620001
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00630001
                                                                        00640001
       01  CD-COUNTER-DISPLAY.                                          00760001
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00770001
                                                                        00780001
       01 ABEND-AREAS.                                                  00790001
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00800001
                                                   COMP SYNC.           00810001
               88  AC-BAD-DATA                     VALUE +4003.         00820001
               88  AC-DB2-ERROR                    VALUE +4013.         00830001
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00840001
                 '*****       ABEND'.                                   00850001
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00860001
                   '*****   PROGRAM: MLKUT428'.                         00870001
           05  AA-PARAGRAPH-LIT.                                        00880001
               10  FILLER              PIC X(17)   VALUE                00890001
                   '***** PARAGRAPH: '.                                 00900001
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00910001
           05  AA-MESSAGE-LINE.                                         00920001
               10  FILLER              PIC X(06)   VALUE '*****'.       00930001
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00940001
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00950001
                   '*****    DB2 ERROR'.                                00960001
           05  AA-DB2-OPERATION-LIT.                                    00970001
               10  FILLER              PIC X(17)   VALUE                00980001
                   '***** OPERATION: '.                                 00990001
               10  AA-DB2-OPERATION.                                    01000001
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        01010001
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        01020001
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        01030001
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        01040001
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        01050001
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        01060001
                                                                        01070001
       EJECT                                                            01080001
                                                                        01090001
      ******************************************************************01100001
      *** DATA INTEGRITY CONTROL CARD                                   01110001
      ******************************************************************01120001
           COPY MLRD110.                                                01130001
                                                                        01140001
                                                                        01150001
      ******************************************************************01151001
      *** DATA INTEGRITY TMNMCLS EXTRACT FILE                           01152001
      ******************************************************************01153001
W26603     COPY MLRD111.                                                01154007
                                                                        01155001
       EJECT                                                            01156001
                                                                        01157001
      ******************************************************************01158001
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01159001
      ******************************************************************01160001
           COPY DPWS004.                                                01170001
                                                                        01180001
      ******************************************************************01190001
      *  DB2 ERRORS                                                     01200001
      ******************************************************************01210001
           COPY SQLCA2.                                                 01220001
                                                                        01230001
      ******************************************************************01240001
      *  DB2 COMMUNICATION AREA                                         01250001
      ******************************************************************01260001
           EXEC SQL                                                     01270001
                INCLUDE SQLCA                                           01280001
           END-EXEC.                                                    01290001
                                                                        01300001
      ******************************************************************01310001
      *  DB2 TMNMCLS TABLE LAYOUT                                       01320001
      ******************************************************************01330001
           EXEC SQL                                                     01340001
                INCLUDE TMNMCLS                                         01350006
           END-EXEC.                                                    01360001
                                                                        01370001
      ******************************************************************01380001
      *  DB2 TMNMCLS CURSOR                                             01390001
      ******************************************************************01400001
           EXEC SQL                                                     01410001
               DECLARE TMNMCLS_CURSOR CURSOR FOR                        01420001
                SELECT FSCL_MN_END_DTE,                                 01430001
                       FSCL_MN_NBR,                                     01440001
                       DEPT_NBR,                                        01450001
                       MAJ_CL_NBR,                                      01460001
                       STR_NBR,                                         01461001
                       SLS_RTL_AMT,                                     01470001
                       MKDN_RTL_AMT,                                    01480001
                       MKUP_RTL_AMT,                                    01490001
                       XFER_IN_RTL_AMT,                                 01500001
                       XFER_OUT_RTL_AMT,                                01510001
                       RCPT_RTL_AMT,                                    01520001
                       RCPT_NET_COST_AMT,                               01530001
                       PADJ_RTL_AMT,                                    01540001
                       PADJ_NET_COST_AMT,                               01550001
                       INV_ADJ_RTL_AMT,                                 01560001
                       SHNK_RTL_AMT,                                    01570003
                       END_INV_RTL_AMT                                  01571003
                  FROM TMNMCLS                                          01580001
                 WHERE (FSCL_MN_END_DTE >= :ML110-STRT-DTE)             01590001
                   AND (FSCL_MN_END_DTE <= :ML110-END-DTE)              01600001
              ORDER BY FSCL_MN_END_DTE,                                 01610001
                       DEPT_NBR,                                        01620001
                       STR_NBR                                          01630001
W26603        WITH UR                                                   01631007
           END-EXEC.                                                    01640001
                                                                        01650001
       EJECT                                                            01660001
                                                                        01670001
      ******************************************************************01680001
       PROCEDURE DIVISION.                                              01690001
      ******************************************************************01700001
                                                                        01710001
      ******************************************************************01720001
       0000-MAINLINE-MLKUT428.                                          01730001
      ******************************************************************01740001
      *                                                                 01750001
           PERFORM 1000-INITIALIZATION.                                 01760001
                                                                        01770001
           PERFORM 2000-MAIN-PROCESS.                                   01780001
                                                                        01790001
           PERFORM 8000-EOJ.                                            01800001
                                                                        01810001
           STOP RUN.                                                    01820001
                                                                        01830001
       EJECT                                                            01840001
                                                                        01850001
      ******************************************************************01860001
       1000-INITIALIZATION.                                             01870001
      ******************************************************************01880001
      *                                                                 01890001
           OPEN INPUT  DATA-INTEGRITY-CTL-FILE                          01900001
                OUTPUT DATA-INTEGRITY-EXTR-FILE.                        01910001
                                                                        01920001
           PERFORM 4000-READ-CONTROL-CARD.                              01930001
                                                                        01940001
           DISPLAY ' '.                                                 01950001
           DISPLAY '*** MLKUT428 INPUT CONTROL CARD VALUES ***'         01960001
           DISPLAY ML110-DATA-INTEGRITY-CC-REC.                         01970001
                                                                        01980001
       EJECT                                                            01990001
                                                                        02000001
      ******************************************************************02010001
       2000-MAIN-PROCESS.                                               02020001
      ******************************************************************02030001
      *                                                                 02040001
           PERFORM 7100-OPEN-CURSOR-TMNMCLS.                            02050001
                                                                        02060001
           PERFORM 7200-FETCH-TMNMCLS.                                  02070001
                                                                        02080001
           PERFORM                                                      02090001
             UNTIL PS-TMNMCLS-IS-DONE                                   02100001
               PERFORM 5000-WRITE-EXTRACT                               02110001
               PERFORM 7200-FETCH-TMNMCLS                               02120001
           END-PERFORM.                                                 02130001
                                                                        02140001
           PERFORM 7300-CLOSE-CURSOR-TMNMCLS.                           02150001
                                                                        02160001
       EJECT                                                            02170001
                                                                        02180001
      ******************************************************************02190001
       4000-READ-CONTROL-CARD.                                          02200001
      ******************************************************************02210001
      *                                                                 02220001
           READ DATA-INTEGRITY-CTL-FILE                                 02230001
             INTO ML110-DATA-INTEGRITY-CC-REC                           02240001
               AT END                                                   02250001
                   MOVE '4000-READ-CONTROL-CARD'                        02260001
                                       TO AA-PARAGRAPH-NAME             02270001
                   MOVE 'CONTROL CARD IS MISSING'                       02280001
                                       TO AA-MESSAGE                    02290001
                   MOVE +4001          TO ABEND-CODE                    02300001
                   PERFORM 9900-COMMON-ABEND-ROUTINE                    02310001
           END-READ.                                                    02320001
                                                                        02330001
       EJECT                                                            02340001
                                                                        02350001
      ******************************************************************02360001
       5000-WRITE-EXTRACT.                                              02370001
      ******************************************************************02380001
      *                                                                 02390001
           MOVE MNMCLS-FSCL-MN-END-DTE  TO ML111-FSCL-MN-END-DTE.       02400001
           MOVE MNMCLS-FSCL-MN-NBR      TO ML111-FSCL-MN-NBR.           02410001
           MOVE MNMCLS-DEPT-NBR         TO ML111-DEPT-NBR.              02420001
           MOVE MNMCLS-MAJ-CL-NBR       TO ML111-MAJ-CL-NBR.            02420101
           MOVE PC-SUB-CL-NBR-ZEROS     TO ML111-SUB-CL-NBR.            02421001
           MOVE MNMCLS-STR-NBR          TO ML111-STR-NBR.               02430001
           MOVE MNMCLS-SLS-RTL-AMT      TO ML111-SLS-RTL-AMT.           02440001
           MOVE MNMCLS-MKDN-RTL-AMT     TO ML111-MKDN-RTL-AMT.          02450001
           MOVE MNMCLS-MKUP-RTL-AMT     TO ML111-MKUP-RTL-AMT.          02460001
           MOVE MNMCLS-XFER-IN-RTL-AMT  TO ML111-XFER-IN-RTL-AMT.       02470001
           MOVE MNMCLS-XFER-OUT-RTL-AMT TO ML111-XFER-OUT-RTL-AMT.      02480001
           MOVE MNMCLS-RCPT-RTL-AMT     TO ML111-RCPT-RTL-AMT.          02490001
           MOVE MNMCLS-RCPT-NET-COST-AMT                                02500001
                                        TO ML111-RCPT-NET-COST-AMT.     02510001
           MOVE MNMCLS-PADJ-RTL-AMT     TO ML111-PADJ-RTL-AMT.          02520001
           MOVE MNMCLS-PADJ-NET-COST-AMT                                02530001
                                        TO ML111-PADJ-NET-COST-AMT.     02540001
           MOVE MNMCLS-INV-ADJ-RTL-AMT  TO ML111-INV-ADJ-RTL-AMT.       02550001
           MOVE MNMCLS-SHNK-RTL-AMT     TO ML111-SHNK-RTL-AMT.          02560001
           MOVE MNMCLS-END-INV-RTL-AMT  TO ML111-END-INV-RTL-AMT.       02570001
                                                                        02580001
           WRITE DATA-INTEGRITY-EXTR-REC                                02590001
               FROM ML111-DATA-INTEGRITY-EXTR-REC.                      02600001
           ADD 1                        TO PA-OUTPUT-RECS-WRITTEN.      02610001
                                                                        02620001
       EJECT                                                            02630001
                                                                        02640001
      ******************************************************************02650001
       7100-OPEN-CURSOR-TMNMCLS.                                        02660001
      ******************************************************************02670001
      *                                                                 02680001
           EXEC SQL                                                     02690001
               OPEN TMNMCLS_CURSOR                                      02700001
           END-EXEC.                                                    02710001
                                                                        02720001
           EVALUATE TRUE                                                02730001
               WHEN SQLCODE = 0                                         02740001
                   CONTINUE                                             02750001
               WHEN OTHER                                               02760001
                   MOVE '7100-OPEN-CURSOR-TMNMCLS'                      02770001
                                       TO AA-PARAGRAPH-NAME             02780001
                   MOVE 'TMNMCLS TABLE ERROR'                           02790001
                                       TO AA-MESSAGE                    02800001
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02810001
                   PERFORM 9901-SQL-ERROR                               02820001
           END-EVALUATE.                                                02830001
                                                                        02840001
       EJECT                                                            02850001
                                                                        02860001
      ******************************************************************02870001
       7200-FETCH-TMNMCLS.                                              02880001
      ******************************************************************02890001
      *                                                                 02900001
           EXEC SQL                                                     02910001
               FETCH TMNMCLS_CURSOR                                     02920001
                INTO :MNMCLS-FSCL-MN-END-DTE,                           02930001
                     :MNMCLS-FSCL-MN-NBR,                               02940001
                     :MNMCLS-DEPT-NBR,                                  02950001
                     :MNMCLS-MAJ-CL-NBR,                                02951001
                     :MNMCLS-STR-NBR,                                   02960001
                     :MNMCLS-SLS-RTL-AMT,                               02970001
                     :MNMCLS-MKDN-RTL-AMT,                              02980001
                     :MNMCLS-MKUP-RTL-AMT,                              02990001
                     :MNMCLS-XFER-IN-RTL-AMT,                           03000001
                     :MNMCLS-XFER-OUT-RTL-AMT,                          03010001
                     :MNMCLS-RCPT-RTL-AMT,                              03020001
                     :MNMCLS-RCPT-NET-COST-AMT,                         03030001
                     :MNMCLS-PADJ-RTL-AMT,                              03040001
                     :MNMCLS-PADJ-NET-COST-AMT,                         03050001
                     :MNMCLS-INV-ADJ-RTL-AMT,                           03060001
                     :MNMCLS-SHNK-RTL-AMT,                              03070003
                     :MNMCLS-END-INV-RTL-AMT                            03071004
           END-EXEC.                                                    03080001
                                                                        03090001
           EVALUATE TRUE                                                03100001
               WHEN SQLCODE = 0                                         03110001
                   ADD +1              TO PA-TMNMCLS-ROWS-FETCHED       03120001
               WHEN SQLCODE = +100                                      03130001
                   MOVE 'Y'            TO PS-TMNMCLS-DONE               03140001
               WHEN OTHER                                               03150001
                   MOVE '7200-FETCH-TMNMCLS'                            03160001
                                       TO AA-PARAGRAPH-NAME             03170001
                   MOVE 'TMNMCLS TABLE ERROR'                           03180001
                                       TO AA-MESSAGE                    03190001
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03200001
                   PERFORM 9901-SQL-ERROR                               03210001
           END-EVALUATE.                                                03220001
                                                                        03230001
       EJECT                                                            03240001
                                                                        03250001
      ******************************************************************03260001
       7300-CLOSE-CURSOR-TMNMCLS.                                       03270001
      ******************************************************************03280001
      *                                                                 03290001
           EXEC SQL                                                     03300001
               CLOSE TMNMCLS_CURSOR                                     03310001
           END-EXEC.                                                    03320001
                                                                        03330001
           EVALUATE TRUE                                                03340001
               WHEN SQLCODE = 0                                         03350001
                   CONTINUE                                             03360001
               WHEN OTHER                                               03370001
                   MOVE '7300-CLOSE-CURSOR-TMNMCLS'                     03380001
                                       TO AA-PARAGRAPH-NAME             03390001
                   MOVE 'TMNMCLS TABLE ERROR'                           03400001
                                       TO AA-MESSAGE                    03410001
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03420001
                   PERFORM 9901-SQL-ERROR                               03430001
           END-EVALUATE.                                                03440001
                                                                        03450001
       EJECT                                                            03460001
                                                                        03470001
      ******************************************************************03480001
       8000-EOJ.                                                        03490001
      ******************************************************************03500001
      *                                                                 03510001
           DISPLAY ' '.                                                 03520001
           DISPLAY '  PROGRAM MLKUT428 PROCESSING SUMMARY'.             03530001
           DISPLAY '  -----------------------------------'.             03540001
           DISPLAY ' '.                                                 03550001
      *                                                                 03560001
           MOVE PA-TMNMCLS-ROWS-FETCHED TO CD-COUNT-HOLD1.              03570001
           DISPLAY 'TMNMCLS ROWS FETCHED =      ' CD-COUNT-HOLD1.       03580001
      *                                                                 03590001
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               03600001
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       03610001
      *                                                                 03620001
           DISPLAY ' '.                                                 03630001
                                                                        03640001
           CLOSE DATA-INTEGRITY-CTL-FILE.                               03650001
           CLOSE DATA-INTEGRITY-EXTR-FILE.                              03660001
                                                                        03670001
       EJECT                                                            03680001
                                                                        03690001
      ******************************************************************03700001
       9900-COMMON-ABEND-ROUTINE.                                       03710001
      ******************************************************************03720001
      *                                                                 03730001
           DISPLAY AA-ABEND-LIT.                                        03740001
           DISPLAY AA-PROGRAM-LIT.                                      03750001
           DISPLAY AA-PARAGRAPH-LIT.                                    03760001
           DISPLAY AA-MESSAGE-LINE.                                     03770001
           DISPLAY ABEND-CODE.                                          03780001
                                                                        03790001
           CALL 'ILBOABN0' USING ABEND-CODE.                            03800001
                                                                        03810001
       EJECT                                                            03820001
                                                                        03830001
      ******************************************************************03840001
       9901-SQL-ERROR.                                                  03850001
      ******************************************************************03860001
      *                                                                 03870001
           CLOSE DATA-INTEGRITY-CTL-FILE                                03880001
                 DATA-INTEGRITY-EXTR-FILE.                              03890001
                                                                        03900001
           DISPLAY AA-ABEND-LIT.                                        03910001
           DISPLAY AA-DB2-ERROR-LIT.                                    03920001
           DISPLAY AA-PROGRAM-LIT.                                      03930001
           DISPLAY AA-PARAGRAPH-LIT.                                    03940001
           DISPLAY AA-MESSAGE-LINE.                                     03950001
           DISPLAY AA-DB2-OPERATION-LIT.                                03960001
           SET AC-DB2-ERROR TO TRUE.                                    03970001
           MOVE +4013                  TO ABEND-CODE.                   03980001
                                                                        03990001
           COPY DPPD004.                                                04000001
           CALL 'ILBOABN0' USING ABEND-CODE.                            04010001
                                                                        04020001
