000100*-----------------------*                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*-----------------------*                                                 
000400 PROGRAM-ID.    TRKCS170.                                                 
000500 AUTHOR.        MARGARET MORRISON.                                        
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  01/99.                                                    
000800 DATE-COMPILED.                                                           
000900*----------------------------------------------------------------*        
001000*   Y170 - IMPORT CUSTOMS ENTRY CRIT SPEC.                       *        
001100*                                                                *        
001600*   THIS PROGRAM ALLOWS THE USER TO ENTER ONE OF THE FOLLOWING:  *        
001610*   1)CUSTOMS ENTRY ID, 2)CONTAINER, 3)PO NUMBER, OR             *        
001620*   4)MBOL AND/OR HBOL WHICH IS USED TO BRING UP THE CUSTOMS     *        
001630*   ENTRY LIST BY WHATEVER VALUE THAT THE USER ENTERED.          *        
001700*----------------------------------------------------------------*        
263697* CHANGED 11/08/2021    BY JIM HUNTER      WR: CHG0263697                 
263697* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263697* MAKING THE CALL DYNAMIC VS STATIC.                                      
263697*---------------------------------------------------------------*         
CICS  * CHANGED  1/12/2023    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001800                                                                          
001900 ENVIRONMENT DIVISION.                                                    
002000                                                                          
002100 DATA DIVISION.                                                           
002200                                                                          
002300 WORKING-STORAGE SECTION.                                                 
002400                                                                          
002500 01  DK-DB2-KEY.                                                          
002600     05  DK-PO-NBR                 PIC S9(09) COMP.                       
002700*                                                                         
002800 01  PC-PROGRAM-CONSTANTS.                                                
002900     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR170A  '.         
003000     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM170 '.         
003100     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS170'.         
003200     05  PC-MAX-RETRIES            PIC S9(04)  VALUE  +3                  
003300                                               COMP.                      
003400     05  PC-TSYMSG-NUMBERS.                                               
003600         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
003610         10  PC-TSYMSG-01882       PIC  9(05)  VALUE  01882.              
003620         10  PC-TSYMSG-01883       PIC  9(05)  VALUE  01883.              
003700*                                                                         
003800 01  PS-PROGRAM-SWITCHES.                                                 
003900     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
004000         88  PS-ERROR                          VALUE  'Y'.                
004100         88  PS-NO-ERROR                       VALUE  'N'.                
004200*                                                                         
004300 01  PV-PROGRAM-VARIABLES.                                                
004400     05  PV-RETRY-COUNTER          PIC S9(04)  VALUE +0                   
004500                                               COMP SYNC.                 
004600*                                                                         
004700 01  PM-PROGRAM-MESSAGES.                                                 
004800     05  PM-ABEND-INFO-A.                                                 
004900         10  FILLER                PIC X(15)   VALUE                      
005000                                   'DP020-SRC-AID: '.                     
005100         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
005200         10  FILLER                PIC X(19)   VALUE                      
005300                                   '. DP020-FK-ACTION: '.                 
005400         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
005500         10  FILLER                PIC X(22)   VALUE                      
005600                                   '. DP020-FK-DISPN-IND: '.              
005700         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
005800         10  FILLER                PIC X(01)   VALUE '.'.                 
005900         10  FILLER                PIC X(19)   VALUE SPACE.               
006000                                                                          
006100*----------------------------------------------------------------*        
006200*    MAP LAYOUT                                                  *        
006300*----------------------------------------------------------------*        
006400                                                                          
006500     COPY TRKM170.                                                        
006600                                                                          
006700 EJECT                                                                    
006800*----------------------------------------------------------------*        
006900*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
007000*----------------------------------------------------------------*        
007100                                                                          
007200     COPY DPWS015.                                                        
007300                                                                          
007400 EJECT                                                                    
007500*----------------------------------------------------------------*        
007600*    FUNCTION KEYS COPYBOOK                                      *        
007700*----------------------------------------------------------------*        
007800                                                                          
007900     COPY DPWS016.                                                        
008000                                                                          
008100 EJECT                                                                    
008200*----------------------------------------------------------------*        
008300*  THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT     *        
008400*  SUB-ROUTINE (DPKUT100).                                       *        
008500*----------------------------------------------------------------*        
008600                                                                          
008700     COPY DPWS010I.                                                       
008800                                                                          
008900 EJECT                                                                    
009000*----------------------------------------------------------------*        
009100*  THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS             *        
009200*  ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                     *        
009300*----------------------------------------------------------------*        
009400                                                                          
009500     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
009700 EJECT                                                                    
009800*----------------------------------------------------------------*        
009900*    STANDARD COMMAREA.                                          *        
010000*----------------------------------------------------------------*        
010100                                                                          
010200     COPY DPWS020.                                                        
010300     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
010400                                                                          
010500*----------------------------------------------------------------*        
010600*    INTER-APPLICATION COMMAREA.                                 *        
010700*----------------------------------------------------------------*        
010800                                                                          
010900         10  INTER-APPL-COMM-AREA PIC X(200).                             
011000     COPY TRWS114.                                                        
011100                                                                          
011200*----------------------------------------------------------------*        
011300*    SPECIFIC COMMMAREA FOR TRKCS170.                            *        
011400*----------------------------------------------------------------*        
011500                                                                          
011600         10  ASC-SPECIFIC-COMMAREA.                                       
011900                                                                          
012000             15  ASC-PO-NBR.                                              
012200                 20  ASC-PO-NBR-N            PIC  9(09).                  
012300             15  ASC-VND-NME                 PIC  X(30).                  
012310             15  ASC-CSTM-ENTRY-ID           PIC  X(12).                  
012320             15  ASC-CONTNR-ID               PIC  X(12).                  
012330             15  ASC-HSE-BOL-NBR             PIC  X(30).                  
012340             15  ASC-MSTR-BOL-NBR            PIC  X(30).                  
012400                                                                          
012900 EJECT                                                                    
013000*----------------------------------------------------------------*        
013100*    ABEND PROCESSING COPYBOOK.                                  *        
013200*----------------------------------------------------------------*        
013300                                                                          
013400     COPY DPWS013.                                                        
013500                                                                          
013600 EJECT                                                                    
013700*---------------------------------------------------------------*         
013800*    DB2 AREA FOR TPURCHS                                       *         
013900*---------------------------------------------------------------*         
014000     EXEC SQL                                                             
014100          INCLUDE TPURCHS                                                 
014200     END-EXEC.                                                            
014300*---------------------------------------------------------------*         
014400*    DB2 AREA FOR TENTNME                                       *         
014500*---------------------------------------------------------------*         
014600     EXEC SQL                                                             
014700          INCLUDE TENTNME                                                 
014800     END-EXEC.                                                            
014900*---------------------------------------------------------------*         
015000*    DB2 AREA FOR COMMUNICATIONS                                *         
015100*---------------------------------------------------------------*         
015200     EXEC SQL                                                             
015300          INCLUDE SQLCA                                                   
015400     END-EXEC.                                                            
015500*---------------------------------------------------------------*         
015600                                                                          
015700 LINKAGE SECTION.                                                         
015800                                                                          
015900 01  DFHCOMMAREA.                                                         
016000     05  FILLER                         OCCURS  1 TO 4072 TIMES           
016100                                        DEPENDING ON EIBCALEN.            
016200         10  FILLER                     PIC X.                            
016300                                                                          
016400*---------------------------------------------------------------*         
016500 PROCEDURE DIVISION.                                                      
016600*---------------------------------------------------------------*         
016700*  THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM.  *         
016800*  THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API   *         
016900*  MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.             *         
017000*                                                               *         
017100*  THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE   *         
017200*  LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.        *         
017300*---------------------------------------------------------------*         
017400 0000-MAIN-MODULE.                                                        
017500                                                                          
017600     INITIALIZE DP030-CICS-API-FIELDS.                                    
017700     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
017800     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
017900     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
018000     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
018100     MOVE LENGTH OF TR170AI        TO DP030-MAP-LENGTH (1).               
018200     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
018300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
018400*                                                                         
018500     PERFORM 1000-CONTROL-PROCESSING                                      
018600*                                                                         
018700     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
018800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
018900                                                                          
019000*---------------------------------------------------------------*         
019100*    CALL THE ARCHITECTURE API.                                 *         
019200*---------------------------------------------------------------*         
019300 0001-CALL-CICS-ARCH-API.                                                 
019400                                                                          
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
019600                                    DFHCOMMAREA                           
019700                                    DP030-CICS-API-FIELDS                 
019800                                    DP020-STANDARD-COMMAREA               
019900                                    TR170AI.                              
020000*                                                                         
020100     IF  DP030-RC-CALL-SUCCESSFUL  OR                                     
020200         DP030-RC-APPL-ERROR                                              
020300         CONTINUE                                                         
020400     ELSE                                                                 
020500         SET DP013-NO-ROLLBACK                                            
020600             DP013-XCTL-DISPLAY-RESTART                                   
020700             DP013-CICS-ABEND      TO TRUE                                
020800         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
020900                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
021200         MOVE DP030-RETURN-CODE                                           
021300                                   TO DP013-MESSAGE-TEXT (3)              
021400         PERFORM DP013-0000-PROCESS-ABEND                                 
021500     END-IF.                                                              
021600                                                                          
021700 EJECT                                                                    
021800*----------------------------------------------------------------*        
021900*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
022000*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
022100*----------------------------------------------------------------*        
022200 1000-CONTROL-PROCESSING.                                                 
022300                                                                          
022400     EVALUATE TRUE                                                        
022500         WHEN DP020-NEXT-ACT-INITIAL                                      
022600             INITIALIZE ASC-SPECIFIC-COMMAREA                             
022700             PERFORM 4000-BUILD-INITIAL-PANEL                             
022800                                                                          
022900         WHEN DP020-NEXT-ACT-READ-MAP                                     
023000             PERFORM 2000-FKEY-PROCESSING                                 
023100                                                                          
023200         WHEN DP020-NEXT-ACT-RETURN                                       
023300             MOVE ASC-PO-NBR-N TO DK-PO-NBR                               
023400             PERFORM 3300-SELECT-VENDOR-NAME                              
023500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
023510             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
023600                                                                          
023700         WHEN OTHER                                                       
023800             SET DP013-LOGIC-ABEND                                        
023900                 DP013-NO-ROLLBACK TO TRUE                                
024000             MOVE '1000-CONTROL-PROCESSING'                               
024100                                   TO  DP013-PARAGRAPH                    
024200             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
024300                                   TO  DP013-MESSAGE-TEXT(1)              
024400             MOVE DP020-NEXT-APPL-ACTIVITY                                
024500                                   TO DP013-MESSAGE-TEXT(2)               
024600             PERFORM DP013-0000-PROCESS-ABEND                             
024700     END-EVALUATE.                                                        
024800                                                                          
024900 EJECT                                                                    
025000*----------------------------------------------------------------*        
025100* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
025200* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
025300* EDITS.                                                         *        
025400*----------------------------------------------------------------*        
025500 2000-FKEY-PROCESSING.                                                    
025600                                                                          
025700     EVALUATE TRUE                                                        
025800         WHEN DP020-SRC-AID = DP016-CLEAR                                 
025900             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
026000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
026100                                                                          
026200         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
026300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
026400             PERFORM 4000-BUILD-INITIAL-PANEL                             
026500             PERFORM 3200-RESET-ATTRIBUTES                                
026600                                                                          
026700         WHEN OTHER                                                       
026800             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
026900             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
027000             IF  PS-NO-ERROR                                              
027100                 SET DP030-CONTINUE-GO TO TRUE                            
027200                 PERFORM 2100-CHECK-FUNCTION-KEY                          
027300             ELSE                                                         
027400                 MOVE DP015-UNP-ALP-BRT-OFF                               
027500                                             TO AENTRIDA                  
027600                 MOVE DP015-RED              TO AENTRIDC                  
027700                 MOVE DP015-REVERSE          TO AENTRIDH                  
027800                 MOVE -1                     TO AENTRIDL                  
027900                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
028000                 MOVE SPACES                 TO AVNDNMEO                  
028100                 SET DP030-OVERRIDE-GO       TO TRUE                      
028200             END-IF                                                       
028300     END-EVALUATE.                                                        
028400                                                                          
028500 EJECT                                                                    
028600*----------------------------------------------------------------*        
028700*  ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED      *        
028800*  FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED  *        
028900*  FROM THE CICS ARCHITECTURE API.                               *        
029000*----------------------------------------------------------------*        
029100 2100-CHECK-FUNCTION-KEY.                                                 
029200                                                                          
029300     EVALUATE TRUE                                                        
029400         WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                         
029500         WHEN DP020-FK-DISPN-GO (DP020-SRC-AID)                           
029600             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
029700                                                                          
029800         WHEN DP020-SRC-AID = DP016-ENTER                                 
029900             MOVE ASC-PO-NBR-N TO DK-PO-NBR                               
030000             PERFORM 3300-SELECT-VENDOR-NAME                              
030100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
030200                                                                          
030300         WHEN OTHER                                                       
030400             SET DP013-NO-ROLLBACK                                        
030500                 DP013-XCTL-DISPLAY-RESTART                               
030600                 DP013-CICS-ABEND  TO TRUE                                
030700             MOVE '2100-CHECK-FUNCTION-KEY'                               
030800                                   TO DP013-PARAGRAPH                     
030900             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
031000                                   TO DP013-MESSAGE-TEXT (1)              
031100             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
031200                                   TO DP013-MESSAGE-TEXT (2)              
031300             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
031400             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
031500                                   TO PM-A-FK-ACTION                      
031600             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
031700                                   TO PM-A-FK-DISPN                       
031800             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
031900             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
032000                                   TO DP013-MESSAGE-TEXT (4)              
032100             PERFORM DP013-0000-PROCESS-ABEND                             
032200     END-EVALUATE.                                                        
032300                                                                          
032400 EJECT                                                                    
032500*----------------------------------------------------------------*        
032600*  MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE    *        
032700*  INTER-APPLICATION COMMAREA.                                   *        
032800*----------------------------------------------------------------*        
032900 2110-PREPARE-INTER-APPL-COMM.                                            
033000                                                                          
033201     INITIALIZE TR114-INTER-APPL-COMMAREA                                 
033202        REPLACING ALPHANUMERIC BY SPACES                                  
033203                  NUMERIC BY ZEROS.                                       
033210                                                                          
033300     MOVE ASC-PO-NBR           TO TR114-PO-NBR.                           
033310     MOVE ASC-CSTM-ENTRY-ID    TO TR114-CSTM-ENTRY-ID.                    
033320     MOVE ASC-CONTNR-ID        TO TR114-CONTNR-ID.                        
033330     MOVE ASC-HSE-BOL-NBR      TO TR114-HSE-BOL-NBR.                      
033340     MOVE ASC-MSTR-BOL-NBR     TO TR114-MSTR-BOL-NBR.                     
033400                                                                          
033500 EJECT                                                                    
033600*----------------------------------------------------------------*        
033700* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
033800* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
033900* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
034000*----------------------------------------------------------------*        
034100 2200-MOVE-SCREEN-TO-COMMAREA.                                            
034200                                                                          
035300     IF  APONBRL > ZERO                                                   
035400          MOVE APONBRI             TO ASC-PO-NBR                          
035510     ELSE                                                                 
035520         IF  APONBRF = DP015-ERASE-EOF                                    
035530              MOVE SPACES          TO ASC-PO-NBR                          
035600     END-IF.                                                              
035700                                                                          
035702     IF  AENTRIDL > ZERO                                                  
035703          MOVE AENTRIDI            TO ASC-CSTM-ENTRY-ID                   
035705     ELSE                                                                 
035706         IF  AENTRIDF = DP015-ERASE-EOF                                   
035707              MOVE SPACES          TO ASC-CSTM-ENTRY-ID                   
035708         END-IF                                                           
035709     END-IF.                                                              
035710                                                                          
035711     IF  ACNTRIDL > ZERO                                                  
035720          MOVE ACNTRIDI            TO ASC-CONTNR-ID                       
035730     ELSE                                                                 
035740         IF  ACNTRIDF = DP015-ERASE-EOF                                   
035750              MOVE SPACES          TO ASC-CONTNR-ID                       
035760         END-IF                                                           
035770     END-IF.                                                              
035780                                                                          
035799     IF  AMBOLL > ZERO                                                    
035800          MOVE AMBOLI              TO ASC-MSTR-BOL-NBR                    
035802     ELSE                                                                 
035803         IF  AMBOLF   = DP015-ERASE-EOF                                   
035804              MOVE SPACES          TO ASC-MSTR-BOL-NBR                    
035805         END-IF                                                           
035806     END-IF.                                                              
035807                                                                          
035808     IF  AHBOLL > ZERO                                                    
035809          MOVE AHBOLI              TO ASC-HSE-BOL-NBR                     
035810     ELSE                                                                 
035811         IF  AHBOLF   = DP015-ERASE-EOF                                   
035812              MOVE SPACES          TO ASC-HSE-BOL-NBR                     
035813         END-IF                                                           
035814     END-IF.                                                              
035815                                                                          
035820 EJECT                                                                    
035900*----------------------------------------------------------------*        
036000* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
036100* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
036200* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
036300* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
036400*----------------------------------------------------------------*        
036500 3000-EDIT-DATA-IN-COMMAREA.                                              
036600                                                                          
036601     PERFORM 3200-RESET-ATTRIBUTES.                                       
036602                                                                          
036760*--- TSYMSG-01882-ONE VALUE MUST BE ENTERED                               
036761     EVALUATE TRUE                                                        
036762         WHEN ASC-CSTM-ENTRY-ID = SPACES  AND                             
036763              ASC-CONTNR-ID     = SPACES  AND                             
036765              ASC-HSE-BOL-NBR   = SPACES  AND                             
036766              ASC-MSTR-BOL-NBR  = SPACES  AND                             
036767             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036768                                          OR LOW-VALUES)                  
036769               SET PS-ERROR                                               
036770                   DP020-MSG-FATAL         TO TRUE                        
036771               MOVE PC-TSYMSG-01882        TO DP020-MSG-NUMBER            
036772               MOVE -1                     TO AENTRIDL                    
036773               SET DP030-SET-CURSOR-APPL-1 TO TRUE                        
036774                                                                          
036775         WHEN ASC-CSTM-ENTRY-ID > SPACES  AND                             
036776              ASC-CONTNR-ID     = SPACES  AND                             
036777              ASC-HSE-BOL-NBR   = SPACES  AND                             
036778              ASC-MSTR-BOL-NBR  = SPACES  AND                             
036779             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036780                                          OR LOW-VALUES)                  
036783               CONTINUE                                                   
036784                                                                          
036786         WHEN ASC-CONTNR-ID     > SPACES  AND                             
036787              ASC-CSTM-ENTRY-ID = SPACES  AND                             
036789              ASC-HSE-BOL-NBR   = SPACES  AND                             
036790              ASC-MSTR-BOL-NBR  = SPACES  AND                             
036792             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036793                                          OR LOW-VALUES)                  
036794               CONTINUE                                                   
036799                                                                          
036801         WHEN ASC-PO-NBR        > LOW-VALUES AND                          
036802              ASC-PO-NBR        > SPACES  AND                             
036803              ASC-PO-NBR        > ZEROES  AND                             
036804              ASC-CSTM-ENTRY-ID = SPACES  AND                             
036805              ASC-CONTNR-ID     = SPACES  AND                             
036806              ASC-HSE-BOL-NBR   = SPACES  AND                             
036807              ASC-MSTR-BOL-NBR  = SPACES                                  
036811*----TSYMSG-008-MUST BE NUMERIC                                           
036812               MOVE ASC-PO-NBR          TO DP010I-UNEDITED-FIELD          
036813               MOVE LENGTH OF ASC-PO-NBR TO DP010I-MAXIMUM-DIGITS         
036814               MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS        
036815               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                    
263697               CALL DP010I-NUMERIC-EDIT-ROUTINE                           
263697                    USING DP010I-NUMERIC-EDIT-AREA                        
036817               IF  DP010I-ERROR-DETECTED                                  
036818                    SET PS-ERROR                                          
036819                        DP020-MSG-FATAL   TO TRUE                         
036820                    MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER             
036821               ELSE                                                       
036822                    MOVE DP010I-NUMERIC-FIELD                             
036823                                          TO ASC-PO-NBR-N                 
036825               END-IF                                                     
036826                                                                          
036842         WHEN ASC-MSTR-BOL-NBR  > SPACES  AND                             
036843              ASC-CSTM-ENTRY-ID = SPACES  AND                             
036844              ASC-CONTNR-ID     = SPACES  AND                             
036845              ASC-HSE-BOL-NBR   = SPACES  AND                             
036847             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036848                                          OR LOW-VALUES)                  
036849               CONTINUE                                                   
036850                                                                          
036851         WHEN ASC-HSE-BOL-NBR   > SPACES  AND                             
036852              ASC-CSTM-ENTRY-ID = SPACES  AND                             
036853              ASC-CONTNR-ID     = SPACES  AND                             
036854              ASC-MSTR-BOL-NBR  = SPACES  AND                             
036855             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036856                                          OR LOW-VALUES)                  
036857               CONTINUE                                                   
036858                                                                          
036859         WHEN ASC-MSTR-BOL-NBR  > SPACES  AND                             
036860              ASC-HSE-BOL-NBR   > SPACES  AND                             
036861              ASC-CSTM-ENTRY-ID = SPACES  AND                             
036862              ASC-CONTNR-ID     = SPACES  AND                             
036863             (ASC-PO-NBR        = SPACES  OR ZEROES                       
036864                                          OR LOW-VALUES)                  
036865               CONTINUE                                                   
036866                                                                          
036868         WHEN OTHER                                                       
036869*--- TSYMSG-01883-ONLY ONE VALUE SHOULD BE ENTERED                        
036870               SET PS-ERROR                                               
036871                   DP020-MSG-FATAL         TO TRUE                        
036872               MOVE PC-TSYMSG-01883        TO DP020-MSG-NUMBER            
036873               MOVE -1                     TO AENTRIDL                    
036874               SET DP030-SET-CURSOR-APPL-1 TO TRUE                        
036875     END-EVALUATE.                                                        
036876                                                                          
036877     IF PS-NO-ERROR                                                       
036878         MOVE -1                     TO AENTRIDL                          
036879         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
036880     END-IF.                                                              
036881                                                                          
039100 EJECT                                                                    
039200*----------------------------------------------------------------*        
039300*  MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE  *        
039400*  MAP.                                                          *        
039500*----------------------------------------------------------------*        
039600 3200-RESET-ATTRIBUTES.                                                   
039700                                                                          
039800     MOVE DP015-UNP-ALP-NOR-OFF    TO APONBRA                             
039810                                      AENTRIDA                            
039820                                      ACNTRIDA                            
039830                                      AHBOLA                              
039840                                      AMBOLA.                             
039900     MOVE DP015-GREEN              TO APONBRC                             
039910                                      AENTRIDC                            
039920                                      ACNTRIDC                            
039930                                      AHBOLC                              
039940                                      AMBOLC.                             
040000     MOVE DP015-UNDERLINE          TO APONBRH                             
040010                                      AENTRIDH                            
040020                                      ACNTRIDH                            
040030                                      AHBOLH                              
040040                                      AMBOLH.                             
040200 EJECT                                                                    
040300*----------------------------------------------------------------*        
040400*  SELECT VENDOR NAME FROM TPURCHS,TENTNME                       *        
040500*----------------------------------------------------------------*        
040600 3300-SELECT-VENDOR-NAME.                                                 
040700                                                                          
040800     PERFORM                                                              
040900         WITH TEST AFTER                                                  
041000         VARYING PV-RETRY-COUNTER                                         
041100         FROM 1 BY 1                                                      
041200         UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
041300             OR (SQLCODE = ZERO)                                          
041400             OR (SQLCODE = +100))                                         
041500                                                                          
041600         EXEC SQL                                                         
041700              SELECT DISTINCT                                             
041800                     A.ENT_NAME_DESC                                      
041900              INTO   :ENTNME-ENT-NAME-DESC                                
042000              FROM   TENTNME  A,                                          
042100                     TPURCHS  B                                           
042200              WHERE  B.PO_NBR       = :DK-PO-NBR                          
042300              AND    A.ENT_ID       = B.ENT_ID                            
042400              AND    VER_STATUS_CDE = 'A'                                 
042500              AND    A.TYPE_CDE     = '01'                                
042600         END-EXEC                                                         
042700                                                                          
042800         EVALUATE TRUE                                                    
042900             WHEN SQLCODE = -904                                          
043000             WHEN SQLCODE = -913                                          
043100                 CONTINUE                                                 
043200             WHEN SQLCODE = ZERO                                          
043300                  MOVE ENTNME-ENT-NAME-DESC TO ASC-VND-NME                
043400             WHEN SQLCODE = +100                                          
043500                  MOVE SPACES               TO ASC-VND-NME                
043600             WHEN SQLWARN0 NOT = SPACES                                   
043700             WHEN SQLCODE < ZERO                                          
043800             WHEN SQLCODE > ZERO                                          
043900                 MOVE '3300-SELECT-VENDOR-NAME'                           
044000                                   TO DP013-PARAGRAPH                     
044100                 MOVE 'SQL ERROR SELECTING VENDOR NAME'                   
044200                                   TO DP013-MESSAGE-TEXT (1)              
044300                 MOVE SQLCA        TO DP013-SQLCA                         
044400                 MOVE 'TENTNME '   TO DP013-DB2-TABLE-NAME (1)            
044500                 SET DP013-DB2-ABEND                                      
044600                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
044700                 PERFORM DP013-0000-PROCESS-ABEND                         
044800         END-EVALUATE                                                     
044900     END-PERFORM.                                                         
045000*                                                                         
045100     IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
045200         MOVE '3300-SELECT-VENDOR-NAME'                                   
045300                                   TO DP013-PARAGRAPH                     
045400         MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT VENDO        
045500-             'R NAME'             TO DP013-MESSAGE-TEXT (1)              
045600         MOVE SQLCA                TO DP013-SQLCA                         
045700         MOVE 'TPURCHS '           TO DP013-DB2-TABLE-NAME (1)            
045800         SET DP013-DB2-ABEND                                              
045900             DP013-XCTL-DISPLAY-RESTART                                   
046000                                   TO TRUE                                
046100         PERFORM DP013-0000-PROCESS-ABEND                                 
046200     END-IF.                                                              
046300                                                                          
046400 EJECT                                                                    
046500*----------------------------------------------------------------*        
046600*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
046700*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
046800*  IT HERE.                                                      *        
046900*----------------------------------------------------------------*        
047000 4000-BUILD-INITIAL-PANEL.                                                
047100                                                                          
047200     MOVE ZEROS                    TO APONBRO.                            
047300     MOVE SPACES                   TO AVNDNMEO                            
047310                                      AENTRIDO                            
047320                                      ACNTRIDO                            
047330                                      AHBOLO                              
047340                                      AMBOLO.                             
047400     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
047500     MOVE -1                       TO AENTRIDL.                           
047600                                                                          
047700 EJECT                                                                    
047800*----------------------------------------------------------------*        
047900*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
048000*----------------------------------------------------------------*        
048100 4400-MOVE-COMMAREA-TO-SCREEN.                                            
048200                                                                          
048300     MOVE ASC-PO-NBR-N             TO APONBRO.                            
048500     MOVE ASC-VND-NME              TO AVNDNMEO.                           
048510     MOVE ASC-CSTM-ENTRY-ID        TO AENTRIDO.                           
048530     MOVE ASC-CONTNR-ID            TO ACNTRIDO.                           
048550     MOVE ASC-HSE-BOL-NBR          TO AHBOLO.                             
048570     MOVE ASC-MSTR-BOL-NBR         TO AMBOLO.                             
048600     MOVE -1                       TO AENTRIDL.                           
048700     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
048800                                                                          
048900 EJECT                                                                    
049000*----------------------------------------------------------------*        
049100*    ABEND PROCESSOR MODULE                                      *        
049200*----------------------------------------------------------------*        
049300                                                                          
049400     COPY DPPD013.                                                        
