       IDENTIFICATION DIVISION.                                         00080001
       PROGRAM-ID.    PXKUT104.                                         00090001
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00100001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00110001
       DATE-WRITTEN.  07/26/2010.                                       00120001
       DATE-COMPILED.                                                   00130001
      ******************************************************************00140001
      * THIS PROGRAM WILL RETRIEVE FASHION SALES ON-HANDS FOR           00150001
      * CONSOLIDATION SKUS GIVEN IN THE INPUT FILE. USED FOR REPORTING. 00160001
      ******************************************************************00240001
      * DATE: 07/26/2010 | MADE BY: THOMAS HANSON | PROJECT: WR37816    00250001
      * DESCRIPTION OF CHANGES: INITIAL PROGRAM IMPLEMENTATION.         00260001
      ******************************************************************00270001
DW0316* DATE: 03/16/2011 | MADE BY: DAVID WELLER | PROJECT: WR39649     00271017
DW0316* DESCRIPTION OF CHANGES: ELIMINATE HARDCODING OF 873 LOCATION    00272017
DW0316******************************************************************00273017
       ENVIRONMENT DIVISION.                                            00280001
       CONFIGURATION SECTION.                                           00290001
       SOURCE-COMPUTER.    IBM-3090.                                    00300001
       OBJECT-COMPUTER.    IBM-3090.                                    00310001
       INPUT-OUTPUT SECTION.                                            00320001
       FILE-CONTROL.                                                    00330001
                                                                        00340001
           SELECT CONSOLIDATION-SKUS-FILE                               00350001
                  ASSIGN TO INP01.                                      00360001
           SELECT RUN-TYPE-CARD                                         00370002
                  ASSIGN TO INP02.                                      00380001
           SELECT OSA-OUTPUT-FILE                                       00390001
                  ASSIGN TO OUT01.                                      00400001
           SELECT STR-OUTPUT-FILE                                       00410001
                  ASSIGN TO OUT02.                                      00420001
                                                                        00430001
       DATA DIVISION.                                                   00440001
       FILE SECTION.                                                    00450001
                                                                        00460001
       FD  CONSOLIDATION-SKUS-FILE                                      00470001
           RECORDING MODE IS F                                          00480001
           LABEL RECORDS OMITTED                                        00490001
           BLOCK CONTAINS 0 RECORDS                                     00500001
           DATA RECORD IS CONSOLIDATION-SKUS-RECORD.                    00510001
       01  CONSOLIDATION-SKUS-RECORD           PIC X(365).              00520001
                                                                        00530001
       FD  RUN-TYPE-CARD                                                00540002
           RECORDING MODE IS F                                          00550001
           LABEL RECORDS OMITTED                                        00560001
           BLOCK CONTAINS 0 RECORDS                                     00570001
           DATA RECORD IS RUN-TYPE-RECORD.                              00580002
       01  RUN-TYPE-RECORD                     PIC X(080).              00590002
                                                                        00600001
       FD  OSA-OUTPUT-FILE                                              00610001
           RECORDING MODE IS F                                          00620001
           LABEL RECORDS OMITTED                                        00630001
           BLOCK CONTAINS 0 RECORDS                                     00640001
           DATA RECORD IS OSA-OUTPUT-RECORD.                            00650001
       01  OSA-OUTPUT-RECORD                   PIC X(059).              00660016
                                                                        00670001
       FD  STR-OUTPUT-FILE                                              00680001
           RECORDING MODE IS F                                          00690001
           LABEL RECORDS OMITTED                                        00700001
           BLOCK CONTAINS 0 RECORDS                                     00710001
           DATA RECORD IS STR-OUTPUT-RECORD.                            00720001
       01  STR-OUTPUT-RECORD                   PIC X(171).              00730013
                                                                        00740001
       WORKING-STORAGE SECTION.                                         00750001
      ******************************************************************00760001
      **PROGRAM SWITCHES                                                00770001
      ******************************************************************00780001
       01  PS-SWITCH-AREAS.                                             00790001
           05  PS-END-OF-FILE-SW               PIC X(01)  VALUE 'N'.    00800001
               88  PS-END-OF-FILE                         VALUE 'Y'.    00810001
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    00820001
           05  PS-END-OF-CSR-SW                PIC X(01)  VALUE 'N'.    00821002
               88  PS-END-OF-CSR                          VALUE 'Y'.    00822002
               88  PS-NOT-END-OF-CSR                      VALUE 'N'.    00823002
      ******************************************************************00830001
      **SYSTEM DATE AND TIME VARIABLES                                  00840001
      ******************************************************************00850001
       01  SYS-DATE-TIME.                                               00860001
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00870014
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00880014
                                                                        00890001
       01  ST-BEG-TIME.                                                 00900001
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00910014
           05  FILLER                          PIC X(01) VALUE ':'.     00920014
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00930014
                                                                        00940001
       01  ST-END-TIME.                                                 00950001
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00960014
           05  FILLER                          PIC X(01) VALUE ':'.     00970014
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00980014
                                                                        00990001
       01  SD-EDIT-DATE.                                                01000001
           05  SD-MONTH                        PIC 9(02) VALUE ZEROS.   01010014
           05  FILLER                          PIC X(01) VALUE '-'.     01020014
           05  SD-DAY                          PIC 9(02) VALUE ZEROS.   01030014
           05  FILLER                          PIC X(01) VALUE '-'.     01040014
           05  SD-CENT                         PIC 9(02) VALUE ZEROS.   01050014
           05  SD-YEAR                         PIC 9(02) VALUE ZEROS.   01060014
      ******************************************************************01080002
      *RUN TYPE CARD LAYOUT (OSA VS STR REPORT OUTPUT)                 *01090002
      ******************************************************************01100002
       01 CC-RUN-TYPE-CARD-LAYOUT.                                      01110002
          05 CC-RUN-TYPE                       PIC X(03) VALUE SPACES.  01120014
          05  FILLER                           PIC X(77) VALUE SPACE.   01130014
      ******************************************************************01150001
      **PROGRAM ACCUMULATORS (COUNTERS)                                 01160001
      ******************************************************************01170001
       01  PA-PROGRAM-ACCUMULATORS.                                     01180001
           05  PA-READ                         PIC 9(11) VALUE ZEROS.   01190001
           05  PA-WRITE-OSA                    PIC 9(11) VALUE ZEROS.   01200001
           05  PA-WRITE-STR                    PIC 9(11) VALUE ZEROS.   01210001
      ******************************************************************01220002
      **PROGRAM VARS                                                    01230002
      ******************************************************************01240002
       01  PV-PROGRAM-VARIABLES.                                        01250002
           05  PV-SKU-NBR-DB2                  PIC  S9(8)V COMP-3.      01260002
      ******************************************************************01480001
      **PROGRAM CONSTANTS                                               01490001
      ******************************************************************01500001
       01  PC-PROGRAM-CONSTANTS.                                        01510001
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'PXKUT104'.01520001
           05  PC-OSA-RPT-LOOKUP               PIC X(03) VALUE 'OSA'.   01521014
           05  PC-STR-RPT-LOOKUP               PIC X(03) VALUE 'STR'.   01522014
      ******************************************************************01530001
      **ABEND CODE INFORMATION                                          01540001
      ******************************************************************01550001
       01  AC-ABEND-FIELDS.                                             01560001
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    01570001
                                                           COMP SYNC.   01580001
               88  AC-EMPTY-FILE-ERROR                   VALUE +4005.   01590001
               88  AC-DB2-SQL-ERROR                      VALUE +4013.   01591002
       01  AC-MISC-ABEND-FIELDS.                                        01610001
           05  AC-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   01620001
           05  AC-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   01630001
           05  AC-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   01640001
           05  AC-DB2-OPERATION                PIC X(40) VALUE SPACE.   01641003
      ******************************************************************01650001
      **COPYBOOKS                                                       01660001
      ******************************************************************01670001
      *INP01                                                            01680001
           COPY PXRD102.                                                01690002
      *OUT01-OSA FILE                                                   01700001
           COPY PXRD103.                                                01710002
      *OUT02-STR FILE                                                   01720001
           COPY PXRD104.                                                01730002
                                                                        01730102
      *----------------------------------------------------------------*01731002
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01732002
      *----------------------------------------------------------------*01733002
           COPY DPWS004.                                                01734002
                                                                        01735002
      *----------------------------------------------------------------*01739202
      *  DB2 MESSAGE AREA                                               01739302
      *----------------------------------------------------------------*01739402
           COPY SQLCA2.                                                 01739502
                                                                        01739602
      *----------------------------------------------------------------*01739702
      *  DB2 COMMUNICATIONS AREA                                        01739802
      *----------------------------------------------------------------*01739902
           EXEC SQL                                                     01740002
             INCLUDE SQLCA                                              01741002
           END-EXEC.                                                    01742002
      *----------------------------------------------------------------*01744002
      *  DB2 TABLE TSKST                                                01745002
      *----------------------------------------------------------------*01746002
           EXEC SQL                                                     01747002
               INCLUDE TSKST                                            01748002
           END-EXEC.                                                    01749002
      *----------------------------------------------------------------*01749102
      *  DB2 TABLE TSKXREF                                              01749202
      *----------------------------------------------------------------*01749302
           EXEC SQL                                                     01749402
               INCLUDE TSKXREF                                          01749502
           END-EXEC.                                                    01749602
DW0316*----------------------------------------------------------------*01749717
DW0316*  DB2 TABLE XST_LOC                                              01749817
DW0316*----------------------------------------------------------------*01749917
DW0316     EXEC SQL                                                     01750017
DW0316         INCLUDE XST00001                                         01750117
DW0316     END-EXEC.                                                    01750217
                                                                        01750317
      *----------------------------------------------------------------*01750417
      *  STORE/SKU ON-HAND-QTY CURSOR                                   01750517
      *----------------------------------------------------------------*01750617
           EXEC SQL                                                     01750717
             DECLARE STR-SKU-ONHND-CSR CURSOR FOR                       01750817
               SELECT A.LOC_NBR                                         01750917
                     ,A.ONHAND_QTY                                      01751017
               FROM   TSKST     A                                       01751317
                     ,TSKXREF   B                                       01751417
DW0316               ,XST_LOC   C                                       01751517
               WHERE  B.SKU = :PV-SKU-NBR-DB2                           01751617
                 AND  A.ITEM_NMBR = B.ITM_NBR                           01751717
                 AND  A.LOC_NBR > 0                                     01751817
DW0316           AND  C.LOC_NBR  = A.LOC_NBR                            01752017
DW0316           AND  C.LOC_TYP_CDE = 'DS'                              01752121
DW0316           AND  C.E_BUS_IND <> 'Y'                                01752221
               ORDER BY A.LOC_NBR                                       01752317
               FOR FETCH ONLY                                           01752417
               WITH UR                                                  01752517
           END-EXEC.                                                    01752617
                                                                        01753002
      ******************************************************************01760002
       PROCEDURE DIVISION.                                              01790001
      ******************************************************************01800001
       0000-MAINLINE.                                                   01810001
                                                                        01820001
           PERFORM 1000-INITIALIZATION.                                 01830001
                                                                        01840001
           PERFORM 2000-PROCESS-RECORDS                                 01850001
                   UNTIL PS-END-OF-FILE.                                01860001
                                                                        01870001
           PERFORM 9100-WRAP-UP.                                        01880015
           GOBACK.                                                      01890001
                                                                        01900001
      ******************************************************************01910001
      **INITIALIZATION ROUTINE                                          01920001
      ******************************************************************01930001
       1000-INITIALIZATION.                                             01940001
                                                                        01950001
           OPEN OUTPUT OSA-OUTPUT-FILE.                                 01960002
           OPEN OUTPUT STR-OUTPUT-FILE.                                 01970002
           OPEN INPUT CONSOLIDATION-SKUS-FILE.                          01980001
           OPEN INPUT RUN-TYPE-CARD.                                    01990002
                                                                        02000001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 02010001
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            02020001
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            02030001
           MOVE SYS-DATE (1:2) TO SD-CENT.                              02040001
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              02050001
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             02060001
           MOVE SYS-DATE (7:2) TO SD-DAY.                               02070001
                                                                        02080001
           PERFORM 5000-READ-FROM-INPUT-FILE.                           02090001
           IF PS-NOT-END-OF-FILE                                        02100001
              PERFORM 1100-READ-RUN-TYPE-CARD                           02110002
           ELSE                                                         02140001
              SET AC-EMPTY-FILE-ERROR        TO TRUE                    02150001
              MOVE '1000-INITIALIZATION'     TO AC-PARAGRAPH-NAME       02160001
              MOVE 'INP01 FILE IS EMPTY'     TO AC-OPERATION-MSG-1      02170001
              PERFORM 9000-NON-SQL-ABEND                                02180001
           END-IF.                                                      02190001
                                                                        02200001
      ***************************************************************** 02210001
      **READ IN THE RUN TYPE, EITHER THE OSA REPORT OR THE STR REPORT   02220014
      ***************************************************************** 02230001
       1100-READ-RUN-TYPE-CARD.                                         02240002
                                                                        02250001
           READ RUN-TYPE-CARD INTO CC-RUN-TYPE-CARD-LAYOUT.             02260002
           IF CC-RUN-TYPE EQUAL SPACES                                  02270002
              SET AC-EMPTY-FILE-ERROR           TO TRUE                 02280001
              MOVE '1100-READ-RUN-TYPE-CARD'    TO AC-PARAGRAPH-NAME    02290002
              MOVE 'INP02 FILE IS EMPTY'        TO AC-OPERATION-MSG-1   02300001
              PERFORM 9000-NON-SQL-ABEND                                02310001
           ELSE                                                         02320001
              DISPLAY ' '                                               02330001
              DISPLAY 'RUN TYPE TO PROCESS: ' CC-RUN-TYPE               02340002
           END-IF.                                                      02350001
                                                                        03250001
      ******************************************************************03260001
      **PROCESS EACH RECORD...                                          03270014
      ******************************************************************03280001
       2000-PROCESS-RECORDS.                                            03290001
                                                                        03290102
      *-> INITIALIZE CURSOR                                             03291002
           SET PS-NOT-END-OF-CSR TO TRUE.                               03292002
                                                                        03300001
      *-> OPEN CURSOR FOR GIVEN SKU                                     03310002
           PERFORM 6300-OPEN-STR-SKU-ONHND-CSR.                         03320002
      *-> FIRST ATTEMPT AT RETRIEVAL                                    03321005
           PERFORM 6400-FETCH-STR-SKU-ONHND-CSR                         03330005
                                                                        03340002
      *-> FETCH UNTIL END OF CURSOR                                     03350002
           PERFORM UNTIL PS-END-OF-CSR                                  03351002
      *-> WRITE OUT THE STORE/SKU/ON-HAND-QTY RECORD (OSA VS STR LAYOUT)03352002
              EVALUATE CC-RUN-TYPE                                      03353002
               WHEN 'OSA'                                               03353102
                 PERFORM 3000-MOVE-OSA-FIELDS                           03353202
                 PERFORM 4000-WRITE-TO-OUTPUT-OSA-FILE                  03353302
               WHEN 'STR'                                               03353402
                 PERFORM 3100-MOVE-STR-FIELDS                           03353502
                 PERFORM 4100-WRITE-TO-OUTPUT-STR-FILE                  03353602
              END-EVALUATE                                              03353702
             PERFORM 6400-FETCH-STR-SKU-ONHND-CSR                       03359019
           END-PERFORM.                                                 03359102
                                                                        03362002
      *-> CLOSE CURSOR FOR THIS SKU                                     03363002
           PERFORM 6500-CLOSE-STR-SKU-ONHND-CSR.                        03364002
                                                                        03365002
      *-> READ A NEW INPUT RECORD                                       03370002
           PERFORM 5000-READ-FROM-INPUT-FILE.                           03380002
                                                                        03480001
      ******************************************************************03481002
      **MOVE OSA FIELDS                                                 03482002
      ******************************************************************03483002
       3000-MOVE-OSA-FIELDS.                                            03484002
                                                                        03485002
           MOVE PXRD102-SKU-NBR         TO PXRD103-SKU-NBR.             03488102
           MOVE PXRD102-BUS-GRP-NBR     TO PXRD103-BUS-GRP-NBR.         03488202
           MOVE PXRD102-BUS-GRP-DESC    TO PXRD103-BUS-GRP-DESC.        03488302
           MOVE TSKST-LOC-NBR           TO PXRD103-STORE-NBR.           03488402
           MOVE TSKST-ONHAND-QTY        TO PXRD103-FS-ON-HANDS-QTY.     03488702
           MOVE PXRD102-CONSOLIDATE-EFF-DTE                             03488802
                TO PXRD103-CONSOLIDATE-EFF-DTE.                         03488902
                                                                        03489402
      ******************************************************************03489502
      **MOVE STR FIELDS                                                 03489602
      ******************************************************************03489702
       3100-MOVE-STR-FIELDS.                                            03489802
                                                                        03489909
           MOVE PXRD102-SKU-NBR         TO PXRD104-SKU-NBR.             03490602
           MOVE TSKST-LOC-NBR           TO PXRD104-STORE-NBR.           03490702
           MOVE TSKST-ONHAND-QTY        TO PXRD104-FS-ON-HANDS-QTY.     03490802
           MOVE PXRD102-FLOOR-PAD-CDE   TO PXRD104-FLOOR-PAD-CDE.       03490902
           MOVE PXRD102-FLOOR-PAD-DESC  TO PXRD104-FLOOR-PAD-DESC.      03491002
           MOVE PXRD102-DEPT-NBR        TO PXRD104-DEPT-NBR.            03491102
           MOVE PXRD102-DEPT-DESC       TO PXRD104-DEPT-DESC.           03491202
           MOVE PXRD102-SUB-CL-NBR      TO PXRD104-SUB-CL-NBR.          03491302
           MOVE PXRD102-SUB-CL-DESC     TO PXRD104-SUB-CL-DESC.         03491402
           MOVE PXRD102-LABEL-NAME      TO PXRD104-LABEL-NAME.          03491502
           MOVE PXRD102-VS-STYLE-NBR    TO PXRD104-VS-STYLE-NBR.        03491602
           MOVE PXRD102-VS-STYLE-DESC   TO PXRD104-VS-STYLE-DESC.       03491702
           MOVE PXRD102-CONSOLIDATE-EFF-DTE                             03491802
                TO PXRD104-CONSOLIDATE-EFF-DTE.                         03491902
                                                                        03492009
      ******************************************************************03495002
      **WRITE OUT TO THE OSA SUMMARY FILE.                              03500014
      ******************************************************************03510001
       4000-WRITE-TO-OUTPUT-OSA-FILE.                                   03520001
                                                                        03530001
           WRITE OSA-OUTPUT-RECORD                                      03540001
                 FROM PXRD103-OSA-CNSLDTN-RPT-DATA.                     03550002
           ADD +1 TO PA-WRITE-OSA.                                      03560001
                                                                        03570001
      ******************************************************************03580001
      **WRITE OUT TO THE STORE DETAILS FILE.                            03590014
      ******************************************************************03600001
       4100-WRITE-TO-OUTPUT-STR-FILE.                                   03610001
                                                                        03620001
           WRITE STR-OUTPUT-RECORD                                      03630001
                 FROM PXRD104-STR-CNSLDTN-RPT-DATA.                     03640003
           ADD +1 TO PA-WRITE-STR.                                      03650001
                                                                        03660001
      ******************************************************************03670001
      **READ IN FROM THE INPUT FILE.                                    03680014
      ******************************************************************03690001
       5000-READ-FROM-INPUT-FILE.                                       03700001
                                                                        03710001
           INITIALIZE PXRD102-CNSLDTN-SKUS.                             03720002
                                                                        03740001
           READ CONSOLIDATION-SKUS-FILE                                 03750001
              INTO PXRD102-CNSLDTN-SKUS                                 03760002
                 AT END                                                 03770001
                 SET PS-END-OF-FILE TO TRUE                             03780001
           END-READ.                                                    03790001
                                                                        03800001
           IF  PS-NOT-END-OF-FILE                                       03810001
               ADD +1                       TO PA-READ                  03820001
               MOVE PXRD102-SKU-NBR         TO PV-SKU-NBR-DB2           03821002
           END-IF.                                                      03830001
                                                                        03840001
      ******************************************************************03850014
      **OPEN THE STORE SKU ONHAND CURSOR                                03870014
      ******************************************************************03880014
       6300-OPEN-STR-SKU-ONHND-CSR.                                     03890002
                                                                        03900002
           EXEC SQL                                                     03910002
             OPEN STR-SKU-ONHND-CSR                                     03920002
           END-EXEC.                                                    03930002
                                                                        03940002
           IF SQLCODE NOT EQUAL ZERO                                    03950002
             MOVE 'OPENING STR SKU CSR   '      TO AC-DB2-OPERATION     03960002
             MOVE '                      '      TO AC-PARAGRAPH-NAME    03970002
             PERFORM 9050-SQL-ABEND-ROUTINE                             03980015
           END-IF.                                                      03990002
                                                                        04000002
      ******************************************************************04022014
      **FETCH THE STORE / SKU ONHAND CURSOR ROW.                        04023014
      ******************************************************************04025014
       6400-FETCH-STR-SKU-ONHND-CSR.                                    04026002
                                                                        04026102
           INITIALIZE DCLTSKST.                                         04026202
                                                                        04027019
           EXEC SQL                                                     04029702
             FETCH STR-SKU-ONHND-CSR                                    04029802
               INTO :TSKST-LOC-NBR                                      04029902
                   ,:TSKST-ONHAND-QTY                                   04030003
           END-EXEC.                                                    04030302
                                                                        04030402
           EVALUATE TRUE                                                04030502
             WHEN SQLCODE = +0                                          04030602
               CONTINUE                                                 04030702
             WHEN SQLCODE = +100                                        04030802
               SET PS-END-OF-CSR             TO TRUE                    04030902
             WHEN OTHER                                                 04031002
               SET PS-END-OF-CSR             TO TRUE                    04031102
               MOVE 'FETCHING STR GP CSR'    TO AC-DB2-OPERATION        04031202
               MOVE '6400-FETCH-STR-LOC-CSR' TO AC-PARAGRAPH-NAME       04031302
               PERFORM 9050-SQL-ABEND-ROUTINE                           04031415
           END-EVALUATE.                                                04031502
                                                                        04032502
      ******************************************************************04032614
      **CLOSE THE STORE/SKU ONHAND CURSOR                               04032714
      ******************************************************************04032914
       6500-CLOSE-STR-SKU-ONHND-CSR.                                    04033002
                                                                        04033102
           EXEC SQL                                                     04033202
             CLOSE STR-SKU-ONHND-CSR                                    04033302
           END-EXEC.                                                    04033402
                                                                        04033502
           IF SQLCODE NOT EQUAL ZERO                                    04033602
             MOVE 'CLOSING STR LOC CSR'         TO AC-DB2-OPERATION     04033702
             MOVE '6500-CLOSE-STR-LOC-CSR'      TO AC-PARAGRAPH-NAME    04033802
             PERFORM 9050-SQL-ABEND-ROUTINE                             04033915
           END-IF.                                                      04034002
                                                                        04035001
      ******************************************************************04040001
      **PERFORMS AN ABEND WHEN A NON-SQL BASED ERROR TAKES PLACE DURING 04060014
      **PROGRAM EXECUTION. LIKELY A CONTROL CARD OR DATE ROUTINE ISSUE. 04070014
      ******************************************************************04080001
       9000-NON-SQL-ABEND.                                              04090001
                                                                        04100001
           PERFORM 9100-WRAP-UP.                                        04110015
                                                                        04120001
           DISPLAY '** ABEND           **'.                             04130001
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          04140001
           DISPLAY '** PARAGRAPH       ** = ' AC-PARAGRAPH-NAME.        04150001
           DISPLAY '** OPERATION       ** = ' AC-OPERATION-MSG-1.       04160001
           DISPLAY '**                 ** = ' AC-OPERATION-MSG-2.       04170001
                                                                        04180001
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         04190001
                                                                        04190102
      ******************************************************************04191014
      **TRIGGERED ON A DB2 ABEND OPERATION.                             04193014
      ******************************************************************04194014
       9050-SQL-ABEND-ROUTINE.                                          04195015
                                                                        04196002
           PERFORM 9100-WRAP-UP.                                        04196115
                                                                        04196202
           DISPLAY ' '.                                                 04197002
           DISPLAY '** ABEND     **'.                                   04198002
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                04199002
           DISPLAY '** PARAGRAPH ** = ' AC-PARAGRAPH-NAME.              04199102
           DISPLAY '** OPERATION ** = ' AC-DB2-OPERATION.               04199202
                                                                        04199302
           COPY DPPD004.                                                04199402
           SET AC-DB2-SQL-ERROR TO TRUE.                                04199502
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         04199602
                                                                        04200001
      ******************************************************************04210001
      **WRAP UP PROCESSING                                              04220001
      ******************************************************************04230001
       9100-WRAP-UP.                                                    04240015
                                                                        04250001
           CLOSE OSA-OUTPUT-FILE.                                       04260002
           CLOSE STR-OUTPUT-FILE.                                       04270002
           CLOSE CONSOLIDATION-SKUS-FILE.                               04280001
           CLOSE RUN-TYPE-CARD.                                         04290002
                                                                        04300001
           PERFORM 9999-DISPLAY-STATISTICS.                             04310001
                                                                        04320001
      ******************************************************************04330001
      **DISPLAY PROGRAM STATS                                           04340001
      ******************************************************************04350001
       9999-DISPLAY-STATISTICS.                                         04360001
                                                                        04370001
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 04380001
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            04390001
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            04400001
                                                                        04410001
           DISPLAY '*************************************************'. 04420001
           DISPLAY '* PROGRAM   = ' PC-PROGRAM-NAME.                    04430001
           DISPLAY '* PURPOSE   = LOOKUP ON-HANDS FROM FASHION SALES'.  04440002
           DISPLAY '* RUN DATE  = ' SD-EDIT-DATE.                       04450001
           DISPLAY '* BEG TIME  = ' ST-BEG-TIME.                        04460001
           DISPLAY '* END TIME  = ' ST-END-TIME.                        04470001
           DISPLAY '*************************************************'. 04480001
           DISPLAY '* INPUT RECORDS READ: ' PA-READ.                    04490001
           DISPLAY '*************************************************'. 04500001
           DISPLAY '* OSA RECORD WRITES: ' PA-WRITE-OSA.                04520001
           DISPLAY '* STR RECORD WRITES: ' PA-WRITE-STR.                04550001
           DISPLAY '*************************************************'. 04560001
                                                                        04570001
      ******************************************************************04580001
