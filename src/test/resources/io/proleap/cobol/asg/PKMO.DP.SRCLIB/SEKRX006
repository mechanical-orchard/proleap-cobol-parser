/* REXX */                                                                      
/* SEKRX006 -                                                         */        
/*                                                                    */        
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = 'SEKRX006 - '                                                         
ORPHAN_FILE_END_OF_FILE = 'Y'                                                   
ORPHAN_FILE_NOT_END_OF_FILE = 'N'                                               
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
ORPHAN_FILE_EOF = ORPHAN_FILE_NOT_END_OF_FILE                                   
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM                                                                     
"EXECIO * DISKR INP01  (STEM USEREXCP. FINIS)"                                  
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP01 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
DO I=1 TO USEREXCP.0                                                            
  USERID_EXCEPTION_LIST = USERID_EXCEPTION_LIST LEFT(USEREXCP.I,8)              
END                                                                             
CALL READ_ORPHAN_RECORD                                                         
DO WHILE (ORPHAN_FILE_EOF = ORPHAN_FILE_NOT_END_OF_FILE)                        
    PARSE PULL INP02_RECORD                                                     
    ORPHAN_USERID = STRIP(SUBSTR(INP02_RECORD,1,8))                             
    UPPER ORPHAN_USERID                                                         
    SELECT                                                                      
        WHEN (WORDPOS(ORPHAN_USERID,USERID_EXCEPTION_LIST) ^= 0),               
                                                              THEN DO           
            SAY CURRPGM || 'USERID EXCEPTION ' || ORPHAN_USERID ||,             
                      ' FOUND'                                                  
        END                                                                     
        WHEN (DATATYPE(ORPHAN_USERID,'W') = 1) THEN DO                          
            STORE_DELUSER_RECORD = ' DU ' || ORPHAN_USERID                      
            CALL WRITE_STORE_DELUSER_RECORD                                     
        END                                                                     
        OTHERWISE DO                                                            
            CORP_DC_ORPHAN_RECORD = INP02_RECORD                                
            CALL WRITE_CORP_DC_ORPHAN_RECORD                                    
        END                                                                     
    END                                                                         
    CALL READ_ORPHAN_RECORD                                                     
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
"EXECIO 0 DISKR OUT02 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_ORPHAN_RECORD                                                */         
/*                                                                   */         
/* Read a RACF orphaned account record.                              */         
/* ----------------------------------------------------------------- */         
READ_ORPHAN_RECORD:                                                             
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN                                                              
        ORPHAN_FILE_EOF = ORPHAN_FILE_END_OF_FILE                               
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_STORE_DELUSER_RECORD                                        */         
/*                                                                   */         
/* Write a delete user command record for an orphaned store user     */         
/* RACF account.                                                     */         
/* ----------------------------------------------------------------- */         
WRITE_STORE_DELUSER_RECORD:                                                     
    OUT01_RECORD = LEFT(STORE_DELUSER_RECORD,80)                                
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_CORP_DC_ORPHAN_RECORD                                       */         
/*                                                                   */         
/* Write a delete user command record for an orphaned store user     */         
/* RACF account.                                                     */         
/* ----------------------------------------------------------------- */         
WRITE_CORP_DC_ORPHAN_RECORD:                                                    
    OUT02_RECORD = LEFT(CORP_DC_ORPHAN_RECORD,11)                               
    PUSH OUT02_RECORD                                                           
    "EXECIO 1 DISKW OUT02"                                                      
    RETURN                                                                      
