000100******************************************************************        
000200*  COPY MEMBER  DPPD031.                                         *        
000300*                                                                *        
000400*  THIS COPY MEMBER CONTAINS THE PROCEDURE DIVISION STATEMENTS   *        
000500*  THAT ARE REQUIRED TO INVOKE THE PROTEGRITY MODULE (PTYPCSL &  *        
000600*  PTYPSLL) FOR BATCH PROGRAMS.                                  *        
000700*                                       WR/IR#: CHG0104429       *        
000800******************************************************************        
000900*                                                                *        
001000*  LISTED BELOW ARE NOTES ON WHAT NEEDS TO BE DEFINED IN THE     *        
001100*  DATA AND PROCEDURE DIVISIONS TO USE THIS COPY MEMBER:         *        
001200*                                                                *        
001300*  ENSURE THAT THE ASSOCIATED WORKING-STORAGE COPY MEMBER        *        
001400*  (DPWS031) IS COPIED INTO THE WORKING-STORAGE SECTION OF YOUR  *        
001500*  PROGRAM.                                                      *        
001600*                                                                *        
001700*  CODE THE FOLLOWING STATEMENTS IN YOUR PROGRAM WHENEVER YOU    *        
001800*  ARE INVOKING THE PROTEGRITY MODULE:                           *        
001900*                                                                *        
002000*    1. INTIALIZE THE PROTEGRITY CSI VARIABLES AND DPWS031       *        
002100*       LAYOUT VARIABLES.                                       *         
002200*    2. MOVE 'PPPPPPPP' TO DP031-PGM-NAME.                       *        
002300*       (INSERT YOUR PROGRAM NAME HERE IN PLACE OF PPPPPPPP)     *        
002400*    3. MOVE '********' TO  DP031-FIELD.                         *        
002500*       (INSERT THE FIELD NAME HERE IN PLACE OF ********)        *        
002600*        EG: MOVE 'TNDR_ID' TO  DP031-FIELD.                     *        
002700*    4. SET AAAAA       TO TRUE.                                 *        
002800*       (INSERT CSI FUNCTION HERE IN PLACE OF AAAAA)             *        
002900*        EG: SET CSI-ENCRYPT-FUNCTION TO TRUE.                   *        
003000*    5. SET YYY         TO TRUE.                                 *        
003100*       (INSERT DATA ELEMENT VARIABLE HERE IN PLACE OF YYY)      *        
003200*        EG: SET DP031-IDNBR TO TRUE.                            *        
003300*    6. MOVE BBBBBB     TO DP031-CLEAR-TEXT.                     *        
003400*       -INCASE OF TOKENIZE OR ENCRYPT                           *        
003500*       MOVE CCCCCC     TO DP031-CIPHER-TEXT.                    *        
003600*       -INCASE OF DETOKENIZE OR DECRYPT                         *        
003700*    7. FOR TOKENIZE OR DETOKENIZATION MOVE THE LENGTH OF THE    *        
003800*       INPUT VARIABLE TO DP031-INPUT-LEN.                       *        
003900*    8. FOR ENCRYPT OR DECRYPT MOVE THE LENGTH OF CLEAR/CIPHER   *        
004000*       VARIABLES TO CSI-CLEAR-TEXT-LENGTH/CSI-CIPHER-TEXT-LENGTH*        
004100*                                                                *        
004200******************************************************************        
004300*  CALL THE PTYPCSL MODULE FOR TOKEN AND DE-TOKENIZATION         *        
004400******************************************************************        
004500 DP031-0000-PROTG-TOKEN-DETOKEN.                                          
004600                                                                          
004700     COMPUTE DP031-INPUT-LEN = DP031-INPUT-LEN + 4.                       
004800                                                                          
004900     MOVE DP031-INPUT-LEN     TO CSI-CLEAR-TEXT-LENGTH.                   
005000     MOVE DP031-INPUT-LEN     TO CSI-CIPHER-TEXT-LENGTH.                  
005100     MOVE LOW-VALUES          TO DP031-CLEAR-KEYID                        
005200                                 DP031-CIPHER-KEYID.                      
005300     MOVE DP031-DATA-ELEMENT  TO CSI-DPS-DATA-ELEMENT.                    
005400                                                                          
005500     CALL DP031-PROTEG-SUBRTN-PTYPCSL USING                               
005600         CSI-FUNCTION                                                     
005700         CSI-CLEAR-TEXT-LENGTH                                            
005800         DP031-CLEAR-DATA                                                 
005900         CSI-CIPHER-TEXT-LENGTH                                           
006000         DP031-CIPHER-DATA                                                
006100         CSI-DPS-DATA-ELEMENT                                             
006200         CSI-RETURN-CODE                                                  
006300         CSI-REASON-CODE.                                                 
006400                                                                          
006410     IF CSI-RETURN-CODE NOT = ZERO                                        
006450        DISPLAY DP031-PROTEG-SUBRTN-PTYPCSL                               
006451                " ERROR: RC = " CSI-RETURN-CODE                           
006460                ", RS = " CSI-REASON-CODE                                 
006470        IF CSI-ENCRYPT-FUNCTION                                           
006480           DISPLAY 'ERROR IN TOKENIZATION    - ' DP031-FIELD              
006490        ELSE                                                              
006491           DISPLAY 'ERROR IN DE-TOKENIZATION - ' DP031-FIELD              
006492        END-IF                                                            
006493        EVALUATE TRUE                                                     
006494           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
006495               (CSI-REASON-CODE = DP031-RS-ACCESS-DENIED1 OR              
006496                CSI-REASON-CODE = DP031-RS-ACCESS-DENIED2)                
006497                DISPLAY DP031-DATA-ELEMENT " - ACCESS DENIED"             
006498                PERFORM DP031-9999-ABEND                                  
006499           WHEN CSI-RETURN-CODE = DP031-RC-12 AND                         
006500               (CSI-REASON-CODE = DP031-RS-DP-NOT-ACTIVE1 OR              
006501                CSI-REASON-CODE = DP031-RS-DP-NOT-ACTIVE2)                
006502                DISPLAY "DATA PROTECTOR NOT ACTIVE"                       
006503                PERFORM DP031-9999-ABEND                                  
006504           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
006505                CSI-REASON-CODE = DP031-RS-INCRCT-ELEMENT                 
006506                DISPLAY DP031-DATA-ELEMENT                                
006507                                      " - INCORRECT DATA ELEMENT"         
006508                PERFORM DP031-9999-ABEND                                  
006509           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
006510                CSI-REASON-CODE = DP031-RS-INVALID-INPUT1                 
006511           WHEN CSI-RETURN-CODE = DP031-RC-12 AND                         
006512                CSI-REASON-CODE = DP031-RS-INVALID-INPUT2                 
006513                DISPLAY "INVALID INPUT/INVALID INPUT LENGTH"              
006514           WHEN CSI-RETURN-CODE = DP031-RC-08 AND                         
006515               (CSI-REASON-CODE = DP031-RS-INP-LEN-ZERO1 OR               
006516                CSI-REASON-CODE = DP031-RS-INP-LEN-ZERO2)                 
006517                DISPLAY "INPUT LENGTH ZERO"                               
006518           WHEN OTHER                                                     
006519                CONTINUE                                                  
006520        END-EVALUATE                                                      
006530     END-IF.                                                              
006900*                                                                         
007000******************************************************************        
007100*  CALL THE PTYPSLL MODULE TO ENCRYPT AND DECRYPT                         
007200******************************************************************        
007300 DP031-1000-PROTG-ENCRYP-DECRYP.                                          
007400                                                                          
007500     MOVE DP031-DATA-ELEMENT  TO CSI-DPS-DATA-ELEMENT.                    
007600                                                                          
007700     CALL DP031-PROTEG-SUBRTN-PTYPSLL USING                               
007800         CSI-FUNCTION                                                     
007900         CSI-CLEAR-TEXT-LENGTH                                            
008000         DP031-CLEAR-TEXT                                                 
008100         CSI-CIPHER-TEXT-LENGTH                                           
008200         DP031-CIPHER-TEXT                                                
008300         CSI-DPS-DATA-ELEMENT                                             
008400         CSI-RETURN-CODE                                                  
008500         CSI-REASON-CODE.                                                 
008600                                                                          
008610     IF CSI-RETURN-CODE NOT = ZERO                                        
008611        DISPLAY DP031-PROTEG-SUBRTN-PTYPSLL                               
008612                " ERROR: RC = " CSI-RETURN-CODE                           
008613                ", RS = " CSI-REASON-CODE                                 
008650        IF CSI-ENCRYPT-FUNCTION                                           
008660           DISPLAY 'ERROR IN ENCRYPTION - ' DP031-FIELD                   
008670        ELSE                                                              
008680           DISPLAY 'ERROR IN DECRYPTION - ' DP031-FIELD                   
008690        END-IF                                                            
008700        EVALUATE TRUE                                                     
008800           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
008801               (CSI-REASON-CODE = DP031-RS-ACCESS-DENIED1 OR              
008802                CSI-REASON-CODE = DP031-RS-ACCESS-DENIED2)                
008803                DISPLAY DP031-DATA-ELEMENT " - ACCESS DENIED"             
008804                PERFORM DP031-9999-ABEND                                  
008805           WHEN CSI-RETURN-CODE = DP031-RC-12 AND                         
008806               (CSI-REASON-CODE = DP031-RS-DP-NOT-ACTIVE1 OR              
008807                CSI-REASON-CODE = DP031-RS-DP-NOT-ACTIVE2)                
008808                DISPLAY "DATA PROTECTOR NOT ACTIVE"                       
008809                PERFORM DP031-9999-ABEND                                  
008810           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
008811                CSI-REASON-CODE = DP031-RS-INCRCT-ELEMENT                 
008812                DISPLAY DP031-DATA-ELEMENT                                
008813                                     " - INCORRECT DATA ELEMENT"          
008814                PERFORM DP031-9999-ABEND                                  
008815           WHEN CSI-RETURN-CODE = DP031-RC-04 AND                         
008816                CSI-REASON-CODE = DP031-RS-INVALID-INPUT1                 
008817           WHEN CSI-RETURN-CODE = DP031-RC-12 AND                         
008818                CSI-REASON-CODE = DP031-RS-INVALID-INPUT2                 
008819                DISPLAY "INVALID INPUT/INVALID INPUT LENGTH"              
008820           WHEN CSI-RETURN-CODE = DP031-RC-12 AND                         
008821                CSI-REASON-CODE = DP031-RS-INP-TOO-SHORT                  
008822                DISPLAY "INPUT TOO SHORT"                                 
008823           WHEN OTHER                                                     
008824                CONTINUE                                                  
008825        END-EVALUATE                                                      
008826     END-IF.                                                              
009100*                                                                         
009200******************************************************************        
009300*  ABEND THE PROGRAM IN THE EVENT THAT A ERROR OR WARNING        *        
009400*  CODE OCCURS.                                                  *        
009500******************************************************************        
009600 DP031-9999-ABEND.                                                        
009700                                                                          
009800     DISPLAY DP031-PROGRAM-NAME                                           
009900             " - ABNORMAL TERMINATION - STOP RUN".                        
009910     MOVE DP031-PROTEG-4022 TO ABEND-CODE.                                
010000     CALL 'ILBOABN0' USING ABEND-CODE.                                    
