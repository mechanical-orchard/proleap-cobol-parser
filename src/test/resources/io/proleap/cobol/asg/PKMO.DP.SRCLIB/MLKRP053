       IDENTIFICATION DIVISION.                                         11/22/94
00002                                                                   MLKRP053
00003  PROGRAM-ID.    MLKRP053                                             LV005
00004  AUTHOR.        TESS BESTE.                                       MLKRP053
00005  INSTALLATION.  KOHLS DEPARTMENT STORES.                          MLKRP053
00006  DATE-WRITTEN.  11/22/93.                                         MLKRP053
00007  DATE-COMPILED.                                                   MLKRP053
00008                                                                   MLKRP053
00009 ******************************************************************MLKRP053
00010 *               MLKRP053 - EDIT ERROR REPORT                      MLKRP053
00011 *                                                                 MLKRP053
00012 *      THIS PROGRAM READS A FILE OF MERCHANDISE LEDGER            MLKRP053
00013 * TRANSACTIONS FOR WHICH ERRORS WERE DETECTED DURING THE EDIT     MLKRP053
00014 * PROCESS, AND CREATES AN EDIT ERROR REPORT.                      MLKRP053
00015 *      THE INPUT FILE CONTAINS AN ERROR CODE FOR EACH FIELD ON THEMLKRP053
00016 * TRANSACTION. THE ERROR CODE INDICATES WHAT TYPE OF ERROR, IF    MLKRP053
00017 * ANY, WAS DETECTED FOR THE FIELD.                                MLKRP053
00018 *      EACH TRANSACTION ON THE INPUT FILE IS REPORTED.  THE FIELDSMLKRP053
00019 * IN ERROR ARE UNDERSCORED WITH ASTERISKS.                        MLKRP053
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      *   03/03/97  TOM MEDREK - FAST TRANSFER SYSTEM CHANGES           MLKRP053
      *     EXPANDED DOCUMENT NUMBER ON INVENTORY TRANSACTION EDIT RPT          
      *     (MLKRP053-1) FROM 5 TO 11 CHARACTERS.  ONLY FAST TRANSFERS          
      *     WILL BE 11 CHARACTERS LONG HOWEVER, AND WILL BE COMPRISED OF        
      *     THE TRANSFER STORE NUMBER, HYPHEN, AND SEQUENCE NUMBER.             
      *   04/10/02  SCOTT JACKSON - NEW SKU CHANGES                     MLKRP053
      *     SKU TO BE AN 8-BYTE FIELD WITH NO DEPT/CLASS/SEQ REFERENCES.        
      *     INPUT FILE WILL CONTAIN ATTRIBUTES ASSOCIATED WITH SKU FROM         
      *     MLKUP020/MLKUP021 AS ML026-RPT2-DEPT-CLASS. MLRD026 CHANGED.        
      *  ** ERROR ADDED FOR SKU NOT FOUND. EXPAND REPORT TO 150 LRECL.          
W26603* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
R26603* W26603  06-08-2004 ROD JANSSEN ADJUSTMENT RECORD TRUNCATION.            
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W28545* W28545  08-22-2005 ROD JANSSEN THE NEW TRANSFER PROCESS                 
P49345* P49345  01-16-2014 COGNIZANT   EXPANDED THE LAYOUT OF THE ERROR         
P49345*                                RECORD TO ACCOMODATE LARGER              
P49345*                                RETAIL VALUE.                            
00020 ******************************************************************MLKRP053
00021                                                                   MLKRP053
00022                                                                   MLKRP053
00023                                                                   MLKRP053
00024  ENVIRONMENT DIVISION.                                            MLKRP053
00025                                                                   MLKRP053
00026  CONFIGURATION SECTION.                                           MLKRP053
00027                                                                   MLKRP053
00028  SOURCE-COMPUTER.        IBM-3090.                                MLKRP053
00029  OBJECT-COMPUTER.        IBM-3090.                                MLKRP053
00030                                                                   MLKRP053
00031                                                                   MLKRP053
00032  INPUT-OUTPUT SECTION.                                            MLKRP053
00033                                                                   MLKRP053
00034  FILE-CONTROL.                                                    MLKRP053
00035      SELECT EDIT-ERROR-FILE                                       MLKRP053
00036          ASSIGN TO INP01.                                         MLKRP053
00037      SELECT EDIT-ERROR-REPORT                                     MLKRP053
00038          ASSIGN TO RPT01.                                         MLKRP053
00039                                                                   MLKRP053
00042  DATA DIVISION.                                                   MLKRP053
00043                                                                   MLKRP053
00044  FILE SECTION.                                                    MLKRP053
00045                                                                   MLKRP053
00046  FD  EDIT-ERROR-FILE                                              MLKRP053
00047      LABEL RECORDS ARE STANDARD                                   MLKRP053
00048      BLOCK CONTAINS 0 RECORDS                                     MLKRP053
00049      RECORDING  MODE IS F.                                        MLKRP053
W21731*01  EDIT-ERROR-RECORD           PIC X(200).                      MLKRP053
R26603 01  EDIT-ERROR-RECORD           PIC X(260).                      MLKRP053
00051                                                                   MLKRP053
00052  FD  EDIT-ERROR-REPORT                                            MLKRP053
00053      LABEL RECORDS ARE OMITTED                                    MLKRP053
00054      RECORD CONTAINS 150 CHARACTERS                               MLKRP053
00055      RECORDING  MODE IS F.                                        MLKRP053
W21731 01  EDIT-ERROR-REPORT-LINE      PIC X(150).                      MLKRP053
00057                                                                   MLKRP053
00058                                                                   MLKRP053
00059  WORKING-STORAGE SECTION.                                         MLKRP053
00060                                                                   MLKRP053
00061  01  PC-PROGRAM-CONSTANTS.                                        MLKRP053
00062      05  PC-MAX-LINE-COUNT            PIC S9(04)    VALUE +55     MLKRP053
00063                                                         COMP SYNC.MLKRP053
00064      05  PC-INVENTORY-ONLY-MODE       PIC  X(20)    VALUE            CL**3
00065                                                '(INVENTORY ONLY)'.MLKRP053
00066                                                                   MLKRP053
00067  01  PV-PROGRAM-VARIABLES.                                        MLKRP053
W20445     05  PV-EDIT-ENT-ID               PIC 9(06)  VALUE ZEROS.     MLKRP053
00068      05  PV-ADVANCE                   PIC S9(03)    VALUE ZERO    MLKRP053
00069                                                     COMP-3.       MLKRP053
00070      05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.   MLKRP053
00071      05  PV-LINE-COUNT                PIC S9(04)    VALUE ZERO    MLKRP053
00072                                                     COMP SYNC.    MLKRP053
00073      05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.  MLKRP053
00074      05  PV-PREV-TRANS-CDE            PIC  X(02)    VALUE SPACE.  MLKRP053
00075                                                                   MLKRP053
00076      05  PV-DATE-YYMMDD.                                          MLKRP053
00077          10  PV-DATE-YYMMDD-YY        PIC  X(02)    VALUE SPACE.  MLKRP053
00078          10  PV-DATE-YYMMDD-MM        PIC  X(02)    VALUE SPACE.  MLKRP053
00079          10  PV-DATE-YYMMDD-DD        PIC  X(02)    VALUE SPACE.  MLKRP053
00080      05  PV-ISO-FMT-DATE.                                         MLKRP053
00081          10  PV-ISO-FMT-CC            PIC  X(02).                 MLKRP053
00082          10  PV-ISO-FMT-YY            PIC  X(02).                 MLKRP053
00083          10  FILLER                   PIC  X.                     MLKRP053
00084          10  PV-ISO-FMT-MM            PIC  X(02).                 MLKRP053
00085          10  FILLER                   PIC  X.                     MLKRP053
00086          10  PV-ISO-FMT-DD            PIC  X(02).                 MLKRP053
00087      05  PV-TIME-HHMMSS.                                          MLKRP053
00088          10  PV-TIME-HHMMSS-HH        PIC  X(02)    VALUE SPACES. MLKRP053
00089          10  PV-TIME-HHMMSS-MM        PIC  X(02)    VALUE SPACES. MLKRP053
00090          10  PV-TIME-HHMMSS-SS        PIC  X(02)    VALUE SPACES. MLKRP053
00091                                                                   MLKRP053
00092  01  SW-SWITCHES.                                                 MLKRP053
00093      05  SW-END-OF-EDIT-ERROR-FILE-SW PIC X         VALUE 'N'.    MLKRP053
00094          88  SW-END-OF-EDIT-ERROR-FILE              VALUE 'Y'.    MLKRP053
00095          88  SW-NOT-END-OF-EDIT-ERROR-FILE          VALUE 'N'.    MLKRP053
00096                                                                   MLKRP053
00097  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.MLKRP053
00098                                                                   MLKRP053
00099 ***************************************************************** MLKRP053
00100 * EDIT-ERROR-RECORD LAYOUT.                                       MLKRP053
00101 ***************************************************************** MLKRP053
00102                                                                   MLKRP053
00103      COPY MLRD026.                                                   CL**5
00104                                                                   MLKRP053
00105                                                                   MLKRP053
00106 ***************************************************************** MLKRP053
00107 * ERROR CODE VALUES.                                              MLKRP053
00108 ***************************************************************** MLKRP053
00109                                                                   MLKRP053
00110      COPY MLRD027.                                                MLKRP053
00111                                                                   MLKRP053
00112                                                                   MLKRP053
00113 ******************************************************************MLKRP053
00114 * STANDARD REPORT HEADING                                         MLKRP053
00115 ******************************************************************MLKRP053
00116                                                                   MLKRP053
00117      COPY DPWS132O.                                               MLKRP053
00118                                                                   MLKRP053
00119  01  HD-WE-WEEK-END-HEADING.                                      MLKRP053
00120      05  FILLER                  PIC  X(46)    VALUE SPACES.      MLKRP053
00121      05  FILLER                  PIC  X(31)    VALUE              MLKRP053
00122                                 'INVENTORY TRANSACTION EDIT FOR '.MLKRP053
00123      05  HD-WE-DATE-MM           PIC  X(02).                      MLKRP053
00124      05  FILLER                  PIC  X        VALUE '/'.         MLKRP053
00125      05  HD-WE-DATE-DD           PIC  X(02).                      MLKRP053
00126      05  FILLER                  PIC  X        VALUE '/'.         MLKRP053
00127      05  HD-WE-DATE-YY           PIC  X(02).                      MLKRP053
00128      05  FILLER                  PIC  X        VALUE SPACE.       MLKRP053
00129      05  HD-WE-PROCESSING-MODE   PIC  X(20)    VALUE SPACE.          CL**3
00130                                                                   MLKRP053
00131  01  HD-CL1-COLUMN-HEADING-1.                                     MLKRP053
00132      05  FILLER                  PIC  X(03)    VALUE SPACE.       MLKRP053
00133      05  FILLER                  PIC  X(05)    VALUE 'FM TO'.     MLKRP053
00134      05  FILLER                  PIC  X(13)    VALUE SPACE.       MLKRP053
W26603     05  FILLER                  PIC  X(13)    VALUE SPACE.       MLKRP053
00135      05  FILLER                  PIC  X(04)    VALUE 'DOC.'.      MLKRP053
P49345     05  FILLER                  PIC  X(41)    VALUE SPACE.       MLKRP053
P49345     05  FILLER                  PIC  X(29)    VALUE              MLKRP053
P49345                            'OLD            NEW         R'.       MLKRP053
W26603     05  FILLER                  PIC  X(05)    VALUE SPACE.       MLKRP053
W26603     05  FILLER                  PIC  X(09)    VALUE              MLKRP053
W26603                                           'INHSE ENT'.           MLKRP053
W26603     05  FILLER                  PIC  X(22)    VALUE SPACE.       MLKRP053
00140      05  FILLER                  PIC  X(03)    VALUE 'NET'.       MLKRP053
00141      05  FILLER                  PIC  X(12)    VALUE SPACE.       MLKRP053
00142      05  FILLER                  PIC  X(02)    VALUE 'TR'.        MLKRP053
W26603     05  FILLER                  PIC  X(03)    VALUE SPACE.       MLKRP053
00143                                                                   MLKRP053
00144  01  HD-CL2-COLUMN-HEADING-2.                                     MLKRP053
00145      05  FILLER                  PIC  X(02)    VALUE 'CD'.        MLKRP053
W26603     05  FILLER                  PIC  X(01)    VALUE SPACE.       MLKRP053
00147      05  FILLER                  PIC  X(05)    VALUE 'STORE'.     MLKRP053
W26603     05  FILLER                  PIC  X(06)    VALUE SPACE.       MLKRP053
00149      05  FILLER                  PIC  X(14)    VALUE              MLKRP053
00150                                                ' CLASS    SKU '.  MLKRP053
W21731     05  FILLER                  PIC  X(10)    VALUE '       NO.'.MLKRP053
00151      05  FILLER                  PIC  X(07)    VALUE SPACES.      MLKRP053
00152      05  FILLER                  PIC  X(04)    VALUE 'DATE'.      MLKRP053
00153      05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP053
00154      05  FILLER                  PIC  X(07)    VALUE 'KM UNTS'.   MLKRP053
00155      05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP053
P49345     05  FILLER                  PIC  X(14)    VALUE 'AMOUNT'.    MLKRP053
00157      05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP053
P49345     05  FILLER                  PIC  X(14)    VALUE 'PRICE'.     MLKRP053
00159      05  FILLER                  PIC  X        VALUE SPACE.       MLKRP053
00160      05  FILLER                  PIC  X(06)    VALUE 'PRICE'.     MLKRP053
00161      05  FILLER                  PIC  X(06)    VALUE 'PO NO'.     MLKRP053
00162      05  FILLER                  PIC  X(05)    VALUE 'S BCH'.     MLKRP053
00163      05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP053
W26603     05  FILLER                  PIC  X(04)    VALUE 'VEND'.      MLKRP053
W26603     05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP053
W26603     05  FILLER                  PIC  X(04)    VALUE ' ID '.      MLKRP053
W26603     05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP053
00166      05  FILLER                  PIC  X(07)    VALUE 'DISCT'.     MLKRP053
00167      05  FILLER                  PIC  X(07)    VALUE 'FREIGHT'.   MLKRP053
00168      05  FILLER                  PIC  X(05)    VALUE SPACES.      MLKRP053
00169      05  FILLER                  PIC  X(04)    VALUE 'COST'.      MLKRP053
00170      05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP053
00171      05  FILLER                  PIC  X(06)    VALUE 'RETAIL'.    MLKRP053
00172      05  FILLER                  PIC  X(02)    VALUE SPACES.      MLKRP053
00173      05  FILLER                  PIC  X(02)    VALUE 'CD'.        MLKRP053
W26603     05  FILLER                  PIC  X(05)    VALUE SPACE.       MLKRP053
00174                                                                   MLKRP053
00175  01  ED-ERROR-DETAIL-LINE.                                        MLKRP053
00176      05  ED-CD                   PIC  X(02).                      MLKRP053
00177      05  FILLER                  PIC  X(01).                      MLKRP053
W26603     05  ED-FM-STORE             PIC  X(05).                      MLKRP053
00179      05  FILLER                  PIC  X.                          MLKRP053
W26603     05  ED-TO-STORE             PIC  X(05).                      MLKRP053
00181      05  FILLER                  PIC  X.                          MLKRP053
00182      05  ED-CLASS                PIC  X(05).                      MLKRP053
00183      05  FILLER                  PIC  X.                          MLKRP053
W21731     05  ED-SKU                  PIC  X(08).                      MLKRP053
W21731     05  FILLER                  PIC  X.                          MLKRP053
           05  ED-FAST-DOC-NBR-11.                                      MLKRP053
               10  ED-FAST-DOC-NBR-4   PIC  X(04).                      MLKRP053
               10  FILLER              PIC  X(01)  VALUE '-'.           MLKRP053
               10  ED-FAST-DOC-NBR-6   PIC  X(06).                      MLKRP053
           05  FILLER  REDEFINES  ED-FAST-DOC-NBR-11.                           
               10  FILLER              PIC  X(04).                      MLKRP053
               10  ED-DOC-NBR-7.                                                
                   15  FILLER          PIC  X(02).                      MLKRP053
00186              15  ED-DOC-NBR-5    PIC  X(05).                      MLKRP053
00187      05  FILLER                  PIC  X.                          MLKRP053
00188      05  ED-DATE.                                                 MLKRP053
00189          10  ED-DATE-MM          PIC  X(02).                      MLKRP053
00190          10  ED-DATE-SLASH1      PIC  X.                          MLKRP053
00191          10  ED-DATE-DD          PIC  X(02).                      MLKRP053
00192          10  ED-DATE-SLASH2      PIC  X.                          MLKRP053
00193          10  ED-DATE-YY          PIC  X(02).                      MLKRP053
00194      05  FILLER                  PIC  X.                          MLKRP053
00195      05  ED-KM                   PIC  X(02).                      MLKRP053
00196      05  FILLER                  PIC  X.                          MLKRP053
00197      05  ED-UNTS-X.                                               MLKRP053
00198          10  ED-UNTS             PIC  ZZ9.                        MLKRP053
00199      05  ED-AMOUNT-X.                                             MLKRP053
P49345         10  ED-AMOUNT           PIC  Z(11).99-.                  MLKRP053
00201      05  FILLER                  PIC  X.                          MLKRP053
00202      05  ED-OLD-PRICE-X.                                          MLKRP053
P49345         10  ED-OLD-PRICE        PIC  Z(11).99-.                  MLKRP053
00204      05  ED-NEW-PRICE-X.                                          MLKRP053
00205          10  ED-NEW-PRICE        PIC  Z(04).99-.                  MLKRP053
00206      05  ED-PO-NO-X.                                              MLKRP053
00207          10  ED-PO-NO            PIC  9(05).                      MLKRP053
00208      05  FILLER                  PIC  X.                          MLKRP053
00209      05  ED-RS                   PIC  X.                          MLKRP053
00210      05  FILLER                  PIC  X.                          MLKRP053
00211      05  ED-BCH                  PIC  X(04).                      MLKRP053
           05  FILLER                  PIC  X(01).                      MLKRP053
00213      05  ED-VENDOR               PIC  X(05).                      MLKRP053
W20445***  05  FILLER                  PIC  X.                          MLKRP053
W20445     05  ED-ENT-ID               PIC  ZZZZZZ.                     MLKRP053
W26603     05  FILLER                  PIC  X.                          MLKRP053
00215      05  ED-DISCT-X.                                              MLKRP053
00216          10  ED-DISCT            PIC  Z(04).99-.                  MLKRP053
00217      05  FILLER                  PIC  X.                          MLKRP053
00218      05  ED-FREIGHT-X.                                            MLKRP053
00219          10  ED-FREIGHT          PIC  Z(04).99-.                  MLKRP053
00220      05  FILLER                  PIC  X.                          MLKRP053
00221      05  ED-NET-COST-X.                                           MLKRP053
00222          10  ED-NET-COST         PIC  Z(05).99-.                  MLKRP053
00223      05  FILLER                  PIC  X.                          MLKRP053
00224      05  ED-RETAIL-X.                                             MLKRP053
00225          10  ED-RETAIL           PIC  Z(05).99-.                  MLKRP053
00226      05  FILLER                  PIC  X.                          MLKRP053
00227      05  ED-TR-CD                PIC  X(02).                      MLKRP053
00228                                                                   MLKRP053
00229  01  EH-ERROR-HIGHLIGHT-LINE.                                     MLKRP053
00230      05  EH-CD                   PIC  X(02).                      MLKRP053
00231      05  FILLER                  PIC  X.                          MLKRP053
W26603     05  EH-FM-STORE             PIC  X(05).                      MLKRP053
00233      05  FILLER                  PIC  X.                          MLKRP053
W26603     05  EH-TO-STORE             PIC  X(05).                      MLKRP053
00235      05  FILLER                  PIC  X.                          MLKRP053
00236      05  EH-CLASS                PIC  X(05).                      MLKRP053
00237 *    05  FILLER                  PIC  X(03).                      MLKRP053
00237      05  FILLER                  PIC  X(01).                      MLKRP053
W21731     05  EH-SKU                  PIC  X(08).                      MLKRP053
W21731     05  FILLER                  PIC  X(01).                      MLKRP053
00238 *    05  EH-DOC-NBR              PIC  X(05).                      MLKRP053
00238      05  EH-DOC-NBR              PIC  X(11).                      MLKRP053
00239      05  FILLER                  PIC  X.                          MLKRP053
00240      05  EH-DATE                 PIC  X(08).                      MLKRP053
00241      05  FILLER                  PIC  X.                          MLKRP053
00242      05  EH-KM                   PIC  XX.                         MLKRP053
00243      05  FILLER                  PIC  X.                          MLKRP053
00244      05  EH-UNTS                 PIC  X(3).                       MLKRP053
P49345     05  EH-AMOUNT               PIC  X(14).                      MLKRP053
00246      05  FILLER                  PIC  X.                          MLKRP053
P49345     05  EH-OLD-PRICE            PIC  X(14).                      MLKRP053
00248      05  FILLER                  PIC  X.                          MLKRP053
00249      05  EH-NEW-PRICE            PIC  X(07).                      MLKRP053
00250      05  FILLER                  PIC  X.                          MLKRP053
00251      05  EH-PO-NO                PIC  X(05).                      MLKRP053
00252      05  FILLER                  PIC  X.                          MLKRP053
00253      05  EH-RS                   PIC  X.                          MLKRP053
00254      05  FILLER                  PIC  X.                          MLKRP053
00255      05  EH-BCH                  PIC  XXXX.                       MLKRP053
00256      05  FILLER                  PIC  X(01).                      MLKRP053
00257      05  EH-VENDOR               PIC  X(05).                      MLKRP053
W20445*    05  FILLER                  PIC  X.                          MLKRP053
W20445     05  EH-ENT-ID               PIC  X(06).                      MLKRP053
W26603     05  FILLER                  PIC  X.                          MLKRP053
00259      05  EH-DISCT                PIC  X(08).                      MLKRP053
00260      05  FILLER                  PIC  X.                          MLKRP053
00261      05  EH-FREIGHT              PIC  X(08).                      MLKRP053
00262      05  FILLER                  PIC  X.                          MLKRP053
00263      05  EH-NET-COST             PIC  X(09).                      MLKRP053
00264      05  FILLER                  PIC  X.                          MLKRP053
00265      05  EH-RETAIL               PIC  X(09).                      MLKRP053
00266      05  FILLER                  PIC  X.                          MLKRP053
00267      05  EH-TR-CD                PIC  X(02).                      MLKRP053
00268                                                                   MLKRP053
00269  01  ER-END-OF-REPORT-LINE.                                       MLKRP053
00270      05  FILLER                       PIC  X(21)    VALUE         MLKRP053
00271                                           '*** END OF REPORT ***'.MLKRP053
00272                                                                   MLKRP053
00273  LINKAGE SECTION.                                                 MLKRP053
00274                                                                   MLKRP053
00275                                                                   MLKRP053
00276  PROCEDURE DIVISION.                                              MLKRP053
00277                                                                   MLKRP053
00278  A100-MAIN-MODULE.                                                MLKRP053
00279                                                                   MLKRP053
00280      OPEN INPUT  EDIT-ERROR-FILE                                  MLKRP053
00281           OUTPUT EDIT-ERROR-REPORT.                               MLKRP053
00282                                                                   MLKRP053
00283      PERFORM B100-INITIALIZATION.                                 MLKRP053
00284      PERFORM B200-PROCESS-CONTROL-CARD.                           MLKRP053
00285      PERFORM B300-CREATE-EDIT-ERROR-REPORT.                       MLKRP053
00286                                                                   MLKRP053
00287      CLOSE EDIT-ERROR-FILE                                        MLKRP053
00288            EDIT-ERROR-REPORT.                                     MLKRP053
00289                                                                   MLKRP053
00290      STOP RUN.                                                    MLKRP053
00291                                                                   MLKRP053
00292                                                                   MLKRP053
00293  B100-INITIALIZATION.                                             MLKRP053
00294                                                                   MLKRP053
00295      INITIALIZE PV-PAGE-COUNT.                                    MLKRP053
00296                                                                   MLKRP053
00297      ACCEPT PV-DATE-YYMMDD FROM DATE.                             MLKRP053
00298      MOVE   PV-DATE-YYMMDD-YY TO DP132O-RUN-YEAR.                 MLKRP053
00299      MOVE   PV-DATE-YYMMDD-MM TO DP132O-RUN-MONTH.                MLKRP053
00300      MOVE   PV-DATE-YYMMDD-DD TO DP132O-RUN-DAY.                  MLKRP053
00301      ACCEPT PV-TIME-HHMMSS FROM TIME.                             MLKRP053
00302      MOVE PV-TIME-HHMMSS-HH   TO DP132O-RUN-HOUR.                 MLKRP053
00303      MOVE PV-TIME-HHMMSS-MM   TO DP132O-RUN-MINUTE.               MLKRP053
00304      MOVE 'MLKRP053'          TO DP132O-PROGRAM-NAME.             MLKRP053
00305      MOVE 1                   TO DP132O-REPORT-NUMBER.            MLKRP053
00306                                                                   MLKRP053
00307                                                                   MLKRP053
00308 ******************************************************************MLKRP053
00309 * MAKE SURE THAT THE EDIT-ERROR-FILE CONTAINS A CONTROL CARD AS   MLKRP053
00310 * THE FIRST RECORD. IF THE CONTROL CARD IS PRESENT, USE THE DATE  MLKRP053
00311 * IT CONTAINS TO BUILD THE DATE IN THE WEEK-ENDING HEADING LINE.  MLKRP053
00312 ******************************************************************MLKRP053
00313  B200-PROCESS-CONTROL-CARD.                                       MLKRP053
00314                                                                   MLKRP053
00315      PERFORM Z100-READ-EDIT-ERROR-FILE.                           MLKRP053
00316                                                                   MLKRP053
00317      IF SW-END-OF-EDIT-ERROR-FILE                                 MLKRP053
00318          DISPLAY '** ABEND **'                                    MLKRP053
00319          DISPLAY '** PGM = MLKRP053'                              MLKRP053
00320          DISPLAY '** NO CONTROL CARD ON EDIT-ERROR-FILE'          MLKRP053
00321          MOVE +4003 TO ABEND-CODE                                 MLKRP053
00322          CALL 'ILBOABN0' USING ABEND-CODE                         MLKRP053
00323      ELSE                                                         MLKRP053
00324          IF NOT ML026-RPT2-CNTL-CARD-HDR-REC                      MLKRP053
00325              DISPLAY '** ABEND **'                                MLKRP053
00326              DISPLAY '** PGM = MLKRP053'                          MLKRP053
00327              DISPLAY '** FIRST RECORD ON EDIT-ERROR-FILE IS NOT'  MLKRP053
00328              DISPLAY '** CONTROL CARD.'                           MLKRP053
00329              MOVE +4003 TO ABEND-CODE                             MLKRP053
00330              CALL 'ILBOABN0' USING ABEND-CODE                     MLKRP053
00331          ELSE                                                     MLKRP053
00332              DISPLAY '** XXXRP053 INPUT CONTROL CARD ='           MLKRP053
00333                       ML026-RPT2-CNTL-CARD-REC                    MLKRP053
00334              MOVE ML026-RPT2-EDIT-DTE TO PV-ISO-FMT-DATE          MLKRP053
00335              MOVE PV-ISO-FMT-MM TO HD-WE-DATE-MM                  MLKRP053
00336              MOVE PV-ISO-FMT-DD TO HD-WE-DATE-DD                  MLKRP053
00337              MOVE PV-ISO-FMT-YY TO HD-WE-DATE-YY                  MLKRP053
00338              IF ML026-RPT2-MODE-INV-ONLY                          MLKRP053
00339                  MOVE PC-INVENTORY-ONLY-MODE                         CL**3
00340                                 TO HD-WE-PROCESSING-MODE             CL**3
00341              END-IF                                               MLKRP053
00342          END-IF                                                   MLKRP053
00343      END-IF.                                                      MLKRP053
00344                                                                   MLKRP053
00345                                                                   MLKRP053
00346 ******************************************************************MLKRP053
00347 * READ THE EDIT-ERROR-FILE AND CREATE THE EDIT-ERROR-REPORT.      MLKRP053
00348 ******************************************************************MLKRP053
00349  B300-CREATE-EDIT-ERROR-REPORT.                                   MLKRP053
00350                                                                   MLKRP053
00351      PERFORM Z300-PRINT-RPT-HEADINGS.                             MLKRP053
00352      PERFORM Z100-READ-EDIT-ERROR-FILE.                           MLKRP053
00353                                                                   MLKRP053
00354      IF SW-NOT-END-OF-EDIT-ERROR-FILE                             MLKRP053
00355          MOVE ML026-RPT2-TRANS-CDE TO PV-PREV-TRANS-CDE           MLKRP053
00356      END-IF.                                                      MLKRP053
00357                                                                   MLKRP053
00358      PERFORM C200-PROCESS-EDIT-ERROR-FILE                         MLKRP053
00359          UNTIL SW-END-OF-EDIT-ERROR-FILE.                         MLKRP053
00360                                                                   MLKRP053
00361      MOVE ER-END-OF-REPORT-LINE TO EDIT-ERROR-REPORT-LINE.        MLKRP053
00362      MOVE 3 TO PV-ADVANCE.                                        MLKRP053
00363      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00364                                                                   MLKRP053
00365                                                                   MLKRP053
00366 ******************************************************************MLKRP053
00367 * READ AND PROCESS THE INPUT FILE. WRITE AN ERROR DETAIL LINE AND MLKRP053
00368 * AN ERROR HIGHLIGHT LINE FOR EACH INPUT RECORD. THE DATA         MLKRP053
00369 * CONTAINED IN EACH LINE WILL BE DEPENDENT UPON THE TYPE OF       MLKRP053
00370 * TRANSACTION BEING PROCESSED.                                    MLKRP053
00371 ******************************************************************MLKRP053
00372  C200-PROCESS-EDIT-ERROR-FILE.                                    MLKRP053
00373                                                                   MLKRP053
00374      IF ML026-RPT2-TRANS-CDE NOT = PV-PREV-TRANS-CDE              MLKRP053
00375          MOVE +99                  TO PV-LINE-COUNT               MLKRP053
00376          MOVE ML026-RPT2-TRANS-CDE TO PV-PREV-TRANS-CDE           MLKRP053
00377      END-IF.                                                      MLKRP053
00378                                                                   MLKRP053
00379      MOVE SPACE TO ED-ERROR-DETAIL-LINE                           MLKRP053
00380                    EH-ERROR-HIGHLIGHT-LINE.                       MLKRP053
00381                                                                   MLKRP053
00382      EVALUATE TRUE                                                MLKRP053
00383          WHEN ML026-RPT2-PURCHASE                                 MLKRP053
00384              PERFORM D010-REPORT-PURCH-ERR                        MLKRP053
00385          WHEN ML026-RPT2-SALE                                     MLKRP053
00386              PERFORM D020-REPORT-SALE-ERR                         MLKRP053
00387          WHEN ML026-RPT2-STORE-TRANSFER                           MLKRP053
00388              PERFORM D030-REPORT-TRANSFER-ERR                     MLKRP053
00389          WHEN ML026-RPT2-MKDOWN                                   MLKRP053
00390          WHEN ML026-RPT2-MKUP                                     MLKRP053
00391              PERFORM D040-REPORT-PRICE-CHANGE-ERR                 MLKRP053
00392          WHEN ML026-RPT2-ADJUSTMENT                               MLKRP053
00393              PERFORM D080-REPORT-ADJUSTMENT-ERR                   MLKRP053
00394          WHEN OTHER                                               MLKRP053
00395              MOVE +4008 TO ABEND-CODE                             MLKRP053
00396              DISPLAY '** ABEND     **'                            MLKRP053
00397              DISPLAY '** PROGRAM = MLKRP053'                      MLKRP053
00398              DISPLAY                                              MLKRP053
00399                    '** PARAGRAPH = C200-PROCESS-EDIT-ERROR-FILE'  MLKRP053
00400              DISPLAY '** TRANS-CDE ' ML026-RPT2-TRANS-CDE         MLKRP053
00401                        ' ON EDIT-ERROR-RECORD IS NOT VALID.'      MLKRP053
00402              DISPLAY '** EDIT-ERROR-RECORD: '                     MLKRP053
00403                      ML026-RPT2-ERR-RECORD                        MLKRP053
00404              CALL 'ILBOABN0'  USING ABEND-CODE                    MLKRP053
00405      END-EVALUATE.                                                MLKRP053
00406                                                                   MLKRP053
00407      PERFORM Z100-READ-EDIT-ERROR-FILE.                           MLKRP053
00408                                                                   MLKRP053
00409                                                                   MLKRP053
00410 ******************************************************************MLKRP053
00411 * REPORT ERRORS THAT OCCURRED IN A PURCHASE TRANSACTION.          MLKRP053
00412 ******************************************************************MLKRP053
00413  D010-REPORT-PURCH-ERR.                                           MLKRP053
00414                                                                   MLKRP053
00415      IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP053
00416          PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP053
00417      END-IF.                                                      MLKRP053
00418                                                                   MLKRP053
00419 *    *------------------------------------------------------------MLKRP053
00420 *    * BUILD AND PRINT THE ERROR DETAIL LINE.                     MLKRP053
00421 *    *------------------------------------------------------------MLKRP053
00422      MOVE ML026-RPT2-TRANS-CDE         TO ED-CD.                  MLKRP053
00423      MOVE ML026-RPT2-STORE-NBR         TO ED-FM-STORE.            MLKRP053
00424      MOVE ML026-RPT2-DEPT-CLASS        TO ED-CLASS.               MLKRP053
00424      MOVE ML026-RPT2-SKU               TO ED-SKU.                 MLKRP053
00425      MOVE ML026-RPT2-DOC-5-NBR         TO ED-DOC-NBR-5.           MLKRP053
00426                                                                   MLKRP053
00427      MOVE ML026-RPT2-FIN-LIAB-MM       TO ED-DATE-MM.             MLKRP053
00428      MOVE ML026-RPT2-FIN-SLASH1        TO ED-DATE-SLASH1.         MLKRP053
00429      MOVE ML026-RPT2-FIN-LIAB-DD       TO ED-DATE-DD.             MLKRP053
00430      MOVE ML026-RPT2-FIN-SLASH2        TO ED-DATE-SLASH2.         MLKRP053
00431      MOVE ML026-RPT2-FIN-LIAB-YY       TO ED-DATE-YY.             MLKRP053
00432                                                                   MLKRP053
00433      MOVE ML026-RPT2-KOHLS-MM          TO ED-KM.                  MLKRP053
00434                                                                   MLKRP053
00435      MOVE ML026-RPT2-AMT-ERR TO ML027-ERR-CDE.                    MLKRP053
00436      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00437          MOVE ML026-RPT2-AMTX          TO ED-AMOUNT-X             MLKRP053
00438      ELSE                                                         MLKRP053
00439          MOVE ML026-RPT2-AMT           TO ED-AMOUNT               MLKRP053
00440      END-IF.                                                      MLKRP053
00441                                                                   MLKRP053
00442      MOVE ML026-RPT2-PO-NBR-ERR TO ML027-ERR-CDE.                 MLKRP053
00443      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00444          MOVE ML026-RPT2-PO-NBRX       TO ED-PO-NO-X              MLKRP053
00445      ELSE                                                         MLKRP053
00446          MOVE ML026-RPT2-PO-NBR        TO ED-PO-NO                MLKRP053
00447      END-IF.                                                      MLKRP053
00448                                                                   MLKRP053
00449      MOVE ML026-RPT2-BATCH-NBR         TO ED-BCH.                 MLKRP053
00450      MOVE ML026-RPT2-VEND-NBR          TO ED-VENDOR.              MLKRP053
00452      MOVE ML026-RPT2-DISC-AMT-ERR TO ML027-ERR-CDE.               MLKRP053
00453      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00454          MOVE ML026-RPT2-DISC-AMTX     TO ED-DISCT-X              MLKRP053
00455      ELSE                                                         MLKRP053
00456          MOVE ML026-RPT2-DISC-AMT      TO ED-DISCT                MLKRP053
00457      END-IF.                                                      MLKRP053
00458                                                                   MLKRP053
00459      MOVE ML026-RPT2-FRGT-AMT-ERR TO ML027-ERR-CDE.               MLKRP053
00460      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00461          MOVE ML026-RPT2-FRGT-AMTX     TO ED-FREIGHT-X            MLKRP053
00462      ELSE                                                         MLKRP053
00463          MOVE ML026-RPT2-FRGT-AMT      TO ED-FREIGHT              MLKRP053
00464      END-IF.                                                      MLKRP053
00465                                                                   MLKRP053
00466      MOVE ML026-RPT2-NET-COST-AMT-ERR TO ML027-ERR-CDE.           MLKRP053
00467      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00468          MOVE ML026-RPT2-NET-COST-AMTX TO ED-NET-COST-X           MLKRP053
00469      ELSE                                                         MLKRP053
00470          MOVE ML026-RPT2-NET-COST-AMT  TO ED-NET-COST             MLKRP053
00471      END-IF.                                                      MLKRP053
00472                                                                   MLKRP053
00473      MOVE ML026-RPT2-RETAIL-AMT-ERR TO ML027-ERR-CDE.             MLKRP053
00474      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00475          MOVE ML026-RPT2-RETAIL-AMTX   TO ED-RETAIL-X             MLKRP053
00476      ELSE                                                         MLKRP053
00477          MOVE ML026-RPT2-RETAIL-AMT    TO ED-RETAIL               MLKRP053
00478      END-IF.                                                      MLKRP053
00479                                                                   MLKRP053
00480      MOVE ML026-RPT2-DR-CR-CDE         TO ED-TR-CD.               MLKRP053
00481                                                                   MLKRP053
00482      MOVE ED-ERROR-DETAIL-LINE TO EDIT-ERROR-REPORT-LINE.         MLKRP053
00483      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00484                                                                   MLKRP053
00485 *    *------------------------------------------------------------MLKRP053
00486 *    * BUILD AND PRINT THE ERROR HIGHLIGHT LINE.                  MLKRP053
00487 *    *------------------------------------------------------------MLKRP053
00488      IF NOT ML026-RPT2-TRANS-CDE-OK                               MLKRP053
00489          MOVE ALL '*' TO EH-CD                                    MLKRP053
00490      END-IF.                                                      MLKRP053
00491      IF NOT ML026-RPT2-STORE-NBR-OK                               MLKRP053
00492          MOVE ALL '*' TO EH-FM-STORE                              MLKRP053
00493      END-IF.                                                      MLKRP053
00494      IF NOT ML026-RPT2-DEPT-CLASS-OK                              MLKRP053
00495          MOVE ALL '*' TO EH-CLASS                                 MLKRP053
00496      END-IF.                                                      MLKRP053
W21731     IF NOT ML026-RPT2-SKU-OK                                     MLKRP053
W21731         MOVE ALL '*' TO EH-SKU                                   MLKRP053
W21731     END-IF.                                                      MLKRP053
00497      IF NOT ML026-RPT2-DOC-5-NBR-OK                               MLKRP053
00498          MOVE ALL '*' TO EH-DOC-NBR                               MLKRP053
00499      END-IF.                                                      MLKRP053
00500      IF NOT ML026-RPT2-FIN-LIAB-DTE-OK                            MLKRP053
00501          MOVE ALL '*' TO EH-DATE                                  MLKRP053
00502      END-IF.                                                      MLKRP053
00503      IF NOT ML026-RPT2-KOHLS-MM-OK                                MLKRP053
00504          MOVE ALL '*' TO EH-KM                                    MLKRP053
00505      END-IF.                                                      MLKRP053
00506      IF NOT ML026-RPT2-AMT-OK                                     MLKRP053
00507          MOVE ALL '*' TO EH-AMOUNT                                MLKRP053
00508      END-IF.                                                      MLKRP053
00509      IF NOT ML026-RPT2-PO-NBR-OK                                  MLKRP053
00510          MOVE ALL '*' TO EH-PO-NO                                 MLKRP053
00511      END-IF.                                                      MLKRP053
00512      IF NOT ML026-RPT2-BATCH-NBR-OK                               MLKRP053
00513          MOVE ALL '*' TO EH-BCH                                   MLKRP053
00514      END-IF.                                                      MLKRP053
00515      IF NOT ML026-RPT2-VEND-NBR-OK                                MLKRP053
00516          MOVE ALL '*' TO EH-VENDOR                                MLKRP053
W26603         MOVE ALL '*' TO EH-ENT-ID                                MLKRP053
00517      END-IF.                                                      MLKRP053
00518      IF NOT ML026-RPT2-DISC-AMT-OK                                MLKRP053
00519          MOVE ALL '*' TO EH-DISCT                                 MLKRP053
00520      END-IF.                                                      MLKRP053
00521      IF NOT ML026-RPT2-FRGT-AMT-OK                                MLKRP053
00522          MOVE ALL '*' TO EH-FREIGHT                               MLKRP053
00523      END-IF.                                                      MLKRP053
00524      IF NOT ML026-RPT2-NET-COST-AMT-OK                            MLKRP053
00525          MOVE ALL '*' TO EH-NET-COST                              MLKRP053
00526      END-IF.                                                      MLKRP053
00527      IF NOT ML026-RPT2-RETAIL-AMT-OK                              MLKRP053
00528          MOVE ALL '*' TO EH-RETAIL                                MLKRP053
00529      END-IF.                                                      MLKRP053
00530      IF NOT ML026-RPT2-DR-CR-CDE-OK                               MLKRP053
00531          MOVE ALL '*' TO EH-TR-CD                                 MLKRP053
00532      END-IF.                                                      MLKRP053
00533                                                                   MLKRP053
00534      MOVE EH-ERROR-HIGHLIGHT-LINE TO EDIT-ERROR-REPORT-LINE.      MLKRP053
00535      MOVE +1                      TO PV-ADVANCE.                  MLKRP053
00536      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00537      MOVE +2                      TO PV-ADVANCE.                  MLKRP053
00538                                                                   MLKRP053
00539                                                                   MLKRP053
00540 ******************************************************************MLKRP053
00541 * REPORT ERRORS THAT OCCURRED IN A SALE TRANSACTION.              MLKRP053
00542 ******************************************************************MLKRP053
00543  D020-REPORT-SALE-ERR.                                            MLKRP053
00544                                                                   MLKRP053
00545      IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP053
00546          PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP053
00547      END-IF.                                                      MLKRP053
00548                                                                   MLKRP053
00549 *    *------------------------------------------------------------MLKRP053
00550 *    * BUILD AND PRINT THE ERROR DETAIL LINE.                     MLKRP053
00551 *    *------------------------------------------------------------MLKRP053
00552      MOVE ML026-RPT2-TRANS-CDE         TO ED-CD.                  MLKRP053
00553      MOVE ML026-RPT2-STORE-NBR         TO ED-FM-STORE.            MLKRP053
00554      MOVE ML026-RPT2-DEPT-CLASS        TO ED-CLASS.               MLKRP053
W21731     MOVE ML026-RPT2-SKU               TO ED-SKU.                 MLKRP053
00555                                                                   MLKRP053
00556      MOVE ML026-RPT2-FIN-LIAB-MM       TO ED-DATE-MM.             MLKRP053
00557      MOVE ML026-RPT2-FIN-SLASH1        TO ED-DATE-SLASH1.         MLKRP053
00558      MOVE ML026-RPT2-FIN-LIAB-DD       TO ED-DATE-DD.             MLKRP053
00559      MOVE ML026-RPT2-FIN-SLASH2        TO ED-DATE-SLASH2.         MLKRP053
00560      MOVE ML026-RPT2-FIN-LIAB-YY       TO ED-DATE-YY.             MLKRP053
00561                                                                   MLKRP053
00562      MOVE ML026-RPT2-AMT-ERR TO ML027-ERR-CDE.                    MLKRP053
00563      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00564          MOVE ML026-RPT2-AMTX          TO ED-AMOUNT-X             MLKRP053
00565      ELSE                                                         MLKRP053
00566          MOVE ML026-RPT2-AMT           TO ED-AMOUNT               MLKRP053
00567      END-IF.                                                      MLKRP053
00568                                                                   MLKRP053
00569      MOVE ED-ERROR-DETAIL-LINE TO EDIT-ERROR-REPORT-LINE.         MLKRP053
00570      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00571                                                                   MLKRP053
00572 *    *------------------------------------------------------------MLKRP053
00573 *    * BUILD AND PRINT THE ERROR HIGHLIGHT LINE.                  MLKRP053
00574 *    *------------------------------------------------------------MLKRP053
00575      IF NOT ML026-RPT2-TRANS-CDE-OK                               MLKRP053
00576          MOVE ALL '*' TO EH-CD                                    MLKRP053
00577      END-IF.                                                      MLKRP053
00578      IF NOT ML026-RPT2-STORE-NBR-OK                               MLKRP053
00579          MOVE ALL '*' TO EH-FM-STORE                              MLKRP053
00580      END-IF.                                                      MLKRP053
00581      IF NOT ML026-RPT2-DEPT-CLASS-OK                              MLKRP053
00582          MOVE ALL '*' TO EH-CLASS                                 MLKRP053
00583      END-IF.                                                      MLKRP053
W21731     IF NOT ML026-RPT2-SKU-OK                                     MLKRP053
W21731         MOVE ALL '*' TO EH-SKU                                   MLKRP053
W21731     END-IF.                                                      MLKRP053
00584      IF NOT ML026-RPT2-FIN-LIAB-DTE-OK                            MLKRP053
00585          MOVE ALL '*' TO EH-DATE                                  MLKRP053
00586      END-IF.                                                      MLKRP053
00587      IF NOT ML026-RPT2-AMT-OK                                     MLKRP053
00588          MOVE ALL '*' TO EH-AMOUNT                                MLKRP053
00589      END-IF.                                                      MLKRP053
00590                                                                   MLKRP053
00591      MOVE EH-ERROR-HIGHLIGHT-LINE TO EDIT-ERROR-REPORT-LINE.      MLKRP053
00592      MOVE +1                      TO PV-ADVANCE.                  MLKRP053
00593      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00594      MOVE +2                      TO PV-ADVANCE.                  MLKRP053
00595                                                                   MLKRP053
00596                                                                   MLKRP053
00597 ******************************************************************MLKRP053
00598 * REPORT ERRORS THAT OCCURRED IN A STORE TRANSFER TRANSACTION.    MLKRP053
00599 ******************************************************************MLKRP053
00600  D030-REPORT-TRANSFER-ERR.                                        MLKRP053
00601                                                                   MLKRP053
00602      IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP053
00603          PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP053
00604      END-IF.                                                      MLKRP053
00605                                                                   MLKRP053
00606 *    *------------------------------------------------------------MLKRP053
00607 *    * BUILD AND PRINT THE ERROR DETAIL LINE.                     MLKRP053
00608 *    *------------------------------------------------------------MLKRP053
00609      MOVE ML026-RPT2-TRANS-CDE         TO ED-CD.                  MLKRP053
00610      MOVE ML026-RPT2-STORE-NBR         TO ED-FM-STORE.            MLKRP053
00611      MOVE ML026-RPT2-REL-STORE-NBR     TO ED-TO-STORE.            MLKRP053
00612      MOVE ML026-RPT2-DEPT-CLASS        TO ED-CLASS.               MLKRP053
W21731     MOVE ML026-RPT2-SKU               TO ED-SKU.                 MLKRP053
00613                                                                   MLKRP053
           IF  ML026-RPT2-DOC-5-NBR = 'FAST '                           MLKRP053
               IF  ML026-RPT2-XFER-DOC-STORE NUMERIC AND                        
                   ML026-RPT2-XFER-DOC-SEQ NUMERIC                              
W28545             MOVE ML026-RPT2-XFER-DOC-STORE (2:4) TO                      
                        ED-FAST-DOC-NBR-11 (1:4)                            MLKU
                   MOVE '-'                      TO                     MLRD005I
                        ED-FAST-DOC-NBR-11 (5:1)                            MLKU
                   MOVE ML026-RPT2-XFER-DOC-SEQ  TO                             
                        ED-FAST-DOC-NBR-11 (6:6)                            MLKU
               ELSE                                                             
                   MOVE ML026-RPT2-XFER-DOC-NBR (1:4) TO                MLRD005I
                        ED-FAST-DOC-NBR-11 (1:4)                            MLKU
                   MOVE '-'                      TO                     MLRD005I
                        ED-FAST-DOC-NBR-11 (5:1)                            MLKU
                   MOVE ML026-RPT2-XFER-DOC-NBR (5:6) TO                MLRD005I
                        ED-FAST-DOC-NBR-11 (6:6)                            MLKU
               END-IF                                                           
           ELSE                                                                 
               MOVE ML026-RPT2-DOC-5-NBR     TO ED-DOC-NBR-5                MLKR
           END-IF                                                               
                                                                                
00615      MOVE ML026-RPT2-FIN-LIAB-MM       TO ED-DATE-MM.             MLKRP053
00616      MOVE ML026-RPT2-FIN-SLASH1        TO ED-DATE-SLASH1.         MLKRP053
00617      MOVE ML026-RPT2-FIN-LIAB-DD       TO ED-DATE-DD.             MLKRP053
00618      MOVE ML026-RPT2-FIN-SLASH2        TO ED-DATE-SLASH2.         MLKRP053
00619      MOVE ML026-RPT2-FIN-LIAB-YY       TO ED-DATE-YY.             MLKRP053
00620                                                                   MLKRP053
00621      MOVE ML026-RPT2-KOHLS-MM          TO ED-KM.                  MLKRP053
00622                                                                   MLKRP053
00623      MOVE ML026-RPT2-QTY-ERR TO ML027-ERR-CDE.                    MLKRP053
00624      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00625          MOVE ML026-RPT2-QTYX          TO ED-UNTS-X               MLKRP053
00626      ELSE                                                         MLKRP053
00627          MOVE ML026-RPT2-QTY           TO ED-UNTS                 MLKRP053
00628      END-IF.                                                      MLKRP053
00629                                                                   MLKRP053
00630      MOVE ML026-RPT2-AMT-ERR TO ML027-ERR-CDE.                    MLKRP053
00631      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00632          MOVE ML026-RPT2-AMTX          TO ED-AMOUNT-X             MLKRP053
00633      ELSE                                                         MLKRP053
00634          MOVE ML026-RPT2-AMT           TO ED-AMOUNT               MLKRP053
00635      END-IF.                                                      MLKRP053
00636                                                                   MLKRP053
00637      MOVE ML026-RPT2-OLD-PR-AMT-ERR    TO ML027-ERR-CDE.          MLKRP053
00638      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00639          MOVE ML026-RPT2-OLD-PR-AMTX   TO ED-OLD-PRICE-X          MLKRP053
00640      ELSE                                                         MLKRP053
00641          MOVE ML026-RPT2-OLD-PR-AMT    TO ED-OLD-PRICE            MLKRP053
00642      END-IF.                                                      MLKRP053
00643                                                                   MLKRP053
00644      MOVE ML026-RPT2-PO-NBR-ERR TO ML027-ERR-CDE.                 MLKRP053
00645      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00646          MOVE ML026-RPT2-PO-NBRX       TO ED-PO-NO-X              MLKRP053
00647      ELSE                                                         MLKRP053
00648          MOVE ML026-RPT2-PO-NBR        TO ED-PO-NO                MLKRP053
00649      END-IF.                                                      MLKRP053
00650                                                                   MLKRP053
00651      MOVE ML026-RPT2-BATCH-NBR         TO ED-BCH.                 MLKRP053
00652                                                                   MLKRP053
00653      MOVE ED-ERROR-DETAIL-LINE TO EDIT-ERROR-REPORT-LINE.         MLKRP053
00654      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00655                                                                   MLKRP053
00656 *    *------------------------------------------------------------MLKRP053
00657 *    * BUILD AND PRINT THE ERROR HIGHLIGHT LINE.                  MLKRP053
00658 *    *------------------------------------------------------------MLKRP053
00659      IF NOT ML026-RPT2-TRANS-CDE-OK                               MLKRP053
00660          MOVE ALL '*' TO EH-CD                                    MLKRP053
00661      END-IF.                                                      MLKRP053
00662      IF NOT ML026-RPT2-STORE-NBR-OK                               MLKRP053
00663          MOVE ALL '*' TO EH-FM-STORE                              MLKRP053
00664      END-IF.                                                      MLKRP053
00665      IF NOT ML026-RPT2-REL-STORE-NBR-OK                           MLKRP053
00666          MOVE ALL '*' TO EH-TO-STORE                              MLKRP053
00667      END-IF.                                                      MLKRP053
00668      IF NOT ML026-RPT2-DEPT-CLASS-OK                              MLKRP053
00669          MOVE ALL '*' TO EH-CLASS                                 MLKRP053
00670      END-IF.                                                      MLKRP053
W21731     IF NOT ML026-RPT2-SKU-OK                                     MLKRP053
W21731         MOVE ALL '*' TO EH-SKU                                   MLKRP053
W21731     END-IF.                                                      MLKRP053
00671      IF NOT ML026-RPT2-DOC-5-NBR-OK                               MLKRP053
00672          MOVE ALL '*' TO EH-DOC-NBR                               MLKRP053
00673      END-IF.                                                      MLKRP053
00674      IF NOT ML026-RPT2-FIN-LIAB-DTE-OK                            MLKRP053
00675          MOVE ALL '*' TO EH-DATE                                  MLKRP053
00676      END-IF.                                                      MLKRP053
00677      IF NOT ML026-RPT2-KOHLS-MM-OK                                MLKRP053
00678          MOVE ALL '*' TO EH-KM                                    MLKRP053
00679      END-IF.                                                      MLKRP053
00680      IF NOT ML026-RPT2-QTY-OK                                     MLKRP053
00681          MOVE ALL '*' TO EH-UNTS                                  MLKRP053
00682      END-IF.                                                      MLKRP053
00683      IF NOT ML026-RPT2-AMT-OK                                     MLKRP053
00684          MOVE ALL '*' TO EH-AMOUNT                                MLKRP053
00685      END-IF.                                                      MLKRP053
00686      IF NOT ML026-RPT2-OLD-PR-AMT-OK                              MLKRP053
00687          MOVE ALL '*' TO EH-OLD-PRICE                             MLKRP053
00688      END-IF.                                                      MLKRP053
00689      IF NOT ML026-RPT2-PO-NBR-OK                                  MLKRP053
00690          MOVE ALL '*' TO EH-PO-NO                                 MLKRP053
00691      END-IF.                                                      MLKRP053
00692      IF NOT ML026-RPT2-BATCH-NBR-OK                               MLKRP053
00693          MOVE ALL '*' TO EH-BCH                                   MLKRP053
00694      END-IF.                                                      MLKRP053
00695                                                                   MLKRP053
00696      MOVE EH-ERROR-HIGHLIGHT-LINE TO EDIT-ERROR-REPORT-LINE.      MLKRP053
00697      MOVE +1                      TO PV-ADVANCE.                  MLKRP053
00698      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00699      MOVE +2                      TO PV-ADVANCE.                  MLKRP053
00700                                                                   MLKRP053
00701                                                                   MLKRP053
00702 ******************************************************************MLKRP053
00703 * REPORT ERRORS THAT OCCURRED IN A PRICE CHANGE (MARKUP OR        MLKRP053
00704 * MARKDOWN) TRANSACTION.                                          MLKRP053
00705 ******************************************************************MLKRP053
00706  D040-REPORT-PRICE-CHANGE-ERR.                                    MLKRP053
00707                                                                   MLKRP053
00708      IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP053
00709          PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP053
00710      END-IF.                                                      MLKRP053
00711                                                                   MLKRP053
00712 *    *------------------------------------------------------------MLKRP053
00713 *    * BUILD AND PRINT THE ERROR DETAIL LINE.                     MLKRP053
00714 *    *------------------------------------------------------------MLKRP053
00715      MOVE ML026-RPT2-TRANS-CDE         TO ED-CD.                  MLKRP053
00716      MOVE ML026-RPT2-STORE-NBR         TO ED-FM-STORE.            MLKRP053
00717      MOVE ML026-RPT2-DEPT-CLASS        TO ED-CLASS.               MLKRP053
W21731     MOVE ML026-RPT2-SKU               TO ED-SKU.                 MLKRP053
00718      MOVE ML026-RPT2-DOC-7-NBR         TO ED-DOC-NBR-7.           MLKRP053
00719      MOVE ML026-RPT2-FIN-LIAB-MM       TO ED-DATE-MM.             MLKRP053
00720      MOVE ML026-RPT2-FIN-SLASH1        TO ED-DATE-SLASH1.         MLKRP053
00721      MOVE ML026-RPT2-FIN-LIAB-DD       TO ED-DATE-DD.             MLKRP053
00722      MOVE ML026-RPT2-FIN-SLASH2        TO ED-DATE-SLASH2.         MLKRP053
00723      MOVE ML026-RPT2-FIN-LIAB-YY       TO ED-DATE-YY.             MLKRP053
00724                                                                   MLKRP053
00725      MOVE ML026-RPT2-KOHLS-MM          TO ED-KM.                  MLKRP053
00726                                                                   MLKRP053
00727      MOVE ML026-RPT2-QTY-ERR TO ML027-ERR-CDE.                    MLKRP053
00728      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00729          MOVE ML026-RPT2-QTYX          TO ED-UNTS-X               MLKRP053
00730      ELSE                                                         MLKRP053
00731          MOVE ML026-RPT2-QTY           TO ED-UNTS                 MLKRP053
00732      END-IF.                                                      MLKRP053
00733                                                                   MLKRP053
00734      MOVE ML026-RPT2-AMT-ERR TO ML027-ERR-CDE.                    MLKRP053
00735      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00736          MOVE ML026-RPT2-AMTX          TO ED-AMOUNT-X             MLKRP053
00737      ELSE                                                         MLKRP053
00738          MOVE ML026-RPT2-AMT           TO ED-AMOUNT               MLKRP053
00739      END-IF.                                                      MLKRP053
00740                                                                   MLKRP053
00741      MOVE ML026-RPT2-OLD-PR-AMT-ERR    TO ML027-ERR-CDE.          MLKRP053
00742      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00743          MOVE ML026-RPT2-OLD-PR-AMTX   TO ED-OLD-PRICE-X          MLKRP053
00744      ELSE                                                         MLKRP053
00745          MOVE ML026-RPT2-OLD-PR-AMT    TO ED-OLD-PRICE            MLKRP053
00746      END-IF.                                                      MLKRP053
00747                                                                   MLKRP053
00748      MOVE ML026-RPT2-NEW-PR-AMT-ERR    TO ML027-ERR-CDE.          MLKRP053
00749      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00750          MOVE ML026-RPT2-NEW-PR-AMTX   TO ED-NEW-PRICE-X          MLKRP053
00751      ELSE                                                         MLKRP053
00752          MOVE ML026-RPT2-NEW-PR-AMT    TO ED-NEW-PRICE            MLKRP053
00753      END-IF.                                                      MLKRP053
00754                                                                   MLKRP053
00755      MOVE ML026-RPT2-RSN-CDE           TO ED-RS.                  MLKRP053
00756      MOVE ML026-RPT2-BATCH-NBR         TO ED-BCH.                 MLKRP053
00757                                                                   MLKRP053
00758      MOVE ED-ERROR-DETAIL-LINE TO EDIT-ERROR-REPORT-LINE.         MLKRP053
00759      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00760                                                                   MLKRP053
00761 *    *------------------------------------------------------------MLKRP053
00762 *    * BUILD AND PRINT THE ERROR HIGHLIGHT LINE.                  MLKRP053
00763 *    *------------------------------------------------------------MLKRP053
00764      IF NOT ML026-RPT2-TRANS-CDE-OK                               MLKRP053
00765          MOVE ALL '*' TO EH-CD                                    MLKRP053
00766      END-IF.                                                      MLKRP053
00767      IF NOT ML026-RPT2-STORE-NBR-OK                               MLKRP053
00768          MOVE ALL '*' TO EH-FM-STORE                              MLKRP053
00769      END-IF.                                                      MLKRP053
00770      IF NOT ML026-RPT2-DEPT-CLASS-OK                              MLKRP053
00771          MOVE ALL '*' TO EH-CLASS                                 MLKRP053
00772      END-IF.                                                      MLKRP053
W21731     IF NOT ML026-RPT2-SKU-OK                                     MLKRP053
W21731         MOVE ALL '*' TO EH-SKU                                   MLKRP053
W21731     END-IF.                                                      MLKRP053
00773      IF NOT ML026-RPT2-DOC-5-NBR-OK                               MLKRP053
00774          MOVE ALL '*' TO EH-DOC-NBR                               MLKRP053
00775      END-IF.                                                      MLKRP053
00776      IF NOT ML026-RPT2-FIN-LIAB-DTE-OK                            MLKRP053
00777          MOVE ALL '*' TO EH-DATE                                  MLKRP053
00778      END-IF.                                                      MLKRP053
00779      IF NOT ML026-RPT2-KOHLS-MM-OK                                MLKRP053
00780          MOVE ALL '*' TO EH-KM                                    MLKRP053
00781      END-IF.                                                      MLKRP053
00782      IF NOT ML026-RPT2-QTY-OK                                     MLKRP053
00783          MOVE ALL '*' TO EH-UNTS                                  MLKRP053
00784      END-IF.                                                      MLKRP053
00785      IF NOT ML026-RPT2-AMT-OK                                     MLKRP053
00786          MOVE ALL '*' TO EH-AMOUNT                                MLKRP053
00787      END-IF.                                                      MLKRP053
00788      IF NOT ML026-RPT2-OLD-PR-AMT-OK                              MLKRP053
00789          MOVE ALL '*' TO EH-OLD-PRICE                             MLKRP053
00790      END-IF.                                                      MLKRP053
00791      IF NOT ML026-RPT2-NEW-PR-AMT-OK                              MLKRP053
00792          MOVE ALL '*' TO EH-NEW-PRICE                             MLKRP053
00793      END-IF.                                                      MLKRP053
00794      IF NOT ML026-RPT2-RSN-CDE-OK                                 MLKRP053
00795          MOVE ALL '*' TO EH-RS                                    MLKRP053
00796      END-IF.                                                      MLKRP053
00797      IF NOT ML026-RPT2-BATCH-NBR-OK                               MLKRP053
00798          MOVE ALL '*' TO EH-BCH                                   MLKRP053
00799      END-IF.                                                      MLKRP053
00800                                                                   MLKRP053
00801      MOVE EH-ERROR-HIGHLIGHT-LINE TO EDIT-ERROR-REPORT-LINE.      MLKRP053
00802      MOVE +1                      TO PV-ADVANCE.                  MLKRP053
00803      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00804      MOVE +2                      TO PV-ADVANCE.                  MLKRP053
00805                                                                   MLKRP053
00806                                                                   MLKRP053
00807 ******************************************************************MLKRP053
00808 * REPORT ERRORS THAT OCCURRED IN AN ADJUSTMENT TRANSACITON.       MLKRP053
00809 ******************************************************************MLKRP053
00810  D080-REPORT-ADJUSTMENT-ERR.                                      MLKRP053
00811                                                                   MLKRP053
00812      IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                         MLKRP053
00813          PERFORM Z300-PRINT-RPT-HEADINGS                          MLKRP053
00814      END-IF.                                                      MLKRP053
00815                                                                   MLKRP053
00816 *    *------------------------------------------------------------MLKRP053
00817 *    * BUILD AND PRINT THE ERROR DETAIL LINE.                     MLKRP053
00818 *    *------------------------------------------------------------MLKRP053
00819      MOVE ML026-RPT2-TRANS-CDE         TO ED-CD.                  MLKRP053
00820      MOVE ML026-RPT2-STORE-NBR         TO ED-FM-STORE.            MLKRP053
00821      MOVE ML026-RPT2-DEPT-CLASS        TO ED-CLASS.               MLKRP053
W21731     MOVE ML026-RPT2-SKU               TO ED-SKU.                 MLKRP053
00822      MOVE ML026-RPT2-FIN-LIAB-MM       TO ED-DATE-MM.             MLKRP053
00823      MOVE ML026-RPT2-FIN-SLASH1        TO ED-DATE-SLASH1.         MLKRP053
00824      MOVE ML026-RPT2-FIN-LIAB-DD       TO ED-DATE-DD.             MLKRP053
00825      MOVE ML026-RPT2-FIN-SLASH2        TO ED-DATE-SLASH2.         MLKRP053
00826      MOVE ML026-RPT2-FIN-LIAB-YY       TO ED-DATE-YY.             MLKRP053
00827      MOVE ML026-RPT2-KOHLS-MM          TO ED-KM.                  MLKRP053
00828      MOVE ML026-RPT2-RSN-CDE           TO ED-RS.                  MLKRP053
00829                                                                   MLKRP053
W20445     MOVE ML026-RPT2-ENT-ID            TO PV-EDIT-ENT-ID.         MLKRP053
W20445     MOVE PV-EDIT-ENT-ID               TO ED-ENT-ID.              MLKRP053
W20445                                                                  MLKRP053
00830      MOVE ML026-RPT2-NET-COST-AMT-ERR TO ML027-ERR-CDE.           MLKRP053
00831      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00832          MOVE ML026-RPT2-NET-COST-AMTX TO ED-NET-COST-X           MLKRP053
00833      ELSE                                                         MLKRP053
00834          MOVE ML026-RPT2-NET-COST-AMT  TO ED-NET-COST             MLKRP053
00835      END-IF.                                                      MLKRP053
00836                                                                   MLKRP053
00837      MOVE ML026-RPT2-RETAIL-AMT-ERR    TO ML027-ERR-CDE.          MLKRP053
00838      IF ML027-ERR-NON-NUMERIC                                     MLKRP053
00839          MOVE ML026-RPT2-RETAIL-AMTX   TO ED-RETAIL-X             MLKRP053
00840      ELSE                                                         MLKRP053
00841          MOVE ML026-RPT2-RETAIL-AMT    TO ED-RETAIL               MLKRP053
00842      END-IF.                                                      MLKRP053
00843                                                                   MLKRP053
00844      MOVE ED-ERROR-DETAIL-LINE TO EDIT-ERROR-REPORT-LINE.         MLKRP053
00845      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00846                                                                   MLKRP053
00847 *    *------------------------------------------------------------MLKRP053
00848 *    * BUILD AND PRINT THE ERROR HIGHLIGHT LINE.                  MLKRP053
00849 *    *------------------------------------------------------------MLKRP053
00850      IF NOT ML026-RPT2-TRANS-CDE-OK                               MLKRP053
00851          MOVE ALL '*' TO EH-CD                                    MLKRP053
00852      END-IF.                                                      MLKRP053
00853      IF NOT ML026-RPT2-STORE-NBR-OK                               MLKRP053
00854          MOVE ALL '*' TO EH-FM-STORE                              MLKRP053
00855      END-IF.                                                      MLKRP053
00856      IF NOT ML026-RPT2-DEPT-CLASS-OK                              MLKRP053
00857          MOVE ALL '*' TO EH-CLASS                                 MLKRP053
00858      END-IF.                                                      MLKRP053
W21731     IF NOT ML026-RPT2-SKU-OK                                     MLKRP053
W21731         MOVE ALL '*' TO EH-SKU                                   MLKRP053
W21731     END-IF.                                                      MLKRP053
00859      IF NOT ML026-RPT2-FIN-LIAB-DTE-OK                            MLKRP053
00860          MOVE ALL '*' TO EH-DATE                                  MLKRP053
00861      END-IF.                                                      MLKRP053
00862      IF NOT ML026-RPT2-KOHLS-MM-OK                                MLKRP053
00863          MOVE ALL '*' TO EH-KM                                    MLKRP053
00864      END-IF.                                                      MLKRP053
00865      IF NOT ML026-RPT2-RSN-CDE-OK                                 MLKRP053
00866          MOVE ALL '*' TO EH-RS                                    MLKRP053
00867      END-IF.                                                      MLKRP053
00868      IF NOT ML026-RPT2-NET-COST-AMT-OK                            MLKRP053
00869          MOVE ALL '*' TO EH-NET-COST                              MLKRP053
00870      END-IF.                                                      MLKRP053
00871      IF NOT ML026-RPT2-RETAIL-AMT-OK                              MLKRP053
00872          MOVE ALL '*' TO EH-RETAIL                                MLKRP053
00873      END-IF.                                                      MLKRP053
00874                                                                   MLKRP053
00875      MOVE EH-ERROR-HIGHLIGHT-LINE TO EDIT-ERROR-REPORT-LINE.      MLKRP053
00876      MOVE +1                      TO PV-ADVANCE.                  MLKRP053
00877      PERFORM Z200-WRITE-REPORT-LINE.                              MLKRP053
00878      MOVE +2                      TO PV-ADVANCE.                  MLKRP053
00879                                                                   MLKRP053
00880                                                                   MLKRP053
00881  Z100-READ-EDIT-ERROR-FILE.                                       MLKRP053
00882                                                                   MLKRP053
00883      READ EDIT-ERROR-FILE                                         MLKRP053
00884          INTO ML026-RPT2-RECORD                                   MLKRP053
00885              AT END SET SW-END-OF-EDIT-ERROR-FILE TO TRUE.        MLKRP053
00886                                                                   MLKRP053
00887                                                                   MLKRP053
00888  Z200-WRITE-REPORT-LINE.                                          MLKRP053
00889                                                                   MLKRP053
00890      WRITE EDIT-ERROR-REPORT-LINE                                 MLKRP053
00891          AFTER ADVANCING PV-ADVANCE.                              MLKRP053
00892      ADD PV-ADVANCE TO PV-LINE-COUNT.                             MLKRP053
00893                                                                   MLKRP053
00894                                                                   MLKRP053
00895 ******************************************************************MLKRP053
00896 * PRINT THE REPORT HEADING LINES.                                 MLKRP053
00897 ******************************************************************MLKRP053
00898  Z300-PRINT-RPT-HEADINGS.                                         MLKRP053
00899                                                                   MLKRP053
00900      ADD 1 TO PV-PAGE-COUNT.                                      MLKRP053
00901      MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    MLKRP053
00902      MOVE DP132O-STANDRD-HEADER-132-COLS TO EDIT-ERROR-REPORT-LINEMLKRP053
00903      WRITE EDIT-ERROR-REPORT-LINE                                 MLKRP053
00904          AFTER ADVANCING PAGE.                                    MLKRP053
00905      MOVE +1 TO PV-LINE-COUNT.                                    MLKRP053
00906                                                                   MLKRP053
00907      WRITE EDIT-ERROR-REPORT-LINE FROM HD-WE-WEEK-END-HEADING     MLKRP053
00908          AFTER ADVANCING 2 LINES.                                 MLKRP053
00909      WRITE EDIT-ERROR-REPORT-LINE FROM HD-CL1-COLUMN-HEADING-1    MLKRP053
00910          AFTER ADVANCING 2 LINES.                                 MLKRP053
00911      WRITE EDIT-ERROR-REPORT-LINE FROM HD-CL2-COLUMN-HEADING-2    MLKRP053
00912          AFTER ADVANCING 1 LINES.                                 MLKRP053
00913      ADD +5 TO PV-LINE-COUNT.                                     MLKRP053
00914      MOVE 3  TO PV-ADVANCE.                                       MLKRP053
00915                                                                   MLKRP053
00916                                                                   MLKRP053
