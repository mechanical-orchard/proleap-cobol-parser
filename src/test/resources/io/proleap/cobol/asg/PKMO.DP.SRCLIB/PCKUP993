000100 TITLE 'DELETE OLD IT FROM PCT_TRN_CLSN_DTL TABLE '.                      
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    PCKUP993.                                                 
000400 AUTHOR.        DENISE HAHN.                                              
000500 DATE-WRITTEN.  JANUARY, 2015.                                            
000600                                                                          
000610******************************************************************        
000621*                                                                *        
000622* THIS PROGRAM WILL USE THE EXTRACT FILE CREATED BY PCKUT987 OF  *        
000630* OLD IT FROM PCT_TRN_CLSN_DTL THAT HAVE NOT PURGED FORM THE     *        
000640* TABLES.  DELETE THE RECORDS                                    *        
000650* EXTRACT IS BASED ON LAST UPDATE DATE (PARM TRNOLD)             *        
000651*                                                                *        
000660******************************************************************        
000700                                                                          
000800 ENVIRONMENT DIVISION.                                                    
000900 INPUT-OUTPUT SECTION.                                                    
001000 FILE-CONTROL.                                                            
001300                                                                          
001310     SELECT IN-TRANSIT-EXTRACT-FILE                                       
001320         ASSIGN TO INP01.                                                 
001330                                                                          
001400 DATA DIVISION.                                                           
001500 FILE SECTION.                                                            
001600                                                                          
002410 FD  IN-TRANSIT-EXTRACT-FILE                                              
002420     RECORDING MODE IS F                                                  
002430     LABEL RECORDS ARE STANDARD                                           
002440     BLOCK CONTAINS 0 RECORDS.                                            
002450                                                                          
002460 01  IN-TRANSIT-RECORD                   PIC  X(50).                      
002470                                                                          
002500 WORKING-STORAGE SECTION.                                                 
002600                                                                          
003000 01  ABEND-CODE                          PIC S9(04)  VALUE ZERO           
003100                                                     COMP SYNC.           
003200 01  PV-PROGRAM-SWITCHES.                                                 
003300     05  PS-EOF-INPUT-SW                 PIC  X(01)  VALUE 'N'.           
003301         88 PS-EOF-INPUT                             VALUE 'Y'.           
003302         88 PS-EOF-INPUT-NO                          VALUE 'N'.           
003303                                                                          
003304 01  PV-PROGRAM-CONSTANTS.                                                
003305     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE 'PCKUP993'.          
003308                                                                          
003310 01  PV-PROGRAM-VARIABLES.                                                
003320     05  PV-ABEND-PARAGRAPH              PIC  X(30)  VALUE SPACE.         
003400     05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(30)  VALUE SPACE.         
003410     05  PV-SQL-CODE                     PIC -999.                        
003500                                                                          
004300     05 PV-COMMIT-COUNT                  PIC S9(09)  COMP-3               
004301                                                     VALUE ZERO.          
004302     05 PV-EXTRACT-COUNT                 PIC S9(09)  COMP-3               
004303                                                     VALUE ZERO.          
004310     05 PV-DELETE-COUNT                  PIC S9(09)  COMP-3               
004311                                                     VALUE ZERO.          
004320     05 PV-NOT-FND-COUNT                 PIC S9(09)  COMP-3               
004400                                                     VALUE ZERO.          
004401     05 PV-TOTAL-SELECT                  PIC S9(09)  COMP-3               
004402                                                     VALUE ZERO.          
004403     05 PV-SELECT-COUNT                 PIC S9(09)  COMP.                 
004500                                                                          
004900     05 PV-DISP-SQL                      PIC -999.                        
004901     05 PV-DISP-EXTR-CNT                 PIC ZZZ,ZZZ,ZZ9.                 
004902     05 PV-DISP-DELETE-CNT               PIC ZZZ,ZZZ,ZZ9.                 
004903     05 PV-DISP-NOTFND-CNT               PIC ZZZ,ZZZ,ZZ9.                 
004904     05 PV-DISP-TOTAL-SELECT             PIC ZZZ,ZZZ,ZZ9.                 
004905                                                                          
004910     05 PV-PURGE-DAYS                    PIC S9(9) COMP.                  
004920     05 PV-CURRENT-DATE                  PIC X(10).                       
004930     05 PV-PURGE-DATE                    PIC X(10).                       
005000                                                                          
005100*                                                                         
010620* IN-TRANSIT DETAIL TABLE                                                 
010630*                                                                         
010640     EXEC SQL                                                             
010650          INCLUDE PCT00015                                                
010660     END-EXEC.                                                            
010670                                                                          
010700*                                                                         
010800* PARM TABLE                                                              
010900*                                                                         
011000     EXEC SQL                                                             
011100          INCLUDE TKRPRPM                                                 
011200     END-EXEC.                                                            
011231                                                                          
011320     COPY DPWS004.                                                        
011321                                                                          
011322     COPY PCRD080.                                                        
011323                                                                          
011330     COPY DPWS004.                                                        
011400                                                                          
011500     EXEC SQL                                                             
011600         INCLUDE SQLCA                                                    
011700     END-EXEC.                                                            
011800     COPY SQLCA2.                                                         
011900/                                                                         
012000 PROCEDURE DIVISION.                                                      
012100                                                                          
012200     PERFORM 1000-INITIALIZE.                                             
012300                                                                          
012400     PERFORM 2000-PROCESS                                                 
012500             UNTIL PS-EOF-INPUT.                                          
012600                                                                          
012710     PERFORM 9000-QUIT.                                                   
012800                                                                          
012900     GOBACK.                                                              
013000                                                                          
013100 1000-INITIALIZE.                                                         
013200                                                                          
013201     OPEN INPUT IN-TRANSIT-EXTRACT-FILE.                                  
013203                                                                          
013214     DISPLAY ' '.                                                         
013215     DISPLAY '*****************************************'.                 
013216     DISPLAY '*                                       *'.                 
013217     DISPLAY '* PCKUP993 - DELETE PCT_TRN_CLSN_DTL    *'.                 
013218     DISPLAY '*                                       *'.                 
013230                                                                          
014610     PERFORM 7000-READ-INPUT.                                             
014650                                                                          
014651     DISPLAY '* PURGE DATE:  '  PC080-PURGE-DATE.                         
014652     DISPLAY '*                                       *'.                 
014653     DISPLAY '*                                       *'.                 
014660                                                                          
014700 2000-PROCESS.                                                            
014710                                                                          
015110     PERFORM 8000-DELETE-DETAIL.                                          
017823                                                                          
017824     PERFORM 7000-READ-INPUT.                                             
017825                                                                          
017826                                                                          
017827 7000-READ-INPUT.                                                         
017828                                                                          
017829      READ IN-TRANSIT-EXTRACT-FILE                                        
017830      INTO PC080-INTRNST-PURGE-REC                                        
017831        AT END                                                            
017832           SET PS-EOF-INPUT TO TRUE                                       
017833        NOT AT END                                                        
017834           ADD  1  TO  PV-EXTRACT-COUNT                                   
017836      END-READ.                                                           
017850                                                                          
017900                                                                          
033210 8000-DELETE-DETAIL.                                                      
033211                                                                          
033212      MOVE ZEROES                TO PCT00015-INTR-UPC-ID                  
033213                                    PCT00015-LOC-NBR.                     
033214      MOVE PC080-INTR-UPC-ID-KEY TO PCT00015-INTR-UPC-ID.                 
033215      MOVE PC080-LOC-NBR         TO PCT00015-LOC-NBR.                     
033216                                                                          
033217***   EXEC SQL                                                            
033218***        SELECT COUNT(*)                                                
033219***          INTO :PV-SELECT-COUNT                                        
033220***          FROM PCT_TRN_CLSN_DTL                                        
033221***        WHERE INTR_UPC_ID        =  :PCT00015-INTR-UPC-ID              
033222***          AND LOC_NBR            =  :PCT00015-LOC-NBR                  
033223***          AND TRN_CLSN_CDE       =  :PC080-TRN-CLSN-CDE                
033224***          AND DATE(IN_TRNST_TRN_TMST)                                  
033225***                                <=  :PC080-PURGE-DATE                  
033226***        WITH UR                                                        
033230***   END-EXEC.                                                           
033231                                                                          
033232      EXEC SQL                                                            
033233           DELETE                                                         
033234             FROM PCT_TRN_CLSN_DTL                                        
033235           WHERE INTR_UPC_ID        =  :PCT00015-INTR-UPC-ID              
033236             AND LOC_NBR            =  :PCT00015-LOC-NBR                  
033237             AND TRN_CLSN_CDE       =  :PC080-TRN-CLSN-CDE                
033238             AND DATE(IN_TRNST_TRN_TMST)                                  
033239                                   <=  :PC080-PURGE-DATE                  
033240      END-EXEC.                                                           
033241                                                                          
033242     EVALUATE TRUE                                                        
033243         WHEN SQLCODE = ZERO                                              
033244              ADD  SQLERRD(3)            TO PV-DELETE-COUNT               
033245              ADD  1                     TO PV-COMMIT-COUNT               
033246***           ADD  PV-SELECT-COUNT       TO PV-TOTAL-SELECT               
033247                                                                          
033248              DISPLAY ' UPC ID: '  PC080-INTR-UPC-ID                      
033249                      ' LOC_NBR: ' PC080-LOC-NBR                          
033250                                                                          
033251              IF  PV-COMMIT-COUNT > 10                                    
033252                  PERFORM 8010-COMMIT                                     
033253                  MOVE ZEROES  TO  PV-COMMIT-COUNT                        
033254              END-IF                                                      
033260                                                                          
033280         WHEN SQLCODE = +100                                              
033281              ADD  1                     TO PV-NOT-FND-COUNT              
033282                                                                          
033283         WHEN OTHER                                                       
033284             MOVE SQLCODE                TO PV-DISP-SQL                   
033285             DISPLAY '**************************************'             
033286             DISPLAY 'ERROR ISSUED ON                       '             
033287             DISPLAY ' DELETE INTRANSIT DELETE              '             
033288             DISPLAY 'SQLCODE: ' PV-DISP-SQL                              
033289             DISPLAY 'UPC ID.: ' PC080-INTR-UPC-ID                        
033290             DISPLAY 'LOC NBR: ' PC080-LOC-NBR                            
033291             DISPLAY 'CODE...: ' PC080-TRN-CLSN-CDE                       
033292             DISPLAY 'DATE...: ' PC080-INTR-UPC-ID                        
033293             DISPLAY '**************************************'             
033294             PERFORM 9100-SQL-ABEND                                       
033295                                                                          
033296     END-EVALUATE.                                                        
033297                                                                          
033298 8010-COMMIT.                                                             
033299                                                                          
033300      EXEC SQL                                                            
033301         COMMIT                                                           
033302      END-EXEC.                                                           
033310                                                                          
033311     MOVE PV-EXTRACT-COUNT            TO PV-DISP-EXTR-CNT.                
033312     MOVE PV-DELETE-COUNT             TO PV-DISP-DELETE-CNT.              
033313     MOVE PV-NOT-FND-COUNT            TO PV-DISP-NOTFND-CNT.              
033314     MOVE PV-TOTAL-SELECT             TO PV-DISP-TOTAL-SELECT.            
033315                                                                          
033316     DISPLAY '* EXTRACT: '    PV-DISP-EXTR-CNT                            
033317             '* DELETE: '     PV-DISP-DELETE-CNT                          
033318             '* NOT FOUND: '  PV-DISP-NOTFND-CNT                          
033319             '* SELECT: '     PV-DISP-TOTAL-SELECT.                       
033320                                                                          
033400 9000-QUIT.                                                               
033500                                                                          
033600     MOVE PV-EXTRACT-COUNT            TO PV-DISP-EXTR-CNT.                
033800     MOVE PV-DELETE-COUNT             TO PV-DISP-DELETE-CNT.              
033900     MOVE PV-NOT-FND-COUNT            TO PV-DISP-NOTFND-CNT.              
034000     MOVE PV-TOTAL-SELECT             TO PV-DISP-TOTAL-SELECT.            
034004                                                                          
034020     DISPLAY '*                                       *'.                 
034021     DISPLAY '* EXTRACT-COUNT..: ' PV-DISP-EXTR-CNT.                      
034022     DISPLAY '* DELETE COUNT...: ' PV-DISP-DELETE-CNT.                    
034026     DISPLAY '* NOT FOUND COUNT: ' PV-DISP-NOTFND-CNT.                    
034027     DISPLAY '* SELECT COUNT...: ' PV-DISP-TOTAL-SELECT.                  
034028     DISPLAY '*                                       *'.                 
034030     DISPLAY '*****************************************'.                 
034031                                                                          
034040     CLOSE IN-TRANSIT-EXTRACT-FILE.                                       
035100                                                                          
035200 9100-SQL-ABEND.                                                          
035300******************************************************************        
035400*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
035500*  ERROR OR WARNING CODE OCCURS.                                          
035600******************************************************************        
035700     DISPLAY '9100-SQL-ABEND'.                                            
035800     DISPLAY '** ABEND     **'.                                           
035900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
036500     DISPLAY '**              ** = '                                      
036600     MOVE SQLCODE TO PV-SQL-CODE.                                         
036700     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
036800     MOVE +4013 TO ABEND-CODE.                                            
036900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
