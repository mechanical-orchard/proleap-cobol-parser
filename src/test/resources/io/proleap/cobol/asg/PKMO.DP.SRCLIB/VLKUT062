      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   VLKUT062.                                                  
       AUTHOR.       PAROMITA ROY.                                              
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. APRIL, 2015.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM VALIDATES THREE PO RELATED FILES - HEADER, DETAIL *        
      * AND MARKER CREATED BY PROCESSES WITH THE RMS PO APPLICATION FOR*        
      * THE SOLE PURPOSE OF TAKING THE DATA AND SENDING IT ALONG TO    *        
      * COMPLIANCE NETWORK.DURING THE FILE VALIDATION IF ANY ISSUE IS  *        
      * ENCOUNTERED ANY OF THE FOLLOWING RETURN CODE WILL BE SET AND   *        
      * THE FILES WILL NOT BE SENT TO COMPLIANCE NETWORK.              *        
      *                                                                *        
      *         1) 4005 - FOR ISSUES IN MARKER FILE.IT MIGHT BE FOR ANY*        
      *                   OF THE FOLLOWING,                            *        
      *                   -> TOTAL NUMBER OF RECORDS PRESENT IN MARKER *        
      *                      FILE IS NOT EQUAL TO TWO.                 *        
      *                   -> TOTAL NUMBER OF RECORDS PRESENT IN MARKER *        
      *                      FILE IS EQUAL TO TWO BUT BOTH THE RECORDS *        
      *                      ARE OF SAME TYPE I.E EITHER POHDR OR PODTL*        
      *                   -> FOR ANY INVALID RECORD TYPES I.E. OTHER   *        
      *                      THAN POHDR AND PODTL PRESENT IN MARKER    *        
      *                      FILE                                      *        
      *                                                                *        
      *         2) 4006 - FOR ISSUES IN HEADER FILE. I.E.              *        
      *                    -> TOTAL NUMBER OF RECORDS IN HEADER FILE IS*        
      *                       NOT MATCHING WITH THE HEADER RECORD COUNT*        
      *                       PRESENT IN MARKER FILE.                  *        
      *                                                                *        
      *         3) 4007 - FOR ISSUES IN DETAIL FILE. I.E.              *        
      *                    -> TOTAL NUMBER OF RECORDS IN DETAIL FILE IS*        
      *                       NOT MATCHING WITH THE DETAIL RECORD COUNT*        
      *                       PRESENT IN MARKER FILE.                  *        
      *                                                                *        
      * PROGRAM WILL RUN WITH RETURN CODE ZERO IF ALL THE THREE FILES  *        
      * ARE VALID                                                      *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT VL029-MARKER-FILE        ASSIGN TO INP01.                     
           SELECT INPUT-PO-HEADER-FILE     ASSIGN TO INP02.                     
           SELECT INPUT-PO-DETAIL-FILE     ASSIGN TO INP03.                     
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  VL029-MARKER-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  INPUT-MARKER-RECORD              PIC X(078).                         
                                                                                
       FD  INPUT-PO-HEADER-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
      *CHG0139557 - START                                                       
      *01  INPUT-PO-HEADER-RECORD           PIC X(474).                         
       01  INPUT-PO-HEADER-RECORD           PIC X(490).                         
      *CHG0139557 - END                                                         
                                                                                
       FD  INPUT-PO-DETAIL-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  INPUT-PO-DETAIL-RECORD           PIC X(181).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************ETKUT018
      *    FILE LAYOUT FOR MARKER RECORD.                               ETKUT018
      ******************************************************************ETKUT018
           COPY VLRD029.                                                   CL*20
      ******************************************************************        
                                                                                
      ***  PROGRAM VARIABLES                                                    
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-MARKER-COUNT               PIC  9(13)    VALUE ZEROES.         
           05 PV-HEADER-COUNT               PIC  9(13)    VALUE ZEROES.         
           05 PV-DETAIL-COUNT               PIC  9(13)    VALUE ZEROES.         
           05 PV-DISPLAY-COUNT              PIC  Z(12)9.                        
                                                                                
      ***  PROGRAM SWITCHES                                                     
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-HDR-INPUT-SW       PIC  X(01)    VALUE 'N'.            
               88  PS-END-OF-HEADER-FILE                  VALUE 'Y'.            
               88  PS-NOT-END-OF-HEADER-FILE              VALUE 'N'.            
           05  PS-END-OF-DTL-INPUT-SW       PIC  X(01)    VALUE 'N'.            
               88  PS-END-OF-DETAIL-FILE                  VALUE 'Y'.            
               88  PS-NOT-END-OF-DETAIL-FILE              VALUE 'N'.            
           05  PS-END-OF-MARKER-INPUT-SW    PIC  X(01)    VALUE 'N'.            
               88  PS-END-OF-MARKER-FILE                  VALUE 'Y'.            
               88  PS-NOT-END-OF-MARKER-FILE              VALUE 'N'.            
           05  PS-VALID-HDR-INPUT-SW        PIC  X(01)    VALUE 'Y'.            
               88  PS-VALID-HEADER-FILE                   VALUE 'Y'.            
               88  PS-INVALID-HEADER-FILE                 VALUE 'N'.            
           05  PS-VALID-DTL-INPUT-SW        PIC  X(01)    VALUE 'Y'.            
               88  PS-VALID-DETAIL-FILE                   VALUE 'Y'.            
               88  PS-INVALID-DETAIL-FILE                 VALUE 'N'.            
           05  PS-VALID-MARKER-INPUT-SW     PIC  X(01)    VALUE 'Y'.            
               88  PS-VALID-MARKER-FILE                   VALUE 'Y'.            
               88  PS-INVALID-MARKER-FILE                 VALUE 'N'.            
                                                                                
       01  WS-MARKER-RECORD.                                                    
           05  WS-MARKER-HEADER-RECORD.                                         
               10  WS-HDR-RCD-TYP           PIC  X(05) VALUE SPACES.            
               10  WS-HDR-RCD-COUNT         PIC  9(13) VALUE ZEROES.            
           05  WS-MARKER-DETAIL-RECORD.                                         
               10  WS-DTL-RCD-TYP           PIC  X(05) VALUE SPACES.            
               10  WS-DTL-RCD-COUNT         PIC  9(13) VALUE ZEROES.            
                                                                                
      ***  PROGRAM CONSTANTS                                                    
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-POHDR                     PIC X(05)  VALUE 'POHDR'.           
           05  PC-PODTL                     PIC X(05)  VALUE 'PODTL'.           
           05  PC-PGM-NAME                  PIC X(08)  VALUE 'VLKUT062'.        
           05  PC-ERR-MSG1                  PIC X(54)  VALUE                    
               'INVALID MARKER FILE - HENCE HDR/DTL FILE NOT PROCESSED'.        
           05  PC-ERR-MSG2                  PIC X(53)  VALUE                    
               'INVALID HEADER FILE - HENCE DETAIL FILE NOT PROCESSED'.         
           05  PC-ERR-MSG3                  PIC X(19)  VALUE                    
               'INVALID DETAIL FILE'.                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
      ******************************************************************        
       0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZE.                                             
           PERFORM 2000-MAIN-PROCESS.                                           
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  OPENS INPUT AND OUTPUT FILES AND PERFORMS INITIALIZATION      *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
           OPEN INPUT VL029-MARKER-FILE                                         
                      INPUT-PO-HEADER-FILE                                      
                      INPUT-PO-DETAIL-FILE.                                     
                                                                                
           DISPLAY 'START OF PROGRAM : ' PC-PGM-NAME.                           
                                                                                
      ******************************************************************        
      *  PERFORMS THE MAIN PROCESSING OF THE PROGRAM                   *        
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
                                                                                
           PERFORM 2100-READ-MARKER-FILE UNTIL                                  
               PS-END-OF-MARKER-FILE OR                                         
               PS-INVALID-MARKER-FILE.                                          
                                                                                
           IF PS-VALID-MARKER-FILE                                              
              PERFORM 2200-READ-HEADER-FILE UNTIL                               
                  PS-END-OF-HEADER-FILE OR                                      
                  PS-INVALID-HEADER-FILE                                        
                                                                                
              IF PS-VALID-HEADER-FILE                                           
                 PERFORM 2300-READ-DETAIL-FILE UNTIL                            
                  PS-END-OF-DETAIL-FILE OR                                      
                  PS-INVALID-DETAIL-FILE                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  READS THE MARKER FILE                                         *        
      ******************************************************************        
       2100-READ-MARKER-FILE.                                                   
                                                                                
           IF PV-MARKER-COUNT <= 2                                              
              READ VL029-MARKER-FILE INTO                                       
                   VL029-MARKER-RECORD                                          
                AT END                                                          
                 SET PS-END-OF-MARKER-FILE        TO TRUE                       
              END-READ                                                          
              PERFORM 2110-PROCESS-MARKER                                       
           ELSE                                                                 
              SET PS-INVALID-MARKER-FILE          TO TRUE                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  VALIDATES THE MARKER FILE                                     *        
      ******************************************************************        
       2110-PROCESS-MARKER.                                                     
                                                                                
           EVALUATE TRUE                                                        
              WHEN PS-END-OF-MARKER-FILE                                        
                 EVALUATE TRUE                                                  
                    WHEN PV-MARKER-COUNT =  0                                   
                    WHEN PV-MARKER-COUNT =  1                                   
                       SET PS-INVALID-MARKER-FILE TO TRUE                       
                                                                                
                    WHEN PV-MARKER-COUNT =  2                                   
                       IF (WS-HDR-RCD-TYP  > SPACES AND                         
                           WS-DTL-RCD-TYP  > SPACES)                            
                        SET PS-VALID-MARKER-FILE  TO TRUE                       
                       ELSE                                                     
                        SET PS-INVALID-MARKER-FILE                              
                                                  TO TRUE                       
                       END-IF                                                   
                 END-EVALUATE                                                   
                                                                                
              WHEN PS-NOT-END-OF-MARKER-FILE                                    
                 EVALUATE TRUE                                                  
                    WHEN VL029-FILE-NAME(10:5) = PC-POHDR                       
                       MOVE VL029-FILE-NAME(10:5) TO WS-HDR-RCD-TYP             
                       MOVE VL029-RECORD-COUNT    TO WS-HDR-RCD-COUNT           
                                                                                
                    WHEN VL029-FILE-NAME(10:5) = PC-PODTL                       
                       MOVE VL029-FILE-NAME(10:5) TO WS-DTL-RCD-TYP             
                       MOVE VL029-RECORD-COUNT    TO WS-DTL-RCD-COUNT           
                                                                                
                    WHEN OTHER                                                  
                       SET PS-INVALID-MARKER-FILE TO TRUE                       
                 END-EVALUATE                                                   
                 ADD 1                            TO PV-MARKER-COUNT            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  READS AND VALIDATES THE HEADER FILE                           *        
      ******************************************************************        
       2200-READ-HEADER-FILE.                                                   
                                                                                
              READ INPUT-PO-HEADER-FILE                                         
                AT END                                                          
                 SET PS-END-OF-HEADER-FILE        TO TRUE                       
              END-READ.                                                         
                                                                                
              EVALUATE TRUE                                                     
                 WHEN PS-NOT-END-OF-HEADER-FILE                                 
                    ADD 1                         TO PV-HEADER-COUNT            
                 WHEN PS-END-OF-HEADER-FILE                                     
                    IF PV-HEADER-COUNT = WS-HDR-RCD-COUNT                       
                       SET PS-VALID-HEADER-FILE   TO TRUE                       
                    ELSE                                                        
                       SET PS-INVALID-HEADER-FILE TO TRUE                       
                    END-IF                                                      
              END-EVALUATE.                                                     
                                                                                
      ******************************************************************        
      *  READS AND VALIDATES THE DETAIL FILE                           *        
      ******************************************************************        
       2300-READ-DETAIL-FILE.                                                   
                                                                                
              READ INPUT-PO-DETAIL-FILE                                         
                AT END                                                          
                 SET PS-END-OF-DETAIL-FILE        TO TRUE                       
              END-READ.                                                         
                                                                                
              EVALUATE TRUE                                                     
                 WHEN PS-NOT-END-OF-DETAIL-FILE                                 
                    ADD 1                         TO PV-DETAIL-COUNT            
                 WHEN PS-END-OF-DETAIL-FILE                                     
                    IF PV-DETAIL-COUNT = WS-DTL-RCD-COUNT                       
                       SET PS-VALID-DETAIL-FILE   TO TRUE                       
                    ELSE                                                        
                       SET PS-INVALID-DETAIL-FILE TO TRUE                       
                    END-IF                                                      
              END-EVALUATE.                                                     
                                                                                
      *****************************************************************         
      *  END OF PROGRAM PROCESSING                                              
      *****************************************************************         
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE VL029-MARKER-FILE                                              
                 INPUT-PO-HEADER-FILE                                           
                 INPUT-PO-DETAIL-FILE.                                          
                                                                                
           EVALUATE TRUE                                                        
              WHEN PS-INVALID-MARKER-FILE                                       
                   DISPLAY PC-ERR-MSG1                                          
                   MOVE 4005                      TO RETURN-CODE                
              WHEN PS-INVALID-HEADER-FILE                                       
                   DISPLAY PC-ERR-MSG2                                          
                   MOVE 4006                      TO RETURN-CODE                
              WHEN PS-INVALID-DETAIL-FILE                                       
                   DISPLAY PC-ERR-MSG3                                          
                   MOVE 4007                      TO RETURN-CODE                
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '**               VLKUT062 SUMMARY              **'.         
           DISPLAY '*************************************************'.         
           MOVE PV-MARKER-COUNT                   TO PV-DISPLAY-COUNT.          
           DISPLAY 'TOTAL MARKER RECORDS READ:' PV-DISPLAY-COUNT.               
           MOVE WS-HDR-RCD-COUNT                  TO PV-DISPLAY-COUNT.          
           DISPLAY 'TOTAL HDR COUNT IN MARKER:' PV-DISPLAY-COUNT.               
           MOVE WS-DTL-RCD-COUNT                  TO PV-DISPLAY-COUNT.          
           DISPLAY 'TOTAL DTL COUNT IN MARKER:' PV-DISPLAY-COUNT.               
           MOVE PV-HEADER-COUNT                   TO PV-DISPLAY-COUNT.          
           DISPLAY 'TOTAL HEADER RECORDS READ:' PV-DISPLAY-COUNT.               
           MOVE PV-DETAIL-COUNT                   TO PV-DISPLAY-COUNT.          
           DISPLAY 'TOTAL DETAIL RECORDS READ:' PV-DISPLAY-COUNT.               
           DISPLAY '*************************************************'.         
           DISPLAY 'END OF PROGRAM   : ' PC-PGM-NAME.                           
                                                                                
