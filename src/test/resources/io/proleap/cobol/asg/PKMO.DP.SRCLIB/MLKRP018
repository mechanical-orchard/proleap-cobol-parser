       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP018.                                                 
       AUTHOR.        TESS BESTE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-18-94.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *           MLKRP018 - ACCUM ADJUSTMENTS                                  
      *                                                                         
      * THIS PROGRAM ACCUMULATES THE AMOUNTS FROM A FILE OF ADJUSTMENT          
      * TRANSACTIONS IN THE MLRD018 FORMAT, WHICH IS INPUT TO MLKUP020          
      * IN JOB MLK140, ON DD'S INP10 AND INP11. THE TRANSACTION COUNT,          
      * TOTAL COST, AND TOTAL RETAIL ARE DISPLAYED.                             
      ******************************************************************        
                                                                       *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
*********  INPUT-RECORD                PIC X(80).                               
           COPY MLRD018.                                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * INPUT RECORD LAYOUT                                                     
      ******************************************************************        
                                                                                
*********INCLUDE MLRD018                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-FILE-SW     PIC X(01)       VALUE 'N'.           
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.           
                                                                                
       01  RP-REPORT-HEADER.                                                    
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(03)       VALUE 'DPT'.         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(05)       VALUE                
               'STORE'.                                                         
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               '  ENT-ID '.                                                     
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               '            RETAIL '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               '              COST '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '  RECORD COUNT '.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-DEPT-NBR                 PIC X(03)       VALUE SPACES.        
           05  RP-DEPT-NBR-9 REDEFINES RP-DEPT-NBR                              
                                           PIC 9(03).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-STORE-NBR                PIC X(05)       VALUE SPACES.        
           05  RP-STORE-NBR-9 REDEFINES RP-STORE-NBR                            
                                           PIC 9(05).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-ENT-ID                   PIC ZZZZZZZZZ   VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-RTL-AMT                  PIC ZZZ,ZZZ,ZZZ,ZZ9.99-              
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-COST-AMT                 PIC ZZZ,ZZZ,ZZZ,ZZ9.99-              
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-RECORD-COUNT             PIC ZZZ,ZZZ,ZZZ,ZZ9                  
                                                           VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CURR-DEPT-NBR            PIC X(03)       VALUE SPACES.        
           05  PV-CURR-STORE-NBR           PIC X(05)       VALUE SPACES.        
           05  PV-CURR-ENT-ID              PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
           05  PV-RECORD-COUNT             PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-RETAIL             PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-COST               PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-BREAK-RECORD-COUNT       PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-BREAK-ACCUM-RTL          PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-BREAK-ACCUM-COST         PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZZ,ZZZ-.                
           05  PV-DISPLAY-RTL-AMT          PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.             
           05  PV-DISPLAY-COST-AMT         PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.             
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  PARM-CONTROL.                                                        
           05  PARM-LENGTH                 PICTURE S9(4)   VALUE ZEROES         
                               COMPUTATIONAL.                                   
           05  PARM-RQ-DEPT-IND            PICTURE X(08)   VALUE SPACES.        
               88  PARM-RQ-DEPT                        VALUE 'DEPT'.            
               88  PARM-RQ-STORE                       VALUE 'STORE'.           
               88  PARM-RQ-VENDOR                      VALUE 'VENDOR'.          
               88  PARM-RQ-DEPT-STORE                  VALUE 'DPTSTORE'.        
               88  PARM-RQ-DEPT-VENDOR                 VALUE 'DPTVEND'.         
               88  PARM-RQ-STORE-VENDOR                VALUE 'STRVEND'.         
                                                                                
       EJECT                                                                    
                                                                                
       PROCEDURE DIVISION USING PARM-CONTROL.                                   
                                                                                
                                                                                
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           IF PARM-LENGTH > ZEROES                                              
               INSPECT PARM-RQ-DEPT-IND                                         
                   REPLACING ALL X'00' BY ' '                                   
               DISPLAY ' REQUEST: ' PARM-RQ-DEPT-IND                            
               DISPLAY RP-REPORT-HEADER                                         
           END-IF.                                                              
                                                                                
           OPEN INPUT INPUT-FILE.                                               
           READ INPUT-FILE                                                      
               AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
                                                                                
           MOVE ML018-ADJ-DEPT-NBR  TO PV-CURR-DEPT-NBR                         
                                       RP-DEPT-NBR.                             
           MOVE ML018-ADJ-STORE-NBR TO PV-CURR-STORE-NBR                        
                                       RP-STORE-NBR.                            
           MOVE ML018-ADJ-ENT-ID    TO PV-CURR-ENT-ID                           
                                       RP-ENT-ID.                               
                                                                                
           PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
               ADD +1 TO PV-RECORD-COUNT                                        
               COMPUTE PV-ACCUM-RETAIL                                          
                     = PV-ACCUM-RETAIL                                          
                     + ML018-ADJ-AT-RTL-AMT                                     
               COMPUTE PV-ACCUM-COST                                            
                     = PV-ACCUM-COST                                            
                     + ML018-ADJ-AT-COST-AMT                                    
               IF PARM-LENGTH > ZEROES                                          
                   IF (PARM-RQ-DEPT AND                                         
                           PV-CURR-DEPT-NBR = ML018-ADJ-DEPT-NBR)               
                       OR (PARM-RQ-STORE AND                                    
                           PV-CURR-STORE-NBR = ML018-ADJ-STORE-NBR)             
                       OR (PARM-RQ-VENDOR AND                                   
                           PV-CURR-ENT-ID = ML018-ADJ-ENT-ID)                   
                       OR (PARM-RQ-DEPT-STORE AND                               
                           PV-CURR-DEPT-NBR = ML018-ADJ-DEPT-NBR AND            
                           PV-CURR-STORE-NBR = ML018-ADJ-STORE-NBR)             
                       OR (PARM-RQ-DEPT-VENDOR AND                              
                           PV-CURR-DEPT-NBR = ML018-ADJ-DEPT-NBR AND            
                           PV-CURR-ENT-ID = ML018-ADJ-ENT-ID)                   
                       OR (PARM-RQ-STORE-VENDOR AND                             
                           PV-CURR-STORE-NBR = ML018-ADJ-STORE-NBR AND          
                           PV-CURR-ENT-ID = ML018-ADJ-ENT-ID)                   
                       ADD +1 TO PV-BREAK-RECORD-COUNT                          
                       COMPUTE PV-BREAK-ACCUM-RTL                               
                             = PV-BREAK-ACCUM-RTL                               
                             + ML018-ADJ-AT-RTL-AMT                             
                       COMPUTE PV-BREAK-ACCUM-COST                              
                             = PV-BREAK-ACCUM-COST                              
                             + ML018-ADJ-AT-COST-AMT                            
                   ELSE                                                         
                       MOVE PV-BREAK-ACCUM-RTL   TO RP-RTL-AMT                  
                       MOVE PV-BREAK-ACCUM-COST  TO RP-COST-AMT                 
                       MOVE PV-BREAK-RECORD-COUNT TO RP-RECORD-COUNT            
                       EVALUATE TRUE                                            
                           WHEN PARM-RQ-DEPT                                    
                               MOVE ZEROES       TO RP-ENT-ID                   
                               MOVE SPACES       TO RP-STORE-NBR                
                           WHEN PARM-RQ-STORE                                   
                               MOVE ZEROES       TO RP-ENT-ID                   
                               MOVE SPACES       TO RP-DEPT-NBR                 
                           WHEN PARM-RQ-VENDOR                                  
                               MOVE SPACES       TO RP-DEPT-NBR                 
                               MOVE SPACES       TO RP-STORE-NBR                
                           WHEN PARM-RQ-DEPT-STORE                              
                               MOVE ZEROES       TO RP-ENT-ID                   
                           WHEN PARM-RQ-DEPT-VENDOR                             
                               MOVE SPACES       TO RP-STORE-NBR                
                           WHEN PARM-RQ-STORE-VENDOR                            
                               MOVE SPACES       TO RP-DEPT-NBR                 
                       END-EVALUATE                                             
                       DISPLAY RP-REPORT-DETAIL                                 
                                                                                
                       MOVE ML018-ADJ-DEPT-NBR   TO PV-CURR-DEPT-NBR            
                                                    RP-DEPT-NBR                 
                       MOVE ML018-ADJ-STORE-NBR  TO PV-CURR-STORE-NBR           
                                                    RP-STORE-NBR                
                       MOVE ML018-ADJ-ENT-ID     TO PV-CURR-ENT-ID              
                                                    RP-ENT-ID                   
                       MOVE +1               TO PV-BREAK-RECORD-COUNT           
                       MOVE ML018-ADJ-AT-RTL-AMT TO PV-BREAK-ACCUM-RTL          
                       MOVE ML018-ADJ-AT-COST-AMT TO PV-BREAK-ACCUM-COST        
                   END-IF                                                       
               END-IF                                                           
               READ INPUT-FILE                                                  
                   AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
               END-READ                                                         
           END-PERFORM.                                                         
                                                                                
           DISPLAY ' '.                                                         
           MOVE PV-RECORD-COUNT TO PV-DISPLAY-COUNT.                            
           DISPLAY 'TRANSACTION COUNT: ' PV-DISPLAY-COUNT.                      
           DISPLAY ' '.                                                         
           MOVE PV-ACCUM-RETAIL TO PV-DISPLAY-RTL-AMT.                          
           MOVE PV-ACCUM-COST   TO PV-DISPLAY-COST-AMT.                         
           DISPLAY 'TOTAL RETAIL:      ' PV-DISPLAY-RTL-AMT                     
                   '      COST:        ' PV-DISPLAY-COST-AMT.                   
                                                                                
           CLOSE INPUT-FILE.                                                    
           STOP RUN.                                                            
