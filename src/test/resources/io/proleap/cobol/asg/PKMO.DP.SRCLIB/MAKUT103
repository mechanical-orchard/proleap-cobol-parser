       IDENTIFICATION DIVISION.                                         00010065
       PROGRAM-ID.      MAKUT103.                                       00020065
       AUTHOR.          SCOTT CIESIELCZYK                               00030065
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040065
       DATE-WRITTEN.    12/02/96.                                       00050065
       DATE-COMPILED.                                                   00060065
      ****************************************************************  00070065
      * THIS PROGRAM WILL GENERATE A CONTROL CARD TO BE USED BY      *  00080065
      * PLATIMIUM'S FAST UNLOAD PRODUCT.                             *  00090065
      ****************************************************************  00100065
      * 001 SCOTT CIESIELCZYK 12/12/96                               *  00110065
      *   - INITIAL DEVELOPMENT                                      *  00120065
      ****************************************************************  00130065
      *                                                                 00140065
       ENVIRONMENT DIVISION.                                            00150065
       INPUT-OUTPUT SECTION.                                            00160065
       FILE-CONTROL.                                                    00170065
           SELECT CONTROL-FILE                                          00180065
               ASSIGN TO CCFILE.                                        00190065
                                                                        00200065
           SELECT OUT01-FILE                                            00210065
               ASSIGN TO OUT01.                                         00220065
           SELECT OUT02-FILE                                            00230065
               ASSIGN TO OUT02.                                         00240065
           SELECT OUT03-FILE                                            00250065
               ASSIGN TO OUT03.                                         00260065
                                                                        00270065
      *                                                                 00280065
       DATA DIVISION.                                                   00290065
       FILE SECTION.                                                    00300065
      *                                                                 00310065
       FD  CONTROL-FILE                                                 00320065
           RECORDING MODE IS F                                          00330065
           BLOCK CONTAINS 0 RECORDS.                                    00340065
       01  CONTROL-RECORD                  PIC  X(80).                  00350065
      *                                                                 00360065
      *                                                                 00370065
       FD  OUT01-FILE                                                   00380065
           RECORDING MODE IS F                                          00390065
           BLOCK CONTAINS 0 RECORDS.                                    00400065
       01  OUT01-REC                       PIC  X(80).                  00410065
      *                                                                 00420065
       FD  OUT02-FILE                                                   00430065
           RECORDING MODE IS F                                          00440065
           BLOCK CONTAINS 0 RECORDS.                                    00450065
       01  OUT02-REC                       PIC  X(80).                  00460065
      *                                                                 00470065
       FD  OUT03-FILE                                                   00480065
           RECORDING MODE IS F                                          00490065
           BLOCK CONTAINS 0 RECORDS.                                    00500065
1022RM*01  OUT03-REC                       PIC  X(80).                  00510067
1022RM 01  OUT03-REC                       PIC  X(28).                  00520067
      *                                                                 00530065
      *                                                                 00540065
       WORKING-STORAGE SECTION.                                         00550065
                                                                        00560065
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00570065
                                                         COMP SYNC.     00580065
           88  ABEND-4001                                VALUE +4001.   00590065
           88  ABEND-4002                                VALUE +4002.   00600065
           88  ABEND-4019                                VALUE +4019.   00610065
                                                                        00620065
       01  PV-PROGRAM-VARIABLES.                                        00630065
           05  PV-REC-COUNT                PIC  9(10)    VALUE ZERO.    00640065
           05  PV-REC2-COUNT               PIC  9(10)    VALUE ZERO.    00650065
           05  PV-REC3-COUNT               PIC  9(10)    VALUE ZERO.    00660065
           05  PV-SQL-SUB                  PIC S9(04)    COMP.          00670065
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   00680065
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)    VALUE SPACE.   00690065
           05  PV-PRINT-LINE               PIC  X(80)    VALUE SPACE.   00700065
1022RM     05  PV-PRINT-LINE-FILE-3        PIC  X(28)    VALUE SPACE.   00710067
           05  PV-SELECT-STMT              PIC  X(80)    VALUE SPACE.   00720065
           05  PV-WHERE-1-STMT             PIC  X(80)    VALUE SPACE.   00730065
           05  PV-WHERE-2-STMT             PIC  X(80)    VALUE SPACE.   00740065
1022RM     05  PV-WHERE-2-STMT-2           PIC  X(28)    VALUE SPACE.   00750067
           05  PV-ORDER-BY-STMT            PIC  X(80)    VALUE SPACE.   00760065
           05  PV-TBL-OWNER-ID             PIC  X(10)    VALUE SPACE.   00770065
1022RM     05  PV-RPLCTL-EXTR-END-TMST     PIC  X(26)    VALUE SPACE.   00771069
      *                                                                 00780065
       01  PC-PROGRAM-CONSTANTS.                                        00790065
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)     VALUE         00800065
                                                          'MAKUT103'.   00810065
           05  PC-MAKUT103                 PIC  X(08)     VALUE         00820065
                                                          'MAKUT103'.   00830065
           05  PC-SINGLE-QUOTE             PIC  X(01)     VALUE         00840065
                                                          ''''.         00850065
           05  PC-LOW-DB2-TMST             PIC  X(26)                   00860065
                  VALUE '0001-01-01-00.00.00.000000'.                   00870065
           05  PC-ONE                      PIC  9(02)    VALUE  1 COMP. 00880065
           05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3 COMP. 00890065
                                                                        00900065
                                                                        00910065
       01  CC-CONTROL-CARD.                                             00920065
           05  CC-APPL-ID             PIC X(02).                        00930065
           05  FILLER                 PIC X(01).                        00940065
           05  CC-EXTR-PRC-ID         PIC X(08).                        00950065
           05  FILLER                 PIC X(01).                        00960065
           05  CC-TBL-OWNER-ID        PIC X(10).                        00970065
           05  CC-LOG-TBL-NME         PIC X(10).                        00980065
           05  CC-DELAY-TYP           PIC X(07).                        00990065
           05  CC-DELAY-AMT           PIC X(02).                        01000065
           05  FILLER                 PIC X(01).                        01010065
           05  CC-TMST-FIELD-NME      PIC X(30).                        01020065
           05  FILLER                 PIC X(08).                        01030065
      *                                                                 01040065
      ******************************************************************01050065
      *    WORK AREA FOR THE ABEND ROUTINE.                             01060065
      ******************************************************************01070065
           COPY DPWS004.                                                01080065
                                                                        01090065
      *                                                                 01100065
       01  SQL-VARIBLES.                                                01110065
           05  SV-APPL-ID                  PIC  X(02).                  01120065
           05  SV-EXTR-PRC-ID              PIC  X(08).                  01130065
           05  SV-LOG-TBL-NME              PIC  X(10).                  01140065
           05  SV-UNLD-END-TMST            PIC  X(26).                  01150065
                                                                        01160065
      *----------------------------------------------------------------*01170065
      *    REPLICATION CONTROL TABLE                                   *01180065
      *----------------------------------------------------------------*01190065
           EXEC SQL                                                     01200065
               INCLUDE TRPLCTL                                          01210065
           END-EXEC.                                                    01220065
                                                                        01230065
      *----------------------------------------------------------------*01240065
      *    SQL COMMUNICATIONS AREA                                     *01250065
      *----------------------------------------------------------------*01260065
           EXEC SQL                                                     01270065
                INCLUDE SQLCA                                           01280065
           END-EXEC.                                                    01290065
                                                                        01300065
       LINKAGE SECTION.                                                 01310065
                                                                        01320065
       PROCEDURE DIVISION.                                              01330065
                                                                        01340065
       0000-MAINLINE.                                                   01350065
      ******************************************************************01360065
      *  CONTROLS THE ENTIRE FLOW OF THE PROGRAM.                       01370065
      *    -- OPEN FILES                                                01380065
      *    -- PERFORM MAIN PROCESSING LOOP                              01390065
      *    -- CLOSE FILES                                               01400065
      *    -- DISPLAY COUNT                                             01410065
      *    -- RETURNS CONTROL TO THE SYSTEM                             01420065
      ******************************************************************01430065
           PERFORM 1000-INITIALIZE.                                     01440065
           PERFORM 3000-CREATE-PLATINUM-FILE.                           01450065
           PERFORM 4000-CREATE-RPLCTL-FILE.                             01460065
                                                                        01470065
           CLOSE CONTROL-FILE                                           01480065
                 OUT01-FILE                                             01490065
                 OUT02-FILE                                             01500065
                 OUT03-FILE.                                            01510065
                                                                        01520065
           GOBACK.                                                      01530065
                                                                        01540065
      *----------------------------------------------------------------*01550065
      *                                                                *01560065
       1000-INITIALIZE.                                                 01570065
      *     -- OPEN FILES.                                             *01580065
      *     -- READ THE INPUT CONTROL FILE.                            *01590065
      *                                                                *01600065
      *----------------------------------------------------------------*01610065
           DISPLAY '* * * * * * * * * * * * * * * * * * *'              01620065
           DISPLAY 'INITIALIZING PROGRAM ' PC-CURRENT-PROGRAM-NAME.     01630065
                                                                        01640065
           OPEN INPUT  CONTROL-FILE                                     01650065
                OUTPUT OUT01-FILE                                       01660065
                       OUT02-FILE                                       01670065
                       OUT03-FILE.                                      01680065
                                                                        01690065
           PERFORM 1100-READ-CONTROL-CARD.                              01700065
           MOVE CC-LOG-TBL-NME TO SV-LOG-TBL-NME.                       01710065
           MOVE CC-APPL-ID     TO SV-APPL-ID.                           01720065
                                                                        01730065
      *    GET THE LAST UNLOAD TIMESTAMP AND SAVE IT.                   01740065
           MOVE PC-MAKUT103 TO SV-EXTR-PRC-ID.                          01750065
           PERFORM 8000-GET-RPLCTL-ROW.                                 01760065
           MOVE RPLCTL-EXTR-END-TMST                                    01770065
             TO SV-UNLD-END-TMST.                                       01780065
                                                                        01790065
      *    GET THE LAST EXTRACT TIMESTAMP.                              01800065
           MOVE CC-EXTR-PRC-ID TO SV-EXTR-PRC-ID.                       01810065
           PERFORM 8000-GET-RPLCTL-ROW.                                 01820065
                                                                        01830065
                                                                        01840065
      *----------------------------------------------------------------*01850065
      *                                                                *01860065
       1100-READ-CONTROL-CARD.                                          01870065
      *    READ IN THE UNLOAD CONTROL CARD INPUT FILE.                 *01880065
      *                                                                *01890065
      *----------------------------------------------------------------*01900065
                                                                        01910065
           READ CONTROL-FILE                                            01920065
                   INTO CC-CONTROL-CARD                                 01930065
           END-READ.                                                    01940065
           DISPLAY 'CONTROL CARD = ' CC-CONTROL-CARD.                   01950065
                                                                        01960065
      *----------------------------------------------------------------*01970065
      *                                                                 01980065
       3000-CREATE-PLATINUM-FILE.                                       01990065
      *----------------------------------------------------------------*02000065
           MOVE CC-TBL-OWNER-ID TO PV-TBL-OWNER-ID.                     02010065
           STRING  'SELECT * FROM '       DELIMITED BY SIZE             02020065
                   PV-TBL-OWNER-ID        DELIMITED BY SIZE             02030065
                   CC-LOG-TBL-NME         DELIMITED BY SIZE             02040065
                                     INTO  PV-SELECT-STMT.              02050065
           STRING  '  WHERE '             DELIMITED BY SIZE             02060065
                   CC-TMST-FIELD-NME      DELIMITED BY SIZE             02070065
                                     INTO  PV-WHERE-1-STMT.             02080065
           STRING  '( TIMESTAMP('         DELIMITED BY SIZE             02090065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02100065
                   RPLCTL-EXTR-END-TMST   DELIMITED BY SIZE             02110065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02120065
                   ') - '                 DELIMITED BY SIZE             02130065
                   CC-DELAY-AMT           DELIMITED BY SIZE             02140065
                   ' '                    DELIMITED BY SIZE             02150065
                   CC-DELAY-TYP           DELIMITED BY SIZE             02160065
                   ')'                    DELIMITED BY SIZE             02170065
                                     INTO  PV-WHERE-2-STMT.             02180065
                                                                        02190067
1022RM     STRING  PC-SINGLE-QUOTE         DELIMITED BY SIZE            02200070
1022RM*            RPLCTL-EXTR-END-TMST    DELIMITED BY SIZE            02210070
1022RM             PV-RPLCTL-EXTR-END-TMST DELIMITED BY SIZE            02211070
1022RM             PC-SINGLE-QUOTE         DELIMITED BY SIZE            02220070
1022RM                               INTO  PV-WHERE-2-STMT-2.           02230067
                                                                        02240067
           STRING  '  ORDER BY '          DELIMITED BY SIZE             02250065
                   CC-TMST-FIELD-NME      DELIMITED BY SIZE             02260065
                                     INTO  PV-ORDER-BY-STMT.            02270065
      ***                                                               02280065
      ***  SELECT THE ROWS TO BE ARCHIVED.                              02290065
      ***                                                               02300065
           MOVE PV-SELECT-STMT         TO  PV-PRINT-LINE.               02310065
           PERFORM 9000-WRITE-OUT01-LINE.                               02320065
           MOVE PV-WHERE-1-STMT        TO  PV-PRINT-LINE.               02330065
           PERFORM 9000-WRITE-OUT01-LINE.                               02340065
           MOVE    '     <='        TO PV-PRINT-LINE.                   02350065
           PERFORM 9000-WRITE-OUT01-LINE.                               02360065
           MOVE PV-WHERE-2-STMT        TO  PV-PRINT-LINE.               02370065
           PERFORM 9000-WRITE-OUT01-LINE.                               02380065
           MOVE PV-ORDER-BY-STMT        TO  PV-PRINT-LINE.              02390065
           PERFORM 9000-WRITE-OUT01-LINE.                               02400065
           MOVE '  ;'                  TO  PV-PRINT-LINE.               02410065
           PERFORM 9000-WRITE-OUT01-LINE.                               02420065
           DISPLAY '***  OUT01 LINES WRITTEN = '                        02430065
                     PV-REC-COUNT.                                      02440065
           MOVE ZEROS TO PV-REC-COUNT.                                  02450065
      ***                                                               02460065
      ***  SELECT THE ROWS TO BE ARCHIVED.                              02470065
      ***                                                               02480065
1022RM*    MOVE PV-WHERE-1-STMT        TO  PV-PRINT-LINE.               02490067
1022RM*    PERFORM 9010-WRITE-OUT03-LINE.                               02500067
1022RM*    MOVE    '     <='        TO PV-PRINT-LINE.                   02510067
1022RM*    PERFORM 9010-WRITE-OUT03-LINE.                               02520067
1022RM*    MOVE PV-WHERE-2-STMT        TO  PV-PRINT-LINE.               02530067
1022RM*    PERFORM 9010-WRITE-OUT03-LINE.                               02540067
1022RM*    MOVE '  ;'                  TO  PV-PRINT-LINE.               02550067
1022RM     MOVE PV-WHERE-2-STMT-2      TO  PV-PRINT-LINE-FILE-3.        02560067
1022RM     PERFORM 9010-WRITE-OUT03-LINE.                               02570067
           DISPLAY '***  OUT03 LINES WRITTEN = '                        02580065
                     PV-REC3-COUNT.                                     02590065
           MOVE ZEROS TO PV-REC3-COUNT.                                 02600065
      *----------------------------------------------------------------*02610065
      *                                                                *02620065
       4000-CREATE-RPLCTL-FILE.                                         02630065
      *    UPDATE THE TRPLCTL TABLE WITH THE CURRENT EXTRACT TIME      *02640065
      *    FRAME FOR THE SPECIFIED TABLE.                              *02650065
      *    THIS ROW WILL BE READ BY MAKUT101 IN VERIFY MODE.           *02660065
      *----------------------------------------------------------------*02670065
           MOVE CC-APPL-ID     TO SV-APPL-ID.                           02680065
           MOVE PC-MAKUT103    TO SV-EXTR-PRC-ID.                       02690065
           STRING  'UPDATE '              DELIMITED BY SIZE             02700065
                   PV-TBL-OWNER-ID        DELIMITED BY SIZE             02710065
                   'TRPLCTL  SET '        DELIMITED BY SIZE             02720065
                                     INTO  PV-PRINT-LINE.               02730065
           PERFORM 9100-WRITE-OUT02-LINE.                               02740065
      *    THE 'EXTR_BEG_TMST' WILL BE THE 'EXTR_END_TMST' OF           02750065
      *    THE LAST UNLOAD.                                             02760065
           STRING  '   EXTR_BEG_TMST='    DELIMITED BY SIZE             02770065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02780065
                   SV-UNLD-END-TMST       DELIMITED BY SIZE             02790065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02800065
                                     INTO  PV-PRINT-LINE.               02810065
           PERFORM 9100-WRITE-OUT02-LINE.                               02820065
           STRING  '  ,EXTR_END_TMST='    DELIMITED BY SIZE             02830065
                   '(TIMESTAMP('          DELIMITED BY SIZE             02840065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02850065
                   RPLCTL-EXTR-END-TMST   DELIMITED BY SIZE             02860065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02870065
                   ') - '                 DELIMITED BY SIZE             02880065
                   CC-DELAY-AMT           DELIMITED BY SIZE             02890065
                   ' '                    DELIMITED BY SIZE             02900065
                   CC-DELAY-TYP           DELIMITED BY SIZE             02910065
                   ')'                    DELIMITED BY SIZE             02920065
                                     INTO  PV-PRINT-LINE.               02930065
           PERFORM 9100-WRITE-OUT02-LINE.                               02940065
           STRING  ' WHERE APPL_ID = '     DELIMITED BY SIZE            02950065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02960065
                   SV-APPL-ID             DELIMITED BY SIZE             02970065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             02980065
                                     INTO  PV-PRINT-LINE.               02990065
           PERFORM 9100-WRITE-OUT02-LINE.                               03000065
           STRING  '   AND EXTR_PRC_ID = ' DELIMITED BY SIZE            03010065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             03020065
                   SV-EXTR-PRC-ID         DELIMITED BY SIZE             03030065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             03040065
                                     INTO  PV-PRINT-LINE.               03050065
           PERFORM 9100-WRITE-OUT02-LINE.                               03060065
           STRING  '   AND LOG_TBL_NME = ' DELIMITED BY SIZE            03070065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             03080065
                   SV-LOG-TBL-NME         DELIMITED BY SIZE             03090065
                   PC-SINGLE-QUOTE        DELIMITED BY SIZE             03100065
                                     INTO  PV-PRINT-LINE.               03110065
           PERFORM 9100-WRITE-OUT02-LINE.                               03120065
           MOVE '  ;'                  TO  PV-PRINT-LINE.               03130065
           PERFORM 9100-WRITE-OUT02-LINE.                               03140065
           DISPLAY '***  OUT02 LINES WRITTEN = '                        03150065
                     PV-REC-COUNT.                                      03160065
           MOVE ZEROS TO PV-REC-COUNT.                                  03170065
      *----------------------------------------------------------------*03180065
      *                                                                 03190065
       8000-GET-RPLCTL-ROW.                                             03200065
      *    RETRIEVE THE LAST BEGIN AND END TMST FOR THE CURRENT TABLE  *03210065
      *    THIS TABLE TELLS US THE LAST TIME FRAME OF DATA THAT WAS    *03220065
      *    RETRIEVED BY THE SPECIFIED PROGRAM (EXTR-PRC-ID).           *03230065
      *----------------------------------------------------------------*03240065
           DISPLAY 'SELECTING FROM TRPLCTL WHERE '.                     03250065
           DISPLAY '       APPL_ID     = ' SV-APPL-ID.                  03260065
           DISPLAY '       EXTR_PRC_ID = ' SV-EXTR-PRC-ID.              03270065
           DISPLAY '       LOG_TBL_NME = ' SV-LOG-TBL-NME.              03280065
           PERFORM                                                      03290065
               WITH TEST AFTER                                          03300065
               VARYING PV-SQL-SUB                                       03310065
               FROM 1 BY 1                                              03320065
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       03330065
                         OR SQLCODE = 0)                                03340065
               EXEC SQL                                                 03350065
                   SELECT                                               03360065
                           APPL_ID                                      03370065
                          ,EXTR_PRC_ID                                  03380065
                          ,LOG_TBL_NME                                  03390065
                          ,EXTR_BEG_TMST                                03400065
                          ,EXTR_END_TMST                                03410065
1022RM                    ,EXTR_END_TMST - 1 MONTH                      03411070
                    INTO                                                03420065
                           :RPLCTL-APPL-ID                              03430065
                          ,:RPLCTL-EXTR-PRC-ID                          03440065
                          ,:RPLCTL-LOG-TBL-NME                          03450065
                          ,:RPLCTL-EXTR-BEG-TMST                        03460065
                          ,:RPLCTL-EXTR-END-TMST                        03470065
1022RM                    ,:PV-RPLCTL-EXTR-END-TMST                     03471070
                    FROM                                                03480065
                          TRPLCTL                                       03490065
                    WHERE                                               03500065
                          APPL_ID     = :SV-APPL-ID                     03510065
                      AND EXTR_PRC_ID = :SV-EXTR-PRC-ID                 03520065
                      AND LOG_TBL_NME = :SV-LOG-TBL-NME                 03530065
               END-EXEC                                                 03540065
                                                                        03550065
               EVALUATE TRUE                                            03560065
                   WHEN SQLCODE = 0                                     03570065
                   WHEN SQLCODE = -904                                  03580065
                   WHEN SQLCODE = -911                                  03590065
                        CONTINUE                                        03600065
                   WHEN SQLWARN NOT = SPACES                            03610065
                   WHEN SQLCODE NOT = ZERO                              03620065
                        MOVE '8000-GET-RPL-CTL'                         03630065
                                      TO  PV-PARAGRAPH-NAME             03640065
                        MOVE 'SELECT FROM TRPLCTL TABLE'                03650065
                                      TO  PV-DB2-OPER-ATTEMPTED         03660065
                        PERFORM 9900-SQL-ABEND                          03670065
               END-EVALUATE                                             03680065
           END-PERFORM.                                                 03690065
                                                                        03700065
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        03710065
               MOVE '8000-GET-RPL-CTL' TO  PV-PARAGRAPH-NAME            03720065
               MOVE 'RETRIES EXCEEDED FOR TRPLCTL'                      03730065
                                      TO  PV-DB2-OPER-ATTEMPTED         03740065
               PERFORM 9900-SQL-ABEND                                   03750065
           END-IF.                                                      03760065
      *----------------------------------------------------------------*03770065
       9000-WRITE-OUT01-LINE.                                           03780065
      *    WRITE THE SELECT STATEMENT LINES.                           *03790065
      *----------------------------------------------------------------*03800065
           DISPLAY PV-PRINT-LINE.                                       03810065
           WRITE OUT01-REC                                              03820065
                     FROM PV-PRINT-LINE.                                03830065
           ADD PC-ONE TO PV-REC-COUNT.                                  03840065
           MOVE SPACES TO PV-PRINT-LINE.                                03850065
      *----------------------------------------------------------------*03860065
       9010-WRITE-OUT03-LINE.                                           03870065
      *    WRITE THE SELECT STATEMENT LINES.                           *03880065
      *----------------------------------------------------------------*03890065
           DISPLAY PV-PRINT-LINE.                                       03900065
           WRITE OUT03-REC                                              03910065
1022RM*              FROM PV-PRINT-LINE.                                03920068
1022RM               FROM PV-PRINT-LINE-FILE-3.                         03921068
           ADD PC-ONE TO PV-REC3-COUNT.                                 03930065
           MOVE SPACES TO PV-PRINT-LINE.                                03940065
      *----------------------------------------------------------------*03950065
       9100-WRITE-OUT02-LINE.                                           03960065
      *    WRITE THE UPDATE STATEMENT LINES.                           *03970065
      *----------------------------------------------------------------*03980065
           DISPLAY PV-PRINT-LINE.                                       03990065
           WRITE OUT02-REC                                              04000065
                     FROM PV-PRINT-LINE.                                04010065
           ADD PC-ONE TO PV-REC-COUNT.                                  04020065
           MOVE SPACES TO PV-PRINT-LINE.                                04030065
      *----------------------------------------------------------------*04040065
      *                                                                *04050065
      *----------------------------------------------------------------*04060065
       9900-SQL-ABEND.                                                  04070065
                                                                        04080065
           SET ABEND-4001             TO TRUE.                          04090065
           DISPLAY '**  ABEND     **'.                                  04100065
           DISPLAY '**  PROGRAM   **  =  MAKUT103'.                     04110065
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            04120065
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        04130065
                                                                        04140065
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         04150065
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        04160065
                                                                        04170065
           COPY DPPD004.                                                04180065
                                                                        04190065
           CALL 'ILBOABN0'  USING ABEND-CODE.                           04200065
