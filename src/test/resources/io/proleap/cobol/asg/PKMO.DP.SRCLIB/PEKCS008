00001  IDENTIFICATION DIVISION.                                                 
00002  PROGRAM-ID.    PEKCS008.                                                 
00003  AUTHOR.        AMY REILAND.                                              
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00005  DATE-WRITTEN.  05-19-92.                                                 
00006  DATE-COMPILED.                                                           
00007 ******************************************************************        
00008 *  PEKCS008 RECEIVES THE DOCUMENT NUMBER OR TEMPORARY STORAGE    *        
00009 *  QUEUE OF DOCUMENT NUMBERS WHICH NEED AN IN-STORE DOCUMENT     *        
00010 *  PRINTED.                                                      *        
00011 *  THE DYNAMIC REPORT SYSTEM (DRS) IS USED TO CREATE THE         *        
00012 *  DOCUMENT.                                                     *        
00013 *                                                                *        
00014 * INPUT FILES:  NONE                                             *        
00015 *                                                                *        
00016 * OUTPUT FILES: PERMANENT PRICE CHANGE IN-STORE DOCUMENT         *        
00017 *                                                                *        
00018 * TABLES:       TSTRPPC (READ)                                   *        
00019 *               TSPPCDT (READ)                                   *        
00020 *               TSTR000 (READ)                                   *        
00021 *               TPPCCTL (READ)                                   *        
00022 *                                                                *        
00023 * DATA BASES:   NONE                                             *        
00024 *                                                                *        
00025 *                                                                *        
00026 ******************************************************************        
00027 * 02/03/95 - TCM -  WR NO. 3289                                  *        
00028 *    - ADD CODING TO B100-INITIALIZE TO PROCESS LOCATION TYPE:   *        
00029 *      RV - RETURN TO VENDOR                                     *        
00030 ******************************************************************        
00027 * 06/06/96 - TCM -  WR NO. 3877                                  *        
00028 *    - CHANGE PC-CLASS-L TO PC-CLASS-V, PC-FORM-8001 TO          *        
00029 *      PC-FORM-8002                                              *        
00020 *    - REPLACE ALL "++INCLUDE" REFERENCES WITH "COPY"            *        
00020 *      REFERENCES, PER STANDARDS                                 *        
00030 ******************************************************************        
00025 * 06/26/96 - TCM -  WR NO. 3966                                  *        
00020 *    - REPLACE PC-DISTRIBUTION-CENTER, PC-RET-TO-VENDOR WITH     *        
00020 *      APPROPRIATE PE049-LOCATION-TYPE-CODES                     *        
00020 *    - ADD PE049-CENT-STK-LOC-TYP-CDE TO B100-INITIALIZE         *        
00020 *      PARAGRAPH PROCESSING                                      *        
00030 ******************************************************************        
00025 * 04/14/97 - MARK PRABUCKI - WR 5171                             *        
00020 *    - REPLACE U9-PRT-DEST WITH A PEWS049 DEFINED DEST.          *        
      *      NAME: PE049-PRT-DEST                                      *        
00020 *      CHANGED DEST FROM U9 TO U6.                               *        
      *    - CHANGED COLS 72-78 TO SPACES ON ANY CHANGED OR ADDED LINES*        
00030 ******************************************************************        
010970*   08/28/97 - JIM ARENDT  - WR 5541                             *        
010970*    - CHANGED HEADING TEXT TO 'GRAND TOTAL PIECES:' INSTEAD OF  *        
010970*      'TOTAL QUANTITY:'.                                        *        
010970*    - ADDED A SUBTOTAL LINE 'TOTAL THIS PAGE__________' TO THE  *        
010970*      DOCUMENT PRINT.                                           *        
010970*    - MOVED HEADING TEXT '* - REDUCTION IN UNITS' TO THE LEFT   *        
010970*      MARGIN.                                                   *        
010970*    - SEE PEPD003 -                                             *        
00030 ******************************************************************        
01097 *   02/17/98 - JIM ARENDT  - WR 5747                             *        
00028 *    - CHANGED PC-FORM-8002, PC-FORM-STD TO PE049-PRT-FORM-STD.  *        
00028 *    - CHANGED PC-CLASS-A, PC-CLASS-V    TO PE049-PRT-CLASS-A.   *        
      *    - ADDED PAGEDEF/FORMDEF TO DRS LOGIC TO ALLOW LANDSCAPE     *        
      *      PRINTING ON ALL MODELS OF PRINTERS.  DO THIS BY ADDING    *        
      *      COPYBOOK DRRD005 (OUTPUT CALL) AND REPLACING COPYBOOK     *        
      *      DRRD002 WITH DRRD006, SO AN OUTPUT STATEMENT NAME CAN     *        
      *      BE ADDED LOGICALLY CONNECTING THE 'OUTP'/'INIT' RECORDS.  *        
      ******************************************************************        
      *   04/06/98 - TODD C. MICHALEK - WR NO. P000176386              *        
      *    - REPLACE LITERAL ("PEKCS008") DRDRS05-DROB-OUTPUT-ID/      *        
      *      DRS06-DRIB-OUTPUT-STMT-NAME (THIS IS THE OUTPUT BLOCK'S   *        
      *      DDNAME) WITH A UNIQUE NAME ("P"+DOCUMENT-NUMBER) TO KEEP  *        
      *      THE OUTPUT BLOCK FROM GETTING DELETED BEFORE PROCESSING   *        
      *      IS COMPLETED (THIS HAPPENS WHEN THE PROGRAM CANNOT        *        
      *      PROCESS QUICKLY ENOUGH THE SUCCESSIVE REQUESTS FOR THE    *        
      *      SAME TYPE/REASON DOCUMENTS).                              *        
      *    - MOVE THE DELETE OF THE OUTPUT BLOCK TO THE END OF         *        
      *      PROCESSING (Z100-CLOSE-FILES), FROM THE BEGINNING OF      *        
      *      PROCESSING (B100-INITIALIZE) (DELETE THE OUTPUT BLOCK     *        
      *      AFTER IT'S NEEDED).  ALSO, ADDED THE DELETE TO THE        *        
      *      A100-MAIN-MODULE PARAGRAPH FOR                            *        
      *      "PE056-PRINT-DOCUMENT-NUMBER = ZEROS" ERROR PROCESSING.   *        
      *    - REMOVED Z225-LINK-TO-DRS PARAGRAPH, THE OUTPUT BLOCK WILL *        
      *      ALWAYS EXIST BEFORE A DELETE IS ATTEMPTED, THEREFORE      *        
      *      ONE DOES NOT HAVE TO WORRY ABOUT DELETING AN OUTPUT       *        
      *      BLOCK THAT MIGHT NOT EXIST.                               *        
W24472******************************************************************        
W24472* 03/03/03 - ABRAHAM JACOB, IBM GLOBAL SERVICES    WR 24472      *        
W24472*    - TSKXREF TABLE IS ADDED.                                   *        
W24472******************************************************************        
01097 * 01/09/04 - JIM ARENDT  -  P000622639                           *        
00028 *  - CHANGED PROGRAM TO PRINT WEB DOCUMENTS TO CLASS 'Z'.        *        
00031 ******************************************************************        
01097 * 09/16/2004 - TODD C. MICHALEK - WR 27209                       *        
00028 *  - DIRECTED THE STORE GENERATED IN-STORES TO THE NEW SXXXXXYN  *        
00028 *    PRINTER THAT WAS ADDED FOR THE 4 DIGIT STORE VPS PRINTER    *        
00028 *    PROJECT, BY ADDING THE FOLLOWING FIELDS TO PV-DEST:         *        
00028 *    FILLER, PV-DEST-STORE-PRINTER                               *        
00031 ******************************************************************        
      * 02/11/05 - TCM -  WR NO. 27559                                 *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH                                          *        
      *    - REPLACED TSTR000 STORE TABLE CALL(S) WITH STORE TABLE     *        
      *      CALL(S) TO XST_LOC                                        *        
      *    - CHANGED THE FILLER REDEFINES DP020-VARIABLE-COMMAREA      *        
      *      FILLER LENGTH AND THE APPROPRIATE MNEMONIC LENGTH         *        
      ******************************************************************        
      * 02/14/05 - TCM -  WR NO. 27559                                 *        
      *    - REMOVED THE SIGN (S) OFF THE PIC S9(04) FIELDS:           *        
      *      PV-DEST-STORE                                             *        
      *      PC-CORPORATE-STORE-NUMBER                                 *        
      *      PC-FINDLAY-DC                                             *        
      ******************************************************************        
      * 10/26/05 - TCM -  WR NO. 28048                                 *        
      *    - IN PEPD003 ADDED THE NEW REASON CODE ("89") FOR           *        
      *      "MARK OUT-OF-STOCK - SALVAGE" IN-STORE DOCUMENTS          *        
      ******************************************************************        
EFC2  * 03/19/10 - CTS -  WR NO. 38216                                 *        
||    *    - PREVENT SENDING PERM PRICE CHANGES TO EFC LOCATION 809    *        
||    *    - IN B1000-INITIALIZE ADDED ONE MORE LOCATION 809 IN THE    *        
||    *      IF CONDITION PV-DEST-STORE                                *        
EFC2  ******************************************************************        
      * 08/18/10 - COGNIZANT (SANDEEP) - WR NO. 38972                  *        
      *    - REMOVE THE HARD CODED EFC LOCATION VALUES 809 AND 873     *        
      *    - VALIDATE THE STORE WHETHER IT IS A E-COMM STORE IN THE    *        
      *      D200- PARAGRAPH AND THEN SET THE PRINT CLASS.             *        
      ******************************************************************        
232969* 10/04/19 - JIM HUNTER - MAINFRAME REFACTORING   CHG0232969     *        
232969*    - SET THE PRINTER ID AND PRINT CLASS IN THE DRS05-COMMAREA  *        
232969*      BEFORE PERFORMING Z250-LINK-TO-DRS IN PARAGRAPH           *        
232969*      B100-INITIALIZE.                                          *        
232969*      PRINT PROCESS WORKS ON THE MAINFRAME WITHOUT THIS CHANGE  *        
232969*      BUT NOT ON THE REFACTORED SIDE.                           *        
233646******************************************************************        
233646* 10/14/19 - JIM HUNTER - MAINFRAME REFACTORING   CHG0233646     *        
233646*    - RECOMPILE WITH UPDATED COPYBOOK PEPD003 THAT HAS THE      *        
233646*      "WITH UR" CLAUSE IN THE SELECT STATEMENT.                 *        
233646*      PRINT PROCESS WORKS ON THE MAINFRAME WITHOUT THIS CHANGE  *        
233646*      BUT NOT ON THE REFACTORED SIDE.                           *        
233646******************************************************************        
00032  ENVIRONMENT DIVISION.                                                    
00033 *                                                                         
00034  CONFIGURATION SECTION.                                                   
00035 *                                                                         
00036  SOURCE-COMPUTER.        IBM-3090.                                        
00037  OBJECT-COMPUTER.        IBM-3090.                                        
00038 *                                                                         
00039 *                                                                         
00040 *                                                                         
00041 ******************************************************************        
00042  DATA DIVISION.                                                           
00043 ******************************************************************        
00044 *                                                                         
00045 *                                                                         
00046 ******************************************************************        
00047  WORKING-STORAGE SECTION.                                                 
00048 ******************************************************************        
00049                                                                           
00050 ******************************************************************        
00051 * COMMAREA PASSED TO THE PROGRAM                                 *        
00052 ******************************************************************        
00053      COPY PEWS056.                                                        
00054                                                                           
00055 ******************************************************************        
00056 * PV PROGRAM VARIABLES                                           *        
00057 ******************************************************************        
00058  01  PV-PROGRAM-VARIABLES.                                                
00059      05  PV-ABSTIME                      PIC S9(18)  VALUE +0             
00060                                                      COMP SYNC.           
00061                                                                           
00062      05  PV-CICS-RESPONSE                PIC S9(04)  VALUE +0             
00063                                                      COMP SYNC.           
00064                                                                           
00065      05  PV-CONTROL-NUMBER               PIC S9(03)  VALUE 0              
00066                                                      COMP-3.              
00067                                                                           
00068      05  PV-DATE.                                                         
00069          10  PV-MM                       PIC 9(02)   VALUE 0.             
00070          10  FILLER                      PIC X(01)   VALUE SPACE.         
00071          10  PV-DD                       PIC 9(02)   VALUE 0.             
00072          10  FILLER                      PIC X(01)   VALUE SPACE.         
00073          10  PV-YY                       PIC 9(02)   VALUE 0.             
00074      05  PV-DEST.                                                         
00075          10  PV-DEST-CODE                PIC X(02)   VALUE SPACES.        
00076          10  PV-DEST-STORE               PIC 9(04)   VALUE ZEROS.         
               10  PV-DEST-STORE-PRINTER       PIC X(02)   VALUE SPACES.        
00077                                                                           
           05  PV-DRS-DDNAME.                                                   
               10  FILLER                      PIC  X(01)  VALUE 'P'.           
               10  PV-DRS-DOCUMENT-NUMBER      PIC  9(07).                      
                                                                                
00078      05  PV-DRS-ABEND-DISPLAY-LINE1.                                      
00079          10  FILLER                      PIC  X(14)  VALUE                
00080              'DRRB ID = DRRB'.                                            
00081          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00082          10  FILLER                      PIC  X(12)  VALUE                
00083              'DRRB FUNC = '.                                              
00084          10  PV-DRS-DRRB-FUNC            PIC  X(04)  VALUE SPACE.         
00085          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00086          10  FILLER                      PIC  X(11)  VALUE                
00087              'DRRB RID = '.                                               
00088          10  PV-DRS-DRRB-RID             PIC  X(08)  VALUE SPACE.         
00089          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00090          10  FILLER                      PIC  X(15)  VALUE                
00091              'DRRB OPTIONS = '.                                           
00092          10  PV-DRS-DRRB-OPTIONS         PIC  X(08)  VALUE SPACE.         
00093                                                                           
00094      05  PV-DRS-ABEND-DISPLAY-LINE2.                                      
00095          10  FILLER                      PIC  X(10)  VALUE                
00096              'DRRB RC = '.                                                
00097          10  PV-DRS-DRRB-RC              PIC  9(04)  VALUE ZERO.          
00098          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00099          10  FILLER                      PIC  X(18)  VALUE                
00100              'DRRB ATTR-ERROR = '.                                        
00101          10  PV-DRS-ATTR-ERROR           PIC  9(02)  VALUE ZERO.          
00102          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00103          10  FILLER                      PIC  X(12)  VALUE                
00104              'DRRB DRDC = '.                                              
00105          10  PV-DRS-DRRB-DRDC            PIC  9(04)  VALUE ZERO.          
00106          10  FILLER                      PIC  X(02)  VALUE SPACE.         
00107          10  FILLER                      PIC  X(13)  VALUE                
00108              'DRRB LINES = '.                                             
00109          10  PV-DRS-DRRB-LINES           PIC  9(04)  VALUE ZERO.          
00110                                                                           
00111      05  PV-ITEM-NUMBER                  PIC S9(04)  VALUE +0             
00112                                                      COMP SYNC.           
00113                                                                           
00114      05  PV-MESSAGE-1.                                                    
00115          10  FILLER                      PIC X(34)   VALUE                
00116              '  PEKCS008 - DOC/QUEUE NOT ENTERED'.                        
00117                                                                           
00118      05  PV-MESSAGE-2.                                                    
00119          10  FILLER                      PIC X(34)   VALUE                
00120              'PEKCS008 - HEADER NOT FOUND FOR '.                          
00121          10  PV-MESSAGE-2-DOC-NBR        PIC 9(07)   VALUE ZEROS.         
00122                                                                           
00123      05  PV-MESSAGE-3.                                                    
00124          10  FILLER                      PIC X(78)   VALUE                
00125              'PEKCS008 - INVALID COMMAREA'.                               
00126                                                                           
00127      05  PV-MESSAGE                      PIC X(78)   VALUE SPACE.         
00128                                                                           
00129      05  PV-STORE-NUM                    PIC S9(04)  VALUE ZEROS          
                                                           COMP.                
00130                                                                           
00131      05  PV-TIME.                                                         
00132          10  PV-HH                       PIC 9(02)   VALUE 0.             
00133          10  FILLER                      PIC X(01)   VALUE SPACE.         
00134          10  PV-MIN                      PIC 9(02)   VALUE 0.             
00135          10  FILLER                      PIC X(03)   VALUE SPACE.         
00135                                                                           
00136 ******************************************************************        
00137 * PC PROGRAM CONSTANTS                                           *        
00138 ******************************************************************        
00139  01  PC-PROGRAM-CONSTANTS.                                                
00140      05  PC-PRINT-QUEUE-LENGTH           PIC S9(04)  VALUE +20            
00141                                                      COMP SYNC.           
00142      05  PC-CORPORATE-STORE-NUMBER       PIC  9(04)  VALUE 0090.          
00145      05  PC-COMMAREA-LENGTH              PIC S9(04)  VALUE +35            
00146                                                      COMP SYNC.           
00147      05  PC-INIT                         PIC  X(04)  VALUE                
00148                                                      'INIT'.              
00149      05  PC-MAX-DT-INDEX                 PIC S9(04)  VALUE +13.           
00150      05  PC-MESSAGE-LENGTH               PIC S9(04)  VALUE +78            
00151                                                      COMP SYNC.           
00152      05  PC-OP-CO-NBR                    PIC  X(03)  VALUE '03'.          
00153      05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE                
00154                                                      'PEKCS008'.          
00155      05  PC-PUT                          PIC  X(04)  VALUE                
00156                                                      'PUT '.              
00157      05  PC-TERM                         PIC  X(04)  VALUE                
00158                                                      'TERM'.              
           05  PC-FINDLAY-DC                   PIC  9(04)  VALUE 0810.          
           05  PC-OUTP                         PIC  X(04)  VALUE 'OUTP'.        
           05  PC-PAGEDEF-FORMDEF              PIC  X(08)  VALUE                
                                                           'L1S002'.            
622639     05  PC-PRT-CLASS-Z                  PIC  X(01)  VALUE 'Z'.           
00165**    05  PC-U9-DEST                      PIC  X(02)  VALUE 'U9'.          
00167                                                                           
00136 ******************************************************************        
00137 * PROGRAM SWITCHES                                               *        
00138 ******************************************************************        
00139  01  SWITCHES.                                                            
00140      05  E-BUS-IND-SW                    PIC X(1)    VALUE 'N'.           
00140          88  E-BUS-IND-NO                            VALUE 'N'.           
00140          88  E-BUS-IND-YES                           VALUE 'Y'.           
00140                                                                           
00168 ******************************************************************        
00169 *    DOCUMENT QUEUE RECORD FOR TS QUEUE PASSED FROM OTHER        *        
00170 *    APPLICATIONS.                                               *        
00171 *                                                                *        
00172 ******************************************************************        
00173  01  DOCUMENT-PRINT-RECORD.                                               
00174      COPY PEWS065.                                                        
00175                                                                           
00176 *                                                                         
00177 *   DB2 PERM IN-STORE PRICE CHANGE HEADER TABLE                           
00178 *                                                                         
00179       EXEC SQL                                                            
00180           INCLUDE TSTRPPC                                                 
00181       END-EXEC.                                                           
00182  EJECT                                                                    
00183 *                                                                         
00184 *   DB2 PERM IN-STORE PRICE CHANGE DETAIL TABLE                           
00185 *                                                                         
00186       EXEC SQL                                                            
00187           INCLUDE TSPPCDT                                                 
00188       END-EXEC.                                                           
00189  EJECT                                                                    
00190 *                                                                         
00191 *   SELLING AND CUSTOMER SERVICE LOCATION TABLE                           
00192 *                                                                         
00193       EXEC SQL                                                            
00194           INCLUDE XST00001                                                
00195       END-EXEC.                                                           
00196  EJECT                                                                    
00197 *                                                                         
00198 *   DB2 PERM CONTROL VALUES TABLE                                         
00199 *                                                                         
00200       EXEC SQL                                                            
00201           INCLUDE TPPCCTL                                                 
00202       END-EXEC.                                                           
00203  EJECT                                                                    
W24472******************************************************************        
W24472*                                                 WR# 24472      *        
W24472*   DB2 SKU CROSS REFERENCE TABLE                                *        
W24472******************************************************************        
W24472*                                                                         
W24472      EXEC SQL                                                            
W24472          INCLUDE TSKXREF                                                 
W24472      END-EXEC.                                                           
W24472 EJECT                                                                    
00204 *                                                                         
00205 *   DB2 COMMUNICATIONS AREA                                               
00206 *                                                                         
00207       EXEC SQL                                                            
00208           INCLUDE SQLCA                                                   
00209       END-EXEC.                                                           
00210 *                                                                         
00211 *  LAYOUT FOR DRS COMMAREA                                                
00212 *                                                                         
00213      COPY DRRD001O.                                                       
00214                                                                           
00182  EJECT                                                                    
      *                                                                         
      *  LAYOUT FOR THE DRS COMMAREA WITH OUTPUT BLOCK                          
      *                                                                         
           COPY DRRD005.                                                        
      *                                                                         
      *  LAYOUT FOR THE DYNAMIC REPORT DETAIL LINES                             
      *                                                                         
           COPY DRRD006.                                                        
00221 *                                                                         
00222 *  PERM PRICE CHANGE CONSTANTS AND WORK AREAS                             
00223 *                                                                         
00224      COPY PEWS049.                                                        
00182  EJECT                                                                    
00226 *                                                                         
00227 *  PERM PRICE CHANGE CONTROL NUMBERS AND AMOUNTS                          
00228 *                                                                         
00229      COPY PEWS048.                                                        
       EJECT                                                                    
00231 *                                                                         
00232 *  LAYOUT FOR THE ABEND COMMUNICATIONS AREA                               
00233 *                                                                         
00234      COPY DPWS013.                                                        
00235 *                                                                         
00236 *  LAYOUT FOR THE STANDARD 132 WIDE REPORT HEADER                         
00237 *                                                                         
00238      COPY DPWS132O.                                                       
       EJECT                                                                    
00240 *                                                                         
00241 *  WORKING STORAGE AREA FOR THE PROCEDURE DIVISION (PEPD003) WHICH        
00242 *  FORMATS THE DOCUMENT.                                                  
00243 *                                                                         
00244      COPY PEWS053.                                                        
       EJECT                                                                    
00246                                                                           
00247  LINKAGE SECTION.                                                         
00248                                                                           
00251 ******************************************************************        
00252  PROCEDURE DIVISION.                                                      
00253 ******************************************************************        
00254 *                                                                         
00255 ******************************************************************        
00256 * A100-MAIN-MODULE  - HIGHEST LEVEL CONTROL ROUTINE.             *        
00257 ******************************************************************        
00258  A100-MAIN-MODULE.                                                        
00259                                                                           
00260      EXEC CICS                                                            
00261          RETRIEVE                                                         
00262          INTO(PE056-SPECIFIC-COMMAREA)                                    
00263          LENGTH(PC-COMMAREA-LENGTH)                                       
00264          RESP(PV-CICS-RESPONSE)                                           
00265      END-EXEC.                                                            
00266                                                                           
00267      IF PV-CICS-RESPONSE = DFHRESP(NORMAL)                                
00268          PERFORM B100-INITIALIZE                                          
00269          IF PE056-PRINT-TRANID = SPACES                                   
00270              IF  PE056-PRINT-DOCUMENT-NUMBER = ZEROS                      
      *  NOTE: IF 'OUTP' PARAMETERS ARE CHANGING BETWEEN RUNS, IT IS            
      *        NECESSARY TO 'DEL'ETE THE 'OUTP' OUTPUT-ID AND THEN              
      *        RE-'ADD' IT.  THIS IS THE CASE BECAUSE CICS MAKES AN             
      *        INITIAL REFERENCE, AND HOLDS THESE PARAMETERS UNTIL A            
      *        DELETE/ADD IS EXECUTED, OR UNTIL CICS IS RECYCLED.               
                       MOVE PC-OUTP    TO DRS05-DRRB-FUNC                       
                       MOVE 'DEL'      TO DRS05-DROB-FUNCTION                   
                       MOVE PV-DRS-DDNAME                                       
                                       TO DRS05-DROB-OUTPUT-ID                  
                       PERFORM Z250-LINK-TO-DRS                                 
00271                  SET DP013-LOGIC-ABEND TO TRUE                            
00272                  MOVE 'A100-MAIN-MODULE'                                  
00273                                  TO DP013-PARAGRAPH                       
00274                  MOVE PV-MESSAGE-1                                        
00275                                  TO DP013-MESSAGE-TEXT(1)                 
00276                  PERFORM Z999-ABEND                                       
00277              ELSE                                                         
00278                  MOVE PE056-PRINT-DOCUMENT-NUMBER                         
00279                                  TO PE053-PV-DOCUMENT-TO-PROCESS          
00280                  PERFORM C100-PROCESS-DOCUMENT                            
00281              END-IF                                                       
00282          ELSE                                                             
00283              MOVE 1              TO PV-ITEM-NUMBER                        
00284              PERFORM Z300-READ-TSQ                                        
00285                                                                           
00286              PERFORM                                                      
00287                  UNTIL PV-CICS-RESPONSE = DFHRESP(ITEMERR)                
00288                                                                           
00289                  MOVE PE065-DOCUMENT-NUMBER                               
00290                                  TO PE053-PV-DOCUMENT-TO-PROCESS          
00291                                     PE056-PRINT-DOCUMENT-NUMBER           
00292                  PERFORM C100-PROCESS-DOCUMENT                            
00293                                                                           
00294                  ADD 1           TO  PV-ITEM-NUMBER                       
00295                  PERFORM Z300-READ-TSQ                                    
00296              END-PERFORM                                                  
00297                                                                           
00298              PERFORM Z400-DELETE-TSQ                                      
00299          END-IF                                                           
00300          PERFORM Z100-CLOSE-FILES                                         
00301      ELSE                                                                 
00302          SET DP013-CICS-ABEND    TO TRUE                                  
00303          MOVE 'A100-MAIN-MODULE' TO DP013-PARAGRAPH                       
00304          MOVE PV-MESSAGE-3       TO DP013-MESSAGE-TEXT(1)                 
00305          PERFORM Z999-ABEND                                               
00306      END-IF.                                                              
00307                                                                           
00309      EXEC CICS                                                            
00310          RETURN                                                           
00311      END-EXEC.                                                            
00313                                                                           
00314      GOBACK.                                                              
00313                                                                           
       EJECT                                                                    
00316 ******************************************************************        
00317 * B100-INITIALIZE.                                                        
00318 * ==> PROCESSES:                                                          
00319 *   - GETS THE DATE AND TIME AND PROVIDES THE INFO NEEDED TO              
00320 *     THE PROCEDURE DIVISION WHICH FORMATS THE DOCUMENT.                  
00321 *   - GETS THE STORE NAME AND NUMBER OF LINES PER DOC                     
00322 *     PERFORMED BY : A100-MAIN-MODULE                                     
00323 ******************************************************************        
00324  B100-INITIALIZE.                                                         
00325                                                                           
00326      EXEC CICS                                                            
00327          ASKTIME ABSTIME (PV-ABSTIME)                                     
00328      END-EXEC.                                                            
00329                                                                           
00330      EXEC CICS                                                            
00331          FORMATTIME ABSTIME (PV-ABSTIME)                                  
00332                     MMDDYY  (PV-DATE)                                     
00333                     DATESEP                                               
00334                     TIME (PV-TIME)                                        
00335                     TIMESEP                                               
00336      END-EXEC.                                                            
00337                                                                           
00339      MOVE PV-MM                  TO PE053-PV-MM.                          
00340      MOVE PV-DD                  TO PE053-PV-DD.                          
00341      MOVE PV-YY                  TO PE053-PV-YY.                          
00342      MOVE PV-HH                  TO PE053-PV-HH.                          
00343      MOVE PV-MIN                 TO PE053-PV-MIN.                         
                                                                                
           MOVE PC-OUTP                TO DRS05-DRRB-FUNC.                      
           MOVE 'ADD'                  TO DRS05-DROB-FUNCTION.                  
           MOVE PE056-PRINT-DOCUMENT-NUMBER                                     
                                       TO PV-DRS-DOCUMENT-NUMBER.               
           MOVE PV-DRS-DDNAME          TO DRS05-DROB-OUTPUT-ID.                 
           MOVE PC-PAGEDEF-FORMDEF     TO DRS05-DROB-FORMDEF                    
                                          DRS05-DROB-PAGEDEF.                   
                                                                                
00393      PERFORM D200-GET-STORE-NAME.                                         
00344                                                                           
00345      IF PE056-PRINT-AT-STORE-NUMBER = PC-CORPORATE-STORE-NUMBER           
00346 **     THE FOLLOWING LINES ARE USED TO PRINT AT CORP ON LASER OR          
00347 **     AT A SPECIFIED PRINTER                                             
               MOVE PE049-PRT-CLASS-A  TO DRS06-DRIB-CLASS                      
00348          IF PE056-PRINTER-ID NOT = SPACES                                 
00349              MOVE PE056-PRINTER-ID                                        
00350                                  TO DRS06-DRIB-DEST                       
00353          ELSE                                                             
                   MOVE PE049-PRT-LOCAL                                         
                                       TO DRS06-DRIB-DEST                       
00357          END-IF                                                           
00358      ELSE                                                                 
00359          IF (PE056-PRINT-AT-LOC-TYPE =                                    
00359                                 PE049-DISTRB-CNTR-LOC-TYP-CDE             
00360          OR  PE056-PRINT-AT-LOC-TYPE =                                    
00360                                 PE049-RET-TO-VND-LOC-TYP-CDE              
00359          OR  PE056-PRINT-AT-LOC-TYPE =                                    
00359                                 PE049-CENT-STK-LOC-TYP-CDE)               
00359              IF PE056-PRINT-AT-STORE-NUMBER = PC-FINDLAY-DC               
                       MOVE PE049-PRT-CLASS-3                                   
                                       TO DRS06-DRIB-CLASS                      
                   ELSE                                                         
                       MOVE PE049-PRT-CLASS-A                                   
                                       TO DRS06-DRIB-CLASS                      
                   END-IF                                                       
                   MOVE PE056-PRINTER-ID                                        
                                       TO DRS06-DRIB-DEST                       
               ELSE                                                             
                   MOVE PE049-PRT-DEST-STORE                                    
                                       TO PV-DEST-CODE                          
                   MOVE PE056-PRINT-AT-STORE-NUMBER                             
                                       TO PV-DEST-STORE                         
                   MOVE PE049-PRT-DEST-STORE-MAIN                               
                                       TO PV-DEST-STORE-PRINTER                 
                   MOVE PV-DEST        TO DRS06-DRIB-DEST                       
                   IF E-BUS-IND-YES                                             
622639                MOVE PC-PRT-CLASS-Z                                       
622639                                 TO DRS06-DRIB-CLASS                      
622639             ELSE                                                         
                      MOVE PE049-PRT-CLASS-A                                    
                                       TO DRS06-DRIB-CLASS                      
622639             END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PE049-PRT-FORM-STD     TO DRS06-DRIB-FORM.                      
                                                                                
232969     MOVE DRS06-DRIB-DEST        TO DRS05-DROB-DESTINATION.               
232969     MOVE DRS06-DRIB-CLASS       TO DRS05-DROB-CLASS.                     
                                                                                
           PERFORM Z250-LINK-TO-DRS.                                            
                                                                                
           MOVE PV-DRS-DDNAME          TO DRS06-DRIB-OUTPUT-STMT-NAME.          
           MOVE 'Y'                    TO DRS06-DRIB-EXTN-IND.                  
                                                                                
00338      MOVE PC-INIT                TO DRS01-DRRB-FUNC.                      
00375      MOVE DRS06-DRIB-HEADER      TO DRS01-COMM-DRIB-HDR.                  
00376      MOVE DRS06-DRIB-ATTRIB      TO DRS01-COMM-DRIB-AT1.                  
00377                                                                           
00378      PERFORM Z200-LINK-TO-DRS.                                            
00379                                                                           
00380      MOVE PC-PUT                 TO DRS01-DRRB-FUNC.                      
00381      MOVE PC-CURRENT-PROGRAM-NAME                                         
00382                                  TO PE053-PC-PROGRAM.                     
00383      MOVE PE056-PRINT-TYPE       TO PE053-PV-PRINT-INDICATION.            
00384      MOVE PE056-MAX-PAGES-TO-PRINT                                        
00385                                  TO PE053-PV-PAGES-TO-PRINT.              
00386      MOVE PE048-INSTR-PAGES-PER-DOC-NBR                                   
00387                                  TO PV-CONTROL-NUMBER.                    
00388      PERFORM D100-GET-CONTROL-NUMBER.                                     
00389      MOVE PPCCTL-PARM-AMT        TO PE048-INSTR-PAGES-PER-DOC-AMT.        
00390                                                                           
00391      MOVE PE056-PRINT-FOR-STORE-NUMBER                                    
00392                                  TO PE053-PV-STORE-TO-PROCESS.            
00395                                                                           
       EJECT                                                                    
00397 ******************************************************************        
00398 * C100-PROCESS-DOCUMENT.                                                  
00399 * ==> PROCESSES:                                                          
00400 *   - PERFORMS THE PARAGRAPH THAT FORMATS THE DOCUMENT                    
00401 *   - PERFORMS THE PARAGRAPH THAT PRINTS THE DOUCUMENT                    
00402 *     PERFORMED BY : A100-MAIN-MODULE                                     
00403 ******************************************************************        
00404  C100-PROCESS-DOCUMENT.                                                   
00395                                                                           
00405      PERFORM PE003-A000-FORMAT-DOCUMENT                                   
00406      IF PE053-PS-NO-ABEND-ERROR                                           
00407          IF PE053-PS-HEADER-FOUND                                         
00408              PERFORM C200-PRINT-REPORT                                    
00409                  VARYING PE053-DT-INDEX                                   
00410                  FROM 1 BY 1                                              
00411                  UNTIL (PE053-DT-INDEX > PE053-PC-MAX-DT-INDEX            
00412                     OR  PE053-DT-INDEX > PE053-PV-DT-ENTRIES)             
00413          ELSE                                                             
00414              MOVE PE056-PRINT-DOCUMENT-NUMBER                             
00415                                  TO PV-MESSAGE-2-DOC-NBR                  
00416              SET DP013-LOGIC-ABEND TO TRUE                                
00417              MOVE 'C100-PROCESS-DOCUMENT'                                 
00418                                  TO DP013-PARAGRAPH                       
00419              MOVE PV-MESSAGE-2   TO DP013-MESSAGE-TEXT(1)                 
00420              PERFORM Z999-ABEND                                           
00421          END-IF                                                           
00422      ELSE                                                                 
00423          IF PE053-PV-ABEND-SQLCODE = ZEROS                                
00424              SET DP013-LOGIC-ABEND TO TRUE                                
00425          ELSE                                                             
00426              SET DP013-DB2-ABEND TO TRUE                                  
00427          END-IF                                                           
00428          MOVE PE053-PV-ABEND-PARAGRAPH                                    
00429                                  TO DP013-PARAGRAPH                       
00430          MOVE PE053-PV-ABEND-MESSAGE                                      
00431                                  TO DP013-MESSAGE-TEXT(1)                 
00432          PERFORM Z999-ABEND                                               
00433      END-IF.                                                              
00444                                                                           
       EJECT                                                                    
00435 ******************************************************************        
00436 * C200-PRINT-REPORT                                                       
00437 * ==> PROCESSES:                                                          
00438 *   - LOOPS THRU THE DOCUMENT TABLE TO PRINT THE DOCUMENT LINES.          
00439 *     PERFORMED BY : C100-PROCESS-DOCUMENT.                               
00440 ******************************************************************        
00441  C200-PRINT-REPORT.                                                       
00444                                                                           
00442      MOVE PE053-DT-DATA(PE053-DT-INDEX) TO DRS01-COMM-DATA.               
00443      MOVE PE053-DT-CTL(PE053-DT-INDEX)  TO DRS01-COMM-CTL.                
00444      PERFORM Z200-LINK-TO-DRS.                                            
00444                                                                           
       EJECT                                                                    
00446 ******************************************************************        
00447 * D100-GET-CONTROL-NUMBER                                                 
00448 * ==> PROCESSES:                                                          
00449 *   - GETS THE NUMBER OF PAGES PER DOCUMENT CONTROL NUMBER                
00450 *     FROM THE TPPCCTL TABLE.                                             
00451 *     PERFORMED BY : B100-INITIALIZE.                                     
00452 ******************************************************************        
00453  D100-GET-CONTROL-NUMBER.                                                 
00454                                                                           
00455      EXEC SQL                                                             
00456          SELECT PARM_AMT                                                  
00457          INTO   :PPCCTL-PARM-AMT                                          
00458          FROM   TPPCCTL                                                   
00459          WHERE (OPERCO_NBR = :PC-OP-CO-NBR                                
00460            AND  PPCCTL_NBR = :PV-CONTROL-NUMBER)                          
00461      END-EXEC.                                                            
00462                                                                           
00463      EVALUATE TRUE                                                        
00464          WHEN SQLCODE = 0                                                 
00465              CONTINUE                                                     
00466          WHEN OTHER                                                       
00467              MOVE 'D100-GET-CONTROL-NUMBER'                               
00468                                  TO DP013-PARAGRAPH                       
00469              MOVE 'INVALID SQL RETURN CODE'                               
00470                                  TO DP013-MESSAGE-TEXT (1)                
00471              MOVE SQLCA          TO DP013-SQLCA                           
00472              MOVE 'TPPCCTL'      TO DP013-DB2-TABLE-NAME (1)              
00473              SET DP013-DB2-ABEND TO TRUE                                  
00474              PERFORM Z999-ABEND                                           
00475      END-EVALUATE.                                                        
00475                                                                           
00476 ******************************************************************        
00477 * D200-GET-STORE-NAME.                                                    
00478 * ==> PROCESSES:                                                          
00479 *   - GETS THE STORE NAME FROM THE XST_LOC TABLE.                         
      *   - CHECKS WHETHER THE GIVEN STORE IS A ECOMM LOCATION OR NOT.          
00480 *     PERFORMED BY : B100-INITIALIZE.                                     
00481 ******************************************************************        
00482  D200-GET-STORE-NAME.                                                     
00475                                                                           
00483      MOVE PE056-PRINT-FOR-STORE-NUMBER                                    
00484                                  TO PV-STORE-NUM.                         
00485                                                                           
00486      EXEC SQL                                                             
00487          SELECT LOC_NM                                                    
                     ,E_BUS_IND                                                 
                     ,LOC_TYP_CDE                                               
00488          INTO   :XST00001-LOC-NM                                          
                     ,:XST00001-E-BUS-IND                                       
                     ,:XST00001-LOC-TYP-CDE                                     
00489          FROM   XST_LOC                                                   
00490          WHERE (LOC_NBR = :PV-STORE-NUM)                                  
00491      END-EXEC.                                                            
00492                                                                           
00493      EVALUATE TRUE                                                        
00494          WHEN SQLCODE = 0                                                 
00495              MOVE XST00001-LOC-NM                                         
00496                                  TO PE053-PV-STORE-NAME                   
                   IF XST00001-E-BUS-IND   = 'Y' AND                            
                      XST00001-LOC-TYP-CDE = 'DS'                               
                      SET E-BUS-IND-YES                                         
                                       TO TRUE                                  
                   ELSE                                                         
                      SET E-BUS-IND-NO                                          
                                       TO TRUE                                  
                   END-IF                                                       
00497          WHEN SQLCODE = +100                                              
00498              MOVE SPACES         TO PE053-PV-STORE-NAME                   
00499              SET  E-BUS-IND-NO   TO TRUE                                  
00500          WHEN OTHER                                                       
00501              MOVE 'D200-GET-STORE-NAME'                                   
00502                                  TO DP013-PARAGRAPH                       
00503              MOVE 'INVALID SQL RETURN CODE'                               
00504                                  TO DP013-MESSAGE-TEXT (1)                
00505              MOVE SQLCA          TO DP013-SQLCA                           
00506              MOVE 'XST_LOC'      TO DP013-DB2-TABLE-NAME (1)              
00507              SET DP013-DB2-ABEND TO TRUE                                  
00508              PERFORM Z999-ABEND                                           
00509          END-EVALUATE.                                                    
00509                                                                           
00511 ******************************************************************        
00512 *Z100-CLOSE-FILES.                                                        
00513 * ==> PROCESSES:                                                          
00514 *   - ISSUES A CALL TO DRS TO CLOSE THE OPEN REPORT FILE.                 
00515 *     PERFORMED BY:  A100-MAIN-MODULE                                     
00516 ******************************************************************        
00510  Z100-CLOSE-FILES.                                                        
           MOVE PC-TERM                TO DRS01-DRRB-FUNC.                      
           PERFORM Z200-LINK-TO-DRS.                                            
      *  NOTE: IF 'OUTP' PARAMETERS ARE CHANGING BETWEEN RUNS, IT IS            
      *        NECESSARY TO 'DEL'ETE THE 'OUTP' OUTPUT-ID AND THEN              
      *        RE-'ADD' IT.  THIS IS THE CASE BECAUSE CICS MAKES AN             
      *        INITIAL REFERENCE, AND HOLDS THESE PARAMETERS UNTIL A            
      *        DELETE/ADD IS EXECUTED, OR UNTIL CICS IS RECYCLED.               
           MOVE PC-OUTP                TO DRS05-DRRB-FUNC.                      
           MOVE 'DEL'                  TO DRS05-DROB-FUNCTION.                  
           MOVE PV-DRS-DDNAME          TO DRS05-DROB-OUTPUT-ID.                 
           PERFORM Z250-LINK-TO-DRS.                                            
00522 ******************************************************************        
00523 *Z200-LINK-TO-DRS.                                                        
00524 * ==> PROCESSES:                                                          
00525 *   - ISSUES THE LINK TO DRS.                                             
00526 *     PERFORMED BY:  B100-INITIALIZE                                      
00527 *                    C200-PRINT-REPORT                                    
00528 *                    Z100-CLOSE-FILES                                     
00529 ******************************************************************        
00530  Z200-LINK-TO-DRS.                                                        
00531                                                                           
00532       EXEC CICS                                                           
00533           LINK                                                            
00534               PROGRAM  ('DRS1INTC')                                       
00535               COMMAREA (DRS01-COMMAREA)                                   
00536       END-EXEC.                                                           
00537                                                                           
00538      EVALUATE TRUE                                                        
00539          WHEN DRS01-DRRB-RC = 0                                           
00540              CONTINUE                                                     
00541          WHEN OTHER                                                       
00542              MOVE 'Z200-LINK-TO-DRS' TO DP013-PARAGRAPH                   
00543              SET DP013-OTHER-ABEND   TO TRUE                              
00544              MOVE DRS01-DRRB-FUNC    TO PV-DRS-DRRB-FUNC                  
00545              MOVE DRS01-DRRB-RID     TO PV-DRS-DRRB-RID                   
00546              MOVE DRS01-DRRD-OPTIONS TO PV-DRS-DRRB-OPTIONS               
00547              MOVE DRS01-DRRB-RC      TO PV-DRS-DRRB-RC                    
00548              MOVE DRS01-DRRB-ATTR-ERROR                                   
00549                                      TO PV-DRS-ATTR-ERROR                 
00550              MOVE DRS01-DRRB-DRDC    TO PV-DRS-DRRB-DRDC                  
00551              MOVE DRS01-DRRB01-LINES TO PV-DRS-DRRB-LINES                 
00552              MOVE PV-DRS-ABEND-DISPLAY-LINE1                              
00553                                      TO DP013-MESSAGE-TEXT (1)            
00554              MOVE PV-DRS-ABEND-DISPLAY-LINE2                              
00555                                      TO DP013-MESSAGE-TEXT (2)            
00556              PERFORM Z999-ABEND                                           
00557      END-EVALUATE.                                                        
00522 ******************************************************************        
      * THIS MODULE EXECUTES A LINK TO DRS USING COMMAREA 3 WHICH IS            
      * THE COMBINED DRS REQUEST BLOCK AND OUTPUT BLOCK FOR THE                 
      * CREATION OF OUTPUT JCL STATEMENTS. THESE JCL STATEMENTS ALLOW           
      * REFERENCING OF PAGEDEF AND FORMDEF STATEMENTS FOR PRINTING AFP          
      * DOCUMENTS VIA DRS.                                                      
      ******************************************************************        
       Z250-LINK-TO-DRS.                                                        
                                                                                
           EXEC CICS                                                            
               LINK                                                             
                   PROGRAM  ('DRS1INTC')                                        
                   COMMAREA (DRS05-COMMAREA)                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DRS05-DRRB-RC = ZERO                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'Z250-LINK-TO-DRS'                                      
                                       TO DP013-PARAGRAPH                       
                   SET DP013-OTHER-ABEND                                        
                                       TO TRUE                                  
                   MOVE DRS05-DRRB-FUNC                                         
                                       TO PV-DRS-DRRB-FUNC                      
                   MOVE DRS05-DRRB-RID TO PV-DRS-DRRB-RID                       
                   MOVE DRS05-DRRD-OPTIONS                                      
                                       TO PV-DRS-DRRB-OPTIONS                   
                   MOVE DRS05-DRRB-RC  TO PV-DRS-DRRB-RC                        
                   MOVE DRS05-DRRB-ATTR-ERROR                                   
                                       TO PV-DRS-ATTR-ERROR                     
                   MOVE DRS05-DRRB-DRDC                                         
                                       TO PV-DRS-DRRB-DRDC                      
                   MOVE DRS05-DRRB01-LINES                                      
                                       TO PV-DRS-DRRB-LINES                     
                   MOVE PV-DRS-ABEND-DISPLAY-LINE1                              
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE PV-DRS-ABEND-DISPLAY-LINE2                              
                                       TO DP013-MESSAGE-TEXT (2)                
                   PERFORM Z999-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
00558  EJECT                                                                    
00559 ******************************************************************        
00560 *Z300-READ-TSQ.                                                           
00561 * ==> PROCESSES:                                                          
00562 *   - READS THE TEMPORARY STORAGE QUEUE PASSED TO THE PROGRAM             
00563 *     PERFORMED BY: A100-MAIN-MODULE                                      
00564 ******************************************************************        
00565  Z300-READ-TSQ.                                                           
00583                                                                           
00566      EXEC CICS                                                            
00567          READQ TS                                                         
00568          QUEUE(PE056-PRINT-QUEUE)                                         
00569          INTO(DOCUMENT-PRINT-RECORD)                                      
00570          LENGTH(PC-PRINT-QUEUE-LENGTH)                                    
00571          ITEM(PV-ITEM-NUMBER)                                             
00572          RESP(PV-CICS-RESPONSE)                                           
00573      END-EXEC.                                                            
00574                                                                           
00575      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                           
00576      AND PV-CICS-RESPONSE NOT = DFHRESP(ITEMERR))                         
00577          SET DP013-CICS-ABEND    TO TRUE                                  
00578          MOVE 'Z300-READ-TSQ'    TO DP013-PARAGRAPH                       
00579          MOVE 'BAD READ OF TSQ'  TO DP013-MESSAGE-TEXT(1)                 
00580          PERFORM Z999-ABEND                                               
00581      END-IF.                                                              
00582                                                                           
       EJECT                                                                    
00585 ******************************************************************        
00586 *Z400-DELETE-TSQ.                                                         
00587 * ==> PROCESSES:                                                          
00588 *   - DELETES THE TEMPORARY STORAGE QUEUE PASSED TO THE PROGRAM           
00589 *     PERFORMED BY: A100-MAIN-MODULE                                      
00590 ******************************************************************        
00591  Z400-DELETE-TSQ.                                                         
00607                                                                           
00592      EXEC CICS                                                            
00593          DELETEQ TS                                                       
00594          QUEUE(PE056-PRINT-QUEUE)                                         
00595          RESP(PV-CICS-RESPONSE)                                           
00596      END-EXEC.                                                            
00597                                                                           
00598      IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                           
00599      AND PV-CICS-RESPONSE NOT = DFHRESP(QIDERR))                          
00600          SET DP013-CICS-ABEND    TO TRUE                                  
00601          MOVE 'Z400-DELETE-TSQ'  TO DP013-PARAGRAPH                       
00602          MOVE 'BAD DELETE OF TSQ'                                         
00603                                  TO DP013-MESSAGE-TEXT(1)                 
00604          PERFORM Z999-ABEND                                               
00605      END-IF.                                                              
00607                                                                           
       EJECT                                                                    
00609 ******************************************************************        
00610 *   STANDARD ABEND ROUTINE                                                
00611 * ==> PROCESSES:                                                          
00612 *   - ISSUES THE ABEND.                                                   
00613 *     PERFORMED BY:  D100-GET-CONTROL-NUMBER                              
00614 *                    D200-GET-STORE-NAME                                  
00615 *                    Z200-LINK-TO-DRS                                     
00616 *                    Z300-READ-TSQ                                        
00617 ******************************************************************        
00618  Z999-ABEND.                                                              
00607                                                                           
00619      MOVE PC-CURRENT-PROGRAM-NAME     TO DP013-PROGRAM.                   
00620      MOVE DFHEIBLK                    TO DP013-DFHEIBLK.                  
00621      INITIALIZE DP013-SRC-TASK-NUMBER                                     
00622                 DP013-STACK.                                              
00623      SET DP013-LINK-RETURN                                                
00624          DP013-NO-ROLLBACK TO TRUE.                                       
00625                                                                           
00626      EXEC CICS                                                            
00627          LINK                                                             
00628              PROGRAM  (DP013-NAME)                                        
00629              COMMAREA (DP013-PARAMETERS)                                  
00630              LENGTH   (DP013-LENGTH)                                      
00631      END-EXEC.                                                            
00632                                                                           
00634      EXEC CICS                                                            
00635          RETURN                                                           
00636      END-EXEC.                                                            
00637                                                                           
00638 *                                                                         
00639 *  PROCEDURE DIVISION TO FORMAT THE DOUCMENT                              
00640 *                                                                         
00641      COPY PEPD003.                                                        
