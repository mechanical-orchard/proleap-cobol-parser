       IDENTIFICATION DIVISION.                                         01/20/95
00003  PROGRAM-ID.         INKUT116.                                       LV001
00004  AUTHOR.             TROY BIRK.                                   INKUT116
00005  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKUT116
00006  DATE-WRITTEN.       11-93.                                       INKUT116
00007  DATE-COMPILED.                                                   INKUT116
00008                                                                   INKUT116
00009 ******************************************************************INKUT116
00010 *  THIS PROGRAM WILL READ IN THE PHYSICAL COUNT FLAT FILE AND     INKUT116
00011 *  RENUMBER ALL SKUS AND REMOVE ALL CLEARANCE RECORDS.            INKUT116
00012 ******************************************************************INKUT116
00013 *CHANGE HISTORY:                                                  INKUT116
00015 * DATE  11/30/94                                                  INKUT116
00016 *     COMMENTED OUT THE IF CHECK IN B300-PROCESS-RECS WHICH       INKUT116
00017 *     BYPASSES CLEARANCE SKUS. THIS IS TO ALLOW CLEARANCE SKUS TO INKUT116
00018 *     BOOK.                                                       INKUT116
00019 *     MADE BY: TROY BIRK______________ WR/IR #: 2325__            INKUT116
00015 * DATE  01/27/98                                                  INKUT116
00016 *     ADDED LOGIC TO PUT THE ENT-ID ON THE OUTPUT FILE TO BE      INKUT116
00017 *     LATER USED IN THE VARIANCE REPORTING                        INKUT116
00019 *     MADE BY: JULIE SEIDER            WR/IR #: 9999__            INKUT116
00015 * DATE  03/16/99                                                  INKUT116
00016 *     ADDED LOGIC TO PUT THE ITEM NBR ON THE OUTPUT FILE TO BE    INKUT116
00017 *     LATER USED IN THE VARIANCE REPORTING                        INKUT116
00019 *     MADE BY: JULIE SEIDER            WR/IR #: 9999__            INKUT116
JF0900* DATE 09/25/00                        CHANGE ID=JF0900                   
JF0900*     ADDED LOGIC TO GET ITEM PRICE AND ITEM STATUS DATA FROM             
JF0900*     TUPCPLS IN ONE PROGRAM. ALSO ADDED A NEW CONTROL CARD TO            
JF0900*     INDICATE WHETHER TO INCLUDE OR EXCLUDE GWP ITEMS, CLEARANCE         
JF0900*     ITEMS, OR DISCONTINUED ITEMS IN THE OUTPUT. A NEW OUTPUT            
JF0900*     FILE ITEMS-NOT-INCLUDED WAS ADDED TO HANDLE THE EXCLUDED            
JF0900*     ITEMS AND ADDED LOGIC TO WRITE OUTPUT TO THE EXCLUDED FILE.         
JF0900*     ADDED LOGIC TO COUNT THE EXCLUDED ITEMS AND TO DISPLAY THEM         
JF0900*     IN A SYSOUT MESSAGE. LOGIC WAS ALSO ADDED TO SET                    
JF0900*     ITEM-STATUS-CODE AND ITEM-TYPE FOR BOTH INCLUDED AND                
JF0900*     EXCLUDED OUTPUT FILES.                                              
JF0900*     MADE BY: JON FIEDLER            WR/IR #: 9999__             INKUT116
W04061* DATE  04/06/2001                   CHANGE ID=W04061                     
W04061*   INK365 - UNIT BOOKING PROCESS EFFICIENCIES (REMOVE JF0900 MOD)        
W04061*   MADE BY: ROD JANSSEN            WR/IR #:99999                         
00020 ******************************************************************INKUT116
RB0401*  CHANGES:                                                               
RB0401*     04/30/01 WR# 21591 RONNA BUTZ (LOOK FOR RB0401 TO IDENTIFY          
RB0401*                                                                         
RB0401*     CHANGES ARE FOR MANIFESTING.                                        
RB0401*     THERE IS A NEW INTRANSIT FIELD ADDED TO INWS016, AND THE            
RB0401*     RECORD LENGTH HAS INCREASE TO 70.                                   
RB0401*                                                                         
RB0401******************************************************************        
W21732*  CHANGES:                                                               
W21732*     12/04/02 WR21732 ASHOK A V, IGSI                                    
W21732*                                                                         
W21732*     CHANGES ARE FOR NI SKU CONVERSION.                                  
W21732*     COPYBOOK INWS016 INCREASED IN SIZE TO 80 BYTES                      
W21732*     ALL REFERENCES TO TSK WERE REPLACED WITH TSKXREF, TVNDSTL           
W21732*     AND IMV_SKU VIEW.                                                   
W21732******************************************************************        
RB0302******************************************************************INKUT116
RB0302*  CHANGES:                                                               
RB0302*     02/27/03 FIX A BUG FROM NIS CHANGES                                 
RB0302*     04/10/03 FIX MORE BUGS AFTER OFFSHORE CHANGES RENUMBERING           
RB0302*              LOGIC                                                      
C21732*     05/02/03 IF EITHER THE IMV_SKU.VS_ID OR TVNDSTL.ENT_ID (IN          
C21732*              SKU VALIDATION) CANNOT BE FOUND, MOVE 0 TO ENT_ID.         
C21732*              PREVENTS CREATING 'INVALID-SKU' RECORD                     
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
W28545* W28545  08-21-2006 ADD LOGIC FOR DEPARTMENT LEVEL                       
W28545*                    INVENTORY PROCESS.                                   
W28545*                    J SELWITSCHKA                                        
C21732******************************************************************INKUT116
      *  CHANGES:                                                               
      *     03/03/09 INC000000707444 TODD C. MICHALEK                           
      *     - IN Z200-CALL-INPD400 ADD ERROR DISPLAY PROCESSING WHEN            
      *       RETURNING FROM A PRICING MODULE CALL                              
W33304* W33304  05-01-2008  HOLDING INVENTORY LEDGER OPEN ACROSS                
W33304*                     PERIODS INVENTORY SHORTAGE REPORTING                
W33304*                     JILL LIN                                            
      ******************************************************************        
                                                                        INKUT116
00022  ENVIRONMENT DIVISION.                                            INKUT116
00024  CONFIGURATION SECTION.                                           INKUT116
00026  SOURCE-COMPUTER.    IBM-3090.                                    INKUT116
00027  OBJECT-COMPUTER.    IBM-3090.                                    INKUT116
00029  INPUT-OUTPUT SECTION.                                            INKUT116
00031  FILE-CONTROL.                                                    INKUT116
00032                                                                   INKUT116
00033      SELECT PHYSICAL-COUNT-FILE                                   INKUT116
00034      ASSIGN TO INP01.                                             INKUT116
00035                                                                   INKUT116
00039      SELECT NEW-PHYSICAL-COUNT-FILE                               INKUT116
00040      ASSIGN TO OUT01.                                             INKUT116
00041                                                                   INKUT116
JF0900     SELECT RECORDS-EXCLUDED-FILE                                         
JF0900     ASSIGN TO OUT02.                                                     
JF0900                                                                          
W21732     SELECT  UPC-ERROR-FILE                                       INKUT102
W21732         ASSIGN TO OUT03.                                         INKUT102
W21732                                                                          
W28545     SELECT  PHYSICAL-NOT-QUALIFIED-FILE                          INKUT102
W28545         ASSIGN TO OUT04.                                         INKUT102
W28545                                                                          
00047  DATA DIVISION.                                                   INKUT116
00049  FILE SECTION.                                                    INKUT116
00050                                                                   INKUT116
00052  FD  PHYSICAL-COUNT-FILE                                          INKUT116
00053      LABEL RECORDS ARE STANDARD                                   INKUT116
00054      RECORDING MODE IS F                                          INKUT116
00055      BLOCK CONTAINS 0 RECORDS.                                    INKUT116
W21732 01  PHYSICAL-COUNT-RECORD       PIC  X(80).                      INKUT116
00057                                                                   INKUT116
00064  FD  NEW-PHYSICAL-COUNT-FILE                                      INKUT116
00065      LABEL RECORDS ARE STANDARD                                   INKUT116
00066      RECORDING MODE IS F                                          INKUT116
00067      BLOCK CONTAINS 0 RECORDS.                                    INKUT116
W21732 01  NEW-PHYSICAL-COUNT-RECORD   PIC  X(80).                      INKUT116
00069                                                                   INKUT116
JF0900 FD  RECORDS-EXCLUDED-FILE                                        INKUT116
JF0900     LABEL RECORDS ARE STANDARD                                   INKUT116
JF0900     RECORDING MODE IS F                                          INKUT116
JF0900     BLOCK CONTAINS 0 RECORDS.                                    INKUT116
W21732 01  RECORDS-EXCLUDED-RECORD     PIC  X(80).                      INKUT116
JF0900                                                                          
W21732 FD  UPC-ERROR-FILE                                               INKUT102
W21732     LABEL RECORDS ARE STANDARD                                   INKUT102
W21732     RECORDING MODE IS F                                          INKUT102
W21732     BLOCK CONTAINS 0 RECORDS.                                    INKUT102
W21732 01  UPC-ERROR-FILE-RECORD      PIC X(50).                        INKUT102
W21732                                                                          
W28545 FD  PHYSICAL-NOT-QUALIFIED-FILE                                  INKUT116
W28545     LABEL RECORDS ARE STANDARD                                   INKUT116
W28545     RECORDING MODE IS F                                          INKUT116
W28545     BLOCK CONTAINS 0 RECORDS.                                    INKUT116
W28545 01  PHYSICAL-NOT-QUALIFIED-REC  PIC  X(80).                      INKUT116
W28545                                                                  INKUT116
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                         INKUT116
      ******************************************************************        
00079 *                                                                 INKUT116
00080  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              INKUT116
00081                                           COMP SYNC.              INKUT116
00082 *                                                                 INKUT116
00113  01  PC-PROGRAM-CONSTANTS.                                        INKUT116
W21732     05  PC-RENUMBERED-SKU-STATUS PIC X(02)   VALUE '98'.         INKUT116
00116      05  PC-CLEARANCE-STATUS      PIC S9(03)  VALUE +30           INKUT116
00117                                               COMP-3.             INKUT116
00118      05  PC-EMPTY-DATE            PIC X(10)   VALUE '9999-09-09'. INKUT116
00119      05  PC-MAX-RETRIES           PIC 9(03)   VALUE 3.            INKUT116
00120      05  PC-OPER-CO               PIC X(02)   VALUE '03'.         INKUT116
00121      05  PC-CO                    PIC X(02)   VALUE 'CO'.         INKUT116
00122 *                                                                 INKUT116
00123  01  PV-PROGRAM-VARIABLES.                                        INKUT116
00133      05  PV-RETRY-COUNT          PIC S9(03)    VALUE ZERO         INKUT116
00134                                                COMP-3.            INKUT116
00135      05  PV-ITEM-NBR             PIC S9(15)V   VALUE ZERO         INKUT116
00136                                                COMP-3.            INKUT116
00137      05  PV-UPC-CODE             PIC S9(15)V                      INKUT116
00138                                  COMP-3        VALUE +0.          INKUT116
W28545     05  PV-PREV-STORE-NBR       PIC 9(04)  VALUE ZERO.                   
W28545     05  PV-PREV-DEPT-NBR        PIC 9(04)  VALUE ZERO.                   
W28545     05  PV-PREV-MCL-NBR         PIC 9(04)  VALUE ZERO.                   
W28545     05  PV-STORE-0000-COUNT     PIC S9(09) VALUE +0  COMP SYNC.          
W28545     05  PV-STORE-XXXX-COUNT     PIC S9(09) VALUE +0  COMP SYNC.          
W28545     05  PV-DEPT-CLASS-COUNT     PIC S9(09) VALUE +0  COMP SYNC.          
00139      05  PV-CONVERT-SKU.                                          INKUT116
W21732         10  PV-CONVERT-DEPT     PIC 9(04)  VALUE ZERO.           INKUT116
W21732         10  PV-CONVERT-CLASS    PIC 9(04)  VALUE ZERO.           INKUT116
W21732         10  PV-CONVERT-SKU-NBR  PIC 9(08)  VALUE ZERO.           INKUT116
00143      05  PV-RENUMBER-SKU         PIC X(08)  VALUE SPACES.         INKUT116
00144      05  PV-SQL-SUB              PIC 9(03)  VALUE ZERO.           INKUT116
00145      05  PV-PARAGRAPH-NAME       PIC X(30)  VALUE SPACES.         INKUT116
JF0900     05  PV-ABEND-MESSAGE        PIC X(75)  VALUE SPACE.                  
00146      05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.         INKUT116
W21732     05  PV-DEPT-TABLE           OCCURS 9999.                     INKUT116
00148          10  PV-SKU-LEVEL-IND    PIC X(01)    VALUE SPACES.       INKUT116
W21732     05  PV-DEPT-TABLE-INDEX     PIC 9(04)    VALUE ZERO.         INKUT116
W21732*                                                                         
W21732     05  PV-KOHLS-UPC-LOW.                                                
W21732         10  FILLER              PIC 9(04)    VALUE 0400.                 
W21732         10  PV-KOHLS-SKU-MIN    PIC 9(08)    VALUE ZEROS.                
W21732         10  FILLER              PIC 9(01)    VALUE 0.                    
W21732     05  PV-KOHLS-UPC-HIGH.                                               
W21732         10  FILLER              PIC 9(04)    VALUE 0400.                 
W21732         10  PV-KOHLS-SKU-MAX    PIC 9(08)    VALUE ZEROS.                
W21732         10  FILLER              PIC 9(01)    VALUE 9.                    
W21732     05  PV-KOHLS-SIGN-UPC.                                               
W21732         10  PV-KOHLS-UPC-MIN-SIGNONLY                                    
W21732                                 PIC S9(13).                              
W21732         10  PV-KOHLS-UPC-MAX-SIGNONLY                                    
W21732                                 PIC S9(13).                              
W21732     05  PV-KOHLS-MDSEAR-UPC.                                             
W21732         10  PV-KOHLS-UPC-MIN    PIC S9(13)V  COMP-3.                     
W21732         10  PV-KOHLS-UPC-MAX    PIC S9(13)V  COMP-3.                     
                                                                                
W21732 01  PV-UPC-ERROR-FILE-RECORD.                                            
W26600     05  PV-UPC-ERROR-STORE      PIC X(04).                          CL*12
W21732     05  FILLER                  PIC X(01)  VALUE SPACES.            CL*12
W21732     05  PV-UPC-ERROR-SKU        PIC X(08).                          CL*12
W21732     05  FILLER                  PIC X(38)  VALUE SPACES.            CL*12
                                                                        INKUT116
00163  01  PS-PROGRAM-SWITCHES.                                         INKUT116
W28545     05  PS-QUALIFIES-FOR-INVENTORY-SW                                    
W28545                                 PIC X        VALUE 'N'.                  
W28545         88  PS-QUALIFIES-FOR-INVENTORY-NO    VALUE 'N'.                  
W28545         88  PS-QUALIFIES-FOR-INVENTORY-YES   VALUE 'Y'.                  
W28545     05  PS-INV-TYPE-SW          PIC X        VALUE 'S'.                  
W28545         88  PS-INV-TYPE-STORE                VALUE 'S'.                  
W28545         88  PS-INV-TYPE-DEPT                 VALUE 'D'.                  
00164      05  PS-ITEM-FOUND-SW        PIC X        VALUE 'N'.          INKUT116
00165          88  PS-ITEM-FOUND                    VALUE 'Y'.          INKUT116
00166          88  PS-ITEM-NOT-FOUND                VALUE 'N'.          INKUT116
00167      05  PS-UPC-FOUND-SW         PIC X        VALUE 'N'.          INKUT116
00168          88  PS-UPC-FOUND                     VALUE 'Y'.          INKUT116
00169          88  PS-UPC-NOT-FOUND                 VALUE 'N'.          INKUT116
00170      05  PS-VALID-SKU-SW           PIC X  VALUE 'Y'.              INKUT116
00171          88  PS-VALID-SKU                 VALUE 'Y'.              INKUT116
00172          88  PS-INVALID-SKU               VALUE 'N'.              INKUT116
00173      05  PS-MORE-INPUT-RECS-SW     PIC X  VALUE 'Y'.              INKUT116
00174          88  PS-MORE-INPUT-RECS           VALUE 'Y'.              INKUT116
00175          88  PS-NO-MORE-INPUT-RECS        VALUE 'N'.              INKUT116
00176      05  PS-NO-MORE-DEPT-ROWS-SW   PIC X  VALUE 'Y'.              INKUT116
00177          88  PS-MORE-DEPT-ROWS            VALUE 'Y'.              INKUT116
00178          88  PS-NO-MORE-DEPT-ROWS         VALUE 'N'.              INKUT116
00179      05  PS-CLEARNCE-SKU-SW        PIC X  VALUE 'N'.              INKUT116
00180          88  PS-CLEARANCE-SKU             VALUE 'Y'.              INKUT116
00181          88  PS-NOT-CLEARANCE-SKU         VALUE 'N'.              INKUT116
00182      05  PS-VALID-RENUMBER-SW      PIC X  VALUE 'N'.              INKUT116
00183          88  PS-VALID-RENUMBER            VALUE 'Y'.              INKUT116
00184          88  PS-INVALID-RENUMBER          VALUE 'N'.              INKUT116
00185                                                                   INKUT116
00186  01  SA-SAVE-AREAS.                                               INKUT116
00187      05  SA-SKU                  PIC X(08)  VALUE SPACES.         INKUT116
                                                                                
      ***************************************************************** INKUT116
      * INPUT/OUTPUT FILE RECORD LAYOUT                                 INKUT116
      ***************************************************************** INKUT116
W26600     COPY INWS016.                                                INKUT116
                                                                        INKUT116
W21732***************************************************************** INKUT116
W21732* COPY BOOK OF NON-INTELLIGENT SKU RE-FORMATTING WORK FIELDS      INKUT116
W21732***************************************************************** INKUT116
W21732     COPY ISWS006.                                                INKUT116
                                                                                
W28545***************************************************************** INKUT116
W28545* COPY BOOK OF COMMON PRICING MODULE                              INKUT116
W28545***************************************************************** INKUT116
W28545     COPY INWS400.                                                INKUT116
W28545                                                                          
00206 ***************************************************************** INKUT116
00207 * ERROR MESSAGE AREA FOR DB2                                      INKUT116
00208 ***************************************************************** INKUT116
00209      COPY DPWS004.                                                INKUT116
00210 *                                                                 INKUT116
W28545***************************************************************** INKUT116
W28545* INVENTORY CONTROL AREA DCLGEN                                   INKUT116
W28545***************************************************************** INKUT116
W28545                                                                  INKUT116
W28545     EXEC SQL                                                     INKUT116
W28545         INCLUDE TINCNTL                                          INKUT116
W28545     END-EXEC.                                                    INKUT116
W28545                                                                  INKUT116
W28545***************************************************************** INKUT116
W28545* INVENTORY PARAMETER AREA DCLGEN                                 INKUT116
W28545***************************************************************** INKUT116
W28545                                                                  INKUT116
W28545     EXEC SQL                                                     INKUT116
W28545         INCLUDE TINVPAR                                          INKUT116
W28545     END-EXEC.                                                    INKUT116
W28545                                                                  INKUT116
W28545***************************************************************** INKUT116
W28545* INT_INV_PARM_MDSE (INT00004) DCLGEN                             INKUT116
W28545***************************************************************** INKUT116
W28545                                                                  INKUT116
W28545     EXEC SQL                                                     INKUT116
W28545         INCLUDE INT00004                                         INKUT116
W28545     END-EXEC.                                                    INKUT116
W28545                                                                  INKUT116
00227 ***************************************************************** INKUT116
00228 * SQL COMM AREA DCLGEN                                            INKUT116
00229 ***************************************************************** INKUT116
00231      EXEC SQL                                                     INKUT116
00232         INCLUDE SQLCA                                             INKUT116
00233      END-EXEC.                                                    INKUT116
00234                                                                   INKUT116
00235 ***************************************************************** INKUT116
00236 *  DEPARTMENT TABLE                                               INKUT116
00237 ***************************************************************** INKUT116
00239      EXEC SQL                                                     INKUT116
00240          INCLUDE TMDSEDP                                          INKUT116
00241      END-EXEC.                                                    INKUT116
00242                                                                   INKUT116
00252      COPY SQLCA2.                                                 INKUT116
00253                                                                   INKUT116
00254 ***************************************************************** INKUT116
00255 * DB2 DEPARTMENT CURSOR.                                          INKUT116
00256 ***************************************************************** INKUT116
00257      EXEC SQL                                                     INKUT116
00258         DECLARE DEPT_CURSOR CURSOR FOR                            INKUT116
00259           SELECT DEPT_NBR,                                        INKUT116
00260                  SKU_LVL_UPD_IND                                  INKUT116
00261             FROM TMDSEDP A, TMDSEAR B                             INKUT116
00262             WHERE (A.MDSEAR_DKEY = B.MDSEAR_DKEY)     AND         INKUT116
00263                   (B.OPERCO_NBR  = '03')              AND         INKUT116
00264                   (B.MDSELV_CDE  = 'DPT')             AND         INKUT116
00265                   (A.SKU_LVL_UPD_IND = 'Y')           AND         INKUT116
00266                   (CURRENT DATE  BETWEEN B.STRT_DTE               INKUT116
00267                                      AND B.END_DTE)               INKUT116
00268                                                                   INKUT116
00269      END-EXEC.                                                    INKUT116
00270  EJECT                                                            INKUT116
00272                                                                   INKUT116
00273 ******************************************************************INKUT116
00271  PROCEDURE DIVISION.                                              INKUT116
00275 ******************************************************************INKUT116
00276  A100-MAINLINE-PROCESSING.                                        INKUT116
00277                                                                   INKUT116
00278      PERFORM B100-OPEN-FILES.                                     INKUT116
                                                                                
00280      PERFORM Z100-READ-INPUT-REC.                                 INKUT116
                                                                                
00282      PERFORM Z600-OPEN-DEPT-CURSOR.                               INKUT116
00283      PERFORM Z700-FETCH-DEPT-ROW.                                 INKUT116
00284                                                                   INKUT116
00285      PERFORM B200-LOAD-DEPT-TABLE                                 INKUT116
00286          UNTIL PS-NO-MORE-DEPT-ROWS.                              INKUT116
00287 *                                                                 INKUT116
00288      PERFORM Z800-CLOSE-DEPT-CURSOR.                              INKUT116
00289 *                                                                 INKUT116
00290      PERFORM B300-PROCESS-RECS                                    INKUT116
00291          UNTIL PS-NO-MORE-INPUT-RECS.                             INKUT116
00292                                                                   INKUT116
00298      PERFORM B400-CLOSE-FILES.                                    INKUT116
00299                                                                   INKUT116
00300      STOP RUN.                                                    INKUT116
00301 *                                                                 INKUT116
00302 ******************************************************************INKUT116
00303 * B100-OPEN-FILES.                                                INKUT116
00304 ******************************************************************INKUT116
00305  B100-OPEN-FILES.                                                 INKUT116
00306                                                                   INKUT116
00307      OPEN INPUT  PHYSICAL-COUNT-FILE                              INKUT116
00309           OUTPUT NEW-PHYSICAL-COUNT-FILE                          INKUT116
W28545                 PHYSICAL-NOT-QUALIFIED-FILE                              
JF0900                 RECORDS-EXCLUDED-FILE                                    
W21732                 UPC-ERROR-FILE.                                          
                                                                        INKUT116
00312 ****************************************************************  INKUT116
00313 * B200-LOAD-DEPT-TABLE.                                           INKUT116
00314 ****************************************************************  INKUT116
00315  B200-LOAD-DEPT-TABLE.                                            INKUT116
00316 *                                                                 INKUT116
00317      MOVE MDSEAR-DEPT-NBR             TO  PV-DEPT-TABLE-INDEX.    INKUT116
00318                                                                   INKUT116
00319      IF MDSEDP-SKU-LVL-UPD-IND = 'Y'                              INKUT116
00320          MOVE MDSEDP-SKU-LVL-UPD-IND      TO  PV-SKU-LEVEL-IND    INKUT116
00321                                           (PV-DEPT-TABLE-INDEX)   INKUT116
00322      END-IF.                                                      INKUT116
00323                                                                   INKUT116
00324      PERFORM Z700-FETCH-DEPT-ROW.                                 INKUT116
00325                                                                   INKUT116
00327 ****************************************************************  INKUT116
00328 * B300-PROCESS-RECS.                                              INKUT116
00329 ****************************************************************  INKUT116
00330  B300-PROCESS-RECS.                                               INKUT116
00331                                                                   INKUT116
W28545     IF  IN016-STORE-9 = PV-PREV-STORE-NBR                                
W28545         CONTINUE                                                         
W28545     ELSE                                                                 
W28545         SET PS-QUALIFIES-FOR-INVENTORY-NO TO TRUE                        
W28545         PERFORM B310-READ-TINVPAR-TABLE                                  
W28545     END-IF.                                                              
W28545     IF  IN016-DEPT-9  = PV-PREV-DEPT-NBR                                 
W28545     AND IN016-CLASS-9 = PV-PREV-MCL-NBR                                  
W28545         CONTINUE                                                         
W28545     ELSE                                                                 
W28545         SET PS-QUALIFIES-FOR-INVENTORY-NO TO TRUE                        
W28545         PERFORM B320-COUNT-DEFAULT-ROWS                                  
W28545     END-IF.                                                              
W28545     PERFORM B330-CHECK-BY-INV-TYPE.                                      
W28545     MOVE IN016-STORE-9        TO PV-PREV-STORE-NBR.                      
W28545     MOVE IN016-DEPT-9         TO PV-PREV-DEPT-NBR.                       
W28545     MOVE IN016-CLASS-9        TO PV-PREV-MCL-NBR.                        
W28545     IF PS-QUALIFIES-FOR-INVENTORY-YES                                    
W28545                                                                          
00332          IF IN016-SKU NOT = SA-SKU                                INKUT116
00333              INITIALIZE PV-RENUMBER-SKU                           INKUT116
00334              SET PS-NOT-CLEARANCE-SKU TO TRUE                     INKUT116
00335              MOVE IN016-SKU TO SA-SKU                             INKUT116
W28545             PERFORM Z200-CALL-INPD400                            INKUT116
                   IF PS-VALID-SKU                                      INKUT116
W28545                 MOVE IN400-ATT-STATUS-OUT TO IN016-STAT-CDE              
W28545                 MOVE IN400-ATT-ITM-NBR-OUT TO IN016-ITEM-NUMBER          
00356              END-IF                                               INKUT116
00357          END-IF                                                   INKUT116
00358                                                                   INKUT116
               IF PS-VALID-SKU                                          INKUT116
W28545             MOVE IN400-ATT-STATUS-OUT TO IN016-STAT-CDE                  
W28545             MOVE IN400-ATT-ITM-NBR-OUT TO IN016-ITEM-NUMBER              
                   MOVE IN016-DEPT      TO PV-DEPT-TABLE-INDEX          INKUT116
00367                  IF (IN016-SCAN-IND NOT = 'Y') AND                INKUT116
00368                     (PV-SKU-LEVEL-IND(PV-DEPT-TABLE-INDEX) = ' ') INKUT116
00369                      CONTINUE                                     INKUT116
00370                  ELSE                                             INKUT116
W28545                     IF IN400-PS-RENUMBER-SKU                     INKUT116
W28545                         MOVE IN400-PRC-SKU-OUT TO IN016-SKU      INKUT116
00373                      END-IF                                       INKUT116
W04061                     PERFORM Z300-WRITE-PHYSCNT-RECORD            INKUT116
00377          ELSE                                                     INKUT116
00378              PERFORM C200-WRITE-INVALID-SKU-MSG                   INKUT116
00379          END-IF                                                   INKUT116
00380                                                                   INKUT116
W28545     ELSE                                                         INKUT116
W28545                                                                  INKUT116
W28545         PERFORM Z310-WRITE-NOT-QUALIFIED                         INKUT116
W28545                                                                  INKUT116
W28545     END-IF.                                                      INKUT116
W28545                                                                  INKUT116
W28545     PERFORM Z100-READ-INPUT-REC.                                 INKUT116
                                                                        INKUT116
W28545**************************************************************    INKUT116
W28545*  READ THE TINVPAR TABLE                                         INKUT116
W28545**************************************************************    INKUT116
W28545 B310-READ-TINVPAR-TABLE.                                         INKUT116
W28545                                                                  INKUT116
W28545     MOVE IN016-STORE-9 TO INVPAR-LOC-NBR.                        INKUT116
W28545                                                                  INKUT116
W28545     PERFORM                                                      INKUT116
W28545         WITH TEST AFTER                                          INKUT116
W28545         VARYING PV-RETRY-COUNT FROM 1 BY 1                       INKUT116
W28545         UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  INKUT116
W28545             OR (SQLCODE = 0)                                     INKUT116
W28545             OR (SQLCODE = +100)                                  INKUT116
W28545                                                                  INKUT116
W28545         EXEC SQL                                                 INKUT116
W28545             SELECT        A.INV_ID                               INKUT116
W28545                          ,A.PLND_INV_DTE                         INKUT116
W28545             INTO         :INVPAR-INV-ID                          INKUT116
W28545                         ,:INVPAR-PLND-INV-DTE                    INKUT116
W28545             FROM          TINVPAR A                              INKUT116
W28545                          ,TINCNTL B                              INKUT116
W28545             WHERE LOC_NBR           = :INVPAR-LOC-NBR            INKUT116
W28545               AND ACTL_UNIT_BK_DTE  = :PC-EMPTY-DATE             INKUT116
W28545               AND B.ACTV_IND        = 'Y'                        INKUT116
W28545               AND A.INV_ID          = B.INV_ID                   INKUT116
W33304               AND A.LOC_INV_STAT_CDE = 'IN'                      INKUT116
W28545             WITH UR                                              INKUT116
W28545         END-EXEC                                                 INKUT116
W28545                                                                  INKUT116
W28545         EVALUATE TRUE                                            INKUT116
W28545             WHEN SQLCODE = 0                                     INKUT116
W28545             WHEN SQLCODE = -904                                  INKUT116
W28545             WHEN SQLCODE = -911                                  INKUT116
W28545                 CONTINUE                                         INKUT116
W28545             WHEN SQLCODE = +100                                  INKUT116
W28545                 DISPLAY 'ERROR FOR STORE: ' IN016-STORE-9        INKUT116
W28545                 DISPLAY 'UNITS MAY HAVE ALREADY BEEN BOOKED'     INKUT116
W28545                 DISPLAY 'OR STORE NUMBER IS INVALID'             INKUT116
W28545                 DISPLAY 'OR INVENTORY CYCLE IS NOT ACTIVE'       INKUT116
W28545                 MOVE 'B310-READ-TINVPAR-TABLE'                   INKUT116
W28545                               TO  PV-PARAGRAPH-NAME              INKUT116
W28545                 MOVE 'STORE NOT FOUND READING TINVPAR'           INKUT116
W28545                               TO  PV-DB2-OPER-ATTEMPTED          INKUT116
W28545                 PERFORM Z900-SQL-ABEND                           INKUT116
W28545             WHEN OTHER                                           INKUT116
W28545                 MOVE 'B310-READ-TINVPAR-TABLE' TO                INKUT116
W28545                                         PV-PARAGRAPH-NAME        INKUT116
W28545                 PERFORM Z900-SQL-ABEND                           INKUT116
W28545         END-EVALUATE                                             INKUT116
W28545     END-PERFORM.                                                 INKUT116
W28545                                                                  INKUT116
W28545     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          INKUT116
W28545         MOVE 'B310-READ-TINVPAR-TABLE' TO                        INKUT116
W28545                                 PV-PARAGRAPH-NAME                INKUT116
W28545         MOVE 'MAX NUMBER OF RETRIES EXCEEDED' TO                 INKUT116
W28545               PV-DB2-OPER-ATTEMPTED                              INKUT116
W28545         PERFORM Z900-SQL-ABEND                                   INKUT116
W28545     END-IF.                                                      INKUT116
W28545                                                                  INKUT116
W28545**************************************************************    INKUT116
W28545* DETERMINE IF THE INVENTORY IS BEING DONE FOR THE WHOLE STORE            
W28545* OR SPECIFIC CLASSES IN ONE OR MORE DEPARTMENTS. FOR A STORE             
W28545* WIDE INVENTORY, ALL RECORDS ARE INCLUDED. FOR A DEPARTMENT              
W28545* INVENTORY, ONLY THOSE DEPARTMENT SKUS WILL BE INCLUDED.                 
W28545* THE INFORMATION FOR THIS DECISION IS CARRIED IN THE                     
W28545* INT_INV_PARM_MDSE TABLE.                                                
W28545**************************************************************    INKUT116
W28545 B320-COUNT-DEFAULT-ROWS.                                                 
W28545                                                                          
W28545     MOVE INVPAR-INV-ID TO INT00004-INV-ID.                               
W28545     MOVE ZEROS         TO INT00004-LOC-NBR                               
W28545                           PV-STORE-0000-COUNT.                           
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         SELECT COUNT (*)                                                 
W28545              INTO :PV-STORE-0000-COUNT                                   
W28545              FROM INT_INV_PARM_MDSE                                      
W28545              WHERE INV_ID   = :INT00004-INV-ID                           
W28545                AND LOC_NBR  = :INT00004-LOC-NBR                          
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545         WHEN SQLCODE = ZERO                                              
W28545         WHEN SQLCODE = +100                                              
W28545             CONTINUE                                                     
W28545         WHEN SQLWARN IS NOT EQUAL TO SPACES                              
W28545         WHEN SQLCODE IS NOT EQUAL TO ZERO                                
W28545         MOVE 'B320-COUNT-DEFAULT-ROWS' TO PV-PARAGRAPH-NAME              
W28545         MOVE 'COUNT     '         TO PV-DB2-OPER-ATTEMPTED               
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545     IF PV-STORE-0000-COUNT = ZEROS                                       
W28545         SET PS-INV-TYPE-STORE         TO TRUE                            
W28545     ELSE                                                                 
W28545         SET PS-INV-TYPE-DEPT          TO TRUE                            
W28545     END-IF.                                                              
W28545                                                                          
W28545******************************************************************        
W28545* CHECK INVENTORY TYPE. IF A STORE INVENTORY, THE RECORD                  
W28545* QUALIFIES FOR INVENTORY. IF A DEPARTMENT INVENTORY, CHECK               
W28545* BY DEPARTMENT AND CLASS TO SEE IF THE RECORD QUALIFIES FOR              
W28545* INVENTORY.                                                              
W28545******************************************************************        
W28545                                                                          
W28545 B330-CHECK-BY-INV-TYPE.                                                  
W28545     EVALUATE TRUE                                                        
W28545         WHEN PS-INV-TYPE-STORE                                           
W28545             SET PS-QUALIFIES-FOR-INVENTORY-YES TO TRUE                   
W28545         WHEN PS-INV-TYPE-DEPT                                            
W28545             PERFORM B340-CHECK-BY-DEPT-CLS                               
W28545     END-EVALUATE.                                                        
W28545                                                                          
W28545******************************************************************        
W28545*  CHECK FOR DEPARTMENT                                                   
W28545******************************************************************        
W28545                                                                          
W28545 B340-CHECK-BY-DEPT-CLS.                                                  
W28545                                                                          
W28545     PERFORM B350-COUNT-DEPT-CLASS                                        
W28545     IF PV-DEPT-CLASS-COUNT > ZERO                                        
W28545         SET PS-QUALIFIES-FOR-INVENTORY-YES TO TRUE                       
W28545     END-IF.                                                              
W28545                                                                          
W28545**************************************************************    INKUT116
W28545*  COUNT DEPARTMENT ROWS FOR INVENTORY ID                         INKUT116
W28545**************************************************************    INKUT116
W28545 B350-COUNT-DEPT-CLASS.                                                   
W28545                                                                          
W28545     MOVE INVPAR-INV-ID TO INT00004-INV-ID.                               
W28545     MOVE IN016-DEPT-9  TO INT00004-DEPT-NBR.                             
W28545     MOVE IN016-CLASS-9 TO INT00004-MAJ-CL-NBR.                           
W28545     MOVE ZEROS         TO PV-DEPT-CLASS-COUNT.                           
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         SELECT COUNT (*)                                                 
W28545              INTO :PV-DEPT-CLASS-COUNT                                   
W28545              FROM INT_INV_PARM_MDSE                                      
W28545              WHERE INV_ID     = :INT00004-INV-ID                         
W28545                AND DEPT_NBR   = :INT00004-DEPT-NBR                       
W28545***             AND MAJ_CL_NBR = :INT00004-MAJ-CL-NBR                     
W28545     END-EXEC.                                                            
W28545                                                                          
W28545     EVALUATE TRUE                                                        
W28545         WHEN SQLCODE = ZERO                                              
W28545         WHEN SQLCODE = +100                                              
W28545             CONTINUE                                                     
W28545         WHEN SQLWARN IS NOT EQUAL TO SPACES                              
W28545         WHEN SQLCODE IS NOT EQUAL TO ZERO                                
W28545         MOVE 'B350-COUNT-DEPT-CLASS' TO PV-PARAGRAPH-NAME                
W28545         MOVE 'COUNT     '         TO PV-DB2-OPER-ATTEMPTED               
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-EVALUATE.                                                        
W28545                                                                          
00383 ******************************************************************INKUT116
00384 * B400-CLOSE-FILES.                                               INKUT116
00385 ******************************************************************INKUT116
00386  B400-CLOSE-FILES.                                                INKUT116
00387                                                                   INKUT116
00388      CLOSE       PHYSICAL-COUNT-FILE                              INKUT116
00389                  NEW-PHYSICAL-COUNT-FILE                          INKUT116
W28545                 PHYSICAL-NOT-QUALIFIED-FILE                              
JF0900                 RECORDS-EXCLUDED-FILE                                    
W21732                 UPC-ERROR-FILE.                                          
00391 *                                                                 INKUT116
00442 ****************************************************************  INKUT116
00443 * C200-WRITE-INVALID-SKU-MSG.                                     INKUT116
00444 ****************************************************************  INKUT116
00445  C200-WRITE-INVALID-SKU-MSG.                                      INKUT116
W04061                                                                  INKUT116
W04061     PERFORM Z350-WRITE-RECS-EXCLUDED.                            INKUT116
W04061                                                                  INKUT116
W21732******************************************************************INKUT102
W21732* D150-WRITE-UPC-ERROR-RECORD    WRITES INTO UPC ERROR FILE       INKUT102
W21732******************************************************************INKUT102
W21732 D150-WRITE-UPC-ERROR-RECORD.                                             
W21732*                                                                         
W21732     INITIALIZE PV-UPC-ERROR-FILE-RECORD                                  
W26600     MOVE IN016-STORE-9       TO  PV-UPC-ERROR-STORE                      
W21732     MOVE IN016-SKU           TO  PV-UPC-ERROR-SKU                        
W21732*                                                                         
W21732     WRITE UPC-ERROR-FILE-RECORD FROM PV-UPC-ERROR-FILE-RECORD.   INKUT102
W21732                                                                          
00592 ****************************************************************  INKUT116
00593 * Z100-READ-INPUT-REC.                                            INKUT116
00594 ****************************************************************  INKUT116
00596  Z100-READ-INPUT-REC.                                             INKUT116
00597                                                                   INKUT116
00598      READ PHYSICAL-COUNT-FILE                                     INKUT116
00599          INTO IN016-PHYSICAL-COUNT-RECORD                         INKUT116
00600            AT END                                                 INKUT116
00601              SET PS-NO-MORE-INPUT-RECS TO TRUE.                   INKUT116
00602 *                                                                 INKUT116
00614 *******************************************************           INKUT116
W28545* Z200-CALL-INPD400.                                              INKUT116
00616 *******************************************************           INKUT116
W28545 Z200-CALL-INPD400.                                               INKUT116
00618                                                                   INKUT116
W28545     INITIALIZE IN400-PV-INPUT-AREA.                              05862000
W28545                                                                  05863000
W28545     SET IN400-PV-SKU-ATTRIB-LOOK TO TRUE.                        05864000
W28545     MOVE IN016-STORE-9       TO IN400-PV-LOC-IN.                 05865000
W28545     MOVE INVPAR-PLND-INV-DTE TO IN400-PV-EFF-DTE-IN.             05866000
W28545     MOVE IN016-SKU           TO IN400-PV-SKU-IN.                 05867000
W28545                                                                  05868000
W28545     PERFORM IN400-LOOKUP-RETAIL.                                 05869000
W28545                                                                  05869100
W28545     IF IN400-PV-ATT-FOUND                                        05869200
W28545         SET PS-VALID-SKU   TO TRUE                               INKUT116
W28545     ELSE                                                                 
W28545         SET PS-INVALID-SKU TO TRUE                               INKUT116
               DISPLAY 'IN016-STORE-9       = ' IN016-STORE-9                   
               DISPLAY 'INVPAR-PLND-INV-DTE = ' INVPAR-PLND-INV-DTE             
               DISPLAY 'IN016-SKU           = ' IN016-SKU                       
               DISPLAY 'IN400-PV-PARAGRAPH  = ' IN400-PV-PARAGRAPH              
W28545     END-IF.                                                      05869900
                                                                                
00661 ****************************************************************  INKUT116
00662 * Z300-WRITE-PHYSCNT-RECORD.                                      INKUT116
00663 ****************************************************************  INKUT116
00664  Z300-WRITE-PHYSCNT-RECORD.                                       INKUT116
00665                                                                   INKUT116
00666      WRITE NEW-PHYSICAL-COUNT-RECORD                              INKUT116
00667          FROM IN016-PHYSICAL-COUNT-RECORD.                        INKUT116
00668                                                                   INKUT116
W28545*****************************************************************         
W28545* Z310-WRITE-NOT-QUALIFIED.                                               
W28545*****************************************************************         
W28545 Z310-WRITE-NOT-QUALIFIED.                                        INKUT116
W28545                                                                  INKUT116
W28545     WRITE PHYSICAL-NOT-QUALIFIED-REC                             INKUT116
W28545         FROM IN016-PHYSICAL-COUNT-RECORD.                        INKUT116
W28545                                                                  INKUT116
JF0900*****************************************************************         
JF0900* Z350-WRITE-RECS-EXCLUDED.                                               
JF0900*****************************************************************         
JF0900 Z350-WRITE-RECS-EXCLUDED.                                        INKUT116
JF0900                                                                  INKUT116
JF0900     WRITE RECORDS-EXCLUDED-RECORD                                INKUT116
JF0900         FROM IN016-PHYSICAL-COUNT-RECORD.                        INKUT116
                                                                        INKUT116
00699 ******************************************************************INKUT116
00700 * Z600-OPEN-DEPT-CURSOR.                                          INKUT116
00702 ******************************************************************INKUT116
00703  Z600-OPEN-DEPT-CURSOR.                                           INKUT116
00704                                                                   INKUT116
00705      INITIALIZE DCLTMDSEDP                                        INKUT116
00706                 DCLTMDSEAR.                                       INKUT116
00707                                                                   INKUT116
00708      PERFORM                                                      INKUT116
00709          WITH TEST AFTER                                          INKUT116
00710          VARYING PV-SQL-SUB                                       INKUT116
00711          FROM 1 BY 1                                              INKUT116
00712          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       INKUT116
00713                    OR SQLCODE = 0                                 INKUT116
00714                    OR SQLCODE = +100)                             INKUT116
00715                                                                   INKUT116
00716          EXEC SQL                                                 INKUT116
00717              OPEN DEPT_CURSOR                                     INKUT116
00718          END-EXEC                                                 INKUT116
00719                                                                   INKUT116
00720          EVALUATE TRUE                                            INKUT116
00721              WHEN SQLCODE = -904                                  INKUT116
00722              WHEN SQLCODE = -911                                  INKUT116
00723              WHEN SQLCODE = +100                                  INKUT116
00724              WHEN SQLCODE = 0                                     INKUT116
00725                  CONTINUE                                         INKUT116
00726              WHEN SQLWARN NOT = SPACES                            INKUT116
00727              WHEN SQLCODE NOT = ZERO                              INKUT116
00728                  MOVE 'Z600-OPEN-DEPT-CURSOR'                     INKUT116
00729                                TO  PV-PARAGRAPH-NAME              INKUT116
00730                  MOVE 'OPEN ON DEPT CURSOR'                       INKUT116
00731                                TO  PV-DB2-OPER-ATTEMPTED          INKUT116
00732                  PERFORM Z900-SQL-ABEND                           INKUT116
00733          END-EVALUATE                                             INKUT116
00734      END-PERFORM.                                                 INKUT116
00735 *                                                                 INKUT116
00736      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        INKUT116
00737          MOVE 'Z600-OPEN-DEPT-CURSOR'                             INKUT116
00738                                TO  PV-PARAGRAPH-NAME              INKUT116
00739          MOVE 'OPEN RETRIES EXCEEDED'                             INKUT116
00740                                TO  PV-DB2-OPER-ATTEMPTED          INKUT116
00741          PERFORM Z900-SQL-ABEND                                   INKUT116
00742      END-IF.                                                      INKUT116
00743                                                                   INKUT116
00744 *                                                                 INKUT116
00745 ******************************************************************INKUT116
00746 *    FETCH THE NEXT ROW OF THE DEPT CURSOR.                       INKUT116
00747 ******************************************************************INKUT116
00748  Z700-FETCH-DEPT-ROW.                                             INKUT116
00749                                                                   INKUT116
00750      EXEC SQL                                                     INKUT116
00751          FETCH DEPT_CURSOR                                        INKUT116
00752          INTO  :MDSEAR-DEPT-NBR,                                  INKUT116
00753                :MDSEDP-SKU-LVL-UPD-IND                            INKUT116
00754      END-EXEC.                                                    INKUT116
00755                                                                   INKUT116
00756      IF SQLCODE = +100                                            INKUT116
00757          SET PS-NO-MORE-DEPT-ROWS   TO TRUE                       INKUT116
00758      ELSE                                                         INKUT116
00759          IF SQLCODE NOT = 0                                       INKUT116
00760               MOVE 'Z700-FETCH-DEPT-ROW'                          INKUT116
00761                                        TO PV-PARAGRAPH-NAME       INKUT116
00762               MOVE 'FETCH ROW FROM DEPT CURSOR'                   INKUT116
00763                                       TO PV-DB2-OPER-ATTEMPTED    INKUT116
00764              PERFORM Z900-SQL-ABEND                               INKUT116
00765          END-IF                                                   INKUT116
00766      END-IF.                                                      INKUT116
00767 *                                                                 INKUT116
00768 ******************************************************************INKUT116
00769 *    CLOSE THE INVENTORY PARAMTERS CURSOR.                        INKUT116
00770 ******************************************************************INKUT116
00771  Z800-CLOSE-DEPT-CURSOR.                                          INKUT116
00772                                                                   INKUT116
00773      PERFORM                                                      INKUT116
00774          WITH TEST AFTER                                          INKUT116
00775          VARYING PV-SQL-SUB                                       INKUT116
00776          FROM 1 BY 1                                              INKUT116
00777          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       INKUT116
00778                    OR SQLCODE = 0)                                INKUT116
00779                                                                   INKUT116
00780          EXEC SQL                                                 INKUT116
00781              CLOSE DEPT_CURSOR                                    INKUT116
00782          END-EXEC                                                 INKUT116
00783                                                                   INKUT116
00784          EVALUATE TRUE                                            INKUT116
00785              WHEN SQLCODE = -904                                  INKUT116
00786              WHEN SQLCODE = -911                                  INKUT116
00787              WHEN SQLCODE = 0                                     INKUT116
00788                  CONTINUE                                         INKUT116
00789              WHEN SQLWARN NOT = SPACES                            INKUT116
00790              WHEN SQLCODE NOT = ZERO                              INKUT116
00791                  MOVE 'Z800-CLOSE-DEPT-CURSOR'                    INKUT116
00792                                TO  PV-PARAGRAPH-NAME              INKUT116
00793                  MOVE 'CLOSE ON DEPT CURSOR'                      INKUT116
00794                                TO  PV-DB2-OPER-ATTEMPTED          INKUT116
00795                  PERFORM Z900-SQL-ABEND                           INKUT116
00796          END-EVALUATE                                             INKUT116
00797      END-PERFORM.                                                 INKUT116
00798 *                                                                 INKUT116
00799      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        INKUT116
00800          MOVE 'Z800-CLOSE-DEPT-CURSOR'                            INKUT116
00801                                TO  PV-PARAGRAPH-NAME              INKUT116
00802          MOVE 'CLOSE RETRIES EXCEEDED'                            INKUT116
00803                                TO  PV-DB2-OPER-ATTEMPTED          INKUT116
00804          PERFORM Z900-SQL-ABEND                                   INKUT116
00805      END-IF.                                                      INKUT116
00806 *                                                                 INKUT116
00807 ****************************************************************  INKUT116
00808 *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *  INKUT116
00809 ****************************************************************  INKUT116
00810  Z900-SQL-ABEND.                                                  INKUT116
00811 *                                                                 INKUT116
00812      MOVE +4013                 TO ABEND-CODE.                    INKUT116
00813      DISPLAY '**  ABEND     **'.                                  INKUT116
00814      DISPLAY '**  PROGRAM   **  =  INKUT116'.                     INKUT116
00815      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            INKUT116
00816      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        INKUT116
00817 *                                                                 INKUT116
00818 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         INKUT116
00819 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        INKUT116
00820 *                                                                 INKUT116
00821      COPY DPPD004.                                                INKUT116
00822 *                                                                 INKUT116
00823      CALL 'ILBOABN0'  USING ABEND-CODE.                           INKUT116
00824 *                                                                 INKUT116
W28545******************************************************************        
W28545*    PRICE LOOK-UP PROCEDURE DIVISION COPYBOOK                            
W28545******************************************************************        
W28545                                                                          
W28545     COPY INPD400.                                                        
