000100*----------------------------------------------------------------*        
000200 IDENTIFICATION DIVISION.                                                 
000300*----------------------------------------------------------------*        
000400 PROGRAM-ID.    EPKUT409.                                                 
000500 AUTHOR.        STRATAGEM-ESH.                                            
000600 DATE-WRITTEN.  APRIL 2, 2007                                             
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*----------------------------------------------------------------*        
001000*   ** CREATE TIME STAMP FILE OF FILE NAMES FOR THE MIDTIER **   *        
001100*----------------------------------------------------------------*        
001200*   THIS PROGRAM PROCESSES A FILE OF FILE NAMES THAT HAVE BEEN            
001300*   FTP'D TO THE MIDTIER SERVER AND CREATES A TIMESTAMP FILES IN          
001400*   XML FORMAT THAT IS SUBSEQUENTLY FTP'D TO THE MIDTIER. THE             
001500*   PROGRAM EXECUTES IN EACH JOB THAT FTP'S THE FILES ASSOCIATED          
001600*   WITH A UNIT OF WORK. FOR EXAMPLE. PLU AND XREF ARE PART OF            
001700*   THE PRICING UNIT OF WORK.                                             
001800*                                                                         
001900*----------------------------------------------------------------*        
002000* HISTORY LOG:                                                            
002100*----------------------------------------------------------------*        
002200* DATE:                                                                   
002300* WR/IR#:                                                                 
002400* PROGRAMMER:                                                             
002500* DESCRIPTION:                                                            
002600*                                                                         
002700* DATE: APRIL 2, 2007                                                     
002800* WR/IR#: WR1467                                                          
002900* PROGRAMMER: ERIC S. HELLAND                                             
003000* DESCRIPTION: INITIAL CREATE                                             
003100*----------------------------------------------------------------*        
003200 ENVIRONMENT DIVISION.                                                    
003300*----------------------------------------------------------------*        
003400 CONFIGURATION SECTION.                                                   
003500 SOURCE-COMPUTER. IBM-3090.                                               
003600 OBJECT-COMPUTER. IBM-3090.                                               
003700                                                                          
003800 INPUT-OUTPUT SECTION.                                                    
003900                                                                          
004000 FILE-CONTROL.                                                            
004100                                                                          
004200     SELECT INPUT-FILE-01                     ASSIGN TO INP01.            
004300                                                                          
004400     SELECT VARIABLE-FILE-01                  ASSIGN TO OUT01.            
004500                                                                          
004600*----------------------------------------------------------------*        
004700 DATA DIVISION.                                                           
004800*----------------------------------------------------------------*        
004900 FILE SECTION.                                                            
005000                                                                          
005100 FD  INPUT-FILE-01                                                        
005200     RECORDING MODE IS F                                                  
005300     BLOCK CONTAINS 0 RECORDS                                             
005400     LABEL RECORDS ARE STANDARD                                           
005500     RECORD CONTAINS 80 CHARACTERS                                        
005600     DATA RECORD IS INPUT-RECORD-01.                                      
005700                                                                          
005800 01  INPUT-RECORD-01               PIC X(80).                             
005900                                                                          
006000 FD  VARIABLE-FILE-01                                                     
006100     RECORDING MODE IS V                                                  
006200     BLOCK CONTAINS 0 RECORDS                                             
006300     LABEL RECORDS ARE STANDARD                                           
006400     RECORD IS VARYING FROM 1 TO 80                                       
006500     DEPENDING ON PV-BYTE-COUNT                                           
006600     DATA RECORD IS VARIABLE-RECORD-01.                                   
006700                                                                          
006800 01  VARIABLE-RECORD-01.                                                  
006900     05 VARIABLE-BYTE              PIC X(01)                              
007000                                   OCCURS 1 TO 80  TIMES                  
007100                                   DEPENDING ON PV-BYTE-COUNT.            
007200                                                                          
007300*----------------------------------------------------------------*        
007400                                                                          
007500 WORKING-STORAGE SECTION.                                                 
007600                                                                          
007700*----------------------------------------------------------------*        
007800* PV- PROGRAM STATUS                                             *        
007900*----------------------------------------------------------------*        
008000 01 PV-PROGRAM-STATUS.                                                    
008100    05  FILLER                     PIC X(27)  VALUE                       
008200        'WORKING STORAGE STARTS HERE'.                                    
008300    05  PV-PARAGRAPH-NAME          PIC X(50)  VALUE SPACES.               
008400    05  PV-DB2-OPERATION           PIC X(50)  VALUE SPACES.               
008500    05  PV-OPERATION-MSG-1         PIC X(40)  VALUE SPACES.               
008600    05  PV-OPERATION-MSG-2         PIC X(40)  VALUE SPACES.               
008700                                                                          
008800 01  AC-ABEND-CODE                 PIC S9(04) VALUE +0 COMP.              
008900     88  AC-CONTROL-CARD-ERROR                VALUE +4003.                
009000     88  AC-DB2-ERROR                         VALUE +4013.                
009100     88  AC-CALENDAR-ROUTINE-ERROR            VALUE +4019.                
009200     88  AC-NON-DB2-ERROR                     VALUE +4444.                
009300                                                                          
009400*----------------------------------------------------------------*        
009500* PC- PROGRAM CONSTANTS                                          *        
009600*----------------------------------------------------------------*        
009700 01  PC-PROGRAM-CONSTANTS.                                                
009800     05  PC-PROGRAM-NAME           PIC X(08) VALUE 'EPKUT409'.            
009900     05  PC-MAX-RETRIES            PIC 9(01) VALUE 5.                     
010000                                                                          
010100*----------------------------------------------------------------*        
010200* PS- PROGRAM SWITCHES                                           *        
010300*----------------------------------------------------------------*        
010400 01  PS-PROGRAM-SWITCHES.                                                 
010500     05  PS-END-OF-FILE-SW         PIC X(01)  VALUE 'N'.                  
010600         88  PS-END-OF-FILE                   VALUE 'Y'.                  
010700         88  PS-NOT-END-OF-FILE               VALUE 'N'.                  
010800     05  PS-END-OF-FETCH-SW        PIC X(01)  VALUE 'N'.                  
010900         88  PS-END-OF-FETCH                  VALUE 'Y'.                  
011000         88  PS-NOT-END-OF-FETCH              VALUE 'N'.                  
011100                                                                          
011200*----------------------------------------------------------------*        
011300* PV- PROGRAM VARIABLES                                          *        
011400*----------------------------------------------------------------*        
011500 01 PV-PROGRAM-VARIABLES.                                                 
011600    05  PV-BYTE-COUNT              PIC 9(04) VALUE ZERO.                  
011700    05  PV-WRITE-REC-LNGTH         PIC 9(03) VALUE ZERO.                  
011800    05  PV-SQL-SUB                 PIC 9(01) VALUE ZERO.                  
011900    05  PV-TALLY-CNT               PIC 9(02) VALUE ZERO.                  
012000    05  PV-WORK-REC                PIC X(80) VALUE SPACES.                
012100    05  PV-INPUT-REC               PIC X(80) VALUE SPACES.                
012200    05  PV-SPACE                   PIC X(01).                             
012300                                                                          
012400    05  PV-VARIABLE-OUTPUT-FIELD.                                         
012500        10  PV-BIT-VARIABLE        PIC  X(01)                             
012600                                   VALUE SPACES                           
012700                                   OCCURS 80 TIMES                        
012800                                   INDEXED BY                             
012900                                       PV-BIT-IDX                         
013000                                       PV-STR-IDX                         
013100                                       PV-EOF-IDX.                        
013200                                                                          
013300                                                                          
013400*----------------------------------------------------------------*        
013500* PV- PROGRAM COUNT VARIABLES                                    *        
013600*----------------------------------------------------------------*        
013700 01 PV-PROGRAM-COUNTERS.                                                  
013800    05  PV-RECS-READ               PIC 9(09) VALUE 0.                     
013900    05  PV-RECS-WRITE              PIC 9(09) VALUE 0.                     
014000                                                                          
014100*----------------------------------------------------------------*        
014200* FILE COPYBOOK                                                  *        
014300*----------------------------------------------------------------*        
014400                                                                          
014500*----------------------------------------------------------------*        
014600* CONTROL CARD COPYBOOK                                          *        
014700*----------------------------------------------------------------*        
014800                                                                          
014900*----------------------------------------------------------------*        
015000* PD- DB2 VARIABLES (NON DCLGEN)                                 *        
015100*----------------------------------------------------------------*        
015200 01  PD-DB2-VARIABLES.                                                    
015300     05  PD-DB2-CURRENT-DATE       PIC X(10).                             
015400     05  PD-DB2-CURRENT-TIME       PIC X(08).                             
015500     05  PD-DB2-CURRENT-TMST       PIC X(26).                             
015600                                                                          
015700*----------------------------------------------------------------*        
015800* DB2 TABLE DCLGEN COPYBOOK                                      *        
015900*----------------------------------------------------------------*        
016000                                                                          
016100*----------------------------------------------------------------*        
016200* DB2 COMMUNICATIONS AREA                                        *        
016300*----------------------------------------------------------------*        
016400     EXEC SQL                                                             
016500          INCLUDE SQLCA                                                   
016600     END-EXEC.                                                            
016700*----------------------------------------------------------------*        
016800* VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        *        
016900*----------------------------------------------------------------*        
017000     COPY DPWS004.                                                        
017100                                                                          
017200*----------------------------------------------------------------*        
017300* DB2 ABEND COPYBOOK                                             *        
017400*----------------------------------------------------------------*        
017500     COPY DPWS900.                                                        
017600                                                                          
017700*----------------------------------------------------------------*        
017800* DB2 MESSAGE AREA                                               *        
017900*----------------------------------------------------------------*        
018000     COPY SQLCA2.                                                         
018100                                                                          
018200******************************************************************        
018300 PROCEDURE DIVISION.                                                      
018400******************************************************************        
018500 0000-MAINLINE.                                                           
018600                                                                          
018700     PERFORM 0100-INITIALIZATION.                                         
018800                                                                          
018900     PERFORM 1000-PROCESS                                                 
019000       UNTIL PS-END-OF-FILE.                                              
019100                                                                          
019200     PERFORM 9200-WRAPUP.                                                 
019300                                                                          
019400     GOBACK.                                                              
019500                                                                          
019600*0000-EXIT.                                                               
019700 EJECT.                                                                   
019800******************************************************************        
019900*     INTIALIZES VARIABLES, OPENS ANY FILES, AND PERFORMS        *        
020000*     ANY SET-UP ROUTINES.                                       *        
020100******************************************************************        
020200 0100-INITIALIZATION.                                                     
020300     MOVE '0100-INITIALIZATION'              TO PV-PARAGRAPH-NAME.        
020400                                                                          
020500     PERFORM 9990-GET-CURRENT.                                            
020600                                                                          
020700     DISPLAY '****************************************'.                  
020800     DISPLAY '**          EPKUT409 STARTED          **'.                  
020900     DISPLAY '****************************************'.                  
021000     DISPLAY '** CURRENT DATE ' PD-DB2-CURRENT-DATE                       
021100             '            **'.                                            
021200     DISPLAY '** CURRENT TIME ' PD-DB2-CURRENT-TIME                       
021300             '              **'.                                          
021400     DISPLAY '****************************************'.                  
021500                                                                          
021600     OPEN INPUT  INPUT-FILE-01                                            
021700          OUTPUT VARIABLE-FILE-01.                                        
021800                                                                          
021900     MOVE '<files>'                TO PV-WORK-REC.                        
022000     MOVE 7                        TO PV-BYTE-COUNT.                      
022100     WRITE VARIABLE-RECORD-01      FROM PV-WORK-REC.                      
022200     ADD 1                         TO PV-RECS-WRITE.                      
022300                                                                          
022400*0100-EXIT.                                                               
022500 EJECT.                                                                   
022600******************************************************************        
022700*     MAIN DRIVING PARAGRAPH OF PROGRAM.                         *        
022800******************************************************************        
022900 1000-PROCESS.                                                            
023000     MOVE '1000-PROCESS'                     TO PV-PARAGRAPH-NAME.        
023100                                                                          
023200     INITIALIZE PV-INPUT-REC.                                             
023300                                                                          
023400     READ INPUT-FILE-01               INTO PV-INPUT-REC                   
023500         AT END                                                           
023600             SET PS-END-OF-FILE       TO TRUE.                            
023700                                                                          
023800     IF PS-NOT-END-OF-FILE                                                
023900        ADD 1                      TO PV-RECS-READ                        
024000        PERFORM 1100-PROCESS-FILE-NAME                                    
024100     END-IF.                                                              
024200                                                                          
024300*1000-EXIT.  EXIT.                                                        
024400 EJECT.                                                                   
024500******************************************************************        
024600*     PROCESS NAME FILES TO OUTPUT FILE.                         *        
024700******************************************************************        
024800 1100-PROCESS-FILE-NAME.                                                  
024900     MOVE '1100-PROCESS-FILE-NAME          ' TO PV-PARAGRAPH-NAME.        
025000                                                                          
025100     MOVE SPACES                   TO VARIABLE-RECORD-01.                 
025200     INITIALIZE PV-TALLY-CNT                                              
025300                PV-WORK-REC.                                              
025400                                                                          
025500     STRING '<file>'                          delimited by size           
025600            PV-INPUT-REC                      delimited by ' '            
025700            '</file>'                         delimited by size           
025800       INTO PV-WORK-REC.                                                  
025900                                                                          
026000      MOVE 0                       TO PV-BYTE-COUNT.                      
026100      MOVE PV-WORK-REC             TO PV-VARIABLE-OUTPUT-FIELD            
026200      PERFORM 1101-FIND-DATA-END                                          
026300          VARYING PV-EOF-IDX FROM 80 BY -1                                
026400            UNTIL PV-BIT-VARIABLE (PV-EOF-IDX) NOT = SPACE.               
026500      PERFORM 1102-FIELD-MOVER                                            
026600          VARYING PV-BIT-IDX FROM 1 BY 1                                  
026700            UNTIL PV-BIT-IDX > PV-EOF-IDX.                                
026800                                                                          
026900     WRITE VARIABLE-RECORD-01.                                            
027000     ADD 1                         TO PV-RECS-WRITE.                      
027100                                                                          
027200*1100-EXIT.  EXIT.                                                        
027300 EJECT.                                                                   
027400*----------------------------------------------------------               
027500*  FINDS END OF "REAL" DATA IS LARGE TEXT FIELDS                          
027600*----------------------------------------------------------               
027700 1101-FIND-DATA-END.                                                      
027800                                                                          
027900     MOVE ' '                     TO PV-SPACE.                            
028000                                                                          
028100*1101-EXIT.  EXIT.                                                        
028200*----------------------------------------------------------               
028300*  MOVES, CHARACTER BY CHARACTER, EP468 FIELDS TO E470                    
028400*  FIELDS                                                                 
028500*----------------------------------------------------------               
028600 1102-FIELD-MOVER.                                                        
028700                                                                          
028800     ADD 1                        TO PV-BYTE-COUNT.                       
028900     MOVE PV-BIT-VARIABLE(PV-BIT-IDX) TO                                  
029000          VARIABLE-BYTE(PV-BYTE-COUNT).                                   
029100                                                                          
029200*4001-EXIT.  EXIT.                                                        
029300******************************************************************        
029400*     CLOSES ALL FILES AND DISPLAYS ANY NEEDED INFORMATION.      *        
029500******************************************************************        
029600 9200-WRAPUP.                                                             
029700     MOVE '9200-WRAPUP'                      TO PV-PARAGRAPH-NAME.        
029800                                                                          
029900     MOVE '</files>'               TO VARIABLE-RECORD-01.                 
030000     MOVE 8                        TO PV-BYTE-COUNT.                      
030100     WRITE VARIABLE-RECORD-01.                                            
030200     ADD 1                         TO PV-RECS-WRITE.                      
030300                                                                          
030400     PERFORM 9201-DISPLAY-TOTALS.                                         
030500                                                                          
030600     IF NOT AC-DB2-ERROR                                                  
030700        PERFORM 9990-GET-CURRENT.                                         
030800                                                                          
030900     DISPLAY '****************************************'.                  
031000     DISPLAY '**          EPKUT409 END              **'.                  
031100     DISPLAY '****************************************'.                  
031200     DISPLAY '**     END DATE ' PD-DB2-CURRENT-DATE                       
031300             '            **'.                                            
031400     DISPLAY '**     END TIME ' PD-DB2-CURRENT-TIME                       
031500             '              **'.                                          
031600     DISPLAY '****************************************'.                  
031700                                                                          
031800     CLOSE INPUT-FILE-01                                                  
031900           VARIABLE-FILE-01.                                              
032000                                                                          
032100*9200-EXIT.  EXIT.                                                        
032200 EJECT                                                                    
032300******************************************************************        
032400*     DISPLAY END OF JOB TOTALS                                  *        
032500******************************************************************        
032600 9201-DISPLAY-TOTALS.                                                     
032700     MOVE '9201-DISPLAY-TOTALS'              TO PV-PARAGRAPH-NAME.        
032800                                                                          
032900     DISPLAY '** RECORDS READ ==========> '                               
033000             PV-RECS-READ                                                 
033100             ' **'.                                                       
033200     DISPLAY '** RECORDS WRITTEN =======> '                               
033300             PV-RECS-WRITE                                                
033400             ' **'.                                                       
033500     DISPLAY '****************************************'.                  
033600                                                                          
033700*9201-EXIT.  EXIT.                                                        
033800 EJECT                                                                    
033900******************************************************************        
034000*     GET CURRENT DATE, TIME AND TIMESTAMP FROM DB2              *        
034100******************************************************************        
034200 9990-GET-CURRENT.                                                        
034300     MOVE '9990-GET-CURRENT'                 TO PV-PARAGRAPH-NAME.        
034400                                                                          
034500     PERFORM                                                              
034600         WITH TEST AFTER                                                  
034700         VARYING PV-SQL-SUB                                               
034800         FROM 1 BY 1                                                      
034900         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
035000                   OR  SQLCODE = 0)                                       
035100                                                                          
035200           EXEC SQL                                                       
035300              SELECT                                                      
035400                     CURRENT DATE                                         
035500                    ,CURRENT TIME                                         
035600                    ,CURRENT TIMESTAMP                                    
035700                INTO                                                      
035800                     :PD-DB2-CURRENT-DATE                                 
035900                    ,:PD-DB2-CURRENT-TIME                                 
036000                    ,:PD-DB2-CURRENT-TMST                                 
036100                FROM                                                      
036200                     SYSIBM.SYSDUMMY1                                     
036300           END-EXEC                                                       
036400                                                                          
036500         EVALUATE TRUE                                                    
036600             WHEN SQLCODE = 0                                             
036700             WHEN SQLCODE = -904                                          
036800             WHEN SQLCODE = -911                                          
036900             WHEN SQLCODE = -913                                          
037000                  CONTINUE                                                
037100             WHEN SQLWARN0 NOT EQUAL SPACE                                
037200             WHEN SQLCODE NOT EQUAL ZERO                                  
037300                  MOVE '9990-GET-CURRENT'                                 
037400                                     TO PV-PARAGRAPH-NAME                 
037500                  MOVE 'SELECT CURR DTE, TME & TMST'                      
037600                                     TO PV-DB2-OPERATION                  
037700                  PERFORM 9998-DB2-ERROR                                  
037800         END-EVALUATE                                                     
037900     END-PERFORM.                                                         
038000                                                                          
038100     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
038200        MOVE '1200-OPEN-REFRESH-CURSOR'                                   
038300                             TO  PV-PARAGRAPH-NAME                        
038400        MOVE 'OPEN RETRIES EXCEEDED'                                      
038500                              TO  PV-DB2-OPERATION                        
038600        PERFORM 9998-DB2-ERROR                                            
038700     END-IF.                                                              
038800                                                                          
038900*9990-EXIT.  EXIT.                                                        
039000 EJECT.                                                                   
039100*****************************************************************         
039200*     DB2 ABEND ROUTINE.                                        *         
039300*****************************************************************         
039400 9998-DB2-ERROR.                                                          
039500                                                                          
039600     PERFORM 9201-DISPLAY-TOTALS.                                         
039700                                                                          
039800     SET AC-DB2-ERROR              TO TRUE.                               
039900     DISPLAY '** ABEND     **'.                                           
040000     DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                        
040100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
040200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.                       
040300                                                                          
040400     COPY DPPD004.                                                        
040500     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
040600                                                                          
040700*9998-EXIT.  EXIT.                                                        
040800******************************************************************        
040900*     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                 *        
041000******************************************************************        
041100 9999-ABEND.                                                              
041200                                                                          
041300     PERFORM 9201-DISPLAY-TOTALS.                                         
041400                                                                          
041500     DISPLAY '** ABEND           **'.                                     
041600     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
041700     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
041800     DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.               
041900     DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.               
042000     CALL 'ILBOABN0' USING AC-ABEND-CODE.                                 
042100                                                                          
