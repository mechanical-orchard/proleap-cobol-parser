00001  IDENTIFICATION DIVISION.                                         03/03/94
00002                                                                   ETKCS331
00003  PROGRAM-ID.    ETKCS331.                                            LV014
00004  AUTHOR.        TESS BESTE.                                          CL**1
00005  INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
00006  DATE-WRITTEN.  03-02-94.                                            CL**1
00007  DATE-COMPILED.                                                      CL**1
00008 *----------------------------------------------------------------*   CL**1
00009 *        E331 - FUNCTIONAL GROUP/TRANSACTION SET ADD                 CL**1
00010 *                                                                    CL**1
00011 * THIS PROGRAM ALLOWS THE USER TO ADD A NEW EDI FUNCTIONAL GROUP     CL**1
00012 * AND TRANSACTION SET, OR TO ADD A NEW TRANSACTION SET TO AN         CL**1
00013 * EXISTING FUNCTIONAL GROUP.                                         CL**1
00014 *                                                                    CL**1
00015 * WHEN THE ADD FUNCTION IS INVOKED, AN EMPTY ADD SCREEN IS           CL**1
00016 * DISPLAYED. THE USER ENTERS THE INFORMATION FOR THE FUNCTIONAL      CL**1
00017 * GROUP AND TRANSACTION SET. ALL FIELDS ARE REQUIRED WITH ONE        CL**1
00018 * EXCEPTION: IF THE FUNCTIONAL GROUP ALREADY EXISTS, THE             CL**1
00019 * FUNCTIONAL GROUP NAME IS NOT REQUIRED.                             CL**1
00020 *                                                                    CL**1
00021 * WHEN THE ENTER OR ADD KEY IS PRESSED, THE DATA IS EDITED.          CL**1
00022 * IF THE ADD KEY IS PRESSED AND NO ERRORS ARE DETECTED DURING        CL**1
00023 * EDITING, THE INFORMATION IS ADDED TO THE TFCNGRP AND/OR TTRNSET    CL**1
00024 * TABLES.                                                            CL**1
00025 *----------------------------------------------------------------*   CL**1
CICS  *  CHANGED 10/26/2022    BY BARB SCHULTZ    WR: CHG0277210      *    CL**1
CICS  *                                                               *    CL**1
CICS  *  ADDED COPYBOOK DPWS930 AND MADE THE CICS ARCHITECTURE CALL   *    CL**1
CICS  *  DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.    *    CL**1
CICS  *----------------------------------------------------------------*   CL**1
00028                                                                      CL**1
00029  ENVIRONMENT DIVISION.                                               CL**1
00030                                                                      CL**1
00031  DATA DIVISION.                                                      CL**1
00032                                                                      CL**1
00033  WORKING-STORAGE SECTION.                                            CL**1
00034                                                                      CL**1
00035  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00036      05  PC-CURRENT-MAP-NAME          PIC  X(8)     VALUE            CL**1
00037                                                          'ET331A'.   CL**1
00038      05  PC-CURRENT-MAPSET-NAME       PIC  X(8)     VALUE            CL**1
00039                                                         'ETKM331'.   CL**1
00040      05  PC-CURRENT-PROGRAM-NAME      PIC  X(8)     VALUE            CL**1
00041                                                       'ETKCS331'.    CL**1
00042      05  PC-TSYMSG-NUMBERS.                                          CL**1
00043          10  PC-TSYMSG-00007          PIC  9(5)     VALUE 00007.     CL**1
00044          10  PC-TSYMSG-00117          PIC  9(5)     VALUE 00117.     CL*14
00045          10  PC-TSYMSG-00223          PIC  9(5)     VALUE 00223.     CL**1
00046          10  PC-TSYMSG-00232          PIC  9(5)     VALUE 00232.     CL**1
00047          10  PC-TSYMSG-00551          PIC  9(5)     VALUE 00551.     CL**1
00048                                                                      CL**1
00049  01  PS-PROGRAM-SWITCHES.                                            CL**1
00050      05  PS-ERROR-SW                  PIC  X        VALUE 'N'.       CL**1
00051          88  PS-ERROR                               VALUE 'Y'.       CL**1
00052          88  PS-NO-ERROR                            VALUE 'N'.       CL**1
00053      05  PS-ADD-SUCCESSFUL-SW         PIC  X.                        CL**9
00054          88 PS-ADD-SUCCESSFUL                       VALUE 'Y'.       CL**1
00055          88 PS-ADD-NOT-SUCCESSFUL                   VALUE 'N'.       CL**1
00056      05 PS-FUNC-GROUP-EXISTS-SW       PIC  X.                        CL**8
00057          88 PS-FUNC-GROUP-EXISTS                    VALUE 'Y'.       CL**8
00058          88 PS-FUNC-GROUP-DOES-NOT-EXIST            VALUE 'N'.       CL**8
00059                                                                      CL**1
00060                                                                      CL**1
00061 *----------------------------------------------------------------*   CL**1
00062 *    MAP LAYOUT                                                  *   CL**1
00063 *----------------------------------------------------------------*   CL**1
00064      COPY ETKM331.                                                   CL**1
00065                                                                      CL**1
00066 *----------------------------------------------------------------*   CL**1
00067 *    ATTRIBUTE SETTINGS COPYBOOK.                                *   CL**1
00068 *----------------------------------------------------------------*   CL**1
00069      COPY DPWS015.                                                   CL**1
00070                                                                      CL**1
00071 *----------------------------------------------------------------*   CL**1
00072 *    FUNCTION KEYS COPYBOOK                                      *   CL**1
00073 *----------------------------------------------------------------*   CL**1
00074      COPY DPWS016.                                                   CL**1
00075                                                                      CL**1
00076 *-----------------------------------------------------------------   CL**1
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.               CL**1
00078 *-----------------------------------------------------------------   CL**1
00079      COPY DPWS030.                                                   CL**1
CICS       COPY DPWS930.                                                   CL**1
00080                                                                      CL**1
00081 *-----------------------------------------------------------------   CL**1
00082 *    STANDARD COMMAREA.                                              CL**1
00083 *-----------------------------------------------------------------   CL**1
00084      COPY DPWS020.                                                   CL**1
00085      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                   CL**1
00086                                                                      CL**1
00087 *-----------------------------------------------------------------   CL**1
00088 *    INTER-APPLICATION COMMAREA.                                     CL**1
00089 *-----------------------------------------------------------------   CL**1
00090          10  INTER-APPL-COMM-AREA PIC X(200).                        CL**1
00091                                                                      CL**1
00092 *-----------------------------------------------------------------   CL**1
00093 *    APPLICATION SPECIFIC COMMMAREA FOR ETKCS331.                    CL**1
00094 *-----------------------------------------------------------------   CL**1
00095          10  ASC-SPECIFIC-COMMAREA.                                  CL**1
00096              15  ASC-MAP-FIELDS.                                     CL*11
00097                  20  ASC-FUNCTIONAL-GROUP                            CL**5
00098                                       PIC  X(2).                     CL*11
00099                  20  ASC-FG-NAME      PIC  X(25).                    CL*11
00100                  20  ASC-TRANSACTION-SET                             CL**5
00101                                       PIC  X(3).                     CL*11
00102                  20  ASC-TS-NAME      PIC  X(25).                    CL*11
00103                  20  ASC-ABBREVIATION PIC  X(3).                     CL*11
00104          10  FILLER                   PIC  X(2807).                  CL*11
00105                                                                      CL**1
00106 *----------------------------------------------------------------*   CL**1
00107 *    ABEND PROCESSING COPYBOOK.                                  *   CL**1
00108 *----------------------------------------------------------------*   CL**1
00109      COPY DPWS013.                                                   CL**1
00110                                                                      CL**1
00111 *---------------------------------------------------------------*    CL**1
00112 *    DB2 AREA FOR FUNCTIONAL GROUP TABLE                             CL*11
00113 *---------------------------------------------------------------*    CL**1
00114      EXEC SQL                                                        CL**1
00115           INCLUDE TFCNGRP                                            CL*11
00116      END-EXEC.                                                       CL**1
00117                                                                      CL**1
00118 *---------------------------------------------------------------*    CL*11
00119 *    DB2 AREA FOR TRANSACTION SET TABLE                              CL*11
00120 *---------------------------------------------------------------*    CL*11
00121      EXEC SQL                                                        CL*11
00122           INCLUDE TTRNSET                                            CL*11
00123      END-EXEC.                                                       CL*11
00124                                                                      CL*11
00125 *---------------------------------------------------------------*    CL**1
00126 *    DB2 AREA FOR COMMUNICATIONS                                     CL**1
00127 *---------------------------------------------------------------*    CL**1
00128      EXEC SQL                                                        CL**1
00129           INCLUDE SQLCA                                              CL**1
00130      END-EXEC.                                                       CL**1
00131                                                                      CL**1
00132                                                                      CL**1
00133  LINKAGE SECTION.                                                    CL**1
00134                                                                      CL**1
00135  01  DFHCOMMAREA.                                                    CL**1
00136      05  FILLER                       OCCURS  1 TO 4072 TIMES        CL**1
00137                                       DEPENDING ON EIBCALEN.         CL**1
00138          10  FILLER                   PIC  X.                        CL**1
00139                                                                      CL**1
00140  EJECT                                                               CL**1
00141  PROCEDURE DIVISION.                                                 CL**1
00142                                                                      CL**1
00143 *---------------------------------------------------------------*    CL**1
00144 *  THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM.  *    CL**1
00145 *  THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API   *    CL**1
00146 *  MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.             *    CL**1
00147 *                                                               *    CL**1
00148 *  THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE   *    CL**1
00149 *  LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.        *    CL**1
00150 *---------------------------------------------------------------*    CL**1
00151  0000-MAIN-MODULE.                                                   CL**1
00152                                                                      CL**1
00153      INITIALIZE DP030-CICS-API-FIELDS.                               CL**1
00154      MOVE +1                       TO DP030-NUMBER-OF-MAPS.          CL**1
00155      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.             CL**1
00156      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).            CL**1
00157      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                          CL**1
00158      MOVE LENGTH OF ET331AI        TO DP030-MAP-LENGTH (1).          CL**1
00159      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).        CL**1
00160      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00161                                                                      CL**1
00162      PERFORM 1000-CONTROL-PROCESSING                                 CL**1
00163                                                                      CL**1
00164      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).        CL**1
00165      PERFORM 0001-CALL-CICS-ARCH-API.                                CL**1
00166                                                                      CL**1
00167                                                                      CL**1
00168  0001-CALL-CICS-ARCH-API.                                            CL**1
00169                                                                      CL**1
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                         CL**1
00171                                     DFHCOMMAREA                      CL**1
00172                                     DP030-CICS-API-FIELDS            CL**1
00173                                     DP020-STANDARD-COMMAREA          CL**1
00174                                     ET331AI.                         CL**1
00175                                                                      CL**1
00176      IF  DP030-RC-CALL-SUCCESSFUL                                    CL**1
00177          CONTINUE                                                    CL**1
00178      ELSE                                                            CL**1
00179          SET DP013-NO-ROLLBACK                                       CL**1
00180              DP013-XCTL-DISPLAY-RESTART                              CL**1
00181              DP013-CICS-ABEND      TO TRUE                           CL**1
00182          MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH           CL**1
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O   CL**1
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)         CL**1
00185          MOVE DP030-RETURN-CODE         TO DP013-MESSAGE-TEXT (3)    CL**1
00186          PERFORM DP013-0000-PROCESS-ABEND                            CL**1
00187      END-IF.                                                         CL**1
00188                                                                      CL**1
00189                                                                      CL**1
00190 *----------------------------------------------------------------*   CL**1
00191 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *   CL**1
00192 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *   CL**1
00193 *----------------------------------------------------------------*   CL**1
00194  1000-CONTROL-PROCESSING.                                            CL**1
00195                                                                      CL**1
00196      EVALUATE TRUE                                                   CL**1
00197          WHEN DP020-NEXT-ACT-INITIAL                                 CL**1
00198              INITIALIZE ASC-SPECIFIC-COMMAREA                        CL**1
00199              PERFORM 4000-BUILD-INITIAL-PANEL                        CL**1
00200                                                                      CL**1
00201          WHEN DP020-NEXT-ACT-READ-MAP                                CL**1
00202              PERFORM 2000-PROCESS-PANEL                              CL**1
00203                                                                      CL**1
00204          WHEN DP020-NEXT-ACT-RETURN                                  CL**1
00205              PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
00206              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00207                                                                      CL**1
00208          WHEN OTHER                                                  CL**1
00209              SET DP013-LOGIC-ABEND                                   CL**1
00210                  DP013-NO-ROLLBACK TO TRUE                           CL**1
00211              MOVE '1000-CONTROL-PROCESSING'                          CL**1
00212                                    TO DP013-PARAGRAPH                CL**1
00213              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'      CL**1
00214                                    TO DP013-MESSAGE-TEXT(1)          CL**1
00215              MOVE DP020-NEXT-APPL-ACTIVITY                           CL**1
00216                                    TO DP013-MESSAGE-TEXT(2)          CL**1
00217              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00218      END-EVALUATE.                                                   CL**1
00219                                                                      CL**1
00220                                                                      CL**1
00221 *----------------------------------------------------------------*   CL**1
00222 * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*   CL**1
00223 * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*   CL**1
00224 * EDITS.                                                         *   CL**1
00225 *----------------------------------------------------------------*   CL**1
00226  2000-PROCESS-PANEL.                                                 CL**1
00227                                                                      CL**1
00228      EVALUATE TRUE                                                   CL**1
00229          WHEN DP020-SRC-AID = DP016-CLEAR                            CL**1
00230              PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
00231              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**1
00232                                                                      CL**1
00233          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                       CL**1
00234              INITIALIZE ASC-MAP-FIELDS                               CL**2
00235              PERFORM 4000-BUILD-INITIAL-PANEL                        CL**1
00236                                                                      CL**1
00237          WHEN OTHER                                                  CL**1
00238              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                    CL**2
00239              PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
00240              IF  PS-NO-ERROR                                         CL**2
00241                  PERFORM 2100-CHECK-FUNCTION-KEY                     CL**2
00242              END-IF                                                  CL**2
00243              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                    CL**2
00244                                                                      CL**1
00245      END-EVALUATE.                                                   CL**1
00246                                                                      CL**1
00247                                                                      CL**1
00248 *----------------------------------------------------------------*   CL**1
00249 *  ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED      *   CL**1
00250 *  FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED  *   CL**1
00251 *  FROM THE CICS ARCHITECTURE API.                               *   CL**1
00252 *----------------------------------------------------------------*   CL**1
00253  2100-CHECK-FUNCTION-KEY.                                            CL**1
00254                                                                      CL**1
00255      EVALUATE TRUE                                                   CL**1
00256          WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                 CL**1
00257              PERFORM 4500-ADD-FG-TSET                                CL*10
00258              IF PS-ADD-SUCCESSFUL                                    CL**8
00259                  INITIALIZE ASC-MAP-FIELDS                           CL**8
00260 *                ---- PREVIOUS OBJECT HAS BEEN ADDED ----            CL**8
00261                  MOVE PC-TSYMSG-00223  TO DP020-MSG-NUMBER           CL**8
00262                  SET  DP020-MSG-INFORMATIONAL TO TRUE                CL**8
00263              END-IF                                                  CL**8
00264                                                                      CL**1
00265          WHEN DP020-SRC-AID = DP016-ENTER                            CL**1
00266              CONTINUE                                                CL**3
00267                                                                      CL**1
00268          WHEN OTHER                                                  CL**1
00269              SET DP013-NO-ROLLBACK                                   CL**1
00270                  DP013-XCTL-DISPLAY-RESTART                          CL**1
00271                  DP013-CICS-ABEND  TO TRUE                           CL**1
00272              MOVE '2100-CHECK-FUNCTION-KEY'                          CL**1
00273                                    TO DP013-PARAGRAPH                CL**1
00274              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'         CL**1
00275                                    TO DP013-MESSAGE-TEXT (1)         CL**1
00276              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00277      END-EVALUATE.                                                   CL**1
00278                                                                      CL**1
00279                                                                      CL**1
00280 *----------------------------------------------------------------*   CL**1
00281 * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*   CL**1
00282 * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *   CL**1
00283 * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *   CL**1
00284 *----------------------------------------------------------------*   CL**1
00285  2200-MOVE-SCREEN-TO-COMMAREA.                                       CL**1
00286                                                                      CL**1
00287      IF AFGRPL > ZERO                                                CL**5
00288          MOVE AFGRPI TO ASC-FUNCTIONAL-GROUP                         CL**5
00289      ELSE                                                            CL**1
00290          IF AFGRPF = DP015-ERASE-EOF                                 CL**5
00291              MOVE SPACE TO ASC-FUNCTIONAL-GROUP                      CL**5
00292          END-IF                                                      CL**1
00293      END-IF.                                                         CL**1
00294                                                                      CL**1
00295      IF AFGNML > ZERO                                                CL**5
00296          MOVE AFGNMI TO ASC-FG-NAME                                  CL**5
00297      ELSE                                                            CL**1
00298          IF AFGNMF = DP015-ERASE-EOF                                 CL**5
00299              MOVE SPACE TO ASC-FG-NAME                               CL**5
00300          END-IF                                                      CL**1
00301      END-IF.                                                         CL**1
00302                                                                      CL**1
00303      IF ATSETL > ZERO                                                CL**5
00304          MOVE ATSETI TO ASC-TRANSACTION-SET                          CL**5
00305      ELSE                                                            CL**1
00306          IF ATSETF = DP015-ERASE-EOF                                 CL**5
00307              MOVE SPACE TO ASC-TRANSACTION-SET                       CL**5
00308          END-IF                                                      CL**1
00309      END-IF.                                                         CL**1
00310                                                                      CL**1
00311      IF ATSNML > ZERO                                                CL**5
00312          MOVE ATSNMI TO ASC-TS-NAME                                  CL**5
00313      ELSE                                                            CL**1
00314          IF ATSNMF = DP015-ERASE-EOF                                 CL**5
00315              MOVE SPACE TO ASC-TS-NAME                               CL**5
00316          END-IF                                                      CL**1
00317      END-IF.                                                         CL**1
00318                                                                      CL**1
00319      IF AABBRL > ZERO                                                CL**5
00320          MOVE AABBRI TO ASC-ABBREVIATION                             CL**5
00321      ELSE                                                            CL**1
00322          IF AABBRF = DP015-ERASE-EOF                                 CL**5
00323              MOVE SPACE TO ASC-ABBREVIATION                          CL**5
00324          END-IF                                                      CL**1
00325      END-IF.                                                         CL**1
00326                                                                      CL**1
00327                                                                      CL**1
00328 *----------------------------------------------------------------*   CL**1
00329 * EDIT EACH FIELD ENTERED BY THE USER. IF AN ERROR IS FOUND,         CL**1
00330 * POSITION THE CURSOR ON THAT FIELD AND DISPLAY THE APPROPRIATE  *   CL**1
00331 * ERROR MESSAGE.                                                     CL**1
00332 *----------------------------------------------------------------*   CL**1
00333  3000-EDIT-DATA-IN-COMMAREA.                                         CL**1
00334                                                                      CL**1
00335      PERFORM 3225-RESET-ATTRIBUTES.                                  CL**1
00336      PERFORM 3250-EDIT-MAP-FIELDS.                                   CL**8
00337                                                                      CL**1
00338      SET DP030-SET-CURSOR-APPL-1 TO TRUE.                            CL**1
00339      IF PS-NO-ERROR                                                  CL**1
00340          MOVE -1 TO AFGRPL                                           CL**5
00341      END-IF.                                                         CL**1
00342                                                                      CL**1
00343                                                                      CL**1
00344  3225-RESET-ATTRIBUTES.                                              CL**1
00345                                                                      CL**1
00346      MOVE DP015-UNP-ALP-NOR-OFF     TO AFGRPA                        CL**6
00347                                        AFGNMA                        CL**6
00348                                        ATSETA                        CL**5
00349                                        ATSNMA                        CL**5
00350                                        AABBRA.                       CL*11
00351                                                                      CL**1
00352      MOVE DP015-GREEN               TO AFGRPC                        CL**6
00353                                        AFGNMC                        CL**6
00354                                        ATSETC                        CL**5
00355                                        ATSNMC                        CL**5
00356                                        AABBRC.                       CL**6
00357                                                                      CL**1
00358      MOVE DP015-UNDERLINE           TO AFGRPH                        CL**6
00359                                        AFGNMH                        CL**6
00360                                        ATSETH                        CL**5
00361                                        ATSNMH                        CL**5
00362                                        AABBRH.                       CL**6
00363                                                                      CL**1
00364                                                                      CL**1
00365  3250-EDIT-MAP-FIELDS.                                               CL**8
00366                                                                      CL**1
00367 *    *------------------------------------------------------------   CL**1
00368 *    * EDIT THE TRANSACTION SET ABBREVIATION.                        CL**6
00369 *    *------------------------------------------------------------   CL**1
00370                                                                      CL**1
00371      IF ASC-ABBREVIATION = SPACE                                     CL**6
00372          SET PS-ERROR                                                CL**6
00373              DP020-MSG-FATAL         TO TRUE                         CL**6
00374 *        ---- REQUIRED FIELD ----                                    CL**6
00375          MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER             CL**6
00376          MOVE DP015-UNP-ALP-BRT-OFF  TO AABBRA                       CL**6
00377          MOVE DP015-RED              TO AABBRC                       CL**6
00378          MOVE DP015-REVERSE          TO AABBRH                       CL**6
00379          MOVE -1                     TO AABBRL                       CL**6
00380      END-IF.                                                         CL**1
00381                                                                      CL**1
00382 *    *------------------------------------------------------------   CL**1
00383 *    * EDIT THE TRANSACTION SET NAME.                                CL**6
00384 *    *------------------------------------------------------------   CL**1
00385                                                                      CL**1
00386      IF  ASC-TS-NAME = SPACE                                         CL**6
00387          SET PS-ERROR                                                CL**6
00388              DP020-MSG-FATAL         TO TRUE                         CL**6
00389 *        ---- REQUIRED FIELDS ----                                   CL**6
00390          MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER             CL**6
00391          MOVE DP015-UNP-ALP-BRT-OFF  TO ATSNMA                       CL**6
00392          MOVE DP015-RED              TO ATSNMC                       CL**6
00393          MOVE DP015-REVERSE          TO ATSNMH                       CL**6
00394          MOVE -1                     TO ATSNML                       CL**6
00395      END-IF.                                                         CL**6
00396                                                                      CL**1
00397 *    *------------------------------------------------------------   CL**1
00398 *    * EDIT THE TRANSACTION SET. A TRANSACTION SET CAN ONLY BE       CL*11
00399 *    * USED FOR ONE FUNCTIONAL GROUP, SO MAKE SURE THE TRANSACTION   CL*11
00400 *    * SET DOES NOT ALREADY EXIST.                                   CL*11
00401 *    *------------------------------------------------------------   CL**1
00402                                                                      CL**1
00403      IF ASC-TRANSACTION-SET = SPACE                                  CL**6
00404          SET PS-ERROR                                                CL**6
00405              DP020-MSG-FATAL         TO TRUE                         CL**6
00406 *        ---- REQUIRED FIELD ----                                    CL**6
00407          MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER             CL**6
00408          MOVE DP015-UNP-ALP-BRT-OFF  TO ATSETA                       CL**6
00409          MOVE DP015-RED              TO ATSETC                       CL**6
00410          MOVE DP015-REVERSE          TO ATSETH                       CL**6
00411          MOVE -1                     TO ATSETL                       CL**6
00412      ELSE                                                            CL*10
00413          PERFORM 5025-READ-TTRNSET                                   CL*11
00414          EVALUATE TRUE                                               CL*11
00415              WHEN SQLCODE = +0                                       CL*11
00416                  SET PS-ERROR                                        CL*11
00417                      DP020-MSG-FATAL         TO TRUE                 CL*11
00418 *                ---- CANNOT ADD - RECORD ALREADY EXISTS ----        CL*14
00419                  MOVE PC-TSYMSG-00117        TO DP020-MSG-NUMBER     CL*14
00420                  MOVE DP015-UNP-ALP-BRT-OFF  TO ATSETA               CL*11
00421                  MOVE DP015-RED              TO ATSETC               CL*11
00422                  MOVE DP015-REVERSE          TO ATSETH               CL*11
00423                  MOVE -1                     TO ATSETL               CL*11
00424              WHEN SQLCODE = +100                                     CL*11
00425                  CONTINUE                                            CL*11
00426              WHEN OTHER                                              CL*11
00427                  SET PS-ERROR                                        CL*11
00428                      DP020-MSG-WARNING TO TRUE                       CL*11
00429 *                ---- WAIT 15 SECONDS, THEN RETRY ----               CL*11
00430                  MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER           CL*11
00431          END-EVALUATE                                                CL*11
00432      END-IF.                                                         CL**6
00433                                                                      CL**1
00434 *    *------------------------------------------------------------   CL**1
00435 *    * EDIT THE FUNCTIONAL GROUP AND FUNCTIONAL GROUP NAME. IF THE   CL*11
00436 *    * FUNCTIONAL GROUP ALREADY EXISTS, THE NAME IS NOT REQUIRED.    CL*11
00437 *    * IF ENTERED, IT WILL BE REPLACED WITH THE NAME ALREADY         CL*11
00438 *    * ESTABLISHED ON THE TFCNGRP TABLE. IF THE FUNCTIONAL GROUP     CL*11
00439 *    * DOES NOT ALREADY EXIST, THE NAME IS REQUIRED.                 CL*11
00440 *    *------------------------------------------------------------   CL**1
00441                                                                      CL**1
00442      IF ASC-FUNCTIONAL-GROUP = SPACE                                 CL**7
00443          SET PS-ERROR                                                CL**7
00444              DP020-MSG-FATAL         TO TRUE                         CL**7
00445 *        ---- REQUIRED FIELD ----                                    CL**7
00446          MOVE PC-TSYMSG-00007        TO DP020-MSG-NUMBER             CL**7
00447          MOVE DP015-UNP-ALP-BRT-OFF  TO AFGRPA                       CL**7
00448          MOVE DP015-RED              TO AFGRPC                       CL**7
00449          MOVE DP015-REVERSE          TO AFGRPH                       CL**7
00450          MOVE -1                     TO AFGRPL                       CL**7
00451          IF ASC-FG-NAME = SPACE                                      CL**7
00452              MOVE DP015-UNP-ALP-BRT-OFF  TO AFGNMA                   CL**7
00453              MOVE DP015-RED              TO AFGNMC                   CL**7
00454              MOVE DP015-REVERSE          TO AFGNMH                   CL**7
00455          END-IF                                                      CL**7
00456      ELSE                                                            CL**7
00457          PERFORM 5000-READ-TFCNGRP                                   CL*10
00458          EVALUATE TRUE                                               CL*11
00459              WHEN SQLCODE = +0                                       CL*11
00460                  SET PS-FUNC-GROUP-EXISTS TO TRUE                    CL*11
00461                  MOVE FCNGRP-FUNC-NME     TO ASC-FG-NAME             CL*11
00462              WHEN SQLCODE = +100                                     CL*11
00463                  SET PS-FUNC-GROUP-DOES-NOT-EXIST TO TRUE            CL*11
00464                  IF ASC-FG-NAME = SPACE                              CL*11
00465                      SET PS-ERROR                                    CL*11
00466                          DP020-MSG-FATAL          TO TRUE            CL*12
00467 *                    ---- REQUIRED FIELD ----                        CL*11
00468                      MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER   CL*12
00469                      MOVE DP015-UNP-ALP-BRT-OFF                      CL*12
00470                                                TO AFGNMA             CL*12
00471                      MOVE DP015-RED            TO AFGNMC             CL*12
00472                      MOVE DP015-REVERSE        TO AFGNMH             CL*12
00473                      MOVE -1                   TO AFGNML             CL*12
00474                  END-IF                                              CL*11
00475              WHEN OTHER                                              CL*11
00476                  SET PS-ERROR                                        CL*11
00477                      DP020-MSG-WARNING TO TRUE                       CL*11
00478 *                ---- WAIT 15 SECONDS, THEN RETRY ----               CL*11
00479                  MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER           CL*11
00480          END-EVALUATE                                                CL*11
00481      END-IF.                                                         CL**1
00482                                                                      CL**1
00483                                                                      CL**1
00484 *-----------------------------------------------------------------   CL*11
00485 * DISPLAY THE SCREEN WITH ALL DATA FIELDS EMPTY AND UNPROTECTED,     CL*11
00486 * AND THE CURSOR ON NETWORK ID.                                      CL*11
00487 *-----------------------------------------------------------------   CL*11
00488  4000-BUILD-INITIAL-PANEL.                                           CL**1
00489                                                                      CL**1
00490      SET DP030-SET-CURSOR-APPL-1 TO TRUE.                            CL**4
00491      MOVE -1 TO AFGRPL.                                              CL**5
00492      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                           CL**1
00493                                                                      CL**1
00494                                                                      CL**1
00495  4400-MOVE-COMMAREA-TO-SCREEN.                                       CL**1
00496                                                                      CL**1
00497      MOVE ASC-FUNCTIONAL-GROUP      TO AFGRPO.                       CL**5
00498      MOVE ASC-FG-NAME               TO AFGNMO.                       CL**5
00499      MOVE ASC-TRANSACTION-SET       TO ATSETO.                       CL**5
00500      MOVE ASC-TS-NAME               TO ATSNMO.                       CL**7
00501      MOVE ASC-ABBREVIATION          TO AABBRO.                       CL**7
00502                                                                      CL**1
00503                                                                      CL**1
00504  4500-ADD-FG-TSET.                                                   CL*10
00505                                                                      CL*10
00506      SET PS-ADD-SUCCESSFUL TO TRUE.                                  CL*10
00507                                                                      CL*10
00508      IF PS-FUNC-GROUP-DOES-NOT-EXIST                                 CL*10
00509          MOVE ASC-FUNCTIONAL-GROUP    TO FCNGRP-FUNC-CDE             CL*13
00510          MOVE ASC-FG-NAME             TO FCNGRP-FUNC-NME             CL*10
00511          PERFORM 5050-INSERT-TFCNGRP-ROW                             CL*10
00512          IF SQLCODE NOT = 0                                          CL*10
00513              SET PS-ADD-NOT-SUCCESSFUL                               CL*10
00514                  DP020-MSG-WARNING TO TRUE                           CL*10
00515 *            ---- WAIT 15 SECONDS, THEN RETRY ----                   CL*10
00516              MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER               CL*10
00517          END-IF                                                      CL*10
00518      END-IF.                                                         CL*13
00519                                                                      CL*10
00520      IF PS-ADD-SUCCESSFUL                                            CL*13
00521          MOVE ASC-TRANSACTION-SET    TO TRNSET-TRAN-SET-CDE          CL*10
00522          MOVE ASC-TS-NAME            TO TRNSET-TRAN-SET-NME          CL*10
00523          MOVE ASC-ABBREVIATION       TO TRNSET-TRAN-SET-NME-ID       CL*10
00524          MOVE ASC-FUNCTIONAL-GROUP   TO TRNSET-FUNC-CDE              CL*10
00525                                                                      CL*10
00526          PERFORM 5100-INSERT-TTRNSET-ROW                             CL*10
00527                                                                      CL*10
00528          IF SQLCODE NOT = 0                                          CL*10
00529              SET PS-ADD-NOT-SUCCESSFUL                               CL*10
00530                  DP020-MSG-WARNING TO TRUE                           CL*10
00531 *            ---- WAIT 15 SECONDS, THEN RETRY ----                   CL*10
00532              MOVE PC-TSYMSG-00551  TO DP020-MSG-NUMBER               CL*10
00533          END-IF                                                      CL*10
00534      END-IF.                                                         CL*10
00535                                                                      CL*10
00536                                                                      CL*10
00537  5000-READ-TFCNGRP.                                                  CL*10
00538                                                                      CL*10
00539      EXEC SQL                                                        CL*10
00540           SELECT FUNC_NME                                            CL*10
00541           INTO   :FCNGRP-FUNC-NME                                    CL*10
00542           FROM TFCNGRP                                               CL*10
00543           WHERE FUNC_CDE = :ASC-FUNCTIONAL-GROUP                     CL*10
00544      END-EXEC.                                                       CL*10
00545                                                                      CL*10
00546      EVALUATE TRUE                                                   CL*10
00547          WHEN SQLCODE = ZERO                                         CL*10
00548          WHEN SQLCODE = +100                                         CL*10
00549          WHEN SQLCODE = -904                                         CL*11
00550          WHEN SQLCODE = -913                                         CL*11
00551              CONTINUE                                                CL*10
00552          WHEN SQLWARN0 NOT = SPACE                                   CL*10
00553          WHEN SQLCODE  NOT = ZERO                                    CL*10
00554              MOVE '5000-READ-TFCNGRP'  TO DP013-PARAGRAPH            CL*10
00555              MOVE 'SELECT FROM TFCNGRP'                              CL*10
00556                                        TO DP013-MESSAGE-TEXT (1)     CL*10
00557              MOVE 'TFCNGRP '           TO DP013-DB2-TABLE-NAME (1)   CL*10
00558              MOVE SQLCA                TO DP013-SQLCA                CL*10
00559              SET DP013-DB2-ABEND       TO TRUE                       CL*10
00560              PERFORM DP013-0000-PROCESS-ABEND                        CL*10
00561      END-EVALUATE.                                                   CL*10
00562                                                                      CL*10
00563                                                                      CL*10
00564  5025-READ-TTRNSET.                                                  CL*11
00565                                                                      CL*11
00566      EXEC SQL                                                        CL*11
00567           SELECT TRAN_SET_NME                                        CL*11
00568           INTO   :TRNSET-TRAN-SET-NME                                CL*11
00569           FROM   TTRNSET                                             CL*11
00570           WHERE  TRAN_SET_CDE = :ASC-TRANSACTION-SET                 CL*11
00571      END-EXEC.                                                       CL*11
00572                                                                      CL*11
00573      EVALUATE TRUE                                                   CL*11
00574          WHEN SQLCODE = ZERO                                         CL*11
00575          WHEN SQLCODE = +100                                         CL*11
00576          WHEN SQLCODE = -904                                         CL*11
00577          WHEN SQLCODE = -913                                         CL*11
00578              CONTINUE                                                CL*11
00579          WHEN SQLWARN0 NOT = SPACE                                   CL*11
00580          WHEN SQLCODE  NOT = ZERO                                    CL*11
00581              MOVE '5000-READ-TTRNSET'  TO DP013-PARAGRAPH            CL*11
00582              MOVE 'SELECT FROM TTRNSET'                              CL*11
00583                                        TO DP013-MESSAGE-TEXT (1)     CL*11
00584              MOVE 'TTRNSET '           TO DP013-DB2-TABLE-NAME (1)   CL*11
00585              MOVE SQLCA                TO DP013-SQLCA                CL*11
00586              SET DP013-DB2-ABEND       TO TRUE                       CL*11
00587              PERFORM DP013-0000-PROCESS-ABEND                        CL*11
00588      END-EVALUATE.                                                   CL*11
00589                                                                      CL*11
00590                                                                      CL*11
00591  5050-INSERT-TFCNGRP-ROW.                                            CL**7
00592                                                                      CL**1
00593      EXEC SQL                                                        CL**1
00594          INSERT INTO TFCNGRP                                         CL*11
00595             (                                                        CL**1
00596              FUNC_CDE                                                CL*11
00597             ,FUNC_NME                                                CL*11
00598             )                                                        CL**1
00599          VALUES                                                      CL**1
00600             (                                                        CL**1
00601              :FCNGRP-FUNC-CDE                                        CL*11
00602             ,:FCNGRP-FUNC-NME                                        CL*11
00603             )                                                        CL**1
00604      END-EXEC.                                                       CL**1
00605                                                                      CL**1
00606      EVALUATE TRUE                                                   CL**1
00607          WHEN SQLCODE = ZERO                                         CL**1
00608          WHEN SQLCODE = -904                                         CL**1
00609          WHEN SQLCODE = -913                                         CL**1
00610              CONTINUE                                                CL**1
00611          WHEN SQLWARN0 NOT = SPACES                                  CL**1
00612          WHEN SQLCODE  NOT = ZERO                                    CL**1
00613              MOVE '5050-INSERT-TFCNGRP-ROW'                          CL*11
00614                                TO DP013-PARAGRAPH                    CL**1
00615              MOVE 'INSERT A ROW INTO THE TFCNGRP TABLE'              CL*11
00616                                TO DP013-MESSAGE-TEXT(1)              CL**1
00617              MOVE 'TFCNGRP'    TO DP013-DB2-TABLE-NAME (1)           CL*11
00618              MOVE SQLCA        TO DP013-SQLCA                        CL**1
00619              SET DP013-DB2-ABEND                                     CL**1
00620                  DP013-XCTL-DISPLAY-RESTART TO TRUE                  CL**1
00621              PERFORM DP013-0000-PROCESS-ABEND                        CL**1
00622      END-EVALUATE.                                                   CL**1
00623                                                                      CL**1
00624                                                                      CL**1
00625  5100-INSERT-TTRNSET-ROW.                                            CL*11
00626                                                                      CL*11
00627      EXEC SQL                                                        CL*11
00628          INSERT INTO TTRNSET                                         CL*11
00629             (                                                        CL*11
00630              TRAN_SET_CDE                                            CL*11
00631             ,TRAN_SET_NME                                            CL*11
00632             ,TRAN_SET_NME_ID                                         CL*11
00633             ,FUNC_CDE                                                CL*11
00634             )                                                        CL*11
00635          VALUES                                                      CL*11
00636             (                                                        CL*11
00637              :TRNSET-TRAN-SET-CDE                                    CL*11
00638             ,:TRNSET-TRAN-SET-NME                                    CL*11
00639             ,:TRNSET-TRAN-SET-NME-ID                                 CL*11
00640             ,:TRNSET-FUNC-CDE                                        CL*11
00641             )                                                        CL*11
00642      END-EXEC.                                                       CL*11
00643                                                                      CL*11
00644      EVALUATE TRUE                                                   CL*11
00645          WHEN SQLCODE = ZERO                                         CL*11
00646          WHEN SQLCODE = -904                                         CL*11
00647          WHEN SQLCODE = -913                                         CL*11
00648              CONTINUE                                                CL*11
00649          WHEN SQLWARN0 NOT = SPACES                                  CL*11
00650          WHEN SQLCODE  NOT = ZERO                                    CL*11
00651              MOVE '5100-INSERT-TTRNSET-ROW'                          CL*11
00652                                TO DP013-PARAGRAPH                    CL*11
00653              MOVE 'INSERT A ROW INTO THE TTRNSET TABLE'              CL*11
00654                                TO DP013-MESSAGE-TEXT(1)              CL*11
00655              MOVE 'TTRNSET'    TO DP013-DB2-TABLE-NAME (1)           CL*11
00656              MOVE SQLCA        TO DP013-SQLCA                        CL*11
00657              SET DP013-DB2-ABEND                                     CL*11
00658                  DP013-XCTL-DISPLAY-RESTART TO TRUE                  CL*11
00659              PERFORM DP013-0000-PROCESS-ABEND                        CL*11
00660      END-EVALUATE.                                                   CL*11
00661                                                                      CL*11
00662                                                                      CL*11
00663 *----------------------------------------------------------------*   CL**1
00664 *    ABEND PROCESSOR MODULE                                      *   CL**1
00665 *----------------------------------------------------------------*   CL**1
00666                                                                      CL**1
00667      COPY DPPD013.                                                   CL**1
