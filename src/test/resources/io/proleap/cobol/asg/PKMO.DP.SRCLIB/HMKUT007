      *----------------------------------------------------------------*00010000
       IDENTIFICATION DIVISION.                                         00020000
      *----------------------------------------------------------------*00030000
       PROGRAM-ID.           HMKUT007.                                  00040000
       AUTHOR.               BOB MCASEY.                                00050000
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00060000
       DATE-WRITTEN          04/24/2003.                                00070000
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      *----------------------------------------------------------------*00100000
      *   HMKUT007 - RETRIEVE USER ID FOR COMMON EXTRACTS AT USR AND   *00110000
      *              DPT LEVELS.                                       *00120000
      *----------------------------------------------------------------*00130000
      *  CHANGED:                                                      *00140000
      *                                                                *00150000
      *    WR/PITS#     DATE        DESCRIPTION                        *00160000
      *    --------   ----------   ---------------------------------   *00170000
      *    WR24706    04-24-2003    INITIAL DEVELOPMENT.               *00180000
      *----------------------------------------------------------------*00190000
                                                                        00200000
           EJECT                                                        00210000
      *----------------------------------------------------------------*00220000
       ENVIRONMENT DIVISION.                                            00230000
      *----------------------------------------------------------------*00240000
       CONFIGURATION SECTION.                                           00250000
                                                                        00260000
       SOURCE-COMPUTER. IBM-3090.                                       00270000
       OBJECT-COMPUTER. IBM-3090.                                       00280000
                                                                        00290000
       INPUT-OUTPUT SECTION.                                            00300000
       FILE-CONTROL.                                                    00310000
           SELECT USR-OUT-FILE               ASSIGN TO OUT01.           00320000
           SELECT DPT-OUT-FILE               ASSIGN TO OUT02.           00330000
           SELECT ROL-OUT-FILE               ASSIGN TO OUT03.           00340003
008200                                                                  00350000
010200*----------------------------------------------------------------*00360000
010300 DATA DIVISION.                                                   00370000
010200*----------------------------------------------------------------*00380000
010600 FILE SECTION.                                                    00390000
1000RM*                                                                 00400000
                                                                        00410000
       FD  USR-OUT-FILE                                                 00420000
           LABEL RECORDS ARE STANDARD                                   00430000
           RECORDING MODE IS F                                          00440000
           BLOCK CONTAINS 0 RECORDS.                                    00450000
       01  USR-OUT-REC              PIC X(88).                          00460001
                                                                        00470000
       FD  DPT-OUT-FILE                                                 00480000
           LABEL RECORDS ARE STANDARD                                   00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  DPT-OUT-REC              PIC X(35).                          00520001
                                                                        00530000
       FD  ROL-OUT-FILE                                                 00540000
           LABEL RECORDS ARE STANDARD                                   00550000
           RECORDING MODE IS F                                          00560000
           BLOCK CONTAINS 0 RECORDS.                                    00570000
       01  ROL-OUT-REC              PIC X(33).                          00580001
                                                                        00590000
                                                                        00600000
       WORKING-STORAGE SECTION.                                         00610000
                                                                        00620000
       01  FILLER                           PIC X(50)    VALUE          00630000
           'BEGINNING OF HMKUT007 WORKING STORAGE'.                     00640000
                                                                        00650000
       01  PA-PROGRAM-ACCUMUALTORS.                                     00660000
           05  PA-USR-RECS-WRITTEN          PIC 9(07)    VALUE 0.       00670000
           05  PA-DPT-RECS-WRITTEN          PIC 9(07)    VALUE 0.       00680000
           05  PA-ROL-RECS-WRITTEN          PIC 9(07)    VALUE 0.       00690000
                                                                        00700000
       01  PC-PROGRAM-CONSTANTS.                                        00710000
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE            00720000
                                                       'HMKUT007'.      00730000
           05  PC-MESSAGES.                                             00740000
               10  PC-SQL-ABEND-MESSAGE     PIC X(23)  VALUE            00750000
               '** ABEND -> SQLERROR = '.                               00760000
               10  PC-PROGRAM-MESSAGE       PIC X(23)  VALUE            00770000
               '** PROGRAM        ** = '.                               00780000
               10  PC-PARAGRAPH-MESSAGE     PIC X(23)  VALUE            00790000
               '** PARAGRAPH-NAME ** = '.                               00800000
                                                                        00810000
       01  PS-PROGRAM-SWITCHES.                                         00820001
           05  PS-END-OF-CURSOR-SW        PIC X(01)      VALUE 'N'.     00830001
               88  PS-END-OF-CURSOR                      VALUE 'Y'.     00840001
                                                                        00850001
       01  PV-PROGRAM-VARIABLES.                                        00860000
           05  PV-DEPT-CHAR                  PIC X(03).                 00870006
           05  PV-DEPT-CHAR-RDF REDEFINES PV-DEPT-CHAR                  00880006
                                             PIC 9(03).                 00890006
           05  PV-ABEND-INFO.                                           00900000
               10  PV-ABEND-PARAGRAPH        PIC X(40)  VALUE SPACE.    00910000
               10  PV-ABEND-CODE COMP SYNC   PIC S9(04) VALUE +0000.    00920000
               10  PV-SQLCODE-DISPLAY        PIC ++++9  VALUE ZERO.     00930000
                                                                        00940000
                                                                        00950000
                                                                        00960000
      *----------------------------------------------------------------*00970000
      *  COMMOM DOMAIN EXTRACT COPYBOOK                                 00980000
      *----------------------------------------------------------------*00990000
           COPY HMRD014.                                                01000000
                                                                        01010000
      *----------------------------------------------------------------*01020000
      * TMDSEAR TABLE                                                   01030000
      *----------------------------------------------------------------*01040000
           EXEC SQL                                                     01050000
               INCLUDE TMDSEAR                                          01060000
           END-EXEC.                                                    01070000
                                                                        01080000
      *----------------------------------------------------------------*01090000
      * TRSPBAR TABLE                                                   01100000
      *----------------------------------------------------------------*01110000
           EXEC SQL                                                     01120000
               INCLUDE TRSPBAR                                          01130000
           END-EXEC.                                                    01140000
                                                                        01150000
      *----------------------------------------------------------------*01160000
      * TWORKER TABLE                                                   01170000
      *----------------------------------------------------------------*01180000
           EXEC SQL                                                     01190000
               INCLUDE TWORKER                                          01200000
           END-EXEC.                                                    01210000
                                                                        01220000
      *----------------------------------------------------------------*01230000
      * TRSPLVL TABLE                                                   01240000
      *----------------------------------------------------------------*01250000
           EXEC SQL                                                     01260000
               INCLUDE TRSPLVL                                          01270000
           END-EXEC.                                                    01280000
                                                                        01290000
      *----------------------------------------------------------------*01300001
      * TWORKER CURSOR                                                  01310001
      *----------------------------------------------------------------*01320001
           EXEC SQL                                                     01330001
                DECLARE MDSG_PRSN_CSR CURSOR FOR                        01340001
                  SELECT UID_NBR                                        01350001
                       , FIR_NAME                                       01360001
                       , LAST_NAME                                      01370001
                    FROM TWORKER                                        01380001
           END-EXEC.                                                    01390001
                                                                        01400000
      *----------------------------------------------------------------*01410001
      * MERCHANDISE RESPONSIBILITY                                      01420001
      *----------------------------------------------------------------*01430001
           EXEC SQL                                                     01440001
                DECLARE DEPT_RSPNT_CSR CURSOR FOR                       01450001
                  SELECT DEPT_NBR                                       01460008
                       , TRS.RSPLVL_CDE                                 01470001
                       , TW.UID_NBR                                     01480001
                       , TRS.WORKER_STRT_DTE                            01490001
                       , TRS.WORKER_END_DTE                             01500001
                    FROM TMDSEAR TMD                                    01510001
                       , TRSPBAR TRS                                    01520001
                       , TWORKER TW                                     01530001
                   WHERE TMD.MDSELV_CDE  = 'DPT'                        01540008
                     AND TMD.MDSEAR_DKEY = TRS.MDSEAR_DKEY              01550008
                     AND TW.OPERCO_NBR   = '03'                         01560008
                     AND TRS.WORKER_NBR  = TW.WORKER_NBR                01570008
           END-EXEC.                                                    01580001
                                                                        01590001
      *----------------------------------------------------------------*01600001
      * MERCHANDISE ROLES                                               01610001
      *----------------------------------------------------------------*01620001
           EXEC SQL                                                     01630001
                DECLARE MDSG_ROL_CSR CURSOR FOR                         01640001
                  SELECT RSPLVL_CDE                                     01650001
                       , RSPLVL_DESC                                    01660001
                    FROM TRSPLVL                                        01670001
           END-EXEC.                                                    01680001
                                                                        01690001
           EXEC SQL                                                     01700001
               INCLUDE SQLCA                                            01710000
           END-EXEC.                                                    01720000
                                                                        01730000
           COPY SQLCA2.                                                 01740000
                                                                        01750000
      *----------------------------------------------------------------*01760000
      *  DB2 ERROR MESSAGES FORMAT AREA.                               *01770000
      *----------------------------------------------------------------*01780000
           COPY DPWS004.                                                01790000
                                                                        01800000
      *----------------------------------------------------------------*01810000
       PROCEDURE DIVISION.                                              01820000
                                                                        01830000
      *----------------------------------------------------------------*01840000
      *    MAINLINE-LOGIC                                              *01850000
      *----------------------------------------------------------------*01860000
                                                                        01870000
       0000-HMKUT007.                                                   01880000
                                                                        01890000
           PERFORM 1000-INITIALIZATION.                                 01900000
                                                                        01910000
           PERFORM 2000-PROCESS-MDSG-PRSN-CSR.                          01920001
           PERFORM 2200-PROCESS-DEPT-RSPNT-CSR.                         01930001
           PERFORM 2400-PROCESS-MDSG-ROL-CSR.                           01940001
                                                                        01950000
           PERFORM 9900-EOJ-LOGIC.                                      01960000
                                                                        01970000
           GOBACK.                                                      01980000
           EJECT                                                        01990000
      *----------------------------------------------------------------*02000000
      *    OPEN FILES                                                  *02010004
      *----------------------------------------------------------------*02020000
       1000-INITIALIZATION.                                             02030000
                                                                        02040000
           DISPLAY '**********************************'.                02050000
           DISPLAY '**------ BEGIN HMKUT007 ------**'.                  02060000
                                                                        02070000
           OPEN OUTPUT  USR-OUT-FILE.                                   02080000
           OPEN OUTPUT  DPT-OUT-FILE.                                   02090000
           OPEN OUTPUT  ROL-OUT-FILE.                                   02100001
                                                                        02110000
      *----------------------------------------------------------------*02120002
      *    2000-PROCESS-MDSG-PRSN-CSR.                                 *02130001
      *      THIS IS PERFORMED ONLY VIA THE 0000-HMKUT007              *02140000
      *      PURPOSE: CREATE USER ID AND NAME EXTRACT                  *02150001
      *----------------------------------------------------------------*02160000
       2000-PROCESS-MDSG-PRSN-CSR.                                      02170001
                                                                        02180001
      *  OPEN CURSOR                                                    02190001
           EXEC SQL                                                     02200001
               OPEN MDSG_PRSN_CSR                                       02210001
           END-EXEC.                                                    02220001
                                                                        02230001
           PERFORM 2010-FETCH-MDSG-PRSN-CSR                             02240001
               UNTIL PS-END-OF-CURSOR.                                  02250001
                                                                        02260001
      *  CLOSE CURSOR                                                   02270001
           EXEC SQL                                                     02280001
               CLOSE MDSG_PRSN_CSR                                      02290001
           END-EXEC.                                                    02300001
                                                                        02310001
       2010-FETCH-MDSG-PRSN-CSR.                                        02320001
                                                                        02330001
      *  FETCH CURSOR                                                   02340001
           EXEC SQL                                                     02350001
               FETCH MDSG_PRSN_CSR                                      02360001
                INTO :HM014-MDSG-USR-ID                                 02370001
                   , :HM014-MDSG-FRST-NM                                02380001
                   , :HM014-MDSG-LAST-NM                                02390001
           END-EXEC.                                                    02400001
                                                                        02410001
           EVALUATE TRUE                                                02420002
             WHEN SQLCODE = ZEROS                                       02430002
               PERFORM 4000-WRITE-MDSG-PRSN-EXTR                        02440002
             WHEN SQLCODE = +100                                        02450002
               SET PS-END-OF-CURSOR TO TRUE                             02460002
             WHEN OTHER                                                 02470002
               DISPLAY PV-ABEND-PARAGRAPH                               02480002
               PERFORM 9999-SQL-ERROR                                   02490002
           END-EVALUATE.                                                02500002
                                                                        02510001
      *----------------------------------------------------------------*02520001
      *    2200-PROCESS-DEPT-RSPNT-CSR.                                *02530001
      *      THIS IS PERFORMED ONLY VIA THE 0000-HMKUT007              *02540001
      *      PURPOSE: CREATE DEPT/USER/ROLE EXTRACT                    *02550001
      *----------------------------------------------------------------*02560001
       2200-PROCESS-DEPT-RSPNT-CSR.                                     02570001
      *  OPEN CURSOR                                                    02580001
           EXEC SQL                                                     02590001
               OPEN DEPT_RSPNT_CSR                                      02600001
           END-EXEC.                                                    02610001
                                                                        02620001
           MOVE 'N' TO PS-END-OF-CURSOR-SW.                             02630001
           PERFORM 2210-FETCH-DEPT-RSPNT-CSR                            02640001
               UNTIL PS-END-OF-CURSOR.                                  02650001
                                                                        02660001
      *  CLOSE CURSOR                                                   02670001
           EXEC SQL                                                     02680001
               CLOSE DEPT_RSPNT_CSR                                     02690001
           END-EXEC.                                                    02700001
                                                                        02710001
       2210-FETCH-DEPT-RSPNT-CSR.                                       02720001
                                                                        02730001
      *  FETCH CURSOR                                                   02740001
           EXEC SQL                                                     02750001
                FETCH DEPT_RSPNT_CSR                                    02760001
                 INTO :PV-DEPT-CHAR                                     02770008
                    , :HM014-DEPT-MDSG-ROL-CDE                          02780001
                    , :HM014-DEPT-USR-ID                                02790001
                    , :HM014-DEPT-EFF-BEG-DTE                           02800001
                    , :HM014-DEPT-EFF-END-DTE                           02810001
           END-EXEC.                                                    02820001
                                                                        02830001
           MOVE PV-DEPT-CHAR-RDF TO HM014-DEPT-RSPNT-DEPT.              02840008
                                                                        02850008
           EVALUATE TRUE                                                02860002
             WHEN SQLCODE = ZEROS                                       02870002
               PERFORM 4200-WRITE-DEPT-RSPNT-EXTR                       02880002
             WHEN SQLCODE = +100                                        02890002
               SET PS-END-OF-CURSOR TO TRUE                             02900002
             WHEN OTHER                                                 02910002
               DISPLAY PV-ABEND-PARAGRAPH                               02920002
               PERFORM 9999-SQL-ERROR                                   02930002
           END-EVALUATE.                                                02940002
                                                                        02950001
      *----------------------------------------------------------------*02960001
      *    2200-PROCESS-DEPT-RSPNT-CSR.                                *02970001
      *      THIS IS PERFORMED ONLY VIA THE 0000-HMKUT007              *02980001
      *      PURPOSE: CREATE MDSE ROLE EXTRACT                         *02990001
      *----------------------------------------------------------------*03000001
       2400-PROCESS-MDSG-ROL-CSR.                                       03010001
      *  OPEN CURSOR                                                    03020001
           EXEC SQL                                                     03030001
               OPEN MDSG_ROL_CSR                                        03040001
           END-EXEC.                                                    03050001
                                                                        03060001
           MOVE 'N' TO PS-END-OF-CURSOR-SW.                             03070001
           PERFORM 2410-FETCH-MDSG-ROL-CSR                              03080001
               UNTIL PS-END-OF-CURSOR.                                  03090001
                                                                        03100001
      *  CLOSE CURSOR                                                   03110001
           EXEC SQL                                                     03120001
               CLOSE MDSG_ROL_CSR                                       03130001
           END-EXEC.                                                    03140001
                                                                        03150001
       2410-FETCH-MDSG-ROL-CSR.                                         03160001
      *  FETCH CURSOR                                                   03170001
           EXEC SQL                                                     03180001
                FETCH MDSG_ROL_CSR                                      03190001
                 INTO :HM014-MDSG-ROL-CDE                               03200007
                    , :HM014-MDSG-ROL-DESC                              03210007
           END-EXEC.                                                    03220001
                                                                        03230001
           EVALUATE TRUE                                                03240002
             WHEN SQLCODE = ZEROS                                       03250002
               PERFORM 4400-WRITE-MDSG-ROL-EXTR                         03260002
             WHEN SQLCODE = +100                                        03270002
               SET PS-END-OF-CURSOR TO TRUE                             03280002
             WHEN OTHER                                                 03290002
               DISPLAY PV-ABEND-PARAGRAPH                               03300002
               PERFORM 9999-SQL-ERROR                                   03310002
           END-EVALUATE.                                                03320002
                                                                        03330001
      *----------------------------------------------------------------*03340001
      *    WRITE THE USR RECORD                                         03350000
      *----------------------------------------------------------------*03360000
       4000-WRITE-MDSG-PRSN-EXTR.                                       03370001
                                                                        03380000
**TEMP*    DISPLAY '4000-WRITE-MDSG-PRSN-EXTR. '.                       03390001
1202RM     WRITE USR-OUT-REC FROM HMRD014-MDSG-PRSN-RECORD.             03400005
           ADD 1 TO PA-USR-RECS-WRITTEN.                                03410000
                                                                        03420000
      *----------------------------------------------------------------*03430001
      *    WRITE THE DEPT RECORD                                        03440001
      *----------------------------------------------------------------*03450001
       4200-WRITE-DEPT-RSPNT-EXTR.                                      03460001
**TEMP*    DISPLAY '4200-WRITE-DEPT-RSPNT-EXTR.'.                       03470001
1202RM     WRITE DPT-OUT-REC FROM HMRD014-DEPT-RSPNT-RECORD.            03480005
           ADD 1 TO PA-DPT-RECS-WRITTEN.                                03490001
                                                                        03500001
      *----------------------------------------------------------------*03510001
      *    WRITE THE ROLE RECORD                                        03520001
      *----------------------------------------------------------------*03530001
       4400-WRITE-MDSG-ROL-EXTR.                                        03540001
**TEMP*    DISPLAY '4400-WRITE-MDSG-ROL-EXTR.'.                         03550001
1202RM     WRITE ROL-OUT-REC FROM HMRD014-MDSG-ROL-RECORD.              03560005
           ADD 1 TO PA-ROL-RECS-WRITTEN.                                03570001
                                                                        03580001
                                                                        03590000
                                                                        03600000
      *----------------------------------------------------------------*03610000
      * 9900-EOJ-LOGIC.                                                 03620000
      * PERFORMED AT END OF INPUT FILE TO CLOSE FILES, CURSOR AND       03630000
      * DISPLAY RECORD COUNTS.                                          03640000
      *----------------------------------------------------------------*03650000
       9900-EOJ-LOGIC.                                                  03660000
                                                                        03670000
           CLOSE  USR-OUT-FILE                                          03680001
                  ROL-OUT-FILE                                          03690001
                  DPT-OUT-FILE.                                         03700000
                                                                        03710000
           DISPLAY 'USR RECS WRITTEN ' PA-USR-RECS-WRITTEN.             03720000
           DISPLAY 'DPT RECS WRITTEN ' PA-DPT-RECS-WRITTEN.             03730000
           DISPLAY 'ROL RECS WRITTEN ' PA-ROL-RECS-WRITTEN.             03740001
           DISPLAY '**------ END HMKUT007 ------**'.                    03750000
                                                                        03760000
084900******************************************************************03770000
085000*  STANDARD DB2 ERROR ABEND ROUTINE                               03780000
085100*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             03790000
085200******************************************************************03800000
       9999-SQL-ERROR.                                                  03810000
143800                                                                  03820000
085600     MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            03830000
085700     DISPLAY '**  ABEND     **'.                                  03840000
085800     DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              03850000
085900     DISPLAY '**  PARAGRAPH **  = ' PV-ABEND-PARAGRAPH.           03860000
086100     DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           03870000
144200*                                                                 03880000
143700     COPY DPPD004.                                                03890000
144300     MOVE +4013                      TO PV-ABEND-CODE.            03900000
144400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                         03910000
144500                                                                  03920000
      *---------------------------------------------------------------* 03930000
      *    END OF SOURCE CODE FOR HMKUT007                            * 03940000
      *---------------------------------------------------------------* 03950000
