      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             ARKUT132.                                        
       AUTHOR.                 COGNIZANT.                                       
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            JULY 2019.                                       
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *  PURPOSE:                                                               
      *  THIS PROGRAM WILL TOKENIZE THE ACCOUNT ID FIELD IN THE                 
      *  INPUT FILE.                                                            
      *  PROTEGRITY MODULE WILL BE INVOKED TO TOKENIZE THE ACCOUNT              
      *  NUMBER                                                                 
      *                                                                         
      *  INPUT FILES :   CDR-ACCT-FILE             DDNAME = INP01               
      *  OUTPUT FILES:   CDR-ACCT-TOKEN-FILE       DDNAME = OUT01               
      *                                                                         
      *****************************************************************         
      *----------------------------------------------------------------*        
      *                                                                *        
      * DATE:        07/24/2019                                        *        
      * WR/IR#:      CHG0230275                                        *        
      * PROGRAMMER:  COGNIZANT                                         *        
      * DESCRIPTION: INITIAL VERSION CREATED FOR FD FILE TRANSFER      *        
      *              PROJECT                                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CDR-ACCT-FILE                                                 
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CDR-ACCT-TOKEN-FILE                                           
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CDR-ACCT-FILE                                                        
           RECORDING MODE IS V                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CDR-ACCT-RECORD             PIC X(1188).                             
                                                                                
       FD  CDR-ACCT-TOKEN-FILE                                                  
           RECORDING MODE IS V                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CDR-ACCT-TOKEN-RECORD       PIC X(1188).                             
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
       01  FILLER                            PIC X(50)  VALUE                   
           ' *** WORKING STORAGE STARTS HERE FOR ARKUT132 *** '.                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW          PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-END-OF-FILE                VALUE 'N'.                 
               88  PS-END-OF-FILE                    VALUE 'Y'.                 
           05  PS-HDR-TRLR-REC-SW         PIC X(01)  VALUE 'N'.                 
               88  PS-NOT-HDR-TRLR-REC               VALUE 'N'.                 
               88  PS-HDR-TRLR-REC                   VALUE 'Y'.                 
           05  PS-ACCT-SSN                PIC X(08)  VALUE SPACES.              
               88  PS-ACCT-ID                        VALUE 'ACCT ID'.           
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE                   
                                                       'ARKUT132'.              
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-READ-COUNT                 PIC  9(09) VALUE ZEROES.           
           05  PV-WRITE-COUNT                PIC  9(09) VALUE ZEROES.           
           05  PV-INPUT-LENGTH               PIC  9(09) VALUE ZEROES.           
           05  PV-OUTPUT-RECORD              PIC  X(1188)                       
                                                        VALUE SPACES.           
           05  PV-ACCT-ID                    PIC  X(16) VALUE SPACES.           
                                                                                
       01  ABEND-CODE                        PIC S9(04) VALUE ZERO              
                                                        COMP SYNC.              
                                                                                
      ******************************************************************        
      * RECORD LAYOUT FOR CD RAWDATA ACCOUNT FILE - INPUT AND OUTPUT  *         
      ******************************************************************        
           COPY ARRD132.                                                        
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY PTYCCSIP.                                                       
                                                                                
      ******************************************************************        
      * LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *        
      * MODULE                                                         *        
      ******************************************************************        
           COPY DPWS031.                                                        
                                                                                
      ******************************************************************        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-PARA                                            
                 UNTIL PS-END-OF-FILE.                                          
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE                                                         
      *     INITIALIZE THE VARIABLES, OPEN THE INPUT AND OUTPUT FILES           
      *     AND READ THE INPUT FILE                                             
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           DISPLAY 'PROGRAM - ' PC-PROGRAM-NAME.                                
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
           MOVE PC-PROGRAM-NAME               TO DP031-PROGRAM-NAME.            
                                                                                
           OPEN INPUT  CDR-ACCT-FILE                                            
                OUTPUT CDR-ACCT-TOKEN-FILE.                                     
                                                                                
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-PARA                                                       
      *   1. PROCESS THE RECORDS IN THE INPUT FILE                              
      *   2. CALL TOKENIZE PARA TO TOKENIZE ACCOUNT ID                          
      *   3. WRITE OUTPUT RECORDS                                               
      ******************************************************************        
       2000-PROCESS-PARA.                                                       
                                                                                
           INITIALIZE PV-ACCT-ID                                                
                      PV-INPUT-LENGTH.                                          
                                                                                
           IF PS-NOT-HDR-TRLR-REC                                               
               IF AR132-CC1291RD-ACCT-ID = SPACES                               
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-ACCT-ID              TO TRUE                          
                   MOVE AR132-CC1291RD-ACCT-ID TO PV-ACCT-ID                    
                                                                                
                   INSPECT FUNCTION REVERSE (PV-ACCT-ID)                        
                   TALLYING PV-INPUT-LENGTH FOR LEADING SPACES                  
                   COMPUTE PV-INPUT-LENGTH =                                    
                   LENGTH OF PV-ACCT-ID - PV-INPUT-LENGTH                       
                   PERFORM 2300-TOKENIZE-DATA                                   
               END-IF                                                           
                                                                                
           END-IF.                                                              
           PERFORM 2200-WRITE-FILE.                                             
           PERFORM 2100-READ-FILE.                                              
                                                                                
      ******************************************************************        
      * 2100-READ-FILE                                                          
      *      READ THE RECORDS IN CD RAWDATA ACCOUNT FILE                        
      ******************************************************************        
       2100-READ-FILE.                                                          
                                                                                
           READ CDR-ACCT-FILE INTO AR132-RECORDS                                
              AT END                                                            
                 SET PS-END-OF-FILE  TO TRUE                                    
              NOT AT END                                                        
                 ADD 1               TO PV-READ-COUNT                           
           END-READ                                                             
                                                                                
           IF AR132-RECORDS(1:1) = 'D'                                          
              SET PS-NOT-HDR-TRLR-REC TO TRUE                                   
           ELSE                                                                 
              SET PS-HDR-TRLR-REC     TO TRUE                                   
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2200-WRITE-FILE                                                         
      *      WRITE THE RECORDS TO OUTPUT                                        
      ******************************************************************        
       2200-WRITE-FILE.                                                         
                                                                                
           INITIALIZE PV-OUTPUT-RECORD.                                         
           MOVE AR132-RECORDS            TO PV-OUTPUT-RECORD.                   
                                                                                
           WRITE CDR-ACCT-TOKEN-RECORD FROM PV-OUTPUT-RECORD.                   
           ADD 1                         TO PV-WRITE-COUNT.                     
                                                                                
      ******************************************************************        
      * 2300-TOKENIZE-DATA                                                      
      *    TOKENIZE ACCOUNT ID                                                  
      ******************************************************************        
       2300-TOKENIZE-DATA.                                                      
                                                                                
           INITIALIZE CSI-PARAMETERS                                            
                      DP031-PROTEG-PGM-PARAMETERS.                              
                                                                                
           MOVE 'ACCT-ID'               TO DP031-FIELD                          
           MOVE PV-ACCT-ID              TO DP031-CLEAR-TEXT                     
           MOVE PV-INPUT-LENGTH         TO DP031-INPUT-LEN                      
           SET DP031-ACCTNBR            TO TRUE                                 
                                                                                
           SET CSI-ENCRYPT-FUNCTION     TO TRUE.                                
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                              
                                                                                
           IF CSI-RETURN-CODE = ZERO                                            
              MOVE DP031-CIPHER-TEXT    TO PV-ACCT-ID                           
              MOVE PV-ACCT-ID           TO AR132-CC1291RD-ACCT-ID               
           ELSE                                                                 
              DISPLAY 'ISSUE IN RECORD# : ' PV-READ-COUNT                       
              PERFORM DP031-9999-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9000-EOJ-ROUTINE                                                        
      *      CLOSE THE INPUT AND OUTPUT FILES, DISPLAY THE MESSAGES             
      *      AND TERMINATE THE PROCESS.                                         
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE CDR-ACCT-FILE                                                  
                 CDR-ACCT-TOKEN-FILE.                                           
                                                                                
           IF PV-READ-COUNT = 0                                                 
              DISPLAY 'INPUT FILE IS EMPTY'                                     
           ELSE                                                                 
              DISPLAY 'RECORDS READ   : ' PV-READ-COUNT                         
              DISPLAY 'RECORDS WRITTEN: ' PV-WRITE-COUNT                        
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *      COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE           
      *      PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                      
      ******************************************************************        
            COPY DPPD031.                                                       
                                                                                
      ******************************************************************        
