000100 TITLE 'EXTRACT OLD IT FROM PCT_TRN_CLSN_DTL TABLE'.                      
000200 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.    PCKUT987.                                                 
000400 AUTHOR.        DENISE HAHN.                                              
000500 DATE-WRITTEN.  JANUARY, 2015.                                            
000600                                                                          
000610******************************************************************        
000620* THIS WILL BE A ONE TIME PROGRAM TO FIX OLD DATA                *        
000621*                                                                *        
000622* THIS PROGRAM WILL CREATE AN EXTRACT FILE OF OLD IT (IN TRANSIT)*        
000630* RECORDS FROM PCT_TRN_CLSN_DTL THAT HAVE NOT PURGED FORM THE    *        
000640* TABLES.                                                        *        
000650* EXTRACT IS BASED ON LAST UPDATE DATE (PARM TRNOLD)             *        
000651*                                                                *        
000652* IF NOT PCT_TRN_CLSN_BAL ROW, A RECORD WILL BE WRITTEN TO THE   *        
000653* EXTRACT FILE.                                                  *        
000654*                                                                *        
000655* EXTRACT FILE WILL BE USED BY PROGRAM PCKUP99? TO DELETE THE    *        
000656* RECORD FROM PCT_TRN_CLSN_DTL TABLE.                            *        
000660******************************************************************        
000700                                                                          
000800 ENVIRONMENT DIVISION.                                                    
000900 INPUT-OUTPUT SECTION.                                                    
001000 FILE-CONTROL.                                                            
001300                                                                          
001310     SELECT IN-TRANSIT-EXTRACT-FILE                                       
001320         ASSIGN TO OUT01.                                                 
001330                                                                          
001400 DATA DIVISION.                                                           
001500 FILE SECTION.                                                            
001600                                                                          
002410 FD  IN-TRANSIT-EXTRACT-FILE                                              
002420     RECORDING MODE IS F                                                  
002430     LABEL RECORDS ARE STANDARD                                           
002440     BLOCK CONTAINS 0 RECORDS.                                            
002450                                                                          
002460 01  IN-TRANSIT-RECORD                   PIC  X(50).                      
002470                                                                          
002500 WORKING-STORAGE SECTION.                                                 
002600                                                                          
003000 01  ABEND-CODE                          PIC S9(04)  VALUE ZERO           
003100                                                     COMP SYNC.           
003200 01  PV-PROGRAM-SWITCHES.                                                 
003300     05  PS-END-OF-FILE-SW               PIC  X(01)  VALUE 'N'.           
003301         88 PS-END-OF-FILE-YES                       VALUE 'Y'.           
003302         88 PS-END-OF-FILE-NO                        VALUE 'N'.           
003303                                                                          
003304 01  PV-PROGRAM-CONSTANTS.                                                
003305     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE 'PCKUT987'.          
003308                                                                          
003310 01  PV-PROGRAM-VARIABLES.                                                
003320     05  PV-ABEND-PARAGRAPH              PIC  X(30)  VALUE SPACE.         
003400     05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(30)  VALUE SPACE.         
003410     05  PV-SQL-CODE                     PIC -999.                        
003500                                                                          
004300     05 PV-EXTRACT-COUNT                 PIC S9(09)  COMP-3               
004400                                                     VALUE ZERO.          
004900     05 PV-DISP-NBR                      PIC ZZZ,ZZZ,ZZ9.                 
004901     05 PV-DISP-DAYS                     PIC ZZZ,ZZZ,ZZ9.                 
004902     05 PV-DISP-ON-BAL                   PIC ZZZ,ZZZ,ZZ9.                 
004903     05 PV-DISP-NOT-ON-BAL               PIC ZZZ,ZZZ,ZZ9.                 
004904                                                                          
004910     05 PV-PURGE-DAYS                    PIC S9(9) COMP.                  
004920     05 PV-CURRENT-DATE                  PIC X(10).                       
004930     05 PV-PURGE-DATE                    PIC X(10).                       
005000                                                                          
005010     05 PV-DETAIL-COUNT                  PIC S9(09)  COMP.                
005020     05 PV-TOTAL-DETAIL-COUNT            PIC  9(11).                      
005021     05 PV-FOUND-ON-BAL-COUNT            PIC  9(11).                      
005022     05 PV-NOT-FOUND-ON-BAL-COUNT        PIC  9(11).                      
005030     05 PV-DISP-TOT-DETAIL               PIC ZZ,ZZZ,ZZZ,ZZZ.              
005100*                                                                         
010620* IN-TRANSIT DETAIL TABLE                                                 
010630*                                                                         
010640     EXEC SQL                                                             
010650          INCLUDE PCT00015                                                
010660     END-EXEC.                                                            
010670                                                                          
010700*                                                                         
010800* PARM TABLE                                                              
010900*                                                                         
011000     EXEC SQL                                                             
011100          INCLUDE TKRPRPM                                                 
011200     END-EXEC.                                                            
011231                                                                          
011232*---------------------------------------------------------------*         
011233*   INTRANSIT_CSR                                               *         
011240*---------------------------------------------------------------*         
011250     EXEC SQL                                                             
011260          DECLARE INTRANSIT_CSR CURSOR FOR                                
011261              SELECT DISTINCT                                             
011262                     INTR_UPC_ID                                          
011263                    ,LOC_NBR                                              
011264                    ,TRN_CLSN_CDE                                         
011299                FROM PCT_TRN_CLSN_DTL                                     
011301               WHERE TRN_CLSN_CDE  =  'IT'                                
011303                 AND DATE(IN_TRNST_TRN_TMST) <= :PV-PURGE-DATE            
011308               WITH UR                                                    
011310     END-EXEC.                                                            
011311                                                                          
011312                                                                          
011320     COPY DPWS004.                                                        
011321                                                                          
011322     COPY PCRD080.                                                        
011323                                                                          
011330     COPY DPWS004.                                                        
011400                                                                          
011500     EXEC SQL                                                             
011600         INCLUDE SQLCA                                                    
011700     END-EXEC.                                                            
011800     COPY SQLCA2.                                                         
011900/                                                                         
012000 PROCEDURE DIVISION.                                                      
012100                                                                          
012110     SET  PS-END-OF-FILE-NO  TO  TRUE                                     
012120                                                                          
012200     PERFORM 1000-INITIALIZE.                                             
012320                                                                          
012400     PERFORM 2000-PROCESS                                                 
012500             UNTIL PS-END-OF-FILE-YES                                     
012600                                                                          
012700     PERFORM 8020-CLOSE-CURSOR.                                           
012701                                                                          
012710     PERFORM 9000-QUIT.                                                   
012800                                                                          
012900     GOBACK.                                                              
013000                                                                          
013100 1000-INITIALIZE.                                                         
013200                                                                          
013201     OPEN OUTPUT IN-TRANSIT-EXTRACT-FILE.                                 
013203                                                                          
013210     PERFORM 1050-GET-PURGE-DATE.                                         
013211                                                                          
013212     MOVE  PV-PURGE-DAYS    TO  PV-DISP-DAYS.                             
013213                                                                          
013214     DISPLAY ' '.                                                         
013215     DISPLAY '*****************************************'.                 
013216     DISPLAY '*                                       *'.                 
013217     DISPLAY '* PCKUT987 - OLD IT EXTRACT-NO BAL REC  *'.                 
013218     DISPLAY '*                                       *'.                 
013219     DISPLAY '* PURGE DAYS:  ' PV-DISP-DAYS                               
013220     DISPLAY '* CURRENT DATE:' PV-CURRENT-DATE                            
013221     DISPLAY '* PURGE DATE:  ' PV-PURGE-DATE                              
013222     DISPLAY '*                                       *'.                 
013230                                                                          
013800     PERFORM 8000-OPEN-CURSOR.                                            
013900     PERFORM 8010-FETCH-CURSOR.                                           
014000                                                                          
014600                                                                          
014610 1050-GET-PURGE-DATE.                                                     
014611                                                                          
014612     EXEC SQL                                                             
014613         SELECT  FUNC_QTY                                                 
014614              ,  CURRENT DATE                                             
014615              , (CURRENT DATE - FUNC_QTY DAYS)                            
014616           INTO :PV-PURGE-DAYS                                            
014617              , :PV-CURRENT-DATE                                          
014618              , :PV-PURGE-DATE                                            
014621           FROM  TKRPRPM                                                  
014622          WHERE LOC_ID            = 90                                    
014623            AND INTFC_SYS_CDE     = 'KHL'                                 
014624            AND SYS_PRC_FUNC_CDE  = 'TRNOLD'                              
014625            AND FUNC_PRC_STAT_CDE = 'AC'                                  
014626           WITH UR                                                        
014627     END-EXEC                                                             
014628                                                                          
014629     EVALUATE TRUE                                                        
014630         WHEN SQLCODE = ZERO                                              
014631             CONTINUE                                                     
014640                                                                          
014641         WHEN SQLCODE < ZERO                                              
014642             DISPLAY '**************************************'             
014643             DISPLAY 'ERROR ISSUED ON                       '             
014644             DISPLAY 'SELECT TKRPRPM                        '             
014645             DISPLAY 'PARM TRNOLD                           '             
014646             DISPLAY '**************************************'             
014647             PERFORM 9100-SQL-ABEND                                       
014648                                                                          
014649     END-EVALUATE.                                                        
014650                                                                          
014660                                                                          
014700 2000-PROCESS.                                                            
014710                                                                          
014720     PERFORM 7000-WRITE-OUTPUT.                                           
015101                                                                          
015110     PERFORM 8010-FETCH-CURSOR.                                           
017823                                                                          
017824                                                                          
017825 7000-WRITE-OUTPUT.                                                       
017826                                                                          
017827     MOVE ','  TO  PC080-COMMA-01                                         
017828                   PC080-COMMA-02                                         
017829                   PC080-COMMA-03                                         
017830                   PC080-COMMA-04                                         
017831                   PC080-COMMA-05.                                        
017838                                                                          
017839     WRITE IN-TRANSIT-RECORD                                              
017840      FROM PC080-INTRNST-PURGE-REC.                                       
017841                                                                          
017842     ADD  1  TO  PV-EXTRACT-COUNT.                                        
017850                                                                          
017900                                                                          
031800 8000-OPEN-CURSOR.                                                        
031810                                                                          
031820      EXEC SQL                                                            
031830           OPEN INTRANSIT_CSR                                             
031840      END-EXEC.                                                           
031896                                                                          
031897     EVALUATE TRUE                                                        
031898         WHEN SQLCODE = ZERO                                              
031899             CONTINUE                                                     
031900                                                                          
031901         WHEN SQLCODE < ZERO                                              
031902             DISPLAY '**************************************'             
031903             DISPLAY 'ERROR ISSUED ON                       '             
031904             DISPLAY 'SELECT OPEN INTRANSIT_CSR             '             
031906             DISPLAY '**************************************'             
031907             PERFORM 9100-SQL-ABEND                                       
031908                                                                          
031909     END-EVALUATE.                                                        
031910                                                                          
033201                                                                          
033210 8010-FETCH-CURSOR.                                                       
033211                                                                          
033212      EXEC SQL                                                            
033213           FETCH INTRANSIT_CSR                                            
033219            INTO :PCT00015-INTR-UPC-ID                                    
033220               , :PCT00015-LOC-NBR                                        
033221               , :PCT00015-TRN-CLSN-CDE                                   
033230      END-EXEC.                                                           
033231                                                                          
033232     EVALUATE TRUE                                                        
033233         WHEN SQLCODE = ZERO                                              
033234              ADD  1                     TO PV-TOTAL-DETAIL-COUNT         
033235                                                                          
033247              MOVE PCT00015-INTR-UPC-ID  TO PC080-INTR-UPC-ID             
033248              MOVE PCT00015-INTR-UPC-ID  TO PC080-INTR-UPC-ID-KEY         
033249              MOVE PCT00015-LOC-NBR      TO PC080-LOC-NBR                 
033250              MOVE PCT00015-TRN-CLSN-CDE TO PC080-TRN-CLSN-CDE            
033251              MOVE PV-PURGE-DATE         TO PC080-PURGE-DATE              
033279                                                                          
033280         WHEN SQLCODE = +100                                              
033281             SET  PS-END-OF-FILE-YES   TO  TRUE                           
033282                                                                          
033283         WHEN OTHER                                                       
033284             DISPLAY '**************************************'             
033285             DISPLAY 'ERROR ISSUED ON                       '             
033286             DISPLAY 'SELECT FETCH INTRANSIT_CSR            '             
033287             DISPLAY '**************************************'             
033288             PERFORM 9100-SQL-ABEND                                       
033289                                                                          
033290     END-EVALUATE.                                                        
033291                                                                          
033292                                                                          
033293 8020-CLOSE-CURSOR.                                                       
033294                                                                          
033295      EXEC SQL                                                            
033296           CLOSE INTRANSIT_CSR                                            
033297      END-EXEC.                                                           
033298                                                                          
033299     EVALUATE TRUE                                                        
033300         WHEN SQLCODE = ZERO                                              
033301             CONTINUE                                                     
033302                                                                          
033303         WHEN SQLCODE < ZERO                                              
033304             DISPLAY '**************************************'             
033305             DISPLAY 'ERROR ISSUED ON                       '             
033306             DISPLAY 'SELECT CLOSE INTRANSIT_CSR            '             
033307             DISPLAY '**************************************'             
033308             PERFORM 9100-SQL-ABEND                                       
033309                                                                          
033310     END-EVALUATE.                                                        
033311                                                                          
033320                                                                          
033400 9000-QUIT.                                                               
033500                                                                          
033800     MOVE PV-TOTAL-DETAIL-COUNT       TO PV-DISP-TOT-DETAIL               
033900*                                                                         
034003     MOVE PV-EXTRACT-COUNT            TO PV-DISP-NBR                      
034004                                                                          
034020     DISPLAY '*                                       *'.                 
034021     DISPLAY '* DETAIL CNT....:  ' PV-DISP-TOT-DETAIL.                    
034022     DISPLAY '*                                       *'.                 
034026     DISPLAY '* EXTRACT COUNT.:  ' PV-DISP-NBR                            
034030     DISPLAY '*****************************************'.                 
034031                                                                          
034040     CLOSE IN-TRANSIT-EXTRACT-FILE.                                       
035100                                                                          
035200 9100-SQL-ABEND.                                                          
035300******************************************************************        
035400*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
035500*  ERROR OR WARNING CODE OCCURS.                                          
035600******************************************************************        
035700     DISPLAY '9100-SQL-ABEND'.                                            
035800     DISPLAY '** ABEND     **'.                                           
035900     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
036500     DISPLAY '**              ** = '                                      
036600     MOVE SQLCODE TO PV-SQL-CODE.                                         
036700     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
036800     MOVE +4013 TO ABEND-CODE.                                            
036900     CALL 'ILBOABN0' USING ABEND-CODE.                                    
