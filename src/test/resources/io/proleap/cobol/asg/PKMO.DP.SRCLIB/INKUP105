      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    INKUP105.                                                 
       AUTHOR.        SHARON AMBROSE - STRATAGEM.                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  12/15/94.                                                 
       DATE-COMPILED.                                                           
      *                                                                         
      *****************************************************************         
      *    CHANGES ACTUAL INVENTORY DATE ON TINVPAR                             
      *                                                                         
      *    CHANGE MADE:  PHY_CNT_TMST WAS ADDED TO THE TINVPAR TABLE            
      *                  AND HAD TO BE ADDED TO THE DB2 STATEMENTS.             
      *    CHANGED BY:   DAN GRUBER     10/23/97                                
      *                                                                         
      *****************************************************************         
      *    CHANGE MADE:  REMOVE WORKING STORAGE AREA NOT USED.  THIS            
      *                  IS PART OF Y2K.                                        
      *    CHANGED BY:   JULIE SEIDLER  04/09/98                                
      *****************************************************************         
      *    CHANGE MADE:  REMOVE WORKING STORAGE AREA NOT USED.  THIS            
      *                  IS PART OF Y2K.                                        
      *    CHANGED BY:   JULIE SEIDLER  04/09/98                                
      *****************************************************************         
      *    CHANGE MADE:  ADD LOGIC TO UPDATE SHORTAGE ACTUAL INVENTORY          
      *                  DATE.  ADD CALL TO TMLCNTL TO OBTAIN OPEN              
      *                  FISCAL MONTH. SHTG-ACTL-INV-DTE IS UPDATED             
      *                  WITH EITHER THE INPUT-CONTROL-CARD-DATE OR             
      *                  THE FSCL-END-MN-DATE WHICH HAVE IS LESS.               
      *    CHANGED BY:   JULIE SEIDLER  08/03/98                                
      *****************************************************************         
      *    CHANGE MADE:  ADD LOGIC TO INCLUDE 3 NEW FIELDS ON TABLE             
      *                  TINVPAR.                                               
      *    CHANGED BY:   MARIA KLAMIK   03/15/99                                
      *****************************************************************         
      *    CHANGE MADE:  REMOVED REFERENCE TO DPG51-SYSTEM-DATE.  THIS          
      *                  FIELD WILL NO LONGER BE USED BY THE CALENDAR           
      *                  ROUTINE.                                               
      *    CHANGED BY:   JULIE SEIDLER  04/28/99                                
      *****************************************************************         
      *    CHANGE MADE:  ADD LOGIC TO INCLUDE NEW FIELD ON TABLE                
      *                  TINVPAR (INVPAR-SHEET-INV-CPLD-IND)                    
      *                          (INVPAR-UNT-BKG-STAT-CDE)                      
      *    CHANGED BY:   JULIE SEIDLER  10/08/99                                
      *****************************************************************         
      *    CHANGE MADE:  ADD LOGIC TO INCLUDE NEW FIELD ON TABLE                
      *                  TINVPAR (INVPAR-FIL-TOT-BAL-CDE)                       
      *    CHANGED BY:   JULIE SEIDLER  04/03/00                                
      *****************************************************************         
      *                                                                         
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
      * 22000    03-07-2001  ADD NEW FIELD STKRM-WDRWL-IND TO TABLE             
      *                                              -V. LEE YANG               
      *                                                                         
RB0201*****************************************************************         
RB0201*    CHANGE MADE:  ADD LOGIC TO INCLUDE NEW FIELD ON TABLE                
RB0201*                  TINVPAR (INVPAR-MFSTNG-IND).                           
RB0201*    CHANGED BY:   RONNA BUTZ     02/28/01                                
W21732*****************************************************************         
W21732*    CHANGE MADE:  ADD LOGIC TO INCLUDE NEW FIELDS ON TABLE               
W21732*                  TINVPAR (INVPAR-MFSTNG-IND).                           
W21732*    CHANGED BY:   RONNA BUTZ     03/24/03                                
25248 *****************************************************************         
25248 * 25248    10-27-2003  UPC CAPABILITIES & STORE NUMBER EXPANSION          
25248 *                      -ROD JANSSEN                                       
26600 * 26600    09-22-2004  REMOVE INVPAR-STORE-NBR FROM DB2 CALLS             
26600 *                      STORE EXPANSION                                    
26600 *                      -SCOTT JACKSON                                     
W28543* W28543   06-15-2004  REMOVE INVPAR-STORE-NBR FROM DB2 CALLS.            
W28543*                      THE 9/22/04 CHANGE WAS NEVER PROMOTED DUE          
W28543*                      TO THE TRANSFER GROUP REQUIRING STORE-NBR          
W28543*                      THRU JAN'05 CYCLE. FIELD REMOVED 2/24/05           
W28543*                      BUT THIS PROGRAM STILL NEEDED TO CHANGE.           
W28543*                      CHNG CURSOR TO SPECIFY COLUMN(VS SELECT *)         
W28543*                      -SCOTT JACKSON (PITS - PM00771605)                 
W28545* W28545   05-11-2006  NEW COLUMNS ADDED TO TINVPAR (INV_ID AND           
W28545*                      INV_CLO_DTE) ADDED TO CURSOR AND INSERT            
W28543*                      -SCOTT JACKSON                                     
S28545* S28545   07-19-2006  DEPARTMENT LEVEL INVENTORY. INCORPORATE            
S28543*                      INVENTORY ID.                                      
S28543*                      -JOHN SELWITSCHKA / STRATAGEM                      
W33304* W33304   05-06-2008  HILO CHANGES,                                      
W33304*                      SEE TKMA.IN.HILO.SRCLIB(INKUP105)                  
W33304*          USE DB2 TABLE TINCNTL INSTEAD OF TMLCNTL                       
W33304*                                                                         
W33304*                      -PATTY JAEGER / CODEWORKS                          
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
      *                                                                         
       01  INPUT-RECORD                       PIC X(80).                        
                                                                                
      *****************************************************************         
       WORKING-STORAGE SECTION.                                                 
      *****************************************************************         
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES       PIC S9(04)  VALUE +3                        
                                                COMP SYNC.                      
           05  PC-CALENDAR-ROUTINE               PIC  X(08) VALUE               
                                                     'DPKUT500'.                
           05  PC-PROGRAM-NAME      PIC  X(08)  VALUE 'INKUP105'.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-TINVPAR-CSR-SW        PIC X       VALUE 'N'.           
               88  PS-END-OF-TINVPAR-CSR                   VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RETRY-COUNT                  PIC S9(04) COMP SYNC             
                                                       VALUE ZERO.              
           05  PV-SQL-SUB                      PIC S9(4)   VALUE ZERO           
                                                           COMP.                
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-DB2-OPER-ATTEMPTED           PIC X(30)   VALUE SPACE.         
           05  PV-MAX-RETRIES                  PIC S9(3)   VALUE +3.            
           05  PV-SHTG-INV-DTE                 PIC X(10)   VALUE SPACE.         
JS0400     05  PV-COUNTER                      PIC 9(06)   VALUE ZERO.          
W33304     05  PV-NULL                         PIC S9(04)  VALUE +0             
W33304                                                      COMP SYNC.          
                                                                                
       01  CC-CONTROL-CARD                      VALUE SPACE.                    
           05  CC-DATE                          PIC X(10).                      
           05  FILLER                           PIC X(70).                      
                                                                                
      ******************************************************************        
      * CALENDAR ROUTINE WORK AREA.                                             
      ******************************************************************        
                                                                                
           COPY DPWS500.                                                        
                                                                                
      ******************************************************************        
      *  DCLGEN FOR THE INVENTORY PARAMETERS DB2 TABLE.                         
      ****************************************************************          
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
S28545******************************************************************        
S28545*  DCLGEN FOR THE INVENTORY PARAMETERS DB2 TABLE.                         
S28545****************************************************************          
S28545     EXEC SQL                                                             
S28545         INCLUDE TINCNTL                                                  
S28545     END-EXEC.                                                            
S28545                                                                          
      *****************************************************************         
      * DB2 COMM AREA - DECLARE CURSOR TINVPAR_CSR                              
      *****************************************************************         
      *                                                                         
            EXEC SQL                                                            
              DECLARE TINVPAR_CSR CURSOR FOR                                    
                  SELECT                                                        
S28545*              INV_ID                                                     
S28545               A.INV_ID                                                   
W28543              ,LOC_NBR                                                    
   |                ,ACTL_INV_DTE                                               
   |                ,PLND_INV_DTE                                               
   |                ,SCAN_INV_IND                                               
   |                ,ACTL_UNIT_BK_DTE                                           
   |                ,ACTL_FIN_BK_DTE                                            
   |                ,PLND_UNIT_BK_DTE                                           
   |                ,PLND_FIN_BK_DTE                                            
   |                ,ACTL_INV_RECD_DTE                                          
S28545*             ,STKRM_DAY_QTY                                              
S28545*             ,CORR_DAY_QTY                                               
S28545              ,A.STKRM_DAY_QTY                                            
S28545              ,A.CORR_DAY_QTY                                             
   |                ,CHG_TMST                                                   
   |                ,CHG_ID_NBR                                                 
   |                ,ACTL_LDG_CTOF_TMST                                         
   |                ,ACTL_DC_CTOFF_DTE                                          
   |                ,ITM_CTOFF_IND                                              
   |                ,SHTG_ACTL_INV_DTE                                          
   |                ,PHY_CNT_TMST                                               
   |                ,SLS_ADJ_CPLD_IND                                           
W33304              ,SCHD_PHY_CNT_CDE                                           
W28543              ,XFER_INV_CTOFF_DTE                                         
   |                ,SHEET_INV_CPLD_IND                                         
   |                ,UNT_BKG_STAT_CDE                                           
   |                ,FIL_TOT_BAL_CDE                                            
   |                ,STKRM_WDRWL_IND                                            
   |                ,MFSTNG_IND                                                 
   |                ,INV_SRVC_CDE                                               
   |                ,JOB_NM                                                     
   |                ,SHEET_INV_REQR_IND                                         
W28543              ,TMST_EDT_STAT_CDE                                          
W28545              ,INV_CLO_DTE                                                
W33304              ,FINCL_INV_STAT_CDE                                         
W33304              ,LOC_INV_STAT_CDE                                           
W33304              ,LOC_DEL_RSN_TXT                                            
W33304              ,LOC_DEL_DTE                                                
W33304              ,B.OPN_FSCL_MN_NBR                                          
W33304              ,B.OPN_FSCL_MN_DTE                                          
S28545*           FROM TINVPAR                                                  
S28545            FROM TINVPAR A                                                
S28545                ,TINCNTL B                                                
                  WHERE PLND_INV_DTE = :CC-DATE                                 
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                               
S28545              AND B.ACTV_IND   = 'Y'                                      
S28545              AND A.INV_ID     = B.INV_ID                                 
            END-EXEC.                                                           
      *                                                                         
      *  DB2 FORMATTED ERROR MESSAGE                                            
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *                                                                         
      ******************************************************************        
      *  CONTROLS THE MAIN PROCESSING OF THE PROGRAM                            
      ******************************************************************        
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM B100-OPEN-FILE.                                              
                                                                                
           PERFORM B200-READ-INPUT-DATE.                                        
                                                                                
           PERFORM B300-VALIDATE-DATE.                                          
                                                                                
                                                                                
           PERFORM Z350-OPEN-CURSOR.                                            
                                                                                
           PERFORM Z400-FETCH-CURSOR.                                           
           PERFORM C200-PROCESS-TINVPAR                                         
              UNTIL PS-END-OF-TINVPAR-CSR.                                      
                                                                                
           DISPLAY '***********************************'                        
           DISPLAY PV-COUNTER ' ROWS UPDATED'                                   
           DISPLAY '***********************************'                        
           PERFORM Z850-CLOSE-CURSOR.                                           
           PERFORM B400-CLOSE-FILE.                                             
      *                                                                         
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  B100 - OPENS INPUT FILE                                                
      ******************************************************************        
       B100-OPEN-FILE.                                                          
                                                                                
            OPEN INPUT INPUT-FILE.                                              
                                                                                
      ******************************************************************        
      *  B200  READS DATE FROM INPUT FILE                                       
      ******************************************************************        
                                                                                
       B200-READ-INPUT-DATE.                                                    
            READ INPUT-FILE                                                     
               INTO CC-CONTROL-CARD.                                            
                                                                                
                                                                                
      ******************************************************************        
      *   CHECKS DATE IS NOT SPACES AND IS A VALID CALENDAR DATE                
      ******************************************************************        
                                                                                
       B300-VALIDATE-DATE.                                                      
                                                                                
           DISPLAY '*** INKUP105 INPUT CONTROL CARD:' CC-CONTROL-CARD.          
                                                                                
           IF CC-DATE = SPACE                                                   
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = INKUP105'                                      
               DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                      
               DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'                 
               MOVE +4003 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
                                                                                
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                               
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-DATE                  TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL PC-CALENDAR-ROUTINE                                             
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-SEVERE-ERROR                                          
                   DISPLAY '** ABEND **'                                        
                   DISPLAY '** PGM = INKUP105'                                  
                   DISPLAY '** PARAGRAPH = B300-VALIDATE-DATE'                  
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
                   DISPLAY '** USING CONTROL CARD DATE AS INPUT.'               
                   DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
                   DISPLAY '** DPG51 = ' DPG51                                  
                   DISPLAY '** DPG52 = ' DPG52                                  
                   DISPLAY '** DPG53 = ' DPG53                                  
                   DISPLAY '** DPG54 = ' DPG54                                  
                   MOVE +4003 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
               WHEN OTHER                                                       
                   CONTINUE                                                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ****************************************************************          
      *  THE SHTG-ACTL-INV-DTE WILL BE UPDATED WITH THE CONTROL CARD            
      *  DATE, NORMALLY. HOWEVER, IN THE EVENT THAT STORES DO INVENTORY         
      *  PASSED THE FISCAL END OF MONTH, THE FISCAL END OF MONTH DATE           
      *  WILL BE ASSIGNED TO THE SHTG-ACTL-INV-DTE INSTEAD.                     
      ****************************************************************          
                                                                                
       B327-DETERMINE-SHTG-DTE.                                                 
                                                                                
W33304     IF CC-DATE > INCNTL-OPN-FSCL-MN-DTE                                  
W33304         MOVE INCNTL-OPN-FSCL-MN-DTE TO PV-SHTG-INV-DTE                   
           ELSE                                                                 
               MOVE CC-DATE         TO PV-SHTG-INV-DTE.                         
                                                                                
                                                                                
      ****************************************************************          
      *  B400 - CLOSES INPUT FILE                                               
      ****************************************************************          
                                                                                
       B400-CLOSE-FILE.                                                         
            CLOSE INPUT-FILE.                                                   
                                                                                
                                                                                
      ******************************************************************        
      *  FETCHES ROW, DELETES AND RE-INSERTS WITH NEW ACTUAL INVENTORY          
      *  DATE                                                                   
      ******************************************************************        
       C200-PROCESS-TINVPAR.                                                    
           PERFORM Z700-DELETE-TINVPAR-ROW.                                     
W33304     PERFORM B327-DETERMINE-SHTG-DTE.                                     
           PERFORM Z800-INSERT-TINVPAR-ROW.                                     
           PERFORM Z400-FETCH-CURSOR.                                           
                                                                                
                                                                                
       Z350-OPEN-CURSOR.                                                        
      ****************************************************************          
      *  OPEN STORE CURSOR                                                      
      ****************************************************************          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
                     (SQLCODE = +100) OR                                        
                     (SQLCODE = 0)                                              
                                                                                
               EXEC SQL                                                         
                  OPEN TINVPAR_CSR                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-END-OF-TINVPAR-CSR TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE 'Z350-OPEN-CURSOR'                                 
                              TO PV-PARAGRAPH-NAME                              
                        MOVE 'OPEN OF CURSOR'                                   
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM Z900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PV-MAX-RETRIES                                       
                MOVE 'Z350-OPEN-TINVPAR TABLE' TO PV-PARAGRAPH-NAME             
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM Z900-SQL-ABEND                                          
           END-IF.                                                              
      *                                                                         
                                                                                
      ******************************************************************        
      *   FETCH TINVPAR CURSOR                                                  
      ******************************************************************        
       Z400-FETCH-CURSOR.                                                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 100                                       
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                   FETCH TINVPAR_CSR                                            
25545              INTO  :INVPAR-INV-ID,                                        
25248                    :INVPAR-LOC-NBR,                                       
                         :INVPAR-ACTL-INV-DTE,                                  
                         :INVPAR-PLND-INV-DTE,                                  
                         :INVPAR-SCAN-INV-IND,                                  
                         :INVPAR-ACTL-UNIT-BK-DTE,                              
                         :INVPAR-ACTL-FIN-BK-DTE,                               
                         :INVPAR-PLND-UNIT-BK-DTE,                              
                         :INVPAR-PLND-FIN-BK-DTE,                               
                         :INVPAR-ACTL-INV-RECD-DTE,                             
                         :INVPAR-STKRM-DAY-QTY,                                 
                         :INVPAR-CORR-DAY-QTY,                                  
                         :INVPAR-CHG-TMST,                                      
                         :INVPAR-CHG-ID-NBR,                                    
                         :INVPAR-ACTL-LDG-CTOF-TMST,                            
                         :INVPAR-ACTL-DC-CTOFF-DTE,                             
                         :INVPAR-ITM-CTOFF-IND,                                 
                         :INVPAR-SHTG-ACTL-INV-DTE,                             
                         :INVPAR-PHY-CNT-TMST,                                  
                         :INVPAR-SLS-ADJ-CPLD-IND,                              
W33304                   :INVPAR-SCHD-PHY-CNT-CDE,                              
                         :INVPAR-XFER-INV-CTOFF-DTE,                            
JS1099                   :INVPAR-SHEET-INV-CPLD-IND,                            
JS1199                   :INVPAR-UNT-BKG-STAT-CDE,                              
22000                    :INVPAR-FIL-TOT-BAL-CDE,                               
RB0201                   :INVPAR-STKRM-WDRWL-IND,                               
RB0201                   :INVPAR-MFSTNG-IND,                                    
RB0201                   :INVPAR-INV-SRVC-CDE,                                  
RB0201                   :INVPAR-JOB-NM,                                        
W21732                   :INVPAR-SHEET-INV-REQR-IND,                            
W21732                   :INVPAR-TMST-EDT-STAT-CDE,                             
W33304                   :INVPAR-INV-CLO-DTE,                                   
W33304                   :INVPAR-FINCL-INV-STAT-CDE,                            
W33304                   :INVPAR-LOC-INV-STAT-CDE,                              
W33304                   :INVPAR-LOC-DEL-RSN-TXT :PV-NULL,                      
W33304                   :INVPAR-LOC-DEL-DTE,                                   
W33304                   :INCNTL-OPN-FSCL-MN-NBR,                               
W33304                   :INCNTL-OPN-FSCL-MN-DTE                                
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                       SET PS-END-OF-TINVPAR-CSR TO TRUE                        
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = 0                                             
                       ADD +1 TO PV-COUNTER                                     
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z400-FETCH TINVPAR'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH CURSOR'                                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z400-FETCH TINVPAR'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
                                                                                
      ******************************************************************        
      *  Z700 - DELETES ROW ON TINVPAAR                                         
      ******************************************************************        
       Z700-DELETE-TINVPAR-ROW.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                   DELETE                                                       
                   FROM TINVPAR                                                 
S28545*            WHERE CURRENT OF TINVPAR_CSR                                 
S28545             WHERE INV_ID  = :INVPAR-INV-ID                               
S28545               AND LOC_NBR = :INVPAR-LOC-NBR                              
                                                                                
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z700-DELETE-TINVPAR-ROW'                           
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN ON CURSOR'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z700-DELETE-TINVPAR-ROW'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
      ***************************************************************           
      *    Z800 - INSERTS NEW ROW ON TINVPAR                                    
      ***************************************************************           
                                                                                
       Z800-INSERT-TINVPAR-ROW.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
               EXEC SQL                                                         
                    INSERT INTO TINVPAR                                         
                       VALUES                                                   
28545                   (:INVPAR-INV-ID,                                        
25248                    :INVPAR-LOC-NBR,                                       
                         :CC-DATE,                                              
                         :INVPAR-PLND-INV-DTE,                                  
                         :INVPAR-SCAN-INV-IND,                                  
                         :INVPAR-ACTL-UNIT-BK-DTE,                              
                         :INVPAR-ACTL-FIN-BK-DTE,                               
                         :INVPAR-PLND-UNIT-BK-DTE,                              
                         :INVPAR-PLND-FIN-BK-DTE,                               
                         :INVPAR-ACTL-INV-RECD-DTE,                             
                         :INVPAR-STKRM-DAY-QTY,                                 
                         :INVPAR-CORR-DAY-QTY,                                  
                          CURRENT TIMESTAMP,                                    
                         :PC-PROGRAM-NAME,                                      
                         :INVPAR-ACTL-LDG-CTOF-TMST,                            
                         :INVPAR-ACTL-DC-CTOFF-DTE,                             
                         :INVPAR-ITM-CTOFF-IND,                                 
                         :PV-SHTG-INV-DTE,                                      
                         :INVPAR-PHY-CNT-TMST,                                  
                         :INVPAR-SLS-ADJ-CPLD-IND,                              
W33304                   :INVPAR-SCHD-PHY-CNT-CDE,                              
                         :INVPAR-XFER-INV-CTOFF-DTE,                            
JS1099                   :INVPAR-SHEET-INV-CPLD-IND,                            
JS1199                   :INVPAR-UNT-BKG-STAT-CDE,                              
22000                    :INVPAR-FIL-TOT-BAL-CDE,                               
RB0201                   :INVPAR-STKRM-WDRWL-IND,                               
RB0201                   :INVPAR-MFSTNG-IND,                                    
RB0201                   :INVPAR-INV-SRVC-CDE,                                  
RB0201                   :INVPAR-JOB-NM,                                        
W21732                   :INVPAR-SHEET-INV-REQR-IND,                            
W21732                   :INVPAR-TMST-EDT-STAT-CDE,                             
W33304                   :INVPAR-INV-CLO-DTE,                                   
W33304                   :INVPAR-FINCL-INV-STAT-CDE,                            
W33304                   :INVPAR-LOC-INV-STAT-CDE,                              
W33304                   :INVPAR-LOC-DEL-RSN-TXT,                               
W33304                   :INVPAR-LOC-DEL-DTE)                                   
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                       MOVE 'Z800-INSERT-TINVPAR-ROW       '                    
                           TO PV-PARAGRAPH-NAME                                 
                       MOVE 'INSERTING INTO TINVPAR TABLE'                      
                           TO PV-DB2-OPER-ATTEMPTED                             
                       PERFORM Z900-SQL-ABEND                                   
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PC-MAX-RETRIES                                       
               MOVE 'Z800-INSERT-TINVPAR-ROW       '                            
                   TO PV-PARAGRAPH-NAME                                         
               MOVE 'MAX RETRIES EXCEEDED   '                                   
                   TO PV-DB2-OPER-ATTEMPTED                                     
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
       Z850-CLOSE-CURSOR.                                                       
      ****************************************************************          
      *  CLOSE TINVPAR_CSR CURSOR                                               
      ****************************************************************          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
                     (SQLCODE = +100) OR                                        
                     (SQLCODE = 0)                                              
                                                                                
               EXEC SQL                                                         
                  CLOSE TINVPAR_CSR                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < 0                                             
                   WHEN SQLCODE > 0                                             
                        MOVE 'Z850-CLOSE-CURSOR'                                
                              TO PV-PARAGRAPH-NAME                              
                        MOVE 'CLOSE OF TINVPAR TABLE'                           
                              TO PV-DB2-OPER-ATTEMPTED                          
                        PERFORM Z900-SQL-ABEND                                  
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF PV-SQL-SUB > PV-MAX-RETRIES                                       
                MOVE 'Z850-CLOSE-CURSOR' TO PV-PARAGRAPH-NAME                   
                MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED            
                PERFORM Z900-SQL-ABEND                                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PERFORMS THE ABEND FOR THE PROGRAM IN THE                               
      * EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = INKUP105'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           IF SQLCODE NOT = -911                                                
               EXEC SQL                                                         
                   ROLLBACK                                                     
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
