       ID  DIVISION.                                                            
       PROGRAM-ID.         INKRP175.                                            
       AUTHOR.             JULIE SEIDLER.                                       
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       09/27/99.                                            
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *   THIS PROGRAM READS OUPUT FROM INKUT102-VALID RECORDS                  
      *   AND INKUT169-ITEM TIMESTAMP FILE AND CREATES THE                      
      *   TIMESTAMP DATA REPORT.  THIS REPORT IS USED FOR                       
      *   TROUBLE-SHOOTING PROBLEMS IN TIMESTAMP DATA RECEIVED FROM             
      *   THE SCANNING SERVICES.                                                
      *                                                                         
      *   TO GENERATE THE REPORT SHOWING 3 COLUMNS OF DATA, 3 TABLES            
      *   WILL BE USED TO SAVE THE RECORDS "WRITTEN". WHEN ALL 3 HOLDING        
      *   TABLES ARE FULL, THEN THE PAGE IS ACTUALLY WRITTEN.                   
      *                                                                         
      *   INP01 IS THE DRIVER (INKUT102) - FORMAT IS INWS009                    
      *   INP02 WILL BE REFERENCED (INKUT169) - FORMAT IS INRD019               
      *   INP03 - GAP CONTROL CARD                                              
      *   RPT01 - TIMESTAMP DATA REPORT                                         
      *----------------------------------------------------------------*        
      *                   CHANGE LOG                                            
      *----------------------------------------------------------------*        
      * XX/XX X. XXXXXXXX                                                       
      *                                                                         
      *----------------------------------------------------------------*        
W21732* 11/18/2002        CHANGED BY SUKUMAR,IGSI    WR# W21732        *        
W21732* CHANGED CURRENT DEPT/CLASS/SEQ NUMBER TO NON-INTELLIGENT SKU.  *        
W21732*----------------------------------------------------------------*        
W26600* 08-18-2004        CHANGED BY MARK JOHNSON    WR#26600                   
W26600* CHANGE PROGRAM FOR STORE NUMBER EXPANSION                               
W33304*----------------------------------------------------------------*        
W33304* 10-21-2008        CHANGED BY THERESE RAMSEYER WR#33304                  
W33304*                   HILO - HOLD INVENTORY LEDGER OPEN                     
W33304*                   --> EXPANDED TIMESTAMP-CUTOFF-REC REC                 
W33304*                       LENGTH FROM 90 TO 100 BYTES TO                    
W33304*                       ACCOMMODATE INRD019 SIMILAR CHANGE                
W33304*----------------------------------------------------------------*        
SR3403* 04-03-2012        CHANGED BY ANUJ GUPTA       WR# WR3403                
SR3403*                   LARGE RETAIL PROJECT                                  
SR3403*----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TIMESTAMP-DATA-FILE                                           
               ASSIGN TO INP01.                                                 
           SELECT TIMESTAMP-CUTOFF-FILE                                         
               ASSIGN TO INP02.                                                 
           SELECT GAP-CONTROL-CARD-FILE                                         
               ASSIGN TO INP03.                                                 
           SELECT TIMESTAMP-REPORT                                              
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TIMESTAMP-DATA-FILE                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS TIMESTAMP-RECORD.                                     
SR3403 01  TIMESTAMP-RECORD             PIC X(100).                             
                                                                                
       FD  TIMESTAMP-CUTOFF-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F                                                 
           DATA RECORD IS TIMESTAMP-CUTOFF-REC.                                 
W33304 01  TIMESTAMP-CUTOFF-REC         PIC X(100).                             
                                                                                
       FD  GAP-CONTROL-CARD-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  GAP-CONTROL-CARD-REC         PIC X(80).                              
                                                                                
       FD  TIMESTAMP-REPORT                                                     
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE         PIC X(200).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                      PIC S9(04)  VALUE +4003              
                                                           COMP SYNC.           
       01  CC-GAP-CNTL-REC.                                                     
           05  FILLER                  PIC X(04).                               
           05  CC-GAP-TIME.                                                     
               10  CC-GAP-HOUR         PIC X(02).                               
               10  FILLER              PIC X(01).                               
               10  CC-GAP-MIN          PIC X(02).                               
           05  FILLER                  PIC X(01).                               
           05  CC-GAP-QTY              PIC 9(03).                               
           05  FILLER                  PIC X(67).                               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  WS-QTY                  PIC 9(06)    VALUE ZERO.                 
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-ABEND-MESSAGE        PIC X(50)    VALUE SPACE.                
           05  PV-SQL-SUB              PIC 9(03)    VALUE ZERO.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-FIRST-TIME           PIC X(01)    VALUE 'Y'.                  
           05  PV-HOLD-ITEM.                                                    
W21732         07  PV-HOLD-DEPT        PIC X(04)    VALUE SPACES.               
W21732         07  PV-HOLD-CLASS       PIC X(04)    VALUE SPACES.               
W21732         07  PV-HOLD-SKU         PIC X(08)    VALUE SPACES.               
           05  PV-HOLD-ITEM-2.                                                  
W21732         07  PV-HOLD-DEPT-2      PIC X(04)    VALUE SPACES.               
W21732         07  PV-HOLD-CLASS-2     PIC X(04)    VALUE SPACES.               
W21732         07  PV-HOLD-SKU-2       PIC X(08)    VALUE SPACES.               
           05  PV-ITEM.                                                         
W21732         07  PV-DEPT             PIC X(04)    VALUE SPACES.               
W21732         07  PV-CLASS            PIC X(04)    VALUE SPACES.               
W21732         07  PV-SKU              PIC X(08)    VALUE SPACES.               
W26600     05  PV-HOLD-STORE           PIC  9(04)   VALUE ZEROS.                
W26600     05  PV-HOLD-STORE-2         PIC  9(04)   VALUE ZEROS.                
           05  PV-HOLD-DATE            PIC X(04).                               
           05  PV-HOLD-DATEA REDEFINES PV-HOLD-DATE.                            
               07  PV-HOLD-MM          PIC X(02).                               
               07  PV-HOLD-DD          PIC X(02).                               
W26600     05  PV-HOLD-TIME            PIC 9(06).                               
           05  PV-HOLD-TIMEA REDEFINES PV-HOLD-TIME.                            
               07  PV-HH               PIC X(02).                               
               07  PV-MIN              PIC X(02).                               
               07  PV-SS               PIC X(02).                               
           05  PV-HOLD-DATE2           PIC X(04).                               
           05  PV-END-OF-INPUT-SW      PIC  X(01)          VALUE 'N'.           
               88  PV-END-OF-INPUT                         VALUE 'Y'.           
               88  PV-NOT-END-OF-INPUT                     VALUE 'N'.           
           05  PV-END-OF-INPUT2-SW     PIC  X(01)          VALUE 'N'.           
               88  PV-END-OF-INPUT2                        VALUE 'Y'.           
               88  PV-NOT-END-OF-INPUT2                    VALUE 'N'.           
           05  PV-EMPTY-CONTROL-SW     PIC  X(01)          VALUE 'N'.           
               88  PV-EMPTY-CONTROL                        VALUE 'Y'.           
           05  PV-ERROR-OCCURRED-SW    PIC  X(01)          VALUE 'N'.           
               88  PV-ERROR-OCCURRED                       VALUE 'Y'.           
           05  PV-NO-ADD-DATA-SW       PIC  X(01)          VALUE 'N'.           
               88  PV-NO-ADD-DATA                          VALUE 'Y'.           
           05  PS-COLUMN-SW                PIC X(01) VALUE '1'.                 
               88  PS-RPT-COL1                       VALUE '1'.                 
               88  PS-RPT-COL2                       VALUE '2'.                 
               88  PS-RPT-COL3                       VALUE '3'.                 
                                                                                
           05  PV-PAGE-COUNT           PIC S9(05)   COMP-3 VALUE 0.             
           05  PV-LINE-COUNT           PIC S9(03)   COMP-3 VALUE 171.           
           05  PV-CURRENT-DATE.                                                 
               10  PV-CD-YY            PIC XX.                                  
               10  PV-CD-MM            PIC XX.                                  
               10  PV-CD-DD            PIC XX.                                  
           05  PV-CURRENT-TIME.                                                 
               10  PV-CT-HH            PIC XX.                                  
               10  PV-CT-MM            PIC XX.                                  
           05  PV-CUTOFF               PIC X(07)    VALUE ' CUTOFF'.            
           05  PV-STATUS-CODE-NUM      PIC 9(03).                               
           05  FILLER REDEFINES PV-STATUS-CODE-NUM.                             
               10  FILLER              PIC 9(01).                               
               10  PV-STAT-CDE-FMT     PIC 9(02).                               
           05  SS-LINE-SUB                     PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
           05  SS-RPT-SUB                      PIC S9(04)  VALUE ZEROES         
                                                           COMP SYNC.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(4)    COMP   VALUE +3.            
           05  PC-1                    PIC X(01)    VALUE '1'.                  
           05  PC-PROGRAM-NAME         PIC X(08)    VALUE 'INKRP175'.           
           05  PC-FILE1-NAME           PIC X(18)    VALUE                       
               'DATA-FILE    *****'.                                            
           05  PC-FILE2-NAME           PIC X(18)    VALUE                       
               'CUTOFF-FILE  *****'.                                            
           05  PC-RPT-COL-MAX          PIC S9(3)    COMP-3 VALUE 55.            
                                                                                
                                                                                
       01  DL-FULL-DETAIL-LINE.                                                 
           05  DL-DETAIL-COL1             PIC X(65)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  DL-DETAIL-COL2             PIC X(64)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  DL-DETAIL-COL3             PIC X(64)  VALUE SPACES.              
                                                                                
                                                                                
       01  TB-REPORT-COL1-TABLE.                                                
           05  TB-REPORT-COL1-TAB OCCURS 55 TIMES.                              
               10  TB-COL1-LINE        PIC  X(65).                              
                                                                                
       01  TB-REPORT-COL2-TABLE.                                                
           05  TB-REPORT-COL2-TAB OCCURS 55 TIMES.                              
               10  TB-COL2-LINE        PIC  X(65).                              
                                                                                
       01  TB-REPORT-COL3-TABLE.                                                
           05  TB-REPORT-COL3-TAB OCCURS 55 TIMES.                              
               10  TB-COL3-LINE        PIC  X(65).                              
                                                                                
       01  HL1-HEADING-LINE-1.                                                  
           05  FILLER                  PIC X(95)    VALUE SPACES.               
           05  FILLER                  PIC X(21)    VALUE                       
               'TIMESTAMP DATA REPORT'.                                         
           05  FILLER                  PIC X(56)    VALUE SPACES.               
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                  PIC X(88)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'GAP: '.                                                         
           05  HL2-GAP-HH              PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(01)    VALUE                       
               ':'.                                                             
           05  HL2-GAP-MM              PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE SPACES.               
           05  FILLER                  PIC X(16)    VALUE                       
               'REASONABLE QTY: '.                                              
           05  HL2-RES-QTY             PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(49)    VALUE SPACES.               
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(08)    VALUE                       
               'STORE:  '.                                                      
W26600     05  HL3-STORE               PIC X(04)    VALUE SPACES.               
           05  FILLER                  PIC X(119)   VALUE SPACES.               
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
W21732         'SKU '.                                                          
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'INVENTORY'.                                                     
           05  FILLER                  PIC X(42)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
W21732         'SKU '.                                                          
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'INVENTORY'.                                                     
           05  FILLER                  PIC X(42)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
W21732         'SKU '.                                                          
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'INVENTORY'.                                                     
           05  FILLER                  PIC X(42)    VALUE SPACES.               
                                                                                
       01  HL5-HEADING-LINE-5.                                                  
           05  FILLER                  PIC X(13)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'TIMESTAMP'.                                                     
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'AREA '.                                                         
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
               'QTY '.                                                          
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE                       
               'PRICE '.                                                        
           05  FILLER                  PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(12)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'TIMESTAMP'.                                                     
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'AREA '.                                                         
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
               'QTY '.                                                          
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE                       
               'PRICE '.                                                        
           05  FILLER                  PIC X(29)    VALUE SPACES.               
           05  FILLER                  PIC X(09)    VALUE                       
               'TIMESTAMP'.                                                     
           05  FILLER                  PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE                       
               'AREA '.                                                         
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE                       
               'QTY '.                                                          
           05  FILLER                  PIC X(03)    VALUE SPACES.               
           05  FILLER                  PIC X(06)    VALUE                       
               'PRICE '.                                                        
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                  PIC X(01)    VALUE SPACES.               
           05  DL-ITEM-NUM.                                                     
W21732         07  DL-SKU-NBR          PIC X(08)    VALUE SPACES.               
W21732     05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  DL-INV-TIMESTAMP.                                                
               07  DL-MM               PIC X(02)    VALUE SPACES.               
               07  DL-SLASH            PIC X(01)    VALUE '/'.                  
               07  DL-DD               PIC X(02)    VALUE SPACES.               
               07  FILLER              PIC X(01)    VALUE SPACE.                
               07  DL-HH               PIC X(02)    VALUE SPACES.               
               07  DL-COLON1           PIC X(01)    VALUE ':'.                  
               07  DL-MIN              PIC X(02)    VALUE SPACES.               
               07  DL-COLON2           PIC X(01)    VALUE ':'.                  
               07  DL-SS               PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-AREA                 PIC X(06)    VALUE SPACES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-QTY                  PIC ZZZ9     VALUE ZEROES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
SR3403     05  DL-PRICE                PIC ZZ,ZZ9.99 VALUE ZEROES.              
SR3403     05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  DL-CUT                  PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE SPACES.               
                                                                                
       01  DL-NO-DETAIL-LINE.                                                   
           05  FILLER                  PIC X(23)    VALUE                       
               '*****  EMPTY TIMESTAMP '.                                       
           05  DL-NO-DET-FILE          PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(91)    VALUE SPACES.               
                                                                                
      *----------------------------------------------------------------*        
      * COPYBOOK FOR REMAINING REPORT HEADING LINES, DETAIL LINE                
      *----------------------------------------------------------------*        
           COPY DPWS200O.                                                       
                                                                                
      *----------------------------------------------------------------*        
      *  EDITED INVENTORY RECORD - INPUT                                        
      *----------------------------------------------------------------*        
           COPY INWS009.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *  EDITED INVENTORY CUTOFF RECORD - INPUT                                 
      *----------------------------------------------------------------*        
           COPY INRD019.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       1000-MAIN-MODULE.                                                        
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* START OF INKRP175               '.                        
           DISPLAY '**********************************'.                        
                                                                                
           PERFORM 1100-HOUSEKEEPING.                                           
                                                                                
           IF PV-NOT-END-OF-INPUT AND PV-NOT-END-OF-INPUT2                      
              PERFORM 2000-PROCESS-INPUT                                        
                  UNTIL PV-END-OF-INPUT                                         
      *       ** WRITE LAST PAGE FOR LAST STORE                                 
              PERFORM 2020-MOVE-DET-LINE2                                       
              PERFORM 2305-WRITE-DETAIL2                                        
              PERFORM 2310-WRITE-HEADER                                         
              PERFORM 5010-WRITE-PRINT-LINE-TABLE                               
                  VARYING SS-RPT-SUB FROM 1 BY 1                                
                  UNTIL SS-RPT-SUB > PC-RPT-COL-MAX                             
              DISPLAY 'STR ' HL3-STORE ' LAST PAGE '                            
                       PV-PAGE-COUNT                                            
           ELSE                                                                 
              IF PV-END-OF-INPUT AND PV-END-OF-INPUT2                           
                 MOVE 'Y' TO PV-ERROR-OCCURRED-SW                               
                 DISPLAY '**********************************'                   
                 DISPLAY '**********************************'                   
                 DISPLAY '     '                                                
                 DISPLAY 'BOTH TIMESTAMP FILES ARE EMPTY'                       
                 DISPLAY '     '                                                
                 DISPLAY '**********************************'                   
                 DISPLAY '**********************************'                   
              ELSE                                                              
                 PERFORM 2310-WRITE-HEADER                                      
                 MOVE 'Y' TO PV-ERROR-OCCURRED-SW                               
                 IF PV-END-OF-INPUT                                             
                    MOVE PC-FILE1-NAME    TO DL-NO-DET-FILE                     
                    WRITE REPORT-LINE FROM DL-NO-DETAIL-LINE                    
                        AFTER ADVANCING 3                                       
                    MOVE 'INVENTORY ITEM FILE IS EMPTY'                         
                        TO  PV-ABEND-MESSAGE                                    
                    DISPLAY '**********************************'                
                    MOVE '1000-MAIN-MODULE' TO PV-PARAGRAPH-NAME                
                    PERFORM 9999-ABEND                                          
                 END-IF                                                         
                 IF PV-END-OF-INPUT2                                            
                    MOVE PC-FILE2-NAME    TO DL-NO-DET-FILE                     
                    WRITE REPORT-LINE FROM DL-NO-DETAIL-LINE                    
                        AFTER ADVANCING 3                                       
                    MOVE 'CUTOFF ITEM TIMESTAMP FILE IS EMPTY'                  
                        TO  PV-ABEND-MESSAGE                                    
                    MOVE '1000-MAIN-MODULE' TO PV-PARAGRAPH-NAME                
                    DISPLAY '**********************************'                
                    PERFORM 9999-ABEND                                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1200-EOJ.                                                    
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    OPEN THE FILES, GET THE FIRST RECORD AND SAVE THE KEYS.     *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       1100-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT  TIMESTAMP-DATA-FILE                                      
                       TIMESTAMP-CUTOFF-FILE                                    
                       GAP-CONTROL-CARD-FILE                                    
                OUTPUT TIMESTAMP-REPORT.                                        
                                                                                
           READ GAP-CONTROL-CARD-FILE INTO CC-GAP-CNTL-REC AT END               
                MOVE 'Y' TO PV-EMPTY-CONTROL-SW                                 
           END-READ.                                                            
                                                                                
           IF PV-EMPTY-CONTROL                                                  
               DISPLAY '**********************************'                     
               DISPLAY '*        INKRP175                *'                     
               DISPLAY '*  GAP DATE CONTROL CARD MISSING *'                     
               DISPLAY '**********************************'                     
               MOVE '  '          TO  HL2-GAP-HH                                
               MOVE '  '          TO  HL2-GAP-MM                                
               MOVE '   '         TO  HL2-RES-QTY                               
           ELSE                                                                 
                DISPLAY '**********************************'                    
                DISPLAY '* CONTROL INFORMATION ===> '                           
                         CC-GAP-CNTL-REC                                        
                DISPLAY '**********************************'                    
                MOVE CC-GAP-HOUR   TO  HL2-GAP-HH                               
                MOVE CC-GAP-MIN    TO  HL2-GAP-MM                               
                MOVE CC-GAP-QTY    TO  HL2-RES-QTY                              
           END-IF.                                                              
                                                                                
           ACCEPT PV-CURRENT-DATE    FROM  DATE.                                
           ACCEPT PV-CURRENT-TIME    FROM  TIME.                                
                                                                                
           MOVE PV-CD-MM               TO  DP200O-RUN-MONTH.                    
           MOVE PV-CD-DD               TO  DP200O-RUN-DAY.                      
           MOVE PV-CD-YY               TO  DP200O-RUN-YEAR.                     
                                                                                
           MOVE PV-CT-HH               TO  DP200O-RUN-HOUR.                     
           MOVE PV-CT-MM               TO  DP200O-RUN-MINUTE.                   
           MOVE PC-PROGRAM-NAME        TO  DP200O-PROGRAM-NAME.                 
           MOVE PC-1                   TO  DP200O-REPORT-NUMBER.                
                                                                                
           PERFORM 2100-READ-TIMESTAMP-FILE.                                    
           IF PV-END-OF-INPUT                                                   
              CONTINUE                                                          
           ELSE                                                                 
              MOVE IN009-STORE-NUMBER  TO PV-HOLD-STORE                         
              MOVE IN009-DEPT          TO PV-HOLD-DEPT                          
                                          PV-DEPT                               
W21732        MOVE IN009-SUB-CLASS     TO PV-HOLD-CLASS                         
                                          PV-CLASS                              
W21732        MOVE IN009-SKU           TO PV-HOLD-SKU                           
W21732                                    PV-SKU                                
           END-IF.                                                              
                                                                                
           PERFORM 2150-READ-CUTOFF-FILE.                                       
           IF PV-END-OF-INPUT2                                                  
              CONTINUE                                                          
           ELSE                                                                 
              MOVE IN019-STR-NBR       TO PV-HOLD-STORE-2                       
              MOVE IN019-DEPT-NBR      TO PV-HOLD-DEPT-2                        
              MOVE IN019-CL-NBR        TO PV-HOLD-CLASS-2                       
W21732        MOVE IN019-SKU           TO PV-HOLD-SKU-2                         
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    CLOSE THE FILES.                                            *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       1200-EOJ.                                                                
                                                                                
           IF PV-ERROR-OCCURRED                                                 
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 2050-CHECK-CUTOFF                                         
           END-IF.                                                              
                                                                                
           CLOSE TIMESTAMP-DATA-FILE                                            
                 TIMESTAMP-CUTOFF-FILE                                          
                 GAP-CONTROL-CARD-FILE                                          
                 TIMESTAMP-REPORT.                                              
                                                                                
           DISPLAY '**********************************'.                        
           DISPLAY '* END OF INKRP175                 '.                        
           DISPLAY '**********************************'.                        
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    FOR EACH STORE, PROCESS THE TIMESTAMP RECORD                *        
      *    INPUT FILE FOR EACH ITEM.  WHEN THE ITEM CHANGES CHECK      *        
      *    FOR THE CUTOFF RECORD.                                      *        
      *----------------------------------------------------------------*        
                                                                                
       2000-PROCESS-INPUT.                                                      
                                                                                
           IF  IN009-STORE-NUMBER = PV-HOLD-STORE                               
               IF PV-ITEM = PV-HOLD-ITEM                                        
                  IF PV-FIRST-TIME = 'Y'                                        
                     PERFORM 2010-MOVE-DET-LINE1                                
                     PERFORM 2300-WRITE-DETAIL                                  
                     MOVE 'N' TO PV-FIRST-TIME                                  
                  ELSE                                                          
                     PERFORM 2015-MOVE-DET-LINE                                 
                     PERFORM 2300-WRITE-DETAIL                                  
                  END-IF                                                        
               ELSE                                                             
                  PERFORM 2050-CHECK-CUTOFF                                     
                  PERFORM 2010-MOVE-DET-LINE1                                   
                  PERFORM 2300-WRITE-DETAIL                                     
               END-IF                                                           
           ELSE                                                                 
      *       ** WRITE CUTOFF RECORD TO RPT FOR PREV STORE                      
      *       ** READ NEXT RECORD FROM CUTOFF FILE FOR NEXT STORE               
               PERFORM 2050-CHECK-CUTOFF                                        
                                                                                
      *       ** WRITE LAST PAGE FOR PREV STORE                                 
              PERFORM 2310-WRITE-HEADER                                         
              PERFORM 5010-WRITE-PRINT-LINE-TABLE                               
                  VARYING SS-RPT-SUB FROM 1 BY 1                                
                  UNTIL SS-RPT-SUB > PC-RPT-COL-MAX                             
              INITIALIZE TB-REPORT-COL1-TABLE                                   
              INITIALIZE TB-REPORT-COL2-TABLE                                   
              INITIALIZE TB-REPORT-COL3-TABLE                                   
              MOVE '1' TO PS-COLUMN-SW                                          
              MOVE +1 TO SS-LINE-SUB                                            
                                                                                
      *       ** WRITE RECORD FOR CURRENT STORE                                 
              PERFORM 2010-MOVE-DET-LINE1                                       
              PERFORM 2300-WRITE-DETAIL                                         
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-TIMESTAMP-FILE.                                    
           IF PV-END-OF-INPUT                                                   
              CONTINUE                                                          
           ELSE                                                                 
              MOVE IN009-DEPT     TO PV-DEPT                                    
W21732        MOVE IN009-SUB-CLASS  TO PV-CLASS                                 
W21732        MOVE IN009-SKU        TO PV-SKU                                   
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *   MOVE DATA TO DETAIL LINE.                                    *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2010-MOVE-DET-LINE1.                                                     
                                                                                
W21732     MOVE IN009-SKU        TO  DL-SKU-NBR                                 
W21732                               PV-HOLD-SKU.                               
W21732     MOVE IN009-DEPT       TO  PV-HOLD-DEPT.                              
W21732     MOVE IN009-SUB-CLASS  TO  PV-HOLD-CLASS.                             
           MOVE IN009-SCAN-DTE TO  PV-HOLD-DATE.                                
           MOVE PV-HOLD-MM     TO  DL-MM.                                       
           MOVE PV-HOLD-DD     TO  DL-DD.                                       
           MOVE IN009-SCAN-TIMESTAMP TO  PV-HOLD-TIME.                          
           MOVE PV-HH          TO  DL-HH.                                       
           MOVE PV-MIN         TO  DL-MIN.                                      
           MOVE PV-SS          TO  DL-SS.                                       
           MOVE '/'            TO  DL-SLASH.                                    
           MOVE ':'            TO  DL-COLON1                                    
                                   DL-COLON2.                                   
           MOVE IN009-AREA-NUMBER TO DL-AREA.                                   
           MOVE IN009-QUANTITY-COMP TO WS-QTY.                                  
           MOVE WS-QTY              TO DL-QTY.                                  
           MOVE IN009-PRICE       TO DL-PRICE.                                  
           MOVE SPACES            TO DL-CUT.                                    
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *   MOVE DATA TO DETAIL LINE - WITHOUT ITEM NUMBER.              *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2015-MOVE-DET-LINE.                                                      
                                                                                
           MOVE SPACES TO DL-ITEM-NUM.                                          
           MOVE IN009-SCAN-DTE TO  PV-HOLD-DATE.                                
           MOVE PV-HOLD-MM     TO  DL-MM.                                       
           MOVE PV-HOLD-DD     TO  DL-DD.                                       
           MOVE IN009-SCAN-TIMESTAMP TO  PV-HOLD-TIME.                          
           MOVE PV-HH          TO  DL-HH.                                       
           MOVE PV-MIN         TO  DL-MIN.                                      
           MOVE PV-SS          TO  DL-SS.                                       
           MOVE '/'            TO  DL-SLASH.                                    
           MOVE ':'            TO  DL-COLON1                                    
                                   DL-COLON2.                                   
           MOVE IN009-AREA-NUMBER TO DL-AREA.                                   
           MOVE IN009-QUANTITY-COMP TO WS-QTY.                                  
           MOVE WS-QTY              TO DL-QTY.                                  
           MOVE IN009-PRICE       TO DL-PRICE.                                  
           MOVE SPACES            TO DL-CUT.                                    
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *   MOVE DATA TO DETAIL LINE.  CUTOFF FILE DATA.                 *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2020-MOVE-DET-LINE2.                                                     
                                                                                
W21732     MOVE IN019-SKU      TO  DL-SKU-NBR.                                  
           MOVE IN019-SCAN-DTE TO  PV-HOLD-DATE.                                
           MOVE PV-HOLD-MM     TO  DL-MM.                                       
           MOVE PV-HOLD-DD     TO  DL-DD.                                       
           MOVE IN019-SCAN-TIMESTAMP TO  PV-HOLD-TIME.                          
           MOVE PV-HH          TO  DL-HH.                                       
           MOVE PV-MIN         TO  DL-MIN.                                      
           MOVE PV-SS          TO  DL-SS.                                       
           MOVE '/'            TO  DL-SLASH.                                    
           MOVE ':'            TO  DL-COLON1                                    
                               DL-COLON2.                                       
           MOVE IN019-SCAN-AREA   TO DL-AREA.                                   
           MOVE IN019-TTL-PHY-COUNT TO  DL-QTY.                                 
           MOVE IN019-UNIT-PRICE  TO DL-PRICE.                                  
           MOVE PV-CUTOFF      TO  DL-CUT.                                      
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHECK FOR CUTOFF RECORD - READ NEXT IF CUTOFF RECORD WRITTEN *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2050-CHECK-CUTOFF.                                                       
                                                                                
           IF (PV-HOLD-STORE = PV-HOLD-STORE-2) AND                             
              (PV-HOLD-ITEM = PV-HOLD-ITEM-2)                                   
              PERFORM 2020-MOVE-DET-LINE2                                       
              PERFORM 2305-WRITE-DETAIL2                                        
              PERFORM 2150-READ-CUTOFF-FILE                                     
              MOVE IN019-STR-NBR   TO PV-HOLD-STORE-2                           
              MOVE IN019-DEPT-NBR  TO PV-HOLD-DEPT-2                            
              MOVE IN019-CL-NBR    TO PV-HOLD-CLASS-2                           
W21732        MOVE IN019-SKU       TO PV-HOLD-SKU-2                             
           ELSE                                                                 
              DISPLAY '**********************************'                      
              DISPLAY 'STORE ON ITEM FILE = ' PV-HOLD-STORE                     
              '       STORE ON TIMESTMP CUTOFF FILE = ' PV-HOLD-STORE-2         
              DISPLAY ' ITEM ON ITEM FILE = ' PV-HOLD-ITEM                      
                 ' ITEM  ON TIMESTMP CUTOFF FILE = ' PV-HOLD-ITEM-2             
              MOVE 'MISSING ITEM TIMESTAMP CUTOFF RECORD'                       
                  TO  PV-ABEND-MESSAGE                                          
              MOVE '2050-CHECK-CUTOFF' TO PV-PARAGRAPH-NAME                     
              PERFORM 9999-ABEND                                                
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    READ THE INP01 FILE.                                        *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2100-READ-TIMESTAMP-FILE.                                                
                                                                                
           IF PV-NOT-END-OF-INPUT2                                              
               READ TIMESTAMP-DATA-FILE                                         
                   INTO IN009-EDITED-INVENTORY-RECORD                           
                    AT END                                                      
                    SET PV-END-OF-INPUT TO TRUE                                 
               END-READ.                                                        
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    READ THE INP02 FILE.                                        *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2150-READ-CUTOFF-FILE.                                                   
                                                                                
           READ TIMESTAMP-CUTOFF-FILE                                           
               INTO IN019-ITEM-REC                                              
                AT END                                                          
                SET PV-END-OF-INPUT2 TO TRUE                                    
           END-READ.                                                            
           IF PV-FIRST-TIME = 'N'                                               
             IF PV-END-OF-INPUT2 AND PV-NOT-END-OF-INPUT                        
                  DISPLAY '**********************************'                  
                  DISPLAY 'STORE ON ITEM FILE = ' PV-HOLD-STORE                 
                  '       STORE ON TIMESTMP CUTOFF FILE = '               2     
                      PV-HOLD-STORE-2                                     2     
                  DISPLAY ' ITEM ON ITEM FILE = ' PV-HOLD-ITEM                  
                     '  ITEM ON TIMESTMP CUTOFF FILE = ' PV-HOLD-ITEM-2         
                  MOVE 'LAST ITEM TIMESTAMP CUTOFF RECORD IS MISSING'           
                      TO  PV-ABEND-MESSAGE                                      
                  MOVE '2150-READ-CUTOFF-FILE' TO PV-PARAGRAPH-NAME             
                  DISPLAY '**********************************'                  
                  PERFORM 9999-ABEND                                            
             END-IF.                                                            
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    WRITE THE DETAIL LINE.                                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2300-WRITE-DETAIL.                                                       
                                                                                
           ADD 1                      TO  PV-LINE-COUNT.                        
                                                                                
           PERFORM 5000-LOAD-PRINT-LINE-TABLE.                                  
                                                                                
      *----------------------------------------------------------------*        
      *                                                                *        
      *    WRITE THE DETAIL LINE2.                                     *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2305-WRITE-DETAIL2.                                                      
                                                                                
           ADD 3                      TO  PV-LINE-COUNT.                        
                                                                                
           PERFORM 5000-LOAD-PRINT-LINE-TABLE.                                  
                                                                                
           MOVE SPACES TO DL-DETAIL-LINE.                                       
           PERFORM 5000-LOAD-PRINT-LINE-TABLE.                                  
                                                                                
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    WRITE THE HEADER LINE.                                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
       2310-WRITE-HEADER.                                                       
                                                                                
           ADD 7                      TO  PV-LINE-COUNT.                        
                                                                                
           IF IN009-STORE-NUMBER NOT = PV-HOLD-STORE                            
              MOVE PV-HOLD-STORE      TO HL3-STORE                              
              DISPLAY 'STR ' HL3-STORE ' LAST PAGE '                            
                       PV-PAGE-COUNT                                            
           ELSE                                                                 
              MOVE IN009-STORE-NUMBER TO HL3-STORE                              
           END-IF.                                                              
                                                                                
           MOVE IN009-STORE-NUMBER TO PV-HOLD-STORE.                            
                                                                                
            ADD 1                   TO  PV-PAGE-COUNT.                          
            MOVE PV-PAGE-COUNT      TO  DP200O-PAGE-NUMBER.                     
            MOVE 10                 TO  PV-LINE-COUNT.                          
            WRITE REPORT-LINE                                                   
               FROM DP200O-STANDRD-HEADER-200-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
            WRITE REPORT-LINE                                                   
               FROM HL1-HEADING-LINE-1.                                         
                                                                                
            WRITE REPORT-LINE FROM HL2-HEADING-LINE-2                           
               AFTER ADVANCING 1.                                               
                                                                                
            MOVE SPACES TO REPORT-LINE.                                         
            WRITE REPORT-LINE.                                                  
                                                                                
            WRITE REPORT-LINE FROM HL3-HEADING-LINE-3                           
               AFTER ADVANCING 1.                                               
                                                                                
            MOVE SPACES TO REPORT-LINE                                          
            WRITE REPORT-LINE.                                                  
                                                                                
            WRITE REPORT-LINE FROM HL4-HEADING-LINE-4                           
               AFTER ADVANCING 1.                                               
                                                                                
            WRITE REPORT-LINE FROM HL5-HEADING-LINE-5                           
               AFTER ADVANCING 1.                                               
                                                                                
            MOVE SPACES TO REPORT-LINE                                          
            WRITE REPORT-LINE                                                   
               AFTER ADVANCING 2.                                               
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *  TO CREATE THE 3 COLUMNS OF DATA ON THE REPORT, RECORDS                 
      *  IN THE 2300-WRITE-DETAIL AND 2310-WRITE-DETAIL2 ROUTINES WILL          
      *  ACTUALLY BE MOVED TO ONE OF 3 RPT-COLUMN-TABLES.                       
      *                                                                         
      *  WHEN THE 3RD RPT-COLUMN-TABLE IS FULL OR WHEN A STORE NUMBER           
      *  CHANGES, THE PAGE WILL ACTUALLY BE WRITTEN IN THE 5010- ROUTINE        
      *----------------------------------------------------------------*        
                                                                                
       5000-LOAD-PRINT-LINE-TABLE.                                              
                                                                                
           ADD +1 TO SS-LINE-SUB.                                               
                                                                                
           IF (SS-LINE-SUB > PC-RPT-COL-MAX)                                    
                  AND PS-COLUMN-SW = '3'                                        
               PERFORM 2310-WRITE-HEADER                                        
               PERFORM 5010-WRITE-PRINT-LINE-TABLE                              
                   VARYING SS-RPT-SUB FROM 1 BY 1                               
                   UNTIL SS-RPT-SUB > PC-RPT-COL-MAX                            
               INITIALIZE TB-REPORT-COL1-TABLE                                  
               INITIALIZE TB-REPORT-COL2-TABLE                                  
               INITIALIZE TB-REPORT-COL3-TABLE                                  
               MOVE '1' TO PS-COLUMN-SW                                         
               MOVE +1 TO SS-LINE-SUB                                           
           END-IF                                                               
                                                                                
           IF (SS-LINE-SUB > PC-RPT-COL-MAX)                                    
               IF  PS-RPT-COL1                                                  
                   MOVE +1 TO SS-LINE-SUB                                       
                   MOVE '2' TO PS-COLUMN-SW                                     
               ELSE                                                             
                   IF  PS-RPT-COL2                                              
                       MOVE +1 TO SS-LINE-SUB                                   
                       MOVE '3' TO PS-COLUMN-SW                                 
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-RPT-COL1                                                       
               MOVE DL-DETAIL-LINE TO TB-COL1-LINE(SS-LINE-SUB)                 
           ELSE                                                                 
               IF PS-RPT-COL2                                                   
                   MOVE DL-DETAIL-LINE TO TB-COL2-LINE(SS-LINE-SUB)             
               ELSE                                                             
                   IF PS-RPT-COL3                                               
                       MOVE DL-DETAIL-LINE                                      
                           TO TB-COL3-LINE(SS-LINE-SUB)                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------*        
       5010-WRITE-PRINT-LINE-TABLE.                                             
                                                                                
           MOVE TB-COL1-LINE(SS-RPT-SUB) TO DL-DETAIL-COL1.                     
           MOVE TB-COL2-LINE(SS-RPT-SUB) TO DL-DETAIL-COL2.                     
           MOVE TB-COL3-LINE(SS-RPT-SUB) TO DL-DETAIL-COL3.                     
           WRITE REPORT-LINE FROM DL-FULL-DETAIL-LINE.                          
                                                                                
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '*********'                                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =   INKRP175'                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           DISPLAY '**'                                                         
           DISPLAY '**'                                                         
           DISPLAY '**********************************'                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
