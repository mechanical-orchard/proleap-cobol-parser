       IDENTIFICATION DIVISION.                                         00010000
      ******************************************************************00020000
                                                                        00030000
       PROGRAM-ID.   XSKUP993.                                          00040000
       AUTHOR.       KRITIVEEN KUSHAL.                                  00050000
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00060000
       DATE-WRITTEN. 08/04/2016.                                        00070031
       DATE-COMPILED.                                                   00080000
                                                                        00090000
      ******************************************************************00100000
      * THIS IS A DATA CORRECTION PROGRAM WHICH WILL UPDATE XST_SKU    *00110001
      * TABLE THAT HAS MISMATCH RECORDS IN INTEGRITY REPORT DUE TO     *00120001
      * DIFFERENCE IN THE REGN_PRIC_IND FILED.                         *00130001
      ******************************************************************00140000
      ******************************************************************00150000
      * CHANGE HISTORY:                                                 00160000
      *  WR/PBI       DATE        DESCRIPTION OF CHANGES                00170031
      *  ---------    --------    ------------------------------------  00180031
      *  CHG0147978   08/04/16    INITIAL IMPLEMENTATION.               00190031
      ******************************************************************00200000
      * CHANGE HISTORY:                                                 00201035
      *  WR/PBI       DATE        DESCRIPTION OF CHANGES                00202035
      *  ---------    --------    ------------------------------------  00203035
      *  CHG0149924   08/30/16    REMOVED WT-UM-CDE COMPARISON STEP.    00204036
      ******************************************************************00205035
       ENVIRONMENT DIVISION.                                            00210000
      ******************************************************************00220000
                                                                        00230000
       CONFIGURATION SECTION.                                           00240000
       SOURCE-COMPUTER.                        IBM-3090.                00250000
       OBJECT-COMPUTER.                        IBM-3090.                00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
       FILE-CONTROL.                                                    00290000
           SELECT SKU-ERR-FILE ASSIGN TO INP01.                         00300001
           SELECT SKU-RPT-FILE ASSIGN TO OUT01.                         00310001
                                                                        00320000
      ******************************************************************00330000
       DATA DIVISION.                                                   00340000
      ******************************************************************00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  SKU-ERR-FILE                                                 00380001
           RECORDING MODE IS F                                          00390000
           LABEL RECORDS ARE STANDARD                                   00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  SKU-ERROR-RECORD             PIC X(400).                     00420001
                                                                        00430000
                                                                        00440000
       FD  SKU-RPT-FILE                                                 00450001
           RECORDING MODE IS F                                          00460000
           LABEL RECORDS ARE STANDARD                                   00470000
           BLOCK CONTAINS 0 RECORDS.                                    00480000
       01  SKU-REPORT-RECORD             PIC X(400).                    00490001
      ******************************************************************00500000
       WORKING-STORAGE SECTION.                                         00510000
      ******************************************************************00520000
      * PROGRAM VARIABLES.                                             *00530000
      ******************************************************************00540000
       01  PV-PROGRAM-VARIABLES.                                        00550000
           05 PV-COMMIT-COUNT               PIC 9(04)  VALUE ZEROES.    00560000
           05 PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.   00570000
           05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.    00580000
           05 PV-DB2-OPERATION-ATTEMPTED    PIC X(30)  VALUE SPACES.    00590000
           05 PV-SQLCODE-DISPLAY            PIC +(8)9  VALUE ZEROES.    00600000
      ******************************************************************00610000
      * PROGRAM SWITCHES.                                              *00620000
      ******************************************************************00630000
       01  PS-PROGRAM-SWITCHES.                                         00640000
           05  PS-END-OF-SKU-FILE-SW     PIC X(01)  VALUE 'N'.          00650001
               88  PS-END-OF-SKU-FILE               VALUE 'Y'.          00660001
               88  PS-NOT-END-OF-SKU-FILE           VALUE 'N'.          00670001
                                                                        00680000
      ******************************************************************00690000
      * PROGRAM ACCUMULATORS.                                          *00700000
      ******************************************************************00710000
       01  PA-PROGRAM-ACCUMULATORS.                                     00720000
           05  PA-SKU-ROWS-READ         PIC S9(09) COMP-3               00730001
                                                       VALUE ZEROES.    00740000
           05  PA-ERROR-RECORD-WRITTEN      PIC S9(09) COMP-3           00750000
                                                       VALUE ZEROES.    00760000
           05  PA-SKU-ROW-UPDATED        PIC S9(09) COMP-3              00770001
                                                       VALUE ZEROES.    00780000
           05  PA-COMMIT-TAKEN              PIC S9(09) COMP-3           00790000
                                                       VALUE ZEROES.    00800000
       01  ABEND-CODE                       PIC S9(04) COMP SYNC        00810000
                                                       VALUE 0.         00820000
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.     00830000
           88  ABEND-4002-CC-INVALID-ERR               VALUE +4002.     00840000
                                                                        00850000
      ******************************************************************00851022
      **PROGRAM CALCULATIONS                                            00852022
      ******************************************************************00853022
       01  PC-PROGRAM-CALCULATIONS.                                     00854022
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00855022
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00856022
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00857022
      ***************************************************************** 00860000
      * PROGRAM CONSTANTS                                               00870000
      ***************************************************************** 00880000
       01  PC-PROGRAM-CONSTANTS.                                        00890000
           05  PC-CURRENT-PROGRAM-NAME       PIC X(08)                  00900000
                                                   VALUE 'XSKUP993'.    00910000
           05  PC-MAX-RETRIES                PIC S9(04) VALUE +5 COMP.  00920000
           05  PC-COMMIT-MAX                 PIC 9(09) VALUE   999.     00930000
                                                                        00940000
      ***************************************************************** 00950020
      * XST_VND_STYL TABLE DCLGEN.                                      00960020
      ***************************************************************** 00970020
           EXEC SQL                                                     00980020
               INCLUDE XST00010                                         00990020
           END-EXEC.                                                    01000020
                                                                        01010020
      ***************************************************************** 01020000
      * XS DOMAIN SKU MISMATCH ERROR FILE LAYOUT.                       01030001
      ***************************************************************** 01040000
           COPY XSRD012.                                                01050001
      ***************************************************************** 01060000
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                01070000
      ***************************************************************** 01080000
           EXEC SQL                                                     01090000
               INCLUDE SQLCA                                            01100014
           END-EXEC.                                                    01110000
                                                                        01120000
      ***************************************************************** 01130000
      * COMMUNICATION WORK AREA.                                        01140000
      ***************************************************************** 01150000
           COPY SQLCA2.                                                 01160000
                                                                        01170000
      ***************************************************************** 01180000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.        01190000
      ***************************************************************** 01200000
           COPY DPWS004.                                                01210000
                                                                        01220000
      ***************************************************************** 01230000
       LINKAGE SECTION.                                                 01240000
                                                                        01250000
       PROCEDURE DIVISION.                                              01260000
                                                                        01270000
      ******************************************************************01280000
      * 0000-MAINLINE-PARA.                                            *01290000
      ******************************************************************01300000
       0000-MAINLINE-PARA.                                              01310000
                                                                        01320000
           PERFORM 1000-INITIALIZE.                                     01330000
                                                                        01340000
           PERFORM 2000-READ-SKU-ERROR-FILE.                            01350001
                                                                        01360000
           PERFORM 3000-PROCESS-RECORDS                                 01370000
             UNTIL PS-END-OF-SKU-FILE.                                  01380001
                                                                        01390000
           PERFORM 4000-EOJ-ROUTINE.                                    01400000
                                                                        01410000
           STOP RUN.                                                    01420000
                                                                        01430000
      ******************************************************************01440000
      * 1000-INITIALIZE.                                               *01450000
      ******************************************************************01460021
       1000-INITIALIZE.                                                 01470000
           INITIALIZE DCLXST00010                                       01480021
                                                                        01490000
           OPEN INPUT  SKU-ERR-FILE.                                    01500001
           OPEN OUTPUT SKU-RPT-FILE.                                    01510001
                                                                        01520000
      ******************************************************************01530000
      * 2000-READ-SKU-ERROR-FILE.                                       01540001
      ******************************************************************01550000
       2000-READ-SKU-ERROR-FILE.                                        01560001
                                                                        01570000
           READ SKU-ERR-FILE                                            01580001
           INTO XSRD012-SKU-ERR-RECORD                                  01590001
             AT END                                                     01600000
               SET PS-END-OF-SKU-FILE TO TRUE                           01610001
             NOT AT END                                                 01620000
               ADD +1                    TO  PA-SKU-ROWS-READ           01630001
           END-READ.                                                    01640000
                                                                        01650000
      ******************************************************************01660000
      * 3000-PROCESS-RECORDS                                            01670000
      ******************************************************************01680000
       3000-PROCESS-RECORDS.                                            01690000
           INITIALIZE DCLXST00010                                       01691021
                                                                        01700000
           IF   XSRD012-XS-INTRL-UPC-ID      = XSRD012-VSM-INTRL-UPC-ID 01730001
           AND XSRD012-XS-STYL-ID            = XSRD012-VSM-STYL-ID      01750001
           AND XSRD012-XS-SKU-NBR            = XSRD012-VSM-SKU-NBR      01760001
           AND XSRD012-XS-SKU-DESC           = XSRD012-VSM-SKU-DESC     01770001
           AND XSRD012-XS-KOHLS-SIZE-CDE     =                          01780001
                                      XSRD012-VSM-KOHLS-SIZE-CDE        01781001
           AND XSRD012-XS-SIZE-DESC          = XSRD012-VSM-SIZE-DESC    01790001
           AND XSRD012-XS-NRF-CLOR-CDE       = XSRD012-VSM-NRF-CLOR-CDE 01800001
           AND XSRD012-XS-NRF-CLOR-DESC      = XSRD012-XS-NRF-CLOR-DESC 01810001
           AND XSRD012-XS-CSTM-CLOR-DESC     =                          01820001
                                      XSRD012-VSM-CSTM-CLOR-DESC        01821001
           AND XSRD012-XS-TX-PRD-CDE-DESC    =                          01830001
                                      XSRD012-VSM-TX-PRD-CDE-DESC       01830100
           AND XSRD012-XS-E-BUS-ITM-IND      = XSRD012-VSM-E-BUS-ITM-IND01831001
           AND XSRD012-XS-E-BUS-PRD-GRP-NBR  =                          01832001
                                      XSRD012-VSM-E-BUS-PRD-GRP-NBR     01832100
C49924*    AND XSRD012-XS-WT-UM-CDE          = XSRD012-VSM-WT-UM-CDE    01833035
           AND XSRD012-XS-WT-UM-QTY          = XSRD012-VSM-WT-UM-QTY    01834001
           AND XSRD012-XS-SIZE-UM-CDE        = XSRD012-VSM-SIZE-UM-CDE  01835001
           AND XSRD012-VSM-WD-UM-QTY         = XSRD012-VSM-WD-UM-QTY    01836001
           AND XSRD012-XS-HT-UM-QTY          = XSRD012-VSM-HT-UM-QTY    01840001
           AND XSRD012-XS-DPTH-UM-QTY        = XSRD012-VSM-DPTH-UM-QTY  01841001
           AND XSRD012-XS-CUSTFACG-CLOR-DESC =                          01842001
                                         XSRD012-VSM-CUSTFACG-CLOR-DESC 01842101
           AND XSRD012-XS-POG-IND            = XSRD012-VSM-POG-IND      01843001
           AND XSRD012-XS-GP-CDE             = XSRD012-VSM-GP-CDE       01844001
           AND XSRD012-XS-REGN-PRIC-IND                                 01850001
                            NOT EQUAL XSRD012-VSM-REGN-PRIC-IND         01860000
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01860122
      *     COMP FIELDS.                                                01860222
           MOVE XSRD012-VSM-INTRL-UPC-ID   TO PC-10-DIGIT               01860323
           MOVE PC-10-DIGIT                TO PC-18-DIGIT               01860523
           MOVE PC-18-DIGIT                TO PC-09-DIGIT               01860723
           MOVE PC-09-DIGIT                TO XST00010-INTR-UPC-ID      01860923
                                                                        01861122
           MOVE XSRD012-VSM-REGN-PRIC-IND  TO XST00010-REGN-PRIC-IND    01861222
                                                                        01862025
               PERFORM 3100-UPDATE-XST-SKU                              01900001
           ELSE                                                         01920001
               PERFORM 3200-WRITE-ERROR-RECORD                          01930001
           END-IF.                                                      01950001
               PERFORM 2000-READ-SKU-ERROR-FILE.                        01960001
      ******************************************************************01970000
      * 3100-UPDATE-XST-SKU.                                            01980001
      ******************************************************************01990000
       3100-UPDATE-XST-SKU.                                             02000001
               EXEC SQL                                                 02020000
                   UPDATE XST_SKU                                       02030001
                      SET REGN_PRIC_IND = :XST00010-REGN-PRIC-IND       02040022
                     ,LAST_UPD_TMST     = CURRENT TIMESTAMP             02050001
                   WHERE INTR_UPC_ID    = :XST00010-INTR-UPC-ID         02060022
               END-EXEC.                                                02070010
             EVALUATE TRUE                                              02080000
               WHEN SQLCODE = ZERO                                      02090000
                 DISPLAY 'INTRL-UPC-ID UPDATED:' PC-10-DIGIT            02091034
                 ADD +1  TO PA-SKU-ROW-UPDATED                          02100001
                 ADD +1         TO PV-COMMIT-COUNT                      02110000
                 IF PV-COMMIT-COUNT > PC-COMMIT-MAX                     02120000
                    MOVE 0 TO PV-COMMIT-COUNT                           02130000
                    PERFORM 3500-UPDATE-COMMIT                          02140000
                 END-IF                                                 02150000
               WHEN OTHER                                               02160000
                  CONTINUE                                              02162024
             END-EVALUATE.                                              02180000
                                                                        02190000
      ***************************************************************** 02200000
      * 3200-WRITE-ERROR-RECORD.                                      * 02210000
      **WRITE RECORDS HAVING OTHER MISMATCHES IN ERROR FILE           * 02220000
      ***************************************************************** 02230000
       3200-WRITE-ERROR-RECORD.                                         02240000
                                                                        02250000
           WRITE SKU-REPORT-RECORD FROM XSRD012-SKU-ERR-RECORD.         02260001
                                                                        02270000
           ADD +1                       TO PA-ERROR-RECORD-WRITTEN.     02280000
                                                                        02290000
       EJECT.                                                           02300000
      ******************************************************************02310000
      * 3500-UPDATE-COMMIT.                                             02320000
      ******************************************************************02330000
       3500-UPDATE-COMMIT.                                              02340000
           PERFORM                                                      02360000
               WITH TEST AFTER                                          02370000
               VARYING PV-SQL-SUB                                       02380000
               FROM 1 BY 1                                              02390000
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                 02400000
                         OR  SQLCODE = 0)                               02410000
                                                                        02420000
               EXEC SQL                                                 02450000
                   COMMIT                                               02460000
               END-EXEC                                                 02470000
                                                                        02480000
               EVALUATE TRUE                                            02490000
                   WHEN SQLCODE = -904                                  02500000
                   WHEN SQLCODE = -911                                  02510000
                   WHEN SQLCODE = -913                                  02520000
                   WHEN SQLCODE = 0                                     02530000
                       ADD +1 TO PA-COMMIT-TAKEN                        02540000
                       CONTINUE                                         02550000
                   WHEN SQLWARN NOT EQUAL SPACES                        02560000
                   WHEN SQLCODE NOT EQUAL ZERO                          02570000
                       MOVE '3500-UPDATE-COMMIT'                        02580000
                                     TO  PV-PARAGRAPH-NAME              02590000
                       MOVE 'COMMIT FAILURE'                            02600000
                                    TO  PV-DB2-OPERATION-ATTEMPTED      02610000
                       PERFORM 9100-SQL-ABEND                           02620000
               END-EVALUATE                                             02630000
           END-PERFORM.                                                 02640000
                                                                        02650000
            IF PV-SQL-SUB GREATER PC-MAX-RETRIES                        02660000
               MOVE '3500-UPDATE-COMMIT'                                02670000
                                    TO  PV-PARAGRAPH-NAME               02680000
               MOVE 'COMMIT RETRIES EXCEEDED'                           02690000
                                    TO  PV-DB2-OPERATION-ATTEMPTED      02700000
               PERFORM 9100-SQL-ABEND                                   02710000
            END-IF.                                                     02720000
                                                                        02730000
      ***************************************************************** 02740000
      * 4000-EOJ-ROUTINE.                                               02750000
      ******************************************************************02760000
       4000-EOJ-ROUTINE.                                                02770000
           PERFORM 3500-UPDATE-COMMIT.                                  02790000
                                                                        02800000
           CLOSE SKU-ERR-FILE.                                          02810001
           PERFORM 9005-DISPLAY-TOTALS.                                 02820000
                                                                        02830000
      ******************************************************************02840000
      * 9100-SQL-ABEND.                                                 02850000
      ******************************************************************02860000
       9100-SQL-ABEND.                                                  02870000
                                                                        02880000
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                          02890000
           DISPLAY ' '.                                                 02900000
           DISPLAY '9100-SQL-ABEND'.                                    02910000
           DISPLAY '** ABEND     **'.                                   02920000
           DISPLAY '** PROGRAM   ** = XSKUP993'.                        02930001
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02940000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.     02950000
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.             02960000
                                                                        02970000
           COPY DPPD004.                                                02980000
                                                                        02990000
           MOVE +4013 TO ABEND-CODE.                                    03000000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03010000
                                                                        03020000
      ******************************************************************03030000
      * 9005-DISPLAY-TOTALS.                                            03040000
      ******************************************************************03050000
       9005-DISPLAY-TOTALS.                                             03060000
           DISPLAY '******** XSKUP993 TOTALS *********'.                03070031
           DISPLAY ' SKU RECS READ    : '     PA-SKU-ROWS-READ.         03080001
           DISPLAY ' SKU ROWS UPDATED : '     PA-SKU-ROW-UPDATED.       03090001
           DISPLAY ' SKU ROWS WRITTEN : '     PA-ERROR-RECORD-WRITTEN.  03100001
           DISPLAY ' NUMBER OF COMMIT TAKN: ' PA-COMMIT-TAKEN.          03110000
           DISPLAY '***** END OF XSKUP993 TOTALS *****'.                03120001
           DISPLAY ' '.                                                 03130000
