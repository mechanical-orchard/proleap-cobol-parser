00001 *----------------------------------------------------------------*11/15/95
00002  IDENTIFICATION DIVISION.                                         APKCS336
00003 *----------------------------------------------------------------*   LV009
00004  PROGRAM-ID.    TRKCS180.                                         APKCS336
00005  AUTHOR.        NIRMAL KUMAR.                                     APKCS336
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          APKCS336
00007  DATE-WRITTEN.  03/10/98.                                            CL**2
00008  DATE-COMPILED.                                                   APKCS336
00009                                                                   APKCS336
00010 *----------------------------------------------------------------*APKCS336
00011 *    Y180 - IMPORT VESSEL CRITERIA SPECIFICATION                 *   CL**2
00012 *                                                                *APKCS336
00013 *    THIS PROGRAM ALLOWS THE USER ENTER A VESSEL NAME AND        *   CL**2
00014 *    OPTIONALLY A VOYAGE NUMBER TO SPECIFY THE TRIP-ID FOR       *   CL**2
00015 *    REQUESTED VESSEL.                                           *   CL**2
00016 *                                                                *APKCS336
00021 *    ALL EDITS ARE DONE IN THE SUBSEQUENT PROGRAMS.              *APKCS336
00023 *----------------------------------------------------------------*APKCS336
CICS  * CHANGED  1/12/2023    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
00027                                                                   APKCS336
00028  ENVIRONMENT DIVISION.                                            APKCS336
00029                                                                   APKCS336
00030  DATA DIVISION.                                                   APKCS336
00031                                                                   APKCS336
00032  WORKING-STORAGE SECTION.                                         APKCS336
00033                                                                   APKCS336
00034  01  PC-PROGRAM-CONSTANTS.                                        APKCS336
00035      05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR180A  '. APKCS336
00036      05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM180 '. APKCS336
00037      05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS180'. APKCS336
00039      05  PC-TSYMSG-NUMBERS.                                       APKCS336
00040          10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.      APKCS336
00041                                                                   APKCS336
00042  01  PS-PROGRAM-SWITCHES.                                         APKCS336
00043      05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.        APKCS336
00044          88  PS-ERROR                          VALUE  'Y'.        APKCS336
00045          88  PS-NO-ERROR                       VALUE  'N'.        APKCS336
00041                                                                   APKCS336
00046  01  PM-PROGRAM-MESSAGES.                                         APKCS336
00046      05  PM-ABEND-INFO-A.                                         APKCS336
00046          10  FILLER                PIC X(15)   VALUE              APKCS336
00046                                    'DP020-SRC-AID: '.             APKCS336
00046          10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.      APKCS336
00046          10  FILLER                PIC X(19)   VALUE              APKCS336
00046                                    '. DP020-FK-ACTION: '.         APKCS336
00046          10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.      APKCS336
00046          10  FILLER                PIC X(22)   VALUE              APKCS336
00046                                    '. DP020-FK-DISPN-IND: '.      APKCS336
00046          10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.      APKCS336
00046          10  FILLER                PIC X(01)   VALUE '.'.         APKCS336
00046          10  FILLER                PIC X(19)   VALUE SPACE.       APKCS336
00046                                                                   APKCS336
00047 *----------------------------------------------------------------*APKCS336
00048 *    MAP LAYOUT                                                  *APKCS336
00049 *----------------------------------------------------------------*APKCS336
00050                                                                   APKCS336
00051      COPY TRKM180.                                                APKCS336
00052                                                                   APKCS336
00064  EJECT                                                            APKCS336
00053 *----------------------------------------------------------------*APKCS336
00054 *    ATTRIBUTE SETTINGS COPYBOOK.                                *APKCS336
00055 *----------------------------------------------------------------*APKCS336
00056                                                                   APKCS336
00057      COPY DPWS015.                                                APKCS336
00058                                                                   APKCS336
00064  EJECT                                                            APKCS336
00059 *----------------------------------------------------------------*APKCS336
00060 *    FUNCTION KEYS COPYBOOK                                      *APKCS336
00061 *----------------------------------------------------------------*APKCS336
00062                                                                   APKCS336
00063      COPY DPWS016.                                                APKCS336
00080                                                                   APKCS336
00064  EJECT                                                            APKCS336
00071 *----------------------------------------------------------------*APKCS336
00072 *  THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS             *APKCS336
00073 *  ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                     *APKCS336
00074 *----------------------------------------------------------------*APKCS336
00080                                                                   APKCS336
00075      COPY DPWS030.                                                APKCS336
CICS       COPY DPWS930.                                                APKCS336
00076  EJECT                                                            APKCS336
00077 *---------------------------------------------------------------* APKCS336
00078 *    STANDARD COMMAREA.                                         * APKCS336
00079 *---------------------------------------------------------------* APKCS336
00080                                                                   APKCS336
00081      COPY DPWS020.                                                APKCS336
00082      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                APKCS336
00083                                                                   APKCS336
00100  EJECT                                                            APKCS336
00084 *---------------------------------------------------------------* APKCS336
00085 *    INTER-APPLICATION COMMAREA.                                * APKCS336
00086 *---------------------------------------------------------------* APKCS336
00087                                                                   APKCS336
00088          10  INTER-APPL-COMM-AREA PIC X(200).                     APKCS336
00089      COPY TRWS180.                                                   CL**8
00100                                                                   APKCS336
00100  EJECT                                                            APKCS336
00090 *---------------------------------------------------------------* APKCS336
00091 *    SPECIFIC COMMMAREA FOR TRKCS180.                           * APKCS336
00092 *---------------------------------------------------------------* APKCS336
00093          10  ASC-SPECIFIC-COMMAREA.                               APKCS336
00094              15  ASC-VSL-NAME                PIC  X(25).             CL**2
00097              15  ASC-VOY-NBR                 PIC  X(10).             CL**2
00097          10  FILLER                          PIC  X(2830).           CL**2
00100                                                                   APKCS336
00100  EJECT                                                            APKCS336
00101 *----------------------------------------------------------------*APKCS336
00102 *    ABEND PROCESSING COPYBOOK.                                  *APKCS336
00103 *----------------------------------------------------------------*APKCS336
00104                                                                   APKCS336
00105      COPY DPWS013.                                                APKCS336
00106                                                                   APKCS336
00107  EJECT                                                            APKCS336
00117 *---------------------------------------------------------------* APKCS336
00108  LINKAGE SECTION.                                                 APKCS336
00117 *---------------------------------------------------------------* APKCS336
00109                                                                   APKCS336
00110  01  DFHCOMMAREA.                                                 APKCS336
00111      05  FILLER                         OCCURS  1 TO 4072 TIMES   APKCS336
00112                                         DEPENDING ON EIBCALEN.    APKCS336
00113          10  FILLER                     PIC X.                    APKCS336
00114                                                                   APKCS336
00117 *---------------------------------------------------------------* APKCS336
00115  PROCEDURE DIVISION.                                              APKCS336
00117 *---------------------------------------------------------------* APKCS336
00118 *  THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM.  * APKCS336
00119 *  THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API   * APKCS336
00120 *  MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.             * APKCS336
00121 *                                                               * APKCS336
00122 *  THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE   * APKCS336
00123 *  LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.        * APKCS336
00124 *---------------------------------------------------------------* APKCS336
00126  0000-MAIN-MODULE.                                                APKCS336
00127                                                                   APKCS336
00128      INITIALIZE DP030-CICS-API-FIELDS.                            APKCS336
00129      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       APKCS336
00130      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          APKCS336
00131      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         APKCS336
00132      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       APKCS336
00133      MOVE LENGTH OF TR180AI        TO DP030-MAP-LENGTH (1).       APKCS336
00134      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     APKCS336
00136      PERFORM 0001-CALL-CICS-ARCH-API.                             APKCS336
00137                                                                   APKCS336
00138      PERFORM 1000-CONTROL-PROCESSING                              APKCS336
00139                                                                   APKCS336
00140      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     APKCS336
00141      PERFORM 0001-CALL-CICS-ARCH-API.                             APKCS336
00142                                                                   APKCS336
00143 *---------------------------------------------------------------* APKCS336
00144 *    CALL THE ARCHITECTURE API.                                 * APKCS336
00145 *---------------------------------------------------------------* APKCS336
00147  0001-CALL-CICS-ARCH-API.                                         APKCS336
00148                                                                   APKCS336
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                      APKCS336
00150                                     DFHCOMMAREA                           
00151                                     DP030-CICS-API-FIELDS                 
00152                                     DP020-STANDARD-COMMAREA               
00153                                     TR180AI.                              
00154                                                                   APKCS336
00155      IF  DP030-RC-CALL-SUCCESSFUL OR                              APKCS336
00156          DP030-RC-APPL-ERROR                                      APKCS336
00157          CONTINUE                                                 APKCS336
00158      ELSE                                                         APKCS336
00159          SET DP013-NO-ROLLBACK                                    APKCS336
00160              DP013-XCTL-DISPLAY-RESTART                           APKCS336
00161              DP013-CICS-ABEND      TO TRUE                        APKCS336
00162          MOVE 'BEFORE 0000-MAIN-MODULE'                           APKCS336
00163                                    TO DP013-PARAGRAPH             APKCS336
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE OAPKCS336
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)      APKCS336
00166          MOVE DP030-RETURN-CODE                                   APKCS336
00167                                    TO DP013-MESSAGE-TEXT (3)      APKCS336
00168          PERFORM DP013-0000-PROCESS-ABEND                         APKCS336
00169      END-IF.                                                      APKCS336
00170                                                                   APKCS336
00170  EJECT                                                            APKCS336
00171 *----------------------------------------------------------------*APKCS336
00172 *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *APKCS336
00173 *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *APKCS336
00174 *----------------------------------------------------------------*APKCS336
00176  1000-CONTROL-PROCESSING.                                         APKCS336
00177                                                                   APKCS336
00178      EVALUATE TRUE                                                APKCS336
00183          WHEN DP020-NEXT-ACT-INITIAL                              APKCS336
00184              INITIALIZE ASC-SPECIFIC-COMMAREA                     APKCS336
00185              PERFORM 4000-BUILD-INITIAL-PANEL                     APKCS336
00186                                                                   APKCS336
00187          WHEN DP020-NEXT-ACT-READ-MAP                             APKCS336
00188              PERFORM 2000-FKEY-PROCESSING                         APKCS336
00189                                                                   APKCS336
00190          WHEN DP020-NEXT-ACT-RETURN                               APKCS336
00191              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 APKCS336
00192              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   APKCS336
00193                                                                   APKCS336
00194          WHEN OTHER                                               APKCS336
00195              SET DP013-LOGIC-ABEND                                APKCS336
00196                  DP013-NO-ROLLBACK TO TRUE                        APKCS336
00197              MOVE '1000-CONTROL-PROCESSING'                       APKCS336
00198                                    TO  DP013-PARAGRAPH            APKCS336
00199              MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   APKCS336
00200                                    TO  DP013-MESSAGE-TEXT(1)      APKCS336
00201              MOVE DP020-NEXT-APPL-ACTIVITY                        APKCS336
00202                                    TO DP013-MESSAGE-TEXT(2)       APKCS336
00203              PERFORM DP013-0000-PROCESS-ABEND                     APKCS336
00204      END-EVALUATE.                                                APKCS336
00205                                                                   APKCS336
00170  EJECT                                                            APKCS336
00206 *----------------------------------------------------------------*APKCS336
00207 * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*APKCS336
00208 * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*APKCS336
00209 * EDITS.                                                         *APKCS336
00210 *----------------------------------------------------------------*APKCS336
00212  2000-FKEY-PROCESSING.                                            APKCS336
00213                                                                   APKCS336
00214      EVALUATE TRUE                                                APKCS336
00215                                                                   APKCS336
00216          WHEN DP020-SRC-AID = DP016-CLEAR                         APKCS336
00217              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   APKCS336
00218              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 APKCS336
00219                                                                   APKCS336
00220          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    APKCS336
00221              INITIALIZE ASC-SPECIFIC-COMMAREA                     APKCS336
00222              PERFORM 4000-BUILD-INITIAL-PANEL                     APKCS336
00223              PERFORM 3200-RESET-ATTRIBUTES                        APKCS336
00224                                                                   APKCS336
00225          WHEN OTHER                                               APKCS336
00226              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 APKCS336
00227              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   APKCS336
00228              IF  PS-NO-ERROR                                      APKCS336
00229                  SET DP030-CONTINUE-GO TO TRUE                    APKCS336
00230                  PERFORM 2100-CHECK-FUNCTION-KEY                  APKCS336
00231              ELSE                                                 APKCS336
00232                  SET DP030-OVERRIDE-GO TO TRUE                    APKCS336
00233              END-IF                                               APKCS336
00234      END-EVALUATE.                                                APKCS336
00235                                                                   APKCS336
00170  EJECT                                                            APKCS336
00236 *----------------------------------------------------------------*APKCS336
00237 *  ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED      *APKCS336
00238 *  FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED  *APKCS336
00239 *  FROM THE CICS ARCHITECTURE API.                               *APKCS336
00240 *----------------------------------------------------------------*APKCS336
00242  2100-CHECK-FUNCTION-KEY.                                         APKCS336
00243                                                                   APKCS336
00244      EVALUATE TRUE                                                APKCS336
00245          WHEN DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                 APKCS336
00245          WHEN DP020-FK-DISPN-GO (DP020-SRC-AID)                   APKCS336
00246              PERFORM 2110-PREPARE-INTER-APPL-COMM                 APKCS336
00247                                                                   APKCS336
00248          WHEN DP020-SRC-AID = DP016-ENTER                         APKCS336
00249              CONTINUE                                             APKCS336
00250                                                                   APKCS336
00251          WHEN OTHER                                               APKCS336
00252              SET DP013-NO-ROLLBACK                                APKCS336
00253                  DP013-XCTL-DISPLAY-RESTART                       APKCS336
00254                  DP013-CICS-ABEND  TO TRUE                        APKCS336
00255              MOVE '2100-CHECK-FUNCTION-KEY'                       APKCS336
00256                                    TO DP013-PARAGRAPH             APKCS336
00257              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      APKCS336
00258                                    TO DP013-MESSAGE-TEXT (1)      APKCS336
00259              MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:' APKCS336
00260                                    TO DP013-MESSAGE-TEXT (2)      APKCS336
00261              MOVE DP020-SRC-AID    TO PM-A-SRC-AID                APKCS336
00259              MOVE DP020-FK-ACTION (DP020-SRC-AID)                 APKCS336
00260                                    TO PM-A-FK-ACTION              APKCS336
00259              MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)              APKCS336
00260                                    TO PM-A-FK-DISPN               APKCS336
00261              MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)      APKCS336
00259              MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'        APKCS336
00260                                    TO DP013-MESSAGE-TEXT (4)      APKCS336
00260              PERFORM DP013-0000-PROCESS-ABEND                     APKCS336
00261      END-EVALUATE.                                                APKCS336
00261                                                                   APKCS336
00170  EJECT                                                            APKCS336
00262 *----------------------------------------------------------------*APKCS336
00263 *  MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE    *APKCS336
00264 *  INTER-APPLICATION COMMAREA.                                   *APKCS336
00265 *----------------------------------------------------------------*APKCS336
00267  2110-PREPARE-INTER-APPL-COMM.                                    APKCS336
00268                                                                   APKCS336
00269      INITIALIZE TR180-INTER-APPL-COMMAREA.                           CL**2
00270      MOVE ASC-VSL-NAME             TO TR180-VSL-NAME.                CL**6
00271      MOVE ASC-VOY-NBR              TO TR180-VOY-NBR.                 CL**6
00268                                                                   APKCS336
00272  EJECT                                                            APKCS336
00273 *----------------------------------------------------------------*APKCS336
00274 * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*APKCS336
00275 * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *APKCS336
00276 * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *APKCS336
00277 *----------------------------------------------------------------*APKCS336
00279  2200-MOVE-SCREEN-TO-COMMAREA.                                    APKCS336
00280                                                                   APKCS336
00281      IF  AVSNML > ZERO                                            APKCS336
00282          MOVE AVSNMI             TO ASC-VSL-NAME                     CL**2
00283      ELSE                                                         APKCS336
00284          IF  AVSNMF = DP015-ERASE-EOF                             APKCS336
00285              MOVE SPACE           TO ASC-VSL-NAME                    CL**2
00286          END-IF                                                   APKCS336
00287      END-IF.                                                      APKCS336
00288                                                                   APKCS336
00289      IF  AVYNOL > ZERO                                               CL**2
00290          MOVE AVYNOI            TO ASC-VOY-NBR                       CL**2
00291      ELSE                                                            CL**2
00292          IF  AVYNOF = DP015-ERASE-EOF                                CL**2
00293              MOVE SPACE           TO ASC-VOY-NBR                     CL**2
00294          END-IF                                                      CL**2
00295      END-IF.                                                         CL**2
00296                                                                      CL**2
00272  EJECT                                                            APKCS336
00297 *----------------------------------------------------------------*APKCS336
00298 * ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *APKCS336
00299 * CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *APKCS336
00300 * A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*APKCS336
00301 * IN THE DESTINATION PROGRAM, NOT HERE.                          *APKCS336
00306 *----------------------------------------------------------------*APKCS336
00308  3000-EDIT-DATA-IN-COMMAREA.                                      APKCS336
00309                                                                   APKCS336
00310      PERFORM 3200-RESET-ATTRIBUTES.                               APKCS336
00311 *--------TSYMSG-007-REQUIRED ENTRY----                               CL**2
00312      IF  ASC-VSL-NAME = SPACES AND                                   CL**2
00290          ASC-VOY-NBR = SPACES                                        CL**2
00319          SET PS-ERROR                                             APKCS336
00320              DP020-MSG-FATAL   TO TRUE                            APKCS336
00322          MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                APKCS336
00327          MOVE -1               TO AVSNML                             CL**2
00328          SET DP030-SET-CURSOR-APPL-1 TO TRUE                      APKCS336
00330      END-IF.                                                      APKCS336
00331                                                                      CL**2
00331      IF PS-NO-ERROR                                                  CL**2
00331          MOVE -1               TO AVSNML                             CL**2
00331      SET DP030-SET-CURSOR-APPL-1                                     CL**2
00331                            TO TRUE                                   CL**2
00331      END-IF.                                                         CL**2
00331                                                                      CL**2
00272  EJECT                                                            APKCS336
00352 *----------------------------------------------------------------*APKCS336
00353 *  MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE  *APKCS336
00354 *  MAP.                                                          *APKCS336
00355 *----------------------------------------------------------------*APKCS336
00357  3200-RESET-ATTRIBUTES.                                           APKCS336
00358                                                                   APKCS336
00359      MOVE DP015-UNP-ALP-NOR-OFF    TO AVSNMA                         CL**2
00360                                       AVYNOA.                        CL**2
00361                                                                      CL**2
00362      MOVE DP015-GREEN              TO AVSNMC                         CL**2
00363                                       AVYNOC.                        CL**2
00364                                                                      CL**2
00365      MOVE DP015-UNDERLINE          TO AVSNMH                         CL**2
00366                                       AVYNOH.                        CL**2
00367                                                                   APKCS336
00272  EJECT                                                            APKCS336
00368 *----------------------------------------------------------------*APKCS336
00369 *  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *APKCS336
00370 *  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *APKCS336
00371 *  IT HERE.                                                      *APKCS336
00372 *----------------------------------------------------------------*APKCS336
00374  4000-BUILD-INITIAL-PANEL.                                        APKCS336
00375                                                                   APKCS336
00375      MOVE SPACES                   TO AVSNMO                      APKCS336
00379                                       AVYNOO.                     APKCS336
00377      SET DP030-SET-CURSOR-APPL-1   TO TRUE.                          CL**2
00378      MOVE -1                       TO AVSNML.                        CL**2
00379                                                                   APKCS336
00272  EJECT                                                            APKCS336
00380 *----------------------------------------------------------------*APKCS336
00381 *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *APKCS336
00382 *----------------------------------------------------------------*APKCS336
00384  4400-MOVE-COMMAREA-TO-SCREEN.                                    APKCS336
00385                                                                   APKCS336
00386      MOVE ASC-VSL-NAME             TO AVSNMO.                        CL**2
00387      MOVE ASC-VOY-NBR              TO AVYNOO.                        CL**2
00388      MOVE -1                       TO AVSNML.                        CL**9
00389      SET DP030-SET-CURSOR-APPL-1   TO TRUE.                          CL**9
00390                                                                   APKCS336
00390  EJECT                                                            APKCS336
00391 *----------------------------------------------------------------*APKCS336
00392 *    ABEND PROCESSOR MODULE                                      *APKCS336
00393 *----------------------------------------------------------------*APKCS336
00394                                                                   APKCS336
00395      COPY DPPD013.                                                APKCS336
