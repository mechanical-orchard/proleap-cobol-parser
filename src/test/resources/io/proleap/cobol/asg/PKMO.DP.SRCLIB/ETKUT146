       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT146.                                       
       AUTHOR.                  SUE PARI.                                       
       DATE-WRITTEN.            05/01/2002.                                     
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM ADDS ENT-ID'S TO THE HEADER RECORDS ON THE STORE  *        
      * ADDRESS CHANGE FILE AND THE STORE/DC RELATIONSHIP EXTRACT FILE *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD                PIC X(60).                               
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 60 CHARACTERS                                        
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD               PIC X(60).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           COPY ETRD032.                                                        
                                                                                
       01  WS-ENT-ID                       PIC 9(9)    VALUE ZERO.              
                                                                                
       01  WS-TEST-ENT-ID.                                                      
           05  WS-ENT-ID-NUMBER            PIC 9(9)    VALUE ZERO.              
           05  WS-ENT-ID-TEST-FLAG         PIC X(1)    VALUE 'T'.               
                                                                                
       01  WS-PROD-ENT-ID                  PIC 9(10)   VALUE ZERO.              
                                                                                
       01  WS-PREV-INCHG-ID-DESC           PIC X(2)    VALUE ZERO.              
       01  WS-PREV-INCHG-ID                PIC X(15)   VALUE ZERO.              
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-INPUT-FILE-SW        PIC X(1)    VALUE 'N'.               
               88  END-OF-INPUT-FILE                   VALUE 'Y'.               
           05  END-OF-ALLVND-SW            PIC X(1)    VALUE 'N'.               
               88  END-OF-ALLVND                       VALUE 'Y'.               
                                                                                
       01  PV-PARAGRAPH-NAME               PIC X(30).                           
       01  PV-DB2-OPER-ATTEMPTED           PIC X(30).                           
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
                                                                                
      ******************************************************************        
      *                SQL ABEND ROUTINE WORKING STORAGE               *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *                      TRADING PARTNER TABLE                     *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *              TRADING PARTNER TRANSACTION SET TABLE             *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *              TRADING PARTNER MULTIPLE ENT-ID TABLE             *        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * RETRIEVE THE ENT-ID'S FOR ALL VENDORS WHO ARE ACTIVELY SET UP  *        
      * TO RECEIVE PRODUCTION 816 DATA.                                *        
      ******************************************************************        
           EXEC SQL                                                             
              DECLARE ALLVND_CURSOR CURSOR FOR                                  
              SELECT  A.ENT_ID,                                                 
                      A.VEND_INCHG_ID,                                          
                      A.VEND_INCHG_ID_DESC,                                     
                      B.PROD_TEST_IND                                           
              FROM    TTRDPRT A,                                                
                      TTRDTRN B                                                 
              WHERE   A.INACTV_DTE = '9999-09-09'                               
                AND   B.TP_DKEY = A.TP_DKEY                                     
                AND   B.TRAN_SET_CDE = '816'                                    
                AND   B.PROD_TEST_IND = 'P'                                     
                AND   B.DIR_IND = 'S'                                           
                AND   B.INACTV_DTE = '9999-09-09'                               
             ORDER BY A.VEND_INCHG_ID_DESC,                                     
                      A.VEND_INCHG_ID                                           
           END-EXEC.                                                            
                                                                                
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           EXEC SQL                                                             
              OPEN ALLVND_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION' TO PV-PARAGRAPH-NAME                    
              MOVE 'OPEN ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED                
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           MOVE ZERO TO WS-PREV-INCHG-ID-DESC.                                  
           MOVE ZERO TO WS-PREV-INCHG-ID.                                       
                                                                                
           OPEN OUTPUT OUTPUT-FILE.                                             
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-ALLVND.                                           
                                                                                
           CLOSE OUTPUT-FILE.                                                   
                                                                                
           EXEC SQL                                                             
              CLOSE ALLVND_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              MOVE 'PROCEDURE DIVISION'  TO PV-PARAGRAPH-NAME                   
              MOVE 'CLOSE ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED               
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS ALL THE ENT-ID'S RETRIEVED WITH THE ALLVND_CURSOR.     *        
      ******************************************************************        
       1000-PROCESS-FILE.                                                       
                                                                                
           EXEC SQL                                                             
              FETCH ALLVND_CURSOR                                               
              INTO :TRDPRT-ENT-ID,                                              
                   :TRDPRT-VEND-INCHG-ID,                                       
                   :TRDPRT-VEND-INCHG-ID-DESC,                                  
                   :TRDTRN-PROD-TEST-IND                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE 'Y' TO END-OF-ALLVND-SW                                   
                 GO TO 1000-PROCESS-FILE-EXIT                                   
              ELSE                                                              
                 MOVE '1000-PROCESS-FILE'   TO PV-PARAGRAPH-NAME                
                 MOVE 'FETCH ALLVND CURSOR' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
           IF TRDPRT-VEND-INCHG-ID-DESC = WS-PREV-INCHG-ID-DESC AND             
              TRDPRT-VEND-INCHG-ID = WS-PREV-INCHG-ID                           
                 GO TO 1000-PROCESS-FILE-EXIT                                   
           ELSE                                                                 
              MOVE TRDPRT-VEND-INCHG-ID-DESC TO WS-PREV-INCHG-ID-DESC           
              MOVE TRDPRT-VEND-INCHG-ID TO WS-PREV-INCHG-ID.                    
                                                                                
           PERFORM 3000-CHECK-TTRDMLT-TABLE                                     
              THRU 3000-CHECK-TTRDMLT-TABLE-EXIT.                               
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           MOVE 'N' TO END-OF-INPUT-FILE-SW.                                    
                                                                                
           PERFORM 2000-READ-INPUT-FILE                                         
              THRU 2000-READ-INPUT-FILE-EXIT                                    
                 UNTIL END-OF-INPUT-FILE.                                       
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * READ THE INPUT FILE AND WRITE EACH RECORD TO THE OUTOUT FILE   *        
      * FOR ALL THE ENT-ID'S RETRIEVED.  ADD THE ENT-ID TO THE OUTPUT  *        
      * FILE WHENEVER THE RECORD TYPE IS 01.                           *        
      ******************************************************************        
       2000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO ET032-RELATIONSHIP-RECORD                       
              AT END                                                            
                 SET END-OF-INPUT-FILE TO TRUE                                  
                 GO TO 2000-READ-INPUT-FILE-EXIT.                               
                                                                                
           IF ET032-RECORD-TYPE-CODE = 01                                       
              IF TRDTRN-PROD-TEST-IND = 'T'                                     
                 MOVE WS-ENT-ID     TO WS-ENT-ID-NUMBER                         
                 MOVE WS-TEST-ENT-ID TO ET032-HDR-ENT-ID                        
              ELSE                                                              
                 MOVE WS-ENT-ID      TO WS-PROD-ENT-ID                          
                 MOVE WS-PROD-ENT-ID TO ET032-HDR-ENT-ID                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE OUTPUT-RECORD FROM ET032-RELATIONSHIP-RECORD.                  
                                                                                
       2000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * FIND THE PARENT ENT-ID FOR THE TRADING PARTNER.                *        
      ******************************************************************        
       3000-CHECK-TTRDMLT-TABLE.                                                
                                                                                
           EXEC SQL                                                             
              SELECT PRNT_ENT_ID                                                
              INTO   :TRDMLT-PRNT-ENT-ID                                        
              FROM   TTRDMLT                                                    
              WHERE  VND_INCHG_ID = :TRDPRT-VEND-INCHG-ID                       
                AND  VND_INCHG_ID_DESC = :TRDPRT-VEND-INCHG-ID-DESC             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +000                                                    
              MOVE TRDMLT-PRNT-ENT-ID TO WS-ENT-ID                              
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 MOVE TRDPRT-ENT-ID TO WS-ENT-ID                                
              ELSE                                                              
                 MOVE '2000-CHECK-TTRDMLT-TABLE' TO PV-PARAGRAPH-NAME           
                 MOVE 'SELECT FROM TTRDMLT' TO PV-DB2-OPER-ATTEMPTED            
                 PERFORM 9999-SQL-ERROR.                                        
                                                                                
       3000-CHECK-TTRDMLT-TABLE-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      * DB2 ABEND PROCESSING - THE CODE WE HOPE IS NEVER NEEDED.       *        
      ******************************************************************        
       9999-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
