      ******************************************************************        
      *  INPD401 - COMMON DEPARTMENT LOOKUP.                                    
      *                                                                         
      *-----------------------------------------------------------------        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR          DESCRIPTION OF CHANGES                 
      * ------- ---------- ------------- -------------------------------        
      * W28582  09-01-2006 J SELWITSCHKA INITIAL IMPLEMENTATION                 
      *                                                                         
      * W33304  05-23-2008 C EBERT       HOLD INVENTORY LEDGER OPEN             
      *                                  ACROSS PERIODS                         
      *                                                                         
      *-----------------------------------------------------------------        
      *PURPOSE: COMMON INVENTORY DEPARTMENT LOOKUP.                             
      *                                                                         
      *NOTE:    REFER TO INWS401 FOR PROCESSING OPTIONS.                        
      *           THE INPUT FIELDS WILL BE INITIALIZED AFTER EACH               
      *           PERFORM OF IN401-INV-DEPT-LOOKUP.                             
      *                                                                         
      *-----------------------------------------------------------------        
      *  HOW TO USE THIS COPYBOOK WITH INWS401:                                 
      *-----------------------------------------------------------------        
      *                                                                         
      *  1.  FILL THE INPUT VARIABLES BEFORE PERFORMING PARAGRAPH               
      *      IN401-INV-DEPT-LOOKUP. AT A MINIMUM, LOCATION NUMBER               
      *      (IN401-PV-LOOKUP-LOC-NBR) AND DEPARTMENT NUMBER                    
      *      (IN401-PV-LOOKUP-DEPT-NBR) MUST BE FILLED. IF INVENTORY ID         
      *      IS AVAILABLE, FILL IN401-PV-LOOKUP-INV-ID. THE LOOKUP IS           
      *      MOVE EFFICIENT IF INVENTORY ID IS PROVIDED.                        
      *                                                                         
      *  2.  PERFORM PARAGRAPH IN401-INV-DEPT-LOOKUP.                           
      *                                                                         
      *  3.  CHECK RESULTS.                                                     
      *      - DEPARTMENT LOOKUP RETURN CODE VARIABLES ARE DEFINED HERE.        
      *        (IN401-PV-COMMON-AREA)                                           
      *         - RETURN STATUS CODE                                            
      *         - RETURN STATUS CODE REASON TEXT                                
      *                                                                         
      *      - ON A SUCCESSFUL EXECUTION CHECK WHETHER THE DEPARTMENT           
      *        QUALIFIES FOR INVENTORY. IF IT QUALIFIES,                        
      *        IN401-PS-QUALIFIES-YES WILL BE TRUE. IF IT DOES NOT              
      *        QUALIFY, IN401-PS-QUALIFIES-NO WILL BE TRUE.                     
      *                                                                         
      *-----------------------------------------------------------------        
      *                                                                         
      *  4.  ERROR HANDLING PASSED TO THE SOURCE MODULE.                        
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
      ******************************************************************        
      ******************************************************************        
      * IN401-INV-DEPT-LOOKUP.                                                  
      *  - INITIALIZE ALL OUTPUT VALUES BEFORE EXECUTING ANY CALLS              
      *  - INITIALIZE ALL INPUT VALUES BEFORE RETURNING TO CALL MODULE          
      ******************************************************************        
       IN401-INV-DEPT-LOOKUP.                                                   
                                                                                
           INITIALIZE IN401-PV-COMMON-AREA.                                     
           SET IN401-PS-QUALIFIES-NO TO TRUE.                                   
           MOVE IN401-PV-LOOKUP-INV-ID   TO IN401-PV-INV-ID.                    
           MOVE IN401-PV-LOOKUP-LOC-NBR  TO IN401-PV-LOC-NBR.                   
           MOVE IN401-PV-LOOKUP-DEPT-NBR TO IN401-PV-DEPT-NBR.                  
                                                                                
           MOVE ZEROES         TO SQLCODE.                                      
                                                                                
           IF IN401-PS-FIRST-TIME                                               
               INITIALIZE IN401-TABLE-AREA                                      
               SET IN401-PS-FIRST-TIME-NO TO TRUE                               
               PERFORM IN401-LOAD-DEPT-INV-TABLE                                
               IF IN401-PV-RET-CODE-OK                                          
                   PERFORM IN401-LOAD-INV-LOC-TABLES                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF IN401-PV-RET-CODE-OK                                              
               PERFORM IN401-CHECK-IF-QUALIFIES                                 
           END-IF.                                                              
                                                                                
           INITIALIZE IN401-PV-INPUT-AREA.                                      
                                                                                
      ******************************************************************        
      * IN401-LOAD-DEPT-INV-TABLE.                                              
      *  -                                                                      
      ******************************************************************        
       IN401-LOAD-DEPT-INV-TABLE.                                               
                                                                                
           SET IN401-DEPT-INDX-MAX TO 99999.                                    
                                                                                
           PERFORM IN401-OPEN-DEPT-INV-CSR.                                     
                                                                                
           PERFORM IN401-FETCH-DEPT-INV                                         
             UNTIL IN401-PS-DEPT-INV-NOT-FOUND                                  
                OR (NOT IN401-PV-RET-CODE-OK).                                  
                                                                                
           PERFORM IN401-CLOSE-DEPT-INV-CSR.                                    
                                                                                
           SET IN401-DEPT-INDX-MAX TO IN401-DEPT-INDX.                          
                                                                                
      ******************************************************************        
      * IN401-OPEN-DEPT-INV-CSR.                                                
      ******************************************************************        
       IN401-OPEN-DEPT-INV-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                OPEN DEPT_INV_CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-OPEN   TO TRUE                           
                   MOVE SQLCODE               TO IN401-PV-SQLCDE                
                   STRING '  PARA: IN401-OPEN-DEPT-INV-CSR - '                  
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** GET ACTV INV LOC.                                                      
      ** FETCH THE ACTIVE INVENTORY ID'S AND ASSOCIATED LOCATIONS.              
      ******************************************************************        
       IN401-FETCH-DEPT-INV.                                                    
                                                                                
           EXEC SQL                                                             
               FETCH DEPT_INV_CSR                                               
               INTO  :INT00004-INV-ID                                           
                    ,:INT00004-LOC-NBR                                          
                    ,:INT00004-DEPT-NBR                                         
                    ,:INT00004-MAJ-CL-NBR                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET IN401-DEPT-INDX UP BY 1                                  
                   IF IN401-DEPT-INDX <= IN401-DEPT-INDX-MAX                    
                       MOVE INT00004-INV-ID TO IN401-DEPT-INV-ID                
                                               (IN401-DEPT-INDX)                
                       MOVE INT00004-LOC-NBR                                    
                         TO IN401-DEPT-LOC-NBR (IN401-DEPT-INDX)                
                       MOVE INT00004-DEPT-NBR                                   
                         TO IN401-DEPT-NBR     (IN401-DEPT-INDX)                
                       MOVE INT00004-MAJ-CL-NBR                                 
                         TO IN401-MAJ-CL-NBR   (IN401-DEPT-INDX)                
                   ELSE                                                         
                       SET IN401-PV-DEPT-INV-OVERFLOW                           
                           IN401-PV-TABLE-OVERFLOW    TO TRUE                   
                   END-IF                                                       
               WHEN SQLCODE = +100                                              
                   SET IN401-PS-DEPT-INV-NOT-FOUND TO TRUE                      
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-CLOSE  TO TRUE                           
                   MOVE SQLCODE     TO IN401-PV-SQLCDE                          
                   STRING '  PARA: IN401-FETCH-DEPT-INV - '                     
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * IN401-CLOSE-DEPT-INV-CSR.                                               
      ******************************************************************        
       IN401-CLOSE-DEPT-INV-CSR.                                                
                                                                                
           EXEC SQL                                                             
               CLOSE DEPT_INV_CSR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-CLOSE  TO TRUE                           
                   MOVE SQLCODE     TO IN401-PV-SQLCDE                          
                   STRING '  PARA: IN401-CLOSE-DEPT-INV-CSR - '                 
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * IN401-LOAD-INV-LOC-TABLES.                                              
      *  -                                                                      
      ******************************************************************        
       IN401-LOAD-INV-LOC-TABLES.                                               
                                                                                
           SET IN401-LOC-INDX-MAX  TO 9999.                                     
           SET IN401-INV-INDX-MAX  TO 999.                                      
                                                                                
           PERFORM IN401-OPEN-INV-LOC-CSR.                                      
                                                                                
           PERFORM IN401-FETCH-INV-LOC                                          
             UNTIL IN401-PS-INV-LOC-NOT-FOUND                                   
                OR (NOT IN401-PV-RET-CODE-OK).                                  
                                                                                
           PERFORM IN401-CLOSE-INV-LOC-CSR.                                     
                                                                                
           SET IN401-INV-INDX-MAX TO IN401-INV-INDX.                            
           SET IN401-LOC-INDX-MAX TO IN401-LOC-INDX.                            
                                                                                
      ******************************************************************        
      * IN401-OPEN-INV-LOC-CSR.                                                 
      ******************************************************************        
       IN401-OPEN-INV-LOC-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                OPEN INV_LOC_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-OPEN   TO TRUE                           
                   MOVE SQLCODE      TO IN401-PV-SQLCDE                         
                   STRING '  PARA: IN401-OPEN-INV-LOC-CSR - '                   
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** GET INV LOC.                                                           
      ** FETCH THE ACTIVE INVENTORY ID'S AND ASSOCIATED LOCATIONS.              
      ******************************************************************        
       IN401-FETCH-INV-LOC.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH INV_LOC_CSR                                                
               INTO  :INVPAR-INV-ID                                             
                    ,:INVPAR-LOC-NBR                                            
33304               ,:INCNTL-OPN-FSCL-MN-DTE                                    
33304               ,:INVPAR-PLND-FIN-BK-DTE                                    
33304               ,:INVPAR-FINCL-INV-STAT-CDE                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   SET IN401-LOC-INDX UP BY 1                                   
                   IF IN401-LOC-INDX <= IN401-LOC-INDX-MAX                      
                       MOVE INVPAR-INV-ID TO IN401-LOC-INV-ID                   
                                              (IN401-LOC-INDX)                  
                       MOVE INVPAR-LOC-NBR TO IN401-LOC-NBR                     
                                              (IN401-LOC-INDX)                  
33304                  MOVE INCNTL-OPN-FSCL-MN-DTE TO                           
33304                       IN401-OPN-FSCL-MN-DTE (IN401-LOC-INDX)              
33304                  MOVE INVPAR-PLND-FIN-BK-DTE TO                           
33304                       IN401-PLND-FIN-BK-DTE (IN401-LOC-INDX)              
33304                  MOVE INVPAR-FINCL-INV-STAT-CDE TO                        
33304                       IN401-FINCL-INV-STAT-CDE (IN401-LOC-INDX)           
                   ELSE                                                         
                       SET IN401-PV-INV-LOC-OVERFLOW                            
                           IN401-PV-TABLE-OVERFLOW    TO TRUE                   
                   END-IF                                                       
                   IF INVPAR-INV-ID = IN401-PV-INV-ID-PREV                      
                       CONTINUE                                                 
                   ELSE                                                         
                       SET IN401-INV-INDX UP BY 1                               
                       IF IN401-INV-INDX <= IN401-INV-INDX-MAX                  
                           MOVE INVPAR-INV-ID TO IN401-INV-ID                   
                                                  (IN401-INV-INDX)              
                           SET IN401-PS-DEPT-INV-NOT-FOUND TO TRUE              
                           SET IN401-INV-0000-NOTFND (IN401-INV-INDX)           
                                                           TO TRUE              
                           PERFORM VARYING IN401-DEPT-INDX FROM 1 BY 1          
                             UNTIL IN401-DEPT-INDX > IN401-DEPT-INDX-MAX        
                                OR IN401-PS-DEPT-INV-FOUND                      
                               IF  INVPAR-INV-ID = IN401-DEPT-INV-ID            
                                                  (IN401-DEPT-INDX)             
                               AND ZERO          = IN401-DEPT-LOC-NBR           
                                                  (IN401-DEPT-INDX)             
                                  SET IN401-INV-0000-FND(IN401-INV-INDX)        
                                                              TO TRUE           
                                  SET IN401-PS-DEPT-INV-FOUND TO TRUE           
                               END-IF                                           
                           END-PERFORM                                          
                       ELSE                                                     
                           SET IN401-PV-INV-OVERFLOW                            
                               IN401-PV-TABLE-OVERFLOW    TO TRUE               
                       END-IF                                                   
                   END-IF                                                       
                   MOVE INVPAR-INV-ID TO IN401-PV-INV-ID-PREV                   
               WHEN SQLCODE = +100                                              
                   SET IN401-PS-INV-LOC-NOT-FOUND TO TRUE                       
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-OPEN   TO TRUE                           
                   MOVE SQLCODE      TO IN401-PV-SQLCDE                         
                   STRING '  PARA: IN401-FETCH-INV-LOC - '                      
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * IN401-CLOSE-INV-LOC-CSR.                                                
      ******************************************************************        
       IN401-CLOSE-INV-LOC-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               CLOSE INV_LOC_CSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET IN401-PV-SQL-ERROR     TO TRUE                           
                   SET IN401-PV-CURSOR-CLOSE  TO TRUE                           
                   MOVE SQLCODE     TO IN401-PV-SQLCDE                          
                   STRING '  PARA: IN401-CLOSE-INV-LOC-CSR - '                  
                                             DELIMITED BY SIZE                  
                          IN401-PV-INV-ID    DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-LOC-NBR   DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                          IN401-PV-DEPT-NBR  DELIMITED BY SIZE                  
                          '  ' DELIMITED BY SIZE                                
                                    INTO IN401-PV-PARAGRAPH                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * IN401-CHECK-IF-QUALIFIES.                                               
      *  -                                                                      
      ******************************************************************        
       IN401-CHECK-IF-QUALIFIES.                                                
                                                                                
           PERFORM IN401-SEARCH-INV-TABLE.                                      
                                                                                
           IF IN401-PS-INV-FOUND                                                
               EVALUATE TRUE                                                    
                 WHEN IN401-PS-STORE-INV                                        
                     SET IN401-PS-QUALIFIES-YES TO TRUE                         
                 WHEN OTHER                                                     
                     PERFORM IN401-CHECK-BY-LOC-DEPT                            
               END-EVALUATE                                                     
           ELSE                                                                 
               SET IN401-PS-INV-LOC-NOT-FOUND TO TRUE                           
               PERFORM VARYING IN401-LOC-INDX FROM 1 BY 1                       
                 UNTIL IN401-PS-INV-LOC-FOUND                                   
                    OR IN401-LOC-INDX > IN401-LOC-INDX-MAX                      
                   IF IN401-PV-LOOKUP-LOC-NBR = IN401-LOC-NBR                   
                                               (IN401-LOC-INDX)                 
                      SET IN401-PS-INV-LOC-FOUND TO TRUE                        
                      MOVE IN401-LOC-INV-ID (IN401-LOC-INDX)                    
                        TO IN401-PV-LOOKUP-INV-ID                               
                      PERFORM IN401-SEARCH-INV-TABLE                            
                   END-IF                                                       
               END-PERFORM                                                      
               IF IN401-PS-INV-LOC-FOUND                                        
                   IF IN401-PS-INV-FOUND                                        
                       EVALUATE TRUE                                            
                         WHEN IN401-PS-STORE-INV                                
                             SET IN401-PS-QUALIFIES-YES TO TRUE                 
                         WHEN OTHER                                             
                             PERFORM IN401-CHECK-BY-LOC-DEPT                    
                       END-EVALUATE                                             
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * IN401-SEARCH-INV-TABLE.                                                 
      *  -                                                                      
      ******************************************************************        
       IN401-SEARCH-INV-TABLE.                                                  
                                                                                
           SET IN401-PS-INV-NOT-FOUND TO TRUE.                                  
           SET IN401-PS-NOT-STORE-INV TO TRUE.                                  
                                                                                
           PERFORM VARYING IN401-INV-INDX FROM 1 BY 1                           
             UNTIL IN401-PS-INV-FOUND                                           
                OR IN401-INV-INDX > IN401-INV-INDX-MAX                          
               IF IN401-PV-LOOKUP-INV-ID = IN401-INV-ID                         
                                          (IN401-INV-INDX)                      
                   SET IN401-PS-INV-FOUND TO TRUE                               
                   EVALUATE TRUE                                                
                       WHEN IN401-INV-0000-NOTFND (IN401-INV-INDX)              
                           SET IN401-PS-STORE-INV TO TRUE                       
                       WHEN IN401-INV-0000-FND (IN401-INV-INDX)                 
                           CONTINUE                                             
                   END-EVALUATE                                                 
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      * IN401-CHECK-BY-LOC-DEPT.                                                
      *  -                                                                      
      ******************************************************************        
       IN401-CHECK-BY-LOC-DEPT.                                                 
                                                                                
           IF IN401-PV-LOOKUP-LOC-NBR > ZERO                                    
               CONTINUE                                                         
           ELSE                                                                 
               SET IN401-PV-INVALID-INPUT                                       
                   IN401-PV-INVALID-LOCATION TO TRUE                            
           END-IF.                                                              
                                                                                
           IF IN401-PV-LOOKUP-DEPT-NBR > ZERO                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET IN401-PV-INVALID-INPUT                                       
                   IN401-PV-INVALID-DEPT  TO TRUE                               
           END-IF.                                                              
                                                                                
           IF IN401-PV-RET-CODE-OK                                              
               SET IN401-PS-STORE-SPEC-NOTFND2 TO TRUE                          
               SET IN401-PS-STORE-SPEC-NOTFND3 TO TRUE                          
               SET IN401-PS-STORE-0000-NOTFND2 TO TRUE                          
                                                                                
      *** SEARCH FOR STORE SPECIFIC ROW (INV-ID, LOC-NBR)                       
               PERFORM VARYING IN401-DEPT-INDX FROM 1 BY 1                      
                 UNTIL IN401-PS-STORE-SPEC-FND2                                 
                    OR IN401-DEPT-INDX > IN401-DEPT-INDX-MAX                    
                   IF  IN401-PV-LOOKUP-INV-ID   = IN401-DEPT-INV-ID             
                                                 (IN401-DEPT-INDX)              
                   AND IN401-PV-LOOKUP-LOC-NBR  = IN401-DEPT-LOC-NBR            
                                                 (IN401-DEPT-INDX)              
                       SET IN401-PS-STORE-SPEC-FND2 TO TRUE                     
                   END-IF                                                       
               END-PERFORM                                                      
                                                                                
      *** SEARCH FOR STORE SPECIFIC ROW (INV-ID, LOC-NBR, DEPT-NBR)             
               PERFORM VARYING IN401-DEPT-INDX FROM 1 BY 1                      
                 UNTIL IN401-PS-STORE-SPEC-FND3                                 
                    OR IN401-DEPT-INDX > IN401-DEPT-INDX-MAX                    
                   IF  IN401-PV-LOOKUP-INV-ID   = IN401-DEPT-INV-ID             
                                                 (IN401-DEPT-INDX)              
                   AND IN401-PV-LOOKUP-LOC-NBR  = IN401-DEPT-LOC-NBR            
                                                 (IN401-DEPT-INDX)              
                   AND IN401-PV-LOOKUP-DEPT-NBR = IN401-DEPT-NBR                
                                                 (IN401-DEPT-INDX)              
                       SET IN401-PS-STORE-SPEC-FND3  TO TRUE                    
                   END-IF                                                       
               END-PERFORM                                                      
                                                                                
               EVALUATE TRUE                                                    
                   WHEN (IN401-PS-STORE-SPEC-FND2 AND                           
                         IN401-PS-STORE-SPEC-FND3)                              
                       SET IN401-PS-QUALIFIES-YES     TO TRUE                   
                   WHEN (IN401-PS-STORE-SPEC-FND2 AND                           
                         IN401-PS-STORE-SPEC-NOTFND3)                           
                       CONTINUE                                                 
                   WHEN IN401-PS-STORE-SPEC-NOTFND2                             
                       PERFORM IN401-SEARCH-0000-DEPT                           
                       IF IN401-PS-STORE-0000-FND2                              
                           SET IN401-PS-QUALIFIES-YES TO TRUE                   
                       END-IF                                                   
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * IN401-SEARCH-0000-DEPT.                                                 
      *  -                                                                      
      ******************************************************************        
       IN401-SEARCH-0000-DEPT.                                                  
                                                                                
      *** SEARCH FOR STORE ZERO ROW                                             
           PERFORM VARYING IN401-DEPT-INDX FROM 1 BY 1                          
             UNTIL IN401-PS-STORE-0000-FND2                                     
                OR IN401-DEPT-INDX > IN401-DEPT-INDX-MAX                        
               IF  IN401-PV-LOOKUP-INV-ID   = IN401-DEPT-INV-ID                 
                                             (IN401-DEPT-INDX)                  
               AND ZERO                     = IN401-DEPT-LOC-NBR                
                                             (IN401-DEPT-INDX)                  
               AND IN401-PV-LOOKUP-DEPT-NBR = IN401-DEPT-NBR                    
                                             (IN401-DEPT-INDX)                  
                   SET IN401-PS-STORE-0000-FND2 TO TRUE                         
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
