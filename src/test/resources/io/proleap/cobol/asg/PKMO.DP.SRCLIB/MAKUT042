       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MAKUT042.                                         00020000
       AUTHOR.        FRANK SANTANDREA.                                 00030000
       DATE-WRITTEN.  FEBRUARY 09 2000.                                 00040000
      ***************************************************************** 00050000
      *          MA - MERGE RCP EXTRACT FILES                         * 00060000
      ***************************************************************** 00070000
      *    THE PURPOSE OF THIS PROGRAM IS TO MERGE MULTIPLE RCP       * 00080000
      *    EXTRACT FIELS INTO ONE. *                                    00090000
      *                                                                 00091000
DGM001* 12/01/2004 DAVE METZGER CHANGED CODE TO ACCOMODATE A 4 BYTES    00092000
DGM001* WR25478    STORE NUMBER.  RCP EXTRACT INCREASED FROM 40 TO 60.  00093000
      ***************************************************************** 00094000
      *                                                                 00095000
       ENVIRONMENT DIVISION.                                            00096000
       INPUT-OUTPUT SECTION.                                            00097000
       FILE-CONTROL.                                                    00098000
           SELECT UPCPLS-UNLOAD-FILE                                    00099000
               ASSIGN TO INP01.                                         00100000
                                                                        00110000
           SELECT SIPREX-UNLOAD-FILE                                    00120000
               ASSIGN TO INP02.                                         00121000
                                                                        00122000
           SELECT SIPEPR-UNLOAD-FILE                                    00123000
               ASSIGN TO INP03.                                         00124000
                                                                        00125000
           SELECT RCP-COMBINED-FILE                                     00126000
               ASSIGN TO OUT01.                                         00127000
                                                                        00128000
       DATA DIVISION.                                                   00129000
       FILE SECTION.                                                    00130000
       FD  UPCPLS-UNLOAD-FILE                                           00140000
           RECORDING MODE IS F                                          00150000
           BLOCK CONTAINS 0 RECORDS.                                    00160000
       01  UPCPLS-UNLOAD-RECORD            PIC  X(054).                 00170000
                                                                        00180000
       FD  SIPREX-UNLOAD-FILE                                           00190000
           RECORDING MODE IS F                                          00200000
           BLOCK CONTAINS 0 RECORDS.                                    00210000
       01  PREX-UNLOAD-RECORD              PIC  X(031).                 00220000
                                                                        00230000
       FD  SIPEPR-UNLOAD-FILE                                           00240000
           RECORDING MODE IS F                                          00250000
           BLOCK CONTAINS 0 RECORDS.                                    00260000
       01  PEPR-UNLOAD-RECORD              PIC  X(016).                 00270000
                                                                        00280000
       FD  RCP-COMBINED-FILE                                            00290000
           RECORDING MODE IS F                                          00300000
           BLOCK CONTAINS 0 RECORDS.                                    00310000
       01  RCP-COMBINED-RECORD             PIC  X(86).                  00320000
                                                                        00330000
       WORKING-STORAGE SECTION.                                         00340000
                                                                        00350000
       01  PV-PROGRAM-VARIABLES.                                        00360000
           05  PV-RCP-OUTPUT-RECS          PIC  9(11)    VALUE ZERO.    00370000
           05  PV-TUPCPLS-CNT              PIC  9(11)    VALUE ZERO.    00371000
           05  PV-TSIPREX-CNT              PIC  9(11)    VALUE ZERO.    00372000
           05  PV-TSIPEPR-CNT              PIC  9(11)    VALUE ZERO.    00373000
DGM001     05  PV-CONV-STR                 PIC  9(04)    VALUE ZERO.    00374000
                                                                        00375000
       01  PC-PROGRAM-CONSTANTS.                                        00376000
           05  PC-CLR-STAT-CDE             PIC X(02)                    00377000
                                               VALUE '30'.              00378000
DGM001     05  PC-STR-ZERO                 PIC X(04) VALUE '0000'.      00379000
           05  PC-TUPCPLS                  PIC X(08)                    00380000
                                               VALUE 'TUPCPLS'.         00390000
           05  PC-TSIPREX                  PIC X(08)                    00400000
                                               VALUE 'TSIPREX'.         00410000
           05  PC-TSIPEPR                  PIC X(08)                    00420000
                                               VALUE 'TSIPEPR'.         00430000
                                                                        00440000
       01  PS-PROGRAM-SWITCHES.                                         00450000
           05  PS-END-UPCPLS-SW            PIC  X(01)    VALUE 'N'.     00460000
               88  PS-END-UPCPLS                         VALUE 'Y'.     00470000
           05  PS-END-SIPREX-SW            PIC  X(01)    VALUE 'N'.     00480000
               88  PS-END-SIPREX                         VALUE 'Y'.     00490000
           05  PS-END-SIPEPR-SW            PIC  X(01)    VALUE 'N'.     00500000
               88  PS-END-SIPEPR                         VALUE 'Y'.     00510000
                                                                        00520000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE TUPCPLS UNLOAD FILE                      00540000
      ******************************************************************00550000
           COPY MARD080.                                                00560000
                                                                        00570000
      ******************************************************************00580000
      *  RECORD LAYOUT FOR THE TSIPREX UNLOAD FILE                      00590000
      ******************************************************************00600000
           COPY MARD081.                                                00610000
                                                                        00620000
      ******************************************************************00630000
      *  RECORD LAYOUT FOR THE TSIPEPR UNLOAD FILE                      00640000
      ******************************************************************00650000
           COPY MARD082.                                                00660000
                                                                        00670000
      ******************************************************************00680000
      *  RECORD LAYOUT FOR THE COMBINED RCP EXTRACT FILE.               00690000
      ******************************************************************00700000
           COPY MARD083.                                                00710000
                                                                        00720000
       PROCEDURE DIVISION.                                              00730000
                                                                        00740000
       0000-MAINLINE.                                                   00750000
      ******************************************************************00760000
      *  CONTROLS THE ENTIRE FLOW OF THE PROGRAM.                       00770000
      *    -- OPEN FILES                                                00780000
      *    -- PERFORM MAIN PROCESSING LOOP                              00790000
      *    -- CLOSE FILES                                               00800000
      *    -- DISPLAY COUNT                                             00810000
      *    -- RETURN CONTROL TO SYSTEM                                  00820000
      ******************************************************************00830000
           DISPLAY '*****************'.                                 00840000
           DISPLAY 'START OF MAKUT042'.                                 00850000
                                                                        00860000
           PERFORM 1000-OPEN-FILES.                                     00870000
           PERFORM 2000-PROCESS-UNLOADS.                                00880000
           PERFORM 1999-CLOSE-FILES.                                    00890000
                                                                        00900000
           DISPLAY 'EXTRACT RECORDS WRITTEN: ' PV-RCP-OUTPUT-RECS.      00910000
                                                                        00920000
           DISPLAY 'END OF MAKUT042'.                                   00930000
           DISPLAY '*****************'.                                 00940000
                                                                        00950000
           STOP RUN.                                                    00960000
                                                                        00970000
      ******************************************************************00980000
      * 1000-OPEN-FILES.                                               *00990000
      ******************************************************************01000000
       1000-OPEN-FILES.                                                 01010000
                                                                        01020000
           OPEN OUTPUT RCP-COMBINED-FILE                                01021000
                 INPUT UPCPLS-UNLOAD-FILE                               01022000
                       SIPREX-UNLOAD-FILE                               01023000
                       SIPEPR-UNLOAD-FILE.                              01024000
                                                                        01025000
      ******************************************************************01026000
      * 1999-CLOSE-FILES.                                              *01026100
      ******************************************************************01026200
       1999-CLOSE-FILES.                                                01026300
                                                                        01026400
           CLOSE RCP-COMBINED-FILE                                      01026500
                 UPCPLS-UNLOAD-FILE                                     01026600
                 SIPREX-UNLOAD-FILE                                     01026700
                 SIPEPR-UNLOAD-FILE.                                    01026800
                                                                        01026900
      ******************************************************************01027000
      * 2000-PROCESS-UNLOADS.                                          *01027100
      * CONTROL THE COMBINING OF THE EXTRACTS                          *01027200
      ******************************************************************01027300
       2000-PROCESS-UNLOADS.                                            01027400
      *  PROCESS ALL THE RECORDS EXTRACTED FROM TUPCPLS.                01027500
           PERFORM 3000-READ-TUPCPLS-UNLOAD.                            01027600
           PERFORM 3010-PROCESS-TUPCPLS UNTIL PS-END-UPCPLS.            01027700
           DISPLAY 'TUPCPLS RECORDS PROCESSED: ' PV-TUPCPLS-CNT.        01027800
                                                                        01027900
           PERFORM 3100-READ-TSIPREX-UNLOAD.                            01028000
           PERFORM 3110-PROCESS-TSIPREX UNTIL PS-END-SIPREX.            01029000
           DISPLAY 'TSIPREX RECORDS PROCESSED: ' PV-TSIPREX-CNT.        01030000
                                                                        01040000
           PERFORM 3200-READ-TSIPEPR-UNLOAD.                            01050000
           PERFORM 3210-PROCESS-TSIPEPR UNTIL PS-END-SIPEPR.            01060000
           DISPLAY 'TSIPEPR RECORDS PROCESSED: ' PV-TSIPEPR-CNT.        01070000
                                                                        01080000
      ******************************************************************01090000
      * 3000-READ-TUPCPLS-UNLOADS.                                     *01100000
      ******************************************************************01110000
       3000-READ-TUPCPLS-UNLOAD.                                        01120000
           READ UPCPLS-UNLOAD-FILE                                      01130000
                INTO MA080-TUPCPLS-UNLOAD-RECORD                        01140000
           AT END                                                       01150000
           SET PS-END-UPCPLS TO TRUE.                                   01160000
                                                                        01170000
           IF NOT PS-END-UPCPLS                                         01180000
              ADD 1 TO PV-TUPCPLS-CNT                                   01190000
           END-IF.                                                      01200000
                                                                        01210000
      ******************************************************************01220000
      * 3010-PROCESS-TUPCPLS.                                          *01230000
      * BUILD THE COMBINED OUTPUT RECORD FROM THE TUPCPLS UNLOAD       *01240000
      ******************************************************************01250000
       3010-PROCESS-TUPCPLS.                                            01260000
           MOVE SPACES                  TO  MA083-RCP-COMBINED-RECORD.  01270000
           MOVE MA080-UPC-ID            TO  MA083-UPC-ID.               01280000
           MOVE MA080-ITM-NBR           TO  MA083-ITM-NBR.              01290000
           MOVE MA080-STR-NBR           TO  PV-CONV-STR.                01300000
           MOVE PV-CONV-STR             TO  MA083-STR-NBR.              01310000
           MOVE MA080-UPC-STAT-CDE      TO  MA083-UPC-STAT-CDE.         01320000
           MOVE MA080-UPC-STAT-CHG-DTE  TO  MA083-UPC-STAT-CHG-DTE.     01330000
DGM001*    MOVE MA080-EFF-END-TMST      TO  MA083-EFF-END-TMST.         01340000
           MOVE PC-TUPCPLS              TO  MA083-REC-ID.               01350000
                                                                        01360000
           PERFORM 5000-WRITE-COMBINED-RECORD.                          01370000
                                                                        01380000
           PERFORM 3000-READ-TUPCPLS-UNLOAD.                            01390000
                                                                        01400000
      ******************************************************************01410000
      * 3100-READ-TSIPREX-UNLOADS.                                     *01420000
      ******************************************************************01430000
       3100-READ-TSIPREX-UNLOAD.                                        01440000
              READ SIPREX-UNLOAD-FILE                                   01450000
                 INTO MA081-TSIPREX-UNLOAD-RECORD                       01460000
               AT END                                                   01470000
                  SET PS-END-SIPREX TO TRUE.                            01480000
                                                                        01490000
                IF NOT PS-END-SIPREX                                    01500000
                   ADD 1 TO PV-TSIPREX-CNT                              01510000
                END-IF.                                                 01520000
                                                                        01530000
      ******************************************************************01540000
      * 3110-PROCESS-TSIPREX.                                          *01550000
      * BUILD THE COMBINED OUTPUT RECORD FROM THE TSIPREX UNLOAD       *01560000
      ******************************************************************01570000
       3110-PROCESS-TSIPREX.                                            01580000
           MOVE SPACES                  TO  MA083-RCP-COMBINED-RECORD.  01590000
           MOVE MA081-UPC-ID            TO  MA083-UPC-ID.               01600000
           MOVE MA081-STR-NBR           TO  PV-CONV-STR.                01610000
           MOVE PV-CONV-STR             TO  MA083-STR-NBR.              01620000
           MOVE PC-CLR-STAT-CDE         TO  MA083-UPC-STAT-CDE.         01630000
           MOVE MA081-BEG-DTE           TO  MA083-BEG-DTE.              01640000
           MOVE MA081-END-DTE           TO  MA083-END-DTE.              01650000
           MOVE MA081-MSTR-CENPPC-NBR   TO  MA083-MSTR-CENPPC-NBR       01660000
           MOVE PC-TSIPREX              TO  MA083-REC-ID.               01670000
                                                                        01671000
           PERFORM 5000-WRITE-COMBINED-RECORD.                          01671100
                                                                        01671200
           PERFORM 3100-READ-TSIPREX-UNLOAD.                            01671300
                                                                        01671400
      ******************************************************************01671500
      * 3200-READ-TSIPEPR-UNLOADS.                                     *01671600
      ******************************************************************01671700
       3200-READ-TSIPEPR-UNLOAD.                                        01671800
              READ SIPEPR-UNLOAD-FILE                                   01671900
                 INTO MA082-TSIPEPR-UNLOAD-RECORD                       01672000
               AT END                                                   01672100
                  SET PS-END-SIPEPR TO TRUE.                            01672200
                                                                        01672300
                IF NOT PS-END-SIPEPR                                    01672400
                   ADD 1 TO PV-TSIPEPR-CNT                              01672500
                END-IF.                                                 01672600
                                                                        01672700
      ******************************************************************01672800
      * 3210-PROCESS-TSIPEPR.                                          *01672900
      * BUILD THE COMBINED OUTPUT RECORD FROM THE TSIPEPR UNLOAD       *01673000
      ******************************************************************01674000
       3210-PROCESS-TSIPEPR.                                            01675000
           MOVE SPACES                  TO  MA083-RCP-COMBINED-RECORD.  01676000
           MOVE MA082-UPC-ID            TO  MA083-UPC-ID.               01677000
           MOVE PC-STR-ZERO             TO  MA083-STR-NBR.              01678000
           MOVE PC-CLR-STAT-CDE         TO  MA083-UPC-STAT-CDE.         01679000
           MOVE MA082-BEG-DTE           TO  MA083-BEG-DTE.              01679100
           MOVE MA082-UPC-STAT-CDE      TO  MA083-UPC-STAT-CDE.         01679200
           MOVE PC-TSIPEPR              TO  MA083-REC-ID.               01679300
                                                                        01679400
           PERFORM 5000-WRITE-COMBINED-RECORD.                          01679500
                                                                        01679600
           PERFORM 3200-READ-TSIPEPR-UNLOAD.                            01679700
                                                                        01679800
                                                                        01679900
      ******************************************************************01680000
      * 5000-WRITE-COMBINED-RECORD.                                    *01680100
      ******************************************************************01680200
       5000-WRITE-COMBINED-RECORD.                                      01680300
                                                                        01680400
           WRITE RCP-COMBINED-RECORD FROM MA083-RCP-COMBINED-RECORD.    01680500
           ADD 1 TO PV-RCP-OUTPUT-RECS.                                 01680600
