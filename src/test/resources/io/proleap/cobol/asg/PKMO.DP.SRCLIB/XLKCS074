000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    XLKCS074.                                                 
000300 AUTHOR.        PAUL OMAN.                                                
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  JUNE 2004.                                                
000600 DATE-COMPILED.                                                           
000700                                                                          
000800****************************************************************          
000900*    XLWS074 - COPYBOOK USED WHEN CALLING THIS PROGRAM.        *          
001000*                                                              *          
001100*    XLKUT074 - BATCH VERSION OF THIS PROGRAM.                 *          
001200*                                                              *          
001300*  NOTE: WHEN CALLING THIS PROGRAM, THE CALLING PROGRAM'S      *          
001400*        DB2 PLAN MUST INCLUDE THE XL COLLECTION ID.           *          
001500*                                                              *          
001600*    PROGRAM IS USED TO ACCESS INTRANSIT AND STORE EXPECTED    *          
001700*        QUANTITIES.  CALLING PROGRAMS ARE REQUIRED TO SUPPLY  *          
001800*        THE TYPE OF ITEM ACCESS: SKU, ITEM, UPC NUMBER, OR    *          
001900*        INTERNAL UPC NUMBER.  THEY ARE ALSO REQUIRED TO       *          
002000*        SUPPLY THE TYPE OF LOCATION ACCESS, IF IT IS FOR AN   *          
002100*        INDIVIDUAL STORE OR ALL STORES.                       *          
002200*                                                              *          
002300*    XLKUT001 IS CALLED TO CONVERT THE SKU, ITEM, OR UPC       *          
002400*        NUMBER TO THE INTERNAL UPC NUMBER SO THAT THE         *          
002500*        PCT_TRN_CLSN_BAL (PCT00014) TABLE CAN BE READ.        *          
002600*                                                              *          
002700****************************************************************          
002800                                                                          
002900 ENVIRONMENT DIVISION.                                                    
003000 CONFIGURATION SECTION.                                                   
003100 SOURCE-COMPUTER. IBM-3090.                                               
003200 OBJECT-COMPUTER. IBM-3090.                                               
003300                                                                          
003400 DATA DIVISION.                                                           
003500 WORKING-STORAGE SECTION.                                                 
003600 01  PV-STUFF.                                                            
003700     05  PV-INTR-UPC-ID               PIC S9(9) VALUE 0 COMP.             
003800     05  PV-LOCATION-NBR              PIC S9(4) VALUE 0 COMP.             
003900     05  PV-TRN-CLSN-QTY              PIC S9(9) VALUE 0 COMP.             
004000     05  PV-TRN-CLSN-CDE              PIC X(02) VALUE SPACES.             
004100                                                                          
004200 01  DK-KEYS.                                                             
004300     05  DK-SUB-CL-NBR                PIC X(03).                          
004400                                                                          
004500 01  PC-CONSTANTS.                                                        
004600     05  PC-IT                       PIC X(02)  VALUE 'IT'.               
004700     05  PC-SE                       PIC X(02)  VALUE 'SE'.               
004800     05  PC-INTERNAL-UPC-CDE         PIC X(02)  VALUE 'I'.                
004900                                                                          
005000***  XLWS001  - COMMAREA USED TO CALL XLKUT001                            
005100     COPY XLWS001.                                                        
005200                                                                          
005300***  PCT00014 - PCT_TRN_CLSN_BAL                                          
005400     EXEC SQL                                                             
005500         INCLUDE PCT00014                                                 
005600     END-EXEC.                                                            
005700                                                                          
005800     EXEC SQL                                                             
005900          INCLUDE SQLCA                                                   
006000     END-EXEC.                                                            
006100                                                                          
006200                                                                          
006300 LINKAGE SECTION.                                                         
006400     COPY XLWS074.                                                        
006500                                                                          
006600 PROCEDURE DIVISION USING XL074-ITEM-COMMAREA.                            
006700                                                                          
006800     SET XL074-SUCCESSFUL        TO TRUE.                                 
006900     INITIALIZE DCLPCT00014.                                              
007000                                                                          
007100     PERFORM 1000-VALIDATE-INPUT.                                         
007200                                                                          
007300     IF XL074-SUCCESSFUL                                                  
007400        PERFORM 2000-CALL-XLKUT001                                        
007500     END-IF.                                                              
007600                                                                          
007700     IF XL074-SUCCESSFUL                                                  
007800        PERFORM 3000-RETRIEVE-QUANTITIES                                  
007900     END-IF.                                                              
008000                                                                          
008100     GOBACK.                                                              
008200                                                                          
008300****************************************************************          
008400*    CHECK THAT THE APPROPRIATE KEY FIELD HAS BEEN FILLED IN   *          
008500*        FOR THE ITEM ACCESS METHOD SELECTED.                  *          
008600*                                                              *          
008700*    CHECK THAT A VALID LOCATION ACCESS METHOD HAS BEEN        *          
008800*        SELECTED WITH ANY REQUIRED INFORMATION.               *          
008900****************************************************************          
009000 1000-VALIDATE-INPUT.                                                     
009100                                                                          
009200     EVALUATE TRUE                                                        
009300         WHEN XL074-BY-SKU                                                
009400              IF XL074-KEY-SKU = 0                                        
009500                 SET XL074-SKU-IS-ZERO TO TRUE                            
009600              END-IF                                                      
009700                                                                          
009800         WHEN XL074-BY-ITEM                                               
009900              IF XL074-KEY-ITEM-NBR = 0                                   
010000                 SET XL074-ITEM-NBR-IS-ZERO TO TRUE                       
010100              END-IF                                                      
010200                                                                          
010300         WHEN XL074-BY-UPC-NBR                                            
010400              IF XL074-KEY-UPC-NBR = 0                                    
010500                 SET XL074-UPC-NBR-IS-ZERO TO TRUE                        
010600              END-IF                                                      
010700                                                                          
010800         WHEN XL074-BY-INTERNAL-UPC                                       
010900              IF XL074-KEY-INTR-UPC = 0                                   
011000                 SET XL074-INTR-UPC-IS-ZERO TO TRUE                       
011100              END-IF                                                      
011200                                                                          
011300         WHEN OTHER                                                       
011400              SET XL074-INVALID-ITEM-ACCESS TO TRUE                       
011500                                                                          
011600     END-EVALUATE.                                                        
011700                                                                          
011800     IF XL074-SUCCESSFUL                                                  
011900        EVALUATE TRUE                                                     
012000            WHEN XL074-BY-STORE                                           
012100                 IF XL074-KEY-LOC-NBR = 0                                 
012200                    SET XL074-LOCATION-NBR-IS-ZERO TO TRUE                
012300                 ELSE                                                     
012400                    MOVE XL074-KEY-LOC-NBR TO PV-LOCATION-NBR             
012500                 END-IF                                                   
012600                                                                          
012700            WHEN XL074-BY-ALL-LOCATIONS                                   
012800                 CONTINUE                                                 
012900                                                                          
013000            WHEN OTHER                                                    
013100                 SET XL074-INVALID-LOCATION-ACCESS TO TRUE                
013200                                                                          
013300        END-EVALUATE.                                                     
013400                                                                          
013500****************************************************************          
013600*    CONVERT THE COMMAREA SKU, ITEM NUMBER, OR UPC NUMBER TO   *          
013700*    THE INTERNAL UPC NUMBER SO THE PCT00014 TABLE CAN BE      *          
013800*    READ.  IF THE COMMAREA INTERNAL UPC IS ENTERED, SEND IT   *          
013900*    TO XLKUT001 TO VALIDATE IT.                               *          
014000*                                                              *          
014100*    WHEN COMING BACK FROM XLKUT001:                           *          
014200*        IF THE XL001-RETURN-CODE WAS SUCCESSFUL, EVERYTHING   *          
014300*           WAS OK, SO JUST CONTINUE ON.                       *          
014400*                                                              *          
014500*        IF THE XL001-SKU-SQLCODE IS +100, THE SKU, ITEM, OR   *          
014600*           INTERNAL UPC WAS NOT FOUND SO SET THE APPROPRIATE  *          
014700*           ERROR SWITCH.                                      *          
014800*                                                              *          
014900*        IF THE XL001-UPC-SQLCODE IS +100, THE UPC NUMBER      *          
015000*           WASN'T FOUND SO SET THE APPROPRIATE ERROR SWITCH.  *          
015100*                                                              *          
015200*        IF NONE OF THE ABOVE CONDITIONS ARE TRUE, THEN THERE  *          
015300*           IS SOME OTHER ERROR FROM XLKUT001, SO MOVE THE     *          
015400*           APPROPRIATE SQLCODE TO THE XLWS074 COMMAREA.       *          
015500*                                                              *          
015600****************************************************************          
015700                                                                          
015800 2000-CALL-XLKUT001.                                                      
015900                                                                          
016000     INITIALIZE XL001-ITEM-COMMAREA.                                      
016100     EVALUATE TRUE                                                        
016200         WHEN XL074-BY-SKU                                                
016300              MOVE XL074-KEY-SKU        TO XL001-KEY-SKU                  
016400              SET XL001-BY-NUMERIC-SKU  TO TRUE                           
016500                                                                          
016600         WHEN XL074-BY-ITEM                                               
016700              MOVE XL074-KEY-ITEM-NBR   TO XL001-KEY-ITM-NBR              
016800              SET XL001-BY-ITEM         TO TRUE                           
016900                                                                          
017000         WHEN XL074-BY-UPC-NBR                                            
017100              MOVE XL074-KEY-UPC-NBR    TO XL001-KEY-UPC-NBR              
017200              SET XL001-BY-UPC-NBR      TO TRUE                           
017300                                                                          
017400         WHEN XL074-BY-INTERNAL-UPC                                       
017500              MOVE XL074-KEY-INTR-UPC   TO XL001-KEY-INTR-UPC-ID          
017600              SET XL001-BY-INTR-UPC-ID  TO TRUE                           
017700                                                                          
017800     END-EVALUATE.                                                        
017900                                                                          
018000     SET XL001-SELECT-SKU                                                 
018100         XL001-SELECT-SKU-STR                                             
018200         XL001-SELECT-UPC      TO  TRUE.                                  
018300                                                                          
018400     CALL XL001-VSM-PGM USING DFHEIBLK                                    
018500                              XL001-COMMAREA                              
018600                              XL001-ITEM-COMMAREA.                        
018700                                                                          
018800     EVALUATE TRUE                                                        
018900         WHEN XL001-SUCCESSFUL                                            
019000              MOVE XL001-INTR-UPC-ID TO PV-INTR-UPC-ID                    
019100                                        XL074-INTR-UPC                    
019200                                                                          
019300         WHEN XL001-SKU-SQLCODE = +100                                    
019400              EVALUATE TRUE                                               
019500                  WHEN XL074-BY-SKU                                       
019600                       SET  XL074-SKU-NOT-FOUND TO TRUE                   
019700                                                                          
019800                  WHEN XL074-BY-ITEM                                      
019900                       SET  XL074-ITEM-NBR-NOT-FOUND TO TRUE              
020000                                                                          
020100                  WHEN XL074-BY-INTERNAL-UPC                              
020200                       SET  XL074-INTR-UPC-NOT-FOUND TO TRUE              
020300                                                                          
020400              END-EVALUATE                                                
020500                                                                          
020600         WHEN XL001-UPC-SQLCODE = +100                                    
020700              SET  XL074-UPC-NOT-FOUND TO TRUE                            
020800                                                                          
020900         WHEN OTHER                                                       
021000              MOVE XL001-INTR-UPC-ID TO PV-INTR-UPC-ID                    
021100                                        XL074-INTR-UPC                    
021200****************************************************************          
021300*    FOLLOWING SQL ERROR CODES ARE SET IN XLKUT001 IN THESE    *          
021400*        PARAGRAPHES.                                          *          
021500*    XL001-SKU-SQLCODE - 1010-, 2010-, 7000-, 7010-, 7020-     *          
021600*    XL001-UPC-SQLCODE - 3010-, 7300-, 7350-                   *          
021700*    XL001-VND-STYL-SQLCODE - 7100-, 7110-                     *          
021800*    XL001-SKU-STR-SQLCODE  - 7200-                            *          
021900*    XL001-WEB-SKU-SQLCODE  - 7400-                            *          
022000*                                                              *          
022100****************************************************************          
022200              EVALUATE TRUE                                               
022300                  WHEN XL001-SKU-SQLCODE NOT = 0                          
022400                       MOVE XL001-SKU-SQLCODE TO XL074-SQLCODE            
022500                                                                          
022600                  WHEN XL001-UPC-SQLCODE NOT = 0                          
022700                       MOVE XL001-UPC-SQLCODE TO XL074-SQLCODE            
022800                                                                          
022900                  WHEN XL001-VND-STYL-SQLCODE NOT = 0                     
023000                       MOVE XL001-VND-STYL-SQLCODE TO                     
023100                                           XL074-SQLCODE                  
023200                                                                          
023300                  WHEN XL001-SKU-STR-SQLCODE NOT = 0                      
023400                       MOVE XL001-SKU-STR-SQLCODE TO                      
023500                                           XL074-SQLCODE                  
023600                                                                          
023700                  WHEN XL001-WEB-SKU-SQLCODE NOT = 0                      
023800                       MOVE XL001-WEB-SKU-SQLCODE TO                      
023900                                          XL074-SQLCODE                   
024000                                                                          
024100              END-EVALUATE                                                
024200                                                                          
024300****************************************************************          
024400*             IF ONE OF THE ABOVE SQLCODES WAS NON-ZERO        *          
024500*                FILL IN THE PARAGRAPH NAME                    *          
024600****************************************************************          
024700              IF XL074-SQLCODE NOT = 0                                    
024800                 IF XL001-ERROR-PARAGRAPH-NBR = SPACES                    
024900                    MOVE XL001-CONTENTION-ERR-PARA-NBR TO                 
025000                                 XL074-CALLED-PROGRAM-PARA                
025100                 ELSE                                                     
025200                    MOVE XL001-ERROR-PARAGRAPH-NBR TO                     
025300                                 XL074-CALLED-PROGRAM-PARA                
025400                 END-IF                                                   
025500              END-IF                                                      
025600                                                                          
025700              IF XL001-SKU-SQLCODE     NOT = 0                            
025800                 SET  XL074-SQL-ERROR TO TRUE                             
025900                 MOVE '2000-' TO XL074-ERROR-PARAGRAPH-NBR                
026000                 MOVE XL001-ERROR-PARAGRAPH-NBR TO                        
026100                                 XL074-CALLED-PROGRAM-PARA                
026200                 MOVE XL001-SQLCA   TO XL074-SQLCA                        
026300              END-IF                                                      
026400                                                                          
026500     END-EVALUATE.                                                        
026600                                                                          
026700****************************************************************          
026800*    RETRIEVE THE INTRANSIT QUANTITY FIRST, THEN THE STORE     *          
026900*        EXPECTED QUANTITY.                                    *          
027000*                                                              *          
027100****************************************************************          
027200                                                                          
027300 3000-RETRIEVE-QUANTITIES.                                                
027400                                                                          
027500     MOVE PC-IT  TO PV-TRN-CLSN-CDE.                                      
027600                                                                          
027700     PERFORM 4000-SELECT-PCT00014.                                        
027800                                                                          
027900     MOVE PCT00014-TRN-CLSN-QTY TO XL074-IT-QTY.                          
028000                                                                          
028100     MOVE PC-SE  TO PV-TRN-CLSN-CDE.                                      
028200                                                                          
028300     PERFORM 4000-SELECT-PCT00014.                                        
028400                                                                          
028500     MOVE PCT00014-TRN-CLSN-QTY TO XL074-SE-QTY.                          
028600                                                                          
028700****************************************************************          
028800*    IF THIS IS A STORE REQUEST, RETRIEVE ONLY THE QUANTITY    *          
028900*       FOR THAT STORE, OTHERWISE RETRIEVE THE SUM OF ALL      *          
029000*       THE QUANTITIES FOR ALL LOCATIONS.                      *          
029100*                                                              *          
029200****************************************************************          
029300                                                                          
029400 4000-SELECT-PCT00014.                                                    
029500                                                                          
029600     IF XL074-BY-STORE                                                    
029700        EXEC SQL                                                          
029800         SELECT   TRN_CLSN_QTY                                            
029900                                                                          
030000           INTO  :PCT00014-TRN-CLSN-QTY                                   
030100                                                                          
030200           FROM  PCT_TRN_CLSN_BAL                                         
030300          WHERE  INTR_UPC_ID   = :PV-INTR-UPC-ID                          
030400            AND  TRN_CLSN_CDE  = :PV-TRN-CLSN-CDE                         
030500            AND  LOC_NBR       = :PV-LOCATION-NBR                         
030600           WITH  UR                                                       
030700        END-EXEC                                                          
030800     ELSE                                                                 
030900        EXEC SQL                                                          
031000         SELECT   SUM(TRN_CLSN_QTY)                                       
031100                                                                          
031200           INTO  :PCT00014-TRN-CLSN-QTY                                   
031300                                                                          
031400           FROM  PCT_TRN_CLSN_BAL                                         
031500          WHERE  INTR_UPC_ID   = :PV-INTR-UPC-ID                          
031600            AND  TRN_CLSN_CDE  = :PV-TRN-CLSN-CDE                         
031700           WITH  UR                                                       
031800        END-EXEC                                                          
031900     END-IF.                                                              
032000                                                                          
032100     EVALUATE TRUE                                                        
032200         WHEN SQLCODE = ZERO                                              
032300              CONTINUE                                                    
032400                                                                          
032500         WHEN SQLCODE = +100                                              
032600         WHEN SQLCODE = -305                                              
032700              MOVE 0 TO PCT00014-TRN-CLSN-QTY                             
032800                                                                          
032900         WHEN OTHER                                                       
033000              SET  XL074-SQL-ERROR TO TRUE                                
033100              MOVE '4000-' TO XL074-ERROR-PARAGRAPH-NBR                   
033200              MOVE SQLCODE TO XL074-SQLCODE                               
033300              MOVE SQLCA   TO XL074-SQLCA                                 
033400                                                                          
033500     END-EVALUATE.                                                        
033600                                                                          
