      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             SAKUP058.                                00050000
       AUTHOR.                 JIM HUNTER.                              00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN.           JULY, 2012.                              00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM WILL CONTROL THE ASSIGNING OF BUSINESS DATES ON    00120000
      * THE COSA PARTITION TABLE (TEPPART).                             00130000
      * IT WAS MODELLED AFTER EPKUP048, WITH CODE ADDED FROM EPKUT051   00140000
      * AND EPKUT052 AS NOTED.  THE CALENDAR DATE ROUTINE WAS CALLED    00150000
      * TO PROVIDE ADDITIONAL DATE ERROR CHECKING.                      00160000
      * IT WILL BEGIN RUNNING AS PART OF THE FSH (FLEXIBLE STORE HOUR)  00170000
      * PROJECT, WHICH WILL ELIMINATE THE USE OF THE TEMPORARY AND      00180000
      * HISTORICAL PARTITIONS FROM THE COSA TABLES.  THE DAILY AND      00190000
      * WEEKLY PARTITION ROLL PROCESSES WILL ALSO BE ELIMINATED.        00200000
      *                                                                 00210000
      * INITIALLY, PARTNS 1-4 WILL REMAIN 'TEMPORARY' PARTNS AND WILL   00220000
      * NOT HAVE TRANSACTIONS STORED IN THEM.  THIS IS TO ALLOW FOR     00230000
      * THE UNLIKELY EVENT THAT A BACKOUT OF WRITING DIRECTLY TO THE    00240000
      * PERM PARTNS WOULD BE NEEDED.                                    00250000
      * THEY WILL CONTINUE TO HOLD THE CURRENT DATE'S BUSINESS DATE -   00260000
      * THE HUB AND FLASH WILL CONTINUE TO ACCESS PARTITION 1 TO GET    00270000
      * THE BUSINESS DATE.                                              00280000
      * OPERATIONAL PARTITIONS 96 THRU 500 WILL BE ADDED TO ACCOMODATE  00290000
      * THE LOSS OF THE HISTORICAL PARTITIONS.                          00300000
      *                                                                 00310000
      * THE HISTORICAL PARTITIONS WILL BE KEPT UNTIL THE 500            00320000
      * OPERATIONAL PARTITIONS HAVE BEEN FILLED (ROUGHLY 13 MONTHS).    00330000
      * AT THAT TIME, THE HISTORICAL TABLES WILL BE DROPPED BY THE      00340000
      * DBA'S AND ALL REFERENCES TO THEM WILL BE REMOVED FROM PROGRAMS. 00350000
      * IN THE MEAN TIME, THE HISTORICAL PARTITIONS WILL NOT ROLL OFF   00360000
      * OF THE HISTORICAL TABLES - PROGRAMS CAN CONTINUE TO USE THEM.   00370000
      *                                                                 00380000
      * THIS PROGRAM WILL RUN NIGHTLY AND WILL:                         00390000
      * 1) UPDATE THE TEPPART SALES DATES ON PARTITIONS 1 THRU 4        00400000
      *    TO REFLECT THE NEXT BUSINESS DAY (CTLCD DATE).               00410000
      *    IN 2013, A NEW TABLE WILL TAKE THE PLACE OF PARTN 1          00420000
      *    FOR HOLDING THE CURRENT BUSINESS DATE AND PARTNS 1 - 4       00430000
      *    WILL BE CONVERTED TO HOLD TRANSACTIONS LIKE THE REST OF      00440000
      *    THE PERMANENT PARTITIONS.                                    00450000
      * 2) FIND THE LOWEST UNASSIGNED PARTITION (SALES DATE =           00460000
      *    '9999-12-31') AND UPDATE IT TO REFLECT THE NEXT              00470000
      *    BUSINESS DAY + 1.  THIS WILL BE THE PARTITION WRITTEN TO     00480000
      *    WHEN 'FUTURE' DATED TRANSACTIONS ARE SENT (TYPICALLY,        00490000
      *    BY EAST COAST STORES WHEN THEY ARE OPEN PAST 1:00 A.M.,      00500000
      *    AND THEIR REGISTERS ARE ROLLED TO THE NEXT BUSINESS DAY).    00510000
      *    IF AN UNASSIGNED PARTITION IS FOUND, SKIP #3 (FINDING        00520000
      *    PARTITION WITH OLDEST DATE).                                 00530000
      * 3) IF AN UNASSIGNED PARTITION IS NOT FOUND IN STEP 2...         00540000
      *    FIND THE OPERATIONAL PARTITION WITH THE OLDEST SALES DATE    00550000
      *    AND UPDATE IT TO REFLECT THE NEXT BUSINESS DAY + 1.  THIS    00560000
      *    WILL BE THE PARTITION WRITTEN TO WHEN 'FUTURE' DATED         00570000
      *    TRANSACTIONS ARE SENT (TYPICALLY, BY EAST COAST STORES       00580000
      *    WHEN THEY ARE OPEN PAST 1:00 A.M., AND THEIR REGISTERS ARE   00590000
      *    ROLLED TO THE NEXT BUSINESS DAY).                            00600000
      * 4) PASS THE OPERATIONAL PARTITION WITH THE ASSIGNED FUTURE DATE 00610000
      *    VIA A CONTROL CARD TO EPK386 WHICH WILL CLEAR ALL OF         00620000
      *    THE COSA TABLES FOR THAT PARTITION.                          00630000
      *                                                                 00640000
      * THIS PROGRAM MUST ASSIGN PARTITIONS 2 DAYS IN ADVANCE TO        00650000
      * ALLOW FOR THE NEXT BUSINESS DATE'S FUTURE TRANSACTIONS.         00660000
      * WHEN THIS PROGRAM RUNS, THE NEW BUSINESS DATE'S PARTITION       00670000
      * MAY ALREADY CONTAIN FUTURE DATED TRANSACTIONS - IT CAN NOT      00680000
      * BE CLEARED!                                                     00690000
      *                                                                 00700000
      * BEFORE THIS PROGRAM RUNS FOR THE FIRST TIME, AN                 00710000
      * INITIALIZATION PROGRAM (EPKUP958) WILL NEED TO BE RUN THAT      00720000
      * WILL ASSIGN THE NEXT BUSINESS DATE'S PARTITION.                 00730000
      * THE INITIALIZATION PROGRAM WILL ALSO ASSIGN NEW PARTITIONS      00740000
      * 96 THRU 500 TO WITH SALES DATES OF '9999-12-31' SO THAT         00750000
      * THEY ARE THE NEXT TO BE ASSIGNED.  UNASSIGNED PARTITIONS        00760000
      * WILL HAVE THE STR_GP_CDE AND DB_STRUC_CDE WILL SET TO 'N'       00770000
      * SO THAT PROGRAMS WON'T ACCIDENTLY REFERENCE THEM.               00780000
      ******************************************************************00790000
      * CHANGE LOG: INITIAL PROGRAM.                                    00800000
      * DATE:  JULY, 2012                                               00810000
      * PRGMR: JIM HUNTER                                               00820000
      * WHY:   FSH (FLEXIBLE STORE HOUR) PROJECT - WR40932              00830000
      *                                                                 00840000
      ******************************************************************00850000
      * DATE:  FEBRUARY, 2013.                                          00860000
      * PRGMR: JIM ARENDT                                               00870000
      * WHY:   FSH13 (FLEXIBLE STORE HOUR) PROJECT - CRQ000000041382    00880000
      *                                                                 00890000
      ******************************************************************00900000
C62728* DATE:  OCTOBER, 2013.                                           00900100
C62728* PRGMR: COGNIZANT                                                00900200
C62728* WHY:   FSH13 (FLEXIBLE STORE HOUR) PROJECT - CHG0062728         00900300
C62728*                                                                 00900400
C62728******************************************************************00900500
      * DATE:  JULY, 2020.                                              00900600
      * PRGMR: JEAN KURK                                                00900700
      * WHY:   RENAME FROM EPKUP058                                     00900800
      *                                                                 00900900
      ******************************************************************00901000
                                                                        00901100
       ENVIRONMENT DIVISION.                                            00901200
      ******************************************************************00901300
                                                                        00901400
       CONFIGURATION SECTION.                                           00901500
       SOURCE-COMPUTER.        IBM-3090.                                00902000
       OBJECT-COMPUTER.        IMB-3090.                                00903000
                                                                        00904000
       INPUT-OUTPUT SECTION.                                            00905000
       FILE-CONTROL.                                                    00906000
                                                                        00907000
           SELECT DATE-CONTROL-FILE                                     00908000
               ASSIGN TO INP01.                                         00909000
                                                                        00910000
           SELECT FUTURE-PARTN-FILE-OUT                                 00920000
               ASSIGN TO OUT01.                                         00930000
                                                                        00940000
      ******************************************************************00950000
       DATA DIVISION.                                                   00960000
      ******************************************************************00970000
       FILE SECTION.                                                    00980000
                                                                        00990000
       FD  DATE-CONTROL-FILE                                            01000000
           RECORDING MODE IS F                                          01010000
           BLOCK CONTAINS 0 CHARACTERS                                  01020000
           LABEL RECORDS ARE STANDARD.                                  01030000
                                                                        01040000
       01  DATE-CONTROL-REC                 PIC  X(80).                 01050000
                                                                        01060000
                                                                        01070000
       FD  FUTURE-PARTN-FILE-OUT                                        01080000
           RECORDING MODE IS F                                          01090000
           LABEL RECORDS ARE STANDARD                                   01100000
           BLOCK CONTAINS 0 RECORDS.                                    01110000
                                                                        01120000
       01  FUTURE-PARTN-RECORD              PIC  X(80).                 01130000
                                                                        01140000
      ******************************************************************01150000
       WORKING-STORAGE SECTION.                                         01160000
      ******************************************************************01170000
                                                                        01180000
       01  ABEND-CODE                  PIC S9(04)   VALUE ZEROS         01190000
                                                    COMP SYNC.          01200000
           88  AC-MISSING-RECORD                    VALUE +4003.        01210000
           88  AC-DATA-ERROR                        VALUE +4008.        01220000
           88  AC-DB2-ERROR                         VALUE +4013.        01230000
           88  AC-CALENDAR-ROUTINE-ERROR            VALUE +4019.        01240000
                                                                        01250000
       01  PC-PROGRAM-CONSTANTS.                                        01260000
           05  PC-PROGRAM-NAME         PIC  X(08)   VALUE 'SAKUP058'.   01270000
           05  PC-OPERATIONAL          PIC  X(01)   VALUE 'O'.          01280000
           05  PC-A                    PIC  X(01)   VALUE 'A'.          01290000
           05  PC-NOT-FOUND            PIC S9(03)   VALUE +100.         01300000
           05  PC-COMP-1               PIC S9(04)   VALUE 1             01310000
                                                    COMP.               01320000
       01  PS-PROGRAM-SWITCHES.                                         01330000
           05  PS-SQL-SW                     PIC X(01)  VALUE ' '.      01340000
               88  PS-ROW-FOUND                         VALUE 'F'.      01350000
               88  PS-ROW-NOT-FOUND                     VALUE 'N'.      01360000
                                                                        01370000
      *----------------------------------------------------------       01380000
      * CONTROL CARD DATE - SCHEDULED DATE                              01390000
      *----------------------------------------------------------       01400000
       01 CD-CONTROL-DATE.                                              01410000
           05  FILLER                    PIC X(20).                     01420000
           05  CD-POS-DATE-CYMD          PIC X(8).                      01430000
           05  FILLER                    REDEFINES CD-POS-DATE-CYMD.    01440000
               10  CD-POS-CENTURY        PIC X(2).                      01450000
               10  CD-POS-YEAR           PIC X(2).                      01460000
               10  CD-POS-MONTH          PIC X(2).                      01470000
               10  CD-POS-DAY            PIC X(2).                      01480000
           05  FILLER                    PIC X(52).                     01490000
                                                                        01500000
                                                                        01510000
       01  PV-PROGRAM-VARIABLES.                                        01520000
           05  PV-PARAGRAPH-NAME       PIC  X(30)   VALUE SPACE.        01530000
           05  PV-OPERATION-MSG-1      PIC  X(40)   VALUE SPACE.        01540000
           05  PV-OPERATION-MSG-2      PIC  X(40)   VALUE SPACE.        01550000
           05  PV-DB2-OPERATION        PIC  X(30)   VALUE SPACE.        01560000
           05  PV-CURR-DATE.                                            01570000
              10 PV-CURR-DATE-CC       PIC  X(02)   VALUE SPACES.       01580000
              10 PV-CURR-DATE-YY       PIC  X(02)   VALUE SPACES.       01590000
              10 FILLER                PIC  X(01)   VALUE '-'.          01600000
              10 PV-CURR-DATE-MM       PIC  X(02)   VALUE SPACES.       01610000
              10 FILLER                PIC  X(01)   VALUE '-'.          01620000
              10 PV-CURR-DATE-DD       PIC  X(02)   VALUE SPACES.       01630000
                                                                        01640000
           05  PV-TODAYS-CTLCD-DATE REDEFINES                           01650000
                  PV-CURR-DATE         PIC  X(10).                      01660000
           05  PV-FUTURE-DATE          PIC  X(10)   VALUE SPACES.       01670000
           05  PV-YESTERDAYS-DATE      PIC  X(10)   VALUE SPACES.       01680000
           05  PV-OLDEST-PARTN         PIC  S9(4)  COMP  VALUE 0.       01690000
           05  PV-OLDEST-PARTN-DATE    PIC  X(10)   VALUE SPACES.       01700000
           05  PV-DAYS-DIFF            PIC  ZZZ,ZZZ,ZZ9-.               01710000
                                                                        01720000
                                                                        01730000
       01  PV-FUTURE-PARTN-RECORD.                                      01740000
           05  PV-REC-FUTURE-PARTN     PIC  9(04)   VALUE ZEROES.       01750000
           05  FILLER                  PIC  X(76)   VALUE SPACE.        01760000
                                                                        01770000
      ******************************************************************01780000
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01790000
      ******************************************************************01800000
                                                                        01810000
           COPY DPWS004.                                                01820000
                                                                        01830000
      ******************************************************************01840000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             01850000
      ******************************************************************01860000
                                                                        01870000
           COPY DPWS500.                                                01880000
                                                                        01890000
      ******************************************************************01900000
      *  SQL AND COBOL DECLARATIONS FOR THE PARTITION TABLE - TEPPART   01910000
      ******************************************************************01920000
                                                                        01930000
           EXEC SQL                                                     01940000
               INCLUDE TEPPART                                          01950000
           END-EXEC.                                                    01960000
                                                                        01970000
FSH13 ******************************************************************01980000
FSH13 *  SQL AND COBOL DECLARATIONS FOR THE EPT_POS_ACTV_SLS_DTE TABLE  01990000
FSH13 ******************************************************************02000000
FSH13                                                                   02010000
FSH13      EXEC SQL                                                     02020000
FSH13          INCLUDE EPT00069                                         02030000
FSH13      END-EXEC.                                                    02040000
                                                                        02050000
      ******************************************************************02060000
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                02070000
      ******************************************************************02080000
                                                                        02090000
           EXEC SQL                                                     02100000
               INCLUDE SQLCA                                            02110000
           END-EXEC.                                                    02120000
                                                                        02130000
      ******************************************************************02140000
       PROCEDURE DIVISION.                                              02150000
      ******************************************************************02160000
                                                                        02170000
      ******************************************************************02180000
       0000-MAINLINE.                                                   02190000
      ******************************************************************02200000
                                                                        02210000
           PERFORM 1000-READ-DATE-CONTROL.                              02220000
                                                                        02230000
FSH13      PERFORM 2000-EPT-SLS-DTE-RTN.                                02240000
                                                                        02250000
FSH13      PERFORM 2200-UPDATE-PARTN1-4-SALE-DATE.                      02260000
           DISPLAY 'PARTN 1 SALES DATE SET TO ' PV-TODAYS-CTLCD-DATE.   02270000
                                                                        02280000
           PERFORM 3000-GET-NEXT-PARTN-TO-ASSIGN.                       02290000
                                                                        02300000
           PERFORM 4000-UPDATE-OLDEST-OPER-DATE.                        02310000
           DISPLAY 'FUTURE DATE PARTN NBR ' PV-OLDEST-PARTN             02320000
                                 ' SET TO ' PV-FUTURE-DATE              02330000
                                                                        02340000
           PERFORM 5000-WRITE-PART-NBR-CNTL-CARD.                       02350000
                                                                        02360000
           STOP RUN.                                                    02370000
                                                                        02380000
                                                                        02390000
      ******************************************************************02400000
       1000-READ-DATE-CONTROL.                                          02410000
      *                                                                 02420000
      ******************************************************************02430000
                                                                        02440000
           OPEN INPUT DATE-CONTROL-FILE.                                02450000
                                                                        02460000
           READ DATE-CONTROL-FILE                                       02470000
             AT END                                                     02480000
               SET AC-MISSING-RECORD TO TRUE                            02490000
               MOVE '1000-READ-DATE-CONTROL'  TO PV-PARAGRAPH-NAME      02500000
               MOVE 'DATE CONTROL RECORD IS MISSING'                    02510000
                                              TO PV-OPERATION-MSG-2     02520000
               PERFORM 9999-ABEND                                       02530000
           END-READ.                                                    02540000
                                                                        02550000
           MOVE DATE-CONTROL-REC TO CD-CONTROL-DATE.                    02560000
                                                                        02570000
           MOVE CD-POS-CENTURY   TO PV-CURR-DATE-CC.                    02580000
           MOVE CD-POS-YEAR      TO PV-CURR-DATE-YY.                    02590000
           MOVE CD-POS-MONTH     TO PV-CURR-DATE-MM.                    02600000
           MOVE CD-POS-DAY       TO PV-CURR-DATE-DD.                    02610000
                                                                        02620000
           CLOSE DATE-CONTROL-FILE.                                     02630000
                                                                        02640000
           DISPLAY 'SAKUP058 MESSAGES'.                                 02650000
           DISPLAY ' '                                                  02660000
           DISPLAY 'TODAYS CTLCD DATE       = ' PV-TODAYS-CTLCD-DATE.   02670000
                                                                        02680000
           PERFORM 1200-GET-CURRENT-PARTN1-DATE.                        02690000
           MOVE EPPART-SLS-DTE                TO PV-YESTERDAYS-DATE.    02700000
           DISPLAY ' '.                                                 02710000
           DISPLAY 'YESTERDAYS DATE WAS     = '  PV-YESTERDAYS-DATE.    02720000
                                                                        02730000
           PERFORM 1300-CTLCD-DATE-DAYS-DIFF.                           02740000
                                                                        02750000
           PERFORM 1500-GET-FUTURE-DATE.                                02760000
                                                                        02770000
                                                                        02780000
                                                                        02790000
      ******************************************************************02800000
       1200-GET-CURRENT-PARTN1-DATE.                                    02810000
      * READ THE CURRENT PROCESSING DATE (PART 1 DATE).                 02820000
      *                                                                 02830000
           EXEC SQL                                                     02840000
              SELECT SLS_DTE                                            02850000
                INTO :EPPART-SLS-DTE                                    02860000
                FROM TEPPART                                            02870000
               WHERE PARTN_NBR    = 1                                   02880000
                 AND DB_STRUC_CDE = 'O'                                 02890000
                 AND STR_GP_CDE  <> 'A'                                 02900000
           END-EXEC.                                                    02910000
                                                                        02920000
           IF SQLCODE = ZERO                                            02930000
              CONTINUE                                                  02940000
           ELSE                                                         02950000
              MOVE '1200-GET-CURRENT-PARTN1-DATE ' TO PV-PARAGRAPH-NAME 02960000
              MOVE SQLCODE                         TO PV-DB2-OPERATION  02970000
              PERFORM 9100-SQL-ABEND                                    02980000
           END-IF.                                                      02990000
                                                                        03000000
                                                                        03010000
                                                                        03020000
      ******************************************************************03030000
       1300-CTLCD-DATE-DAYS-DIFF.                                       03040000
      * COMPARE PART 1 DATE AND CTLCD DATE TO GET DAYS DIFFERANCE.      03050000
      *                                                                 03060000
      * IF CTLCD DATE (NEXT DATE) IS 1 DAY GREATER THAN YESTERDAY'S     03070000
      * PARTN 1 PROCESSING DATE, IT IS VALID.                           03080000
      *                                                                 03090000
      * OTHERWISE, IT SHOULD BE CONSIDERED INVALID AND PROCESSING       03100000
      * SHOULD NOT CONTINUE.                                            03110000
      ******************************************************************03120000
                                                                        03130000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              03140000
                                                                        03150000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   03160000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   03170000
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   03180000
           SET  DPG53-LK-CMPR-DTE-ISO-FMT    TO TRUE.                   03190000
                                                                        03200000
           MOVE PV-TODAYS-CTLCD-DATE    TO DPG52-LK-DATE-INPUT.         03210000
           MOVE PV-YESTERDAYS-DATE      TO DPG53-LK-CMPR-DATE-INPUT.    03220000
                                                                        03230000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    03240000
                                                   DPG54 DPG55 DPG56.   03250000
           EVALUATE TRUE                                                03260000
               WHEN DPG54-DATE-VALID                                    03270000
                   IF DPG54-CMPR-DATE-VALID-IND = 'Y' THEN              03280000
                      MOVE DPG55-DAYS-DIFFERENCE TO PV-DAYS-DIFF        03290000
                      IF DPG55-DAYS-DIFFERENCE = 1                      03300000
                         DISPLAY 'TODAYS CTLCD DATE IS 1 GREATER THAN'  03310000
                         ' YESTERDAYS DATE - IT IS VALID'               03320000
                      ELSE                                              03330000
                         MOVE '1300-CTLCD-DATE-DAYS-DIFF '              03340000
                                                  TO PV-PARAGRAPH-NAME  03350000
                         DISPLAY 'TODAYS CTLCD DATE IS NOT 1 GT YESTER' 03360000
                         'DAYS DATE - IT IS INVALID'                    03370000
                         DISPLAY 'PV-TODAYS-CTLCD-DATE    = '           03380000
                                  PV-TODAYS-CTLCD-DATE                  03390000
                         DISPLAY 'PV-YESTERDAYS-DATE      = '           03400000
                                  PV-YESTERDAYS-DATE                    03410000
                         DISPLAY 'DIFFERANCE = ' PV-DAYS-DIFF           03420000
                         SET AC-DATA-ERROR TO TRUE                      03430000
                         PERFORM 9999-ABEND                             03440000
                      END-IF                                            03450000
                   ELSE                                                 03460000
                      MOVE '1300-CTLCD-DATE-DAYS-DIFF '                 03470000
                                                  TO PV-PARAGRAPH-NAME  03480000
                      MOVE 'ERROR COMPARING DATES'                      03490000
                                                  TO PV-OPERATION-MSG-1 03500000
                      DISPLAY 'PV-TODAYS-CTLCD-DATE    = '              03510000
                               PV-TODAYS-CTLCD-DATE                     03520000
                      DISPLAY 'PV-YESTERDAYS-DATE      = '              03530000
                               PV-YESTERDAYS-DATE                       03540000
                      DISPLAY 'THE COMPARE DATE INDICATOR: '            03550000
                               DPG54-CMPR-DATE-VALID-IND                03560000
                              ' INDICATES AN INVALID DATE '             03570000
                      PERFORM 9200-CAL-ABEND                            03580000
                   END-IF                                               03590000
               WHEN OTHER                                               03600000
                   MOVE '1300-CTLCD-DATE-DAYS-DIFF '                    03610000
                                                 TO PV-PARAGRAPH-NAME   03620000
                   MOVE 'ERROR COMPARING DATES'  TO PV-OPERATION-MSG-1  03630000
                   DISPLAY 'PV-TODAYS-CTLCD-DATE    = '                 03640000
                            PV-TODAYS-CTLCD-DATE                        03650000
                   DISPLAY 'PV-YESTERDAYS-DATE      = '                 03660000
                            PV-YESTERDAYS-DATE                          03670000
                   PERFORM 9200-CAL-ABEND                               03680000
           END-EVALUATE.                                                03690000
                                                                        03700000
                                                                        03710000
      ******************************************************************03720000
       1500-GET-FUTURE-DATE.                                            03730000
      *                                                                 03740000
      *    CALL THE CALENDAR ROUTINE TO HELP INSURE THE VALIDITY OF THE 03750000
      *    THE CTLCD DATE AND TO CALCULATE THE DATE FOR THE NEXT        03760000
      *    PARTITION CYCLE TO BE ASSIGNED - THIS WILL BE 1 DAY INTO     03770000
      *    THE FUTURE.                                                  03780000
      ******************************************************************03790000
                                                                        03800000
           INITIALIZE DPG51                                             03810000
                      DPG52                                             03820000
                      DPG53                                             03830000
                      DPG54                                             03840000
                      DPG55                                             03850000
                      DPG56.                                            03860000
                                                                        03870000
           SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE.                      03880000
           SET DPG51-INCREMENT-DATE       TO TRUE.                      03890000
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      03900000
           MOVE PV-TODAYS-CTLCD-DATE      TO DPG52-LK-DATE-INPUT.       03910000
           MOVE 1                         TO DPG51-INCR-DECR-DAYS-9.    03920000
                                                                        03930000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                03940000
                                          DPG52                         03950000
                                          DPG53                         03960000
                                          DPG54                         03970000
                                          DPG55                         03980000
                                          DPG56.                        03990000
                                                                        04000000
           EVALUATE TRUE                                                04010000
              WHEN DPG54-NO-ERROR                                       04020000
                 MOVE DPG55-DB2-ISO-DATE-FMT     TO PV-FUTURE-DATE      04030000
              WHEN OTHER                                                04040000
                   MOVE '1500-GET-FUTURE-DATE'   TO PV-PARAGRAPH-NAME   04050000
                   DISPLAY ' PV-TODAYS-CTLCD-DATE   = '                 04060000
                             PV-TODAYS-CTLCD-DATE                       04070000
                   PERFORM 9200-CAL-ABEND                               04080000
              END-EVALUATE.                                             04090000
                                                                        04100000
           DISPLAY ' '.                                                 04110000
           DISPLAY 'NEW FUTURE DATE         = ' PV-FUTURE-DATE.         04120000
                                                                        04130000
FSH13 ******************************************************************04140000
FSH13  2000-EPT-SLS-DTE-RTN.                                            04150000
FSH13 *     UPDATE THE ROW ON EPT_POS_ACTV_SLS_DTE.                     04160000
FSH13 *     IF ROW NOT FOUND, INSERT A ROW.                             04170000
FSH13 ******************************************************************04180000
FSH13                                                                   04190000
FSH13      PERFORM 2100-UPDATE-EPT-SLS-DTE.                             04200000
FSH13                                                                   04210000
FSH13      IF PS-ROW-FOUND                                              04220000
FSH13         DISPLAY                                                   04230000
FSH13            'SUCCESSFUL UPDATE OF EPT_POS_ACTV_SLS_DTE SET TO '    04240000
FSH13                                   PV-TODAYS-CTLCD-DATE            04250000
FSH13      ELSE                                                         04260000
FSH13         PERFORM 2150-INSERT-EPT-SLS-DTE                           04270000
FSH13         IF PS-ROW-FOUND                                           04280000
FSH13            DISPLAY                                                04290000
FSH13            'SUCCESSFUL INSERT OF EPT_POS_ACTV_SLS_DTE SET TO '    04300000
FSH13                                   PV-TODAYS-CTLCD-DATE            04310000
FSH13         END-IF                                                    04320000
FSH13      END-IF.                                                      04330000
FSH13                                                                   04340000
FSH13 ******************************************************************04350000
FSH13  2100-UPDATE-EPT-SLS-DTE.                                         04360000
FSH13 *     UPDATE THE ROW ON EPT_POS_ACTV_SLS_DTE.                     04370000
FSH13 ******************************************************************04380000
FSH13                                                                   04390000
FSH13      EXEC SQL                                                     04400000
FSH13         UPDATE EPT_POS_ACTV_SLS_DTE                               04410000
FSH13            SET POS_ACTV_SLS_DTE = :PV-TODAYS-CTLCD-DATE           04420000
FSH13               ,LAST_UPD_TMST    = CURRENT TIMESTAMP               04430000
FSH13      END-EXEC.                                                    04440000
FSH13                                                                   04450000
FSH13      EVALUATE TRUE                                                04460000
FSH13         WHEN SQLCODE = ZERO                                       04470000
FSH13            SET PS-ROW-FOUND     TO TRUE                           04480000
FSH13                                                                   04490000
FSH13         WHEN SQLCODE = +100                                       04500000
FSH13            SET PS-ROW-NOT-FOUND TO TRUE                           04510000
FSH13                                                                   04520000
FSH13         WHEN OTHER                                                04530000
FSH13            MOVE '2100-UPDATE-EPT-SLS-DTE'                         04540000
FSH13                                          TO PV-PARAGRAPH-NAME     04550000
FSH13            MOVE SQLCODE                  TO PV-DB2-OPERATION      04560000
FSH13            MOVE 'TRYING TO UPDATE SALES DATE '                    04570000
FSH13                                          TO PV-OPERATION-MSG-1    04580000
FSH13            PERFORM 9100-SQL-ABEND                                 04590000
FSH13      END-EVALUATE.                                                04600000
FSH13                                                                   04610000
FSH13 ******************************************************************04620000
FSH13  2150-INSERT-EPT-SLS-DTE.                                         04630000
FSH13 *     INSERT A ROW INTO EPT_POS_ACTV_SLS_DTE                      04640000
FSH13 ******************************************************************04650000
FSH13                                                                   04660000
FSH13      EXEC SQL                                                     04670000
FSH13         INSERT INTO EPT_POS_ACTV_SLS_DTE                          04680000
FSH13                  ( POS_ACTV_SLS_DTE                               04690000
FSH13                   ,LAST_UPD_TMST )                                04700000
FSH13                VALUES                                             04710000
FSH13                  ( :PV-TODAYS-CTLCD-DATE                          04720000
FSH13                   ,CURRENT TIMESTAMP )                            04730000
FSH13      END-EXEC.                                                    04740000
FSH13                                                                   04750000
FSH13      EVALUATE TRUE                                                04760000
FSH13         WHEN SQLCODE = ZERO                                       04770000
FSH13            SET PS-ROW-FOUND TO TRUE                               04780000
FSH13                                                                   04790000
FSH13         WHEN OTHER                                                04800000
FSH13            MOVE '2150-INSERT-EPT-SLS-DTE'                         04810000
FSH13                                       TO PV-PARAGRAPH-NAME        04820000
FSH13            MOVE SQLCODE               TO PV-DB2-OPERATION         04830000
FSH13            MOVE 'TRYING TO INSERT SALES DATE '                    04840000
FSH13                                       TO PV-OPERATION-MSG-1       04850000
FSH13            MOVE PV-TODAYS-CTLCD-DATE  TO PV-OPERATION-MSG-2       04860000
FSH13            PERFORM 9100-SQL-ABEND                                 04870000
FSH13      END-EVALUATE.                                                04880000
                                                                        04890000
      ******************************************************************04900000
FSH13  2200-UPDATE-PARTN1-4-SALE-DATE.                                  04910000
      *     UPDATE TEPPART TABLE CONTROL DATE, WHICH IS KEPT IN         04920000
      *     PARTITION 1.                                                04930000
      ******************************************************************04940000
                                                                        04950000
           EXEC SQL                                                     04960000
              UPDATE TEPPART                                            04970000
                 SET SLS_DTE      = :PV-TODAYS-CTLCD-DATE               04980000
               WHERE PARTN_NBR   IN (1,2,3,4)                           04990000
                 AND DB_STRUC_CDE = 'O'                                 05000000
                 AND STR_GP_CDE  <> 'A'                                 05010000
           END-EXEC.                                                    05020000
                                                                        05030000
           IF SQLCODE = ZERO                                            05040000
              CONTINUE                                                  05050000
           ELSE                                                         05060000
FSH13         MOVE '2200-UPDATE-PARTN1-4-SALE-DATE'                     05070000
                                            TO PV-PARAGRAPH-NAME        05080000
              MOVE SQLCODE                  TO PV-DB2-OPERATION         05090000
              MOVE 'TRYING TO SET PARTN 1-4 SALES DATES TO '            05100000
                                            TO PV-OPERATION-MSG-1       05110000
              MOVE PV-TODAYS-CTLCD-DATE     TO PV-OPERATION-MSG-2       05120000
              PERFORM 9100-SQL-ABEND                                    05130000
           END-IF.                                                      05140000
                                                                        05150000
      ******************************************************************05160000
       3000-GET-NEXT-PARTN-TO-ASSIGN.                                   05170000
      *     PERFORM PARA 3050-GET-UNASSIGNED-PARTN FIRST TO GET THE     05180000
      *     LOWEST UNASSIGNED OPERATIONAL PARTN NBR.                    05190000
      *     IF ALL NEW PARTNS HAVE BEEN ASSIGNED, PERFORM PARA          05200000
      *     3100-GET-OLDEST-DATE-PARTN TO FIND THE NEXT PARTN TO USE.   05210000
      *     THE DATE AND PARTITION NUMBER WILL BE USED IN PARA          05220000
      *     4000-UPDATE-OLDEST-OPER-DATE TO SET THE DATE ON THE PARTN   05230000
      *     TO THE FUTURE DATE,                                         05240000
      *     AND IN PARA 5000-WRITE-PART-NBR-CNTL-CARD TO CREATE THE     05250000
      *     FUTURE PARTITION CONTROL CARD (WHICH WILL BE USED TO CLEAR  05260000
      *     THE PARTITION).                                             05270000
      ******************************************************************05280000
                                                                        05290000
           PERFORM 3050-GET-UNASSIGNED-PARTN.                           05300000
                                                                        05310000
           IF PS-ROW-FOUND                                              05320000
              MOVE EPPART-PARTN-NBR          TO PV-OLDEST-PARTN         05330000
                                                PV-REC-FUTURE-PARTN     05340000
              MOVE EPPART-SLS-DTE            TO PV-OLDEST-PARTN-DATE    05350000
              DISPLAY ' '                                               05360000
              DISPLAY 'UNASSIGNED DATE WAS     = ' EPPART-SLS-DTE       05370000
              DISPLAY 'UNASSIGNED PARTN WAS    = ' EPPART-PARTN-NBR     05380000
           ELSE                                                         05390000
              PERFORM 3100-GET-OLDEST-DATE-PARTN                        05400000
              IF PS-ROW-FOUND                                           05410000
                 MOVE EPPART-PARTN-NBR          TO PV-OLDEST-PARTN      05420000
                                                   PV-REC-FUTURE-PARTN  05430000
                 MOVE EPPART-SLS-DTE            TO PV-OLDEST-PARTN-DATE 05440000
                 DISPLAY ' '                                            05450000
                 DISPLAY 'OLDEST DATE WAS         = ' EPPART-SLS-DTE    05460000
                 DISPLAY 'OLDEST DATE PARTN WAS   = ' EPPART-PARTN-NBR  05470000
                 PERFORM 3200-OLDEST-DATE-DAYS-DIFF                     05480000
              END-IF                                                    05490000
           END-IF.                                                      05500000
                                                                        05510000
                                                                        05520000
      ******************************************************************05530000
       3050-GET-UNASSIGNED-PARTN.                                       05540000
      *     SELECT THE LOWEST UNASSIGNED OPERATIONAL PARTN NBR.         05550000
      *     UNASSIGNED PARTN NBRS ARE INITIALIZED WITH SALES DATES      05560000
      *     = '9999-12-31'.                                             05570000
      ******************************************************************05580000
                                                                        05590000
           EXEC SQL                                                     05600000
              SELECT A.PARTN_NBR,                                       05610000
                     A.SLS_DTE                                          05620000
                INTO :EPPART-PARTN-NBR,                                 05630000
                     :EPPART-SLS-DTE                                    05640000
                FROM TEPPART A                                          05650000
               WHERE A.DB_STRUC_CDE = 'N'                               05660000
                 AND A.STR_GP_CDE   = 'N'                               05670000
                 AND A.SLS_DTE      = '9999-12-31'                      05680000
                 AND A.PARTN_NBR    =                                   05690000
                     (SELECT MIN(B.PARTN_NBR)                           05700000
                        FROM TEPPART B                                  05710000
                       WHERE B.DB_STRUC_CDE = 'N'                       05720000
                         AND B.STR_GP_CDE   = 'N'                       05730000
                         AND B.SLS_DTE      = '9999-12-31')             05740000
           END-EXEC.                                                    05750000
                                                                        05760000
           IF SQLCODE = ZERO                                            05770000
              SET PS-ROW-FOUND  TO TRUE                                 05780000
           ELSE                                                         05790000
              IF SQLCODE = +100                                         05800000
                 SET PS-ROW-NOT-FOUND  TO TRUE                          05810000
              ELSE                                                      05820000
                 MOVE '3050-GET-UNASSIGNED-PARTN' TO PV-PARAGRAPH-NAME  05830000
                 MOVE SQLCODE                     TO PV-DB2-OPERATION   05840000
                 PERFORM 9100-SQL-ABEND                                 05850000
              END-IF                                                    05860000
           END-IF.                                                      05870000
                                                                        05880000
                                                                        05890000
                                                                        05900000
      ******************************************************************05910000
       3100-GET-OLDEST-DATE-PARTN.                                      05920000
      *     (FROM EPKUT051 - PARA. 5000-)                               05930000
      *     SELECT THE OLDEST OPERATIONAL SLS_DTE AND THE PARTN_NBR     05940000
      *     ASSOCIATED WITH IT.                                         05950000
      ******************************************************************05960000
                                                                        05970000
           EXEC SQL                                                     05980000
              SELECT A.PARTN_NBR,                                       05990000
                     A.SLS_DTE                                          06000000
                INTO :EPPART-PARTN-NBR,                                 06010000
                     :EPPART-SLS-DTE                                    06020000
                FROM TEPPART A                                          06030000
               WHERE A.DB_STRUC_CDE = 'O'                               06040000
                 AND A.STR_GP_CDE   = 'A'                               06050000
                 AND A.SLS_DTE      =                                   06060000
                     (SELECT MIN(B.SLS_DTE)                             06070000
                        FROM TEPPART B                                  06080000
                       WHERE B.DB_STRUC_CDE = 'O'                       06090000
                         AND B.STR_GP_CDE   = 'A')                      06100000
           END-EXEC.                                                    06110000
                                                                        06120000
           IF SQLCODE = ZERO                                            06130000
              SET PS-ROW-FOUND  TO TRUE                                 06140000
           ELSE                                                         06150000
              MOVE '3100-GET-OLDEST-DATE-PARTN' TO PV-PARAGRAPH-NAME    06160000
              MOVE SQLCODE                      TO PV-DB2-OPERATION     06170000
              PERFORM 9100-SQL-ABEND                                    06180000
           END-IF.                                                      06190000
                                                                        06200000
                                                                        06210000
      ******************************************************************06220000
       3200-OLDEST-DATE-DAYS-DIFF.                                      06230000
      * COMPARE THE OLDEST PARTITION DATE FOUND TO THE PARTITION 1      06240000
C62728* CONTROL DATE.  IF THE DIFFERENCE IS NOT AT LEAST 493 DAYS,      06250000
      * THERE MAY BE A PROBLEM AND WE DO NOT WANT TO CLEAR THE          06260000
      * OLDEST DATE'S PARTITION - STOP PROCESSING.                      06270000
      * THIS COULD INDICATE THAT THE NEW OPERATIONAL PARTITIONS         06280000
      * WEREN'T INITIALIZED PROPERLY.                                   06290000
      * THIS PARA WILL BE REQUIRED WHEN AN OLD PARTN IS TO BE OVERLAID  06300000
      * (PARTN FOUND IN PARA 3100-GET-OLDEST-DATE-PARTN),               06310000
      * BUT IS NOT REQUIRED WHEN AN UNASSIGNED PARTN IS FOUND IN PARA   06320000
      * 3050-GET-UNASSIGNED-PARTN.                                      06330000
      ******************************************************************06340000
                                                                        06350000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              06360000
                                                                        06370000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   06380000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   06390000
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   06400000
           SET  DPG53-LK-CMPR-DTE-ISO-FMT    TO TRUE.                   06410000
                                                                        06420000
           MOVE PV-YESTERDAYS-DATE      TO DPG52-LK-DATE-INPUT.         06430000
           MOVE PV-OLDEST-PARTN-DATE    TO DPG53-LK-CMPR-DATE-INPUT.    06440000
                                                                        06450000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    06460000
                                                   DPG54 DPG55 DPG56.   06470000
           EVALUATE TRUE                                                06480000
               WHEN DPG54-DATE-VALID                                    06490000
                   IF DPG54-CMPR-DATE-VALID-IND = 'Y' THEN              06500000
                      MOVE DPG55-DAYS-DIFFERENCE TO PV-DAYS-DIFF        06510000
C62728                IF  DPG55-DAYS-DIFFERENCE > 493                   06520000
C62728                   DISPLAY 'OLDEST PARTN DATE FOUND IS GT 493'    06530000
                            ' DAYS OLD - IT IS VALID'                   06540000
                      ELSE                                              06550000
                         MOVE '3200-OLDEST-DATE-DAYS-DIFF '             06560000
                                                  TO PV-PARAGRAPH-NAME  06570000
                         DISPLAY 'OLDEST PARTN DATE FOUND IS NOT GT '   06580000
C62728                      '493 DAYS OLD - IT IS INVALID'              06590000
                         DISPLAY 'PV-YESTERDAYS-DATE      = '           06600000
                                  PV-YESTERDAYS-DATE                    06610000
                         DISPLAY 'PV-OLDEST-PARTN-DATE    = '           06620000
                                  PV-OLDEST-PARTN-DATE                  06630000
                         DISPLAY 'DAYS DIFFERANCE         = '           06640000
                                  PV-DAYS-DIFF                          06650000
                         SET AC-DATA-ERROR TO TRUE                      06660000
                         PERFORM 9999-ABEND                             06670000
                      END-IF                                            06680000
                   ELSE                                                 06690000
                      MOVE '3200-OLDEST-DATE-DAYS-DIFF '                06700000
                                                  TO PV-PARAGRAPH-NAME  06710000
                      MOVE 'ERROR COMPARING DATES'                      06720000
                                                  TO PV-OPERATION-MSG-1 06730000
                      DISPLAY 'PV-YESTERDAYS-DATE      = '              06740000
                               PV-YESTERDAYS-DATE                       06750000
                      DISPLAY 'PV-OLDEST-PARTN-DATE    = '              06760000
                               PV-OLDEST-PARTN-DATE                     06770000
                      DISPLAY 'THE COMPARE DATE INDICATOR: '            06780000
                               DPG54-CMPR-DATE-VALID-IND                06790000
                              ' INDICATES AN INVALID DATE '             06800000
                      PERFORM 9200-CAL-ABEND                            06810000
                   END-IF                                               06820000
               WHEN OTHER                                               06830000
                   MOVE '3200-OLDEST-DATE-DAYS-DIFF '                   06840000
                                                 TO PV-PARAGRAPH-NAME   06850000
                   MOVE 'ERROR COMPARING DATES'  TO PV-OPERATION-MSG-1  06860000
                   DISPLAY 'PV-YESTERDAYS-DATE      = '                 06870000
                            PV-YESTERDAYS-DATE                          06880000
                   DISPLAY 'PV-OLDEST-PARTN-DATE    = '                 06890000
                            PV-OLDEST-PARTN-DATE                        06900000
                   PERFORM 9200-CAL-ABEND                               06910000
           END-EVALUATE.                                                06920000
                                                                        06930000
                                                                        06940000
      ******************************************************************06950000
       4000-UPDATE-OLDEST-OPER-DATE.                                    06960000
      *     (FROM EPKUT052 - PARA. 5000-)                               06970000
      *     UPDATE EITHER:                                              06980000
      *                                                                 06990000
      *     THE UNASSIGNED PARTN WITH THE LOWEST PARTN NBR (DETERMINED  07000000
      *     IN PARA 3050-GET-UNASSIGNED-PARTN)                          07010000
      *         O R                                                     07020000
      *     THE OLDEST OPERATIONAL SALES DATE (DETERMINED IN            07030000
      *     PARA 3100-GET-OLDEST-DATE-PARTN)                            07040000
      *                                                                 07050000
      *     WITH THE FUTURE PARTITION DATE.                             07060000
      ******************************************************************07070000
                                                                        07080000
           EXEC SQL                                                     07090000
              UPDATE TEPPART                                            07100000
                 SET SLS_DTE               = :PV-FUTURE-DATE            07110000
                   , STR_GP_CDE            = 'A'                        07120000
                   , DB_STRUC_CDE          = 'O'                        07130000
               WHERE PARTN_NBR             = :PV-OLDEST-PARTN           07140000
                 AND SLS_DTE               = :PV-OLDEST-PARTN-DATE      07150000
                 AND DB_STRUC_CDE         <> 'H'                        07160000
           END-EXEC.                                                    07170000
                                                                        07180000
           IF SQLCODE = ZERO                                            07190000
              SET PS-ROW-FOUND              TO TRUE                     07200000
           ELSE                                                         07210000
              MOVE '4000-UPDATE-OLDEST-OPER-DATE' TO PV-PARAGRAPH-NAME  07220000
              MOVE SQLCODE                  TO PV-DB2-OPERATION         07230000
              STRING 'UPDATING PARTN '                                  07240000
                      PV-REC-FUTURE-PARTN                               07250000
                     ' OLD SALES DATE'                                  07260000
                     DELIMITED BY SIZE                                  07270000
                     INTO PV-OPERATION-MSG-1                            07280000
              STRING PV-OLDEST-PARTN-DATE                               07290000
                     ' TO '                                             07300000
                     PV-FUTURE-DATE                                     07310000
                     DELIMITED BY SIZE                                  07320000
                     INTO PV-OPERATION-MSG-2                            07330000
              PERFORM 9100-SQL-ABEND                                    07340000
           END-IF.                                                      07350000
                                                                        07360000
                                                                        07370000
      ******************************************************************07380000
       5000-WRITE-PART-NBR-CNTL-CARD.                                   07390000
      *     (FROM EPKUT051 - PARA. 6000-)                               07400000
      *     WRITE THE PARTITION NUMBER CONTROL CARD WHICH CONTAINS      07410000
      *     THE PARTITION NUMBER TO BE CLEARED OUT AND USED FOR THE     07420000
      *     NEXT FUTURE DATE (TOMORROW).                                07430000
      ******************************************************************07440000
                                                                        07450000
           OPEN OUTPUT FUTURE-PARTN-FILE-OUT.                           07460000
                                                                        07470000
           WRITE FUTURE-PARTN-RECORD     FROM PV-FUTURE-PARTN-RECORD.   07480000
                                                                        07490000
           CLOSE FUTURE-PARTN-FILE-OUT.                                 07500000
                                                                        07510000
                                                                        07520000
      ******************************************************************07530000
       9100-SQL-ABEND.                                                  07540000
      *     SQL ABEND CODE.                                             07550000
      ******************************************************************07560000
                                                                        07570000
           DISPLAY '** ABEND     **'.                                   07580000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                07590000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07600000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07610000
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-1.             07620000
           DISPLAY '** DATA      ** = ' PV-OPERATION-MSG-2.             07630000
                                                                        07640000
      *************** SQLCA MESSAGE DISPLAY COPY BOOK ******************07650000
                                                                        07660000
           COPY DPPD004.                                                07670000
                                                                        07680000
           SET AC-DB2-ERROR TO TRUE.                                    07690000
           CALL 'ILBOABN0' USING ABEND-CODE.                            07700000
                                                                        07710000
                                                                        07720000
      ******************************************************************07730000
      * 9200-CAL-ABEND                                                  07740000
      *     ABEND PROGRAM WHEN A CALENDAR ROUTINE ERROR OCCURS.         07750000
      ******************************************************************07760000
       9200-CAL-ABEND.                                                  07770000
                                                                        07780000
           DISPLAY '** ABEND           **'.                             07790000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          07800000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07810000
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-1.       07820000
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-2.       07830000
           DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            07840000
           SET AC-CALENDAR-ROUTINE-ERROR  TO TRUE.                      07850000
           DISPLAY '** ERROR MESSAGE      = ' DPG54-ERROR-MESSAGE.      07860000
           DISPLAY '** DPG51 = ' DPG51.                                 07870000
           DISPLAY '** DPG52 = ' DPG52.                                 07880000
           DISPLAY '** DPG53 = ' DPG53.                                 07890000
           DISPLAY '** DPG54 = ' DPG54.                                 07900000
           DISPLAY '** DPG55 = ' DPG55.                                 07910000
           DISPLAY '** DPG56 = ' DPG56.                                 07920000
           CALL 'ILBOABN0' USING ABEND-CODE.                            07930000
                                                                        07940000
                                                                        07950000
      ******************************************************************07960000
      * 9999-ABEND                                                      07970000
      *     ABEND PROGRAM WHEN A NON-DB2 AND NON-CALENDAR ROUTINE       07980000
      *     ERROR OCCURS.                                               07990000
      ******************************************************************08000000
       9999-ABEND.                                                      08010000
                                                                        08020000
           DISPLAY '** ABEND           **'.                             08030000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          08040000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        08050000
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-1.       08060000
           DISPLAY '** DATA            ** = ' PV-OPERATION-MSG-2.       08070000
           CALL 'ILBOABN0' USING ABEND-CODE.                            08080000
                                                                        08090000
      ******************* END OF SAKUP058 ******************************08100000
