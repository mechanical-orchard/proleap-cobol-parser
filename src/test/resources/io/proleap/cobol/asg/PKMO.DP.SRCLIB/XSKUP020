       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP020.                                         00020022
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050021
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070023
      **       VSM ITEM MESSAGE - NUMBER 120 - VENDOR UPC DELETE        00080023
      ******************************************************************00090023
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO MARK UPC'S FOR DELETION00100030
      ***     USING COLUMN LAST_ACTN_CDE (WHY? POS NEEDS A LOG OF WHAT  00110031
      ***     UPC'S HAVE BEEN DELETED TO UPDATE THE STORE FILES)        00120031
      ***************************IMPORTANT******************************00130031
      ***ANY UPC DELETES CAUSED BY A STYLE DELETE WILL NOT DELETE ANY   00140031
      ***DATA BECAUSE OF CASCADING DELETES WHEN THE STYLE WAS DELETED.  00150031
      ******************************************************************00160023
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00170023
      ******************************************************************00180023
       ENVIRONMENT DIVISION.                                            00190000
       CONFIGURATION SECTION.                                           00200000
       SOURCE-COMPUTER.  IBM-3090.                                      00210000
       OBJECT-COMPUTER.  IBM-3090.                                      00220000
       DATA DIVISION.                                                   00230000
                                                                        00240005
       WORKING-STORAGE SECTION.                                         00250000
      ******************************************************************00260005
      **PROGRAM SWITCHES                                                00270005
      ******************************************************************00280005
       01  PS-PROGRAM-SWITCHES.                                         00290005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00300005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00310005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00320005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00330027
               88  PS-ACTION-REVERSE                  VALUE 01.         00340027
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00350027
               88  PS-ACTION-NOTHING                  VALUE 02.         00360027
      ******************************************************************00370000
      **PROGRAM VARIABLES                                               00380000
      ******************************************************************00390000
       01  PC-PROGRAM-COUNTERS.                                         00400007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00410007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00420007
      ******************************************************************00430015
      **PROGRAM CALCULATIONS                                            00440015
      ******************************************************************00450015
       01  PC-PROGRAM-CALCULATIONS.                                     00460015
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00470015
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00480015
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00490015
      ******************************************************************00500000
      **PROGRAM VARIABLES                                               00510024
      ******************************************************************00520000
       01  PV-PROGRAM-VARIABLES.                                        00530024
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00540024
      ******************************************************************00550000
      **COPYBOOKS                                                       00560000
      ******************************************************************00570000
      ***VSM ITEM COPYBOOK                                              00580005
           COPY IMRD008.                                                00590033
      ***ABEND COPYBOOK                                                 00600005
           COPY DPWS004.                                                00610033
      ***DB2 MESSAGE AREA                                               00620000
           COPY SQLCA2.                                                 00630033
      ***DATE ROUTINE COPYBOOK                                          00640005
           COPY DPWS500.                                                00650033
      ******************************************************************00660000
      **TABLE INCLUSIONS                                                00670000
      ******************************************************************00680000
      ***DB2 COMMUNICATIONS AREA                                        00690000
           EXEC SQL                                                     00700000
                INCLUDE SQLCA                                           00710000
           END-EXEC.                                                    00720000
      ***XST_UPC TABLE                                                  00730011
           EXEC SQL                                                     00740000
               INCLUDE XST00012                                         00750013
           END-EXEC.                                                    00760000
                                                                        00770000
       LINKAGE SECTION.                                                 00780000
      ******************************************************************00790000
      **LINKAGE VARIABLES                                               00800000
      ******************************************************************00810000
       01  LV-IMRD008-RECORD               PIC X(450).                  00820005
       01  LV-RETURN-CODE                  PIC S9(04).                  00830005
       01  LV-ACTION-IND                   PIC 9(02).                   00840027
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00850027
                                                                        00860000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00870005
                                LV-RETURN-CODE,                         00880027
                                LV-ACTION-IND,                          00890027
                                LV-CURRENT-TIMESTAMP.                   00900027
      ******************************************************************00910000
       0000-MAINLINE.                                                   00920000
                                                                        00930000
            PERFORM 1000-INITIALIZATION.                                00940000
            PERFORM 2000-PROCESS-MESSAGE                                00950023
                    UNTIL PS-PROCESS-COMPLETE.                          00960005
            PERFORM 9999-WRAPUP.                                        00970000
                                                                        00980000
       EJECT.                                                           00990005
      ***************************************************************** 01000000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  01010007
      ***************************************************************** 01020000
       1000-INITIALIZATION.                                             01030000
                                                                        01040000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    01050024
                                                                        01060000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01070008
           INITIALIZE PC-PROGRAM-COUNTERS.                              01080026
                                                                        01090008
           MOVE LV-IMRD008-RECORD              TO IMRD008-RECORD.       01100008
                                                                        01110016
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01120016
      *     COMP FIELDS.                                                01130016
           MOVE IM008-ITEM-KEY-UPC-ID      TO PC-10-DIGIT.              01140016
           MOVE PC-10-DIGIT                TO PC-18-DIGIT.              01150016
           MOVE PC-18-DIGIT                TO PC-09-DIGIT.              01160016
           MOVE PC-09-DIGIT                TO XST00012-UPC-ID.          01170016
                                                                        01180007
       EJECT.                                                           01190005
      ***************************************************************** 01200000
      **UPDATES ACTION CODE FOR UPC_ID TO D FOR DELETED                 01210031
      ***************************************************************** 01220000
       2000-PROCESS-MESSAGE.                                            01230023
                                                                        01240000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01250024
                                                                        01260005
           EXEC SQL                                                     01270005
              UPDATE XST_UPC                                            01280030
              SET LAST_ACTN_CDE = 'D'                                   01290030
                 ,LAST_UPD_TMST = CURRENT TIMESTAMP                     01291033
              WHERE UPC_ID      = :XST00012-UPC-ID                      01300030
           END-EXEC.                                                    01310005
                                                                        01320005
           EVALUATE TRUE                                                01330005
      ***SUCCESSFUL PROCESSING                                          01340031
              WHEN SQLCODE = ZERO                                       01350005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01352033
              WHEN SQLCODE = +100                                       01360032
                   MOVE ZEROS                  TO SQLCODE               01370032
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01380008
                   DISPLAY 'XSKUP020 - UPC ID ' IM008-ITEM-KEY-UPC-ID   01381033
                           ' NOT FOUND TO DELETE'                       01382033
      ***ROW/TABLE LOCK                                                 01390031
              WHEN SQLCODE = -911                                       01400005
              WHEN SQLCODE = -913                                       01410007
                   ADD +1                      TO PC-RETRY-COUNTER      01420007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01430007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01440007
                   ELSE                                                 01450007
                       ADD +1                  TO PC-RETRY-COUNTER      01460007
                   END-IF                                               01470007
              WHEN OTHER                                                01480012
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01490005
           END-EVALUATE.                                                01500005
                                                                        01510000
       EJECT.                                                           01520005
      ******************************************************************01530000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01540000
      ******************************************************************01550000
       9999-WRAPUP.                                                     01560000
                                                                        01570000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01580028
                                                                        01590000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01600028
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01610027
                                                                        01620027
           EXIT PROGRAM.                                                01630000
                                                                        01640000
       EJECT.                                                           01650005
