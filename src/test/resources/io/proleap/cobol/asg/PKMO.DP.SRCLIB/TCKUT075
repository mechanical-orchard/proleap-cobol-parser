      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT075.                                00050000
       AUTHOR.                 DAN PAYNTER                              00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            05/2023.                                 00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  PURPOSE:                                                       00120000
      *  CREATE DAILY TRAILING TRANSACTION SUMMARY TOTALS CATEGORY      00130000
      ***************************************************************** 00145000
      *  05/31/2023    DAN PAYNTER CHG0289532                           00146000
      *  NEW PROGRAM TO CREATE TRAILING TRANSACTION CATEGORIES ON       00147000
      *  TSASUM IN ORDER TO AUTOMATE THE WEEKLY ADJUSTMENTS.            00147100
      ***************************************************************** 00148000
092523*  09/25/23      DAN PAYNTER          CHG0293030                  00149300
092523*  OUTPUT THE 'PROCESSED' TRAILING TRANSACTIONS TO OUTPUT FILE    00149400
      ***************************************************************** 00149500
       ENVIRONMENT DIVISION.                                            00150000
      ***************************************************************** 00160000
                                                                        00170000
       CONFIGURATION SECTION.                                           00180000
                                                                        00190000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
                                                                        00210000
       OBJECT-COMPUTER.        IBM-3090.                                00220000
                                                                        00230000
       INPUT-OUTPUT SECTION.                                            00240000
                                                                        00250000
       FILE-CONTROL.                                                    00260000
                                                                        00270000
           SELECT CREDIT-REPORT-FILE                                    00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
           SELECT TRAN-UPDT-FILE                                        00340000
               ASSIGN TO OUT01.                                         00341000
                                                                        00350000
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
                                                                        00430000
       FILE SECTION.                                                    00440000
                                                                        00450000
       FD  CREDIT-REPORT-FILE                                           00530000
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS.                                    00550000
                                                                        00560000
       01  CREDIT-REPORT-REC           PIC X(100).                      00570000
                                                                        00590000
       FD  TRAN-UPDT-FILE                                               00600000
           RECORDING MODE IS F                                          00610000
           LABEL RECORDS ARE STANDARD                                   00620000
           BLOCK CONTAINS 0 RECORDS                                     00630000
           RECORD CONTAINS 250 CHARACTERS                               00640000
           DATA RECORD IS TRAN-UPDT-RECORD.                             00650000
                                                                        00660000
       01  TRAN-UPDT-RECORD                PIC X(250).                  00670000
                                                                        00680000
      ******************************************************************00720000
       WORKING-STORAGE SECTION.                                         00730000
      ******************************************************************00740000
       01  FILLER                            PIC X(50)  VALUE           00750000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT075 *** '.        00760000
                                                                        00770000
        01  PS-PROGRAM-SWITCHES.                                        00990000
           05  PS-END-OF-CREDIT-SW           PIC X(01)  VALUE 'N'.      01020000
               88  PS-NOT-END-OF-CREDIT-FILE            VALUE 'N'.      01030000
               88  PS-END-OF-CREDIT-FILE                VALUE 'Y'.      01040000
                                                                        01080000
      ******************************************************************01140000
      *   PROGRAM CONSTANTS                                            *01150000
      ******************************************************************01160000
       01  PC-CONSTANTS.                                                01170000
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE           01190000
                                                       'TCKUT075'.      01190100
                                                                        01190200
       01  PROGRAM-ACCUMULATORS.                                        01191000
           05  PA-READ                       PIC S9(11) VALUE ZERO      01191100
                                                       COMP-3.          01191200
           05  PA-TTS-COUNT                  PIC S9(9) VALUE ZERO       01192000
                                                       COMP-3.          01192100
           05  PA-TTR-COUNT                  PIC S9(9) VALUE ZERO       01192200
                                                       COMP-3.          01193000
           05  PA-TTN-COUNT                  PIC S9(9) VALUE ZERO       01194000
                                                       COMP-3.          01195000
           05  PA-TTC-COUNT                  PIC S9(9) VALUE ZERO       01196000
                                                       COMP-3.          01197000
           05  PA-TTP-COUNT                  PIC S9(9) VALUE ZERO       01198000
                                                       COMP-3.          01199000
           05  PA-TTO-COUNT                  PIC S9(9) VALUE ZERO       01200000
                                                       COMP-3.          01210000
           05  PA-DISPLAY-COUNT              PIC S9(11) VALUE ZERO.     01220000
                                                                        01230000
      ******************************************************************02087000
      *               SYSTEM TIME AND DATE VARIABLES                    02088000
      ******************************************************************02089000
       01  SYS-DATE-TIME.                                               02089100
           05  SYS-DATE                     PIC  X(08) VALUE SPACES.    02089200
           05  SYS-TIME                     PIC  X(08) VALUE SPACES.    02089300
                                                                        02089400
       01  ST-BEG-TIME.                                                 02089500
           05  ST-BEG-HR                    PIC  9(02) VALUE ZEROS.     02089600
           05  FILLER                       PIC  X(01) VALUE ':'.       02089700
           05  ST-BEG-MN                    PIC  9(02) VALUE ZEROS.     02089800
                                                                        02089900
       01  ST-END-TIME.                                                 02090000
           05  ST-END-HR                    PIC  9(02) VALUE ZEROS.     02090100
           05  FILLER                       PIC  X(01) VALUE ':'.       02090200
           05  ST-END-MN                    PIC  9(02) VALUE ZEROS.     02090300
                                                                        02090400
       01  SD-EDIT-DATE.                                                02090500
           05  SD-MONTH                     PIC  9(02) VALUE ZEROS.     02090600
           05  FILLER                       PIC  X(01) VALUE '-'.       02090700
           05  SD-DAY                       PIC  9(02) VALUE ZEROS.     02090800
           05  FILLER                       PIC  X(01) VALUE '-'.       02090900
           05  SD-CENT                      PIC  9(02) VALUE ZEROS.     02091000
           05  SD-YEAR                      PIC  9(02) VALUE ZEROS.     02091100
                                                                        02460000
      ***************************************************************** 02810000
      *  CREDIT REPORT INPUT FILE                                       02820000
      ******************************************************************02830000
           COPY TCRD008.                                                02840000
                                                                        02850000
      ***************************************************************** 02851000
      *  WS FIELDS FOR TSASUM DB2 UPDATE                                02852000
      ***************************************************************** 02853000
           COPY TCRD075.                                                02854000
                                                                        02855000
       PROCEDURE DIVISION.                                              05150000
                                                                        05160000
      ******************************************************************05170000
      * 0000-MAINLINE-PARAGRAPH                                         05180000
      ******************************************************************05190000
       0000-MAINLINE-PARAGRAPH.                                         05200000
                                                                        05210000
           PERFORM 1000-INITIALIZE-PROGRAM.                             05220000
                                                                        05230000
           IF NOT PS-END-OF-CREDIT-FILE                                 05240000
               PERFORM 2000-PROCESS                                     05310000
                   UNTIL PS-END-OF-CREDIT-FILE                          05320000
           END-IF.                                                      05330000
                                                                        05340000
           CLOSE CREDIT-REPORT-FILE                                     05460000
                 TRAN-UPDT-FILE.                                        05490200
           PERFORM 9500-CONTROLS.                                       05491000
                                                                        05500000
           GOBACK.                                                      05510000
                                                                        05520000
      ******************************************************************05530000
      * 1000-INITIALIZE-PROGRAM                                         05540000
      *     OPEN FILES.                                                 05550000
      ******************************************************************05560000
       1000-INITIALIZE-PROGRAM.                                         05570000
                                                                        05580000
           OPEN INPUT  CREDIT-REPORT-FILE                               05590000
                OUTPUT TRAN-UPDT-FILE.                                  05601000
                                                                        05630000
      ******************************************************************06060000
      * PRIMING READ                                                    06070000
      ******************************************************************06090000
           PERFORM 8000-READ-CREDIT-FILE.                               06100000
                                                                        06110000
      ***************************************************************** 06240000
      * 2000-PROCESS                                                    06250000
      *        PROCESS THROUGH THE CREDIT REPORT RECORDS WRITING OUT    06260000
      *        TSASUM UPDATE RECORDS.                                   06270000
      ***************************************************************** 06300000
       2000-PROCESS.                                                    06310000
                                                                        06320000
           PERFORM 7000-WRITE-TSASUM-UPDATES.                           06718000
                                                                        06719000
           PERFORM 8000-READ-CREDIT-FILE.                               06720000
                                                                        11620000
      ******************************************************************11630000
      * 7000-WRITE-TSASUM-UPDATE                                        11640000
      *     WRITE THE OUTPUT RECORD WITH TSASUM UPDATE DETAILS.         11641000
      ******************************************************************11680000
       7000-WRITE-TSASUM-UPDATES.                                       11690000
                                                                        11760000
      * START OF STORE/REG SPECIFIC UPDATE  (TTS/TTR/TTC)               11770000
      * TRAILING TRANSACTION SALES AND TRAILING TRANSACTION RETURNS     11771000
      * TRAILING TRANSACTION PAYMENTS                                   11772000
           INITIALIZE TC075-TRAN-UPDT-RECORD.                           11780000
           MOVE TC008-PARTN-NBR        TO TC075-SASUM-PARTN-NBR.        11790000
           MOVE TC008-ORG-STR-NBR      TO TC075-SASUM-STR-NBR.          11801000
           IF TC008-ORG-STR-NBR = 0                                     11802000
               MOVE TC008-STR-NBR      TO TC075-SASUM-STR-NBR           11803000
           END-IF.                                                      11804000
           MOVE TC008-RGST-ID          TO TC075-SASUM-RGST-ID.          11810000
           EVALUATE TRUE                                                11811000
               WHEN TC008-TRN-TYP-CDE = '01'                            11811100
               WHEN TC008-TRN-TYP-CDE = '02'                            11811200
                   MOVE 'TTS'          TO TC075-SASUM-SUM-CTG-CDE       11811300
                   ADD +1 TO PA-TTS-COUNT                               11811400
               WHEN TC008-TRN-TYP-CDE = '11'                            11811500
               WHEN TC008-TRN-TYP-CDE = '12'                            11811600
               WHEN TC008-TRN-TYP-CDE = '24'                            11811700
               WHEN TC008-TRN-TYP-CDE = '25'                            11811800
                   MOVE 'TTR'          TO TC075-SASUM-SUM-CTG-CDE       11811900
                   ADD +1 TO PA-TTR-COUNT                               11812000
               WHEN TC008-TRN-TYP-CDE = '30'                            11812100
092523         WHEN TC008-TRN-TYP-CDE = '31'                            11812200
                   MOVE 'TTC'          TO TC075-SASUM-SUM-CTG-CDE       11812300
                   ADD +1 TO PA-TTC-COUNT                               11812400
               WHEN OTHER                                               11812500
                   MOVE 'TTO'          TO TC075-SASUM-SUM-CTG-CDE       11812600
                   ADD +1 TO PA-TTO-COUNT                               11812700
           END-EVALUATE.                                                11813000
           MOVE TC008-TNDR-AMT         TO TC075-SASUM-AMOUNT.           11830000
           MOVE 1                      TO TC075-SASUM-COUNT.            11840000
                                                                        11930000
           WRITE TRAN-UPDT-RECORD                                       11940000
               FROM TC075-TRAN-UPDT-RECORD.                             11950000
                                                                        11960000
      * START OF REGISTER 0 UPDATE (TTN/TTP)                            11960100
      * TRAILING TRANSACTION NET AND TRAILING TRANSACTION PAYMENTS      11960200
           INITIALIZE TC075-TRAN-UPDT-RECORD.                           11961000
           MOVE TC008-PARTN-NBR        TO TC075-SASUM-PARTN-NBR.        11962000
           IF TC008-ORG-STR-NBR = 0                                     11963100
               MOVE TC008-STR-NBR      TO TC075-SASUM-STR-NBR           11963300
           END-IF.                                                      11963400
           MOVE TC008-ORG-STR-NBR      TO TC075-SASUM-STR-NBR.          11964000
           MOVE ZERO                   TO TC075-SASUM-RGST-ID.          11966000
           EVALUATE TRUE                                                11967000
               WHEN TC008-TRN-TYP-CDE = '01'                            11968000
               WHEN TC008-TRN-TYP-CDE = '02'                            11969000
               WHEN TC008-TRN-TYP-CDE = '11'                            11969200
               WHEN TC008-TRN-TYP-CDE = '12'                            11969300
               WHEN TC008-TRN-TYP-CDE = '24'                            11969400
               WHEN TC008-TRN-TYP-CDE = '25'                            11969500
                   MOVE 'TTN'          TO TC075-SASUM-SUM-CTG-CDE       11969600
                   ADD +1 TO PA-TTN-COUNT                               11969700
               WHEN TC008-TRN-TYP-CDE = '30'                            11969800
092523         WHEN TC008-TRN-TYP-CDE = '31'                            11969900
                   MOVE 'TTP'          TO TC075-SASUM-SUM-CTG-CDE       11970000
                   ADD +1 TO PA-TTP-COUNT                               11970100
               WHEN OTHER                                               11970200
                   MOVE 'TTO'          TO TC075-SASUM-SUM-CTG-CDE       11970300
                   ADD +1 TO PA-TTO-COUNT                               11970400
           END-EVALUATE.                                                11971000
           MOVE TC008-TNDR-AMT         TO TC075-SASUM-AMOUNT.           11973000
           MOVE 1                      TO TC075-SASUM-COUNT.            11973100
                                                                        11974000
           WRITE TRAN-UPDT-RECORD                                       11975000
               FROM TC075-TRAN-UPDT-RECORD.                             11976000
                                                                        13120000
      ******************************************************************13130000
      * 8000-READ-INPUT-FILE                                            13140000
      *     READ THE ADJUSTMENT INPUT FILE                              13150000
      ******************************************************************13160000
       8000-READ-CREDIT-FILE.                                           13170000
                                                                        13180000
           READ CREDIT-REPORT-FILE INTO TC008-EXTRACT-RECORD            13190000
               AT END                                                   13200000
                   SET PS-END-OF-CREDIT-FILE TO TRUE                    13210000
           END-READ.                                                    13220000
           IF NOT PS-END-OF-CREDIT-FILE                                 13220100
               ADD 1 TO PA-READ                                         13220200
           END-IF.                                                      13220300
                                                                        13221000
           IF TC008-TRLING-TRN-IND = 'Y'                                13222000
               SET TC008-TRAILNG-TRAN TO TRUE                           13223000
           END-IF.                                                      13224000
                                                                        13230000
      ******************************************************************13421000
      * DISPLAY CONTROLS FROM THE PROGRAM                               13422000
      ******************************************************************13423000
       9500-CONTROLS.                                                   13424000
                                                                        13425000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 13426000
                                                                        13427000
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            13428000
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            13429000
                                                                        13429100
           DISPLAY '                    '.                              13429200
           DISPLAY '**********************'.                            13429300
           DISPLAY '   TCKUT075 CONTROLS  '.                            13429400
           DISPLAY '**********************'.                            13429500
           DISPLAY 'RUN DATE  = ' SD-EDIT-DATE.                         13429600
           DISPLAY 'BEG TIME  = ' ST-BEG-TIME.                          13429700
           DISPLAY 'END TIME  = ' ST-END-TIME.                          13429800
           DISPLAY '======================'.                            13429900
           DISPLAY 'RECORDS READ   = ' PA-READ.                         13430000
           DISPLAY '                '.                                  13430200
           MOVE PA-READ      TO PA-DISPLAY-COUNT.                       13430300
           DISPLAY 'INPUT RECORDS READ  = ' PA-DISPLAY-COUNT.           13431100
           MOVE PA-TTS-COUNT TO PA-DISPLAY-COUNT.                       13431200
           DISPLAY 'TTS RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431300
           MOVE PA-TTR-COUNT TO PA-DISPLAY-COUNT.                       13431400
           DISPLAY 'TTR RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431500
           MOVE PA-TTN-COUNT TO PA-DISPLAY-COUNT.                       13431600
           DISPLAY 'TTN RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431700
           MOVE PA-TTC-COUNT TO PA-DISPLAY-COUNT.                       13431800
           DISPLAY 'TTC RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13431900
           MOVE PA-TTP-COUNT TO PA-DISPLAY-COUNT.                       13432000
           DISPLAY 'TTP RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13432100
           MOVE PA-TTO-COUNT TO PA-DISPLAY-COUNT.                       13432200
           DISPLAY 'TTO RECORDS WRITTEN = ' PA-DISPLAY-COUNT.           13432300
           DISPLAY '                '.                                  13432400
                                                                        13432500
