       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    IMKUT093.                                         00020000
       AUTHOR.        PAUL KEBER.                                       00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10-01-2007.                                       00050000
       DATE-COMPILED.                                                   00060000
                                                                                
      ******************************************************************00070000
      *     IMKUT093 - ITEM DOMAIN TUPC/TSKXREF UNLOADS MERGE           00080000
      ******************************************************************00090000
      *     MERGE THE INPUT ITEM DOMAIN TUPC AND TSKXREF UNLOAD FILES   00091000
      *     ON UPC ID TO CREATE THE ITEM DOMAIN TUPC/TSKXREF UNLOADS    00092000
      *     MERGE OUTPUT FILE.                                          00092000
      *-----------------------------------------------------------------        
      *    WR/PITS#     DATE        DESCRIPTION                                 
      *    --------   --------     ---------------------------------            
      *    WR32284    07/14/2008   D. THEEL - INCREASED INPUT LRECL AND         
      *                            ADDED LOGIC TO WRITE NEW EFF BEG DTE         
      *                            FROM INPUT FILE TO OUTPUT FILE.              
W36546*                                                                         
W36546*    WR36546    08/19/2010   COGNIZANT - PROCESS THE NEW TUPC             
W36546*                            TEST PRODUCT TYPE SEQUENCE NUMBER            
W36546*                                                                         
W38861*    WR38861    08/19/2010   COGNIZANT - PROCESS THE NEW TUPC             
W38861*                            WARE HOUSE REPLENISHMENT INDICATOR           
W38861*                                                                         
      ******************************************************************00093000
      *                                                                 00094000
       ENVIRONMENT DIVISION.                                            00094100
      *                                                                 00094200
       CONFIGURATION SECTION.                                           00094300
      *                                                                 00094400
       SOURCE-COMPUTER.        IBM-3090.                                00094500
       OBJECT-COMPUTER.        IBM-3090.                                00094600
      *                                                                 00094700
       INPUT-OUTPUT SECTION.                                            00094800
       FILE-CONTROL.                                                    00094900
                                                                        00095000
           SELECT TUPC-UNLOAD-FILE                                      00095100
               ASSIGN TO INP01.                                         00095200
                                                                        00095300
           SELECT TSKXREF-UNLOAD-FILE                                   00095400
               ASSIGN TO INP02.                                         00095500
                                                                        00095600
           SELECT MERGE-OUTPUT-FILE                                     00095700
               ASSIGN TO OUT01.                                         00095800
                                                                        00095900
      ******************************************************************00096000
       DATA DIVISION.                                                   00097000
      ******************************************************************00098000
                                                                        00099000
       FILE SECTION.                                                    00100000
                                                                        00110000
       FD  TUPC-UNLOAD-FILE                                             00120000
           RECORDING MODE IS F                                          00130000
           LABEL RECORDS ARE STANDARD                                   00140000
           BLOCK CONTAINS 0 RECORDS.                                    00150000
                                                                        00160000
W38861*01  TUPC-UNLOAD-RECORD          PIC X(135).                      00170000
W38861 01  TUPC-UNLOAD-RECORD          PIC X(138).                      00170000
                                                                        00180000
       FD  TSKXREF-UNLOAD-FILE                                          00190000
           RECORDING MODE IS F                                          00200000
           LABEL RECORDS ARE STANDARD                                   00210000
           BLOCK CONTAINS 0 RECORDS.                                    00220000
                                                                        00230000
       01  TSKXREF-UNLOAD-RECORD       PIC X(19).                       00240000
                                                                        00250000
       FD  MERGE-OUTPUT-FILE                                            00260000
           RECORDING MODE IS F                                          00261000
           LABEL RECORDS ARE STANDARD                                   00262000
           BLOCK CONTAINS 0 RECORDS.                                    00263000
                                                                        00264000
       01  MERGE-OUTPUT-RECORD         PIC X(160).                      00265000
                                                                        00266000
      *                                                                 00267000
      ******************************************************************00268000
       WORKING-STORAGE SECTION.                                         00269000
      ******************************************************************00270000
                                                                        00280000
       01  PV-PROGRAM-VARIABLES.                                        00320000
           05  PV-UPC-ID-DOUBLE        PIC S9(10)     VALUE +0  COMP.           
           05  PV-TUPC-UNLOAD-READ     PIC S9(09)     VALUE +0  COMP.   00330000
           05  PV-TSKXREF-UNLOAD-READ  PIC S9(09)     VALUE +0  COMP.   00340000
           05  PV-ITEM-MATCHES-WRITTEN PIC S9(09)     VALUE +0  COMP.   00360000
           05  PV-TUPC-UNLOAD-DROPPED  PIC S9(09)     VALUE +0  COMP.   00330000
           05  PV-TSKXREF-UNLD-DROPPED PIC S9(09)     VALUE +0  COMP.   00340000
           05  PV-UPC-ID-DISPLAY       PIC Z(10)      VALUE ZERO.       00340000
                                                                        00370000
       01  PS-PROGRAM-SWITCHES.                                         00380000
           05  PS-TUPC-UNLOAD-DONE-SW  PIC   X(01)     VALUE 'N'.       00390001
               88  PS-TUPC-UNLOAD-DONE                 VALUE 'Y'.       00400000
               88  PS-TUPC-UNLOAD-NOT-DONE             VALUE 'N'.       00410000
           05  PS-TSKXREF-UNLOAD-DONE-SW PIC X(01)     VALUE 'N'.       00420001
               88  PS-TSKXREF-UNLOAD-DONE              VALUE 'Y'.       00430000
               88  PS-TSKXREF-UNLOAD-NOT-DONE          VALUE 'N'.       00440000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TUPC UNLOAD INPUT FILE       00540000
      ******************************************************************00580000
           COPY IMRD051.                                                00450000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TSKXREF UNLOAD INPUT FILE    00540000
      ******************************************************************00580000
           COPY IMRD062.                                                00450000
                                                                        00450000
      ******************************************************************00530000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TUPC/TSKXREF UNLOADS MERGE   00540000
      *  OUTPUT FILE                                                    00540000
      ******************************************************************00580000
           COPY IMRD063.                                                00450000
                                                                        00590000
      ******************************************************************00600000
       PROCEDURE DIVISION.                                              00610000
      ******************************************************************00620000
                                                                        00630000
       0100-MAINLINE-IMKUT093.                                          00640000
      *                                                                 00650000
           PERFORM 1000-INITIALIZATION.                                 00651000
                                                                        00652000
           PERFORM 2000-PROCESS-ITEM-RECS                               00653000
               UNTIL PS-TUPC-UNLOAD-DONE                                00654000
                 OR  PS-TSKXREF-UNLOAD-DONE.                            00655000
                                                                        00656000
           PERFORM 8000-EOJ.                                            00657000
                                                                        00658000
           GOBACK.                                                      00659000
                                                                        00660000
       EJECT                                                            00670000
                                                                        00680000
      ******************************************************************00690000
      *  OPEN FILES AND READ THE FIRST RECORD OF EACH INPUT FILE.       00700000
      ******************************************************************00710000
       1000-INITIALIZATION.                                             00720000
      *                                                                 00730000
           OPEN INPUT  TUPC-UNLOAD-FILE                                 00740000
                       TSKXREF-UNLOAD-FILE.                             00750000
           OPEN OUTPUT MERGE-OUTPUT-FILE.                               00760000
                                                                        00770000
           MOVE +0 TO IM051-UPC-ID.                                     00770000
           MOVE +0 TO IM062-INTR-UPC-ID.                                00770000
           MOVE LOW-VALUES TO IM063-FILLER.                             00770000
                                                                        00770000
           PERFORM 4000-READ-TUPC-UNLOAD-DATA.                          00780000
           PERFORM 4100-READ-TSKXREF-UNLOAD-DATA.                       00790000
                                                                        00800000
           DISPLAY '****************************************************00810000
      -            '**'.                                                00820000
                                                                        00830000
       EJECT                                                            00840000
                                                                        00850000
      ******************************************************************00860000
      *  FIND ANY MATCHING ITEMS WITH THE SAME UPC ID                   00870000
      ******************************************************************00880000
       2000-PROCESS-ITEM-RECS.                                          00890000
      *                                                                 00900000
           IF IM051-UPC-ID = IM062-INTR-UPC-ID                          00910000
               PERFORM 5000-CREATE-MERGE-FILE-REC                       00940000
               PERFORM 4000-READ-TUPC-UNLOAD-DATA                       00950000
               PERFORM 4100-READ-TSKXREF-UNLOAD-DATA                    00960000
           ELSE                                                         00970000
               IF IM051-UPC-ID > IM062-INTR-UPC-ID                      00971000
                   ADD +1 TO PV-TSKXREF-UNLD-DROPPED                            
                   MOVE IM062-INTR-UPC-ID TO PV-UPC-ID-DOUBLE                   
                   MOVE PV-UPC-ID-DOUBLE  TO PV-UPC-ID-DISPLAY                  
                   DISPLAY 'EXTRA TSKXREF UNLOAD RECORD DROPPED FOR UPC         
      -                    'ID: ' PV-UPC-ID-DISPLAY                             
                   PERFORM 4100-READ-TSKXREF-UNLOAD-DATA                01010000
               ELSE                                                     01011000
                   ADD +1 TO PV-TUPC-UNLOAD-DROPPED                             
                   MOVE IM051-UPC-ID      TO PV-UPC-ID-DOUBLE                   
                   MOVE PV-UPC-ID-DOUBLE  TO PV-UPC-ID-DISPLAY                  
                   DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID:        
      -                    ' ' PV-UPC-ID-DISPLAY                                
                   PERFORM 4000-READ-TUPC-UNLOAD-DATA                   01011100
               END-IF                                                   01016200
           END-IF.                                                      01016900
                                                                        01017000
       EJECT                                                            01018000
                                                                        01019000
      ***************************************************************** 01020000
      *  READ AN ITEM DOMAIN TUPC UNLOAD FILE RECORD                    01030000
      ***************************************************************** 01040000
       4000-READ-TUPC-UNLOAD-DATA.                                      01050000
      *                                                                 01060000
           READ TUPC-UNLOAD-FILE INTO IM051-ITEM-TUPC-UNLD-REC          01070000
             AT END                                                     01080000
                 SET PS-TUPC-UNLOAD-DONE TO TRUE                        01090001
             NOT AT END                                                 01110000
                 ADD +1   TO PV-TUPC-UNLOAD-READ                        01120000
           END-READ.                                                    01130000
                                                                        01140000
      ***************************************************************** 01150000
      *  READ AN ITEM DOMAIN TSKXREF UNLOAD FILE RECORD                 01160000
      ***************************************************************** 01170000
       4100-READ-TSKXREF-UNLOAD-DATA.                                   01180000
                                                                        01190000
           READ TSKXREF-UNLOAD-FILE INTO IM062-ITEM-TSKXREF-UNLD-REC    01200000
             AT END                                                     01210000
                 SET PS-TSKXREF-UNLOAD-DONE TO TRUE                     01090001
             NOT AT END                                                 01230000
                 ADD +1   TO PV-TSKXREF-UNLOAD-READ                     01240000
           END-READ.                                                    01250000
                                                                        01260000
       EJECT                                                            01270000
                                                                        01280000
      ******************************************************************01290000
      *  CREATE AN ITEM DOMAIN TUPC/TSKXREF UNLOADS MERGE FILE RECORD   01300000
      ******************************************************************01310000
       5000-CREATE-MERGE-FILE-REC.                                      01320000
      *                                                                 01330000
           MOVE IM051-UPC-ID             TO IM063-UPC-ID.               01340000
           MOVE IM051-SUB-CL-MDSEAR-ID   TO IM063-SUB-CL-MDSEAR-ID.     01340000
           MOVE IM051-VS-ID              TO IM063-VS-ID.                01340000
           MOVE IM062-ITM-NBR            TO IM063-ITM-NBR.              01340000
           MOVE IM062-SKU                TO IM063-SKU.                  01340000
           MOVE IM051-INTR-UPC-DESC      TO IM063-INTR-UPC-DESC.        01340000
           MOVE IM062-SKU-STAT-CDE       TO IM063-SKU-STAT-CDE.         01340000
           MOVE IM051-INNR-PK-QTY        TO IM063-INNR-PK-QTY.          01340000
           MOVE IM051-OUTR-PK-QTY        TO IM063-OUTR-PK-QTY.          01340000
           MOVE IM051-KOHLS-SIZE-CDE     TO IM063-KOHLS-SIZE-CDE.       01340000
           MOVE IM051-NRF-CLOR-CDE       TO IM063-NRF-CLOR-CDE.         01340000
           MOVE IM051-CSTM-CLOR-DESC     TO IM063-CSTM-CLOR-DESC.       01340000
           MOVE IM051-CUSTFACG-CLOR-DESC TO IM063-CUSTFACG-CLOR-DESC.   01340000
           MOVE IM051-REGN-PRIC-IND      TO IM063-REGN-PRIC-IND.        01340000
           MOVE IM051-RPLNMT-IND         TO IM063-RPLNMT-IND.           01340000
           MOVE IM051-KY-ITM-IND         TO IM063-KY-ITM-IND.           01340000
           MOVE IM051-POG-IND            TO IM063-POG-IND.              01340000
           MOVE IM051-GP-CDE             TO IM063-GP-CDE.               01340000
           MOVE IM051-SEA-SEQ-NBR        TO IM063-SEA-SEQ-NBR.          01340000
           MOVE IM051-UNT-COST-1ST-AMT   TO IM063-UNT-COST-1ST-AMT.     01340000
           MOVE IM051-TX-PRD-CDE-SEQ-NBR TO IM063-TX-PRD-CDE-SEQ-NBR.   01340000
           MOVE IM051-WT-UM-CDE          TO IM063-WT-UM-CDE.            01340000
           MOVE IM051-WT-UM-QTY          TO IM063-WT-UM-QTY.            01340000
           MOVE IM051-SIZE-UM-CDE        TO IM063-SIZE-UM-CDE.          01340000
           MOVE IM051-WD-UM-QTY          TO IM063-WD-UM-QTY.            01340000
           MOVE IM051-HT-UM-QTY          TO IM063-HT-UM-QTY.            01340000
           MOVE IM051-DPTH-UM-QTY        TO IM063-DPTH-UM-QTY.          01340000
           MOVE IM051-TRNSTN-IND         TO IM063-TRNSTN-IND.           01340000
           MOVE IM051-PO-SHRT-CYC-IND    TO IM063-PO-SHRT-CYC-IND.              
           MOVE IM051-EFF-BEG-DTE        TO IM063-EFF-BEG-DTE.                  
W36546     MOVE IM051-TST-PRD-TYP-SEQ-NBR                                       
W36546                                   TO IM063-TST-PRD-TYP-SEQ-NBR.          
W38861     MOVE IM051-WHSE-RPLNT-IND     TO IM063-WHSE-RPLNT-IND.               
                                                                                
           WRITE MERGE-OUTPUT-RECORD                                    01341000
               FROM IM063-ITEM-TUPC-MERGE-REC.                          01350000
                                                                        01360000
           ADD 1 TO PV-ITEM-MATCHES-WRITTEN.                            01370000
                                                                        01380000
      ******************************************************************01390000
      *  FINAL PROCESSING FOR THE PROGRAM.                              01400000
      ******************************************************************01410000
       8000-EOJ.                                                        01420000
      *                                                                 01430000
           IF PS-TUPC-UNLOAD-NOT-DONE                                           
               PERFORM 8100-EXTRA-TUPC-RECS                                 0065
                 UNTIL PS-TUPC-UNLOAD-DONE                                  0065
           END-IF.                                                      01016900
                                                                        01430000
           IF PS-TSKXREF-UNLOAD-NOT-DONE                                        
               PERFORM 8200-EXTRA-TSKXREF-RECS                              0065
                 UNTIL PS-TSKXREF-UNLOAD-DONE                               0065
           END-IF.                                                      01016900
                                                                                
           DISPLAY ' '.                                                 01440000
           DISPLAY '*** IMKUT093 RECORD COUNT SUMMARY'.                 01450000
           DISPLAY 'TUPC UNLOAD RECORDS READ   = '  PV-TUPC-UNLOAD-READ.1460000 
           DISPLAY 'TSKXREF UNLOAD RECS READ   = '                      01470000
                 PV-TSKXREF-UNLOAD-READ.                                        
           DISPLAY 'ITEM MERGE RECORDS WRITTEN = '                      01480000
                 PV-ITEM-MATCHES-WRITTEN.                               01490000
           DISPLAY 'TUPC UNLOAD RECS DROPPED   = '                      01460000
                 PV-TUPC-UNLOAD-DROPPED.                                01490000
           DISPLAY 'TSKXREF UNLD RECS DROPPED  = '                      01460000
                 PV-TSKXREF-UNLD-DROPPED.                               01490000
           DISPLAY '****************************************************00810000
      -            '**'.                                                00820000
                                                                        01500000
           CLOSE TUPC-UNLOAD-FILE                                       01510000
                 TSKXREF-UNLOAD-FILE                                    01520000
                 MERGE-OUTPUT-FILE.                                     01530000
                                                                        01540000
      ******************************************************************01390000
      *  PROCESS EXTRA TUPC RECORDS.                                    01400000
      ******************************************************************01410000
       8100-EXTRA-TUPC-RECS.                                            01420000
      *                                                                 01430000
           ADD +1 TO PV-TUPC-UNLOAD-DROPPED.                                    
           MOVE IM051-UPC-ID      TO PV-UPC-ID-DOUBLE.                          
           MOVE PV-UPC-ID-DOUBLE  TO PV-UPC-ID-DISPLAY.                         
           DISPLAY 'EXTRA TUPC UNLOAD RECORD DROPPED FOR UPC ID: '              
                   PV-UPC-ID-DISPLAY.                                           
           PERFORM 4000-READ-TUPC-UNLOAD-DATA.                                  
                                                                                
      ******************************************************************01390000
      *  PROCESS EXTRA TSKXREF RECORDS.                                 01400000
      ******************************************************************01410000
       8200-EXTRA-TSKXREF-RECS.                                         01420000
      *                                                                 01430000
           ADD +1 TO PV-TSKXREF-UNLD-DROPPED.                                   
           MOVE IM062-INTR-UPC-ID TO PV-UPC-ID-DOUBLE.                          
           MOVE PV-UPC-ID-DOUBLE  TO PV-UPC-ID-DISPLAY.                         
           DISPLAY 'EXTRA TSKXREF UNLOAD RECORD DROPPED FOR UPC ID: '           
                    PV-UPC-ID-DISPLAY.                                          
           PERFORM 4100-READ-TSKXREF-UNLOAD-DATA.                               
                                                                                
