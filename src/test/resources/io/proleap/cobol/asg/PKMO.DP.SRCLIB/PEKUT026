       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.      PEKUT026.                                               
       AUTHOR.          KARI SUTTON.                                            
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    JANUARY 1998.                                           
       DATE-COMPILED.                                                           
      ******************************************************************        
      * THIS PROGRAM READS A PRICE CHANGE FILE AND CHANGES THE FISCAL  *        
      * YEAR, FISCAL MONTH, AND FISCAL WEEK IF THEY DON'T MATCH THE    *        
      * FISCAL CONTROL CARD THAT IS READ.                              *        
      *                                                                *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * NOTES:                                                         *        
      *                                                                *        
      * IN LEDGER PROGRAM MLKUP020, IF THE REASON CODE IS SPACES OR    *        
      * ZEROES, THE LEDGER BATCH NUMBER IS MOVED TO THE REASON CODE.   *        
      *                                                                *        
      * THE CHANGES TO PASS ALONG THE REASON CODE TO THE LEDGER FILES  *        
      * WERE INSTALLED IN THE MIDDLE OF A PERIOD.  WHEN THE FIRST GDG  *        
      * RAN THROUGH, THERE WERE REASONS OF '888' AND '999'.  SO, THIS  *        
      * PROGRAM CHECKS TO SEE IF THE BATCH CODE AND REASON CODE ARE    *        
      * EQUAL TO '888' (INSTR DOCUMENTS) OR '999' (BUYER GENERATED     *        
      * DOCUMENTS.  IF THEY ARE EQUAL THE REASON CODE IS RETRIEVED     *        
      * FROM THE PERM TABLES.                                          *        
      *                                                                *        
      * THE SAME THING HAPPENDED FOR DEBIT ALLOWANCES.  STORE NUMBER   *        
      * 400 INDICATES A DEBIT ALLOWANCE.  IN THE PROGRAM THAT PROCESSES*        
      * THE DATA ENTRY FILE, WE MOVED '999' TO THE REASON CODE.        *        
      * HOWEVER, THE FIRST TIME THROUGH THERE WERE REASON CODE OF '410'*        
      * '420'; WHICH MATCHED THE BATCH NUMBERS.                        *        
      ******************************************************************        
      * 12/21/98 - RAJ -  WR NO. 6041 (Y2K)                            *        
      *    - DELETE HARD CODED CENTURY ALONG WITH OTHER Y2K CHANGES    *        
      ******************************************************************        
      * 01/10/00 - JIM ARENDT WR NO. PM00336970                        *        
      *    - CHANGED ALL ILB0ABNO TO ILBOABN0.                         *        
      ******************************************************************        
      * 10/28/03 - MOYER    WR NO. WR0024472                           *        
      *    - RECOMPILE FOR MLRD023 CHANGE OVER A YEAR AGO              *        
      ******************************************************************        
      * 05/12/04 - TODD C. MICHALEK -  WR NO. WR26602                  *        
      *    - CHANGE THE "IF ML023-PC-STORE-NBR = '400'" STATEMENT IN   *        
      *      B200-PROCESS-PRCHG-RECORD TO                              *        
      *      "IF ML023-PC-STORE-NBR = '00400'"                         *        
      *    - BECAUSE ML023-PC-STORE-NBR IS NOW 5 BYTES LONG, NEED TO   *        
      *      ADD PC-STORE-NBR-5X AND MOVE PV-STR-NBR TO STRPPC-STR-NBR *        
      *      IN C500-GET-INSTR-REASON                                  *        
      ******************************************************************        
      * 02/14/05 - TCM -  WR NO. 27559                                 *        
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4    *        
      *      DIGITS IN LENGTH                                          *        
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE *        
      *      CALLS                                                     *        
W26606* 01/24/06 - JOHN SELWITSCHKA -  WR NO. WR28582                  *        
W26606*    - PRICE CHANGE COPYBOOKS EXPANSION.                         *        
W26606*      MODIFIED PRICE-CHANGE-FILE LRECL (IN AND OUT).            *        
P41629* P001041629 07-01-2007 ROD JANSSEN INVALID REASON CODE FOR BYR           
P41629*                                   MARKDOWN.                             
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PRICE-CHANGE-FILE-IN                                          
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN                       TO INP02.                           
                                                                                
           SELECT PRICE-CHANGE-FILE-OUT                                         
               ASSIGN                       TO OUT01.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PRICE-CHANGE-FILE-IN                                                 
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
W26606 01  PRICE-CHANGE-RECORD-IN       PIC X(130).                             
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  CONTROL-CARD-RECORD          PIC X(80).                              
                                                                                
       FD  PRICE-CHANGE-FILE-OUT                                                
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
W26606 01  PRICE-CHANGE-RECORD-OUT      PIC X(130).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE                
                                                           'PEKUT026'.          
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME            PIC X(30)      VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(30)      VALUE SPACES.        
           05  PV-FISCAL-CCYY.                                                  
               10  PV-FISCAL-CC             PIC X(02)      VALUE SPACES.        
               10  PV-FISCAL-YY             PIC X(02)      VALUE SPACES.        
           05  PV-FISCAL-MONTH              PIC X(02)      VALUE SPACES.        
           05  PV-FISCAL-WEEK               PIC 9(01)      VALUE ZERO.          
           05  PV-RSN-CDE.                                                      
               10  FILLER                   PIC X(01)      VALUE '0'.           
               10  PV-RSN-CDE-VALUE         PIC X(02)      VALUE SPACES.        
           05  PV-STORE-RSN-CDE.                                                
               10  PV-STORE-TYPE-RSN        PIC X(01)      VALUE SPACES.        
               10  PV-STORE-CDE-RSN         PIC X(02)      VALUE SPACES.        
           05  PV-STORE-NBR-5X.                                                 
               10  FILLER                   PIC  X(01)     VALUE ZEROES.        
               10  PV-STORE-NBR             PIC  9(04)     VALUE ZEROES.        
           05  PV-ERROR-LITERAL.                                                
               10  FILLER                   PIC X(13) VALUE                     
                                                        '**  RETURNED '..       
               10  PV-ERROR-IND             PIC X(16) VALUE ' '.                
                   88  PV-WARNING          VALUE  'WARNING ERROR. *'.           
                   88  PV-SEVERE           VALUE  'SEVERE ERROR.  *'.           
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-PRCHG-FILE-SW      PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-PRCHG-FILE                    VALUE 'Y'.           
           05  PS-END-OF-CONTROL-FILE-SW    PIC X(01)      VALUE 'N'.           
               88  PS-END-OF-CNTL-CRD-FILE                 VALUE 'Y'.           
      ******************************************************************        
      * PA PROGRAM ACCUMULATORS                                        *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-PRCHG-FILE-RECS-READ      PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-PRCHG-FILE-RECS-WRITTEN   PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-PRCHG-FILE-RECS-INSTR     PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-PRCHG-FILE-RECS-BUYER     PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
           05  PA-PRCHG-FILE-RECS-DEBIT     PIC S9(11)     COMP-3               
                                                           VALUE ZEROES.        
      ******************************************************************        
      * CR CONTROL CARD RECORD                                         *        
      ******************************************************************        
       01  CR-CONTROL-CARD-RECORD.                                              
           05  CR-CALENDAR-TYPE             PIC  X(02)     VALUE SPACES.        
           05  FILLER                       PIC  X(01)     VALUE SPACES.        
           05  CR-PERIOD-END-DATE           PIC  X(10)     VALUE SPACES.        
           05  FILLER                       PIC  X(67)     VALUE SPACES.        
      ******************************************************************        
      *    MERCHANDISE LEDGER PRICE CHANGE RECORD                      *        
      ******************************************************************        
W26606     COPY MLRD023.                                                        
      ******************************************************************        
      *    CALENDAR ROUTINE COPY MEMBERS                               *        
      ******************************************************************        
           COPY DPWS500.                                                        
      ******************************************************************        
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *        
      ******************************************************************        
           COPY DPWS004.                                                        
      ******************************************************************        
      *    PE-DEPARTMENT MARKUP/MARKDOWN TABLE                         *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCENPPC                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSTRPPC                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    SQL COMMUNICATIONS AREA DCLGEN                              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
      ******************************************************************        
      * AC ABEND CODES                                                 *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04)     VALUE ZEROES         
                                                           COMP                 
                                                           SYNC.                
           88  AC-ABEND-4013-DB2-ERROR                     VALUE +4013.         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       A100-MAINLINE.                                                           
      ******************************************************************        
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS-PRCHG-RECORDS                                   
               UNTIL PS-END-OF-PRCHG-FILE.                                      
                                                                                
           PERFORM B300-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      *     B100-INITIALIZE                                            *        
      * ==> PROCESSES:                                                 *        
      *     - OPENS THE PRICE CHANGE FILES AND THE CONTROL CARD FILE.  *        
      *     - CALLS THE CALENDAR ROUTINE TO GET THE FISCAL YEAR, MONTH,*        
      *       AND WEEK OF THE CONTROL CARD DATE THAT IS PASSED IN.     *        
      *     - READS THE FIRST RECORD FROM THE PRICE CHANGE FILE.       *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B100-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  PRICE-CHANGE-FILE-IN                                     
                       CONTROL-CARD-FILE.                                       
           OPEN OUTPUT PRICE-CHANGE-FILE-OUT.                                   
                                                                                
           PERFORM C100-READ-CONTROL-CARD.                                      
           PERFORM C200-CALL-CALENDAR-ROUTINE.                                  
           PERFORM C300-READ-PRICE-CHANGE-FILE.                                 
                                                                                
      ******************************************************************        
      *     B200-PROCESS-PRCHG-RECORDS                                 *        
      * ==> PROCESSES:                                                 *        
      *     - TESTS TO SEE IF THE FISCAL YEAR, MONTH, AND WEEK ON THE  *        
      *       PRICE CHANGE RECORD EQUALS THE FISCAL YEAR, MONTH, AND   *        
      *       WEEK OF THE CONTROL CARD DATE.  IF THEY ARE NOT EQUAL    *        
      *       THE CONTROL DATE'S FISCAL VALUES ARE MOVED TO THE PRICE  *        
      *       CHANGE RECORD.                                           *        
      *     - READS THE NEXT PRICE CHANGE RECORD                       *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B200-PROCESS-PRCHG-RECORDS.                                              
                                                                                
           IF ML023-PC-FSCL-YR = PV-FISCAL-CCYY                                 
               CONTINUE                                                         
           ELSE                                                                 
              MOVE PV-FISCAL-CCYY TO ML023-PC-FSCL-YR                           
           END-IF.                                                              
                                                                                
           IF ML023-PC-FSCL-PER = PV-FISCAL-MONTH                               
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-FISCAL-MONTH TO ML023-PC-FSCL-PER                        
           END-IF.                                                              
                                                                                
           IF ML023-PC-FSCL-WK = PV-FISCAL-WEEK                                 
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-FISCAL-WEEK TO ML023-PC-FSCL-WK                          
           END-IF.                                                              
                                                                                
           EVALUATE ML023-PC-BATCH-NBR                                          
               WHEN '0888'                                                      
                   IF ML023-PC-MKDN-MKUP-RSN-CDE = '888'                        
                       PERFORM C500-GET-INSTR-REASON                            
                       ADD +1     TO PA-PRCHG-FILE-RECS-INSTR                   
                   END-IF                                                       
               WHEN '0999'                                                      
                   IF ML023-PC-MKDN-MKUP-RSN-CDE = '999'                        
                       PERFORM C600-GET-BUYER-REASON                            
                       ADD +1     TO PA-PRCHG-FILE-RECS-BUYER                   
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           IF ML023-PC-STORE-NBR = '00400'                                      
               IF ML023-PC-MKDN-MKUP-RSN-CDE = '999'                            
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE '999' TO ML023-PC-MKDN-MKUP-RSN-CDE                     
                   ADD +1     TO PA-PRCHG-FILE-RECS-DEBIT                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     THE MEANING OF THE VALUES IN ML023-PC-CATEGORY ARE IN      *        
      *     MLRD012O UNDER ML012O-MKDN-MKON-CATEGORY.  THE VALUES      *        
      *     THEY ARE CONVERTED TO INDICATE THE ORIGIN AND ALSO THE     *        
      *     ORDER THAT THE MARKDOWN / MARKUP REPORTS WILL BE SORTED IN.*        
      *                                                                *        
      *     FOR EXAMPLE, BUYER GENERATED MARKDOWNS/UPS ARE THE FIRST   *        
      *     ONES LISTED ON THE REPORT, SO THE ORIGIN CODE IS 1 FOR     *        
      *     BUYERS.  THE NEXT MARKDOWNS/UPS ARE THE IN-STORES, SO      *        
      *     THE ORIGIN CODE IS 2 AND SO ON.                            *        
      ******************************************************************        
           EVALUATE ML023-PC-CATEGORY                                           
               WHEN '1'                                                         
                   MOVE '3'            TO ML023-PC-CATEGORY                     
               WHEN '2'                                                         
                   MOVE '4'            TO ML023-PC-CATEGORY                     
               WHEN '3'                                                         
                   MOVE '5'            TO ML023-PC-CATEGORY                     
               WHEN '4'                                                         
                   MOVE '1'            TO ML023-PC-CATEGORY                     
               WHEN '5'                                                         
                   MOVE '2'            TO ML023-PC-CATEGORY                     
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    MARKUPS ARE REALLY MARKDOWN CANCELLATIONS SO THEY SHOULD    *        
      *    BE NEGATIVE, AND MARKUP CANCELLATIONS WOULD THEREFORE BE    *        
      *    POSITIVE.                                                   *        
      ******************************************************************        
           IF ML023-PC-MKUP OR ML023-PC-MKUP-CANCEL                             
               MULTIPLY -1 BY ML023-PC-EXTD-RTL-AMT                             
           END-IF.                                                              
                                                                                
           PERFORM C400-WRITE-PRICE-CHANGE-FILE.                                
                                                                                
           PERFORM C300-READ-PRICE-CHANGE-FILE.                                 
                                                                                
      ******************************************************************        
      *     B300-EOJ-ROUTINE                                           *        
      * ==> PROCESSES:                                                 *        
      *     - CLOSES THE PRICE-CHANGE-FILES AND THE CONTROL CARD FILE. *        
      *     - DISPLAYS THE NUMBER OF PRICE CHANGE RECORDS READ AND     *        
      *       THE NUMBER OF RECORDS WRITTEN.                           *        
      * ==> PERFORMED FROM: A100-MAINLINE                              *        
      ******************************************************************        
       B300-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE PRICE-CHANGE-FILE-IN                                           
                 CONTROL-CARD-FILE                                              
                 PRICE-CHANGE-FILE-OUT.                                         
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS READ:  '               
                    PA-PRCHG-FILE-RECS-READ.                                    
                                                                                
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS WRIT:  '               
                    PA-PRCHG-FILE-RECS-WRITTEN.                                 
                                                                                
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS INST:  '               
                    PA-PRCHG-FILE-RECS-INSTR.                                   
                                                                                
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS BUYE:  '               
                    PA-PRCHG-FILE-RECS-BUYER.                                   
                                                                                
           DISPLAY 'TOTAL NUMBER OF PRICE CHANGE RECORDS DEBI:  '               
                    PA-PRCHG-FILE-RECS-DEBIT.                                   
                                                                                
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      *     C100-READ-CONTROL-CARD                                     *        
      * ==> PROCESSES:                                                 *        
      *     - READS THE CONTROL CARD FILE                              *        
      *     - IF THE FILE IS EMPTY, THE PROGRAM ABENDS                 *        
      * ==> PERFORMED FROM: B100-INITIALIZE                            *        
      ******************************************************************        
       C100-READ-CONTROL-CARD.                                                  
                                                                                
           READ CONTROL-CARD-FILE INTO CR-CONTROL-CARD-RECORD                   
               AT END                                                           
                   SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                         
                                                                                
           IF PS-END-OF-CNTL-CRD-FILE                                           
               DISPLAY ' '                                                      
               DISPLAY '** ABEND     **'                                        
               DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME                    
               DISPLAY '** PARAGRAPH **  = C100-READ-CONTROL-CARD'              
               DISPLAY '** CONTROL FILE IS EMPTY  '                             
               DISPLAY ' '                                                      
               MOVE +4002 TO ABEND-CODE                                         
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           ELSE                                                                 
               DISPLAY ' '                                                      
               DISPLAY 'CONTROLS FOR PROGRAM ' PC-PROGRAM-NAME ': '             
               DISPLAY ' '                                                      
               DISPLAY 'CONTROL CARD DATE:  ' CR-PERIOD-END-DATE                
               DISPLAY ' '                                                      
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C200-CALL-CALENDAR-ROUTINE                                 *        
      * ==> PROCESSES:                                                 *        
      *     -CALLS THE CALENDAR ROUTINE TO RETRIEVE THE FISCAL YEAR,   *        
      *      MONTH, AND WEEK                                           *        
      * ==> PERFORMED FROM:  B100-INITIALIZE                           *        
      *       CR-CALENDAR-TYPE AND CR-PERIOD-END-DATE COME FROM       *        
      *        CONTROL-CARD-FILE (READ INTO CR-CONTROL-CARD-RECORD IN  *        
      *        PARAGRAPH C100-READ-CONTROL-CARD.) Ù                    *        
      ******************************************************************        
       C200-CALL-CALENDAR-ROUTINE.                                              
                                                                                
           MOVE CR-CALENDAR-TYPE            TO DPG51-CALENDAR-TYPE.             
           SET  DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                            
           MOVE CR-PERIOD-END-DATE          TO DPG52-LK-DATE-INPUT.             
           SET  DPG52-LK-DTE-ISO-FMT        TO TRUE.                            
           INITIALIZE DPG53.                                                    
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-NO-ERROR                                              
                   CONTINUE                                                     
               WHEN DPG54-WARNING-ERROR                                         
                   SET PV-WARNING TO TRUE                                       
                   PERFORM Z811-DATE-ROUTINE-ERROR                              
                   DISPLAY                                                      
                    '* PLEASE INVESTIGATE FURTHER.                   *'         
                                                                                
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
      * -------------------------------------------------------------- *        
      * *      EXAMINE THE "SEVERE ERROR" RESPONSE FROM THE DATE RTN.* *        
      * *      THESE CONDITIONS ARE *NOT* MUTUALLY EXCLUSIVE;        * *        
      * *      EACH CONDITION IS DESCRIBED BY A SEPARATE INDICATOR.  * *        
      * ---------------------------------------------------------------*        
                   IF  DPG54-CALENDAR-TYPE-INVALID                              
                        DISPLAY '** DPG54-CALENDAR-TYPE-INVALID'                
                   END-IF                                                       
                                                                                
                   IF  DPG54-DATE-INVALID                                       
                        DISPLAY '** DPG54-DATE-INVALID'                         
                   END-IF                                                       
                                                                                
                   IF  DPG54-INCR-DECR-INVALID                                  
                        DISPLAY '** DPG54-INCR-DECR-INVALID'                    
                   END-IF                                                       
                                                                                
                   DISPLAY ' '                                                  
                   DISPLAY '** ABEND     **'                                    
                   SET PV-SEVERE  TO TRUE                                       
                   PERFORM Z811-DATE-ROUTINE-ERROR                              
                   MOVE +4019 TO ABEND-CODE                                     
                   CALL 'ILBOABN0' USING ABEND-CODE                             
           END-EVALUATE.                                                        
                                                                                
           MOVE DPG56-FISCAL-YEAR-CCYY    TO PV-FISCAL-CCYY.                    
           MOVE DPG56-FISCAL-PERIOD-NBR   TO PV-FISCAL-MONTH.                   
           MOVE DPG56-FISCAL-PERIOD-WEEK  TO PV-FISCAL-WEEK.                    
                                                                                
           DISPLAY 'FISCAL YEAR:        ' PV-FISCAL-CCYY.                       
           DISPLAY 'FISCAL MONTH:       ' PV-FISCAL-MONTH.                      
           DISPLAY 'FISCAL WEEK:        ' PV-FISCAL-WEEK.                       
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      *     C300-READ-PRICE-CHANGE-FILE                                *        
      * ==> PROCESSES:                                                 *        
      *     - READS A RECORD FROM THE PRICE CHANGE FILE                *        
      *     - COUNTS THE NUMBER OF RECORDS READ                        *        
      * ==> PERFORMED FROM: B100-INITIALIZE                            *        
      *                     B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C300-READ-PRICE-CHANGE-FILE.                                             
                                                                                
           READ PRICE-CHANGE-FILE-IN INTO ML023-PC-RECORD                       
               AT END                                                           
                   SET PS-END-OF-PRCHG-FILE TO TRUE.                            
                                                                                
           IF PS-END-OF-PRCHG-FILE                                              
               CONTINUE                                                         
           ELSE                                                                 
               ADD +1     TO PA-PRCHG-FILE-RECS-READ                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C400-WRITE-PRICE-CHANGE-FILE                               *        
      * ==> PROCESSES:                                                 *        
      *     - WRITES THE PRICE CHANGE RECORD                           *        
      * ==> PERFORMED FROM: B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C400-WRITE-PRICE-CHANGE-FILE.                                            
                                                                                
           WRITE PRICE-CHANGE-RECORD-OUT                                        
               FROM ML023-PC-RECORD.                                            
                                                                                
           ADD +1     TO PA-PRCHG-FILE-RECS-WRITTEN.                            
                                                                                
      ******************************************************************        
      *     C500-GET-INSTR-REASON                                      *        
      * ==> PROCESSES:                                                 *        
      *     - GETS THE REASON CODE FOR AN IN STORE DOCUMENT            *        
      * ==> PERFORMED FROM: B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C500-GET-INSTR-REASON.                                                   
                                                                                
           MOVE ML023-PC-STORE-NBR TO PV-STORE-NBR-5X.                          
           MOVE PV-STORE-NBR       TO STRPPC-DEST-LOC-NBR.                      
           MOVE ML023-PC-DOC-7-NBR TO STRPPC-DOC-NBR.                           
                                                                                
           EXEC SQL                                                             
             SELECT  RSN_CDE                                                    
                   , TYP_CDE                                                    
               INTO  :STRPPC-RSN-CDE                                            
                   , :STRPPC-TYP-CDE                                            
               FROM  TSTRPPC                                                    
              WHERE  DEST_LOC_NBR = :STRPPC-DEST-LOC-NBR                        
                AND  DOC_NBR      = :STRPPC-DOC-NBR                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               MOVE STRPPC-TYP-CDE    TO PV-STORE-TYPE-RSN                      
               MOVE STRPPC-RSN-CDE    TO PV-STORE-CDE-RSN                       
               MOVE PV-STORE-RSN-CDE  TO ML023-PC-MKDN-MKUP-RSN-CDE             
           ELSE                                                                 
               MOVE 'C500-GET-INSTR-REASON           '                          
                                   TO PV-PARAGRAPH-NAME                         
               MOVE 'ERROR ON SELECT OF TSTCPPC    '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     C600-GET-BUYER-REASON                                      *        
      * ==> PROCESSES:                                                 *        
      *     - GETS THE REASON CODE FOR A BUYER GENERATED DOCUMENT      *        
      * ==> PERFORMED FROM: B200-PROCESS-PRCHG-RECORDS                 *        
      ******************************************************************        
       C600-GET-BUYER-REASON.                                                   
                                                                                
           MOVE ML023-PC-DOC-7-NBR TO CENPPC-CENPPC-NBR.                        
                                                                                
           EXEC SQL                                                             
             SELECT  RSN_CDE                                                    
               INTO  :CENPPC-RSN-CDE                                            
               FROM  TCENPPC                                                    
              WHERE  OPERCO_NBR = '03'                                          
                AND  CENPPC_NBR = :CENPPC-CENPPC-NBR                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               MOVE CENPPC-RSN-CDE    TO PV-RSN-CDE-VALUE                       
               MOVE PV-RSN-CDE        TO ML023-PC-MKDN-MKUP-RSN-CDE             
           ELSE                                                                 
P41629     IF SQLCODE = +100                                                    
P41629         MOVE '999'             TO ML023-PC-MKDN-MKUP-RSN-CDE             
P41629         DISPLAY '** NOT FOUND - C600-GET-BUYER-REASON'                   
P41629         DISPLAY '** REASON CODE SET TO: '                                
P41629                                  ML023-PC-MKDN-MKUP-RSN-CDE              
P41629         DISPLAY '* RECORD: ', ML023-PC-RECORD                            
P41629         DISPLAY '* ML023-PC-MKDN-MKUP-RSN-CDE: ',                        
P41629                                  ML023-PC-MKDN-MKUP-RSN-CDE              
P41629         DISPLAY '* ML023-PC-DOC-7-NBR: ', ML023-PC-DOC-7-NBR             
P41629         DISPLAY '******** '                                              
P41629     ELSE                                                                 
P41629         DISPLAY '** ABEND '                                              
P41629         DISPLAY '* RECORD: ', ML023-PC-RECORD                            
P41629         DISPLAY '* ML023-PC-MKDN-MKUP-RSN-CDE: ',                        
P41629                                  ML023-PC-MKDN-MKUP-RSN-CDE              
P41629         DISPLAY '* ML023-PC-DOC-7-NBR: ', ML023-PC-DOC-7-NBR             
P41629         DISPLAY '******** '                                              
               MOVE 'C600-GET-BUYER-REASON           '                          
                                   TO PV-PARAGRAPH-NAME                         
P41629         MOVE 'ERROR ON SELECT OF TCENPPC    '                            
                                   TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *     Z811-DATE-ROUTINE-CALL.                                    *        
      ******************************************************************        
       Z811-DATE-ROUTINE-ERROR.                                                 
                                                                                
           DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME.                       
           DISPLAY '** PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                     
           DISPLAY '** BAD CALL TO CALENDAR MODULE '                            
                       DP500-KOHLS-CALENDAR-ROUTINE.                            
           DISPLAY  PV-ERROR-LITERAL.                                           
           DISPLAY '* ERROR MESSAGE FROM CALLED MODULE ON NEXT LINE.*'.         
           DISPLAY DPG54-ERROR-MESSAGE.                                         
           DISPLAY '* CALLING PARMLISTS DISPLAYED ON NEXT 2 LINES.  *'.         
           DISPLAY '* DPG51 : ' DPG51.                                          
           DISPLAY '* DPG52 : ' DPG52.                                          
                                                                                
      ******************************************************************        
      *     Z900-SQL-ABEND                                             *        
      * ==> PROCESSES:                                                 *        
      *     - PERFORMS ABEND PROCESSING FOR SQL ERRORS.                *        
      * ==> PERFORMED FROM: C500-GET-INSTR-REASON                      *        
      *                     C600-GET-BUYER-REASON                      *        
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           DISPLAY 'Z900-SQL-ABEND'.                                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
      * FORMAT.                                                        *        
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           SET AC-ABEND-4013-DB2-ERROR TO TRUE.                                 
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
