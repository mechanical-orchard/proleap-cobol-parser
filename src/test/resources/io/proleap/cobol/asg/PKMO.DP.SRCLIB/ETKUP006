00001 *                                                                 12/05/94
00002 *                                                                 ETKUP006
00003 *                                                                    LV003
00004  IDENTIFICATION DIVISION.                                         ETKUP006
00005  PROGRAM-ID.    ETKUP006.                                         ETKUP006
00006  AUTHOR.        KIM WRIGHT.                                       ETKUP006
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUP006
00008  DATE-WRITTEN.  06/02/94.                                         ETKUP006
00009  DATE-COMPILED.                                                   ETKUP006
00010 ******************************************************************ETKUP006
00011 *  ETKUP006 -  INSERT NEW TRANSMISSIONS                           ETKUP006
00012 *                                                                 ETKUP006
00013 *  THE PURPOSE OF THIS PROGRAM IS TO INSERT NEW TRANSMISSIONS INTOETKUP006
00014 *  DB2 TABLES.  ANY ERROR RECORDS WILL POPULATE THE INTERCHANGE   ETKUP006
00015 *  ERROR TABLE AND ERROR NOTES TABLE.  FUNCTIONAL ACKNOWLEDGEMENTSETKUP006
00016 *  ARE VERIFIED USING FUNCTIONAL GROUP CONTROL NUMBERS AND THE    ETKUP006
00017 *  FUNCTIONAL GROUP/TRANSACTION SET TRANSMISSION TABLE IS UPDATED.ETKUP006
00018 ******************************************************************ETKUP006
00019 *     ADD THE PRODUCTION/TEST INDICATOR FROM THE INPUT FILE          CL**3
00020 *     TO THE TRANSMISSION DB.                                        CL**3
00021 *                                                                    CL**3
00022 *     12/05/94 - DAN FRANKLIN - WR 3138                              CL**3
************************************************************************   CL**3
JF1102* 11-21-2002 JON FIEDLER  PITS# PM00509248                                
JF1102* INCREASED THE SIZE OF THE TPDKEY INTERNAL TABLE TO 13 IN ORDER          
JF1102* TO ACCOMADATE THE GROWTH IN VENDORS WHO SHARE THE SAME ID.              
JF1102* ALSO REMOVED CODE THAT PROCESSED INBOUND 997S SINCE THIS                
JF1102* PROGRAM NO LONGER PROCESSES INBOUND DATA.                               
************************************************************************        
JF0505* 05-27-2005 JON FIEDLER  PITS# P000766450                                
JF0505* INCREASED THE SIZE OF THE TPDKEY INTERNAL TABLE TO 25 IN ORDER          
JF0505* TO ACCOMADATE THE GROWTH IN VENDORS WHO SHARE THE SAME ID.              
JF0505* ALSO REMOVED CODE THAT PROCESSED INBOUND 997S SINCE THIS                
JF0505* PROGRAM NO LONGER PROCESSES INBOUND DATA.                               
************************************************************************        
00024 *                                                                 ETKUP006
00025 *                                                                 ETKUP006
00026  ENVIRONMENT DIVISION.                                            ETKUP006
00027 *                                                                 ETKUP006
00028 *                                                                 ETKUP006
00029  CONFIGURATION SECTION.                                           ETKUP006
00030                                                                   ETKUP006
00031  SOURCE-COMPUTER. IBM-3090.                                       ETKUP006
00032  OBJECT-COMPUTER. IBM-3090.                                       ETKUP006
00033 *                                                                 ETKUP006
00034 *                                                                 ETKUP006
00035  INPUT-OUTPUT SECTION.                                            ETKUP006
00036                                                                   ETKUP006
00037  FILE-CONTROL.                                                    ETKUP006
00038                                                                   ETKUP006
00039      SELECT EDI-TSET-FILE                                         ETKUP006
00040          ASSIGN TO INP01.                                         ETKUP006
00041                                                                   ETKUP006
00042 *                                                                 ETKUP006
00043  DATA DIVISION.                                                   ETKUP006
00044 *                                                                 ETKUP006
00045                                                                   ETKUP006
00046  FILE SECTION.                                                    ETKUP006
00047                                                                   ETKUP006
00048  FD  EDI-TSET-FILE                                                ETKUP006
00049      RECORDING MODE IS F                                          ETKUP006
00050      LABEL RECORDS ARE STANDARD                                   ETKUP006
00051      BLOCK CONTAINS 0 RECORDS                                     ETKUP006
00052      RECORD CONTAINS 200 CHARACTERS                               ETKUP006
00053      DATA RECORD IS EDI-TSET-FILE-RECORD.                         ETKUP006
00054                                                                   ETKUP006
00055  01  EDI-TSET-FILE-RECORD   PIC X(200).                           ETKUP006
00056                                                                   ETKUP006
00057 *                                                                 ETKUP006
00058  WORKING-STORAGE SECTION.                                         ETKUP006
00059                                                                   ETKUP006
00060 ******************************************************************ETKUP006
00061 *    FIELDS NECESSARY TO CALL THE SQLCA MESSAGE MODULE.           ETKUP006
00062 ******************************************************************ETKUP006
00063      COPY DPWS004.                                                ETKUP006
00064                                                                   ETKUP006
00065 ******************************************************************ETKUP006
00066 *    TRADING PARTNERSHIP DATALESS  RECORD LAYOUT.                 ETKUP006
00067 ******************************************************************ETKUP006
00068      COPY ETWS020.                                                   CL**3
00069                                                                   ETKUP006
00070                                                                   ETKUP006
00071  01  WS-WORKING-STORAGE.                                          ETKUP006
00072      05  WS-SUB                           PIC S9(9)  COMP         ETKUP006
00073                                                      VALUE ZEROES.ETKUP006
00074      05  WS-T-SUB                         PIC S9(9)  COMP         ETKUP006
00075                                                      VALUE ZEROES.ETKUP006
00076      05  WS-INIT-SUB                      PIC S9(9)  COMP         ETKUP006
00077                                                      VALUE ZEROES.ETKUP006
00078      05  WS-REJECT-SEQ-NUM                PIC S9(9)  COMP         ETKUP006
00079                                                      VALUE ZEROES.ETKUP006
00080      05  WS-SAVE-TSET                     PIC X(03)  VALUE SPACES.ETKUP006
00081      05  WS-SAVE-DATE-TIME                PIC X(26)  VALUE SPACES.ETKUP006
00082      05  WS-DIR-IND                       PIC X(01)  VALUE SPACES.ETKUP006
00083      05  WS-ROUTED-IND                    PIC X(01)  VALUE SPACES.ETKUP006
00084      05  WS-FORMAT-YYMMDD.                                        ETKUP006
00085          10  WS-FORMAT-YY                 PIC X(02)  VALUE SPACES.ETKUP006
00086          10  WS-FORMAT-MM                 PIC X(02)  VALUE SPACES.ETKUP006
00087          10  WS-FORMAT-DD                 PIC X(02)  VALUE SPACES.ETKUP006
00088      05  WS-FORMAT-HHMNSS.                                        ETKUP006
00089          10  WS-FORMAT-HH                 PIC X(02)  VALUE SPACES.ETKUP006
00090          10  WS-FORMAT-MN                 PIC X(02)  VALUE SPACES.ETKUP006
00091          10  WS-FORMAT-SS                 PIC X(02)  VALUE SPACES.ETKUP006
00092      05  WS-FORMAT-DATE.                                          ETKUP006
00093          10  WS-FORMAT-DATE-CC            PIC X(02)  VALUE SPACES.ETKUP006
00094          10  WS-FORMAT-DATE-YY            PIC X(02)  VALUE SPACES.ETKUP006
00095          10  FILLER                       PIC X(01)  VALUE '-'.   ETKUP006
00096          10  WS-FORMAT-DATE-MM            PIC X(02)  VALUE SPACES.ETKUP006
00097          10  FILLER                       PIC X(01)  VALUE '-'.   ETKUP006
00098          10  WS-FORMAT-DATE-DD            PIC X(02)  VALUE SPACES.ETKUP006
00099      05  WS-FORMAT-TIME.                                          ETKUP006
00100          10  WS-FORMAT-TIME-HH            PIC X(02)  VALUE SPACES.ETKUP006
00101          10  FILLER                       PIC X(01)  VALUE '.'.   ETKUP006
00102          10  WS-FORMAT-TIME-MN            PIC X(02)  VALUE SPACES.ETKUP006
00103          10  FILLER                       PIC X(01)  VALUE '.'.   ETKUP006
00104          10  WS-FORMAT-TIME-SS            PIC X(02)  VALUE SPACES.ETKUP006
00105                                                                   ETKUP006
00106      05  WS-MAILBOX-REC-DTE               PIC X(10)  VALUE SPACES.ETKUP006
00107      05  WS-MAILBOX-REC-TME               PIC X(08)  VALUE SPACES.ETKUP006
00108      05  WS-MAILBOX-PU-DTE                PIC X(10)  VALUE SPACES.ETKUP006
00109      05  WS-MAILBOX-PU-TME                PIC X(08)  VALUE SPACES.ETKUP006
00110                                                                   ETKUP006
00111      05  WS-SAVE-FG-CTL-NBR               PIC X(09)  VALUE SPACES.ETKUP006
00112      05  WS-VEND-ACK-IND                  PIC X(01)  VALUE SPACES.ETKUP006
00113                                                                   ETKUP006
00114      05  WS-FG-SEQ-NBR                    PIC S9(9)  COMP         ETKUP006
00115                                                      VALUE ZEROES.ETKUP006
00116      05  WS-TS-SEQ-NBR                    PIC S9(9)  COMP         ETKUP006
00117                                                      VALUE ZEROES.ETKUP006
00118      05  WS-DB2-TP-DKEY                   PIC S9(9)  COMP         ETKUP006
00119                                                      VALUE ZEROES.ETKUP006
00120      05  WS-DB2-ERR-SEQ-NBR               PIC S9(9)  COMP         ETKUP006
00121                                                      VALUE ZEROES.ETKUP006
00122      05  WS-DB2-FG-CTL-NBR                PIC S9(9)V COMP-3       ETKUP006
00123                                                      VALUE ZEROES.ETKUP006
00124      05  WS-DB2-TRAN-SET                  PIC X(03)  VALUE SPACES.ETKUP006
00125      05  WS-DB2-CRE-DTE                   PIC X(10)  VALUE SPACES.ETKUP006
00126      05  WS-DB2-CRE-TME                   PIC X(10)  VALUE SPACES.ETKUP006
00127      05  WS-DB2-INCHG-CTL-NBR             PIC S9(9)V COMP-3       ETKUP006
00128                                                      VALUE ZEROES.ETKUP006
           05  WS-RES-IND                       PIC X(01)  VALUE SPACES.        
00129                                                                   ETKUP006
00135      05  WS-SAVE-TP-DKEY-DATA.                                    ETKUP006
JF0505         10  WS-SAVE-TP-DKEY-TABLE OCCURS 25 TIMES.               ETKUP006
00137              15  WS-SAVE-TP-DKEY          PIC S9(9)  COMP         ETKUP006
00138                                                      VALUE ZEROES.ETKUP006
00142      05  WS-30-CHAR-FIELD                 PIC X(30)  VALUE SPACES.ETKUP006
00143      05  WS-TALLY                         PIC S9(04) COMP         ETKUP006
00144                                                      VALUE +0.    ETKUP006
00145                                                                   ETKUP006
00146  01  PV-PROGRAM-VARIABLES.                                        ETKUP006
00147      05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.ETKUP006
00148      05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.ETKUP006
00149                                                                   ETKUP006
00150  01  PS-PROGRAM-SWITCHES.                                         ETKUP006
00151      05  PS-END-OF-FILE-SW                PIC X(01)  VALUE 'N'.   ETKUP006
00152          88 PS-END-OF-FILE                           VALUE 'Y'.   ETKUP006
00153          88 PS-NOT-END-OF-FILE                       VALUE 'N'.   ETKUP006
00154                                                                   ETKUP006
00155  01  ABEND-CODE                          PIC S9(04)  VALUE +0     ETKUP006
00156                                                      COMP SYNC.   ETKUP006
00157  EJECT                                                            ETKUP006
00158 ******************************************************************ETKUP006
00159 *    TRANSACTION SET DB2 TABLE                                    ETKUP006
00160 ******************************************************************ETKUP006
00161                                                                   ETKUP006
00162      EXEC SQL                                                     ETKUP006
00163          INCLUDE TTRNSET                                          ETKUP006
00164      END-EXEC.                                                    ETKUP006
00165                                                                   ETKUP006
00166 ******************************************************************ETKUP006
00167 *    INTERCHANGE TRANSMISSION DB2 TABLE                           ETKUP006
00168 ******************************************************************ETKUP006
00169                                                                   ETKUP006
00170      EXEC SQL                                                     ETKUP006
00171          INCLUDE TINTTRN                                          ETKUP006
00172      END-EXEC.                                                    ETKUP006
00173                                                                   ETKUP006
00174 ******************************************************************ETKUP006
00175 *    FUNCTIONAL GROUP/TRANSACTION SET TRANSMISSION DB2 TABLE      ETKUP006
00176 ******************************************************************ETKUP006
00177                                                                   ETKUP006
00178      EXEC SQL                                                     ETKUP006
00179          INCLUDE TFGTRST                                          ETKUP006
00180      END-EXEC.                                                    ETKUP006
00181                                                                   ETKUP006
00182 ******************************************************************ETKUP006
00183 *    INTERCHANGE ERRORS                                           ETKUP006
00184 ******************************************************************ETKUP006
00185                                                                   ETKUP006
00186      EXEC SQL                                                     ETKUP006
00187          INCLUDE TINTERR                                          ETKUP006
00188      END-EXEC.                                                    ETKUP006
00189                                                                   ETKUP006
00190 ******************************************************************ETKUP006
00191 *    INTERCHANGE ERROR NOTES                                      ETKUP006
00192 ******************************************************************ETKUP006
00193                                                                   ETKUP006
00194      EXEC SQL                                                     ETKUP006
00195          INCLUDE TINTNOT                                          ETKUP006
00196      END-EXEC.                                                    ETKUP006
00197                                                                   ETKUP006
00198 ******************************************************************ETKUP006
00199 *    SQL COMMUNICATION AREA                                       ETKUP006
00200 ******************************************************************ETKUP006
00201                                                                   ETKUP006
00202      EXEC SQL                                                     ETKUP006
00203          INCLUDE SQLCA                                            ETKUP006
00204      END-EXEC.                                                    ETKUP006
00205                                                                   ETKUP006
00220      COPY SQLCA2.                                                 ETKUP006
00221  EJECT                                                            ETKUP006
00222 *                                                                 ETKUP006
00223 *                                                                 ETKUP006
00224  LINKAGE SECTION.                                                 ETKUP006
00225 *                                                                 ETKUP006
00226 *                                                                 ETKUP006
00227  PROCEDURE DIVISION.                                              ETKUP006
00228                                                                   ETKUP006
00229  EJECT                                                            ETKUP006
00230  A100-MAIN-MODULE.                                                ETKUP006
00231 ******************************************************************ETKUP006
00232 *  OPEN INPUT FILE.                                               ETKUP006
00233 *  READ FIRST RECORD IN INPUT FILE.                               ETKUP006
00234 *  PROCESS EACH RECORD IN INPUT FILE.                             ETKUP006
00235 *  CLOSE FILE.                                                    ETKUP006
00236 *  STOP PROCESSING.                                               ETKUP006
00237 ******************************************************************ETKUP006
00238 *    DISPLAY 'A100-MAIN-MODULE'.                                  ETKUP006
00239                                                                   ETKUP006
00240      OPEN INPUT EDI-TSET-FILE.                                    ETKUP006
00241                                                                   ETKUP006
00242      PERFORM Z100-READ-TSET-RECORD.                               ETKUP006
00243                                                                   ETKUP006
00244      PERFORM B100-PROCESS-TSET-FILE                               ETKUP006
00245          UNTIL PS-END-OF-FILE.                                    ETKUP006
00246                                                                   ETKUP006
00247      CLOSE EDI-TSET-FILE.                                         ETKUP006
00248                                                                   ETKUP006
00249      STOP RUN.                                                    ETKUP006
00250                                                                   ETKUP006
00251  EJECT                                                            ETKUP006
00252  B100-PROCESS-TSET-FILE.                                          ETKUP006
00253 ******************************************************************ETKUP006
00254 *  PROCESS HEADER, DETAIL, OR ERROR RECORD BASED ON RECORD TYPE   ETKUP006
00255 *  INDICATOR ON THE INPUT RECORD.                                 ETKUP006
00256 *                                                                 ETKUP006
00257 *  PERFORMED BY A100-MAIN-MODULE.                                 ETKUP006
00258 ******************************************************************ETKUP006
00259 *    DISPLAY 'B100-PROCESS-TSET-FILE'.                            ETKUP006
00260                                                                   ETKUP006
00261      IF ET020-HEADER                                              ETKUP006
00262         PERFORM C100-PROCESS-HEADER-REC                           ETKUP006
00263      ELSE                                                         ETKUP006
00264         IF ET020-DETAIL                                           ETKUP006
00265            PERFORM C200-PROCESS-DETAIL-REC                        ETKUP006
00266         ELSE                                                      ETKUP006
00267            IF ET020-ERROR                                         ETKUP006
00268               PERFORM C300-PROCESS-ERROR-REC                      ETKUP006
00269            END-IF                                                 ETKUP006
00270         END-IF                                                    ETKUP006
00271      END-IF.                                                      ETKUP006
00272                                                                   ETKUP006
00273      PERFORM Z100-READ-TSET-RECORD.                               ETKUP006
00274                                                                   ETKUP006
00275  EJECT                                                            ETKUP006
00276  C100-PROCESS-HEADER-REC.                                         ETKUP006
00277 ******************************************************************ETKUP006
00278 *  INITIALIZE VARIABLES TO BE USED FOR CURRENT TRANSMISSION.      ETKUP006
00279 *  INSERT HEADER DATA INTO THE INTERCHANGE TRANSACTION TABLE.     ETKUP006
00280 *                                                                 ETKUP006
00281 *  PERFORMED BY B100-PROCESS-TSET-FILE.                           ETKUP006
00282 ******************************************************************ETKUP006
00283 *    DISPLAY 'C100-PROCESS-HEADER-REC'.                           ETKUP006
00284                                                                   ETKUP006
00285      MOVE +1 TO WS-SUB                                            ETKUP006
00286                 WS-FG-SEQ-NBR                                     ETKUP006
00287                 WS-TS-SEQ-NBR                                     ETKUP006
00288                 WS-REJECT-SEQ-NUM.                                ETKUP006
00289                                                                   ETKUP006
00290      MOVE SPACES TO WS-SAVE-TSET.                                 ETKUP006
00291                                                                   ETKUP006
00292      MOVE +1 TO WS-INIT-SUB.                                      ETKUP006
00293                                                                   ETKUP006
00294      PERFORM                                                      ETKUP006
JF0505         UNTIL WS-INIT-SUB > 25                                   ETKUP006
00296             MOVE ZEROES TO WS-SAVE-TP-DKEY(WS-INIT-SUB)           ETKUP006
00297             ADD +1 TO WS-INIT-SUB                                 ETKUP006
00298      END-PERFORM.                                                 ETKUP006
00299                                                                   ETKUP006
00300      MOVE ET020-DIR-IND TO WS-DIR-IND.                            ETKUP006
00301                                                                   ETKUP006
00302      EXEC SQL                                                     ETKUP006
00303          SET :WS-SAVE-DATE-TIME = CURRENT TIMESTAMP               ETKUP006
00304      END-EXEC.                                                    ETKUP006
00305                                                                   ETKUP006
00306      EVALUATE TRUE                                                ETKUP006
00307         WHEN SQLCODE = 0                                          ETKUP006
00308              CONTINUE                                             ETKUP006
00309         WHEN OTHER                                                ETKUP006
00310               MOVE 'C100-PROCESS-HEADER-REC' TO                   ETKUP006
00311                    PV-PARAGRAPH-NAME                              ETKUP006
00312                                                                   ETKUP006
00313               MOVE 'INSERT INTERCHANGE TRANSACTION ' TO           ETKUP006
00314                    PV-DB2-OPERATION-ATTEMPTED                     ETKUP006
00315                                                                   ETKUP006
00316               PERFORM Z999-SQL-ABEND                              ETKUP006
00317                                                                   ETKUP006
00318      END-EVALUATE.                                                ETKUP006
00319                                                                   ETKUP006
00320      PERFORM D100-INSERT-INT-TRANS-DB                             ETKUP006
JF0505        UNTIL ET020-TP-DKEY(WS-SUB) = 0 OR  WS-SUB > 25.          ETKUP006
00322                                                                   ETKUP006
00323  EJECT                                                            ETKUP006
00324  C200-PROCESS-DETAIL-REC.                                         ETKUP006
00325 ******************************************************************ETKUP006
00326 *  WHENEVER THE TRANSACTION SET CODE OR THE FUNCTIONAL GROUP      ETKUP006
00327 *  CONTROL NUMBER CHANGE, INCREMENT THE FUNCTIONAL GROUP SEQUENCE ETKUP006
00328 *  NUMBER AND RESET THE TRANSACTION SET SEQUENCE NUMBER TO ZERO.  ETKUP006
00329 *                                                                 ETKUP006
00330 *  PERFORMED BY B100-PROCESS-TSET-FILE.                           ETKUP006
00331 ******************************************************************ETKUP006
00332 *    DISPLAY 'C200-PROCESS-DETAIL-REC'.                           ETKUP006
00333                                                                   ETKUP006
00334      MOVE +1 TO WS-SUB.                                           ETKUP006
00335                                                                   ETKUP006
00336      IF WS-SAVE-TSET = SPACES                                     ETKUP006
00337         MOVE ET020-TRAN-SET-CDE TO WS-SAVE-TSET                   ETKUP006
00338         MOVE ET020-FG-CTL-NBR   TO WS-SAVE-FG-CTL-NBR             ETKUP006
00339      ELSE                                                         ETKUP006
00340         IF WS-SAVE-TSET NOT = ET020-TRAN-SET-CDE OR               ETKUP006
00341            WS-SAVE-FG-CTL-NBR NOT = ET020-FG-CTL-NBR              ETKUP006
00342                                                                   ETKUP006
00343            ADD +1 TO WS-FG-SEQ-NBR                                ETKUP006
00344            MOVE +1 TO WS-TS-SEQ-NBR                               ETKUP006
00345                                                                   ETKUP006
00346            MOVE ET020-TRAN-SET-CDE TO WS-SAVE-TSET                ETKUP006
00347            MOVE ET020-FG-CTL-NBR   TO WS-SAVE-FG-CTL-NBR          ETKUP006
00348         END-IF                                                    ETKUP006
00349      END-IF.                                                      ETKUP006
00350                                                                   ETKUP006
00351      PERFORM D200-PROCESS-FG-TS-TRANS-DB                          ETKUP006
JF0505        UNTIL (WS-SAVE-TP-DKEY(WS-SUB) = 0 OR WS-SUB > 25).       ETKUP006
00353                                                                   ETKUP006
00354      ADD +1 TO WS-TS-SEQ-NBR.                                     ETKUP006
00355                                                                   ETKUP006
00356  EJECT                                                            ETKUP006
00357  C300-PROCESS-ERROR-REC.                                          ETKUP006
00358 ******************************************************************ETKUP006
00359 *  PROCESS THE ERROR RECORD FOR EACH OF THE POSSIBLE TRADING      ETKUP006
JF0505*  PARTNER DATALESS KEYS (MAXIMUM OF 25).                         ETKUP006
00361 *                                                                 ETKUP006
00362 *  PERFORMED BY B100-PROCESS-TSET-FILE.                           ETKUP006
00363 ******************************************************************ETKUP006
00364 *    DISPLAY 'C300-PROCESS-ERROR-REC'.                            ETKUP006
00365                                                                   ETKUP006
00366      MOVE +1 TO WS-SUB.                                           ETKUP006
00367                                                                   ETKUP006
00368      PERFORM D300-PROCESS-INTCHG-ERR-DB                           ETKUP006
JF0505       UNTIL WS-SAVE-TP-DKEY(WS-SUB) = 0 OR WS-SUB > 25.          ETKUP006
00370                                                                   ETKUP006
00371      ADD +1 TO WS-REJECT-SEQ-NUM.                                 ETKUP006
00372                                                                   ETKUP006
00373  EJECT                                                            ETKUP006
00374  D100-INSERT-INT-TRANS-DB.                                        ETKUP006
00375 ******************************************************************ETKUP006
00376 *  FORMAT THE INPUT DATES INTO DB2 FORM THEN INSERT THE HEADER    ETKUP006
00377 *  DATA INTO THE INTERCHANGE TRANSMISSION TABLE.                  ETKUP006
00378 *                                                                 ETKUP006
00379 *  PERFORMED BY C100-PROCESS-HEADER-REC                           ETKUP006
00380 ******************************************************************ETKUP006
00381 *    DISPLAY 'D100-INSERT-INT-TRANS-DB'.                          ETKUP006
00382                                                                   ETKUP006
00383      PERFORM E500-FORMAT-DATES.                                   ETKUP006
00384                                                                   ETKUP006
00385      MOVE ET020-TP-DKEY(WS-SUB) TO WS-DB2-TP-DKEY.                ETKUP006
00386      MOVE ET020-INCHG-CTL-NBR   TO WS-DB2-INCHG-CTL-NBR.          ETKUP006
00387                                                                   ETKUP006
00388      EXEC SQL                                                     ETKUP006
00389         INSERT INTO TINTTRN                                       ETKUP006
00390             (TP_DKEY,                                             ETKUP006
00391              TP_DKEY_TMST,                                        ETKUP006
00392              DIR_IND,                                             ETKUP006
00393              INCHG_CNTL_NBR,                                      ETKUP006
00394              CRE_DTE,                                             ETKUP006
00395              CRE_TME,                                             ETKUP006
00396              MAILBOX_REC_DTE,                                     ETKUP006
00397              MAILBOX_REC_TME,                                     ETKUP006
00398              MAILBOX_PU_DTE,                                      ETKUP006
00399              MAILBOX_PU_TME,                                      ETKUP006
00400              ERR_IND,                                             ETKUP006
00401              PROD_TEST_IND)                                       ETKUP006
00402         VALUES                                                    ETKUP006
00403             (:WS-DB2-TP-DKEY,                                     ETKUP006
00404              :WS-SAVE-DATE-TIME,                                  ETKUP006
00405              :ET020-DIR-IND,                                      ETKUP006
00406              :WS-DB2-INCHG-CTL-NBR,                               ETKUP006
00407              :WS-DB2-CRE-DTE,                                     ETKUP006
00408              :WS-DB2-CRE-TME,                                     ETKUP006
00409              :WS-MAILBOX-REC-DTE,                                 ETKUP006
00410              :WS-MAILBOX-REC-TME,                                 ETKUP006
00411              :WS-MAILBOX-PU-DTE,                                  ETKUP006
00412              :WS-MAILBOX-PU-TME,                                  ETKUP006
00413              :ET020-ERROR-IND,                                    ETKUP006
00414              :ET020-PROD-TEST-IND)                                ETKUP006
00415      END-EXEC.                                                    ETKUP006
00416                                                                   ETKUP006
00417       EVALUATE TRUE                                               ETKUP006
00418           WHEN SQLCODE = +000                                     ETKUP006
00419               CONTINUE                                            ETKUP006
00420           WHEN SQLWARN NOT = SPACES                               ETKUP006
00421           WHEN SQLCODE NOT = ZERO AND SQLCODE NOT = -180          ETKUP006
00422               MOVE 'D100-INSERT-INT-TRANS-DB' TO                  ETKUP006
00423                    PV-PARAGRAPH-NAME                              ETKUP006
00424                                                                   ETKUP006
00425               MOVE 'INSERT INTERCHANGE TRANSACTION ' TO           ETKUP006
00426                    PV-DB2-OPERATION-ATTEMPTED                     ETKUP006
00427                                                                   ETKUP006
00428               PERFORM Z999-SQL-ABEND                              ETKUP006
00429                                                                   ETKUP006
00421           WHEN SQLCODE NOT = ZERO AND SQLCODE = -180              ETKUP006
                    DISPLAY 'INVALID TP ID    : ' ET020-VEND-INCHG-ID           
                    DISPLAY 'INVALID ISA CNTL : ' ET020-INCHG-CTL-NBR           
                    DISPLAY 'INVALID TIMESTAMP: ' WS-SAVE-DATE-TIME             
00407               DISPLAY 'INVALID CRE-DTE:   ' WS-DB2-CRE-DTE        ETKUP006
00408               DISPLAY 'INVALID CRE-TME:   ' WS-DB2-CRE-TME        ETKUP006
00409               DISPLAY 'INVALID REC-TME:   ' WS-MAILBOX-REC-DTE    ETKUP006
00410               DISPLAY 'INVALID REC-TME:   ' WS-MAILBOX-REC-TME    ETKUP006
00411               DISPLAY 'INVALID PU-DTE:    ' WS-MAILBOX-PU-DTE     ETKUP006
00412               DISPLAY 'INVALID PU-TME:    ' WS-MAILBOX-PU-TME     ETKUP006
00422               MOVE 'D100-INSERT-INT-TRANS-DB' TO                  ETKUP006
00423                    PV-PARAGRAPH-NAME                              ETKUP006
00424                                                                   ETKUP006
00425               MOVE 'INSERT INTERCHANGE TRANSACTION ' TO           ETKUP006
00426                    PV-DB2-OPERATION-ATTEMPTED                     ETKUP006
00427                                                                   ETKUP006
00428               PERFORM Z999-SQL-ABEND                              ETKUP006
00429                                                                   ETKUP006
00430       END-EVALUATE.                                               ETKUP006
00431                                                                   ETKUP006
00432       MOVE ET020-TP-DKEY(WS-SUB) TO WS-SAVE-TP-DKEY(WS-SUB).      ETKUP006
00433                                                                   ETKUP006
00434       ADD +1 TO WS-SUB.                                           ETKUP006
00435                                                                   ETKUP006
00436  EJECT                                                            ETKUP006
00437  D200-PROCESS-FG-TS-TRANS-DB.                                     ETKUP006
00438 ******************************************************************ETKUP006
00439 *  SET THE VENDOR ACKNOWLEDGEMENT INDICATOR - FOR ALL OF THE      ETKUP006
00440 *  TRANSMISSIONS KOHL'S IS SENDING (BESIDES FUNCTIONAL ACKNOWLEDGEETKUP006
00441 *  MENT - 997), THE INDICATOR SHOULD BE SET TO 'N'.  ALL OTHER    ETKUP006
00442 *  TRANSMISSION INDICATORS SHOULD BE SET TO SPACES.               ETKUP006
00443 *                                                                 ETKUP006
00450 *  PERFORMED BY C200-PROCESS-DETAIL-REC.                          ETKUP006
00451 ******************************************************************ETKUP006
00452 *    DISPLAY 'D200-PROCESS-FG-TS-TRANS-DB'.                       ETKUP006
00453                                                                   ETKUP006
00454      IF ET020-TRAN-SET-CDE = '997'                                ETKUP006
00455         MOVE SPACES TO WS-VEND-ACK-IND                            ETKUP006
00456      ELSE                                                         ETKUP006
00457         IF WS-DIR-IND = 'S'                                       ETKUP006
00458            MOVE 'N' TO WS-VEND-ACK-IND                            ETKUP006
00459         ELSE                                                      ETKUP006
00460            MOVE SPACES TO WS-VEND-ACK-IND                         ETKUP006
00461         END-IF                                                    ETKUP006
00462      END-IF.                                                      ETKUP006
00463                                                                   ETKUP006
00464      PERFORM E400-INSERT-FG-TS-TRANS-DB.                          ETKUP006
00465                                                                   ETKUP006
00489      ADD +1 TO WS-SUB.                                            ETKUP006
00490                                                                   ETKUP006
00491  EJECT                                                            ETKUP006
00492  D300-PROCESS-INTCHG-ERR-DB.                                      ETKUP006
00493 ******************************************************************ETKUP006
00494 *  INSERT ONE RECORD INTO THE INTERCHANGE ERROR DB FOR EACH ERROR ETKUP006
00495 *  FOR A DATALESS KEY.  ONLY INSERT ONE RECORD INTO THE ERROR NOTEETKUP006
00496 *  TABLE FOR EACH DATALESS KEY (REGARDLESS OF NUMBER OF ERRORS).  ETKUP006
00497 *                                                                 ETKUP006
00498 *  PERFORMED BY C300-PROCESS-ERROR-REC.                           ETKUP006
00499 ******************************************************************ETKUP006
00500 *    DISPLAY 'D300-PROCESS-INTCHG-ERR-DB'.                        ETKUP006
00501                                                                   ETKUP006
00502      MOVE WS-SAVE-TP-DKEY(WS-SUB) TO WS-DB2-TP-DKEY.              ETKUP006
00503      MOVE ET020-ERR-SEQ-NBR-9     TO WS-DB2-ERR-SEQ-NBR.          ETKUP006
00504                                                                   ETKUP006
00505      EXEC SQL                                                     ETKUP006
00506         INSERT INTO TINTERR                                       ETKUP006
00507             (TP_DKEY,                                             ETKUP006
00508              TP_DKEY_TMST,                                        ETKUP006
00509              REJ_SEQ_NBR,                                         ETKUP006
00510              ERR_MSG_DESC,                                        ETKUP006
00511              SEG_ID,                                              ETKUP006
00512              SEQ_SEQ_NBR)                                         ETKUP006
00513         VALUES                                                    ETKUP006
00514             (:WS-DB2-TP-DKEY,                                     ETKUP006
00515              :WS-SAVE-DATE-TIME,                                  ETKUP006
00516              :WS-REJECT-SEQ-NUM,                                  ETKUP006
00517              :ET020-ERR-MSG,                                      ETKUP006
00518              :ET020-ERR-SEG-ID,                                   ETKUP006
00519              :WS-DB2-ERR-SEQ-NBR)                                 ETKUP006
00520      END-EXEC.                                                    ETKUP006
00521                                                                   ETKUP006
00522       EVALUATE TRUE                                               ETKUP006
00523           WHEN SQLCODE = +000                                     ETKUP006
00524               CONTINUE                                            ETKUP006
00525           WHEN SQLWARN NOT = SPACES                               ETKUP006
00526           WHEN SQLCODE NOT = ZERO                                 ETKUP006
00527               MOVE 'C300-PROCESS-ERROR-REC' TO                    ETKUP006
00528                    PV-PARAGRAPH-NAME                              ETKUP006
00529                                                                   ETKUP006
00530               MOVE 'INSERT INTERCHANGE TRANSACTION ' TO           ETKUP006
00531                    PV-DB2-OPERATION-ATTEMPTED                     ETKUP006
00532                                                                   ETKUP006
00533               PERFORM Z999-SQL-ABEND                              ETKUP006
00534                                                                   ETKUP006
00535       END-EVALUATE.                                               ETKUP006
00536                                                                   ETKUP006
00537       IF WS-REJECT-SEQ-NUM = 1                                    ETKUP006
00538          PERFORM E300-INSERT-INT-ERR-NOTES-DB                     ETKUP006
00539       END-IF.                                                     ETKUP006
00540                                                                   ETKUP006
00541       ADD +1 TO WS-SUB.                                           ETKUP006
00542                                                                   ETKUP006
00543  EJECT                                                            ETKUP006
00609  E300-INSERT-INT-ERR-NOTES-DB.                                    ETKUP006
00610 ******************************************************************ETKUP006
00611 *  INSERT ONE RECORD INTO THE ERROR NOTES DB2 TABLE FOR EACH      ETKUP006
00612 *  TRANSMISSION THAT HAS ERRORS.                                  ETKUP006
00613 *                                                                 ETKUP006
00614 *  PERFORMED BY D300-PROCESS-INTCHG-ERR-DB.                       ETKUP006
00615 ******************************************************************ETKUP006
00616 *    DISPLAY 'E300-INSERT-INT-ERR-NOTES-DB'.                      ETKUP006
00617                                                                   ETKUP006
00618       MOVE WS-SAVE-TP-DKEY(WS-SUB) TO WS-DB2-TP-DKEY.             ETKUP006
00619                                                                   ETKUP006
      ***********************************                                       
      *     THIS CODE CAN BE UNCOMMENTED, WHEN ETKUT020 IS FIXED                
      ***********************************                                       
      *     IF ET020-CONTROL-NUM-ERROR                                          
      *        MOVE 'Y' TO WS-RES-IND                                           
      *     ELSE                                                                
      *        MOVE 'N' TO WS-RES-IND                                           
      *     END-IF.                                                             
      ***********************************                                       
                                                                                
            MOVE 'N' TO WS-RES-IND.                                             
                                                                                
00620       EXEC SQL                                                    ETKUP006
00621          INSERT INTO TINTNOT                                      ETKUP006
00622              (TP_DKEY,                                            ETKUP006
00623               TP_DKEY_TMST,                                       ETKUP006
00624               ACT_MEMO_DESC,                                      ETKUP006
00625               RES_IND,                                            ETKUP006
00626               CONTACT_ID,                                         ETKUP006
00627               RES_TMST)                                           ETKUP006
00628          VALUES                                                   ETKUP006
00629              (:WS-DB2-TP-DKEY,                                    ETKUP006
00630               :WS-SAVE-DATE-TIME,                                 ETKUP006
00631               ' ',                                                ETKUP006
00632               :WS-RES-IND,                                        ETKUP006
00633               ' ',                                                ETKUP006
00634               '9999-09-09-09.09.09.000000')                       ETKUP006
00635       END-EXEC.                                                   ETKUP006
00636                                                                   ETKUP006
00637        EVALUATE TRUE                                              ETKUP006
00638            WHEN SQLCODE = +000                                    ETKUP006
00639                CONTINUE                                           ETKUP006
00640            WHEN SQLWARN NOT = SPACES                              ETKUP006
00641            WHEN SQLCODE NOT = ZERO                                ETKUP006
00642                MOVE 'E300-INSERT-INT-ERR-NOTES-DB' TO             ETKUP006
00643                     PV-PARAGRAPH-NAME                             ETKUP006
00644                                                                   ETKUP006
00645                MOVE 'INSERT INTERCHANGE ERROR NOTES ' TO          ETKUP006
00646                     PV-DB2-OPERATION-ATTEMPTED                    ETKUP006
00647                                                                   ETKUP006
00648                PERFORM Z999-SQL-ABEND                             ETKUP006
00649                                                                   ETKUP006
00650        END-EVALUATE.                                              ETKUP006
00651                                                                   ETKUP006
00652  EJECT                                                            ETKUP006
00653  E400-INSERT-FG-TS-TRANS-DB.                                      ETKUP006
00654 ******************************************************************ETKUP006
00655 *  INSERT TRANSMISSION DETAIL INTO FG/TS TRANSMISSION DB.         ETKUP006
00656 *                                                                 ETKUP006
00657 *  PERFORMED BY D200-PROCESS-FG-TS-TRANS-DB.                      ETKUP006
00658 ******************************************************************ETKUP006
00659 *    DISPLAY 'E400-INSERT-FG-TS-TRANS-DB'.                        ETKUP006
00660                                                                   ETKUP006
00661      MOVE WS-SAVE-TP-DKEY(WS-SUB) TO WS-DB2-TP-DKEY.              ETKUP006
00662      MOVE ET020-FG-CTL-NBR-9      TO WS-DB2-FG-CTL-NBR.           ETKUP006
00663                                                                   ETKUP006
00664      MOVE SPACES TO WS-30-CHAR-FIELD.                             ETKUP006
00665                                                                   ETKUP006
00666      MOVE +0 TO WS-TALLY.                                         ETKUP006
00667                                                                   ETKUP006
00668      INSPECT ET020-TRAN-SET-DTL                                   ETKUP006
00669          TALLYING WS-TALLY                                        ETKUP006
00670          FOR                                                      ETKUP006
00671              LEADING ZEROES.                                      ETKUP006
00672                                                                   ETKUP006
00673      MOVE ET020-TRAN-SET-DTL(1 + WS-TALLY:30 - WS-TALLY) TO       ETKUP006
00674           WS-30-CHAR-FIELD.                                       ETKUP006
00675                                                                   ETKUP006
00676      MOVE WS-30-CHAR-FIELD TO ET020-TRAN-SET-DTL.                 ETKUP006
00677                                                                   ETKUP006
00678      EXEC SQL                                                     ETKUP006
00679         INSERT INTO TFGTRST                                       ETKUP006
00680             (TP_DKEY,                                             ETKUP006
00681              TP_DKEY_TMST,                                        ETKUP006
00682              FUNC_CDE_SEQ_NBR,                                    ETKUP006
00683              TRN_SET_SEQ_NBR,                                     ETKUP006
00684              TRN_SET_CDE,                                         ETKUP006
00685              FUNC_CDE_CNTL_NBR,                                   ETKUP006
00686              VEND_ACK_IND,                                        ETKUP006
00687              TRN_SET_CNTL_NBR,                                    ETKUP006
00688              TRN_DTL_TXT,                                         ETKUP006
00689              CHARS_XMIT_NBR,                                      ETKUP006
00690              RTE_IND)                                             ETKUP006
00691         VALUES                                                    ETKUP006
00692             (:WS-DB2-TP-DKEY,                                     ETKUP006
00693              :WS-SAVE-DATE-TIME,                                  ETKUP006
00694              :WS-FG-SEQ-NBR,                                      ETKUP006
00695              :WS-TS-SEQ-NBR,                                      ETKUP006
00696              :ET020-TRAN-SET-CDE,                                 ETKUP006
00697              :WS-DB2-FG-CTL-NBR,                                  ETKUP006
00698              :WS-VEND-ACK-IND,                                    ETKUP006
00699              :ET020-TSET-CTL-NBR,                                 ETKUP006
00700              :ET020-TRAN-SET-DTL,                                 ETKUP006
00701               0,                                                  ETKUP006
00702              :ET020-ROUTED-IND)                                   ETKUP006
00703      END-EXEC.                                                    ETKUP006
00704                                                                   ETKUP006
00705       EVALUATE TRUE                                               ETKUP006
00706           WHEN SQLCODE = +000                                     ETKUP006
00707               CONTINUE                                            ETKUP006
00708           WHEN SQLWARN NOT = SPACES                               ETKUP006
00709           WHEN SQLCODE NOT = ZERO                                 ETKUP006
00710               MOVE 'D200-INSERT-FS-TS-TRANS-DB' TO                ETKUP006
00711                    PV-PARAGRAPH-NAME                              ETKUP006
00712                                                                   ETKUP006
00713               MOVE 'INSERT INTERCHANGE TRANSACTION ' TO           ETKUP006
00714                    PV-DB2-OPERATION-ATTEMPTED                     ETKUP006
00715                                                                   ETKUP006
00716               PERFORM Z999-SQL-ABEND                              ETKUP006
00717                                                                   ETKUP006
00718       END-EVALUATE.                                               ETKUP006
00719                                                                   ETKUP006
00720  EJECT                                                            ETKUP006
00721  E500-FORMAT-DATES.                                               ETKUP006
00722 ******************************************************************ETKUP006
00723 *  CHANGE DATES FROM THE INPUT FILE FORMAT INTO DB2 FORMAT.       ETKUP006
00724 *                                                                 ETKUP006
00725 *  PERFORMED BY D100-INSERT-INT-TRANS-DB.                         ETKUP006
00726 ******************************************************************ETKUP006
00727 *    DISPLAY 'E500-FORMAT-DATES'.                                 ETKUP006
00728                                                                   ETKUP006
00729      IF ET020-RCPT-DTE NOT = ZEROES                               ETKUP006
00730         MOVE ET020-RCPT-DTE TO WS-FORMAT-YYMMDD                   ETKUP006
              IF WS-FORMAT-YY > 80                                              
                 MOVE '19' TO WS-FORMAT-DATE-CC                                 
              ELSE                                                              
                 MOVE '20' TO WS-FORMAT-DATE-CC                                 
              END-IF                                                            
00731         MOVE WS-FORMAT-MM   TO WS-FORMAT-DATE-MM                  ETKUP006
00732         MOVE WS-FORMAT-DD   TO WS-FORMAT-DATE-DD                  ETKUP006
00733         MOVE WS-FORMAT-YY   TO WS-FORMAT-DATE-YY                  ETKUP006
00734         MOVE WS-FORMAT-DATE TO WS-MAILBOX-REC-DTE                 ETKUP006
00735                                                                   ETKUP006
00736         MOVE ET020-RCPT-TME TO WS-FORMAT-HHMNSS                   ETKUP006
00737         MOVE WS-FORMAT-HH   TO WS-FORMAT-TIME-HH                  ETKUP006
00738         MOVE WS-FORMAT-MN   TO WS-FORMAT-TIME-MN                  ETKUP006
00739         MOVE WS-FORMAT-SS   TO WS-FORMAT-TIME-SS                  ETKUP006
00740         MOVE WS-FORMAT-TIME TO WS-MAILBOX-REC-TME                 ETKUP006
00741      ELSE                                                         ETKUP006
00742         MOVE '9999-09-09' TO WS-MAILBOX-REC-DTE                   ETKUP006
00743         MOVE '09.09.09'   TO WS-MAILBOX-REC-TME                   ETKUP006
00744      END-IF.                                                      ETKUP006
00745                                                                   ETKUP006
00746      IF ET020-PKUP-DTE NOT = ZEROES                               ETKUP006
00747         MOVE ET020-PKUP-DTE TO WS-FORMAT-YYMMDD                   ETKUP006
00748         MOVE WS-FORMAT-MM   TO WS-FORMAT-DATE-MM                  ETKUP006
00749         MOVE WS-FORMAT-DD   TO WS-FORMAT-DATE-DD                  ETKUP006
00750         MOVE WS-FORMAT-YY   TO WS-FORMAT-DATE-YY                  ETKUP006
              IF WS-FORMAT-YY > 80                                              
                 MOVE '19'        TO WS-FORMAT-DATE-CC                          
              ELSE                                                              
                 MOVE '20'        TO WS-FORMAT-DATE-CC                          
              END-IF                                                            
00751         MOVE WS-FORMAT-DATE TO WS-MAILBOX-PU-DTE                  ETKUP006
00752                                                                   ETKUP006
00753         MOVE ET020-PKUP-TME TO WS-FORMAT-HHMNSS                   ETKUP006
00754         MOVE WS-FORMAT-HH   TO WS-FORMAT-TIME-HH                  ETKUP006
00755         MOVE WS-FORMAT-MN   TO WS-FORMAT-TIME-MN                  ETKUP006
00756         MOVE WS-FORMAT-SS   TO WS-FORMAT-TIME-SS                  ETKUP006
00757         MOVE WS-FORMAT-TIME TO WS-MAILBOX-PU-TME                  ETKUP006
00758      ELSE                                                         ETKUP006
00759         MOVE '9999-09-09' TO WS-MAILBOX-PU-DTE                    ETKUP006
00760         MOVE '09.09.09'   TO WS-MAILBOX-PU-TME                    ETKUP006
00761      END-IF.                                                      ETKUP006
00762                                                                   ETKUP006
00763      MOVE ET020-CRE-DTE  TO WS-FORMAT-YYMMDD.                     ETKUP006
00764      MOVE WS-FORMAT-MM   TO WS-FORMAT-DATE-MM.                    ETKUP006
00765      MOVE WS-FORMAT-DD   TO WS-FORMAT-DATE-DD.                    ETKUP006
00766      MOVE WS-FORMAT-YY   TO WS-FORMAT-DATE-YY.                    ETKUP006
           IF WS-FORMAT-DATE-YY > 80                                            
              MOVE '19' TO WS-FORMAT-DATE-CC                                    
           ELSE                                                                 
              MOVE '20' TO WS-FORMAT-DATE-CC                                    
           END-IF.                                                              
00767      MOVE WS-FORMAT-DATE TO WS-DB2-CRE-DTE.                       ETKUP006
00768                                                                   ETKUP006
00769      MOVE ET020-CRE-TME  TO WS-FORMAT-HHMNSS.                     ETKUP006
00770      MOVE WS-FORMAT-HH   TO WS-FORMAT-TIME-HH.                    ETKUP006
00771      MOVE WS-FORMAT-MN   TO WS-FORMAT-TIME-MN.                    ETKUP006
00772      MOVE WS-FORMAT-SS   TO WS-FORMAT-TIME-SS.                    ETKUP006
00773      MOVE WS-FORMAT-TIME TO WS-DB2-CRE-TME.                       ETKUP006
00774                                                                   ETKUP006
00775  EJECT                                                            ETKUP006
00776  Z100-READ-TSET-RECORD.                                           ETKUP006
00777 ******************************************************************ETKUP006
00778 *    READ THE TRANSACTION SET FILE                                ETKUP006
00779 *                                                                 ETKUP006
00780 *    PERFORMED BY A100-MAIN-MODULE                                ETKUP006
00781 *                 B100-PROCESS-TSET-FILE.                         ETKUP006
00782 ******************************************************************ETKUP006
00783 *    DISPLAY 'Z100-READ-TSET-RECORD'.                             ETKUP006
00784                                                                   ETKUP006
00785      READ EDI-TSET-FILE                                           ETKUP006
00786          INTO ET020-TSET-DETAIL-RECORD                            ETKUP006
00787          AT END                                                   ETKUP006
00788              SET PS-END-OF-FILE TO TRUE.                          ETKUP006
00789                                                                   ETKUP006
00790  EJECT                                                            ETKUP006
00870  Z999-SQL-ABEND.                                                  ETKUP006
00871 ***************************************************************** ETKUP006
00872 *    ABEND ON AN SQL ERROR OR WARNING                             ETKUP006
00873 ***************************************************************** ETKUP006
00874 *    DISPLAY 'Z999-SQL-ABEND'.                                    ETKUP006
00875                                                                   ETKUP006
00876      DISPLAY '  '.                                                ETKUP006
00877      DISPLAY '** ABEND **'.                                       ETKUP006
00878      DISPLAY 'ETKUP006 - ABEND'.                                  ETKUP006
00879      DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                    ETKUP006
00880      DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.           ETKUP006
00881                                                                   ETKUP006
00882      COPY DPPD004.                                                ETKUP006
00883                                                                   ETKUP006
00884      EXEC SQL                                                     ETKUP006
00885          ROLLBACK                                                 ETKUP006
00886      END-EXEC.                                                    ETKUP006
00887                                                                   ETKUP006
00888      MOVE +4013 TO ABEND-CODE.                                    ETKUP006
00889      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP006
00890                                                                   ETKUP006
00891  EJECT                                                            ETKUP006
