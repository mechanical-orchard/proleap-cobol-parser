       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT201.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            01/15/2005.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  COMMON RECALC EXTRACT AND REFORMAT                            *        
      *                                                                *        
      *  THIS PROGRAM TAKES IN A LIST OF FISCAL DATES THAT HAVE BEEN   *        
      *  EXTRACTED FOR CURRENT FISCAL PERIOD (MULTIPLE DATES FOR       *        
      *  WEEKLY AND 1 FOR MONTHLY), AND GENERATE A ROW FROM THE        *        
      *  HISTORICAL FILE FOR EACH DATE(S).  THIS PROGRAM WILL ALSO     *        
      *  COMPLETE THE OTHER FIELDS THAT CAN BE DONE.                   *        
      *  NOTE:                                                         *        
      *    THIS PROGRAM RELIES ON HAVING THE FISCAL DATES IN THE       *        
      *    CONTROL CARD IN DESCENDING ORDER.                           *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 26006    2005-03-09  CHANGE TO SPLIT THE FILE UP INTO MULTIPLE *        
      *                      FILES BASE ON FISCAL DATES OR DEPTS -VLY  *        
      * 23843    2005-04-05  SET TYPE TO ZERO FOR ADDED HISTORY RECORD *        
      *                      BECAUSE SETTING TO +4 DISRUPT OTHER       *        
      *                      HISTORICAL TYPE THAT HAVE BEEN EXTRACTED  *        
X23843* 23843    2005-04-19  SIMPLIFY SPLIT LOGIC, REMOVE SPLIT BY DTE *        
      *                                                                *        
LUNGIS*          2006-10-11  L. MSIKINYA - ADD HISTORICAL DATA LOGIC   *        
LUNGIS*                                    FOR BRAND VENDOR - COMMON   *        
LUNGIS*                                    RECALC PROJECT.             *        
W34567* 34567    2008-04-25  J. SELWITSCHKA                            *        
W34567*                      CHANGES TO HANDLE PRORATED SHRINK TO BV   *        
W34567*                      LEVEL. PASS PRORATED SHRINK THRU          *        
W34567*                      CURR-SHNK-RTL-AMT.                        *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT REQUEST-DATE-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT HISTORICAL-DATA-FILE                                          
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CURRENT-DATA-FILE                                             
               ASSIGN TO INP03.                                                 
                                                                                
26006      SELECT COMMON-RECALC-EXTRACT-1                                       
26006          ASSIGN TO OUT01.                                                 
26006                                                                           
26006      SELECT COMMON-RECALC-EXTRACT-2                                       
26006          ASSIGN TO OUT02.                                                 
26006                                                                           
26006      SELECT COMMON-RECALC-EXTRACT-3                                       
26006          ASSIGN TO OUT03.                                                 
26006                                                                           
26006      SELECT COMMON-RECALC-EXTRACT-4                                       
26006          ASSIGN TO OUT04.                                                 
26006                                                                           
26006      SELECT COMMON-RECALC-EXTRACT-5                                       
26006          ASSIGN TO OUT05.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-DATE-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-DATE-REC                PIC X(80).                           
                                                                                
       FD  HISTORICAL-DATA-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
LUNGIS 01  HISTORICAL-DATA-REC             PIC X(400).                          
                                                                                
       FD  CURRENT-DATA-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
LUNGIS 01  CURRENT-DATA-REC                PIC X(400).                          
                                                                                
26006  FD  COMMON-RECALC-EXTRACT-1                                              
26006      RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
LUNGIS     RECORD CONTAINS 400 CHARACTERS                                       
26006      DATA RECORD IS COMMON-RECALC-REC-1.                                  
LUNGIS 01  COMMON-RECALC-REC-1             PIC X(400).                          
                                                                                
26006  FD  COMMON-RECALC-EXTRACT-2                                              
26006      RECORDING MODE IS F                                                  
26006      LABEL RECORDS ARE OMITTED                                            
LUNGIS     RECORD CONTAINS 400 CHARACTERS                                       
26006      DATA RECORD IS COMMON-RECALC-REC-2.                                  
26006  01  COMMON-RECALC-REC-2             PIC X(400).                          
26006                                                                           
26006  FD  COMMON-RECALC-EXTRACT-3                                              
26006      RECORDING MODE IS F                                                  
26006      LABEL RECORDS ARE OMITTED                                            
LUNGIS     RECORD CONTAINS 400 CHARACTERS                                       
26006      DATA RECORD IS COMMON-RECALC-REC-3.                                  
LUNGIS 01  COMMON-RECALC-REC-3             PIC X(400).                          
26006                                                                           
26006  FD  COMMON-RECALC-EXTRACT-4                                              
26006      RECORDING MODE IS F                                                  
26006      LABEL RECORDS ARE OMITTED                                            
LUNGIS     RECORD CONTAINS 400 CHARACTERS                                       
26006      DATA RECORD IS COMMON-RECALC-REC-4.                                  
LUNGIS 01  COMMON-RECALC-REC-4             PIC X(400).                          
26006                                                                           
26006  FD  COMMON-RECALC-EXTRACT-5                                              
26006      RECORDING MODE IS F                                                  
26006      LABEL RECORDS ARE OMITTED                                            
LUNGIS     RECORD CONTAINS 400 CHARACTERS                                       
26006      DATA RECORD IS COMMON-RECALC-REC-5.                                  
LUNGIS 01  COMMON-RECALC-REC-5             PIC X(400).                          
26006                                                                           
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  COMMON RECALC DATA EXTRACT                                             
      ******************************************************************        
      *01  ML070-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-DEFAULT-PARTN-GROUP      PIC S9(04)      VALUE +1             
                               COMP.                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-REQUEST-DATE      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-DATE                     VALUE 'Y'.           
               88  NOT-END-OF-REQUEST-DATE                 VALUE 'N'.           
           05  PS-END-OF-HISTORY-DATA      PIC X           VALUE 'N'.           
               88  END-OF-HISTORY-DATA                     VALUE 'Y'.           
               88  NOT-END-OF-HISTORY-DATA                 VALUE 'N'.           
           05  PS-END-OF-CURRENT-DATA      PIC X           VALUE 'N'.           
               88  END-OF-CURRENT-DATA                     VALUE 'Y'.           
               88  NOT-END-OF-CURRENT-DATA                 VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT REQUEST CONTROL REC ***'.                                
                                                                                
       01  RQ-CNTL-AREA.                                                        
           05  RQ-COMMENT-IND              PIC X(01)       VALUE SPACE.         
               88  RQ-COMMENTS                             VALUE '*'.           
           05  RQ-FSCL-PARTN-NBR           PIC 9(04)       VALUE ZEROES.        
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-FSCL-PERIOD-NBR          PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-FSCL-END-DTE             PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-FSCL-END-DTE.                                
               10  RQ-FSCL-END-DTE-CCYY    PIC X(04).                           
               10  RQ-FSCL-END-DTE-DASH-1  PIC X(01).                           
                   88  RQ-FSCL-END-DATE-VALID-DASH-1       VALUE '-'.           
               10  RQ-FSCL-END-DTE-MM      PIC X(02).                           
               10  RQ-FSCL-END-DTE-DASH-2  PIC X(01).                           
                   88  RQ-FSCL-END-DATE-VALID-DASH-2       VALUE '-'.           
               10  RQ-FSCL-END-DTE-DD      PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACE.         
           05  RQ-FSCL-DEPT-NBR            PIC 9(04)       VALUE ZEROES.        
           05  FILLER                      PIC X(56)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
26006      05  PV-TEMP-COMPUTE             PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-WRITE-INDX               PIC S9(04)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-OUTPUT-1                 PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-OUTPUT-2                 PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-OUTPUT-3                 PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-OUTPUT-4                 PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
26006      05  PV-OUTPUT-5                 PIC S9(15)      VALUE ZEROES         
26006                          COMP-3.                                          
           05  PV-TOTAL-OUTPUT             PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-HIST-EXTRACTED           PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-HIST-READ                PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-HIST-ADDED               PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-CURR-EXTRACTED           PIC S9(15)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CURR-READ                PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-SHRINK-ONLY-COUNT        PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-SHRINK-AND-CURR-COUNT    PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-LOAD-SHRINK-COUNT        PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-LOAD-WO-SHRINK-COUNT     PIC S9(15)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PARTN-NBR-HOLD           PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PARTN-GROUP-HOLD         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-DEPT-NBR-HOLD            PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DEPT-COUNT               PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-SHRINK-KEY-DATA.                                              
               10  PV-SHRINK-FSCL-END-DTE  PIC X(10)       VALUE SPACES.        
               10  PV-SHRINK-DEPT-NBR      PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SHRINK-MAJ-CL-NBR    PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SHRINK-SUB-CL-NBR    PIC S9(4)       VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SHRINK-LOC-NBR       PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
W34567         10  PV-SHRINK-ENT-ID        PIC S9(9)       VALUE ZEROES         
W34567                         COMP.                                            
W34567         10  PV-SHRINK-ML-LWST-ID    PIC S9(9)       VALUE ZEROES         
W34567                         COMP.                                            
W34567     05  PV-CURR-SHNK-RTL-AMT        PIC S9(11)V99   VALUE ZEROES         
W34567                         COMP-3.                                          
           05  PV-SHRINK-RTL               PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-SHRINK-SALES-AMT         PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-FSCL-END-DTE             PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE TABLE AREA ***'.                                
                                                                                
       01  PT-PROGRAM-TABLE-AREA.                                               
           05  PT-FSCL-INDX                PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PT-FSCL-COUNT               PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PT-FSCL-INDX-MAX            PIC S9(04)      VALUE +10            
                               COMP-3.                                          
           05  PT-FSCL-DPT-INDX            PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
           05  PT-FSCL-DPT-MAX             PIC S9(04)      VALUE +9999          
                               COMP-3.                                          
           05  PT-FSCL-TABLE OCCURS 010 TIMES.                                  
               10  PT-FSCL-PERIOD-NBR      PIC X(02)       VALUE SPACES.        
               10  PT-FSCL-END-DTE         PIC X(10)       VALUE SPACES.        
               10  PT-FSCL-REC-IN-COUNT    PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-FSCL-ADD-TO-HISTORY  PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-FSCL-MIN-PARTN       PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
               10  PT-FSCL-PARTN-COUNT     PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
               10  PT-FSCL-DEPT-TABLE OCCURS 9999 TIMES.                        
                   15  PT-FSCL-DEPT-NBR    PIC S9(04)      VALUE ZEROES         
                               COMP-3.                                          
                   15  PT-FSCL-PARTN-NBR   PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
                   15  PT-FSCL-PARTN-GROUP PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
26006              15  PT-FSCL-DEPT-SEQ    PIC S9(04)      VALUE ZEROES         
26006                          COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** ABEND AREA BEGIN ***'.                                          
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  CONTROL-CARD-ABEND                          VALUE +4001.         
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  NON-SQL-ABEND-CODE                          VALUE +4004.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4016.         
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           IF PT-FSCL-COUNT > ZEROES                                            
               DISPLAY ' FISCAL DATE/PERIOD LOADED: ' PT-FSCL-COUNT             
               DISPLAY ' '                                                      
               MOVE ZEROES         TO PV-DEPT-NBR-HOLD                          
               PERFORM Z200-READ-HISTORICAL-DATA                                
               PERFORM B200-PROCESS-HISTORY                                     
                   UNTIL END-OF-HISTORY-DATA                                    
                                                                                
               DISPLAY ' '                                                      
               MOVE ZEROES         TO PV-DEPT-NBR-HOLD                          
               PERFORM Z300-READ-CURRENT-DATA                                   
               PERFORM B300-PROCESS-CURRENT                                     
                   UNTIL END-OF-CURRENT-DATA                                    
           ELSE                                                                 
               DISPLAY ' *** NO CONTROL/FISCAL DATE TO PROCESS.'                
               SET LOGICAL-ISSUE-ABEND   TO TRUE                                
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           OPEN INPUT  REQUEST-DATE-FILE                                        
                       HISTORICAL-DATA-FILE                                     
                       CURRENT-DATA-FILE                                        
26006           OUTPUT COMMON-RECALC-EXTRACT-1                                  
26006                  COMMON-RECALC-EXTRACT-2                                  
26006                  COMMON-RECALC-EXTRACT-3                                  
26006                  COMMON-RECALC-EXTRACT-4                                  
26006                  COMMON-RECALC-EXTRACT-5.                                 
                                                                                
           PERFORM B125-INIT-PT-FSCL.                                           
                                                                                
           PERFORM B150-PROCESS-RQ-CNTL.                                        
           DISPLAY ' '                                                          
                   PT-FSCL-END-DTE (PT-FSCL-COUNT)                              
                   ' HAS '                                                      
                   PT-FSCL-PARTN-COUNT(PT-FSCL-COUNT)                           
                   ' PARTITIONS WITH '                                          
                   PV-DEPT-COUNT                                                
                   ' DEPT ' FUNCTION CURRENT-DATE.                              
                                                                                
      ******************************************************************        
      * INITIALIZE THE REQUEST FISCAL TABLE                                     
      ******************************************************************        
       B125-INIT-PT-FSCL.                                                       
                                                                                
           MOVE ZEROES             TO PT-FSCL-COUNT                             
                                      PT-FSCL-INDX.                             
           PERFORM                                                              
               PT-FSCL-INDX-MAX TIMES                                           
               ADD +1                  TO PT-FSCL-INDX                          
               MOVE SPACES TO PT-FSCL-PERIOD-NBR(PT-FSCL-INDX)                  
                              PT-FSCL-END-DTE (PT-FSCL-INDX)                    
               MOVE ZEROES TO PT-FSCL-REC-IN-COUNT(PT-FSCL-INDX)                
                              PT-FSCL-ADD-TO-HISTORY(PT-FSCL-INDX)              
                              PT-FSCL-PARTN-COUNT(PT-FSCL-INDX)                 
                                                                                
               MOVE ZEROES TO PT-FSCL-DPT-INDX                                  
               PERFORM                                                          
                   PT-FSCL-DPT-MAX TIMES                                        
                   ADD +1                  TO PT-FSCL-DPT-INDX                  
                   MOVE ZEROES                                                  
                       TO PT-FSCL-PARTN-NBR  (PT-FSCL-INDX,                     
                                              PT-FSCL-DPT-INDX)                 
                          PT-FSCL-DEPT-NBR   (PT-FSCL-INDX,                     
                                              PT-FSCL-DPT-INDX)                 
                          PT-FSCL-PARTN-GROUP(PT-FSCL-INDX,                     
                                              PT-FSCL-DPT-INDX)                 
26006                     PT-FSCL-DEPT-SEQ   (PT-FSCL-INDX,                     
26006                                         PT-FSCL-DPT-INDX)                 
               END-PERFORM                                                      
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *    PROCESS REQUEST CONTROL CARD AND/OR GET REQUEST DATES                
      ******************************************************************        
       B150-PROCESS-RQ-CNTL.                                                    
                                                                                
           DISPLAY 'PARTITION INFORMATION:'.                                    
           DISPLAY '  ----+----1----+----2----+----3----+----4'.                
           PERFORM Z100-READ-REQUEST-DATE.                                      
                                                                                
           MOVE ZEROES             TO PT-FSCL-COUNT.                            
                                                                                
           PERFORM                                                              
               UNTIL END-OF-REQUEST-DATE                                        
                                                                                
               DISPLAY '  ' RQ-CNTL-AREA                                        
      * IGNORE COMMENT RECORDS                                                  
               IF RQ-COMMENTS                                                   
                   CONTINUE                                                     
               ELSE                                                             
      * VALIDATE FISCAL DATE                                                    
                   IF RQ-FSCL-END-DTE-CCYY IS NUMERIC                           
                       AND RQ-FSCL-END-DTE-MM IS NUMERIC                        
                       AND RQ-FSCL-END-DTE-DD IS NUMERIC                        
                       AND RQ-FSCL-END-DATE-VALID-DASH-1                        
                       AND RQ-FSCL-END-DATE-VALID-DASH-2                        
      * LOAD PERIOD/DATE                                                        
                       IF PT-FSCL-COUNT = ZEROES                                
                           OR (PT-FSCL-END-DTE (PT-FSCL-COUNT)                  
                               NOT = RQ-FSCL-END-DTE)                           
                           IF PT-FSCL-COUNT NOT = ZEROES                        
                               DISPLAY ' '                                      
                                      PT-FSCL-END-DTE (PT-FSCL-COUNT)           
                                       ' HAS '                                  
                                      PT-FSCL-PARTN-COUNT(PT-FSCL-COUNT)        
                                       ' PARTITIONS WITH '                      
                                      PV-DEPT-COUNT                             
                                       ' DEPT ' FUNCTION CURRENT-DATE           
                           END-IF                                               
                           MOVE ZEROES             TO PV-DEPT-COUNT             
                           ADD +1                  TO PT-FSCL-COUNT             
                           MOVE RQ-FSCL-PERIOD-NBR                              
                               TO PT-FSCL-PERIOD-NBR(PT-FSCL-COUNT)             
                           MOVE RQ-FSCL-END-DTE                                 
                               TO PT-FSCL-END-DTE (PT-FSCL-COUNT)               
                           MOVE RQ-FSCL-PARTN-NBR                               
                               TO PT-FSCL-MIN-PARTN(PT-FSCL-COUNT)              
                       END-IF                                                   
                                                                                
      * COUNT THE NUMBER OF PARTITIONS                                          
                       IF RQ-FSCL-PARTN-NBR                                     
                           NOT = PV-PARTN-NBR-HOLD                              
                           ADD +1  TO PT-FSCL-PARTN-COUNT(PT-FSCL-COUNT)        
                           MOVE RQ-FSCL-PARTN-NBR TO PV-PARTN-NBR-HOLD          
                       END-IF                                                   
                       PERFORM B175-LOAD-DEPT-PARTN                             
                   END-IF                                                       
               END-IF                                                           
                                                                                
               PERFORM Z100-READ-REQUEST-DATE                                   
                                                                                
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *    LOAD DEPT AND ASSOCIATED PARTITION NUMBER                            
      ******************************************************************        
       B175-LOAD-DEPT-PARTN.                                                    
                                                                                
           ADD +1                  TO PV-DEPT-COUNT.                            
           MOVE RQ-FSCL-DEPT-NBR   TO PT-FSCL-DPT-INDX.                         
           MOVE RQ-FSCL-DEPT-NBR                                                
               TO PT-FSCL-DEPT-NBR(PT-FSCL-COUNT,                               
                                   PT-FSCL-DPT-INDX).                           
26006      MOVE PV-DEPT-COUNT                                                   
26006          TO PT-FSCL-DEPT-SEQ(PT-FSCL-COUNT,                               
26006                              PT-FSCL-DPT-INDX).                           
           MOVE RQ-FSCL-PARTN-NBR                                               
               TO PT-FSCL-PARTN-NBR (PT-FSCL-COUNT,                             
                                     PT-FSCL-DPT-INDX).                         
           COMPUTE PT-FSCL-PARTN-GROUP(PT-FSCL-COUNT,                           
                                       PT-FSCL-DPT-INDX)                        
                   = PT-FSCL-PARTN-NBR(PT-FSCL-COUNT,                           
                                       PT-FSCL-DPT-INDX)                        
                     - PT-FSCL-MIN-PARTN(PT-FSCL-COUNT) + 1.                    
                                                                                
      ******************************************************************        
      *    PROCESS THE HISTORICAL DATA                                          
      ******************************************************************        
       B200-PROCESS-HISTORY.                                                    
                                                                                
           IF PT-FSCL-COUNT = 1                                                 
               AND ML070-DEPT-NBR = PV-DEPT-NBR-HOLD                            
26006          ADD +1      TO PV-HIST-EXTRACTED                                 
               MOVE PT-FSCL-PERIOD-NBR (1)                                      
                   TO ML070-FSCL-PERIOD                                         
               MOVE PT-FSCL-END-DTE (1)                                         
                   TO ML070-FSCL-END-DTE                                        
               MOVE PV-PARTN-NBR-HOLD   TO ML070-PARTN-NBR                      
               MOVE PV-PARTN-GROUP-HOLD TO ML070-PARTN-GROUP                    
               PERFORM Z600-WRITE-COMMON-RECALC                                 
           ELSE                                                                 
               IF ML070-DEPT-NBR NOT = PV-DEPT-NBR-HOLD                         
                   DISPLAY ' PROCESSING HISTORY DEPT: '                         
                           ML070-DEPT-NBR                                       
                           ' ' FUNCTION CURRENT-DATE                            
                   MOVE ML070-DEPT-NBR TO PV-DEPT-NBR-HOLD                      
               END-IF                                                           
                                                                                
               MOVE ZEROES             TO PT-FSCL-INDX                          
               PERFORM                                                          
                   PT-FSCL-COUNT TIMES                                          
                                                                                
                   ADD +1      TO PT-FSCL-INDX                                  
                   ADD +1      TO PV-HIST-EXTRACTED                             
                   MOVE PT-FSCL-PERIOD-NBR (PT-FSCL-INDX)                       
                       TO ML070-FSCL-PERIOD                                     
                   MOVE PT-FSCL-END-DTE (PT-FSCL-INDX)                          
                       TO ML070-FSCL-END-DTE                                    
                   PERFORM B500-LOAD-PARTN-INFO                                 
                   MOVE ML070-PARTN-NBR   TO PV-PARTN-NBR-HOLD                  
                   MOVE ML070-PARTN-GROUP TO PV-PARTN-GROUP-HOLD                
                                                                                
                   PERFORM Z600-WRITE-COMMON-RECALC                             
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
           PERFORM Z200-READ-HISTORICAL-DATA.                                   
                                                                                
      ******************************************************************        
      *    PROCESS THE CURRENT DATA                                             
      *    THIS PROCESS RELIES ON CURRENT RECORD BEING IN THE RIGHT SEQ:        
      *       DEPT/MAJ/SUB-CLASS/LOC/ENT/FISCAL-DATE                            
      ******************************************************************        
       B300-PROCESS-CURRENT.                                                    
                                                                                
      * SHRINK ONLY RECORD?                                                     
           IF ML070-SHRINK-TYPE-TYPE                                            
               ADD +1      TO PV-SHRINK-ONLY-COUNT                              
               MOVE ML070-FSCL-END-DTE      TO PV-SHRINK-FSCL-END-DTE           
               MOVE ML070-DEPT-NBR          TO PV-SHRINK-DEPT-NBR               
               MOVE ML070-MAJ-CL-NBR        TO PV-SHRINK-MAJ-CL-NBR             
               MOVE ML070-SUB-CL-NBR        TO PV-SHRINK-SUB-CL-NBR             
               MOVE ML070-LOC-NBR           TO PV-SHRINK-LOC-NBR                
W34567         MOVE ML070-ML-LWST-ID        TO PV-SHRINK-ML-LWST-ID             
W34567         MOVE ML070-CURR-SHNK-RTL-AMT TO PV-CURR-SHNK-RTL-AMT             
               MOVE ML070-SHNK-RTL-AMT      TO PV-SHRINK-RTL                    
               MOVE ML070-SHNK-SLS-RTL-AMT  TO PV-SHRINK-SALES-AMT              
W34567         IF ML070-LOC-NBR NOT = ZERO                                      
W34567             PERFORM B310-GET-PARTN                                       
W34567             PERFORM Z600-WRITE-COMMON-RECALC                             
W34567         END-IF                                                           
           ELSE                                                                 
      * SHRINK AND CURRENT RECORD EXIST, SET TYPE TO CURRENT                    
               IF ML070-SHRINK-AND-CURR-EXIST                                   
                   ADD +1      TO PV-SHRINK-AND-CURR-COUNT                      
                   SET ML070-CURR-FISCAL-TYPE TO TRUE                           
               ELSE                                                             
                   IF ML070-FSCL-END-DTE      = PV-SHRINK-FSCL-END-DTE          
                       AND ML070-DEPT-NBR     = PV-SHRINK-DEPT-NBR              
                       AND ML070-MAJ-CL-NBR   = PV-SHRINK-MAJ-CL-NBR            
                       AND ML070-SUB-CL-NBR   = PV-SHRINK-SUB-CL-NBR            
                       AND ML070-LOC-NBR      = PV-SHRINK-LOC-NBR               
W34567                 AND ML070-ML-LWST-ID   = PV-SHRINK-ML-LWST-ID            
                       ADD +1      TO PV-LOAD-SHRINK-COUNT                      
W34567                 MOVE PV-CURR-SHNK-RTL-AMT                                
W34567                     TO ML070-CURR-SHNK-RTL-AMT                           
                       MOVE PV-SHRINK-RTL                                       
                           TO ML070-SHNK-RTL-AMT                                
                       MOVE PV-SHRINK-SALES-AMT                                 
                           TO ML070-SHNK-SLS-RTL-AMT                            
                   ELSE                                                         
                       ADD +1      TO PV-LOAD-WO-SHRINK-COUNT                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
W34567         PERFORM B310-GET-PARTN                                           
                                                                                
               ADD +1      TO PV-CURR-EXTRACTED                                 
               PERFORM Z600-WRITE-COMMON-RECALC                                 
                                                                                
               IF ML070-FSCL-END-DTE < PT-FSCL-END-DTE (1)                      
                   PERFORM B400-ADD-HISTORICAL                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM Z300-READ-CURRENT-DATA.                                      
                                                                                
W34567******************************************************************        
W34567*    GET PARTITION                                                        
W34567******************************************************************        
W34567 B310-GET-PARTN.                                                          
W34567                                                                          
W34567* DON'T SEARCH, IF PREVIOUS FOUND AND ONLY 1 WEEK OR MONTHLY              
W34567     IF PT-FSCL-COUNT = 1                                                 
W34567         AND ML070-DEPT-NBR = PV-DEPT-NBR-HOLD                            
W34567         MOVE PV-PARTN-NBR-HOLD   TO ML070-PARTN-NBR                      
W34567         MOVE PV-PARTN-GROUP-HOLD TO ML070-PARTN-GROUP                    
W34567     ELSE                                                                 
W34567         IF ML070-DEPT-NBR NOT = PV-DEPT-NBR-HOLD                         
W34567             DISPLAY ' PROCESSING CURRENT DEPT: '                         
W34567                     ML070-DEPT-NBR                                       
W34567                     ' ' FUNCTION CURRENT-DATE                            
W34567             MOVE ML070-DEPT-NBR TO PV-DEPT-NBR-HOLD                      
W34567         END-IF                                                           
W34567                                                                          
W34567* FIND PARTITION NUMBER AND GROUP FOR CURRENT RECORD                      
W34567         PERFORM                                                          
W34567             VARYING PT-FSCL-INDX FROM 1 BY 1                             
W34567             UNTIL PT-FSCL-INDX > PT-FSCL-COUNT                           
W34567                 OR ML070-FSCL-END-DTE                                    
W34567                     = PT-FSCL-END-DTE(PT-FSCL-INDX)                      
W34567             CONTINUE                                                     
W34567         END-PERFORM                                                      
W34567* PARTITION NUMBER AND GROUP NOT FOUND                                    
W34567         IF PT-FSCL-INDX > PT-FSCL-COUNT                                  
W34567             MOVE ZEROES             TO ML070-PARTN-NBR                   
W34567                                        ML070-PARTN-GROUP                 
W34567                                        PV-PARTN-NBR-HOLD                 
W34567                                        PV-PARTN-GROUP-HOLD               
W34567         ELSE                                                             
W34567* SET PARTITION NUMBER AND GROUP                                          
W34567             PERFORM B500-LOAD-PARTN-INFO                                 
W34567             MOVE ML070-PARTN-NBR   TO PV-PARTN-NBR-HOLD                  
W34567             MOVE ML070-PARTN-GROUP TO PV-PARTN-GROUP-HOLD                
W34567         END-IF                                                           
W34567     END-IF.                                                              
W34567                                                                          
      ******************************************************************        
      *    BUILD/ADD HISTORICAL DATA                                            
      ******************************************************************        
       B400-ADD-HISTORICAL.                                                     
                                                                                
23843      MOVE ZEROES                   TO ML070-TYPE.                         
           MOVE ML070-CURR-RCPT-RTL-AMT                                         
               TO ML070-SEAS-RCPT-RTL-AMT.                                      
           MOVE ML070-CURR-RCPT-NET-COST-AMT                                    
               TO ML070-SEAS-RCPT-NET-COST-AMT.                                 
           MOVE ML070-CURR-PADJ-RTL-AMT                                         
               TO ML070-SEAS-PADJ-RTL-AMT.                                      
           MOVE ML070-CURR-PADJ-NET-COST-AMT                                    
               TO ML070-SEAS-PADJ-NET-COST-AMT.                                 
           MOVE ML070-CURR-MKUP-RTL-AMT                                         
               TO ML070-SEAS-MKUP-RTL-AMT.                                      
           MOVE ML070-CURR-XFER-IN-RTL-AMT                                      
               TO ML070-SEAS-XFER-IN-RTL-AMT.                                   
           MOVE ML070-CURR-XFER-OUT-RTL-AMT                                     
               TO ML070-SEAS-XFER-OUT-RTL-AMT.                                  
LUNGIS     MOVE ML070-INV-COST-ADJ1                                             
LUNGIS         TO ML070-SEAS-INV-COST-ADJ1.                                     
LUNGIS     MOVE ML070-INV-COST-ADJ2                                             
LUNGIS         TO ML070-SEAS-INV-COST-ADJ2.                                     
           MOVE ZEROES  TO ML070-PREV-END-INV-RTL-AMT                           
                           ML070-PREV-END-INV-COST-AMT                          
                           ML070-CURR-RCPT-RTL-AMT                              
                           ML070-CURR-PADJ-RTL-AMT                              
                           ML070-CURR-MKUP-RTL-AMT                              
                           ML070-CURR-MKDN-RTL-AMT                              
                           ML070-CURR-XFER-IN-RTL-AMT                           
                           ML070-CURR-XFER-OUT-RTL-AMT                          
                           ML070-CURR-SLS-RTL-AMT                               
                           ML070-CURR-INV-ADJ-RTL-AMT                           
                           ML070-CURR-SHNK-RTL-AMT                              
                           ML070-CURR-END-INV-RTL-AMT                           
                           ML070-CURR-CUM-MKUP-PCT                              
                           ML070-CURR-END-INV-COST-AMT                          
                           ML070-CURR-SLS-COST-AMT                              
                           ML070-CURR-GRO-MRGN-AMT                              
                           ML070-CURR-RCPT-NET-COST-AMT                         
                           ML070-CURR-PADJ-NET-COST-AMT                         
                           ML070-BEG-SEAS-INV-RTL-AMT                           
                           ML070-BEG-SEAS-INV-COST-AMT                          
                           ML070-SHNK-RTL-AMT                                   
                           ML070-SHNK-SLS-RTL-AMT                               
LUNGIS                     ML070-INV-COST-ADJ1                                  
LUNGIS                     ML070-INV-COST-ADJ1.                                 
                                                                                
           MOVE ML070-FSCL-END-DTE TO PV-FSCL-END-DTE.                          
      * GENERATE SEASON-TO-DATE ROW FOR ALL DATES THAT ARE                      
      * GREATER THAN CURRENT ROW'S DATE                                         
           MOVE ZEROES             TO PT-FSCL-INDX.                             
           PERFORM                                                              
               PT-FSCL-COUNT TIMES                                              
                                                                                
               ADD +1                 TO PT-FSCL-INDX                           
               IF PT-FSCL-END-DTE (PT-FSCL-INDX) > PV-FSCL-END-DTE              
                   ADD +1      TO PV-HIST-ADDED                                 
                   MOVE PT-FSCL-PERIOD-NBR (PT-FSCL-INDX)                       
                       TO ML070-FSCL-PERIOD                                     
                   MOVE PT-FSCL-END-DTE (PT-FSCL-INDX)                          
                       TO ML070-FSCL-END-DTE                                    
                   PERFORM B500-LOAD-PARTN-INFO                                 
                                                                                
                   PERFORM Z600-WRITE-COMMON-RECALC                             
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      *    RETRIEVE THE PARTITION NUMBER AND GROUP                              
      ******************************************************************        
       B500-LOAD-PARTN-INFO.                                                    
                                                                                
           IF PT-FSCL-PARTN-COUNT(PT-FSCL-INDX) > +1                            
               MOVE ML070-DEPT-NBR         TO PT-FSCL-DPT-INDX                  
               MOVE PT-FSCL-PARTN-NBR(PT-FSCL-INDX,                             
                                      PT-FSCL-DPT-INDX)                         
                   TO ML070-PARTN-NBR                                           
               MOVE PT-FSCL-PARTN-GROUP(PT-FSCL-INDX,                           
                                        PT-FSCL-DPT-INDX)                       
                   TO ML070-PARTN-GROUP                                         
           ELSE                                                                 
               MOVE PT-FSCL-MIN-PARTN(PT-FSCL-INDX)                             
                   TO ML070-PARTN-NBR                                           
               MOVE PC-DEFAULT-PARTN-GROUP TO ML070-PARTN-GROUP                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' **** HISTORY INPUT:        ' PV-HIST-READ.                 
           DISPLAY '        HISTORY EXTRACTED:  ' PV-HIST-EXTRACTED.            
           DISPLAY ' **** CURRENT INPUT:        ' PV-CURR-READ.                 
           DISPLAY '        HISTORY ADDED:      ' PV-HIST-ADDED.                
           DISPLAY '        CURRENT EXTRACTED:  ' PV-CURR-EXTRACTED.            
           DISPLAY '        SHRINK ONLY COUNT:  ' PV-SHRINK-ONLY-COUNT.         
           DISPLAY '        SHRINK W/CURRENT:   '                               
                   PV-SHRINK-AND-CURR-COUNT.                                    
           DISPLAY '        LOAD SHRINK COUNT:  ' PV-LOAD-SHRINK-COUNT.         
           DISPLAY '        LOAD W/O SHRINK:    '                               
                   PV-LOAD-WO-SHRINK-COUNT.                                     
           COMPUTE PV-TOTAL-OUTPUT = PV-HIST-EXTRACTED                          
                                   + PV-OUTPUT-1                                
                                   + PV-OUTPUT-2                                
                                   + PV-OUTPUT-3                                
                                   + PV-OUTPUT-4                                
                                   + PV-OUTPUT-5.                               
26006      DISPLAY ' **** OUTPUT 1:             ' PV-OUTPUT-1.                  
26006      DISPLAY ' **** OUTPUT 2:             ' PV-OUTPUT-2.                  
26006      DISPLAY ' **** OUTPUT 3:             ' PV-OUTPUT-3.                  
26006      DISPLAY ' **** OUTPUT 4:             ' PV-OUTPUT-4.                  
26006      DISPLAY ' **** OUTPUT 5:             ' PV-OUTPUT-5.                  
           DISPLAY ' **** TOTAL EXTRACTED:      ' PV-TOTAL-OUTPUT.              
                                                                                
           CLOSE   REQUEST-DATE-FILE                                            
                   HISTORICAL-DATA-FILE                                         
                   CURRENT-DATA-FILE                                            
26006              COMMON-RECALC-EXTRACT-1                                      
26006              COMMON-RECALC-EXTRACT-2                                      
26006              COMMON-RECALC-EXTRACT-3                                      
26006              COMMON-RECALC-EXTRACT-4                                      
26006              COMMON-RECALC-EXTRACT-5.                                     
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z100-READ-REQUEST-DATE.                                                  
                                                                                
           READ REQUEST-DATE-FILE INTO RQ-CNTL-AREA                             
               AT END SET END-OF-REQUEST-DATE TO TRUE.                          
                                                                                
      ******************************************************************        
      *    READ HISTORICAL DATA                                                 
      ******************************************************************        
       Z200-READ-HISTORICAL-DATA.                                               
                                                                                
           ADD +1 TO PV-HIST-READ.                                              
           READ HISTORICAL-DATA-FILE INTO ML070-COMMON-RECALC-EXTRACT           
               AT END SET END-OF-HISTORY-DATA TO TRUE.                          
                                                                                
      ******************************************************************        
      *    READ CURRENT DATA                                                    
      ******************************************************************        
       Z300-READ-CURRENT-DATA.                                                  
                                                                                
           ADD +1 TO PV-CURR-READ.                                              
           READ CURRENT-DATA-FILE    INTO ML070-COMMON-RECALC-EXTRACT           
               AT END SET END-OF-CURRENT-DATA TO TRUE.                          
                                                                                
      ******************************************************************        
      *    WRITE OUT COMMON-RECALC EXTRACT                                      
      ******************************************************************        
       Z600-WRITE-COMMON-RECALC.                                                
                                                                                
      * DEPT THAT DOES NOT EXIST IN THE FIRST FISCAL DATE WILL BE SENT          
      * TO FILE 1.                                                              
26006      DIVIDE PT-FSCL-DEPT-SEQ(1, ML070-DEPT-NBR) BY 5                      
26006          GIVING PV-TEMP-COMPUTE                                           
26006          REMAINDER PV-WRITE-INDX.                                         
26006      ADD +1 TO PV-WRITE-INDX.                                             
26006      IF PV-WRITE-INDX > 5                                                 
26006          MOVE +5         TO PV-WRITE-INDX                                 
26006      ELSE                                                                 
26006          IF PV-WRITE-INDX < 1                                             
26006              MOVE +1         TO PV-WRITE-INDX                             
26006          END-IF                                                           
26006      END-IF.                                                              
26006                                                                           
26006      EVALUATE PV-WRITE-INDX                                               
26006          WHEN 1                                                           
26006              ADD +1          TO PV-OUTPUT-1                               
26006              WRITE COMMON-RECALC-REC-1                                    
26006                  FROM ML070-COMMON-RECALC-EXTRACT                         
26006          WHEN 2                                                           
26006              ADD +1          TO PV-OUTPUT-2                               
26006              WRITE COMMON-RECALC-REC-2                                    
26006                  FROM ML070-COMMON-RECALC-EXTRACT                         
26006          WHEN 3                                                           
26006              ADD +1          TO PV-OUTPUT-3                               
26006              WRITE COMMON-RECALC-REC-3                                    
26006                  FROM ML070-COMMON-RECALC-EXTRACT                         
26006          WHEN 4                                                           
26006              ADD +1          TO PV-OUTPUT-4                               
26006              WRITE COMMON-RECALC-REC-4                                    
26006                  FROM ML070-COMMON-RECALC-EXTRACT                         
26006          WHEN 5                                                           
26006              ADD +1          TO PV-OUTPUT-5                               
26006              WRITE COMMON-RECALC-REC-5                                    
26006                  FROM ML070-COMMON-RECALC-EXTRACT                         
26006      END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
