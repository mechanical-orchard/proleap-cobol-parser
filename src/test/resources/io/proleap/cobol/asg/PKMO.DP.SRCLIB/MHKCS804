      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    MHKCS804.                                                 
       AUTHOR.        KRISTIN PETERSON.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-10-2004.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *               CICS PROGRAM - MHKCS804 - F804                   *        
      *                                                                *        
      *        MERCHANDISE INQUIRY - REGIONAL CLEARANCE PRICE SCREEN   *        
      *                                                                *        
      *  THIS PROGRAM WILL DISPLAY THE CLEARANCE PRICE BY STORE FOR A  *        
      *  SPECIFIC SKU.  THIS IS AN ONLINE NON-SELECTABLE INQUIRY LIST. *        
      *                                                                *        
      *  THIS PROGRAM MAY ONLY BE CALLED WITH A SINGLE SKU.  THIS      *        
      *  PROGRAM EXPECTS THAT THE SKU NUMBER, SKU DESCRIPTION, AND     *        
      *  ITEM NUMBER WILL BE PASSED IN THE INTER-APPLICATION COMMAREA. *        
      *                                                                *        
      ******************************************************************        
      *  MAINTENANCE                                                   *        
      ******************************************************************        
      *  08/10/2004 - KRISTIN PETERSON  (WR# 26354)                    *        
      *    INITIAL INSTALLATION.  NOTE THAT THIS IS A REVISED VERSION  *        
      *    OF PROGRAM FSKCS080.                                        *        
      ******************************************************************        
      *  08/30/2004 - MARIA KLAMIK  (P000686856)                       *        
      *    INITIAL INSTALLATION - FIX ASC-SPECIFIC-COMMAREA INITIALIZE *        
      *    PROCESSING - TEMP STORAGE QUEUE NAME INITIALIZED IN WRONG   *        
      *    PARAGRAPH.                                                  *        
      ******************************************************************        
262978* CHANGED 10/21/2021    BY JIM HUNTER      WR: CHG0262978                 
262978* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
262978* MAKING THE CALL DYNAMIC VS STATIC.                                      
262978******************************************************************        
CICS  * CHANGED 10/28/2022    BY JIM HUNTER      WR: CHG0277420                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME            PIC X(08)   VALUE                 
                                                          'MH804A  '.           
           05  PC-CURRENT-MAPSET-NAME         PIC X(08)   VALUE                 
                                                          'MHKM804 '.           
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)   VALUE                 
                                                          'MHKCS804'.           
           05  PC-SEL-QUEUE-SEQ               PIC 9(01)   VALUE 2.              
           05  PC-DTL-QUEUE-SEQ               PIC 9(01)   VALUE 3.              
           05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
           05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13             
                                                          COMP SYNC.            
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
                                                          COMP SYNC.            
           05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
                                                          COMP SYNC.            
           05  PC-ABSTIME                     PIC S9(18)  VALUE ZERO            
                                                          COMP SYNC.            
           05  PC-SYSTEM-DATE                 PIC X(06).                        
                                                                                
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.          
           05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.          
           05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
           05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.          
           05  PC-TSYMSG-00113                PIC 9(5)    VALUE 00113.          
           05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.          
           05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.          
           05  PC-TSYMSG-02134                PIC 9(5)    VALUE 02134.          
                                                                                
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'Y'.           
               88  PS-ERROR                                VALUE 'N'.           
           05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-LIST-END                             VALUE 'Y'.           
               88  PS-NO-LIST-END                          VALUE 'N'.           
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-STORES-PROCESSED-SW         PIC  X       VALUE 'N'.           
               88  PS-ALL-STORES-PROCESSED                 VALUE 'Y'.           
               88  PS-ALL-STORES-NOT-PROCESSED             VALUE 'N'.           
           05  PS-DETAIL-SW                   PIC  X       VALUE 'N'.           
               88  PS-NO-DETAIL-FOUND                      VALUE 'Y'.           
               88  PS-DETAIL-FOUND                         VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-TODAYS-DATE                 PIC 9(7)    VALUE ZERO.           
           05  FILLER REDEFINES PV-TODAYS-DATE.                                 
               10  FILLER                     PIC X(2).                         
               10  PV-TODAYS-YYDDD            PIC 9(5).                         
           05  PV-EFF-DATE                    PIC X(10)   VALUE SPACES.         
           05  FILLER REDEFINES PV-EFF-DATE.                                    
               10  PV-EFF-DATE-CCYY           PIC X(04).                        
               10  FILLER                     PIC X(01).                        
               10  PV-EFF-DATE-MM             PIC X(02).                        
               10  FILLER                     PIC X(01).                        
               10  PV-EFF-DATE-DD             PIC X(02).                        
           05  PV-EFF-DATE-FMT.                                                 
               10  PV-EFF-DATE-FMT-MM         PIC X(02)   VALUE SPACES.         
               10  PV-SLASH-1                 PIC X(01)   VALUE '/'.            
               10  PV-EFF-DATE-FMT-DD         PIC X(02)   VALUE SPACES.         
               10  PV-SLASH-2                 PIC X(01)   VALUE '/'.            
               10  PV-EFF-DATE-FMT-CCYY       PIC X(04)   VALUE SPACES.         
           05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-CICS-RESP-ENROUTE           PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
                                                          COMP SYNC.            
           05  PV-STORES-FETCHED              PIC S9(09)  VALUE +0              
                                                          COMP-3.               
           05  PV-STORE-NMBR                  PIC S9(04)  VALUE +0              
                                                          COMP.                 
           05  PV-CITY                        PIC X(30)   VALUE SPACES.         
           05  PV-STATE                       PIC X(02)   VALUE SPACES.         
           05  PV-MKT-CLUSTER                 PIC X(30)   VALUE SPACES.         
                                                                                
      *---------------------------------------------------------------*         
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
      *---------------------------------------------------------------*         
           COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
           COPY DPWS020.                                                        
           COPY MHWS000.                                                        
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-TSK-HEADER-INFO.                                     
                       20  ASC-SKU.                                             
                           25  ASC-DEPT-NMBR        PIC  X(03).                 
                           25  ASC-DEPT-9           REDEFINES                   
                               ASC-DEPT-NMBR        PIC  9(03).                 
                           25  ASC-MCL-NMBR         PIC  X(02).                 
                           25  ASC-MCL-9            REDEFINES                   
                               ASC-MCL-NMBR                                     
                                                    PIC  99.                    
                           25  ASC-SCL-NMBR         PIC  X(02).                 
                           25  ASC-SCL-9            REDEFINES                   
                               ASC-SCL-NMBR         PIC  99.                    
                           25  ASC-SKU-NMBR         PIC  X(08).                 
                           25  ASC-SKU-NMBR-9       REDEFINES                   
                               ASC-SKU-NMBR         PIC  9(08).                 
                   15  ASC-SKU-DESC                 PIC  X(25).                 
                   15  ASC-REG-RTL-AMT              PIC  ZZZZZZZ.ZZ.            
                   15  ASC-SKU-ITEM-NBR             PIC  S9(15)V COMP-3.        
                   15  ASC-HEADER-ALREADY-MOVED-SW  PIC X(01).                  
                       88  ASC-HEADER-ALREADY-MOVED        VALUE 'Y'.           
                       88  ASC-HEADER-NOT-ALREADY-MOVED    VALUE 'N'.           
                   15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
                       20  ASC-BLOCK-MODIFIED-SW                                
                                                 PIC X(01).                     
                           88  ASC-BLOCK-NOT-MODIFIED    VALUE 'N'.             
                           88  ASC-BLOCK-MODIFIED        VALUE 'Y'.             
                       20  ASC-ITEM-ARRAY.                                      
                           25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.                
                               30  ASC-LIST-ITEM-NUMBER    PIC S9(09)           
                                                           COMP-3.              
                               30  ASC-STORE-NMBR          PIC  9(05).          
                               30  ASC-STORE-SHORT-NAME    PIC  X(10).          
                               30  ASC-CLR-RETAIL-AMT      PIC  S9(7)V99        
                                                                COMP-3.         
                               30  ASC-METRO-AREA          PIC  X(15).          
                               30  ASC-STATE               PIC  X(02).          
                               30  ASC-MKT-CLSTR           PIC  X(15).          
                               30  ASC-EFF-DTE             PIC  X(10).          
                   15  ASC-BLK-SUB                         PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  ASC-ITEM-SUB                        PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
                                                               COMP-3.          
                   15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
                                                               COMP-3.          
                   15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
                       88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.         
                       88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.         
                   15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
                       88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
                   15  ASC-LIST-QUEUE-NAME.                                     
                       20  ASC-LIST-TERM-ID                PIC  X(04).          
                       20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
                                                               COMP-3.          
                       20  ASC-LIST-SEQ                    PIC  9(01).          
                   15  ASC-SEL-QUEUE-NAME.                                      
                       20  ASC-SEL-TERM-ID                 PIC  X(04).          
                       20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
                                                                COMP-3.         
                       20  ASC-SEL-SEQ                     PIC  9(01).          
                   15  ASC-START-ITEM-REQUEST.                                  
                       20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
                   15  ASC-INVALID-RCP-SKU-SW              PIC  X(01).          
                       88  ASC-INVALID-RCP-SKU              VALUE 'Y'.          
                       88  ASC-VALID-RCP-SKU                VALUE 'N'.          
      *---------------------------------------------------------------*         
      *    TS QUEUE AREA                                              *         
      *---------------------------------------------------------------*         
                                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
           COPY DPWS013.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *          DATE ROUTINE COPYBOOKS                               *         
      *---------------------------------------------------------------*         
           COPY DPWS500.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *          DATE ROUTINE COPYBOOKS                               *         
      *---------------------------------------------------------------*         
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
           COPY MHKM804.                                                        
                                                                                
       01  MR-OCCURS-LAYOUT          REDEFINES  MH804AO.                        
           05  FILLER                PIC X(122).                                
           05  MR-SCROLL-AREA OCCURS 13 TIMES.                                  
               10  MR-STORE-NMBRL             PIC S9(04) COMP.                  
               10  MR-STORE-NMBRA             PIC  X(01).                       
               10  MR-STORE-NMBRC             PIC  X(01).                       
               10  MR-STORE-NMBRH             PIC  X(01).                       
               10  MR-STORE-NMBR              PIC  X(05).                       
                                                                                
               10  MR-STORE-NAMEL             PIC S9(04) COMP.                  
               10  MR-STORE-NAMEA             PIC  X(01).                       
               10  MR-STORE-NAMEC             PIC  X(01).                       
               10  MR-STORE-NAMEH             PIC  X(01).                       
               10  MR-STORE-NAME              PIC  X(10).                       
                                                                                
               10  MR-RTL-AMTL                PIC S9(04) COMP.                  
               10  MR-RTL-AMTA                PIC  X(01).                       
               10  MR-RTL-AMTC                PIC  X(01).                       
               10  MR-RTL-AMTH                PIC  X(01).                       
               10  MR-RTL-AMT                 PIC  ZZZZZZZ.ZZ.                  
                                                                                
               10  MR-METRO-AREAL             PIC S9(04) COMP.                  
               10  MR-METRO-AREAA             PIC  X(01).                       
               10  MR-METRO-AREAC             PIC  X(01).                       
               10  MR-METRO-AREAH             PIC  X(01).                       
               10  MR-METRO-AREA              PIC  X(15).                       
                                                                                
               10  MR-STATEL                  PIC S9(04) COMP.                  
               10  MR-STATEA                  PIC  X(01).                       
               10  MR-STATEC                  PIC  X(01).                       
               10  MR-STATEH                  PIC  X(01).                       
               10  MR-STATE                   PIC  X(02).                       
                                                                                
               10  MR-MKT-CLSTRL              PIC S9(04) COMP.                  
               10  MR-MKT-CLSTRA              PIC  X(01).                       
               10  MR-MKT-CLSTRC              PIC  X(01).                       
               10  MR-MKT-CLSTRH              PIC  X(01).                       
               10  MR-MKT-CLSTR               PIC  X(15).                       
                                                                                
               10  MR-EFF-DTEL                PIC S9(04) COMP.                  
               10  MR-EFF-DTEA                PIC  X(01).                       
               10  MR-EFF-DTEC                PIC  X(01).                       
               10  MR-EFF-DTEH                PIC  X(01).                       
               10  MR-EFF-DTE                 PIC  X(10).                       
                                                                                
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSK                                           *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSK                                                     
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TSKXREF                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TSKXREF                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE UPC SKU TABLE TO INCLUDE  *         
      *  REPLENISHMENT SKUS                                           *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *  SQL AND COBOL DECLARATIONS FOR THE UPC TABLE TO INCLUDE      *         
      *  REPLENISHMENT SKUS                                           *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TUPCPLS                                       *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TUPCPLS                                                 
           END-EXEC.                                                            
                                                                                
      * --------------------------------------------------------------*         
      *    LOCATION HIERARCHY -- XMT_LOC                              *         
      * --------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      * --------------------------------------------------------------*         
      *    LOCATION HIERARCHY -- XMT_STR_GP_MBSHP                     *         
      * --------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00003                                                 
           END-EXEC.                                                            
                                                                                
      * --------------------------------------------------------------*         
      *    LOCATION HIERARCHY -- XMT_STR_GP                           *         
      * --------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00004                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    CURSOR TO OBTAIN ALL STORE NUMBERS, EFFECTIVE DATE         *         
      *    AND THE UNIT RETAIL AMOUNT FOR A SPECIFIC SKU/ITEM         *         
      *    THAT IS CLEARANCE.                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                DECLARE CL-STORES-CSR CURSOR                                    
                FOR SELECT                                                      
                      STR_NBR                                                   
                     ,EFF_BEG_TMST                                              
                     ,UNT_RTL_AMT                                               
                FROM  TUPCPLS                                                   
                WHERE ITM_NBR      = :ASC-SKU-ITEM-NBR                          
                  AND OPERCO_NBR   = '03'                                       
                  AND UPC_STAT_CDE = '30'                                       
                  AND STR_NBR ^= 0                                              
                  AND ((EFF_BEG_TMST <= CURRENT TIMESTAMP                       
                      AND EFF_END_TMST >= CURRENT TIMESTAMP)                    
                      OR (EFF_BEG_TMST <= CURRENT TIMESTAMP                     
                         AND EFF_END_TMST IS NULL))                             
                ORDER BY STR_NBR                                                
           END-EXEC.                                                            
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * MAINLINE PARAGRAPH                                                      
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
           MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
           MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
           SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
           MOVE LENGTH OF MH804AI       TO DP030-MAP-LENGTH (1).                
           MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING.                                     
                                                                                
           MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
      ******************************************************************        
      *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
      *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
      ******************************************************************        
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
                                                                                
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          MH804AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK TO TRUE                                    
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                     TO DP013-MESSAGE-TEXT (3)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
      * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
      *                                                                *        
      * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
      *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
      *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
      *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
      ******************************************************************        
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EXEC CICS                                                            
               ASKTIME                                                          
                   ABSTIME(PC-ABSTIME)                                          
           END-EXEC.                                                            
                                                                                
           EXEC CICS                                                            
               FORMATTIME                                                       
                   ABSTIME(PC-ABSTIME)                                          
                   MMDDYY(PC-SYSTEM-DATE)                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
MK                 INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
                                            ASC-SEL-TERM-ID                     
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                         TO ASC-LIST-TASK-NUMBER                
                                            ASC-SEL-TASK-NUMBER                 
                   MOVE PC-DTL-QUEUE-SEQ TO ASC-LIST-SEQ                        
                   MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
                   MOVE PC-DTL-QUEUE-SEQ TO ASC-SEL-SEQ                         
                   PERFORM 1100-PROCESS-INTER-APPL-COMM                         
                   IF  PS-COMMAREA-NOT-VALID                                    
                       SET DP020-NEXT-ACT-APPL-ERROR                            
                                     TO  TRUE                                   
                   ELSE                                                         
                       IF ASC-INVALID-RCP-SKU                                   
                          PERFORM 2050-DELETE-DETAIL-QUEUE                      
                          MOVE PC-TSYMSG-02134 TO DP020-MSG-NUMBER              
                          SET DP020-MSG-INFORMATIONAL TO TRUE                   
                          PERFORM 4000-BUILD-INITIAL-PANEL                      
                       ELSE                                                     
                          PERFORM 2050-DELETE-DETAIL-QUEUE                      
                          PERFORM 4000-BUILD-INITIAL-PANEL                      
                       END-IF                                                   
                   END-IF                                                       
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                     TO PV-TOP-ITEM                             
                   PERFORM 2000-PROCESS-PANEL                                   
               WHEN DP020-NEXT-ACT-RETURN                                       
                   MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
                                     TO PV-TOP-ITEM                             
                   MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL                
                   PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                         
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA       *        
      ******************************************************************        
                                                                                
       1100-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           SET PS-COMMAREA-VALID            TO TRUE.                            
           MOVE SPACE                       TO ASC-START-ITEM-REQUEST.          
                                                                                
           MOVE MH000-DEPT-NUMBER-X    TO ASC-DEPT-NMBR.                        
           MOVE MH000-MCL-NUMBER-X     TO ASC-MCL-NMBR.                         
           MOVE MH000-SCL-NUMBER-X     TO ASC-SCL-NMBR.                         
           MOVE MH000-SKU-NUMBER-X     TO ASC-SKU-NMBR.                         
           MOVE MH000-SKU-NUMBER-9     TO SKXREF-SKU.                           
           MOVE MH000-SKU-DESCRIPTION  TO ASC-SKU-DESC.                         
           MOVE MH000-ITEM-NUMBER      TO ASC-SKU-ITEM-NBR.                     
                                                                                
           IF  (ASC-DEPT-NMBR > SPACES)                                         
           AND (ASC-MCL-NMBR > SPACES)                                          
           AND (ASC-SCL-NMBR > SPACES)                                          
           AND (ASC-SKU-NMBR > SPACES)                                          
           AND (ASC-SKU-DESC > SPACES)                                          
           AND (ASC-SKU-ITEM-NBR > 0)                                           
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PC-TSYMSG-00148 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-FATAL TO TRUE                                      
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
      ******************************************************************        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                        
                   PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
                   PERFORM 2050-DELETE-DETAIL-QUEUE                             
                   IF ASC-INVALID-RCP-SKU                                       
                      MOVE PC-TSYMSG-02134 TO DP020-MSG-NUMBER                  
                      SET DP020-MSG-INFORMATIONAL TO TRUE                       
                   END-IF                                                       
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                   MOVE +1 TO PV-TOP-ITEM                                       
                   PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                   PERFORM 2110-BROWSE-BACKWARD                                 
                   PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                   PERFORM 2120-BROWSE-FORWARD                                  
                   PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
                   IF  PS-ERROR                                                 
                       CONTINUE                                                 
                   ELSE                                                         
                       IF ASC-INVALID-RCP-SKU                                   
                          MOVE PC-TSYMSG-02134 TO DP020-MSG-NUMBER              
                          SET DP020-MSG-INFORMATIONAL TO TRUE                   
                       END-IF                                                   
                       PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                     
                       PERFORM 3200-RESET-ATTRIBUTES                            
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   SET DP013-CICS-ABEND TO TRUE                                 
                   MOVE '2000-PROCESS-PANEL' TO DP013-PARAGRAPH                 
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    DELETE THE TEMPORARY STORAGE QUEUE.                         *        
      ******************************************************************        
       2050-DELETE-DETAIL-QUEUE.                                                
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
              (PV-CICS-RESP = DFHRESP(QIDERR))                                  
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-NO-ROLLBACK TO TRUE                                    
                                                                                
               MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH               
               MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'          
                              TO DP013-MESSAGE-TEXT (1)                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                                
                                                                                
      ******************************************************************        
      *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
      *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
      *    MESSAGE.                                                    *        
      ******************************************************************        
       2110-BROWSE-BACKWARD.                                                    
                                                                                
           SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
               GIVING PV-TOP-ITEM.                                              
                                                                                
           IF  PV-TOP-ITEM < +1                                                 
               MOVE +1              TO PV-TOP-ITEM                              
      *-->     -- TOP OF LIST --                                                
               MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                         
               SET DP020-MSG-INFORMATIONAL TO TRUE                              
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
      *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
      ******************************************************************        
       2120-BROWSE-FORWARD.                                                     
                                                                                
           ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
               GIVING PV-TOP-ITEM.                                              
           IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
      *-->     -- BOTTOM OF LIST --                                             
               MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-INFORMATIONAL                                      
                                         TO TRUE                                
           ELSE                                                                 
               COMPUTE PV-BOTTOM-ITEM =                                         
                   PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
               IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
      *-->         -- BOTTOM OF LIST --                                         
                   MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      ******************************************************************        
       2130-CHECK-FOR-JUMP-BROWSE.                                              
                                                                                
           IF ASTRTITL > ZEROES                                                 
               MOVE ASTRTITI TO ASC-START-ITEM-REQUEST.                         
                                                                                
           IF (ASC-START-ITEM-REQUEST = SPACE)                                  
               MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
                                         TO PV-TOP-ITEM                         
           ELSE                                                                 
               MOVE ASC-START-ITEM-REQUEST                                      
                                         TO DP010I-UNEDITED-FIELD               
               MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
               MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED                                  
                                         TO TRUE                                
262978         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262978              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                         TO ASC-START-ITEM-REQUEST-N            
                   IF  ASC-START-ITEM-REQUEST-N > ZERO                          
                       IF  ASC-START-ITEM-REQUEST-N >                           
                                                ASC-NUMBER-OF-ITEMS-READ        
                           MOVE SPACE TO ASC-START-ITEM-REQUEST                 
                           COMPUTE PV-TOP-ITEM =                                
                                   ASC-NUMBER-OF-ITEMS-READ                     
                                   - PC-ITEMS-PER-PANEL + 1                     
                           IF  PV-TOP-ITEM < +1                                 
                               MOVE +1   TO PV-TOP-ITEM                         
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       ELSE                                                     
                           MOVE ASC-START-ITEM-REQUEST-N                        
                                         TO PV-TOP-ITEM                         
                           MOVE SPACE    TO ASC-START-ITEM-REQUEST              
                           COMPUTE PV-BOTTOM-ITEM =                             
                               PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
                           IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
      *-->                     -- BOTTOM OF LIST --                             
                               MOVE PC-TSYMSG-00070                             
                                         TO DP020-MSG-NUMBER                    
                               SET DP020-MSG-INFORMATIONAL                      
                                         TO TRUE                                
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
      *-->             --- VALUE OUT OF RANGE ----                              
                       MOVE PC-TSYMSG-00101                                     
                                         TO DP020-MSG-NUMBER                    
                       MOVE -1           TO ASTRTITL                            
                       SET DP030-SET-CURSOR-APPL-1                              
                                         TO TRUE                                
                       MOVE DP015-REVERSE                                       
                                         TO ASTRTITH                            
                       MOVE DP015-RED    TO ASTRTITC                            
                       MOVE DP015-UNP-NUM-BRT-OFF                               
                                         TO ASTRTITA                            
                       SET DP020-MSG-FATAL TO TRUE                              
                       SET PS-ERROR      TO TRUE                                
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE -1               TO ASTRTITL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
                   MOVE DP015-REVERSE    TO ASTRTITH                            
                   MOVE DP015-RED        TO ASTRTITC                            
                   MOVE DP015-UNP-NUM-BRT-OFF                                   
                                         TO ASTRTITA                            
                   SET DP020-MSG-FATAL   TO TRUE                                
                   SET PS-ERROR          TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    RESET ATTRIBUTES.                                           *        
      ******************************************************************        
       3200-RESET-ATTRIBUTES.                                                   
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
           MOVE DP015-GREEN              TO ASTRTITC.                           
           MOVE DP015-UNDERLINE          TO ASTRTITH.                           
           MOVE -1                       TO ASTRTITL.                           
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
                                                                                
      ******************************************************************        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      ******************************************************************        
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
                                       ASC-BLOCK-ENTRIES (2).                   
           MOVE +1                  TO  ASC-BLK-SUB.                            
           SET ASC-ITEMS-LEFT-ON-DB TO  TRUE.                                   
                                                                                
           MOVE +0                  TO ASC-ITEM-SUB                             
                                       ASC-NUMBER-OF-ITEMS-READ                 
                                       ASC-NUMBER-OF-SELECTED-ITEMS             
                                       ASC-HIGHEST-BLOCK-WRITTEN                
                                       ASC-START-OF-SELECTED-RANGE              
                                       ASC-END-OF-SELECTED-RANGE                
                                       ASC-ITEM-AT-TOP-OF-PANEL.                
                                                                                
                                                                                
           PERFORM 7000-SELECT-TUPCPLS-CORP.                                    
                                                                                
           MOVE 'N' TO PS-STORES-PROCESSED-SW.                                  
           MOVE 0   TO PV-STORES-FETCHED.                                       
           MOVE SPACES TO PV-EFF-DATE-FMT.                                      
           PERFORM 4100-OPEN-STORES-CURSOR.                                     
           PERFORM 4200-FETCH-STORES-CURSOR.                                    
           PERFORM 4300-PROCESS-STORES-CURSOR                                   
               UNTIL PS-ALL-STORES-PROCESSED.                                   
           PERFORM 4400-CLOSE-STORES-CURSOR.                                    
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
               MOVE +1                       TO  ASC-BLK-SUB                    
               PERFORM 6100-WRITE-TEMP-STORAGE                                  
               ADD +1                        TO  ASC-BLK-SUB                    
               PERFORM 6100-WRITE-TEMP-STORAGE                                  
                                                                                
               MOVE 1                        TO  ASC-BLK-SUB                    
                                                 PV-BLOCK-NUMBER                
               PERFORM 6000-READ-TEMP-STORAGE                                   
               IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
                   MOVE 2                    TO  ASC-BLK-SUB                    
                                                 PV-BLOCK-NUMBER                
                   PERFORM 6000-READ-TEMP-STORAGE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE +1                 TO  PV-TOP-ITEM.                             
                                                                                
           IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
            AND ASC-VALID-RCP-SKU                                               
               SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               MOVE -1             TO ASTRTITL                                  
               SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
               PERFORM 5800-MOVE-COMMAREA-TO-SCREEN                             
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    OPEN THE CLEARANCE STORES CURSOR.                           *        
      ******************************************************************        
       4100-OPEN-STORES-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
                OPEN CL-STORES-CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                    MOVE '4100-OPEN-STORES-CURSOR'                              
                                 TO  DP013-PARAGRAPH                            
                    MOVE 'OPEN CLEARANCE STORES CURSOR'                         
                                 TO  DP013-MESSAGE-TEXT (1)                     
                    MOVE SQLCA   TO  DP013-SQLCA                                
                    SET DP013-DB2-ABEND                                         
                                 TO  TRUE                                       
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    FETCH CLEARANCE STORES INFORMATION                          *        
      ******************************************************************        
       4200-FETCH-STORES-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
                FETCH CL-STORES-CSR                                             
                INTO :UPCPLS-STR-NBR,                                           
                     :UPCPLS-EFF-BEG-TMST,                                      
                     :UPCPLS-UNT-RTL-AMT                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD 1 TO PV-STORES-FETCHED                                  
                    MOVE UPCPLS-EFF-BEG-TMST TO PV-EFF-DATE                     
                    MOVE PV-EFF-DATE-CCYY TO PV-EFF-DATE-FMT-CCYY               
                    MOVE PV-EFF-DATE-MM TO PV-EFF-DATE-FMT-MM                   
                    MOVE PV-EFF-DATE-DD TO PV-EFF-DATE-FMT-DD                   
                    MOVE '/' TO PV-SLASH-1                                      
                                PV-SLASH-2                                      
               WHEN SQLCODE = +100                                              
                    SET PS-ALL-STORES-PROCESSED TO TRUE                         
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE  NOT EQUAL ZERO                                     
                    MOVE '4200-FETCH-STORES-CURSOR'                             
                                        TO DP013-PARAGRAPH                      
                    MOVE 'FETCH CL-STORES CURSOR'                               
                                        TO DP013-MESSAGE-TEXT (1)               
                    MOVE SQLCA          TO DP013-SQLCA                          
                    SET DP013-DB2-ABEND TO TRUE                                 
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    PROCESS CLEARANCE STORE CURSOR.                             *        
      ******************************************************************        
       4300-PROCESS-STORES-CURSOR.                                              
                                                                                
           MOVE UPCPLS-STR-NBR      TO  PV-STORE-NMBR.                          
                                                                                
           PERFORM 7100-SELECT-STORE-DATA.                                      
           PERFORM 5700-MOVE-DATA-TO-TEMP-STORAGE.                              
           PERFORM 4200-FETCH-STORES-CURSOR.                                    
                                                                                
      ******************************************************************        
      *    CLOSE STORES CURSOR.                                        *        
      ******************************************************************        
       4400-CLOSE-STORES-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
               CLOSE CL-STORES-CSR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE  NOT EQUAL ZERO                                     
                    MOVE '4400-CLOSE-STORES-CURSOR'                             
                                 TO  DP013-PARAGRAPH                            
                    MOVE 'CLOSE STORES CURSOR'                                  
                                 TO  DP013-MESSAGE-TEXT (1)                     
                    MOVE SQLCA   TO  DP013-SQLCA                                
                    SET DP013-DB2-ABEND                                         
                                 TO  TRUE                                       
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    MOVE DATA TO PROGRAM VARIABLE AREAS                         *        
      ******************************************************************        
       5700-MOVE-DATA-TO-TEMP-STORAGE.                                          
                                                                                
           IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                                
               ADD +1                TO ASC-ITEM-SUB                            
           ELSE                                                                 
               ADD +1                TO ASC-BLK-SUB                             
               MOVE +1               TO ASC-ITEM-SUB                            
               IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                         
                   MOVE +1           TO ASC-BLK-SUB                             
                   SET ASC-BLOCK-MODIFIED (1)                                   
                                     TO TRUE                                    
                   PERFORM 6100-WRITE-TEMP-STORAGE                              
                   MOVE ASC-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY)               
                                     TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)          
                   ADD +1            TO ASC-BLK-SUB                             
                   INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                   
                   SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                      
                                     TO TRUE                                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD +1                    TO ASC-NUMBER-OF-ITEMS-READ.               
                                                                                
           IF PS-NO-DETAIL-FOUND                                                
              MOVE ASC-NUMBER-OF-ITEMS-READ   TO ASC-LIST-ITEM-NUMBER           
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE PV-STORE-NMBR              TO ASC-STORE-NMBR                 
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE SPACES                     TO ASC-STORE-SHORT-NAME           
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE 0                           TO ASC-CLR-RETAIL-AMT            
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE 'NO DETAIL AVAIL'           TO ASC-METRO-AREA                
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE SPACES                      TO ASC-STATE                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE SPACES                      TO ASC-MKT-CLSTR                 
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE SPACES                      TO ASC-EFF-DTE                   
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
           ELSE                                                                 
              MOVE ASC-NUMBER-OF-ITEMS-READ   TO ASC-LIST-ITEM-NUMBER           
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE XMT00001-LOC-NBR           TO ASC-STORE-NMBR                 
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE XMT00001-LOC-10-ABBR-NM    TO ASC-STORE-SHORT-NAME           
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE UPCPLS-UNT-RTL-AMT          TO ASC-CLR-RETAIL-AMT            
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE PV-CITY                     TO ASC-METRO-AREA                
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE PV-STATE                    TO ASC-STATE                     
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE PV-MKT-CLUSTER              TO ASC-MKT-CLSTR                 
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
              MOVE PV-EFF-DATE-FMT             TO ASC-EFF-DTE                   
                                           (ASC-BLK-SUB ASC-ITEM-SUB)           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
      ******************************************************************        
       5800-MOVE-COMMAREA-TO-SCREEN.                                            
                                                                                
           MOVE ASC-DEPT-NMBR   TO ADEPTO                                       
           MOVE ASC-MCL-NMBR    TO AMJCLSO                                      
           MOVE ASC-SCL-NMBR    TO ASBCLSO                                      
           MOVE ASC-SKU-NMBR    TO ASKUO                                        
           MOVE ASC-SKU-DESC    TO ASKUDSCO                                     
           MOVE ASC-REG-RTL-AMT TO ARRAMTO                                      
                                                                                
           IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
               AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
               OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
                   MOVE +1               TO ASC-BLK-SUB                         
                   PERFORM 6100-WRITE-TEMP-STORAGE                              
                   ADD +1                TO ASC-BLK-SUB                         
                   PERFORM 6100-WRITE-TEMP-STORAGE                              
                   DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
                       GIVING PV-BLOCK-NUMBER                                   
                       REMAINDER PV-REMAINDER                                   
                   IF  PV-REMAINDER > ZERO                                      
                       ADD +1            TO PV-BLOCK-NUMBER                     
                   END-IF                                                       
                   MOVE +1               TO ASC-BLK-SUB                         
                   PERFORM 6000-READ-TEMP-STORAGE                               
                   ADD +1                TO ASC-BLK-SUB                         
                                            PV-BLOCK-NUMBER                     
                   PERFORM 6000-READ-TEMP-STORAGE                               
           END-IF.                                                              
                                                                                
           MOVE +1                       TO ASC-BLK-SUB.                        
                                                                                
           COMPUTE ASC-ITEM-SUB =                                               
               ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
               + PC-ITEMS-PER-PANEL).                                           
                                                                                
           IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
               ADD +1                    TO ASC-BLK-SUB                         
               SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
           END-IF.                                                              
                                                                                
           COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
               ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
                                                                                
           PERFORM 5810-FORMAT-LIST-LINES                                       
               VARYING PS-SUB                                                   
               FROM PC-ITEMS-PER-PANEL BY -1                                    
               UNTIL PS-SUB < +1.                                               
                                                                                
           COMPUTE ASC-ITEMS-DISPLAYED =                                        
               ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
                                                                                
           MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
                                            ASTRTITO.                           
           MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
                                                                                
           MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
           MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
                                                                                
           IF  DP020-MSG-NUMBER-X = SPACE                                       
               IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
      *-->         --- MORE ITEMS TO DISPLAY ----                               
                   MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      ******************************************************************        
       5810-FORMAT-LIST-LINES.                                                  
                                                                                
           IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
               SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
               MOVE SPACES             TO MR-STORE-NMBR    (PS-SUB)             
                                          MR-STORE-NAME    (PS-SUB)             
                                          MR-METRO-AREA    (PS-SUB)             
                                          MR-STATE         (PS-SUB)             
                                          MR-MKT-CLSTR     (PS-SUB)             
                                          MR-EFF-DTE       (PS-SUB)             
               MOVE ZERO               TO MR-RTL-AMT       (PS-SUB)             
                                                                                
           ELSE                                                                 
               IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
                   MOVE ASC-STORE-NMBR (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-STORE-NMBR (PS-SUB)                
                   MOVE ASC-STORE-SHORT-NAME (ASC-BLK-SUB ASC-ITEM-SUB)         
                                       TO MR-STORE-NAME (PS-SUB)                
                   MOVE ASC-CLR-RETAIL-AMT (ASC-BLK-SUB ASC-ITEM-SUB)           
                                       TO MR-RTL-AMT (PS-SUB)                   
                   MOVE ASC-METRO-AREA (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-METRO-AREA (PS-SUB)                
                   MOVE ASC-STATE      (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-STATE (PS-SUB)                     
                   MOVE ASC-MKT-CLSTR  (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-MKT-CLSTR (PS-SUB)                 
                   MOVE ASC-EFF-DTE    (ASC-BLK-SUB ASC-ITEM-SUB)               
                                       TO MR-EFF-DTE (PS-SUB)                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
           IF  ASC-ITEM-SUB < + 1                                               
               MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
               MOVE +1                 TO ASC-BLK-SUB                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      ******************************************************************        
       6000-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
             OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND            TO TRUE                          
               SET DP013-NO-ROLLBACK           TO TRUE                          
               MOVE '6000-READ-TEMP-STORAGE'   TO DP013-PARAGRAPH               
               MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
                                               TO DP013-MESSAGE-TEXT(1)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
                                                                                
           IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
               INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      ******************************************************************        
       6100-WRITE-TEMP-STORAGE.                                                 
                                                                                
           MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
                                         TO PV-LIST-ITEM-NUMBER.                
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
                                                                                
           IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
           OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
               IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
                   PERFORM 6110-REWRITE-ASC-LISTQ                               
               END-IF                                                           
           ELSE                                                                 
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
               ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
               PERFORM 6120-WRITE-ASC-LISTQ                                     
               ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
           END-IF.                                                              
                                                                                
           SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                              
                                         TO TRUE.                               
                                                                                
      ******************************************************************        
      *  REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-     *        
      *  SPECIFIC COMMAREA.                                            *        
      ******************************************************************        
       6110-REWRITE-ASC-LISTQ.                                                  
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   REWRITE                                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND          TO TRUE                            
               SET DP013-NO-ROLLBACK         TO TRUE                            
               MOVE '6110-REWRITE-ASC-LISTQ' TO DP013-PARAGRAPH                 
               MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
                                             TO DP013-MESSAGE-TEXT(1)           
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-       *        
      *  SPECIFIC COMMAREA.                                            *        
      ******************************************************************        
       6120-WRITE-ASC-LISTQ.                                                    
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE (ASC-LIST-QUEUE-NAME)                                  
                   FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
                   ITEM  (PV-BLOCK-NUMBER)                                      
                   RESP  (PV-CICS-RESP)                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
               SET DP013-CICS-ABEND        TO TRUE                              
               SET DP013-NO-ROLLBACK       TO TRUE                              
               MOVE '6120-WRITE-ASC-LISTQ' TO DP013-PARAGRAPH                   
               MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
                                           TO DP013-MESSAGE-TEXT(1)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    READ THE CORPORATE ROW CONTAINING THE REGULAR RETAIL AMOUNT *        
      *    FOR THE SPECIFIED SKU.                                      *        
      ******************************************************************        
       7000-SELECT-TUPCPLS-CORP.                                                
                                                                                
           INITIALIZE DCLTUPCPLS.                                               
                                                                                
           EXEC SQL                                                             
              SELECT  STR_NBR                                                   
                     ,EFF_BEG_TMST                                              
                     ,UNT_RTL_AMT                                               
                INTO :UPCPLS-STR-NBR                                            
                    ,:UPCPLS-EFF-BEG-TMST                                       
                    ,:UPCPLS-UNT-RTL-AMT                                        
                FROM  TUPCPLS                                                   
                WHERE STR_NBR      = 0                                          
                  AND ITM_NBR      = :ASC-SKU-ITEM-NBR                          
                  AND OPERCO_NBR   = '03'                                       
                  AND ((EFF_BEG_TMST <= CURRENT TIMESTAMP                       
                      AND EFF_END_TMST >= CURRENT TIMESTAMP)                    
                      OR (EFF_BEG_TMST <= CURRENT TIMESTAMP                     
                         AND EFF_END_TMST IS NULL))                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    MOVE UPCPLS-UNT-RTL-AMT TO ASC-REG-RTL-AMT                  
               WHEN SQLCODE = +100                                              
                    MOVE ZERO TO ASC-REG-RTL-AMT                                
               WHEN SQLCODE  NOT = ZERO                                         
               WHEN SQLWARN0 NOT = SPACES                                       
                    MOVE '7000-SELECT-TUPCPLS-CORP'                             
                                        TO DP013-PARAGRAPH                      
                    MOVE 'SELECT TUPCPLS'   TO DP013-MESSAGE-TEXT (1)           
                    MOVE SQLCA          TO DP013-SQLCA                          
                    SET DP013-DB2-ABEND TO TRUE                                 
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    SELECT STORE NAME AND LOCATION INFORMATION FROM THE XMT     *        
      *    LOCATION TABLES.                                            *        
      ******************************************************************        
       7100-SELECT-STORE-DATA.                                                  
                                                                                
           INITIALIZE DCLXMT00001                                               
                      DCLXMT00003                                               
                      DCLXMT00004.                                              
                                                                                
           MOVE SPACES TO PV-CITY                                               
                          PV-STATE                                              
                          PV-MKT-CLUSTER.                                       
                                                                                
           EXEC SQL                                                             
                                                                                
              SELECT A.LOC_NBR                                                  
                   , A.LOC_10_ABBR_NM                                           
                   , C.STR_GP_NM                                                
                   , C.ST_CDE                                                   
                                                                                
                INTO :XMT00001-LOC-NBR                                          
                   , :XMT00001-LOC-10-ABBR-NM                                   
                   , :XMT00004-STR-GP-NM                                        
                   , :XMT00004-ST-CDE                                           
                                                                                
                FROM XMT_LOC          A                                         
                   , XMT_STR_GP_MBSHP B                                         
                   , XMT_STR_GP       C                                         
                                                                                
               WHERE A.LOC_NBR        = :PV-STORE-NMBR                          
                 AND A.LOC_NBR        = B.LOC_NBR                               
                 AND B.STR_GP_TYP_CDE = 2000                                    
                 AND ((B.EFF_BEG_DTE <= CURRENT DATE)                           
                 AND ((B.EFF_END_DTE >= CURRENT DATE)                           
                  OR  (B.EFF_END_DTE IS NULL)))                                 
                 AND B.STR_GP_TYP_CDE = C.STR_GP_TYP_CDE                        
                 AND B.STR_GP_ID      = C.STR_GP_ID                             
             WITH UR                                                            
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    SET PS-DETAIL-FOUND TO TRUE                                 
                    MOVE XMT00004-STR-GP-NM TO PV-CITY                          
                    MOVE XMT00004-ST-CDE    TO PV-STATE                         
                    PERFORM 7110-SELECT-MKT-CLSTR-DATA                          
               WHEN SQLCODE = +100                                              
                    SET PS-NO-DETAIL-FOUND TO TRUE                              
               WHEN SQLCODE  NOT = ZERO                                         
               WHEN SQLWARN0 NOT = SPACES                                       
                    MOVE '7100-SELECT-STORE-DATA'                               
                                        TO DP013-PARAGRAPH                      
                    MOVE 'SELECT STORE DETAIL'                                  
                                        TO DP013-MESSAGE-TEXT (1)               
                    MOVE SQLCA          TO DP013-SQLCA                          
                    SET DP013-DB2-ABEND TO TRUE                                 
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    SELECT MARKET CLUSTER INFORMATION FROM THE XMT STORE GROUP  *        
      *    TABLES.                                                     *        
      ******************************************************************        
       7110-SELECT-MKT-CLSTR-DATA.                                              
                                                                                
           INITIALIZE DCLXMT00003                                               
                      DCLXMT00004.                                              
                                                                                
           EXEC SQL                                                             
                                                                                
              SELECT B.STR_GP_NM                                                
                                                                                
                INTO :XMT00004-STR-GP-NM                                        
                                                                                
                FROM XMT_STR_GP_MBSHP A                                         
                   , XMT_STR_GP       B                                         
                                                                                
               WHERE A.LOC_NBR        = :PV-STORE-NMBR                          
                 AND A.STR_GP_TYP_CDE = 7000                                    
                 AND ((A.EFF_BEG_DTE <= CURRENT DATE)                           
                 AND ((A.EFF_END_DTE >= CURRENT DATE)                           
                  OR  (A.EFF_END_DTE IS NULL)))                                 
                 AND A.STR_GP_TYP_CDE = B.STR_GP_TYP_CDE                        
                 AND A.STR_GP_ID      = B.STR_GP_ID                             
             WITH UR                                                            
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    MOVE XMT00004-STR-GP-NM TO PV-MKT-CLUSTER                   
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE  NOT = ZERO                                         
               WHEN SQLWARN0 NOT = SPACES                                       
                    MOVE '7100-SELECT-STORE-DATA'                               
                                        TO DP013-PARAGRAPH                      
                    MOVE 'SELECT STORE DETAIL'                                  
                                        TO DP013-MESSAGE-TEXT (1)               
                    MOVE SQLCA          TO DP013-SQLCA                          
                    SET DP013-DB2-ABEND TO TRUE                                 
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    READ THE TSK TABLE FOR SKU INFORMATION - SKU DESCRIPTION    *        
      *    AND ITEM NUMBER.                                            *        
      ******************************************************************        
       7200-SELECT-TSK-DETAIL.                                                  
                                                                                
           INITIALIZE DCLTSK.                                                   
                                                                                
           EXEC SQL                                                             
              SELECT  DEPT                                                      
                     ,SUBCLASS                                                  
                     ,SKU                                                       
                     ,SKU_DESC                                                  
                     ,ITEM_NMBR                                                 
                     ,REGN_PRIC_IND                                             
                INTO :SKXREF-DEPT                                               
                    ,:SKXREF-SUBCLASS                                           
                    ,:SKXREF-SKU                                                
                    ,:TSK-SKU-DESC                                              
                    ,:TSK-ITEM-NMBR                                             
                    ,:TSK-REGN-PRIC-IND                                         
                FROM  TSK,                                                      
                      TSKXREF                                                   
                WHERE ITEM_NMBR = ITM_NBR                                       
                  AND SKU = :SKXREF-SKU                                         
                  AND SKU_STAT_CDE <> '00'                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                    MOVE SKXREF-DEPT     TO ASC-DEPT-9                          
                    MOVE SKXREF-CLASS    TO ASC-MCL-9                           
                    MOVE SKXREF-SUBCLASS TO ASC-SCL-9                           
                    MOVE TSK-SKU-DESC    TO ASC-SKU-DESC                        
                    MOVE TSK-ITEM-NMBR   TO ASC-SKU-ITEM-NBR                    
               WHEN SQLCODE  NOT = ZERO                                         
               WHEN SQLWARN0 NOT = SPACES                                       
                    MOVE '7200-SELECT-TSK-DETAIL'                               
                                        TO DP013-PARAGRAPH                      
                    MOVE 'SELECT TSK'   TO DP013-MESSAGE-TEXT (1)               
                    MOVE SQLCA          TO DP013-SQLCA                          
                    SET DP013-DB2-ABEND TO TRUE                                 
                    PERFORM DP013-0000-PROCESS-ABEND                            
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    ABEND PROCESSOR MODULE                                      *        
      ******************************************************************        
                                                                                
           COPY DPPD013.                                                        
