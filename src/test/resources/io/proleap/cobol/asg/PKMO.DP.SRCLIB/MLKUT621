       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUT621.                                       
       AUTHOR.                  RONNA MCDONALD.                                 
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            08/24/2022.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  CLEAN UP DEPT BRAND VENDOR RECORDS THAT ARE OBSOLETE          *        
      *                                                                *        
      *  THE INITIALIZATION PART OF THIS PROGRAM HAS A PROCESS TO      *        
      *  BUILD INTERNAL TABLES.  ONE CONTAINS DATES FOR THE PAST 14    *        
      *  MONTHS AND ANOTHER CONTAINS DIRECT SHIP VENDORS.  THESE       *        
      *  INTERNAL TABLES WILL BE USED WHEN PROCESSING THROUGH THE      *        
      *  DRIVING CURSOR OF THIS PROGRAM.                               *        
      *                                                                *        
      *                                                                *        
      *  THE DRIVER OF THIS PROCESS IS A CURSOR THAT IDENTIFIES        *        
      *  RECORDS IN THE IMT_DEPT_BRND_VND BY IDENTIFYING ROWS THAT     *        
      *  WHERE CREATED 3 OR MORE YEARS AGO; DOES NOT INCLUDE VENDORS   *        
      *  THAT ARE CONSIGNMENT VENDORS; ALSO DO NOT INCLUDE ANY         *        
      *  DEPARTMENTS THAT HAVE ALL OF THE SUBCLASSES BLOCKED AND       *        
      *  LISTED IN MLSI013 (THE MLSI013 DEPARTMENTS ARE:  101, 272,    *        
      *  712, 822, 831, 833, 834, 984, 985)                            *        
      *                                                                *        
      *  FROM THE DRIVING CURSOR PERFORM THE FOLLOWING CHECKS:         *        
      *                                                                *        
      *  MAINTAIN THE IMT_DEPT_BRND_VND BY IDENTIFYING ROWS THAT       *        
      *  WHERE CREATED 3 OR MORE YEARS AGO AND                         *        
      *  DO NOT INCLUDE VENDORS THAT ARE CONSIGNMENT VENDORS.          *        
      *  ALSO DO NOT INCLUDE ANY DEPARTMENTS THAT HAVE ALL OF          *        
      *  THE SUBCLASSES BLOCKED AND LISTED IN MLSI013                  *        
      *  MLSI013 DEPARTMENTS ARE:  101, 272, 712, 822, 831, 833,       *        
      *                            834, 984, 985                       *        
      *  PERFORM THE FOLLOWING CHECKS:                                 *        
      *                                                                *        
      *  1.  IF THE COMBINATION HAD A DIRECT SHIP VENDOR, DO NOT       *        
      *      END THE ROW.                                              *        
      *      A CURSOR WILL GO AGAINST THE TPURCHS TABLE AND BRING      *        
      *      BACK A LIST OF VENDORS.  THE VENDORS WILL BE              *        
      *      ADDED TO AN INTERNAL TABLE, SO THE TABLE CAN BE SEARCHED  *        
      *      FOR THE VENDOR.  IF THE VENDOR IS NOT                     *        
      *      FOUND THEN PROCEDE TO CHECK #2.                           *        
      *                                                                *        
      *  2.  IF THE COMBINATION HAS ANY ENDING INVENTORY RETAIL DOLLARS*        
      *      WITHIN THE PAST 12 MONTHS, THEN DO NOT END THE ROW.       *        
      *      (NOTE:  THIS PROCESS WILL QUERY THE DATA ON THE           *        
      *      MLT_MNLY_SUM ONE MONTH AT A TIME (BECAUSE THERE IS        *        
      *      TOO MUCH DATA TO QUERY AN ENTIRE YEAR AT A TIME), AND     *        
      *      IF THERE IS ENDING INVENTORY RETAIL DOLLARS FOUND         *        
      *      IN A SPECIFIC MONTH, THE RECORD WILL GET ADDED TO THE     *        
      *      DO NOT END FILE, AND THEN THE NEXT DBV COMBINATION        *        
      *      WILL GO THROUGH THE CHECKING.                             *        
      *                                                                *        
      *  WHERE CREATED 3 OR MORE YEARS AGO, THAT DO NOT HAVE                    
      *  ANY ACTIVITY (MEANING THERE IS ZERO DOLLARS IN THE                     
      *  ENDING INVENTORY RETAIL AMOUNT.                                        
      *                                                                *        
      *  CREATE TWO CSV FILES. ONE TO SHOW THE DEPT BRAND VENDOR       *        
      *  COMBINATIONS THAT HAVE ZERO DOLLARS, AND ONE FOR THE          *        
      *  DEPT BRAND VENDOR COMBINATIONS THAT DO NOT HAVE ZERO DOLLARS  *        
      *                                                                *        
      *  THIS PROGRAM WILL USE A CURSOR TO IDENTIFY ALL THE            *        
      *  DEPT BRAND VENDOR COMBINATIONS THAT ARE AT LEAST 3 YEARS      *        
      *  OLD AND ARE STILL ACTIVE.  IF A ROW IS ACTIVE, THE END        *        
      *  DATE IS NULL.                                                 *        
      *                                                                *        
      *  FOR EACH ROW THAT IS FETCHED EXECUTE A QUERY THAT WILL        *        
      *  DETERMINE IF THERE ARE ANY EIR DOLLARS FOR THAT DEPT          *        
      *  BRAND VENDOR COMBINATION.                                     *        
      *                                                                *        
      *  IF THERE ARE ANY DOLLARS, THEN LEAVE THE ROW AS IS AND        *        
      *  WRITE A RECORD TO THE CSV FILE FOR ACTIVE ROWS.               *        
      *                                                                *        
      *  IF THERE ARE ZERO DOLLARS (OR NO RECORDS) AND WRITE A         *        
      *  RECORD THE CSV FILE FOR ROWS THAT WERE ENDED.  THIS FILE      *        
      *  WILL BE USED AS INPUT TO ANOTHER PROGRAM TO END THE ROW.      *        
      *                                                                *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DEPT-BRND-VND-END                                             
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT DEPT-BRND-VND-ACTIVE                                          
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DEPT-BRND-VND-END                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DEPT-BRND-VND-END-REC           PIC X(100).                          
                                                                                
       FD  DEPT-BRND-VND-ACTIVE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DEPT-BRND-VND-ACTIVE-REC        PIC X(100).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  CSV FILE LAYOUT                                                        
      ******************************************************************        
           COPY MLRD621.                                                        
                                                                                
                                                                                
?COM  *                                                                         
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-CSR               PIC X           VALUE 'N'.           
               88  END-OF-CSR                              VALUE 'Y'.           
               88  NOT-END-OF-CSR                          VALUE 'N'.           
           05  PS-END-OF-VND-CSR           PIC X           VALUE 'N'.           
               88  END-OF-VND-CSR                          VALUE 'Y'.           
               88  NOT-END-OF-VND-CSR                      VALUE 'N'.           
           05  PS-DSV-SWITCH               PIC X           VALUE 'N'.           
               88  DSV-YES                                 VALUE 'Y'.           
               88  DSV-NO                                  VALUE 'N'.           
           05  PS-RECORD-STATUS            PIC X           VALUE SPACE.         
               88  END-DATE-THIS-RECORD                    VALUE 'I'.           
               88  DO-NOT-END-DATE-THIS-RECORD             VALUE 'A'.           
           05  PS-STOP-EIR-CHECK-STATUS    PIC X           VALUE 'N'.           
               88  STOP-EIR-CHECK                          VALUE 'Y'.           
               88  CONTINUE-EIR-CHECK                      VALUE 'N'.           
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-NULL-INDICATOR           PIC S9(04) COMP SYNC                 
                                                       VALUE ZERO.              
           05  PV-START-DATE-RANGE         PIC X(10)   VALUE SPACES.            
           05  PV-END-DATE-RANGE           PIC X(10)   VALUE SPACES.            
                                                                                
           05  PV-LBL-SEQ-NBR-NBR          PIC 9(06)   VALUE ZERO.              
                                                                                
           05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACE.     MLKRP054
           05  PV-ABEND-MESSAGE            PIC  X(75)  VALUE SPACE.     MLKRP054
           05  PV-SQL-SUB                  PIC  9(03)  VALUE ZERO.      MLKRP054
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)  VALUE SPACE.     MLKRP054
           05  PV-OPER-ATTEMPTED           PIC X(25)   VALUE SPACES.            
           05  PV-DTE-SUB                  PIC  9(02)  VALUE ZERO.              
           05  PV-COUNT                    PIC  S9(9)  VALUE ZEROS COMP.        
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-A                        PIC X       VALUE 'A'.               
           05  PC-H                        PIC X       VALUE 'H'.               
           05  PC-I                        PIC X       VALUE 'I'.               
           05  PC-VND-MAX                  PIC S9(04) VALUE +9999               
                                                           COMP SYNC.           
           05  PC-MAX-DTE-SUB              PIC  9(02) VALUE  14.                
                                                                                
       01  DSV-DIRECT-SHIP-VEND-TABLE.                                          
           05  DSV-VEND-ENTRY              OCCURS 10000 TIMES                   
                                           ASCENDING KEY IS                     
                                           DSV-ENT-ID                           
                                           INDEXED BY DSV-VEND-IDX.             
               10  DSV-ENT-ID              PIC  S9(9) COMP.                     
      *                                                                         
RONNA  01  DTE-14M-MN-END-DTE-TABLE.                                            
RONNA      05  DTE-MN-END-ENTRY            OCCURS 14.                           
RONNA          10  DTE-FSCL-MN-END-DTE     PIC X(10).                           
00202                                                                   MLKRP054
RONNA  01  DTE-SUB-X.                                                           
RONNA      05  DTE-SUB                     PIC 9(02).                           
      *                                                                         
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  CONTROL-CARD-ABEND                          VALUE +4001.         
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  AC-TABLE-OVERFLOW                           VALUE +4004.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4016.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TDTATTR       -                                                      
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMDSEAR       -  MBS STRUCTURE                                       
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMLCNTL       -                                                      
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TVSCHVL       -                                                      
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    XMT_VND       - MERCHANDISING DOMAIN VENDOR TABLE                    
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE XMT00000                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_BAL - ML'S PARTITION BALANCING TABLE             *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00002                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_CNTL - ML'S PARTITION CONTROL TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_MNLY_SUM - ML'S MONTHLY DETAIL DATA TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00004                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    IMT_DEPT_BRND_VND                                          *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE IMT00017                                                 
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TPURCHS              PO TABLE                              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TPURCHS                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMDARVS              ITEM STYLE                            *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DEPT BRAND VENDOR CURSOR THAT DRIVES THIS PROCESS          *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE DEPT_BRND_VND_CSR CURSOR FOR                      
                SELECT DISTINCT A.DEPT_BRND_VND_ID                              
                     , A.DEPT_NBR                                               
                     , B.CHARTC_VAL_DESC                                        
                     , B.CHARTC_SEQ_NBR                                         
                     , C.PRIM_VND_NM                                            
                     , C.ENT_ID                                                 
                FROM IMT_DEPT_BRND_VND A                                        
                    ,TVSCHVL B                                                  
                    ,XMT_VND C                                                  
                    ,TMDSEAR D                                                  
                WHERE A.EFF_END_DTE IS NULL                                     
                AND A.LBL_SEQ_NBR = B.CHARTC_SEQ_NBR                            
                AND B.CHARTC_CDE = 3                                            
                AND A.ENT_ID = C.ENT_ID                                         
                AND DECIMAL(D.DEPT_NBR)  = A.DEPT_NBR                           
                AND A.DEPT_NBR NOT IN ( 101, 272, 712, 822, 831, 833,           
                                        834, 984, 985                )          
                AND D.MDSELV_CDE = 'DPT'                                        
                AND D.MDSEAR_DESC NOT LIKE '%CONSIGN%'                          
                AND CURRENT DATE - 3 YEARS > EFF_BEG_DTE                        
                ORDER BY A.DEPT_NBR                                             
                        ,B.CHARTC_SEQ_NBR                                       
                        ,C.ENT_ID                                               
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    DIRECT SHIP VENDOR                                         *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE DIRECT_SHIP_VND_CSR CURSOR FOR                    
                SELECT DISTINCT                                                 
                       A.ENT_ID                                                 
                FROM TPURCHS A                                                  
                WHERE PO_TYP_CDE = 'CD' -- CONSUMER DIRECT PO                   
                ORDER BY A.ENT_ID                                               
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-OPEN-DEPT-BRND-VND-CSR.                                 
                                                                                
           MOVE ZERO                TO IMT00017-DEPT-BRND-VND-ID.               
           MOVE ZERO                TO IMT00017-DEPT-NBR.                       
           MOVE SPACES              TO VSCHVL-CHARTC-VAL-DESC.                  
           MOVE ZERO                TO VSCHVL-CHARTC-SEQ-NBR.                   
           MOVE ZERO                TO XMT00000-ENT-ID.                         
           MOVE SPACES              TO XMT00000-PRIM-VND-NM.                    
                                                                                
           PERFORM Z200-FETCH-DEPT-BRND-VND-CSR.                                
                                                                                
           PERFORM B200-CLEANUP-DEPT-BRND-VND                                   
               UNTIL END-OF-CSR.                                                
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
                                                                                
      *    GET THE OPEN FISCAL MONTH END DATE AND THE PREVIOUS MONTHS           
      *    FISCAL MONTH END DATE                                                
      *    THESE DATES WILL BE USED WHEN LOOKING FOR EIR DOLLARS                
      *    FOR A GIVEN DEPT BRAND VENDOR COMBINATION.                           
           PERFORM Z000-GET-FSCL-MN-END-DATES.                                  
                                                                                
           MOVE 1 TO DTE-SUB.                                                   
RONNA      PERFORM B150-POPULATE-DTE-ARRAY UNTIL                                
RONNA         DTE-SUB >= PC-MAX-DTE-SUB.                                        
                                                                                
           OPEN OUTPUT DEPT-BRND-VND-END                                        
                       DEPT-BRND-VND-ACTIVE.                                    
                                                                                
      *    LOAD UP VENDOR DATA IN AN INTERNAL TABLE                             
069000     SET DSV-VEND-IDX TO 1.                                               
069100     PERFORM B300-LOAD-VND-ENTRIES.                                       
069200                                                                          
      *    SET AN INDICATOR FOR A HEADER RECORD                                 
           MOVE PC-H TO ML621-REC-TYPE-H.                                       
                                                                                
           WRITE DEPT-BRND-VND-END-REC      FROM ML621-CSV-TE-REC-HEAD.         
           WRITE DEPT-BRND-VND-ACTIVE-REC   FROM ML621-CSV-TE-REC-HEAD.         
                                                                                
      ******************************************************************        
      *    POPULATE DATE ARRAY THAT WILL BE USED WHEN SEARCHING                 
      *    FOR EIR  DATE ARRAY THAT WILL BE USED WHEN SEARCHING                 
      *    THIS ARRAY WILL HAVE THE MOST RECENT DATE IN ARRAY INDEX 1           
      *    WHERE INDEX 14 WILL BE THE FURTHEST DATE IN THE PAST.                
      ******************************************************************        
       B150-POPULATE-DTE-ARRAY.                                                 
           MOVE DTATTR-FSCL-MN-END-DTE TO DTE-FSCL-MN-END-DTE(DTE-SUB).         
      *    MOVE PV-END-DATE-RANGE      TO DTE-FSCL-MN-END-DTE(DTE-SUB).         
           ADD +1 TO DTE-SUB.                                                   
           MOVE DTATTR-PFM-END-DTE     TO DTE-FSCL-MN-END-DTE(DTE-SUB)          
                                          DTATTR-KY-DTE.                        
      *    MOVE PV-START-DATE-RANGE    TO DTE-FSCL-MN-END-DTE(DTE-SUB).         
           ADD +1 TO DTE-SUB.                                                   
      *             MOVE DTATTR-PFM-END-DTE     TO PV-START-DATE-RANGE          
      *             MOVE DTATTR-FSCL-MN-END-DTE TO PV-END-DATE-RANGE            
           IF DTE-SUB < PC-MAX-DTE-SUB                                          
              PERFORM Z800-GET-PREV-2-MN-END-DTES.                              
                                                                                
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION INFO                                               
      ******************************************************************        
       B200-CLEANUP-DEPT-BRND-VND.                                              
                                                                                
           MOVE IMT00017-DEPT-BRND-VND-ID TO ML621-DEPT-BRND-VND-ID             
           MOVE IMT00017-DEPT-NBR         TO ML621-DEPT-NBR                     
           MOVE VSCHVL-CHARTC-SEQ-NBR     TO PV-LBL-SEQ-NBR-NBR                 
           MOVE PV-LBL-SEQ-NBR-NBR        TO ML621-LBL-SEQ-NBR-X                
           MOVE VSCHVL-CHARTC-VAL-DESC    TO ML621-BRAND-NAME                   
           MOVE XMT00000-ENT-ID           TO ML621-ENT-ID                       
           MOVE XMT00000-PRIM-VND-NM      TO ML621-VENDOR-NAME                  
                                                                                
           IF NOT-END-OF-CSR THEN                                               
              SET DO-NOT-END-DATE-THIS-RECORD TO TRUE.                          
                                                                                
              PERFORM B500-SEARCH-FOR-DSV                                       
                                                                                
      *       IF THIS DBV IS NOT A DSV THEN CHECK EIR ACTIVITY                  
              IF DSV-NO THEN                                                    
RONNA *          CHECK FOR EIR DOLLARS STARTING WITH THIS                       
RONNA *          OPEN FISCAL MONTH END DATE AND THE PREVIOUS                    
RONNA *          FISCAL MONTH END DATE, AND CONTINUE CHECKING                   
RONNA *          MOVING THE MONTHS TO CHECK BACK IN TIME 2 MONTHS               
RONNA *          AT A TIME UNTIL EITHER DOLLARS ARE FOUND OR                    
RONNA *          UNTIL 14 MONTHS HISTORY HAS BEEN CHECKED.                      
RONNA *          THE REASON FOR THIS IS TO CATCH ANY ACTIVITY                   
RONNA *          FOR DEPT BRAND VENDORS THAT MAY HAVE SEASONAL                  
RONNA *          MERCHANDISE.                                                   
RONNA *          THERE IS TOO MUCH DATA TO CHECK ALL MONTHS IN ONE              
RONNA *          QUERY SO IT IS SPLIT UP INTO 2 MONTH INCREMENTS.               
RONNA            SET CONTINUE-EIR-CHECK TO TRUE                                 
                 MOVE IMT00017-DEPT-BRND-VND-ID                                 
                                        TO MLT00004-ML-LO-MDSE-LVL-ID           
                 MOVE IMT00017-DEPT-NBR                                         
                                        TO MLT00004-DEPT-NBR                    
                                                                                
RONNA            PERFORM Z400-CHECK-EIR-SUM-DBV                                 
RONNA                VARYING PV-DTE-SUB FROM 1                                  
                     BY 2                                                       
                     UNTIL END-DATE-THIS-RECORD OR                              
                           PV-DTE-SUB >= 14 OR                                  
                           STOP-EIR-CHECK                                       
              END-IF                                                            
                                                                                
              IF END-DATE-THIS-RECORD THEN                                      
RONNA2*       CHECK IF THERE IS A STYLE FOR THE DEPARTMENT                      
RONNA2*       BRAND.  IF A STYLE EXISTS, THEN DO NOT END THE ROW.               
RONNA2           PERFORM Z850-CHECK-DEBT-BRND-FOR-STYLE                         
RONNA2        END-IF                                                            
RONNA2                                                                          
              IF END-DATE-THIS-RECORD THEN                                      
RONNAD*          DISPLAY 'RECORD ENDED FOR DEPT NUMBER, DKEY : '                
RONNAD*              IMT00017-DEPT-NBR '    '                                   
RONNAD*              IMT00017-DEPT-BRND-VND-ID                                  
                                                                                
      *          SET THE RECORD TYPE TO INACTIVATE                              
      *          WRITE CSV FILE FOR INACTIVATED ROW                             
                                                                                
                 MOVE PC-I TO ML621-REC-TYPE                                    
                 WRITE DEPT-BRND-VND-END-REC    FROM ML621-CSV-TE-REC           
                                                                                
              ELSE                                                              
RONNAD*          DISPLAY 'RECORD STILL ACTIVE '                                 
RONNAD*                  'DEPT: ' IMT00017-DEPT-NBR                             
RONNAD*                  'DKEY: ' IMT00017-DEPT-BRND-VND-ID                     
                                                                                
      *          SET THE RECORD TYPE TO ACTIVE                                  
      *          WRITE CSV FILE FOR ACTIVE ROW                                  
                                                                                
                 MOVE PC-A TO ML621-REC-TYPE                                    
                 WRITE DEPT-BRND-VND-ACTIVE-REC FROM ML621-CSV-TE-REC           
              END-IF.                                                           
                                                                                
           MOVE ZERO                TO IMT00017-DEPT-BRND-VND-ID.               
           MOVE ZERO                TO IMT00017-DEPT-NBR.                       
           MOVE SPACES              TO VSCHVL-CHARTC-VAL-DESC.                  
           MOVE ZERO                TO VSCHVL-CHARTC-SEQ-NBR.                   
           MOVE ZERO                TO XMT00000-ENT-ID.                         
           MOVE SPACES              TO XMT00000-PRIM-VND-NM.                    
                                                                                
           PERFORM Z200-FETCH-DEPT-BRND-VND-CSR.                                
                                                                                
                                                                                
069500******************************************************************        
069600* B300-LOAD-VND-ENTRIES - INITIALIZE THE DCLGEN, OPENS AND                
069700* VERIFIES THE STORE CURSOR, DOES PRIMING FETCH OF STORE TABLE,           
069800* LOADS THE STORE CLASS TABLE UNTIL END OF DATA, CLOSING AND              
069900* VERIFIES THE STORE CURSOR.  THE STORE DOCUMENT TABLE IS INIT-           
070000* ALIZED WITH NUMERIC DATA BEING REPLACED BY 999.  THIS IS RE-            
070100* QUIRED, BECAUSE A BINARY SEARCH WILL BE PERFORMED ON THE STORE          
070200* NUMBER.                                                                 
070300******************************************************************        
070400 B300-LOAD-VND-ENTRIES.                                                   
070500     INITIALIZE DSV-DIRECT-SHIP-VEND-TABLE                                
W26603         REPLACING NUMERIC DATA BY 99999.                                 
070700                                                                          
W26603     INITIALIZE DCLTPURCHS.                                               
070900                                                                          
           PERFORM Z500-OPEN-DIRECT-SHIP-VND-CSR.                               
                                                                                
072700     PERFORM Z600-FETCH-DIRECT-SHIP-VND-CSR.                              
072800     PERFORM                                                              
072900         UNTIL SQLCODE = +100                                             
073000         IF  DSV-VEND-IDX < PC-VND-MAX                                    
073100             PERFORM B400-LOAD-DSV                                        
073200         ELSE                                                             
073300             DISPLAY '** ABEND **'                                        
073400             DISPLAY '** PGM = MLKUT621 **'                               
073500             DISPLAY '** VENDOR TABLE IS TOO SMALL'                       
073600             DISPLAY '** INCREASE VENDOR TABLE SIZE'                      
073700             SET AC-TABLE-OVERFLOW TO TRUE                                
073800             CALL 'ILBOABN0' USING ABEND-CODE                             
073900         END-IF                                                           
074000     END-PERFORM.                                                         
074100                                                                          
           PERFORM Z700-CLOSE-DIRECT-SHIP-VND-CSR.                              
075800                                                                          
RONNA **   PERFORM Z900-COMMIT.                                                 
                                                                                
080400******************************************************************        
080500* B400-LOAD-DSV    - THE STORE IS CHECKED TO SEE IF IT'S CURRENTLY        
080600* OPEN. IF SO, THE STORE NAME, NUMBER AND FINANCIAL CORPORATION           
080700* NUMBER IS MOVED INTO THE STORE DOCUMENT TABLE.                          
080800******************************************************************        
080900 B400-LOAD-DSV.                                                           
081000                                                                          
W26603     MOVE PURCHS-ENT-ID                                                   
081400         TO DSV-ENT-ID ( DSV-VEND-IDX).                                   
081900     SET  DSV-VEND-IDX UP BY 1.                                           
082000                                                                          
082100     PERFORM Z600-FETCH-DIRECT-SHIP-VND-CSR.                              
                                                                                
094300******************************************************************        
094400* B500-SEARCH-FOR-DSV  'SEARCH ALL' IS A BINARY SEARCHON THE              
094500* ENT ID (VENDOR).  THE TABLE IS A LIST OF DIRECT SHIP VENDORS            
094500* THAT IS BUILT UPON LOOKING AT ALL THE PO'S WITH A PO TYPE OF            
094500* CD FOR CONSUMER DIRECT                                                  
094600******************************************************************        
094700 B500-SEARCH-FOR-DSV.                                                     
094800     SEARCH ALL DSV-VEND-ENTRY                                            
094900         AT END                                                           
095700             SET DSV-NO TO TRUE                                           
095900         WHEN DSV-ENT-ID (DSV-VEND-IDX) = XMT00000-ENT-ID                 
RONNAD*            DISPLAY 'ENT-ID IS DSV: ' XMT00000-ENT-ID                    
RONNAD*                    ' DKEY: ' IMT00017-DEPT-BRND-VND-ID                  
096000             SET DSV-YES TO TRUE                                          
           END-SEARCH.                                                          
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
                                                                                
           CLOSE   DEPT-BRND-VND-END                                            
                   DEPT-BRND-VND-ACTIVE.                                        
                                                                                
      *****************************************************************         
      *Z000-GET-FSCL-MN-END-DATES.                                              
      *                                                                         
      *  READ THE MARKDOWN RECORDS THAT CONTAIN THE NEGATIVE ON-HANDS           
      *****************************************************************         
       Z000-GET-FSCL-MN-END-DATES.                                              
                                                                                
            EXEC SQL                                                            
               SELECT B.PFM_END_DTE                                             
                     ,B.FSCL_MN_END_DTE                                         
               INTO :DTATTR-PFM-END-DTE                                         
                   ,:DTATTR-FSCL-MN-END-DTE                                     
               FROM TMLCNTL A                                                   
                   ,TDTATTR B                                                   
               WHERE A.OPN_FSCL_MN_DTE = B.KY_DTE                               
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE DTATTR-PFM-END-DTE     TO PV-START-DATE-RANGE          
                    MOVE DTATTR-FSCL-MN-END-DTE TO PV-END-DATE-RANGE            
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    '5000-GET-OPEN-MONTH-DATES'                          
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    OPEN CURSOR                                                          
      ******************************************************************        
       Z100-OPEN-DEPT-BRND-VND-CSR.                                             
                                                                                
           EXEC SQL                                                             
               OPEN DEPT_BRND_VND_CSR                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-CSR         TO TRUE                               
           ELSE                                                                 
               MOVE 'OPEN DEPT_BRND_VND_CSR' TO PV-OPER-ATTEMPTED               
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT DBV RECORD FROM THE CURSOR                            
      ******************************************************************        
       Z200-FETCH-DEPT-BRND-VND-CSR.                                            
                                                                                
           EXEC SQL                                                             
                FETCH DEPT_BRND_VND_CSR                                         
                  INTO                                                          
                       :IMT00017-DEPT-BRND-VND-ID                               
                      ,:IMT00017-DEPT-NBR                                       
                      ,:VSCHVL-CHARTC-VAL-DESC                                  
                      ,:VSCHVL-CHARTC-SEQ-NBR                                   
                      ,:XMT00000-PRIM-VND-NM                                    
                      ,:XMT00000-ENT-ID                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-CSR TO TRUE                                       
               WHEN OTHER                                                       
                   MOVE 'FETCH DEPT_BRND_VND_CSR' TO PV-OPER-ATTEMPTED          
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE TDTATTR CURSOR                                                 
      ******************************************************************        
       Z300-CLOSE-DEPT-BRND-VND-CSR.                                            
                                                                                
           EXEC SQL                                                             
                CLOSE DEPT_BRND_VND_CSR                                         
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE DEPT_BRND_VND_CSR'    TO PV-OPER-ATTEMPTED           
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    CHECK FOR ENDING INVENTORY DOLLARS FOR THE DEPT BRAND                
      *    VENDOR COMBINATION.  IF THERE ARE ANY DOLLARS OTHER THAN             
      *    ZERO THEN THE DEPT BRAND VENDOR ROW CANNOT BE ENDED.                 
      ******************************************************************        
       Z400-CHECK-EIR-SUM-DBV.                                                  
                                                                                
      *    NOTE THE DATE RANGE FOR THIS QUERY IS THE FISCAL                     
      *    MONTH END DATE FOR TWO MONTHS AT A TIME                              
                                                                                
           MOVE PV-DTE-SUB                   TO DTE-SUB.                        
           MOVE DTE-FSCL-MN-END-DTE(DTE-SUB) TO PV-END-DATE-RANGE.              
           ADD +1                            TO DTE-SUB.                        
           MOVE DTE-FSCL-MN-END-DTE(DTE-SUB) TO PV-START-DATE-RANGE.            
                                                                                
RONNAD*    DISPLAY 'MLT00004-ML-LO-MDSE-LVL-ID:  '                              
RONNAD*             MLT00004-ML-LO-MDSE-LVL-ID                                  
RONNAD*            ' MLT00004-DEPT-NBR: ' MLT00004-DEPT-NBR                     
RONNAD*            'DTE-SUB: ' DTE-SUB                                          
RONNAD*            'PV-START-DATE-RANGE:  ' PV-START-DATE-RANGE                 
RONNAD*            'PV-END-DATE-RANGE:    ' PV-END-DATE-RANGE.                  
                                                                                
           EXEC SQL                                                             
                                                                                
                SELECT  SUM(C.END_INV_RTL_AMT)                                  
                INTO   :MLT00004-END-INV-RTL-AMT   :PV-NULL-INDICATOR           
                FROM   MLT_PARTN_CNTL   A                                       
                      ,MLT_PARTN_BAL    B                                       
                      ,MLT_MNLY_SUM     C                                       
                WHERE  A.TBL_TM_LVL_CDE  = 'M'                                  
                AND A.FSCL_DTE BETWEEN :PV-START-DATE-RANGE AND                 
                                       :PV-END-DATE-RANGE                       
                AND A.PARTN_NBR          = C.PARTN_NBR                          
                AND A.DEPT_GP_CDE        = B.DEPT_GP_CDE                        
                AND A.FSCL_DTE BETWEEN B.EFF_BEG_DTE AND B.EFF_END_DTE          
                AND A.TBL_TM_LVL_CDE     = B.TBL_TM_LVL_CDE                     
                AND B.DEPT_NBR           = C.DEPT_NBR                           
                AND B.DEPT_NBR           = :MLT00004-DEPT-NBR                   
                AND C.ML_LO_MDSE_LVL_ID  = :MLT00004-ML-LO-MDSE-LVL-ID          
                WITH UR                                                         
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
RONNAD*        DISPLAY 'PV-NULL-INDICATOR: ' PV-NULL-INDICATOR                  
RONNAD*          ' MLT00004-END-INV-RTL-AMT: ' MLT00004-END-INV-RTL-AMT         
      *        CHECK THE NULL INDICATOR                                         
      *        -1 MEANS THE VALUE IS NULL BECAUSE NO ROWS WERE FOUND            
      *         0 MEANS A VALUE WAS RETURNED AND CAN BE CHECKED                 
                   IF PV-NULL-INDICATOR < 0                                     
RONNAD*                DISPLAY 'NULL IND NEGATIVE'                              
      *                NO ROWS WERE FOUND                                       
      *                STOP CHECKING IF ALL 14 MONTHS WERE CHECKED              
                       IF DTE-SUB >= PC-MAX-DTE-SUB                             
                          SET END-DATE-THIS-RECORD TO TRUE                      
                       ELSE                                                     
      *                KEEP CHECKING THE REMAINING 14 MONTHS                    
                          CONTINUE                                              
                   ELSE                                                         
                       IF PV-NULL-INDICATOR = 0                                 
RONNAD*                DISPLAY 'NULL IND = 0    '                               
      *                ROWS WERE FOUND AND CHECK THE SUM AMOUNT                 
                          IF MLT00004-END-INV-RTL-AMT = 0                       
                             IF DTE-SUB >= 14                                   
      *                         THEN 14 MONTHS HAVE BEEN CHECKED                
      *                         AND THERE IS NO DOLLAR AMOUNT FOUND             
      *                         MARKING THIS TO END DATE WILL DROP              
      *                         OUT OF LOOP                                     
                                SET END-DATE-THIS-RECORD TO TRUE                
                             ELSE                                               
      *                         NOT ALL 14 MONTHS HAVE BEEN CHECKED             
      *                         AND SO FAR NOTHING HAS BEEN FOUND               
      *                         SO KEEP CHECKING THROUGH REST OF MONTHS         
                                CONTINUE                                        
                          ELSE                                                  
      *                      EIR AMOUNT HAS BEEN FOUND AND IT IS                
      *                      NOT ZERO STOP CHECKING TO DROP OUT OF LOOP         
RONNAD*     IF DTE-SUB > 2                                                      
RONNAD*        DISPLAY 'STOP EIR CHECK OCCURRED 3 OR MORE '                     
RONNAD*                'MONTHS BACK'                                            
RONNAD*        DISPLAY 'MLT00004-ML-LO-MDSE-LVL-ID:  '                          
RONNAD*                 MLT00004-ML-LO-MDSE-LVL-ID                              
RONNAD*                ' MLT00004-DEPT-NBR: ' MLT00004-DEPT-NBR                 
RONNAD*                'DTE-SUB: ' DTE-SUB                                      
RONNAD*                'PV-START-DATE-RANGE:  ' PV-START-DATE-RANGE             
RONNAD*                'PV-END-DATE-RANGE:    ' PV-END-DATE-RANGE               
RONNAD*     END-IF                                                              
                             SET STOP-EIR-CHECK TO TRUE                         
RONNAD*     DISPLAY 'STOP-EIR-CHECK SET TO TRUE'                                
RONNAD*             'DTE-SUB = ' DTE-SUB                                        
                          END-IF                                                
                       END-IF                                                   
                   END-IF                                                       
               WHEN OTHER                                                       
                   DISPLAY ' LVL-CDE: '                                         
                           MLT00002-TBL-TM-LVL-CDE                              
                           ' DEPT: '                                            
                           MLT00002-DEPT-NBR                                    
                           ' EFF-BEG-DTE: '                                     
                           MLT00002-EFF-BEG-DTE                                 
                           ' DEPT-GP-CDE: '                                     
                           MLT00002-DEPT-GP-CDE                                 
                   MOVE 'CHECK FOR EXISTING ROW' TO PV-OPER-ATTEMPTED           
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    OPEN CURSOR                                                          
      ******************************************************************        
       Z500-OPEN-DIRECT-SHIP-VND-CSR.                                           
                                                                                
           EXEC SQL                                                             
               OPEN DIRECT_SHIP_VND_CSR                                         
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               SET NOT-END-OF-VND-CSR         TO TRUE                           
           ELSE                                                                 
               DISPLAY '** ABEND - MLKUT621 **'                                 
               DISPLAY '** OPEN ERROR ON STORE TABLE'                           
               MOVE 'OPEN DIRECT_SHIP_VND_CSR' TO PV-OPER-ATTEMPTED             
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD DIRECT SHIP VENDOR CURSOR                      
      ******************************************************************        
       Z600-FETCH-DIRECT-SHIP-VND-CSR.                                          
                                                                                
           EXEC SQL                                                             
                FETCH DIRECT_SHIP_VND_CSR                                       
                  INTO                                                          
                       :PURCHS-ENT-ID                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-VND-CSR TO TRUE                                   
               WHEN OTHER                                                       
                   MOVE 'FETCH DIRECT_SHIP_VND_CSR' TO PV-OPER-ATTEMPTED        
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE DSV CURSOR                                                     
      ******************************************************************        
       Z700-CLOSE-DIRECT-SHIP-VND-CSR.                                          
                                                                                
           EXEC SQL                                                             
                CLOSE DIRECT_SHIP_VND_CSR                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'CLOSE DIRECT_SHIP_VND_CSR'  TO PV-OPER-ATTEMPTED           
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *Z800-GET-PREVIOUS-2-MN-END-DTES                                          
      *                                                                         
      *  READ THE MARKDOWN RECORDS THAT CONTAIN THE NEGATIVE ON-HANDS           
      *****************************************************************         
       Z800-GET-PREV-2-MN-END-DTES.                                             
                                                                                
            EXEC SQL                                                            
               SELECT A.PFM_END_DTE                                             
                     ,B.PFM_END_DTE                                             
               INTO :DTATTR-FSCL-MN-END-DTE                                     
                   ,:DTATTR-PFM-END-DTE                                         
               FROM TDTATTR A                                                   
                   ,TDTATTR B                                                   
               WHERE A.KY_DTE = :DTATTR-KY-DTE                                  
               AND B.KY_DTE = A.PFM_END_DTE                                     
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE DTATTR-PFM-END-DTE     TO PV-START-DATE-RANGE          
                    MOVE DTATTR-FSCL-MN-END-DTE TO PV-END-DATE-RANGE            
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    '5000-GET-OPEN-MONTH-DATES'                          
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      *Z850-CHECK-DEBT-BRND-FOR-STYLE                                           
      *                                                                         
      *  READ THE MARKDOWN RECORDS THAT CONTAIN THE NEGATIVE ON-HANDS           
      *****************************************************************         
       Z850-CHECK-DEBT-BRND-FOR-STYLE.                                          
                                                                                
           MOVE ZERO                      TO PV-COUNT.                          
           MOVE ZERO                      TO PV-NULL-INDICATOR.                 
           MOVE IMT00017-DEPT-NBR         TO MDARVS-DEPT-NBR.                   
           MOVE PV-LBL-SEQ-NBR-NBR        TO MDARVS-LBL-SEQ-NBR.                
                                                                                
            EXEC SQL                                                            
               SELECT COUNT(*)                                                  
               INTO :PV-COUNT                                                   
RONNA3*        INTO :PV-COUNT    :PV-NULL-INDICATOR                             
               FROM TMDARVS A                                                   
               WHERE A.DEPT_NBR    = :MDARVS-DEPT-NBR                           
               AND   A.LBL_SEQ_NBR = :MDARVS-LBL-SEQ-NBR                        
               WITH UR                                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
RONNA3             IF PV-COUNT <= 0                                             
RONNA3*            IF PV-NULL-INDICATOR < 0                                     
RONNA2*                DISPLAY 'NULL IND NEGATIVE'                              
RONNA2*                NO ROWS WERE FOUND                                       
                       SET END-DATE-THIS-RECORD TO TRUE                         
                   ELSE                                                         
RONNA2*                ROWS WERE FOUND                                          
                       SET DO-NOT-END-DATE-THIS-RECORD TO TRUE                  
                   END-IF                                                       
               WHEN OTHER                                                       
                   DISPLAY 'MDARVS-DEPT-NBR '    MDARVS-DEPT-NBR                
                   DISPLAY 'MDARVS-LBL-SEQ-NBR ' MDARVS-LBL-SEQ-NBR             
                   MOVE    'Z850-CHECK-DEBT-BRND-FOR-STYLE'                     
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT COUNT OF STYLE  '                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM Z900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    COMMIT DB2 COMMAND.                                                  
      ******************************************************************        
       Z900-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'COMMIT DB2 COMMAND  ' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-CODE         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUT621'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
