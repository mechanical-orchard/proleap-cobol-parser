       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             XSKRP019.                                00020000
       AUTHOR.                 KOHLS DEPARTMENT STORES.                 00030000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00040000
       DATE-WRITTEN.           01/29/2003.                              00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      ***XS DOMAIN - XST_SUB_CL REPORT                                  00080000
      ******************************************************************00090000
      **THIS PROGRAM FORMATS XST_SUB_CL ERROR DATA INTO A REPORT        00100000
      ******************************************************************00110000
      *DATE:01/29/2003   MADE BY:CHRIS ALBRIGHT INITIAL WRITE           00120000
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.                        IBM-3090.                00160000
       OBJECT-COMPUTER.                        IBM-3090.                00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT INPUT-FILE                                            00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT REPORT-FILE                                           00230000
                  ASSIGN TO RPT01.                                      00240000
                                                                        00250000
       DATA DIVISION.                                                   00260000
       FILE SECTION.                                                    00270000
                                                                        00280000
       FD  INPUT-FILE                                                   00290000
           RECORDING MODE IS F                                          00300000
           LABEL RECORDS ARE STANDARD                                   00310000
           BLOCK CONTAINS 0 CHARACTERS.                                 00320000
       01  INPUT-RECORD                    PIC X(200).                  00330000
                                                                        00340000
       FD  REPORT-FILE                                                  00350000
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 CHARACTERS.                                 00380000
       01  REPORT-RECORD                   PIC X(133).                  00390000
                                                                        00400000
       WORKING-STORAGE SECTION.                                         00410000
      ******************************************************************00420000
      *SWITCHES                                                         00430000
      ******************************************************************00440000
       01  PS-PROGRAM-SWITCHES.                                         00450000
           05  PS-END-OF-INPUT-FILE-SW    PIC X(01)   VALUE 'N'.        00460000
               88  PS-EOF-INPUT-FILE                  VALUE 'Y'.        00470000
               88  PS-NOT-EOF-INPUT-FILE              VALUE 'N'.        00480000
      ******************************************************************00490000
      **TIME FIELDS                                                     00500000
      ******************************************************************00510000
       01  DF-DATETIME-FIELDS.                                          00520000
           05  DF-ACCEPT-TIME.                                          00530000
               10  DF-ACCEPT-HR             PIC 9(02) VALUE ZEROS.      00540000
               10  DF-ACCEPT-MIN            PIC 9(02) VALUE ZEROS.      00550000
               10  DF-ACCEPT-SEC            PIC 9(02) VALUE ZEROS.      00560000
               10  DF-ACCEPT-HSEC           PIC 9(02) VALUE ZEROS.      00570000
           05  DF-ACCEPT-DATE.                                          00580000
               10  DF-ACCEPT-YR             PIC 9(02) VALUE ZEROS.      00590000
               10  DF-ACCEPT-MO             PIC 9(02) VALUE ZEROS.      00600000
               10  DF-ACCEPT-DY             PIC 9(02) VALUE ZEROS.      00610000
      ******************************************************************00620000
      *PROGRAM CONSTANTS                                                00630000
      ******************************************************************00640000
       01  PC-PROGRAM-CONSTANTS.                                        00650000
           05  PC-PROGRAM-NAME            PIC X(08) VALUE 'XSKRP019'.   00660000
      ******************************************************************00670000
      **PROGRAM COUNTS                                                  00680000
      ******************************************************************00690000
       01  PC-PROGRAM-COUNTERS.                                         00700000
           05  PC-LINE-COUNT              PIC 9(03) VALUE ZEROS.        00710000
           05  PC-PAGE-COUNT              PIC 9(03) VALUE ZEROS.        00720000
           05  PC-RECORDS-READ            PIC 9(06) VALUE ZEROS.        00730000
      ***************************************************************** 00740000
      **ABEND CODE FIELDS                                               00750000
      ***************************************************************** 00760000
       01  ABEND-CODE                     PIC S9(4)                     00770000
                                                 VALUE ZEROS COMP SYNC. 00780000
       01  PV-MISC-ABEND-FIELDS.                                        00790000
           05  PV-PARAGRAPH-NAME          PIC  X(30)    VALUE SPACE.    00800000
           05  PV-OPERATION-MSG-1         PIC  X(40)    VALUE SPACE.    00810000
           05  PV-OPERATION-MSG-2         PIC  X(40)    VALUE SPACE.    00820000
           05  PV-DB2-OPERATION           PIC  X(30)    VALUE SPACES.   00830000
      ***************************************************************** 00840000
      **REPORT HEADINGS                                                 00850000
      ***************************************************************** 00860000
       01  REPORT-HEADING-1.                                            00870000
           05  FILLER                     PIC  X(56)  VALUE SPACES.     00880000
           05  FILLER                     PIC  X(22)                    00890000
                                 VALUE 'XS DOMAIN ERROR REPORT'.        00900000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00910000
       01  REPORT-HEADING-2.                                            00920000
           05  FILLER                     PIC  X(53)  VALUE SPACES.     00930000
           05  FILLER                     PIC  X(27)                    00940000
                                 VALUE 'XST_SUB_CL INTEGRITY REPORT'.   00950000
           05  FILLER                     PIC  X(37)  VALUE SPACES.     00960000
       01  REPORT-HEADING-3.                                            00970000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     00980000
           05  FILLER                     PIC  X(08)                    00990000
                                 VALUE 'DEPT_NBR'.                      01000000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01010000
           05  FILLER                     PIC  X(10)                    01020000
                                 VALUE 'MAJ_CL_NBR'.                    01030000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01040000
           05  FILLER                     PIC  X(10)                    01041000
                                 VALUE 'SUB_CL_NBR'.                    01042000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01042100
           05  FILLER                     PIC  X(09)                    01042200
                                 VALUE 'SUB_CL_NM'.                     01042300
           05  FILLER                     PIC  X(23)  VALUE SPACES.     01042400
           05  FILLER                     PIC  X(13)                    01042500
                                 VALUE 'POS_MDSE_DESC'.                 01042600
           05  FILLER                     PIC  X(19)  VALUE SPACES.     01042700
           05  FILLER                     PIC  X(12)                    01042800
                                 VALUE 'TAX_DFLT_IND'.                  01042900
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043000
           05  FILLER                     PIC  X(13)                    01043100
                                 VALUE 'DISC_ELIG_IND'.                 01043200
       01  REPORT-HEADING-4.                                            01043300
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043400
           05  FILLER                     PIC  X(08)  VALUE ALL '-'.    01043500
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043600
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01043700
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01043800
           05  FILLER                     PIC  X(10)  VALUE ALL '-'.    01043900
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01044000
           05  FILLER                     PIC  X(30)  VALUE ALL '-'.    01045000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01045100
           05  FILLER                     PIC  X(30)  VALUE ALL '-'.    01045200
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01045300
           05  FILLER                     PIC  X(12)  VALUE ALL '-'.    01045400
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01045500
           05  FILLER                     PIC  X(13)  VALUE ALL '-'.    01045600
      ***************************************************************** 01045700
      **REPORT DETAIL                                                   01045800
      ***************************************************************** 01045900
       01  REPORT-DETAIL-1.                                             01046000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01047000
           05  RD-XS-DEPT-NBR             PIC  ZZZ9.                    01048000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01049000
           05  RD-XS-MAJ-CL-NBR           PIC  ZZZ9.                    01050000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01060000
           05  RD-XS-SUB-CL-NBR           PIC  ZZZ9.                    01070000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01080000
           05  RD-XS-SUB-CL-NME           PIC  X(30).                   01090000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01100000
           05  RD-XS-POS-MDSE-DESC        PIC  X(30).                   01110000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01120000
           05  RD-XS-TAX-DFLT-IND         PIC  X(01).                   01121000
           05  FILLER                     PIC  X(13)  VALUE SPACES.     01122000
           05  RD-XS-DISC-ELIG-IND        PIC  X(01).                   01123000
                                                                        01124000
       01  REPORT-DETAIL-2.                                             01125000
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01126000
           05  RD-VSM-DEPT-NBR            PIC  ZZZ9.                    01127000
           05  FILLER                     PIC  X(06)  VALUE SPACES.     01128000
           05  RD-VSM-MAJ-CL-NBR          PIC  ZZZ9.                    01129000
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01129100
           05  RD-VSM-SUB-CL-NBR          PIC  ZZZ9.                    01129200
           05  FILLER                     PIC  X(08)  VALUE SPACES.     01129300
           05  RD-VSM-SUB-CL-NME          PIC  X(30).                   01129400
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01129500
           05  RD-VSM-POS-MDSE-DESC       PIC  X(30).                   01129600
           05  FILLER                     PIC  X(02)  VALUE SPACES.     01129700
           05  RD-VSM-TAX-DFLT-IND        PIC  X(01).                   01129800
           05  FILLER                     PIC  X(13)  VALUE SPACES.     01129900
           05  RD-VSM-DISC-ELIG-IND       PIC  X(01).                   01130000
                                                                        01130100
       01  RF-END-OF-REPORT.                                            01130200
           05  FILLER                     PIC X(57)  VALUE SPACES.      01130300
           05  FILLER                     PIC X(21)                     01130400
                                 VALUE '***END OF REPORT***'.           01130500
           05  FILLER                     PIC X(57)  VALUE SPACES.      01130600
      ***************************************************************** 01130700
      **COPYBOOKS                                                       01130800
      ***************************************************************** 01130900
      ****ABEND ROUTINE INFORMATION                                     01131000
           COPY DPWS900.                                                01132000
      ****ABEND ROUTINE INFORMATION                                     01133000
           COPY DPWS004.                                                01134008
      ***STANDARD REPORT HEADER FOR A 132 COLUMN REPORT.                01135000
           COPY DPWS132O.                                               01136000
      ***INPUT RECORD                                                   01137000
           COPY XSRD008.                                                01138000
                                                                        01139000
       PROCEDURE DIVISION.                                              01140000
      ******************************************************************01150000
       0000-MAINLINE.                                                   01160000
                                                                        01170000
           PERFORM 1000-INITIALIZATION.                                 01180000
           PERFORM 2000-PROCESS-FILES                                   01190000
                   UNTIL PS-EOF-INPUT-FILE.                             01200000
           PERFORM 9999-WRAPUP.                                         01210000
           GOBACK.                                                      01220000
                                                                        01230000
       EJECT.                                                           01240000
      ******************************************************************01250000
      *INITIALIZATION ROUTINE                                           01260000
      ******************************************************************01270000
       1000-INITIALIZATION.                                             01280000
                                                                        01290000
           OPEN INPUT   INPUT-FILE                                      01300000
                OUTPUT  REPORT-FILE.                                    01310000
                                                                        01320000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01330000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01340000
                                                                        01350000
           PERFORM 1100-SETUP-HEADINGS.                                 01360000
                                                                        01370000
           PERFORM 9999-READ-INPUT-FILE.                                01380000
                                                                        01390000
       EJECT.                                                           01400000
      ******************************************************************01410000
      *SETS UP ALL HEADING STATIC VARIABLES FOR THE REPORT DURATION     01420000
      ******************************************************************01430000
       1100-SETUP-HEADINGS.                                             01440000
                                                                        01450000
           MOVE '1200-SETUP-HEADINGS' TO PV-PARAGRAPH-NAME.             01460000
                                                                        01470000
           MOVE DF-ACCEPT-YR          TO DP132O-RUN-YEAR.               01480000
           MOVE DF-ACCEPT-MO          TO DP132O-RUN-MONTH.              01490000
           MOVE DF-ACCEPT-DY          TO DP132O-RUN-DAY.                01500000
           MOVE DF-ACCEPT-HR          TO DP132O-RUN-HOUR.               01510000
           MOVE DF-ACCEPT-MIN         TO DP132O-RUN-MINUTE.             01520000
                                                                        01530000
           MOVE PC-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.                01540000
           MOVE 1                TO DP132O-REPORT-NUMBER.               01550000
                                                                        01560000
           PERFORM 4000-PRINT-HEADINGS.                                 01570000
                                                                        01580000
       EJECT.                                                           01590000
      ******************************************************************01600000
      *PROCESS BOTH FILES                                               01610000
      ******************************************************************01620000
       2000-PROCESS-FILES.                                              01630000
                                                                        01640000
           MOVE '2000-PROCESS-FILES' TO PV-PARAGRAPH-NAME.              01650000
                                                                        01660000
           PERFORM 4100-PRINT-DETAIL.                                   01670000
                                                                        01680000
           IF  PC-LINE-COUNT > 60                                       01690000
               PERFORM 4000-PRINT-HEADINGS                              01700000
           END-IF.                                                      01710000
                                                                        01720000
           PERFORM 9999-READ-INPUT-FILE.                                01730000
                                                                        01740000
       EJECT.                                                           01750000
      ******************************************************************01760000
      *PRINTS REPORT HEADINGS AT TOP OF PAGE                            01770000
      ******************************************************************01780000
       4000-PRINT-HEADINGS.                                             01790000
                                                                        01800000
           MOVE '4000-PRINT-HEADINGS' TO PV-PARAGRAPH-NAME.             01810000
                                                                        01820000
           INITIALIZE PC-LINE-COUNT.                                    01830000
           ADD +1                     TO PC-PAGE-COUNT.                 01840000
           MOVE PC-PAGE-COUNT         TO DP132O-PAGE-NUMBER.            01850000
                                                                        01860000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      01870000
                                                  AFTER ADVANCING PAGE. 01880000
           WRITE REPORT-RECORD FROM REPORT-HEADING-1                    01890000
                                               AFTER ADVANCING 2 LINES. 01900000
           WRITE REPORT-RECORD FROM REPORT-HEADING-2                    01910000
                                               AFTER ADVANCING 1 LINES. 01920000
           WRITE REPORT-RECORD FROM REPORT-HEADING-3                    01930000
                                               AFTER ADVANCING 3 LINES. 01940000
           WRITE REPORT-RECORD FROM REPORT-HEADING-4                    01950000
                                               AFTER ADVANCING 1 LINES. 01960000
                                                                        01970000
           ADD +7                     TO PC-LINE-COUNT.                 01980000
                                                                        01990000
       EJECT.                                                           02000000
      ******************************************************************02010000
      *PRINTS DETAIL REPORT                                             02020000
      ******************************************************************02030000
       4100-PRINT-DETAIL.                                               02040000
                                                                        02050000
           MOVE '4100-PRINT-DETAIL' TO PV-PARAGRAPH-NAME.               02060000
                                                                        02070000
           MOVE XSRD006-XS-DEPT-NBR TO RD-XS-DEPT-NBR.                  02080000
           MOVE XSRD006-XS-MAJ-CL-NBR TO RD-XS-MAJ-CL-NBR.              02090000
           MOVE XSRD006-XS-SUB-CL-NBR TO RD-XS-SUB-CL-NBR.              02100000
           MOVE XSRD006-XS-SUB-CL-NME TO RD-XS-SUB-CL-NME.              02110000
           MOVE XSRD006-XS-POS-MDSE-DESC TO RD-XS-POS-MDSE-DESC.        02120000
           MOVE XSRD006-XS-TAX-DFLT-IND TO RD-XS-TAX-DFLT-IND.          02130000
           MOVE XSRD006-XS-DISC-ELIG-IND TO RD-XS-DISC-ELIG-IND.        02140000
                                                                        02150000
           MOVE XSRD006-VSM-DEPT-NBR TO RD-VSM-DEPT-NBR.                02160000
           MOVE XSRD006-VSM-MAJ-CL-NBR TO RD-VSM-MAJ-CL-NBR.            02170000
           MOVE XSRD006-VSM-SUB-CL-NBR TO RD-VSM-SUB-CL-NBR.            02180000
           MOVE XSRD006-VSM-SUB-CL-NME TO RD-VSM-SUB-CL-NME.            02190000
           MOVE XSRD006-VSM-POS-MDSE-DESC TO RD-VSM-POS-MDSE-DESC.      02200000
           MOVE XSRD006-VSM-TAX-DFLT-IND TO RD-VSM-TAX-DFLT-IND.        02210000
           MOVE XSRD006-VSM-DISC-ELIG-IND TO RD-VSM-DISC-ELIG-IND.      02220000
                                                                        02230000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-1                     02240000
                                            AFTER ADVANCING 2 LINES.    02250000
           WRITE REPORT-RECORD FROM REPORT-DETAIL-2                     02260000
                                            AFTER ADVANCING 1 LINE.     02270000
                                                                        02280000
           ADD +3                   TO PC-LINE-COUNT.                   02290000
                                                                        02300000
       EJECT.                                                           02310000
      ******************************************************************02320000
      *READS INPUT FILE                                                 02330000
      ******************************************************************02340000
       9999-READ-INPUT-FILE.                                            02350000
                                                                        02360000
           READ INPUT-FILE INTO XSRD008-SUBCLASS-ERR-RECORD             02370000
               AT END SET PS-EOF-INPUT-FILE TO TRUE                     02380000
           END-READ.                                                    02390000
                                                                        02400000
           ADD +1  TO PC-RECORDS-READ.                                  02410000
                                                                        02420000
       EJECT.                                                           02430000
      ******************************************************************02440000
      *PROGRAM END PROCESSING                                           02450000
      ******************************************************************02460000
       9999-WRAPUP.                                                     02470000
                                                                        02480000
           WRITE REPORT-RECORD FROM RF-END-OF-REPORT                    02490000
                                    AFTER ADVANCING 2 LINES.            02500000
                                                                        02510000
           CLOSE INPUT-FILE                                             02520000
                 REPORT-FILE.                                           02530000
                                                                        02540000
           DISPLAY '*************************************************'. 02550000
           DISPLAY '* PROGRAM..................: ' PC-PROGRAM-NAME.     02560000
           DISPLAY '* RUN DATE.................: ' DF-ACCEPT-DATE.      02570000
           DISPLAY '* RUN TIME.................: ' DF-ACCEPT-TIME.      02580000
           DISPLAY '*--------- COUNTS FOR THIS RUN ---------'.          02590000
           DISPLAY '* RECORDS READ........: ' PC-RECORDS-READ.          02600000
           DISPLAY '*************************************************'. 02610000
                                                                        02620000
       EJECT.                                                           02630000
                                                                        02640000
       EJECT.                                                           02650000
