       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.         SSKUT017.                                    00020010
       AUTHOR.             COGNIZANT.                                   00030010
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040000
       DATE-WRITTEN.       DEC, 2009.                                   00050010
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      * THIS PROGRAM PREPARES THE TRN-CTG-SUM-FILE UPDATE FILE TO BE   *00080021
      * PROCESSED BY SSKUP001 UNDER THE DB2 CHECKPOINT RESTART SYSTEM. *00090018
      *                                                                *00100000
      * THE PROGRAM READS THE INPUT PURGE RECORDS, REFORMATS THE       *00110000
      * RECORDS AND SETS A CHECKPOINT INDICATOR ON THE RECORD BASED    *00120000
      * ON HOW CHECKPOINTS WILL BE TAKEN BY THE MAINTENANCE PROGRAM.   *00130000
      * AFTER A RECORD HAS BEEN REFORMATTED IT IS PASSED TO THE        *00140000
      * TRANSACTION PREPARATION MODULE (DPKUT900) WHICH DOES SOME MORE *00150000
      * PROCESSING ON THE RECORDS AND WRITES THE REFORMATTED RECORDS   *00160000
      * OUT TO A SEQUENTIAL FILE.                                      *00170000
      *                                                                *00180000
      * THE PROGRAM COMMUNICATES WITH THE TRANSACTION PREPARATION      *00190000
      * MODULE USING FUNCTION CODES (OPEN, WRITE, & CLOSE) AND CHECKS  *00200000
      * THE RETURN CODE AFTER EACH REQUEST.  IF AN INVALID RETURN CODE *00210000
      * IS RECEIVED, ANY INFORMATION THAT MAY BE HELPFUL IN RESOLVING  *00220000
      * THE BAD RETURN CODE IS DISPLAYED AND THE PROGRAM ABENDS.       *00230006
      ******************************************************************00240006
C27938* DATE: 05/24/2012                                                00250027
C27938* WHO: KAREN GIVENS (REFER TO 'C27938' FOR ALL CHANGES)           00260027
C27938* WHY: ADDED NEW TRN_DTE TO EPT_TRN_CTG_SUM                       00270027
C27938******************************************************************00280027
260107* DATE: 08/16/2021                                                00281028
260107* WHO: JIM HUNTER   (REFER TO '260107' FOR ALL CHANGES)           00282028
260107* WHY: ADDED COPYBOOK DPWS990 TO MAKE CALL TO DPKUT900 DYNAMIC    00283028
260107******************************************************************00284028
       ENVIRONMENT DIVISION.                                            00290000
       CONFIGURATION SECTION.                                           00300000
       SOURCE-COMPUTER.    IBM-3090.                                    00310000
       OBJECT-COMPUTER.    IBM-3090.                                    00320000
                                                                        00330000
       INPUT-OUTPUT SECTION.                                            00340000
       FILE-CONTROL.                                                    00350000
           SELECT TRN-CTG-SUM-FILE ASSIGN TO INP01.                     00360012
      ******************************************************************00370000
       DATA DIVISION.                                                   00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  TRN-CTG-SUM-FILE                                             00410012
           RECORDING MODE IS F                                          00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           LABEL RECORDS ARE OMITTED.                                   00440000
                                                                        00450000
C27938 01  TRN-CTG-SUM-REC                 PIC X(47).                   00460027
      ******************************************************************00470000
       WORKING-STORAGE SECTION.                                         00480000
       01  ABEND-CODE                       PIC S9(04) VALUE +0  COMP.  00490000
                                                                        00500000
       01  PROGRAM-CONSTANTS.                                           00510000
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE 'SSKUT017'.00520008
           05  PC-TRAN-PREP-FUNC-CODES.                                 00530000
               10  PC-TRAN-PREP-FUNC-OPEN   PIC X(05)  VALUE 'OPEN '.   00540000
               10  PC-TRAN-PREP-FUNC-WRITE  PIC X(05)  VALUE 'WRITE'.   00550000
               10  PC-TRAN-PREP-FUNC-CLOSE  PIC X(05)  VALUE 'CLOSE'.   00560000
           05  PC-JOB-NAME                  PIC X(08)  VALUE 'SSK001  '.00570008
           05  PC-PLAN-NAME                 PIC X(08)  VALUE 'SSKUP001'.00580023
C27938     05  PC-TRANS-RECORD-LENGTH       PIC S9(04) VALUE +047 COMP. 00590027
                                                                        00600000
      ******************************************************************00610000
       01  PROGRAM-VARIABLES.                                           00620000
           05  PV-CHECKPOINT-TYPE           PIC X(01)  VALUE SPACE.     00630000
               88  CONTROL-BREAK                       VALUE 'C'.       00640000
               88  LOGICAL-TRANSACTION                 VALUE 'L'.       00650000
               88  TIME-INTERVAL                       VALUE 'T'.       00660000
               88  REQUESTED-BY-PROGRAM                VALUE 'R'.       00670000
           05  PV-HOLD-TRN-STR-NBR         PIC  S9(4) COMP VALUE ZERO.  00680011
                                                                        00690000
      ******************************************************************00700000
       01  PROGRAM-ACCUMULATORS.                                        00710000
           05  PA-INPUT-RECORDS-READ        PIC S9(11) VALUE 0  COMP-3. 00720000
                                                                        00730000
           05  PA-OUTPUT-RECS-WRITTEN       PIC S9(11) VALUE 0  COMP-3. 00740000
                                                                        00750000
      ******************************************************************00760000
       01  PROGRAM-SWITCHES.                                            00770000
           05  PS-INPUT-UPDATE-EOF-SW       PIC X(01)  VALUE 'N'.       00780000
               88  PS-INPUT-UPDATE-EOF                 VALUE 'Y'.       00790000
           05  PS-FIRST-TRANS-READ-SW       PIC X(01)  VALUE 'Y'.       00800000
               88  FIRST-TRANS-READ                    VALUE 'Y'.       00810000
      ***************************************************************   00820009
      * RECORD LAYOUT FOR INPUT FILE TRN-CTG-SUM-FILE                   00830009
      ***************************************************************   00840009
           COPY SSRD020.                                                00850020
      ******************************************************************00860004
      *  TRANSACTION PREPARATION MODULE LINKAGE SECTION                *00870004
      ******************************************************************00880004
260107     COPY DPWS990.                                                00890028
                                                                        00900004
           COPY DPLS000.                                                00901028
                                                                        00902028
      ******************************************************************00910000
      *  SQL COMMUNICATIONS WORK AREA                                  *00920000
      ******************************************************************00930000
           COPY SQLCA2.                                                 00940000
                                                                        00950000
      ******************************************************************00960000
       PROCEDURE DIVISION.                                              00970000
      ******************************************************************00980000
      * A100-MAINLINE-PROCESSING                                        00990000
      *    CONTROLS FLOW OF THE PROGRAM                                *01000000
      ******************************************************************01010000
       A100-MAINLINE-PROCESSING.                                        01020000
                                                                        01030000
           PERFORM 1000-INITIALIZE.                                     01040000
                                                                        01050000
           PERFORM 2000-PROCESS-UPDATE-INPUT-FILE                       01060000
               UNTIL PS-INPUT-UPDATE-EOF.                               01070000
                                                                        01080000
           PERFORM 3000-END-OF-JOB-ROUTINE.                             01090000
                                                                        01100000
           GOBACK.                                                      01110000
      ******************************************************************01120000
      *  INITIALIZE                                                     01130000
      ******************************************************************01140000
       1000-INITIALIZE.                                                 01150000
                                                                        01160000
           PERFORM C100-ISSUE-OPEN-REQUEST.                             01170000
                                                                        01180000
           OPEN INPUT TRN-CTG-SUM-FILE.                                 01190012
                                                                        01200000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         01210000
           IF PS-INPUT-UPDATE-EOF                                       01220000
               DISPLAY ' '                                              01230000
               DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME          01240000
               DISPLAY 'UPDATE INPUT FILE IS EMPTY'                     01250000
               DISPLAY ' '                                              01260000
           END-IF.                                                      01270000
                                                                        01280000
      ******************************************************************01290000
      * 2000-PROCESS-UPDATE-INPUT-FILE.                                *01300000
      *     READ AN UPDATE INPUT RECORD.                               *01310000
      *     IF END-OF-FILE, STOP PROCESSING.                           *01320000
      *     IF A RECORD IS READ, GATHER WHATEVER OTHER DATA            *01330000
      *     IS NECESSARY TO TAKE CHECKPOINTS BASED ON THE TYPE OF      *01340000
      *     CHECKPOINTS BEING TAKEN BY XSKUP096 AND ISSUE A WRITE      *01350000
      *     REQUEST TO THE TRANSACTION PREPARATION PROGRAM.            *01360000
      ******************************************************************01370000
       2000-PROCESS-UPDATE-INPUT-FILE.                                  01380000
                                                                        01390000
           IF FIRST-TRANS-READ                                          01400000
               MOVE 'N'                 TO PS-FIRST-TRANS-READ-SW       01410000
           END-IF.                                                      01420000
                                                                        01430000
           IF CONTROL-BREAK                                             01440000
               PERFORM C300-CKPT-BY-CNTL-BREAK                          01450000
           ELSE                                                         01460000
               IF LOGICAL-TRANSACTION                                   01470000
                   PERFORM C400-CKPT-BY-LOGICAL-TRANS                   01480000
               ELSE                                                     01490000
                   PERFORM C500-CKPT-BY-TIME-OR-REQUEST                 01500000
               END-IF                                                   01510000
           END-IF.                                                      01520000
                                                                        01530000
           PERFORM C200-READ-UPDATE-INPUT-FILE.                         01540000
                                                                        01550000
      ******************************************************************01560000
      * 3000-END-OF-JOB-ROUTINE                                        *01570000
      ******************************************************************01580000
       3000-END-OF-JOB-ROUTINE.                                         01590000
                                                                        01600000
           CLOSE TRN-CTG-SUM-FILE.                                      01610012
                                                                        01620000
           DISPLAY ' '.                                                 01630000
           DISPLAY 'MESSAGES FOR PROGRAM ' PC-PROGRAM-NAME.             01640000
           DISPLAY 'NUMBER OF INPUT RECORDS READ:          '            01650000
                   PA-INPUT-RECORDS-READ.                               01660000
           DISPLAY 'NUMBER OF TRANS/PREP RECORDS WRITTEN:  '            01670000
                   PA-OUTPUT-RECS-WRITTEN.                              01680000
           DISPLAY ' '.                                                 01690000
                                                                        01700000
           PERFORM C600-ISSUE-CLOSE-REQUEST.                            01710000
                                                                        01720000
      ******************************************************************01730000
      * C100-ISSUE-OPEN-REQUEST                                        *01740000
      *     ISSUE AN 'OPEN' REQUEST TO THE TRANSACTION PREPARATION     *01750000
      *     MODULE.                                                    *01760000
      ******************************************************************01770000
       C100-ISSUE-OPEN-REQUEST.                                         01780000
                                                                        01790000
           MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              01800000
           MOVE PC-JOB-NAME            TO DP000-JOB-NAME.               01810000
           MOVE PC-PLAN-NAME           TO DP000-PLAN-NAME.              01820000
                                                                        01830000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         01840000
                                                                        01850000
           MOVE DP000-CKPT-TYPE-CODE   TO PV-CHECKPOINT-TYPE.           01860000
                                                                        01870000
      ******************************************************************01880000
      * C200-READ-UPDATE-INPUT-FILE                                    *01890000
      ******************************************************************01900000
       C200-READ-UPDATE-INPUT-FILE.                                     01910000
                                                                        01920000
           READ TRN-CTG-SUM-FILE                                        01930012
                INTO SS020-TRN-CTG-SUM-RECORD                           01940020
               AT END                                                   01950000
                   SET PS-INPUT-UPDATE-EOF TO TRUE.                     01960000
                                                                        01970000
           IF PS-INPUT-UPDATE-EOF                                       01980000
               CONTINUE                                                 01990000
           ELSE                                                         02000000
               ADD +1 TO PA-INPUT-RECORDS-READ                          02010000
           END-IF.                                                      02020000
                                                                        02030000
      ******************************************************************02040000
      * C300-CKPT-BY-CNTL-BREAK                                        *02050000
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *02060000
      *     TION RECORD "ON" IF THERE IS A CHANGE IN THE STORE         *02070000
      *     NUMBER.  AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *02080000
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *02090000
      *     MODULE.                                                    *02100000
      * NOTE: AT THIS TIME CONTROL/LOGIC BREAK PROCESSING IS IDENTICAL.*02110000
      ******************************************************************02120000
       C300-CKPT-BY-CNTL-BREAK.                                         02130000
                                                                        02140000
           IF SS020-TRN-STR-NBR = PV-HOLD-TRN-STR-NBR                   02150020
               MOVE 'N' TO DP000-CHECKPOINT-IND                         02160000
           ELSE                                                         02170000
               MOVE 'Y' TO DP000-CHECKPOINT-IND                         02180000
               MOVE SS020-TRN-STR-NBR TO PV-HOLD-TRN-STR-NBR            02190020
           END-IF.                                                      02200000
                                                                        02210000
           MOVE PC-TRANS-RECORD-LENGTH   TO DP000-TRANS-REC-LENGTH.     02220004
           MOVE SS020-TRN-CTG-SUM-RECORD TO DP000-TRANS-REC.            02230020
           MOVE PC-TRAN-PREP-FUNC-WRITE  TO DP000-FUNC-CODE.            02240004
           MOVE PC-JOB-NAME              TO DP000-JOB-NAME.             02250004
           MOVE PC-PLAN-NAME             TO DP000-PLAN-NAME.            02260004
                                                                        02270000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02280000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02290000
                                                                        02300000
      ******************************************************************02310000
      * C400-CKPT-BY-LOGICAL-TRANS.                                    *02320000
      *     THIS ROUTINE SETS THE CHECKPOINT INDICATOR ON THE TRANSAC- *02330000
      *     TION RECORD "ON".  THE UPDATE PROGRAM WILL CHECK THE       *02340000
      *     CHECKPOINT FREQUENCY TO KNOW WHETHER A CHECKPOINT SHOULD BE*02350000
      *     TAKEN.   AFTER THE TRANSACTION RECORD HAS BEEN FORMATTED,  *02360000
      *     A WRITE REQUEST IS ISSUED TO THE TRANSACTION PREPARATION   *02370000
      *     MODULE.                                                    *02380000
      ******************************************************************02390000
       C400-CKPT-BY-LOGICAL-TRANS.                                      02400000
                                                                        02410000
           MOVE 'Y'                      TO DP000-CHECKPOINT-IND.       02420006
           MOVE PC-TRANS-RECORD-LENGTH   TO DP000-TRANS-REC-LENGTH.     02430004
           MOVE SS020-TRN-CTG-SUM-RECORD TO DP000-TRANS-REC.            02440020
           MOVE PC-TRAN-PREP-FUNC-WRITE  TO DP000-FUNC-CODE.            02450004
           MOVE PC-JOB-NAME              TO DP000-JOB-NAME.             02460004
           MOVE PC-PLAN-NAME             TO DP000-PLAN-NAME.            02470004
                                                                        02480000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02490000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02500000
                                                                        02510000
      ******************************************************************02520000
      * C500-CKPT-BY-TIME-OR-REQUEST.                                  *02530000
      *     THIS ROUTINE BUILDS THE TRANSACTION RECORD AND ISSUES A    *02540000
      *     WRITE REQUEST TO THE TRANSACTION PREPARATION MODULE IF     *02550000
      *     CHECKPOINTS ARE TAKEN BY TIME INTERVAL (SECONDS) OR        *02560000
      *     REQUESTED BY THE MAINTENANCE PROGRAM.                      *02570000
      ******************************************************************02580000
       C500-CKPT-BY-TIME-OR-REQUEST.                                    02590000
                                                                        02600000
           MOVE 'N'                        TO DP000-CHECKPOINT-IND.     02610000
           MOVE PC-TRANS-RECORD-LENGTH     TO DP000-TRANS-REC-LENGTH.   02620000
           MOVE SS020-TRN-CTG-SUM-RECORD   TO DP000-TRANS-REC.          02630020
           MOVE PC-TRAN-PREP-FUNC-WRITE    TO DP000-FUNC-CODE.          02640000
           MOVE PC-JOB-NAME                TO DP000-JOB-NAME.           02650000
           MOVE PC-PLAN-NAME               TO DP000-PLAN-NAME.          02660000
                                                                        02670000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02680000
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                            02690000
                                                                        02700000
      ******************************************************************02710000
      * C600-ISSUE-CLOSE-REQUEST                                       *02720000
      *     ISSUE A 'CLOSE' REQUEST TO THE TRANSACTION PREPARATION     *02730000
      *     MODULE.                                                    *02740000
      ******************************************************************02750000
       C600-ISSUE-CLOSE-REQUEST.                                        02760000
                                                                        02770000
           MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             02780000
           MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              02790000
           MOVE PC-PLAN-NAME            TO DP000-PLAN-NAME.             02800000
                                                                        02810000
           PERFORM Z900-CALL-TRANS-PREP-MODULE.                         02820000
                                                                        02830000
      ******************************************************************02840000
      *  TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES        *02850000
      ******************************************************************02860000
           COPY DPPD000.                                                02870000
