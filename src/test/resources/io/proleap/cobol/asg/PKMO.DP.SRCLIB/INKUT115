00001 *-----------------------*                                         12/27/93
00002  IDENTIFICATION DIVISION.                                         INKUT115
00003 *-----------------------*                                            LV018
00004  PROGRAM-ID.    INKUT115.                                         INKUT115
00005  AUTHOR.        MIKE FURLICK - STRATAGEM.                         INKUT115
00006  DATE-WRITTEN.  NOVEMBER, 1993.                                   INKUT115
00007                                                                   INKUT115
00008 *---------------------------------------------------------------* INKUT115
00009 *                                                               * INKUT115
00010 *    THIS PROGRAM READS THE CICS ESDS VSAM FILE OF ADJUSTMENTS  * INKUT115
00011 *    AND CREATES A FASHION SALES ADJUSTMENT FLAT FILE.          * INKUT115
00012 *                                                               * INKUT115
00013 *---------------------------------------------------------------* INKUT115
00013 *   05/11/99  -  JULIE SEIDLER                                  * INKUT115
00013 *                ADDED LOGIC TO PASS A 'UNIT-BOOK-IND' TO THE   * INKUT115
00013 *                FASHION SALES UPDATE PROGRAM.                  * INKUT115
00013 *---------------------------------------------------------------* INKUT115
W21732*   01/24/03  -  SUDHARSHAN ADDURI,IGSI    WR # 21732           * INKUT115
W21732*                REPLACED KDC1801 COPYBOOK WITH FSRD035 COPYBOOK* INKUT115
W21732*                FETCHED ITEM NUMBER FROM TSKXREF TABLE TO WRITE* INKUT115
W21732*                INTO OUTPUT FILE.                              * INKUT115
W21732*---------------------------------------------------------------* INKUT115
W26600*   09/09/04  -  S. JACKSON                WR # 26600           * INKUT115
W26600*                STORE NUMBER EXPANSION. MODIFY FOR INWS015 AND * INKUT115
W26600*                RECOMPILE OF FSRD035                           * INKUT115
W26600*---------------------------------------------------------------* INKUT115
00014                                                                   INKUT115
00015  ENVIRONMENT DIVISION.                                            INKUT115
00016  CONFIGURATION SECTION.                                           INKUT115
00017  SOURCE-COMPUTER. IBM-3090.                                       INKUT115
00018  OBJECT-COMPUTER. IBM-3090.                                       INKUT115
00019                                                                   INKUT115
00020  INPUT-OUTPUT SECTION.                                            INKUT115
00021  FILE-CONTROL.                                                    INKUT115
00022                                                                   INKUT115
00023      SELECT INVENTORY-ADJUSTMENTS                                 INKUT115
00024          ORGANIZATION IS SEQUENTIAL                                  CL**6
00025          FILE STATUS IS PV-VSAM-STATUS                               CL**6
00026          ASSIGN TO AS-INP01.                                         CL**8
00027                                                                   INKUT115
00028      SELECT FASHION-ADJUSTMENTS                                   INKUT115
00029          ASSIGN TO OUT01.                                         INKUT115
00030                                                                   INKUT115
00031  DATA DIVISION.                                                   INKUT115
00032  FILE SECTION.                                                    INKUT115
00033                                                                   INKUT115
00034  FD  INVENTORY-ADJUSTMENTS                                        INKUT115
00035      BLOCK CONTAINS 0 CHARACTERS.                                    CL**9
00036                                                                   INKUT115
00037  01  INVENTORY-ADJUSTMENTS-RECORD  PIC X(30).                        CL*14
00038                                                                   INKUT115
00039  FD  FASHION-ADJUSTMENTS                                          INKUT115
00040      RECORDING MODE IS F                                          INKUT115
00041      BLOCK CONTAINS 0 CHARACTERS                                  INKUT115
00042      LABEL RECORDS ARE STANDARD.                                  INKUT115
00043                                                                   INKUT115
W21732 01  FASHION-ADJUSTMENTS-RECORD    PIC X(200).                       CL*12
00045 /                                                                 INKUT115
00046  WORKING-STORAGE SECTION.                                         INKUT115
00047                                                                   INKUT115
00048  01  PS-PROGRAM-CONSTANTS.                                           CL*12
00049      05  PC-DFSMSA70               PIC X(08)  VALUE 'DFSMSA70'.      CL*12
W21732     05  PC-MAX-RETRIES            PIC S9(03) VALUE +3                    
W21732                                              COMP SYNC.                  
00050                                                                      CL*12
00051  01  PS-PROGRAM-SWITCHES.                                            CL*12
00052      05  PS-DATA-SW                PIC X      VALUE 'N'.             CL*12
00053          88  PS-END-OF-DATA                   VALUE 'Y'.          INKUT115
00054          88  PS-NOT-END-OF-DATA               VALUE 'N'.          INKUT115
00055                                                                   INKUT115
00056  01  PV-PROGRAM-VARIABLES.                                        INKUT115
00057      05  PV-WRITE-COUNT            PIC S9(9)  VALUE ZERO          INKUT115
00058                                               COMP-3.             INKUT115
00059      05  PV-READ-COUNT             PIC S9(9)  VALUE ZERO          INKUT115
00060                                               COMP-3.             INKUT115
W21732     05  PV-RETRY-COUNT            PIC S9(04) VALUE +0                    
W21732                                              COMP.                       
00061      05  PV-VSAM-STATUS            PIC X(02)  VALUE SPACE.           CL**6
00062          88  PV-GOOD-STATUS                   VALUE '  ' '00'.       CL*15
00063          88  PV-VERIFIED                      VALUE '97'.            CL*10
W21732     05  PV-PROGRAM-NAME           PIC X(08)  VALUE 'INKUT115'.           
00064      05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.        INKUT115
W21732     05  PV-DB2-OPERATION-ATTEMPTED PIC X(30)   VALUE SPACE.              
00065      05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.        INKUT115
W26600     05  PV-STORE-NUMBER           PIC 9(04).                        CL*12
W21732     05  PV-WORK-QTY               PIC 9(9).                         CL*17
W21732     05  PV-SKU-NBR                PIC 9(8).                         CL*17
00072                                                                   INKUT115
00073  01  ABEND-CODE                    PIC S9(4)  VALUE ZERO          INKUT115
00074                                               COMP.               INKUT115
00075      88  ABEND-4001                           VALUE 4001.         INKUT115
00076      88  ABEND-4002                           VALUE 4002.         INKUT115
00077      88  ABEND-4003                           VALUE 4003.         INKUT115
00078      88  ABEND-4004                           VALUE 4004.         INKUT115
00079      88  ABEND-4005                           VALUE 4005.         INKUT115
00080      88  ABEND-4006                           VALUE 4006.         INKUT115
00081      88  ABEND-4013                           VALUE 4013.         INKUT115
00082 /                                                                 INKUT115
00083 *                                                                 INKUT115
00084 *    INVENTORY ADJUSTMENT DATA STRUCTURE                          INKUT115
00085 *                                                                 INKUT115
00086      COPY INWS015.                                                   CL*18
00087 *                                                                 INKUT115
00088 *    FASHION SALES ADJUSTMENT DATA STRUCTURE                      INKUT115
00089 *                                                                 INKUT115
W21732     COPY FSRD035.                                                   CL*12
W21732*                                                                         
W21732*    SQL ABEND ROUTINE WORKING STORAGE                                    
W21732*                                                                         
W21732                                                                          
W21732     COPY DPWS004.                                                        
W21732                                                                          
00091 /                                                                 INKUT115
W21732*****************************************************************         
W21732* DB2 TABLE DCLGENS NEEDED FOR THIS PROGRAM.                              
W21732*****************************************************************         
W21732                                                                          
W21732     EXEC SQL                                                             
W21732         INCLUDE TSKXREF                                                  
W21732     END-EXEC.                                                            
W21732                                                                          
W21732     EXEC SQL                                                             
W21732         INCLUDE SQLCA                                                    
W21732     END-EXEC.                                                            
W21732                                                                          
W21732      EJECT.                                                              
W21732*                                                                         
00092  PROCEDURE DIVISION.                                              INKUT115
00093                                                                   INKUT115
00094      PERFORM 1000-MAINLINE.                                       INKUT115
00095      GOBACK.                                                      INKUT115
00096                                                                   INKUT115
00097  1000-MAINLINE.                                                   INKUT115
00098                                                                   INKUT115
00099      PERFORM 2000-OPEN-FILES.                                     INKUT115
00100      PERFORM 3000-PROCESS-ADJUSTMENTS                                CL**2
00101              UNTIL PS-END-OF-DATA.                                INKUT115
00102                                                                   INKUT115
00103      PERFORM 4000-CLOSE-FILES.                                    INKUT115
00104      PERFORM 9000-DISPLAY-TOTALS.                                 INKUT115
00105 /                                                                 INKUT115
00106 *---------------------------------------------------------------* INKUT115
00107 *                                                               * INKUT115
00108 *    THIS ROUTINE SIMPLY OPENS THE FILES.                       * INKUT115
00109 *                                                               * INKUT115
00110 *---------------------------------------------------------------* INKUT115
00111                                                                   INKUT115
00112  2000-OPEN-FILES.                                                    CL**2
00113                                                                   INKUT115
00114      OPEN INPUT  INVENTORY-ADJUSTMENTS                            INKUT115
00115           OUTPUT FASHION-ADJUSTMENTS.                             INKUT115
00116                                                                      CL**6
00117      IF (PV-GOOD-STATUS                                              CL*10
00118      OR  PV-VERIFIED)                                                CL*10
00119          CONTINUE                                                    CL*10
00120      ELSE                                                            CL*10
00121          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**6
00122          DISPLAY '*    *** INKUT115 ABEND ***     *'                 CL**6
00123          DISPLAY '*                               *'                 CL**6
00124          DISPLAY '*  INVALID STATUS ON CICS VSAM  *'                 CL**6
00125          DISPLAY '*  ADJUSTMENT DATASET - OPEN    *'                 CL**6
00126          DISPLAY '*                               *'                 CL**6
00127          DISPLAY '*  STATUS = '                                      CL**6
00128                   PV-VSAM-STATUS                                     CL*11
00129                  '                   *'                              CL*11
00130          DISPLAY '*                               *'                 CL**7
00131          DISPLAY '* * * * * * * * * * * * * * * * *'                 CL**6
00132          SET ABEND-4002           TO  TRUE                           CL**6
00133          CALL 'ILBOABN0'       USING  ABEND-CODE                     CL**7
00134      END-IF.                                                         CL**6
00135                                                                      CL**6
00136      PERFORM 3100-READ-INVENTORY.                                    CL**3
W21732     MOVE SPACES                 TO  FS035-ADJUSTMENT-RECORD.        CL*16
00139 /                                                                 INKUT115
00140 *---------------------------------------------------------------* INKUT115
00141 *                                                               * INKUT115
00142 *    PROCESS THE INVENTORY ADJUSTMENTS FLAT FILE.               * INKUT115
00143 *                                                               * INKUT115
00144 *---------------------------------------------------------------* INKUT115
00145                                                                   INKUT115
00146  3000-PROCESS-ADJUSTMENTS.                                           CL**2
00147                                                                   INKUT115
00148      ADD 1                 TO  PV-READ-COUNT.                     INKUT115
00149      IF (IN015-STORE-NBR      NUMERIC                                CL**5
00150      AND IN015-SKU-NBR        NUMERIC                                CL**5
00151      AND IN015-ADJUSTMENT-QTY NUMERIC)                               CL**5
00152          PERFORM 3200-WRITE-FASHION                                  CL**4
00153          ADD 1             TO  PV-WRITE-COUNT                        CL**4
00154      ELSE                                                            CL**4
00155          DISPLAY '* INVALID ADJUSTMENT RECORD ==> '                  CL**4
00156                  IN015-ADJUSTMENT-RECORD                             CL**4
00157          DISPLAY '*                               '                  CL**4
00158                  '....5...10...15...20...25...30...35'               CL*16
00159          DISPLAY '*'                                                 CL**4
00160      END-IF.                                                         CL**4
00161      PERFORM 3100-READ-INVENTORY.                                    CL**2
00162                                                                   INKUT115
00163 *--------------------------------------------------------------*  INKUT115
00164 *                                                              *  INKUT115
00165 *    HERE WE READ THE INVENTORY ADJUSTMENT FILE.               *  INKUT115
00166 *                                                              *  INKUT115
00167 *--------------------------------------------------------------*  INKUT115
00168                                                                   INKUT115
00169  3100-READ-INVENTORY.                                                CL**2
00170                                                                   INKUT115
00171      READ INVENTORY-ADJUSTMENTS                                      CL**2
00172           INTO IN015-ADJUSTMENT-RECORD                            INKUT115
00173           AT END SET PS-END-OF-DATA TO TRUE                       INKUT115
00174      END-READ.                                                    INKUT115
00175                                                                   INKUT115
00176 *--------------------------------------------------------------*  INKUT115
00177 *                                                              *  INKUT115
00178 *    HERE WE WRITE THE FASHION SALES ADJUSTMENT RECORD.        *  INKUT115
00179 *                                                              *  INKUT115
00180 *--------------------------------------------------------------*  INKUT115
00181                                                                   INKUT115
00182  3200-WRITE-FASHION.                                                 CL**2
00183                                                                   INKUT115
W21732     IF IN015-SKU-NBR (6:3) = '000' OR '999'                              
W21732         MOVE ZEROES             TO FS035-ITM-NBR                         
W21732     ELSE                                                                 
W21732         PERFORM 3300-GET-ITEM-NBR                                        
W21732     END-IF.                                                              
W21732                                                                     CL*12
W21732     MOVE 'INKUT115'             TO  FS035-ADJ-SRC-PGM-NM.           CL*12
W21732     MOVE IN015-STORE-NBR        TO  PV-STORE-NUMBER.                CL*12
W21732     MOVE PV-STORE-NUMBER        TO  FS035-LOC-NBR.                  CL*12
W21732     MOVE IN015-ADJUSTMENT-TYPE  TO  FS035-ADJ-TYP.                  CL*12
00188                                                                   INKUT115
W21732     MOVE IN015-ADJUSTMENT-SIGN  TO  FS035-ACTN-CDE.                 CL*12
00190      MOVE IN015-ADJUSTMENT-QTY   TO  PV-WORK-QTY.                    CL*17
W21732     MOVE PV-WORK-QTY            TO  FS035-ADJ-QTY.                  CL*17
W21732     MOVE 'INOL'                 TO  FS035-BTCH-NBR.                 CL*12
W21732     MOVE IN015-UNIT-BOOK-IND    TO  FS035-UNT-BKG-IND.              CL*17
00193                                                                   INKUT115
00194      WRITE FASHION-ADJUSTMENTS-RECORD                                CL**2
W21732           FROM FS035-ADJUSTMENT-RECORD.                             CL*13
00196                                                                   INKUT115
W21732*--------------------------------------------------------------*  INKUT115
W21732*                                                              *  INKUT115
W21732*    GET THE ITEM NUMBER FROM TSKXREF TABLE.                   *  INKUT115
W21732*                                                              *  INKUT115
W21732*--------------------------------------------------------------*  INKUT115
W21732 3300-GET-ITEM-NBR.                                                       
W21732                                                                          
W21732         MOVE IN015-SKU-NBR        TO PV-SKU-NBR.                         
W21732         MOVE PV-SKU-NBR           TO SKXREF-SKU.                         
W21732                                                                          
W21732     PERFORM                                                              
W21732         WITH TEST AFTER                                                  
W21732         VARYING PV-RETRY-COUNT                                           
W21732         FROM 1 BY 1                                                      
W21732         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
W21732                   OR (SQLCODE = ZERO)                                    
W21732                   OR (SQLCODE = +100))                                   
W21732         EXEC SQL                                                         
W21732           SELECT ITM_NBR                                                 
W21732             INTO :SKXREF-ITM-NBR                                         
W21732           FROM TSKXREF                                                   
W21732           WHERE SKU    = :SKXREF-SKU                                     
W21732         END-EXEC                                                         
W21732                                                                          
W21732         EVALUATE TRUE                                                    
W21732             WHEN SQLCODE = ZERO                                          
W21732                MOVE SKXREF-ITM-NBR  TO FS035-ITM-NBR                     
W21732             WHEN SQLCODE = +100                                          
W21732                MOVE ZEROES          TO FS035-ITM-NBR                     
W21732             WHEN SQLCODE = -911                                          
W21732             WHEN SQLCODE = -913                                          
W21732                CONTINUE                                                  
W21732             WHEN SQLWARN0 NOT = SPACES                                   
W21732             WHEN SQLCODE < ZERO                                          
W21732             WHEN SQLCODE > ZERO                                          
W21732                DISPLAY 'SKU  :  ' SKXREF-SKU                             
W21732                MOVE '3300-GET-ITEM-NBR'                                  
W21732                                   TO PV-PARAGRAPH-NAME                   
W21732                MOVE 'SELECT A ROW ON THE TSKXREF TABLE'                  
W21732                                   TO PV-DB2-OPERATION-ATTEMPTED          
W21732                PERFORM 9999-SQL-ABEND                                    
W21732          END-EVALUATE                                                    
W21732      END-PERFORM.                                                        
W21732                                                                          
W21732      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                 
W21732                DISPLAY 'SKU  :  ' SKXREF-SKU                             
W21732          MOVE '3300-GET-ITEM-NBR' TO PV-PARAGRAPH-NAME                   
W21732                                                                          
W21732          MOVE 'MAX RETRIES EXCEEDED ON TSKXREF TABLE'                    
W21732                                   TO PV-DB2-OPERATION-ATTEMPTED          
W21732          PERFORM 9999-SQL-ABEND                                          
W21732      END-IF.                                                             
W21732                                                                          
W21732*                                                                         
00197 *--------------------------------------------------------------*  INKUT115
00198 *                                                              *  INKUT115
00199 *    CLOSE THE FLAT FILE.                                      *  INKUT115
00200 *                                                              *  INKUT115
00201 *--------------------------------------------------------------*  INKUT115
00202                                                                   INKUT115
00203  4000-CLOSE-FILES.                                                   CL**2
00204                                                                   INKUT115
00205      CLOSE INVENTORY-ADJUSTMENTS                                  INKUT115
00206            FASHION-ADJUSTMENTS.                                      CL**2
00207                                                                   INKUT115
00208 *--------------------------------------------------------------*  INKUT115
00209 *                                                              *  INKUT115
00210 *    DISPLAY THE NUMBER OF RECORDS READ/WRITTEN.               *  INKUT115
00211 *                                                              *  INKUT115
00212 *--------------------------------------------------------------*  INKUT115
00213                                                                   INKUT115
00214  9000-DISPLAY-TOTALS.                                             INKUT115
00215                                                                   INKUT115
00216      DISPLAY ' '.                                                 INKUT115
00217      DISPLAY '* * * * * * * * * * * * * * * * * * *'.             INKUT115
00218      DISPLAY '*                                   *'.             INKUT115
00219      DISPLAY '*       **INKUT115 COUNTS**         *'.             INKUT115
00220      DISPLAY '*  '                                                INKUT115
00221              PV-READ-COUNT                                        INKUT115
00222              ' INVENTORY RECORDS READ *'.                         INKUT115
00223      DISPLAY '*  '                                                INKUT115
00224              PV-WRITE-COUNT                                       INKUT115
00225              ' FASHION RECORDS WRITTEN*'.                         INKUT115
00226      DISPLAY '*                                   *'.             INKUT115
00227      DISPLAY '* * * * * * * * * * * * * * * * * * *'.             INKUT115
W21732******************************************************************        
W21732*  STANDARD DB2 ERROR ABEND ROUTINE                                       
W21732*  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
W21732******************************************************************        
W21732 9999-SQL-ABEND.                                                          
W21732                                                                          
W21732     SET  ABEND-4013             TO TRUE.                                 
W21732     DISPLAY '**  ABEND     **'.                                          
W21732     DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
W21732     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
W21732     DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
W21732                                                                          
W21732*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
W21732*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
W21732                                                                          
W21732     COPY DPPD004.                                                        
W21732                                                                          
W21732     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
W21732                                                                          
