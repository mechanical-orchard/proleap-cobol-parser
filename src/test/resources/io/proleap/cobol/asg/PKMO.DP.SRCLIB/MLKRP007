00001  IDENTIFICATION DIVISION.                                         07/08/94
00002  PROGRAM-ID.             MLKRP007.                                MLKRP007
00003  AUTHOR.                 ROBERT KERHIN.                              LV004
00004  INSTALLATION.           KOHLS.                                   MLKRP007
00005  DATE-WRITTEN.           08/17/87.                                MLKRP007
00006  DATE-COMPILED.          08/17/87.                                MLKRP007
00007 ******************************************************************MLKRP007
00008 *    THIS PROGRAM PRODUCES A REPORT OF ALL TRANSFERS FROM THE     MLKRP007
00009 *    DISTRIBUTION CENTER WHICH IS VERY SIMILAR TO THE INTERSTORE  MLKRP007
00010 *    TRANSFER REPORT MLKRP005.  THE PROGRAMS MAIN PURPOSE IS TO   MLKRP007
00011 *    IDENTIFY MISSING AND DUPLICATE DOCUMENTS THAT FLOW INTO THE  MLKRP007
00012 *    WEEKLY UPDATE. THIS REPORT SHOULD HELP REDUCE SOME PAPERWORK MLKRP007
00013 *    SHORTAGE BY GIVING THE SHORTAGE CONTROL GROUP DOCUMENTS AND  MLKRP007
00014 *    BATCH NUMBERS TO RESEARCH POSSIBLE ERRORS.                   MLKRP007
00015 *    MODIFIED   6/29/94    WR:2836                                   CL**2
00016 *        STEVE FLUNKER - REDUCED LINES PER PAGE TO 60                CL**4
00017 *    MODIFIED   10/4/88    PROJECT:    P10200                        CL**2
00018 *        BRUCE BLANK-CPU                                             CL**2
00019 *        EXPANDED PAGE COUNT FROM 3 BYTES TO 5 BYTES              MLKRP007
00017 *    MODIFIED   03/12/02   PROJECT: 23511                            CL**2
00018 *        CAROL WOJCIECHOWSKI                                         CL**2
00019 *        EXPANDED FIELDS FOR PRICE AND QUANTITY                   MLKRP007
00020 ******************************************************************MLKRP007
00021                                                                   MLKRP007
00022  ENVIRONMENT DIVISION.                                            MLKRP007
00023                                                                   MLKRP007
00024  CONFIGURATION SECTION.                                           MLKRP007
00025  SOURCE-COMPUTER.        IBM-4381.                                MLKRP007
00026  OBJECT-COMPUTER.        IBM-4381.                                MLKRP007
00027                                                                   MLKRP007
00028  INPUT-OUTPUT SECTION.                                            MLKRP007
00029  FILE-CONTROL.                                                    MLKRP007
00030      SELECT TRANSFER-INPUT-FILE ASSIGN TO MLRD003I.               MLKRP007
00031      SELECT PRINT-FILE          ASSIGN TO MLRP001O.               MLKRP007
00032  DATA DIVISION.                                                   MLKRP007
00033  FILE SECTION.                                                    MLKRP007
00034                                                                   MLKRP007
00035  FD  TRANSFER-INPUT-FILE                                          MLKRP007
00036      LABEL RECORDS ARE STANDARD                                   MLKRP007
00037      BLOCK CONTAINS 0 RECORDS                                     MLKRP007
00038      RECORD CONTAINS 125 CHARACTERS                               MLKRP007
00039      DATA RECORD IS TRANSFER-REC.                                 MLKRP007
00040  01  TRANSFER-REC        PIC X(125).                              MLKRP007
00041                                                                   MLKRP007
00042  FD  PRINT-FILE                                                   MLKRP007
00043      LABEL RECORDS ARE STANDARD                                   MLKRP007
00044      RECORD CONTAINS 132 CHARACTERS                               MLKRP007
00045      DATA RECORD IS PRINT-REC.                                    MLKRP007
00046  01  PRINT-REC           PIC X(132).                              MLKRP007
00047                                                                   MLKRP007
00048  WORKING-STORAGE SECTION.                                         MLKRP007
00049  01  MV-MISC-VARIABLES.                                           MLKRP007
00051      05  ABEND-CODE             PIC 9(02) COMP   VALUE ZERO.      MLKRP007
00052      05  MV-BLANK-LINE          PIC X(132)       VALUE SPACE.     MLKRP007
00053      05  MV-PRINT-LINE          PIC X(132)       VALUE SPACE.     MLKRP007
00054      05  MV-REPORT-NUMBER       PIC X(08) VALUE 'MLKRP007'.       MLKRP007
00055      05  MV-PAGE-COUNTER        PIC 9(05) COMP-3 VALUE ZERO.      MLKRP007
00056      05  MV-LINE-COUNTER        PIC 9(02) COMP-3 VALUE 70.        MLKRP007
00057      05  MV-PREVIOUS-BATCH      PIC 9(03)        VALUE ZERO.      MLKRP007
00058      05  MV-PREVIOUS-STORE      PIC 9(05)        VALUE ZERO.      MLKRP007
00059      05  MV-PREVIOUS-DOCUMENT   PIC 9(06)        VALUE ZERO.      MLKRP007
00060      05  MV-CURRENT-DOCUMENT    PIC 9(06)        VALUE ZERO.      MLKRP007
00061      05  MV-BETWEEN-DOCUMENTS   PIC 9(06)        VALUE ZERO.      MLKRP007
00062      05  MV-MISSING-DOCUMENT    PIC 9(06)        VALUE ZERO.      MLKRP007
23511      05  MV-EXTENDED-RETAIL     PIC S9(13)V99    VALUE ZERO.      MLKRP007
00064      05  MV-LAST-MISSING-DOCUMENT PIC 9(06)      VALUE ZERO.      MLKRP007
00065      05  MV-STRIPPED-STORE-NUMBER.                                MLKRP007
00066          10  MV-STORE-FIRST-NUMB  PIC 9(01)      VALUE ZERO.      MLKRP007
00067          10  MV-SHORT-STORE-NUMB  PIC 9(03)      VALUE ZERO.      MLKRP007
00068          10  MV-STORE-CHECK-DIGIT PIC 9(01)      VALUE ZERO.      MLKRP007
00069                                                                   MLKRP007
00070  01  TF-TRANSFER-FILE-SWITCH    PIC X(03)        VALUE 'NO'.      MLKRP007
00071      88  TF-END-OF-TRANSFER-FILE                 VALUE 'YES'.     MLKRP007
00072      88  TF-NOT-THE-END                          VALUE 'NO'.      MLKRP007
00073                                                                   MLKRP007
00074  01  VB-VERIFY-BATCH-TYPE       PIC X(03)        VALUE 'YES'.     MLKRP007
00075      88  VB-VALID-BATCH-NUMBER                   VALUE 'YES'.     MLKRP007
00076                                                                   MLKRP007
00077  01  MD-MISSING-DOCUMENT-SWITCH PIC X(03)        VALUE 'NO'.      MLKRP007
00078      88  MD-ALL-MISSING-WERE-PRINTED             VALUE 'YES'.     MLKRP007
00079      88  MD-MORE-MISSING-LEFT                    VALUE 'NO'.      MLKRP007
00080                                                                   MLKRP007
00081  01  HL-HEADING-LINE-ONE.                                         MLKRP007
00082      05  FILLER                 PIC X(09)        VALUE SPACE.     MLKRP007
23511      05  FILLER                 PIC X(38)        VALUE            MLKRP007
23511             'DOCUMENT      BATCH          DPT/CLS  '.             MLKRP007
00085      05  FILLER                 PIC X(19)        VALUE SPACE.     MLKRP007
23511      05  FILLER                 PIC X(26)        VALUE            MLKRP007
23511             ' # OF         UNIT        '.                         MLKRP007
00088      05  FILLER                 PIC X(22)        VALUE            MLKRP007
23511             '     EXTENDED    KOHLS'.                             MLKRP007
23511      05  FILLER                 PIC X(18)        VALUE SPACE.     MLKRP007
00090                                                                   MLKRP007
00091  01  HL-HEADING-LINE-TWO.                                         MLKRP007
00092      05  FILLER                 PIC X(10)        VALUE SPACE.     MLKRP007
00093      05  FILLER                 PIC X(43)        VALUE            MLKRP007
00094             'NUMBER       NUMBER          NUMBER        '.        MLKRP007
23511      05  FILLER                 PIC X(27)        VALUE            MLKRP007
23511             ' DATE         UNITS        '.                        MLKRP007
00097      05  FILLER                 PIC X(34)        VALUE            MLKRP007
23511             'PRICE             RETAIL     MONTH'.                 MLKRP007
23511      05  FILLER                 PIC X(18)        VALUE SPACE.     MLKRP007
00099                                                                   MLKRP007
00100  01  HL-HEADING-LINE-THREE.                                       MLKRP007
00101      05  FILLER                 PIC X(50)        VALUE SPACE.     MLKRP007
00102      05  FILLER                 PIC X(33)        VALUE            MLKRP007
00103             'DISTRIBUTION CENTER TRANSFER LIST'.                  MLKRP007
00104                                                                   MLKRP007
00105  01  DL-DETAIL-LINE-ONE.                                          MLKRP007
00106      05  DL-MESSAGE-LINE        PIC X(10)        VALUE SPACE.     MLKRP007
00107      05  DL-DOCUMENT-NUMBER     PIC Z99999       VALUE ZERO.      MLKRP007
00108      05  FILLER                 PIC X(08)        VALUE SPACE.     MLKRP007
00109      05  DL-BATCH-NUMBER        PIC 999          VALUE ZERO.      MLKRP007
00110      05  FILLER                 PIC X(12)        VALUE SPACE.     MLKRP007
00111      05  DL-DEPT-NUMBER         PIC 9(03)        VALUE ZERO.      MLKRP007
00112      05  FILLER                 PIC X(01)        VALUE '-'.       MLKRP007
00113      05  DL-CLASS-NUMBER        PIC 9(02)        VALUE ZERO.      MLKRP007
00114      05  FILLER                 PIC X(08)        VALUE SPACE.     MLKRP007
00115      05  DL-TRANSFER-DATE-MM    PIC Z9           VALUE ZERO.      MLKRP007
00116      05  FILLER                 PIC X(01)        VALUE '/'.       MLKRP007
00117      05  DL-TRANSFER-DATE-DD    PIC 99           VALUE ZERO.      MLKRP007
23511      05  FILLER                 PIC X(05)        VALUE SPACE.     MLKRP007
23511      05  DL-NUMBER-OF-UNITS     PIC ZZZZZZZZ9-   VALUE ZERO.      MLKRP007
23511      05  FILLER                 PIC X(04)        VALUE SPACE.     MLKRP007
00121      05  DL-UNIT-PRICE          PIC ZZZZ9.99-    VALUE ZERO.      MLKRP007
23511      05  FILLER                 PIC X(03)        VALUE SPACE.     MLKRP007
23511      05  DL-EXTENDED-RETAIL     PIC ZZZZZZZZZZZZ9.99-                     
23511                                                  VALUE ZERO.              
23511      05  FILLER                 PIC X(04)        VALUE SPACE.     MLKRP007
00125      05  DL-KOHLS-MONTH         PIC Z9           VALUE ZERO.      MLKRP007
00126                                                                   MLKRP007
00127  01  MM-MULTIPLE-DOCUMENTS-LINE.                                  MLKRP007
00128      05  FILLER                 PIC X(36)        VALUE            MLKRP007
00129             '** ERROR **  MISSING DOCUMENTS FROM '.               MLKRP007
00130      05  MM-FIRST-DOCUMENT      PIC Z99999.                       MLKRP007
00131      05  FILLER                 PIC X(04)        VALUE            MLKRP007
00132             ' TO '.                                               MLKRP007
00133      05  MM-LAST-DOCUMENT       PIC Z99999.                       MLKRP007
00134      05  FILLER                 PIC X(80)        VALUE SPACE.     MLKRP007
00135                                                                   MLKRP007
00136  01  SM-SINGLE-DOCUMENT-LINE.                                     MLKRP007
00137      05  FILLER                 PIC X(32)        VALUE            MLKRP007
00138             '** ERROR **  MISSING DOCUMENT # '.                   MLKRP007
00139      05  SM-SINGLE-DOC          PIC Z99999.                       MLKRP007
00140      05  FILLER                 PIC X(94)        VALUE SPACE.     MLKRP007
00141                                                                   MLKRP007
00142  01  SD-SYSTEM-DATE.                                              MLKRP007
00143      05  SD-YY              PIC 9(02).                            MLKRP007
00144      05  SD-MM              PIC 9(02).                            MLKRP007
00145      05  SD-DD              PIC 9(02).                            MLKRP007
00146  01  ST-SYSTEM-TIME.                                              MLKRP007
00147      05  ST-HH              PIC 9(02).                            MLKRP007
00148      05  ST-MM              PIC 9(02).                            MLKRP007
00149      05  ST-SS              PIC 9(02).                            MLKRP007
00150                                                                   MLKRP007
00151                                                                   MLKRP007
00152      COPY MLRD003I.                                                  CL**3
00153      COPY DPWS132O.                                                  CL**3
00154 ******************************************************************MLKRP007
00155  PROCEDURE DIVISION.                                              MLKRP007
00156 ******************************************************************MLKRP007
00157  A000-MAIN-LOOP.                                                  MLKRP007
00158      OPEN INPUT TRANSFER-INPUT-FILE.                              MLKRP007
00159      OPEN OUTPUT PRINT-FILE.                                      MLKRP007
00161      MOVE MV-REPORT-NUMBER TO DP132O-PROGRAM-NAME.                MLKRP007
00162      MOVE 1 TO DP132O-REPORT-NUMBER.                              MLKRP007
00163      ACCEPT SD-SYSTEM-DATE FROM DATE.                             MLKRP007
00164      MOVE SD-MM TO DP132O-RUN-MONTH.                              MLKRP007
00165      MOVE SD-DD TO DP132O-RUN-DAY.                                MLKRP007
00166      MOVE SD-YY TO DP132O-RUN-YEAR.                               MLKRP007
00167      ACCEPT ST-SYSTEM-TIME FROM TIME.                             MLKRP007
00168      MOVE ST-HH TO DP132O-RUN-HOUR.                               MLKRP007
00169      MOVE ST-MM TO DP132O-RUN-MINUTE.                             MLKRP007
00170      PERFORM Z100-PRINT-HEADINGS.                                 MLKRP007
00171      PERFORM B010-READ-TRANSACTION                                MLKRP007
00172      IF TF-END-OF-TRANSFER-FILE                                   MLKRP007
00173          PERFORM A010-END-JOB.                                    MLKRP007
00174      MOVE ML003I-DOCUMENT-NUMBER TO MV-CURRENT-DOCUMENT.          MLKRP007
00175      SUBTRACT 1 FROM MV-CURRENT-DOCUMENT GIVING                   MLKRP007
00176          MV-PREVIOUS-DOCUMENT.                                    MLKRP007
00177      PERFORM B000-PROCESS-TRANSFERS                               MLKRP007
00178         UNTIL TF-END-OF-TRANSFER-FILE.                            MLKRP007
00179  A000-END.                                                        MLKRP007
00180  A010-END-JOB.                                                    MLKRP007
00181      PERFORM Z200-PRINT-END-NOTICE.                               MLKRP007
00182      CLOSE TRANSFER-INPUT-FILE                                    MLKRP007
00183             PRINT-FILE.                                           MLKRP007
00184      STOP RUN.                                                    MLKRP007
00185                                                                   MLKRP007
00186 ******************************************************************MLKRP007
00187 *   THIS PARAGRAPH IDENTIFIES MISSING AND DUPLICATE DOCUMENTS     MLKRP007
00188 *   BY KEEPING TRACK OF THE PREVIOUS AND CURRENT DOCUMENT AND     MLKRP007
00189 *   BATCH NUMBERS.  A DUPLICATE DOCUMENT NUMBER IS DEFINED AS     MLKRP007
00190 *   A DOCUMENT NUMBER THAT APPEARS IN MORE THAN ONE BATCH.        MLKRP007
00191 ******************************************************************MLKRP007
00192  B000-PROCESS-TRANSFERS.                                          MLKRP007
00193      MOVE ML003I-DOCUMENT-NUMBER TO DL-DOCUMENT-NUMBER.           MLKRP007
00194      MOVE ML003I-BATCH-NUMBER TO DL-BATCH-NUMBER.                 MLKRP007
00195      MOVE ML003I-KOHLS-MONTH TO DL-KOHLS-MONTH.                   MLKRP007
00196      MOVE TRANSFER-MM TO DL-TRANSFER-DATE-MM.                     MLKRP007
00197      MOVE TRANSFER-DD TO DL-TRANSFER-DATE-DD.                     MLKRP007
00198      MOVE ML003I-DEPARTMENT-NUMBER TO DL-DEPT-NUMBER.             MLKRP007
00199      MOVE ML003I-CLASS-NUMBER TO DL-CLASS-NUMBER.                 MLKRP007
00200      MOVE ML003I-TRANSFER-UNITS TO DL-NUMBER-OF-UNITS.            MLKRP007
00201      MOVE ML003I-TRANSFER-RETAIL TO DL-UNIT-PRICE.                MLKRP007
00202      COMPUTE MV-EXTENDED-RETAIL ROUNDED =                         MLKRP007
00203          ML003I-TRANSFER-UNITS *                                  MLKRP007
00204              ML003I-TRANSFER-RETAIL.                              MLKRP007
00205      MOVE MV-EXTENDED-RETAIL TO DL-EXTENDED-RETAIL.               MLKRP007
00206      SUBTRACT MV-PREVIOUS-DOCUMENT FROM MV-CURRENT-DOCUMENT       MLKRP007
00207          GIVING MV-BETWEEN-DOCUMENTS.                             MLKRP007
00208      IF MV-BETWEEN-DOCUMENTS = 0                                  MLKRP007
00209          IF ML003I-BATCH-NUMBER = MV-PREVIOUS-BATCH               MLKRP007
00210             MOVE DL-DETAIL-LINE-ONE TO MV-PRINT-LINE              MLKRP007
00211              PERFORM Z110-PRINT-A-LINE                            MLKRP007
00212          ELSE                                                     MLKRP007
00213              MOVE 'DUPLICATE ' TO DL-MESSAGE-LINE                 MLKRP007
00214              MOVE DL-DETAIL-LINE-ONE TO MV-PRINT-LINE             MLKRP007
00215              PERFORM Z110-PRINT-A-LINE                            MLKRP007
00216      ELSE                                                         MLKRP007
00217          IF MV-BETWEEN-DOCUMENTS = 1                              MLKRP007
00218              MOVE DL-DETAIL-LINE-ONE TO MV-PRINT-LINE             MLKRP007
00219              PERFORM Z110-PRINT-A-LINE                            MLKRP007
00220          ELSE                                                     MLKRP007
00221              MOVE MV-PREVIOUS-DOCUMENT TO MV-MISSING-DOCUMENT     MLKRP007
00222              PERFORM C000-PROCESS-MISSING-DOCUMENTS               MLKRP007
00223                  UNTIL MD-ALL-MISSING-WERE-PRINTED                MLKRP007
00224              MOVE 'NO' TO MD-MISSING-DOCUMENT-SWITCH              MLKRP007
00225              MOVE DL-DETAIL-LINE-ONE TO MV-PRINT-LINE             MLKRP007
00226              PERFORM Z110-PRINT-A-LINE.                           MLKRP007
00227      MOVE SPACES TO DL-MESSAGE-LINE.                              MLKRP007
00228      MOVE MV-CURRENT-DOCUMENT TO MV-PREVIOUS-DOCUMENT.            MLKRP007
00229      MOVE ML003I-BATCH-NUMBER TO MV-PREVIOUS-BATCH.               MLKRP007
00230      PERFORM B010-READ-TRANSACTION.                               MLKRP007
00231      MOVE ML003I-DOCUMENT-NUMBER TO MV-CURRENT-DOCUMENT.          MLKRP007
00232  B000-END.                                                        MLKRP007
00233                                                                   MLKRP007
00234  B010-READ-TRANSACTION.                                           MLKRP007
00235      READ TRANSFER-INPUT-FILE INTO ML003I-TRANSFER-RECORD         MLKRP007
00236          AT END                                                   MLKRP007
00237              MOVE 'YES' TO TF-TRANSFER-FILE-SWITCH.               MLKRP007
00238  B010-END.                                                        MLKRP007
00239                                                                   MLKRP007
00240 ******************************************************************MLKRP007
00241 *    THIS PARAGRAPH IS PERFORMED WHEN THE NUMBER OF MISSING       MLKRP007
00242 *    DOCUMENTS IS GREATER THAN ONE.  IF THE NUMBER IS GREATER     MLKRP007
00243 *    THAN TEN THEN A RANGE OF NUMBERS IS PRINTED INSTEAD OF ONE   MLKRP007
00244 *    LINE PER DOCUMENT.                                           MLKRP007
00245 ******************************************************************MLKRP007
00246  C000-PROCESS-MISSING-DOCUMENTS.                                  MLKRP007
00247      ADD 1 MV-PREVIOUS-DOCUMENT GIVING MV-PREVIOUS-DOCUMENT.      MLKRP007
00248      IF MV-BETWEEN-DOCUMENTS > 10                                 MLKRP007
00249          SUBTRACT 1 FROM MV-CURRENT-DOCUMENT GIVING               MLKRP007
00250              MV-LAST-MISSING-DOCUMENT                             MLKRP007
00251          MOVE MV-PREVIOUS-DOCUMENT TO MM-FIRST-DOCUMENT           MLKRP007
00252          MOVE MV-LAST-MISSING-DOCUMENT TO MM-LAST-DOCUMENT        MLKRP007
00253          MOVE MM-MULTIPLE-DOCUMENTS-LINE TO MV-PRINT-LINE         MLKRP007
00254          PERFORM Z110-PRINT-A-LINE                                MLKRP007
00255          MOVE 'YES' TO MD-MISSING-DOCUMENT-SWITCH                 MLKRP007
00256      ELSE                                                         MLKRP007
00257          IF MV-PREVIOUS-DOCUMENT = MV-CURRENT-DOCUMENT            MLKRP007
00258              MOVE 'YES' TO MD-MISSING-DOCUMENT-SWITCH             MLKRP007
00259          ELSE                                                     MLKRP007
00260              MOVE MV-PREVIOUS-DOCUMENT TO                         MLKRP007
00261                  SM-SINGLE-DOC                                    MLKRP007
00262              MOVE SM-SINGLE-DOCUMENT-LINE TO MV-PRINT-LINE        MLKRP007
00263              PERFORM Z110-PRINT-A-LINE.                           MLKRP007
00264  C000-END.                                                        MLKRP007
00265                                                                   MLKRP007
00266  Z100-PRINT-HEADINGS.                                             MLKRP007
00267      ADD 1 TO MV-PAGE-COUNTER.                                    MLKRP007
00268      MOVE 6 TO MV-LINE-COUNTER.                                   MLKRP007
00269      MOVE MV-PAGE-COUNTER TO DP132O-PAGE-NUMBER.                  MLKRP007
00270      WRITE PRINT-REC FROM DP132O-STANDRD-HEADER-132-COLS          MLKRP007
00271          AFTER ADVANCING PAGE.                                    MLKRP007
00272      WRITE PRINT-REC FROM HL-HEADING-LINE-THREE AFTER             MLKRP007
00273          ADVANCING 1 LINE.                                        MLKRP007
00274      WRITE PRINT-REC FROM HL-HEADING-LINE-ONE AFTER               MLKRP007
00275          ADVANCING 2 LINES.                                       MLKRP007
00276      WRITE PRINT-REC FROM HL-HEADING-LINE-TWO AFTER               MLKRP007
00277          ADVANCING 1 LINE.                                        MLKRP007
00278      WRITE PRINT-REC FROM MV-BLANK-LINE AFTER                     MLKRP007
00279          ADVANCING 1 LINE.                                        MLKRP007
00280  Z100-END.                                                        MLKRP007
00281                                                                   MLKRP007
00282  Z110-PRINT-A-LINE.                                               MLKRP007
00283      IF MV-LINE-COUNTER > 60                                         CL**4
00284          PERFORM Z100-PRINT-HEADINGS.                             MLKRP007
00285      WRITE PRINT-REC FROM MV-PRINT-LINE                           MLKRP007
00286          AFTER ADVANCING 1 LINE.                                  MLKRP007
00287      ADD 1 MV-LINE-COUNTER GIVING MV-LINE-COUNTER.                MLKRP007
00288  Z110-END.                                                        MLKRP007
00310                                                                   MLKRP007
00311  Z200-PRINT-END-NOTICE.                                           MLKRP007
00312       MOVE '  ***   END OF REPORT   *** ' TO MV-PRINT-LINE.       MLKRP007
00313       PERFORM Z110-PRINT-A-LINE.                                  MLKRP007
00314  Z200-END.                                                        MLKRP007
