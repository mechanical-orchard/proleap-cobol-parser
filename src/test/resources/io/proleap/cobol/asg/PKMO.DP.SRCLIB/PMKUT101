      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PMKUT101.                                       00050000
       AUTHOR.          MARTIN BRADLEY.                                 00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.    SEP 2003.                                       00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ******************************************************************00110000
      * THIS PROGRAM READS IN A CONTROL CARD WITH A DATE, PROMOTION ID,*00120000
      * PROMO TYPE INDICATOR, AND A HISTORY/FUTURE INDICATOR.          *00130000
      * ANOTHER CONTROL CARD IS READ WHICH CONTAINS ALL THE SAMPLE     *00140000
      * STORES WHOSE PRICES SHOULD BE EVALUATED.                       *00150000
      * ALL INPUTS ON THE FIRST CONTROL CARD ARE VALIDATED.  IF ALL IS *00160000
      * WELL, PRICED DETERMINATION HEADERS ARE CREATED FOR EVERY       *00170000
      * SAMPLE STORE.  THEN ALL ACTIVE UPC_IDS ARE RETRIEVED.  FOR     *00171000
      * EACH UPC_ID, A PDE DETAIL RECORD IS CREATED FOR EACH SAMPLE    *00172000
      * STORE.  SO THE TOTAL NUMBER OF RECORDS IN THE OUTPUT DATASET   *00173000
      * IS # OF SAMPLE STORES X # OF ACTIVE UPCS.  SUBSEQUENT SORTING  *00174000
      * STEPS REDUCE THE DATA DOWN TO THE LOWEST PRICE FOR EACH UPC_ID.*00175000
      ******************************************************************00176000
      ******************************************************************00177000
      **   CHANGE LOG:                                                 *00178000
      *    01/30/2005 - KELLY WIESNER                                  *00179000
      *     STORE NUMBER EXPANSION PROJECT.                            *00180000
      *                                                                *00181000
      *    11/11/2005 - ERIC ZUNKE - WR29647                           *00182000
      *     POPULATE XS050-TIME WITH THE ACTUAL PROMO START TIME       *00183000
      *     RATHER THAN '00.00.00'.  THE 12/9/2005 NIGHT OWL NEEDS     *00184000
      *     TO BE TREATED LIKE AN LPS PROMOTION.  USUALLY AN LPS STARTS 00185000
      *     AT  MIDNIGHT BUT NIGHT OWLS DON'T.                          00186000
      *                                                                *00187000
      *    11/15/2005 - ERIC ZUNKE - WR29647                           *00188000
      *     SET XS050-INCL-PART-DAY-SW APPROPRIATELY. IF THE PROMOTION *00188100
      *     IS TRULY JUST PART OF A DAY, INDICATOR = 'Y', OTHERWISE    *00188200
      *     SET THE INDICATOR TO 'N' AS IT WAS PREVIOUSLY.             *00188300
      *                                                                *00188400
W36026*    03/26/2009 - PAUL KEBER - WR36026                           *00188501
W36026*     CHANGED THE ACTIVE-UPCS-CSR CURSOR TO ACCESS WORKING TABLE *00188601
W36026*     PMTW_CORP_SKU INSTEAD OF XST_SLU_LOC.                      *00188701
      *                                                                *00188901
      ******************************************************************00189401
       ENVIRONMENT DIVISION.                                            00189501
      ******************************************************************00189601
                                                                        00189701
       CONFIGURATION SECTION.                                           00189801
                                                                        00189901
       SOURCE-COMPUTER. IBM-3090.                                       00190000
       OBJECT-COMPUTER. IBM-3090.                                       00200000
                                                                        00210000
       INPUT-OUTPUT SECTION.                                            00220000
                                                                        00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT PROMO-ID-FILE                                         00260000
               ASSIGN                       TO INP01.                   00270000
                                                                        00280000
           SELECT STORE-CONTROL-CARD                                    00290000
               ASSIGN                       TO INP02.                   00300000
                                                                        00310000
           SELECT BATCH-FILE-OUT                                        00320000
               ASSIGN                       TO OUT01.                   00330000
                                                                        00340000
      ******************************************************************00350000
       DATA DIVISION.                                                   00360000
      ******************************************************************00370000
                                                                        00380000
       FILE SECTION.                                                    00390000
                                                                        00400000
       FD  PROMO-ID-FILE                                                00410000
           RECORDING MODE F                                             00420000
           BLOCK CONTAINS 0 RECORDS                                     00430000
           LABEL RECORDS ARE STANDARD.                                  00440000
                                                                        00450000
       01  PROMO-ID-RECORD                   PIC X(80).                 00460000
                                                                        00470000
       FD  STORE-CONTROL-CARD                                           00480000
           RECORDING MODE F                                             00490000
           BLOCK CONTAINS 0 RECORDS                                     00500000
           LABEL RECORDS ARE STANDARD.                                  00510000
                                                                        00520000
       01  STORE-CONTROL-CARD-REC            PIC X(80).                 00530000
                                                                        00540000
       FD  BATCH-FILE-OUT                                               00550000
           RECORDING MODE V                                             00560000
           BLOCK CONTAINS 0 RECORDS                                     00570000
           LABEL RECORDS ARE STANDARD.                                  00580000
                                                                        00590000
       01  BATCH-FILE-RECORD-OUT             PIC X(44).                 00600000
                                                                        00610000
       WORKING-STORAGE SECTION.                                         00620000
                                                                        00630000
      ******************************************************************00640000
      * PC PROGRAM CONSTANTS                                           *00650000
      ******************************************************************00660000
       01  PC-PROGRAM-CONSTANTS.                                        00670000
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE        00680000
                                                           'PMKUT101'.  00690000
                                                                        00700000
      ******************************************************************00710000
      * PV PROGRAM VARIABLES                                           *00720000
      ******************************************************************00730000
       01  PV-PROGRAM-VARIABLES.                                        00740000
           05  PV-PARAGRAPH-NAME            PIC X(40)    VALUE SPACES.  00750000
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(40)    VALUE SPACES.  00760000
                                                                        00770000
           05  PV-RUN-DATE                  PIC X(10)    VALUE SPACES.  00780000
           05  PV-RUN-DATE-RDF REDEFINES PV-RUN-DATE.                   00790000
               10  PV-RUN-DATE-CCYY         PIC X(04).                  00800000
               10  PV-RUN-DATE-D1           PIC X(01).                  00810000
               10  PV-RUN-DATE-MM           PIC X(02).                  00820000
               10  PV-RUN-DATE-D2           PIC X(01).                  00830000
               10  PV-RUN-DATE-DD           PIC X(02).                  00840000
           05  PV-RUN-PROMO-ID              PIC S9(10)V COMP-3.         00841000
                                                                        00841100
           05  PV-RUN-PROMO-IND             PIC X(01)    VALUE SPACES.  00841200
               88 PS-RUN-MAIN-PROMO                      VALUE 'M'.     00841300
               88 PS-RUN-NON-MAIN-PROMO                  VALUE 'N'.     00841400
               88 PS-RUN-LPS-PROMO                       VALUE 'L'.     00841500
                                                                        00841600
           05  PV-PROMO-STRT-TMSTMP         PIC X(08)    VALUE SPACES.  00841700
           05  PV-PROMO-PART-DAY-IND        PIC X(01)    VALUE SPACES.  00841800
           05  PV-PROMO-STRT-HR             PIC X(02)    VALUE SPACES.  00841900
           05  PV-PROMO-END-HR              PIC X(02)    VALUE SPACES.  00842000
                                                                        00842100
           05  PV-RUN-HISTORY-IND           PIC X(01)    VALUE SPACES.  00842200
               88 PS-RUN-HISTORY-PROMO                   VALUE 'H'.     00843000
               88 PS-RUN-FUTURE-PROMO                    VALUE 'F'.     00843100
               88 PS-RUN-LPS                             VALUE 'L'.     00843200
                                                                        00843300
           05  PV-CLEAR-COLUMNS             PIC X(01)    VALUE 'N'.     00843400
                                                                        00843500
           05  PV-MAX-STORE                 PIC 9(04) VALUE 0.          00843600
           05  PV-STR-NBR                   PIC S9(04)   COMP.          00843700
           05  PV-STR-NBR-4                 PIC S9(04)   COMP.          00843800
           05  PV-INSERT-COUNT              PIC S9(04)   COMP VALUE 0.  00843900
           05  PV-INTR-UPC-ID               PIC S9(09)   COMP.          00844000
           05  PV-STORE-SUB                 PIC 9(04)    VALUE 0.       00845000
                                                                        00846000
           05  PT-STORE-TABLE                                           00847000
               OCCURS 20 TIMES.                                         00848000
               10  PT-STORE-NBR             PIC S9(04)   COMP.          00849000
                                                                        00850000
      ******************************************************************00860000
      * PS PROGRAM SWITCHES                                            *00870000
      ******************************************************************00880000
       01  PS-PROGRAM-SWITCHES.                                         00890000
           05  PS-END-OF-PROMO-ID-FILE-SW      PIC X(01)     VALUE 'N'. 00900000
               88  PS-END-OF-PROMO-ID-FILE                   VALUE 'Y'. 00910000
           05  PS-INVALID-STORES-SW            PIC X(01)     VALUE 'N'. 00920000
               88  PS-INVALID-STORES                         VALUE 'Y'. 00930000
           05  PS-INVALID-PROMO-SW             PIC X(01)     VALUE 'N'. 00940000
               88  PS-INVALID-PROMO                          VALUE 'Y'. 00950000
           05  PS-END-OF-STORE-CNTL-CARD-SW    PIC X(01)     VALUE 'N'. 00960000
               88  PS-END-OF-STORE-CNTL-CARD                 VALUE 'Y'. 00970000
           05  PS-STORES-READ-SW               PIC X(01)     VALUE 'N'. 00980000
               88  PS-STORES-READ                            VALUE 'Y'. 00990000
           05  PS-END-OF-ACT-UPCS-CSR-SW       PIC X(01)     VALUE 'N'. 01000000
               88  PS-END-OF-ACT-UPCS-CSR                    VALUE 'Y'. 01010000
               88  PS-NOT-END-OF-ACT-UPCS-CSR                VALUE 'N'. 01020000
                                                                        01030000
      ******************************************************************01040000
      * PA PROGRAM ACCUMULATORS                                        *01050000
      ******************************************************************01060000
       01  PA-PROGRAM-ACCUMULATORS.                                     01070000
           05  PA-STORE-RECS-READ              PIC S9(11)     COMP-3    01080000
                                                           VALUE ZEROES.01090000
           05  PA-BATCH-RECS-WRITTEN           PIC S9(11)     COMP-3    01100000
                                                           VALUE ZEROES.01110000
           05  PA-ACT-UPCS-REC                 PIC S9(11) COMP-3        01120000
                                                           VALUE ZEROES.01130000
                                                                        01140000
      ******************************************************************01150000
      * CC CONTROL CARD RECORDS                                         01160000
      ******************************************************************01170000
       01  CC-STORE-CONTROL-CARD-RECORD.                                01180000
           05  CC-STR-NBR                   PIC  X(04)     VALUE SPACES.01190000
           05  FILLER                       PIC  X(76)     VALUE SPACES.01200000
                                                                        01210000
      ******************************************************************01220000
           COPY PMRD122.                                                01230000
                                                                        01240000
      ******************************************************************01250000
           COPY XSRD050.                                                01260000
                                                                        01270000
      ******************************************************************01280000
           COPY XSRD051.                                                01290000
                                                                        01300000
      ******************************************************************01310000
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004     *01320000
      ******************************************************************01330000
           COPY DPWS004.                                                01340000
                                                                        01350000
      ******************************************************************01360000
      *  TABLE INCLUDES                                              *  01370000
      ******************************************************************01380000
      *  TABLE XST_LOC                                                  01390000
           EXEC SQL INCLUDE XST00001 END-EXEC.                          01400000
      ******************************************************************01410000
      *  TABLE XST_SKU_LOC                                              01420000
           EXEC SQL INCLUDE XST00008 END-EXEC.                          01430000
      ******************************************************************01440000
      *  TABLE XST_PROMO                                                01450000
           EXEC SQL INCLUDE XST00026 END-EXEC.                          01460000
      ******************************************************************01470002
                                                                        01470102
W36026******************************************************************01471002
W36026* DCLGEN TABLE(PMTW_CORP_SKU)                                    *01472002
W36026******************************************************************01473002
W36026     EXEC SQL DECLARE PMTW_CORP_SKU TABLE                         01474002
W36026     ( INTR_UPC_ID        INTEGER         NOT NULL                01475002
W36026      ,BEG_TMST           TIMESTAMP       NOT NULL                01476002
W36026      ,END_TMST           TIMESTAMP                               01477002
W36026      ,UNT_RTL_AMT        DECIMAL(009, 2) NOT NULL                01478002
W36026      ,LOC_SKU_STAT_CDE   CHAR(0002)      NOT NULL                01479002
W36026      ,GP_RTL_QTY         SMALLINT                                01479102
W36026      ,GP_RTL_AMT         DECIMAL(009, 2)                         01479202
W36026      ,UNT_RTL_CHG_DTE    DATE            NOT NULL                01479302
W36026      ,UPC_STAT_CHG_DTE   DATE            NOT NULL                01479402
W36026      ,SKU_NBR            DECIMAL(008, 0) NOT NULL                01479502
W36026      ,STYL_ID            DECIMAL(008, 0) NOT NULL                01479602
W36026      ,REGN_PRIC_IND      CHAR(0001)      NOT NULL                01479702
W36026      ,DEPT_NBR           DECIMAL(004, 0) NOT NULL                01479802
W36026      ,MAJ_CL_NBR         DECIMAL(004, 0) NOT NULL                01479902
W36026      ,SUB_CL_NBR         DECIMAL(004, 0) NOT NULL                01480002
W36026     ) END-EXEC.                                                  01480102
                                                                        01480202
                                                                        01481000
      ******************************************************************01490000
      *    SQL COMMUNICATIONS AREA DCLGEN                              *01500000
      ******************************************************************01510000
           EXEC SQL                                                     01520000
               INCLUDE SQLCA                                            01530000
           END-EXEC.                                                    01540000
                                                                        01550000
           COPY SQLCA2.                                                 01560000
                                                                        01570000
      ******************************************************************01580000
      *    ACTIVE-UPCS-CSR:                                             01590000
      *      - RETRIEVES ALL NON-CLEARANCED CORPORATE INTERNAL UPCS AS  01600001
      *        OF THE RUN DATE.                                         01601001
      ******************************************************************01610000
           EXEC SQL                                                     01620000
               DECLARE ACTIVE-UPCS-CSR                                  01630000
               CURSOR FOR                                               01640000
               SELECT INTR_UPC_ID                                       01650000
W36026         FROM PMTW_CORP_SKU                                       01660001
               WHERE BEG_TMST <= TIMESTAMP(:PV-RUN-DATE, '00.00.00')    01670001
                 AND (END_TMST >= TIMESTAMP(:PV-RUN-DATE, '00.00.00')   01700001
                      OR END_TMST IS NULL)                              01710001
                 AND LOC_SKU_STAT_CDE IN ('10','20','25','40','90')     01720001
               WITH UR                                                  01740000
           END-EXEC.                                                    01750000
                                                                        01760000
      ******************************************************************01770000
      * AC ABEND CODES                                                 *01780000
      ******************************************************************01790000
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0. 01800000
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.   01810000
           88  ABEND-4002-READ-ERROR                     VALUE +4002.   01820000
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.   01830000
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.   01840000
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.   01850000
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.   01860000
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.   01870000
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.   01880000
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.   01890000
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.   01900000
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.   01910000
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.   01920000
                                                                        01930000
       PROCEDURE DIVISION.                                              01940000
                                                                        01950000
      ******************************************************************01960000
      * MAIN PROCESSING                                                 01970000
      ******************************************************************01980000
                                                                        01990000
       A100-PROGRAM-MAINLINE.                                           02000000
                                                                        02010000
           PERFORM B100-OPEN-FILES.                                     02020000
           PERFORM B150-READ-PROMO-ID-FILE.                             02030000
                                                                        02040000
           IF NOT PS-END-OF-PROMO-ID-FILE                               02050000
                                                                        02060000
      *        VALIDATE THE INPUT PARAMETERS                            02070000
               PERFORM D100-VALIDATE-PROMO                              02080000
                                                                        02090000
               IF PS-INVALID-PROMO                                      02100000
                   DISPLAY ' '                                          02110000
                   DISPLAY '** ABEND     **'                            02120000
                   DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME        02130000
                   DISPLAY '** PARAGRAPH **  = A100-PROGRAM-MAINLINE'   02140000
                   DISPLAY '** RUN DATE DOES NOT FALL WITHIN THE    **' 02150000
                   DISPLAY '** SPECIFIED PROMO, OR PROMO IS INVALID **' 02160000
                   DISPLAY ' '                                          02170000
                   MOVE +4003 TO ABEND-CODE                             02180000
                   CALL 'ILBOABN0' USING ABEND-CODE                     02190000
               END-IF                                                   02200000
                                                                        02210000
               PERFORM D200-VALIDATE-PROMO-IND                          02220000
               PERFORM D300-VALIDATE-HIST-IND                           02230000
                                                                        02240000
               PERFORM C100-PROCESS-STORES                              02250000
                   UNTIL PS-END-OF-STORE-CNTL-CARD                      02260000
                                                                        02270000
               IF PS-INVALID-STORES                                     02280000
                   DISPLAY ' '                                          02290000
                   DISPLAY '** ABEND     **'                            02300000
                   DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME        02310000
                   DISPLAY '** PARAGRAPH **  = A100-PROGRAM-MAINLINE'   02320000
                   DISPLAY '** INVALID STORES (SEE LIST ABOVE) **'      02330000
                   DISPLAY ' '                                          02340000
                   MOVE +4003 TO ABEND-CODE                             02350000
                   CALL 'ILBOABN0' USING ABEND-CODE                     02360000
               END-IF                                                   02370000
                                                                        02380000
               DISPLAY 'STORES INSERTED INTO ARRAY: ' PV-INSERT-COUNT   02390000
               MOVE PV-STORE-SUB TO PV-MAX-STORE                        02400000
                                                                        02410000
               PERFORM C130-CREATE-STORE-HEADERS                        02420000
                                                                        02430000
      *        NOW PROCESS ALL ACTIVE UPCS                              02440000
               PERFORM C200-OPEN-ACT-UPCS-CSR                           02450000
               PERFORM C300-FETCH-ACT-UPCS-CSR                          02460000
               PERFORM C400-PROCESS-ACT-UPCS-CSR                        02470000
                   UNTIL PS-END-OF-ACT-UPCS-CSR                         02480000
               PERFORM C500-CLOSE-ACT-UPCS-CSR                          02490000
           END-IF.                                                      02500000
                                                                        02510000
           PERFORM B900-EOJ-ROUTINE.                                    02520000
                                                                        02530000
           GOBACK.                                                      02540000
                                                                        02550000
      ***************************************************************** 02560000
      * B100-OPEN-FILES.                                              * 02570000
      * ==> PROCESSES:                                                * 02580000
      *  - OPEN TWO INPUT FILES AND ONE OUTPUT FILE                   * 02590000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 02600000
      ******************************************************************02610000
       B100-OPEN-FILES.                                                 02620000
                                                                        02630000
           OPEN INPUT PROMO-ID-FILE                                     02640000
                      STORE-CONTROL-CARD.                               02650000
           OPEN OUTPUT BATCH-FILE-OUT.                                  02660000
                                                                        02670000
      ***************************************************************** 02680000
      * B150-READ-PROMO-ID-FILE.                                      * 02690000
      * ==> PROCESSES:                                                * 02700000
      *  - READ IN THE RUN CONTROL CARD                               * 02710000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 02720000
      ***************************************************************** 02730000
       B150-READ-PROMO-ID-FILE.                                         02740000
                                                                        02750000
           READ PROMO-ID-FILE INTO PM122-PROMO-ID-REC                   02760000
               AT END                                                   02770000
                   SET PS-END-OF-PROMO-ID-FILE TO TRUE.                 02780000
                                                                        02790000
           IF PS-END-OF-PROMO-ID-FILE                                   02800000
               DISPLAY ' '                                              02810000
               DISPLAY '** EMPTY FILE. '                                02820000
               DISPLAY ' '                                              02830000
           ELSE                                                         02840000
               MOVE PM122-RUN-DTE-CCYY   TO PV-RUN-DATE-CCYY            02850000
               MOVE PM122-RUN-DTE-MM     TO PV-RUN-DATE-MM              02860000
               MOVE PM122-RUN-DTE-DD     TO PV-RUN-DATE-DD              02870000
               MOVE '-'                  TO PV-RUN-DATE-D1              02880000
                                            PV-RUN-DATE-D2              02890000
               MOVE PM122-PROMO-ID       TO PV-RUN-PROMO-ID             02900000
               MOVE PM122-PROMO-TYP-IND  TO PV-RUN-PROMO-IND            02910000
               MOVE PM122-HIST-FUT-IND   TO PV-RUN-HISTORY-IND          02920000
                                                                        02930000
               DISPLAY ' '                                              02940000
               DISPLAY 'CONTROLS FOR PROGRAM ' PC-PROGRAM-NAME ': '     02950000
               DISPLAY '  RUN DATE:          ' PV-RUN-DATE              02960000
               DISPLAY '  RUN PROMO ID:      ' PV-RUN-PROMO-ID          02970000
               DISPLAY '  PROMO INDICATOR:   ' PV-RUN-PROMO-IND         02980000
               DISPLAY '  HISTORY INDICATOR: ' PV-RUN-HISTORY-IND       02990000
               DISPLAY ' '                                              03000000
           END-IF.                                                      03010000
                                                                        03020000
      ***************************************************************** 03030000
      * B900-EOJ-ROUTINE.                                             * 03040000
      * ==> PROCESSES:                                                * 03050000
      *  - CLOSE FILES AND DISPLAY TOTALS                             * 03060000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 03070000
      ******************************************************************03080000
       B900-EOJ-ROUTINE.                                                03090000
                                                                        03100000
           CLOSE PROMO-ID-FILE                                          03110000
                 STORE-CONTROL-CARD                                     03120000
                 BATCH-FILE-OUT.                                        03130000
                                                                        03140000
           DISPLAY ' '                                                  03150000
           DISPLAY 'TOTAL NUMBER OF RECORDS WRITTEN:  '                 03160000
                                                  PA-BATCH-RECS-WRITTEN 03170000
           DISPLAY ' '.                                                 03180000
                                                                        03190000
      ***************************************************************** 03200000
      * C100-PROCESS-STORES.                                          * 03210000
      * ==> PROCESSES:                                                * 03220000
      *  - READ SAMPLE STORES, VALIDATE, AND INSERT TO TEMP TABLE.    * 03230000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 03240000
      ***************************************************************** 03250000
       C100-PROCESS-STORES.                                             03260000
                                                                        03270000
           READ STORE-CONTROL-CARD INTO CC-STORE-CONTROL-CARD-RECORD    03280000
               AT END                                                   03290000
                   SET PS-END-OF-STORE-CNTL-CARD TO TRUE.               03300000
                                                                        03310000
           IF PS-END-OF-STORE-CNTL-CARD AND NOT PS-STORES-READ          03320000
               DISPLAY ' '                                              03330000
               DISPLAY '** ABEND     **'                                03340000
               DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME            03350000
               DISPLAY '** PARAGRAPH **  = C100-PROCESS-STORES'         03360000
               DISPLAY '** SAMPLE STORE CONTROL FILE IS EMPTY  '        03370000
               DISPLAY ' '                                              03380000
               MOVE +4003 TO ABEND-CODE                                 03390000
               CALL 'ILBOABN0' USING ABEND-CODE                         03400000
           ELSE                                                         03410000
               IF NOT PS-END-OF-STORE-CNTL-CARD                         03420000
                   SET PS-STORES-READ TO TRUE                           03430000
                   MOVE CC-STR-NBR TO PV-STR-NBR-4                      03440000
                                                                        03450000
                   PERFORM C110-VALIDATE-STORE                          03460000
                                                                        03470000
                   IF NOT PS-INVALID-STORES                             03480000
                       PERFORM C120-INSERT-STORE                        03490000
                   END-IF                                               03500000
               END-IF                                                   03510000
           END-IF.                                                      03520000
                                                                        03530000
      ***************************************************************** 03540000
      * C110-VALIDATE-STORE.                                          * 03550000
      * ==> PROCESSES:                                                * 03560000
      *  - CHECK IF EACH STORE IS VALID AND OPEN AS OF THE DATE.      * 03570000
      * ==> SQLCODE:                                                  * 03580000
      *  - 0    = FOUND SUCCESSFULLY                                  * 03590000
      *  - 100 = ROW NOT FOUND                                        * 03600000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 03610000
      ***************************************************************** 03620000
       C110-VALIDATE-STORE.                                             03630000
                                                                        03640000
           EXEC SQL                                                     03650000
               SELECT                                                   03660000
                    OPN_DTE                                             03670000
               INTO                                                     03680000
                    :XST00001-OPN-DTE                                   03690000
               FROM                                                     03700000
                    XST_LOC                                             03710000
               WHERE                                                    03720000
                    LOC_NBR = :PV-STR-NBR-4                             03730000
               WITH UR                                                  03740000
           END-EXEC.                                                    03750000
                                                                        03760000
           EVALUATE TRUE                                                03770000
               WHEN SQLCODE = 0                                         03780000
                   IF XST00001-OPN-DTE > PV-RUN-DATE                    03790000
                       DISPLAY 'WARNING:  STORE ' PV-STR-NBR-4          03800000
                           ' OPENED AFTER THE RUN DATE'                 03810000
                   END-IF                                               03820000
                                                                        03830000
               WHEN SQLCODE = 100                                       03840000
                   SET PS-INVALID-STORES TO TRUE                        03850000
                  DISPLAY '**ERROR: STORE ' PV-STR-NBR-4 ' INVALID'     03860000
                                                                        03870000
               WHEN OTHER                                               03880000
                   MOVE 'C110-VALIDATE-STORE' TO PV-PARAGRAPH-NAME      03890000
                   MOVE 'ERROR ON SELECT OF XST_LOC'                    03900000
                                           TO PV-DB2-OPERATION-ATTEMPTED03910000
                   PERFORM Z900-SQL-ABEND                               03920000
           END-EVALUATE.                                                03930000
                                                                        03940000
      ******************************************************************03950000
      * C120-INSERT-STORE                                               03960000
      *     - INSERTS THE STORE IN THE STORE ARRAY.                     03970000
      ******************************************************************03980000
       C120-INSERT-STORE.                                               03990000
                                                                        04000000
           ADD +1 TO PV-STORE-SUB.                                      04010000
           MOVE PV-STR-NBR-4 TO PV-STR-NBR                              04020000
           DISPLAY 'INSERTING STORE ' PV-STR-NBR                        04030000
           MOVE PV-STR-NBR TO PT-STORE-NBR (PV-STORE-SUB).              04040000
           ADD +1 TO PV-INSERT-COUNT.                                   04050000
                                                                        04060000
      ******************************************************************04070000
      * C130-CREATE-STORE-HEADERS                                       04080000
      *     - CREATES HEADER RECORDS FOR EVERY SAMPLE STORE.            04090000
      ******************************************************************04100000
       C130-CREATE-STORE-HEADERS.                                       04110000
                                                                        04120000
           PERFORM WITH TEST BEFORE                                     04130000
               VARYING PV-STORE-SUB FROM +1 BY +1                       04140000
               UNTIL (PV-STORE-SUB > PV-MAX-STORE)                      04150000
                                                                        04160000
               MOVE PT-STORE-TABLE(PV-STORE-SUB) TO PV-STR-NBR          04170000
               PERFORM M200-POPULATE-HEADER-RECORD                      04180000
               PERFORM M300-WRITE-HEADER-RECORD                         04190000
           END-PERFORM.                                                 04200000
                                                                        04210000
      ***************************************************************** 04220000
      * C200-OPEN-ACT-UPCS-CSR.                                       * 04230000
      * ==> PROCESSES:                                                * 04240000
      *  - OPENS ACTIVE-UPCS-CSR                                      * 04250000
      * ==> SQLCODE:                                                  * 04260000
      *  - 0 = SUCCESSFULL OPEN                                       * 04270000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 04280000
      ***************************************************************** 04290000
       C200-OPEN-ACT-UPCS-CSR.                                          04300000
                                                                        04310000
           EXEC SQL                                                     04320000
               OPEN ACTIVE-UPCS-CSR                                     04330000
           END-EXEC.                                                    04340000
                                                                        04350000
           EVALUATE TRUE                                                04360000
               WHEN SQLCODE = +0                                        04370000
                   MOVE ZERO TO PA-ACT-UPCS-REC                         04380000
                                                                        04390000
               WHEN OTHER                                               04400000
                   MOVE 'C200-OPEN-ACT-UPCS-CSR   '                     04410000
                                     TO PV-PARAGRAPH-NAME               04420000
                   MOVE 'OPEN ERROR ON ACTIVE-UPCS-CSR    '             04430000
                                     TO PV-DB2-OPERATION-ATTEMPTED      04440000
                   PERFORM Z900-SQL-ABEND                               04450000
           END-EVALUATE.                                                04460000
                                                                        04470000
      ***************************************************************** 04480000
      * C300-FETCH-ACT-UPCS-CSR.                                      * 04490000
      * ==> PROCESSES:                                                * 04500000
      *  - WHEN DATA IS BROUGHT BACK FROM THE CURSOR FETCH IT TO THE  * 04510000
      *    VARIABLES LISTED.                                          * 04520000
      * ==> SQLCODE:                                                  * 04530000
      *  - 0    = SUCCESSFULL OPEN                                    * 04540000
      *  - 100 = NO DATA WAS FOUND                                    * 04550000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 04560000
      ***************************************************************** 04570000
       C300-FETCH-ACT-UPCS-CSR.                                         04580000
                                                                        04590000
           EXEC SQL                                                     04600000
               FETCH ACTIVE-UPCS-CSR                                    04610000
               INTO :XST00008-INTR-UPC-ID                               04620000
           END-EXEC.                                                    04630000
                                                                        04640000
           EVALUATE TRUE                                                04650000
               WHEN SQLCODE = 0                                         04660000
                   ADD 1 TO PA-ACT-UPCS-REC                             04670000
                   MOVE XST00008-INTR-UPC-ID    TO PV-INTR-UPC-ID       04680000
                                                                        04690000
               WHEN SQLCODE = 100                                       04700000
                   DISPLAY 'RECORDS FROM ACTIVE-UPCS-CSR = '            04710000
                       PA-ACT-UPCS-REC                                  04720000
                   SET PS-END-OF-ACT-UPCS-CSR TO TRUE                   04730000
                                                                        04740000
               WHEN OTHER                                               04750000
                   DISPLAY 'RECORDS FROM ACTIVE-UPCS-CSR = '            04760000
                       PA-ACT-UPCS-REC                                  04770000
                   MOVE 'C300-FETCH-ACT-UPCS-CSR   '                    04780000
                                     TO PV-PARAGRAPH-NAME               04790000
                   MOVE 'FETCH ERROR ON ACTIVE-UPCS-CSR   '             04800000
                                     TO PV-DB2-OPERATION-ATTEMPTED      04810000
                   PERFORM Z900-SQL-ABEND                               04820000
           END-EVALUATE.                                                04830000
                                                                        04840000
      ***************************************************************** 04850000
      * C400-PROCESS-ACT-UPCS-CSR.                                    * 04860000
      * ==> PROCESSES:                                                * 04870000
      *  - FETCH THE NEXT ROW IN THE SELECT STATEMENT.                * 04880000
      * ==> PERFORMED FROM: A100-MAINLINE                             * 04890000
      ***************************************************************** 04900000
       C400-PROCESS-ACT-UPCS-CSR.                                       04910000
                                                                        04920000
           MOVE 'C400' TO PV-PARAGRAPH-NAME.                            04930000
      *    PROCESS THIS UPC FOR EACH SAMPLE STORE.                      04940000
           PERFORM WITH TEST BEFORE                                     04950000
               VARYING PV-STORE-SUB FROM +1 BY +1                       04960000
               UNTIL (PV-STORE-SUB > PV-MAX-STORE)                      04970000
                                                                        04980000
               MOVE PT-STORE-TABLE(PV-STORE-SUB) TO PV-STR-NBR          04990000
               PERFORM M400-POPULATE-DETAIL-RECORD                      05000000
               PERFORM M500-WRITE-DETAIL-RECORD                         05010000
           END-PERFORM.                                                 05020000
                                                                        05030000
           PERFORM C300-FETCH-ACT-UPCS-CSR.                             05040000
                                                                        05050000
      ***************************************************************** 05060000
      * C500-CLOSE-ACT-UPCS-CSR.                                      * 05070000
      * ==> PROCESSES:                                                * 05080000
      *  - CLOSE THE ACTIVE UPCS CURSOR.                              * 05090000
      * ==> SQLCODE:                                                  * 05100000
      *  - 0    = CLOSE WAS SUCCESSFULL                               * 05110000
      ***************************************************************** 05120000
       C500-CLOSE-ACT-UPCS-CSR.                                         05130000
                                                                        05140000
           EXEC SQL                                                     05150000
               CLOSE ACTIVE-UPCS-CSR                                    05160000
           END-EXEC.                                                    05170000
                                                                        05180000
           EVALUATE TRUE                                                05190000
               WHEN SQLCODE = +0                                        05200000
                   CONTINUE                                             05210000
                                                                        05220000
               WHEN OTHER                                               05230000
                   MOVE 'C500-CLOSE-ACT-UPCS-CSR   '                    05240000
                                     TO PV-PARAGRAPH-NAME               05250000
                   MOVE 'CLOSE ERROR ON ACTIVE-UPCS-CSR    '            05260000
                                     TO PV-DB2-OPERATION-ATTEMPTED      05270000
                   PERFORM Z900-SQL-ABEND                               05280000
           END-EVALUATE.                                                05290000
                                                                        05300000
      ******************************************************************05310000
       D100-VALIDATE-PROMO.                                             05320000
      * VERIFY THAT THE RUN DATE SPECIFIED FALLS WITHIN THE PROMO ID    05330000
      * SPECIFIED.                                                      05340000
      ******************************************************************05350000
                                                                        05360000
           EXEC SQL                                                     05370000
               SELECT PROMO_ID                                          05380000
                    , SUBSTR(CHAR(DFLT_STRT_TMST),12,8)                 05390000
                    , SUBSTR(CHAR(DFLT_STRT_TMST),12,2)                 05400000
                    , SUBSTR(CHAR(DFLT_END_TMST),12,2)                  05401000
                      END                                               05430000
               INTO :XST00026-PROMO-ID                                  05440000
                    ,:PV-PROMO-STRT-TMSTMP                              05450000
                    ,:PV-PROMO-STRT-HR                                  05460000
                    ,:PV-PROMO-END-HR                                   05461000
               FROM XST_PROMO                                           05470000
               WHERE PROMO_ID = :PV-RUN-PROMO-ID                        05480000
                   AND :PV-RUN-DATE BETWEEN DATE(DFLT_STRT_TMST)        05490000
                           AND DATE(DFLT_END_TMST)                      05500000
                   AND STAT_CDE = 'S'                                   05510000
               WITH UR                                                  05520000
           END-EXEC.                                                    05530000
                                                                        05540000
           EVALUATE TRUE                                                05550000
               WHEN SQLCODE = +0                                        05560000
                   IF (PV-PROMO-STRT-HR NOT EQUAL '00' OR               05561000
                       PV-PROMO-END-HR NOT EQUAL '23')                  05562000
                       MOVE 'Y' TO PV-PROMO-PART-DAY-IND                05563100
                    ELSE                                                05563200
                       MOVE 'N' TO PV-PROMO-PART-DAY-IND                05563300
                    END-IF                                              05564000
                                                                        05580000
               WHEN SQLCODE = +100                                      05590000
                   SET PS-INVALID-PROMO TO TRUE                         05600000
                                                                        05610000
               WHEN OTHER                                               05620000
                   MOVE 'D100-VALIDATE-PROMO'                           05630000
                                     TO PV-PARAGRAPH-NAME               05640000
                   MOVE 'ERROR SELECTING XST_PROMO'                     05650000
                                     TO PV-DB2-OPERATION-ATTEMPTED      05660000
                   PERFORM Z900-SQL-ABEND                               05670000
           END-EVALUATE.                                                05680000
                                                                        05690000
      ******************************************************************05700000
       D200-VALIDATE-PROMO-IND.                                         05710000
      * VERIFY THAT THE PROMO TYPE INDICATOR IS VALID.  MUST BE ONE OF  05720000
      * THE FOLLOWING: 'M' = MAIN, 'N' = NON-MAIN, 'L' = LPS            05730000
      ******************************************************************05740000
                                                                        05750000
           EVALUATE TRUE                                                05760000
               WHEN PS-RUN-MAIN-PROMO                                   05770000
               WHEN PS-RUN-NON-MAIN-PROMO                               05780000
               WHEN PS-RUN-LPS-PROMO                                    05790000
                   CONTINUE                                             05800000
                                                                        05810000
               WHEN OTHER                                               05820000
                   MOVE 'D200-VALIDATE-PROMO-IND' TO PV-PARAGRAPH-NAME  05830000
                                                                        05840000
                   MOVE 'PROMO TYPE INDICATOR IS NOT VALID'             05850000
                                 TO PV-DB2-OPERATION-ATTEMPTED          05860000
                   PERFORM Z900-SQL-ABEND                               05870000
           END-EVALUATE.                                                05880000
                                                                        05890000
      ******************************************************************05900000
       D300-VALIDATE-HIST-IND.                                          05910000
      * VERIFY THAT THE HISTORY/FUTURE FLAG IS VALID.  MUST BE ONE OF   05920000
      * THE FOLLOWING: 'H' = HISTORY, 'F' = FUTURE.                     05930000
      ******************************************************************05940000
                                                                        05950000
           EVALUATE TRUE                                                05960000
               WHEN PS-RUN-HISTORY-PROMO                                05970000
               WHEN PS-RUN-FUTURE-PROMO                                 05980000
               WHEN PS-RUN-LPS                                          05990000
                   CONTINUE                                             06000000
                                                                        06010000
               WHEN OTHER                                               06020000
                   MOVE 'D300-VALIDATE-HIST-IND' TO PV-PARAGRAPH-NAME   06030000
                                                                        06040000
                   MOVE 'HISTORY/FUTURE INDICATOR IS NOT VALID'         06050000
                                 TO PV-DB2-OPERATION-ATTEMPTED          06060000
                   PERFORM Z900-SQL-ABEND                               06070000
           END-EVALUATE.                                                06080000
                                                                        06090000
      ***************************************************************** 06100000
       M200-POPULATE-HEADER-RECORD.                                     06110000
      ***************************************************************** 06120000
                                                                        06130000
           SET XS050-RECORD-TYPE-HEADER TO TRUE.                        06140000
                                                                        06150000
           MOVE PV-STR-NBR               TO XS050-STORE                 06160000
           MOVE PV-RUN-DATE              TO XS050-DATE.                 06170000
           MOVE PV-PROMO-STRT-TMSTMP     TO XS050-TIME.                 06180000
           MOVE PV-RUN-PROMO-ID          TO XS050-PROMO-ID.             06190000
           SET XS050-MDSE-ID-INTR-UPC-ID TO TRUE.                       06200000
                                                                        06210000
           MOVE 'Y'                     TO XS050-INCL-UNAD-SW.          06220000
           MOVE PV-PROMO-PART-DAY-IND   TO XS050-INCL-PART-DAY-SW.      06230000
           MOVE 'N'                     TO XS050-INCL-GOLD-STR-SW.      06240000
           MOVE 'N'                     TO XS050-INCL-LWST-SIX-WK-SW.   06250000
           MOVE 'Y'                     TO XS050-INCL-GP-PRC-SW.        06260000
           MOVE 'N'                     TO XS050-INCL-BWP-PROMO-SW.     06270000
           MOVE 'Y'                     TO XS050-INCL-TIERED-PROMO-SW.  06280000
           MOVE 'N'                     TO XS050-INCL-ADD-PCT-AMT-SW.   06290000
           MOVE 'PM'                    TO XS050-CALLING-APPLICATION-ID.06300000
                                                                        06310000
      ******************************************************************06320000
       M300-WRITE-HEADER-RECORD.                                        06330000
      ******************************************************************06340000
                                                                        06350000
           WRITE BATCH-FILE-RECORD-OUT                                  06360000
               FROM XS050-PD-HEADER-RECORD.                             06370000
                                                                        06380000
           ADD +1     TO PA-BATCH-RECS-WRITTEN.                         06390000
                                                                        06400000
      ******************************************************************06410000
       M400-POPULATE-DETAIL-RECORD.                                     06420000
      ******************************************************************06430000
                                                                        06440000
           SET XS051-RECORD-TYPE-DETAIL TO TRUE.                        06450000
           MOVE PV-STR-NBR              TO XS051-STORE.                 06460000
           MOVE XS050-DATE              TO XS051-DATE.                  06470000
           MOVE XS050-TIME              TO XS051-TIME.                  06480000
           MOVE XS050-PROMO-ID          TO XS051-PROMO-ID.              06490000
           MOVE PV-INTR-UPC-ID          TO XS051-INTR-UPC-ID.           06500000
           MOVE ZEROES                  TO XS051-SKU.                   06510000
           MOVE ZEROES                  TO XS051-UPC.                   06520000
                                                                        06530000
      ******************************************************************06540000
       M500-WRITE-DETAIL-RECORD.                                        06550000
      ******************************************************************06560000
                                                                        06570000
           WRITE BATCH-FILE-RECORD-OUT                                  06580000
               FROM XS051-PD-DETAIL-RECORD.                             06590000
                                                                        06600000
           ADD +1     TO PA-BATCH-RECS-WRITTEN.                         06610000
                                                                        06620000
      ******************************************************************06630000
      *     Z900-SQL-ABEND                                             *06640000
      * ==> PROCESSES:                                                 *06650000
      *     - PERFORMS ABEND PROCESSING FOR SQL ERRORS.                *06660000
      * ==> PERFORMED FROM:                                            *06670000
      ******************************************************************06680000
       Z900-SQL-ABEND.                                                  06690000
                                                                        06700000
           DISPLAY 'Z900-SQL-ABEND'.                                    06710000
           DISPLAY '**  ABEND     **'.                                  06720000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              06730000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            06740000
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.   06750000
                                                                        06760000
      ******************************************************************06770000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *06780000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *06790000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *06800000
      * FORMAT.                                                        *06810000
      ******************************************************************06820000
           COPY DPPD004.                                                06830000
                                                                        06840000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            06850000
           CALL 'ILBOABN0' USING ABEND-CODE.                            06860000
