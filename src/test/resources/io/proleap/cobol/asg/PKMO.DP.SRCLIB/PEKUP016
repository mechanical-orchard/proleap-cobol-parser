00005  IDENTIFICATION DIVISION.                                                 
00008  PROGRAM-ID.    PEKUP016.                                                 
00009  AUTHOR.        AMY REILAND.                                              
00010  INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
00011  DATE-WRITTEN.  10/17/93.                                                 
00012  DATE-COMPILED.                                                           
00014 *****************************************************************         
00015 *  IN-STORE PRICE CHANGE DOCUMENT ARCHIVAL TO HISTORY PROCESSING          
00016 *                                                                         
00017 *  THIS PROGRAM WILL COPY THOSE IN-STORE PROCE CHANGE DOCUMENTS           
00018 *  WHICH ARE 2 MONTHS OR OLDER TO THE HISTORY DOCUMENT FILES.             
00019 *  THEY WILL THEN BE DELETED OFF THE CURRENT FILES.                       
00020 *                                                                         
00021 *  ONLY THOSE DOCUMENTS WHICH ARE IN 'CP' OR 'CA' STATUS WILL             
00022 *  BE SELECTED FOR ARCHIVAL.                                              
00023 *                                                                         
00024 *  TABLES ACCESSED ARE:-                                                  
00025 *      TSTRPPC IN-STORE PERMANENT PRICE CHANGE HEADER                     
00026 *      TSPPCDT IN-STORE PERMANENT PRICE CHANGE DETAIL                     
00027 *  TABLES WHICH HAVE ROWS INSERTED ARE:-                                  
00028 *      TSTRPCH IN-STORE HISTORY PERMANENT PRICE CHANGE HEADER             
00029 *      TSPCDTH IN-STORE HISTORY PERMANENT PRICE CHANGE DETAIL             
00030 *  TABLES WHICH HAVE ROWS DELETED THROUGH CASCADING DELETE:               
00031 *      TSTRPPC IN-STORE PERMANENT PRICE CHANGE HEADER                     
00032 *      TSPPCDT IN-STORE PERMANENT PRICE CHANGE DETAIL                     
00033 *****************************************************************         
00038 *  AMENDMENT HISTORY STARTING ON XX/XX/XX                                 
00039 *                                                                         
00040 *    DATE      CHANGED BY     DESCRIPTION OF CHANGE                       
00041 *  ========    =============  =================================           
00043 *  06/03/02    JACK KRAMER    ADDED 'WITH HOLD' ON HEADER-CURSOR          
00043 *                             AND REMOVED OPEN OF HEADER-CURSOR           
00043 *                             AFTER COMMITS.                              
00042 *                                                                         
W24472*  02/16/03    ASHOK A V      REPLACED DEPT, CLASS AND SEQUENCE           
W24472*              IGSI           NUMBER WITH 8 DIGIT SKU.                    
      *                                                                         
      *  11/17/03    TODD           ADD CODE TO MOVE DEPT_NBR,                  
      *              MICHALEK       MAJ_CL_NBR, AND SUB_CL_NBR FROM             
      *                             TSPPCDT TO TSPCDTH                          
      *****************************************************************         
      * 03/28/05 - TODD C. MICHALEK -  WR NO. 27559                             
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4             
      *      DIGITS IN LENGTH                                                   
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE          
      *      CALLS                                                              
      *****************************************************************         
00044  ENVIRONMENT DIVISION.                                                    
00047  CONFIGURATION SECTION.                                                   
00049  SOURCE-COMPUTER. IBM-3090.                                               
00050  OBJECT-COMPUTER. IBM-3090.                                               
00053  INPUT-OUTPUT SECTION.                                                    
00055  FILE-CONTROL.                                                            
00061  DATA DIVISION.                                                           
00065  FILE SECTION.                                                            
      *                                                                         
00068  WORKING-STORAGE SECTION.                                                 
00070 *****************************************************************         
00071 *    PERM CONSTANTS                                                       
00072 *****************************************************************         
00074      COPY PEWS049.                                                        
00076 *****************************************************************         
00077 *    PERM CONSTANTS FOR CONTROL TABLE                                     
00078 *****************************************************************         
00080      COPY PEWS048.                                                        
00083 *****************************************************************         
00084 *    DB2 COMMUNICATION AREA                                               
00085 *****************************************************************         
00087      COPY DPWS004.                                                        
00089 *****************************************************************         
00090 *    INCLUDE DATE ROUTINE COPY MEMBER                                     
00091 *****************************************************************         
00092      COPY DPWS500.                                                        
      *                                                                         
00095  01  ABEND-CODE                         PIC S9(04)   VALUE +0             
00096                                                      COMP SYNC.           
00097      88  ABEND-4008-DATA-ERROR                       VALUE +4008.         
00098      88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
00099 *                                                                         
00100  01  PC-PROGRAM-CONSTANTS.                                                
00101      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)   VALUE                
00102                                                      'PEKUP016'.          
00103      05  PC-60-DAYS                     PIC  9(02)   VALUE 60.            
00104      05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
00105                                                      COMP-3.              
00106      05  PC-ROW-NOT-FOUND               PIC S9(4)    VALUE +100.          
00107      05  PC-DEADLOCK                    PIC S9(4)    VALUE -913.          
00108      05  PC-UNAVAILABLE-RESOURCES       PIC S9(4)    VALUE -904.          
00109      05  PC-CALENDAR-PGM                PIC  X(8)    VALUE                
00110                                                      'DPKUT500'.          
00111      05  PC-MAX-DOCUMENTS               PIC  9(4)    VALUE 1.             
00113 *                                                                         
00114  01  PV-PROGRAM-VARIABLES.                                                
00115      05  PV-CURRENT-DATE.                                                 
00116          10  PV-CURRENT-CC              PIC  9(02)   VALUE ZEROES.        
00117          10  PV-CURRENT-YY              PIC  9(02)   VALUE ZEROES.        
00118          10  PV-DASH-1                  PIC  X(01)   VALUE SPACES.        
00119          10  PV-CURRENT-MM              PIC  9(02)   VALUE ZEROES.        
00120          10  PV-DASH-2                  PIC  X(01)   VALUE SPACES.        
00121          10  PV-CURRENT-DD              PIC  9(02)   VALUE ZEROES.        
00122      05  PV-DATE-60-DAYS-AGO            PIC  X(10)   VALUE SPACES.        
00123      05  PV-SQL-SUB                     PIC S9(4)    COMP.                
00124      05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
00125      05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
00126      05  PV-COUNTER                     PIC  9(04)   VALUE ZEROES.        
00127      05  PV-TODAYS-DATE.                                                  
00128          10  PV-TODAYS-YY               PIC  9(02)   VALUE ZEROES.        
00129          10  PV-TODAYS-MM               PIC  9(02)   VALUE ZEROES.        
00130          10  PV-TODAYS-DD               PIC  9(02)   VALUE ZEROES.        
00131      05  PV-TODAYS-DATE-GREG.                                             
00132          10  PV-TODAYS-MM-GREG          PIC  9(02)   VALUE ZEROES.        
00133          10  PV-TODAYS-DD-GREG          PIC  9(02)   VALUE ZEROES.        
00134          10  PV-TODAYS-YY-GREG          PIC  9(02)   VALUE ZEROES.        
00135 *                                                                         
00136  01  PS-PROGRAM-SWITCHES.                                                 
00137      05  PS-END-OF-COMPLETED-SW         PIC  X(01)   VALUE 'N'.           
00138          88  PS-END-OF-COMPLETED                     VALUE 'Y'.           
00139          88  PS-NOT-END-OF-COMPLETED                 VALUE 'N'.           
00140 *                                                                         
00141  01  RC-TABLE-ROW-CNTRS.                                                  
00142      05  RC-STRPPC-ROW-READ-CNTR        PIC S9(08)   VALUE +0             
00143                                                      COMP-3.              
00144      05  RC-SPPCDT-ROW-READ-CNTR        PIC S9(08)   VALUE +0             
00145                                                      COMP-3.              
00146      05  RC-SPCDTH-ROW-INSERT-CNTR      PIC S9(08)   VALUE +0             
00147                                                      COMP-3.              
00148      05  RC-STRPCH-ROW-INSERT-CNTR      PIC S9(08)   VALUE +0             
00149                                                      COMP-3.              
00150 *                                                                         
00151  01  DI-TABLE-ROW-CNTRS.                                                  
00152      05  DI-STRPPC-ROW-READ-CNTR        PIC  Z(07)9  VALUE ZEROS.         
00153      05  DI-SPPCDT-ROW-READ-CNTR        PIC  Z(07)9  VALUE ZEROS.         
00154      05  DI-STRPCH-ROW-INSERT-CNTR      PIC  Z(07)9  VALUE ZEROS.         
00155      05  DI-SPCDTH-ROW-INSERT-CNTR      PIC  Z(07)9  VALUE ZEROS.         
00159 *****************************************************************         
00160 *    DB2 IN-STORE PERMANENT PRICE CHANGE HEADER TABLE                     
00161 *****************************************************************         
00162      EXEC SQL                                                             
00163          INCLUDE TSTRPPC                                                  
00164      END-EXEC.                                                            
00165                                                                           
00166      EXEC SQL                                                             
00167        DECLARE HEADER-CURSOR CURSOR WITH HOLD FOR                         
00169            SELECT DEST_LOC_NBR,                                           
00170                   DOC_NBR,                                                
00171                   DOC_TYP_CDE,                                            
00172                   EFF_DTE,                                                
00173                   PR_CHG_CPL_NAME,                                        
00174                   RSN_CDE,                                                
00175                   TYP_CDE,                                                
00176                   STATUS_CDE,                                             
00177                   PREV_STATUS_CDE,                                        
00178                   DEPT_AREA_DEST,                                         
00179                   ORGN_LOC_NBR,                                           
00180                   CRE_DTE,                                                
00181                   CRE_ID,                                                 
00182                   CAN_DTE,                                                
00183                   CAN_ID,                                                 
00184                   ENTR_DTE,                                               
00185                   ENTR_TIME,                                              
00186                   ENTR_ID,                                                
00187                   MGMT_APPRVL_DTE,                                        
00188                   MGMT_APPRVL_ID,                                         
00189                   MGMT_APPRVL_NAME,                                       
00190                   REJ_DTE,                                                
00191                   REJ_ID,                                                 
00192                   CPL_DTE,                                                
00193                   CPL_TIME,                                               
00194                   TOT_ACT_PR_CHG_AMT,                                     
00195                   PGS_TO_PRT_NBR                                          
00196              FROM TSTRPPC                                                 
00198             WHERE (DOC_TYP_CDE                                            
00199                        =  :PE049-STORE-DOC-TYPE-CDE                       
00200               AND  (STATUS_CDE                                            
00201                        =  :PE049-IS-COMPLETED-STATUS-CDE                  
00202                 AND CPL_DTE                                               
00203                        <= :PV-DATE-60-DAYS-AGO)                           
00204                OR  (STATUS_CDE                                            
00205                        =  :PE049-IS-CANCELLED-STATUS-CDE                  
00206                 AND CAN_DTE                                               
00207                        <= :PV-DATE-60-DAYS-AGO))                          
00208      END-EXEC.                                                            
00211 *****************************************************************         
00212 *    DB2 IN-STORE PERMANENT PRICE CHANGE DETAIL TABLE                     
00213 *****************************************************************         
00215      EXEC SQL                                                             
00216          INCLUDE TSPPCDT                                                  
00217      END-EXEC.                                                            
00218                                                                           
00219      EXEC SQL                                                             
00220        DECLARE DETAIL-CURSOR CURSOR FOR                                   
00222          SELECT DEST_LOC_NBR,                                             
00223                 DOC_NBR,                                                  
W24472                SKU_NBR,                                                  
00227                 ROW_NBR,                                                  
00228                 ACTL_PR_CHG_QTY,                                          
00229                 OLD_PR,                                                   
00230                 NEW_PR,                                                   
00231                 ACTL_PR_CHG_AMT,                                          
00232                 UPC_NBR,                                                  
00233                 ITEM_NBR,                                                 
                      DEPT_NBR,                                                 
                      MAJ_CL_NBR,                                               
                      SUB_CL_NBR                                                
00234        FROM TSPPCDT                                                       
00236        WHERE (DEST_LOC_NBR = :STRPPC-DEST-LOC-NBR                         
00237          AND  DOC_NBR      = :STRPPC-DOC-NBR)                             
00238      END-EXEC.                                                            
00241 *****************************************************************         
00242 *    DB2 IN-STORE HISTORY PERMANENT PRICE CHANGE HEADER TABLE             
00243 *****************************************************************         
00244      EXEC SQL                                                             
00245          INCLUDE TSTRPCH                                                  
00246      END-EXEC.                                                            
00249 *****************************************************************         
00250 *    DB2 IN-STORE HISTORY PERMANENT PRICE CHANGE DETAIL TABLE             
00251 *****************************************************************         
00252      EXEC SQL                                                             
00253          INCLUDE TSPCDTH                                                  
00254      END-EXEC.                                                            
00257 *****************************************************************         
00258 *    DB2 COMMUNICATION AREA.                                              
00259 *****************************************************************         
00260                                                                           
00261      EXEC SQL                                                             
00262          INCLUDE SQLCA                                                    
00263      END-EXEC.                                                            
00264 *                                                                         
00265  EJECT                                                                    
00266 *                                                                         
00267 *                                                                         
00268 *                                                                         
00269 *****************************************************************         
00270  PROCEDURE DIVISION.                                                      
00271 *****************************************************************         
00272                                                                           
00273 ******************************************************************        
00274 *A100-MAIN-MODULE                                                *        
00275 *  ==> PROCESSES:                                                *        
00276 *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
00277 *  ==> PERFORMED FROM:                                           *        
00278 *                     HIGHEST LEVEL IN PROGRAM                   *        
00279 ******************************************************************        
00280                                                                           
00281  A100-MAIN-MODULE.                                                        
00282                                                                           
00283      PERFORM B100-PRE-PROCESSING.                                         
00284                                                                           
00285      PERFORM B200-PROCESS-TABLE.                                          
00286                                                                           
00287      PERFORM Z940-DISPLAY-CONTROL-INFO.                                   
00288                                                                           
00289      STOP RUN.                                                            
00290 *                                                                         
00291  EJECT                                                                    
00292 ******************************************************************        
00293 *B100-PRE-PROCESSING                                             *        
00294 *  ==> PROCESSES:                                                *        
00295 *    - LOCKS THE TABLES                                          *        
00296 *    - CALCULATE DATES TO BE USED FOR EXTRACTION OF COMPLETED    *        
00297 *      AND CANCELLED DOCUMENTS.                                  *        
00298 *  ==> PERFORMED FROM:                                           *        
00299 *                     A100-MAIN-MODULE.                          *        
00300 ******************************************************************        
00301  B100-PRE-PROCESSING.                                                     
00302                                                                           
00303      PERFORM X900-LOCK-TABLES.                                            
00304                                                                           
00305      ACCEPT PV-TODAYS-DATE                                                
00306          FROM DATE.                                                       
00307      MOVE PV-TODAYS-DATE         TO DPG51-SYSTEM-DATE.                    
00308                                                                           
00309      MOVE PV-TODAYS-YY           TO PV-TODAYS-YY-GREG.                    
00310      MOVE PV-TODAYS-MM           TO PV-TODAYS-MM-GREG.                    
00311      MOVE PV-TODAYS-DD           TO PV-TODAYS-DD-GREG.                    
00312                                                                           
00313      MOVE PV-TODAYS-DATE-GREG    TO DPG52-LK-DATE-INPUT.                  
00314      SET DPG52-LK-DTE-GREG                                                
00315          DPG51-ACTUAL-CALENDAR-ONLY                                       
00316          DPG51-DO-NOT-INCR-DECR-DATE                                      
00317                                  TO TRUE.                                 
00318      PERFORM Y100-CALL-CALENDAR.                                          
00319      MOVE DPG55-DB2-ISO-DATE-FMT TO PV-CURRENT-DATE.                      
00320 *                                                                         
00321      SET DPG52-LK-DTE-ISO-FMT                                             
00322          DPG51-ACTUAL-CALENDAR-ONLY                                       
00323          DPG51-DECREMENT-DATE    TO TRUE.                                 
00324      MOVE PC-60-DAYS             TO DPG51-INCR-DECR-DAYS-9.               
00325      MOVE PV-CURRENT-DATE        TO DPG52-LK-DATE-INPUT.                  
00326                                                                           
00327      PERFORM Y100-CALL-CALENDAR.                                          
00328      MOVE DPG55-DB2-ISO-DATE-FMT TO PV-DATE-60-DAYS-AGO.                  
00329                                                                           
00330 *                                                                         
00331  EJECT                                                                    
00332 ******************************************************************        
00333 *B200-PROCESS-TABLE                                              *        
00334 *  ==> PROCESSES:                                                *        
00335 *    - PROCESSES ALL DOCUMENTS THAT HAVE BEEN COMPLETED OR WERE  *        
00336 *      CANCELLED MORE THAN 60 DAYS AGO                           *        
00337 *  ==> PERFORMED FROM: A100-MAIN-MODULE                          *        
00338 ******************************************************************        
00339  B200-PROCESS-TABLE.                                                      
00340                                                                           
00341      MOVE ZERO TO PV-COUNTER.                                             
00342                                                                           
00343      PERFORM M600-OPEN-COMPLETED-CURSOR.                                  
00344      PERFORM C100-PROCESS-COMPLETED-DOC.                                  
00345      PERFORM M700-CLOSE-COMPLETED-CURSOR.                                 
00346 *                                                                         
00347  EJECT                                                                    
00348 ******************************************************************        
00349 *C100-PROCESS-COMPLETED-DOC                                      *        
00350 *  ==> PROCESSES:                                                *        
00351 *    - DETERMINES IF A DOCUMENT HAS BEEN COMPLETED OR CANCELLED  *        
00352 *      FOR 60 DAYS OR GREATER                                    *        
00353 *    - INSERTS RECORD INTO HISTORY                               *        
00354 *    - THERE IS A COUNTER USED TO DETERMINE WHEN A RECORD IS     *        
00355 *      COMMITTED.  THE CURSOR NEEDS TO BE REOPENED AFTER THE     *        
00356 *      COMMIT BECAUSE A COMMIT WILL CLOSE THE ANY CURSORS THAT   *        
00357 *      ARE OPEN.                                                 *        
00358 *    - DELETES RECORD OFF OF CURRENT                             *        
00359 *    - COMMITS THE CHANGES                                       *        
00360 *   ==> PERFORMED FROM: B200-PROCESS-TABLE                       *        
00361 ******************************************************************        
00362  C100-PROCESS-COMPLETED-DOC.                                              
00363                                                                           
00364      PERFORM                                                              
00365         UNTIL PS-END-OF-COMPLETED                                         
00366                                                                           
00367         PERFORM M500-READ-CENT-PC                                         
00368         EVALUATE TRUE                                                     
00369             WHEN SQLCODE = ZERO                                           
00370                 ADD 1            TO RC-STRPPC-ROW-READ-CNTR               
00371                                     PV-COUNTER                            
00372                 PERFORM D210-CREATE-COMPLETED-HISTORY                     
00373                 PERFORM M100-DELETE-CURRENT-DOC                           
00374                 IF PV-COUNTER > PC-MAX-DOCUMENTS                          
00375                     PERFORM M200-COMMIT-DOCUMENT                          
00376 * REM 6/3/02 JK     PERFORM M600-OPEN-COMPLETED-CURSOR                    
00377                 END-IF                                                    
00378             WHEN SQLCODE = PC-ROW-NOT-FOUND                               
00379                 SET PS-END-OF-COMPLETED                                   
00380                                  TO TRUE                                  
00381             WHEN OTHER                                                    
00382                 MOVE 'C100-PROCESS-COMPLETED-DOC'                         
00383                                  TO PV-PARAGRAPH-NAME                     
00384                 MOVE 'FETCH COMPLETE DOC'                                 
00385                                  TO PV-DB2-OPERATION-ATTEMPTED            
00386                 PERFORM Z950-SQL-ABEND                                    
00387         END-EVALUATE                                                      
00388                                                                           
00389      END-PERFORM.                                                         
00390 *                                                                         
00391  EJECT                                                                    
00392 ******************************************************************        
00393 *D210-CREATE-COMPLETED-HISTORY                                   *        
00394 *  ==> PROCESSES:                                                *        
00395 *    - CONTROLS CREATION OF HISTORY FOR THE IN-STORE PRICE CHANGE*        
00396 *      DOCUMENTS                                                 *        
00397 *  ==> PERFORMED FROM:                                           *        
00398 *                     D100-CREATE-HISTORY-DOCS                   *        
00399 ******************************************************************        
00400  D210-CREATE-COMPLETED-HISTORY.                                           
00401                                                                           
00402      PERFORM M300-INSERT-HIST-PC.                                         
00403                                                                           
00404      PERFORM E400-CREATE-HISTORY-DETAIL.                                  
00405 *                                                                         
00406  EJECT                                                                    
00407 ******************************************************************        
00408 *E400-CREATE-HISTORY-DETAIL.                                     *        
00409 *  ==> PROCESSES:                                                *        
00410 *    - OPEN CURSOR FOR THE DETAIL TABLE                          *        
00411 *    - PROCESS ALL DETAIL FOR THE DOCUMENT                       *        
00412 *    - CLOSES CURSOR FOR THE DETAIL TABLE                        *        
00413 *  ==> PERFORMED FROM:                                           *        
00414 *                D210-CREATE-COMPLETED-HISTORY                   *        
00415 ******************************************************************        
00416  E400-CREATE-HISTORY-DETAIL.                                              
00417      PERFORM M650-OPEN-DETAIL.                                            
00418      PERFORM F400-BUILD-DETAIL-HIST.                                      
00419      PERFORM M750-CLOSE-DETAIL.                                           
00420 *                                                                         
00421  EJECT                                                                    
00422 ******************************************************************        
00423 *F400-BUILD-DETAIL-HIST.                                         *        
00424 *  ==> PROCESSES:                                                *        
00425 *    - CONTROLS THE CREATIONS OF HISTORY ROWS FOR THE DETAIL     *        
00426 *      TABLE                                                     *        
00427 *  ==> PERFORMED FROM:                                           *        
00428 *                     E400-CREATE-HISTORY-DETAIL.                         
00429 ******************************************************************        
00430  F400-BUILD-DETAIL-HIST.                                                  
00431                                                                           
00432      PERFORM                                                              
00433          UNTIL SQLCODE = PC-ROW-NOT-FOUND                                 
00434                                                                           
00435          EXEC SQL                                                         
00436            FETCH DETAIL-CURSOR                                            
00438            INTO :SPPCDT-DEST-LOC-NBR,                                     
00439                 :SPPCDT-DOC-NBR,                                          
W24472                :SPPCDT-SKU-NBR,                                          
00443                 :SPPCDT-ROW-NBR,                                          
00444                 :SPPCDT-ACTL-PR-CHG-QTY,                                  
00445                 :SPPCDT-OLD-PR,                                           
00446                 :SPPCDT-NEW-PR,                                           
00447                 :SPPCDT-ACTL-PR-CHG-AMT,                                  
00448                 :SPPCDT-UPC-NBR,                                          
00449                 :SPPCDT-ITEM-NBR,                                         
                      :SPPCDT-DEPT-NBR,                                         
                      :SPPCDT-MAJ-CL-NBR,                                       
                      :SPPCDT-SUB-CL-NBR                                        
00450           END-EXEC                                                        
00451                                                                           
00452           EVALUATE TRUE                                                   
00453               WHEN SQLCODE = PC-ROW-NOT-FOUND                             
00454                   CONTINUE                                                
00455               WHEN SQLCODE = ZERO                                         
00456                   ADD +1         TO RC-SPPCDT-ROW-READ-CNTR               
00457                   PERFORM M400-INSERT-HIST-DETAIL                         
00458               WHEN SQLWARN0 NOT = SPACE                                   
00459               WHEN SQLCODE NOT = ZERO                                     
00460                   MOVE 'F400-BUILD-HISTORY-DETAIL'                        
00461                                  TO PV-PARAGRAPH-NAME                     
00462                   MOVE 'SELECT INTO TSPPCDT TABLE '                       
00463                                  TO PV-DB2-OPERATION-ATTEMPTED            
00464                   PERFORM Z950-SQL-ABEND                                  
00465           END-EVALUATE                                                    
00466       END-PERFORM.                                                        
00467 *                                                                         
00468  EJECT                                                                    
00469 ******************************************************************        
00470 *M100-DELETE-CURRENT-REC                                         *        
00471 *  ==> PROCESSES:                                                *        
00472 *    - DELETES THE HEADER AND ANY OTHER RECORD ASSCOIATED WITH   *        
00473 *      THE HEADER RECORD.                                        *        
00474 *  ==> PERFORMED FROM:                                           *        
00475 ******************************************************************        
00476  M100-DELETE-CURRENT-DOC.                                                 
00477                                                                           
00478      EXEC SQL                                                             
00479          DELETE                                                           
00480          FROM TSTRPPC                                                     
00482          WHERE (DEST_LOC_NBR = :STRPPC-DEST-LOC-NBR                       
00483            AND  DOC_NBR      = :STRPPC-DOC-NBR                            
00484            AND  DOC_TYP_CDE                                               
00485                            = :PE049-STORE-DOC-TYPE-CDE)                   
00486      END-EXEC.                                                            
00487                                                                           
00488      EVALUATE TRUE                                                        
00489          WHEN SQLCODE = ZERO                                              
00490              CONTINUE                                                     
00491          WHEN OTHER                                                       
00492              MOVE 'M100-DELETE-CURRENT-DOC'                               
00493                                  TO PV-PARAGRAPH-NAME                     
00494              MOVE 'DELETE DOCUMENT INFO'                                  
00495                                  TO PV-DB2-OPERATION-ATTEMPTED            
00496              PERFORM Z950-SQL-ABEND                                       
00497      END-EVALUATE.                                                        
00498 *                                                                         
00499  EJECT                                                                    
00500 ******************************************************************        
00501 *M200-COMMIT-DOCUMENT                                            *        
00502 *  ==> PROCESSES:                                                *        
00503 *    - COMMITS THE CHANGES DONE FOR A DOCUMENT                   *        
00504 *  ==> PERFORMED FROM:                                           *        
00505 ******************************************************************        
00506  M200-COMMIT-DOCUMENT.                                                    
00507                                                                           
00508      EXEC SQL                                                             
00509          COMMIT                                                           
00510      END-EXEC.                                                            
00511                                                                           
00512      EVALUATE TRUE                                                        
00513          WHEN SQLCODE = ZERO                                              
00514              CONTINUE                                                     
00515          WHEN OTHER                                                       
00516              MOVE 'M200-COMMIT-DOCUMENT'                                  
00517                                  TO PV-PARAGRAPH-NAME                     
00518              MOVE 'DB2 COMMIT'   TO PV-DB2-OPERATION-ATTEMPTED            
00519              PERFORM Z950-SQL-ABEND                                       
00520      END-EVALUATE.                                                        
00521 *                                                                         
00522  EJECT                                                                    
00523 ******************************************************************        
00524 *M300-INSERT-HIST-PC.                                            *        
00525 *  ==> PROCESSES:                                                *        
00526 *    - INSERTS THE IN-STORE DOCUMENT HEADER HISTORY ROW          *        
00527 *  ==> PERFORMED FROM:                                           *        
00528 *                     D210-CREATE-COMPLETED-HISTORY              *        
00529 ******************************************************************        
00530  M300-INSERT-HIST-PC.                                                     
00531                                                                           
00532      MOVE STRPPC-STATUS-CDE      TO STRPPC-PREV-STATUS-CDE.               
00533      MOVE PE049-IS-HISTORY-STATUS-CDE                                     
00534                                  TO STRPPC-STATUS-CDE.                    
00535                                                                           
00536       EXEC SQL                                                            
00537          INSERT INTO TSTRPCH                                              
00539                  (DEST_LOC_NBR,                                           
00540                   DOC_NBR,                                                
00541                   DOC_TYP_CDE,                                            
00542                   EFF_DTE,                                                
00543                   PR_CHG_CPL_NAME,                                        
00544                   RSN_CDE,                                                
00545                   TYP_CDE,                                                
00546                   STATUS_CDE,                                             
00547                   PREV_STATUS_CDE,                                        
00548                   DEPT_AREA_DEST,                                         
00549                   ORGN_LOC_NBR,                                           
00550                   CRE_DTE,                                                
00551                   CRE_ID,                                                 
00552                   CAN_DTE,                                                
00553                   CAN_ID,                                                 
00554                   ENTR_DTE,                                               
00555                   ENTR_TIME,                                              
00556                   ENTR_ID,                                                
00557                   MGMT_APPRVL_DTE,                                        
00558                   MGMT_APPRVL_ID,                                         
00559                   MGMT_APPRVL_NAME,                                       
00560                   REJ_DTE,                                                
00561                   REJ_ID,                                                 
00562                   CPL_DTE,                                                
00563                   CPL_TIME,                                               
00564                   TOT_ACT_PR_CHG_AMT,                                     
00565                   PGS_TO_PRT_NBR)                                         
00567          VALUES (:STRPPC-DEST-LOC-NBR,                                    
00568                  :STRPPC-DOC-NBR,                                         
00569                  :STRPPC-DOC-TYP-CDE,                                     
00570                  :STRPPC-EFF-DTE,                                         
00571                  :STRPPC-PR-CHG-CPL-NAME,                                 
00572                  :STRPPC-RSN-CDE,                                         
00573                  :STRPPC-TYP-CDE,                                         
00574                  :STRPPC-STATUS-CDE,                                      
00575                  :STRPPC-PREV-STATUS-CDE,                                 
00576                  :STRPPC-DEPT-AREA-DEST,                                  
00577                  :STRPPC-ORGN-LOC-NBR,                                    
00578                  :STRPPC-CRE-DTE,                                         
00579                  :STRPPC-CRE-ID,                                          
00580                  :STRPPC-CAN-DTE,                                         
00581                  :STRPPC-CAN-ID,                                          
00582                  :STRPPC-ENTR-DTE,                                        
00583                  :STRPPC-ENTR-TIME,                                       
00584                  :STRPPC-ENTR-ID,                                         
00585                  :STRPPC-MGMT-APPRVL-DTE,                                 
00586                  :STRPPC-MGMT-APPRVL-ID,                                  
00587                  :STRPPC-MGMT-APPRVL-NAME,                                
00588                  :STRPPC-REJ-DTE,                                         
00589                  :STRPPC-REJ-ID,                                          
00590                  :STRPPC-CPL-DTE,                                         
00591                  :STRPPC-CPL-TIME,                                        
00592                  :STRPPC-TOT-ACT-PR-CHG-AMT,                              
00593                  :STRPPC-PGS-TO-PRT-NBR)                                  
00594       END-EXEC.                                                           
00595                                                                           
00596       IF  SQLCODE = 0                                                     
00597           ADD +1                 TO RC-STRPCH-ROW-INSERT-CNTR             
00598                                                                           
00599       ELSE                                                                
00600           MOVE 'M300-INSERT-HIST-PC '                                     
00601                                  TO PV-PARAGRAPH-NAME                     
00602           MOVE 'INSERT FOR TABLE TSTRPCH '                                
00603                                  TO PV-DB2-OPERATION-ATTEMPTED            
00604           PERFORM Z950-SQL-ABEND                                          
00605       END-IF.                                                             
00606 *                                                                         
00607  EJECT                                                                    
00608 ******************************************************************        
00609 *M400-INSERT-HIST-DETAIL.                                                 
00610 *  ==> PROCESSES:                                                *        
00611 *    - INSERTS THE HISTORY DETAIL ROWS                           *        
00612 *  ==> PERFORMED FROM:                                           *        
00613 *                     F400-BUILD-HISTORY-DETAIL                  *        
00614 ******************************************************************        
00615  M400-INSERT-HIST-DETAIL.                                                 
00616                                                                           
00617      EXEC SQL                                                             
00618          INSERT                                                           
00619             INTO TSPCDTH                                                  
00621              (DEST_LOC_NBR,                                               
00622               DOC_NBR,                                                    
W24472              SKU_NBR,                                                    
00626               ROW_NBR,                                                    
00627               ACTL_PR_CHG_QTY,                                            
00628               OLD_PR,                                                     
00629               NEW_PR,                                                     
00630               ACTL_PR_CHG_AMT,                                            
00631               UPC_NBR,                                                    
00632               ITEM_NBR,                                                   
                    DEPT_NBR,                                                   
                    MAJ_CL_NBR,                                                 
                    SUB_CL_NBR)                                                 
00633          VALUES                                                           
00635                (:SPPCDT-DEST-LOC-NBR,                                     
00636                 :SPPCDT-DOC-NBR,                                          
W24472                :SPPCDT-SKU-NBR,                                          
00640                 :SPPCDT-ROW-NBR,                                          
00641                 :SPPCDT-ACTL-PR-CHG-QTY,                                  
00642                 :SPPCDT-OLD-PR,                                           
00643                 :SPPCDT-NEW-PR,                                           
00644                 :SPPCDT-ACTL-PR-CHG-AMT,                                  
00645                 :SPPCDT-UPC-NBR,                                          
00646                 :SPPCDT-ITEM-NBR,                                         
                      :SPPCDT-DEPT-NBR,                                         
                      :SPPCDT-MAJ-CL-NBR,                                       
                      :SPPCDT-SUB-CL-NBR)                                       
00647      END-EXEC.                                                            
00648                                                                           
00649      EVALUATE TRUE                                                        
00650          WHEN SQLCODE = ZERO                                              
00651             ADD +1               TO RC-SPCDTH-ROW-INSERT-CNTR             
00652          WHEN OTHER                                                       
00653              MOVE 'M400-INSERT-HIST-DETAIL'                               
00654                                  TO PV-PARAGRAPH-NAME                     
00655              MOVE 'INSERT INTO TSPCDTH TABLE '                            
00656                                  TO PV-DB2-OPERATION-ATTEMPTED            
00657              PERFORM Z950-SQL-ABEND                                       
00658      END-EVALUATE.                                                        
00659 *                                                                         
00660  EJECT                                                                    
00661 ******************************************************************        
00662 *M500-READ-CENT-PC                                               *        
00663 *  ==> PROCESSES:                                                *        
00664 *    - OPENS CURSOR FOR CENRAL PRICE CHANGE DOCUMENTS AND        *        
00665 *    - READS FIRST ROW                                           *        
00666 *  ==> PERFORMED FROM:                                           *        
00667 *                     B100-PRE-PROCESSING                        *        
00668 ******************************************************************        
00669  M500-READ-CENT-PC.                                                       
00670                                                                           
00671      EXEC SQL                                                             
00672          FETCH HEADER-CURSOR                                              
00674            INTO  :STRPPC-DEST-LOC-NBR,                                    
00675                  :STRPPC-DOC-NBR,                                         
00676                  :STRPPC-DOC-TYP-CDE,                                     
00677                  :STRPPC-EFF-DTE,                                         
00678                  :STRPPC-PR-CHG-CPL-NAME,                                 
00679                  :STRPPC-RSN-CDE,                                         
00680                  :STRPPC-TYP-CDE,                                         
00681                  :STRPPC-STATUS-CDE,                                      
00682                  :STRPPC-PREV-STATUS-CDE,                                 
00683                  :STRPPC-DEPT-AREA-DEST,                                  
00684                  :STRPPC-ORGN-LOC-NBR,                                    
00685                  :STRPPC-CRE-DTE,                                         
00686                  :STRPPC-CRE-ID,                                          
00687                  :STRPPC-CAN-DTE,                                         
00688                  :STRPPC-CAN-ID,                                          
00689                  :STRPPC-ENTR-DTE,                                        
00690                  :STRPPC-ENTR-TIME,                                       
00691                  :STRPPC-ENTR-ID,                                         
00692                  :STRPPC-MGMT-APPRVL-DTE,                                 
00693                  :STRPPC-MGMT-APPRVL-ID,                                  
00694                  :STRPPC-MGMT-APPRVL-NAME,                                
00695                  :STRPPC-REJ-DTE,                                         
00696                  :STRPPC-REJ-ID,                                          
00697                  :STRPPC-CPL-DTE,                                         
00698                  :STRPPC-CPL-TIME,                                        
00699                  :STRPPC-TOT-ACT-PR-CHG-AMT,                              
00700                  :STRPPC-PGS-TO-PRT-NBR                                   
00701      END-EXEC.                                                            
00702 *                                                                         
00703  EJECT                                                                    
00704 ******************************************************************        
00705 *M600-OPEN-COMPLETED-CURSOR                                      *        
00706 *  ==> PROCESSES:                                                *        
00707 *    - OPENS COMPLETED DOCUMENTS CURSOR                          *        
00708 *  ==> PERFORMED FROM:                                           *        
00709 ******************************************************************        
00710  M600-OPEN-COMPLETED-CURSOR.                                              
00711                                                                           
00712      EXEC SQL                                                             
00713          OPEN HEADER-CURSOR                                               
00714      END-EXEC.                                                            
00715      EVALUATE TRUE                                                        
00716          WHEN SQLCODE = ZERO                                              
00717              CONTINUE                                                     
00718          WHEN OTHER                                                       
00719              MOVE 'M600-OPEN-COMPLETED-CURSOR'                            
00720                                  TO PV-PARAGRAPH-NAME                     
00721              MOVE 'OPEN CP/CA HEADER CURSOR'                              
00722                                  TO PV-DB2-OPERATION-ATTEMPTED            
00723              PERFORM Z950-SQL-ABEND                                       
00724      END-EVALUATE.                                                        
00725 *                                                                         
00726  EJECT                                                                    
00727 ******************************************************************        
00728 *M650-OPEN-DETAIL                                                *        
00729 *  ==> PROCESSES:                                                *        
00730 *    - OPENS DETAIL TABLE CURSOR                                 *        
00731 *  ==> PERFORMED FROM:                                           *        
00732 ******************************************************************        
00733  M650-OPEN-DETAIL.                                                        
00734                                                                           
00735      EXEC SQL                                                             
00736          OPEN DETAIL-CURSOR                                               
00737      END-EXEC.                                                            
00738                                                                           
00739      EVALUATE TRUE                                                        
00740          WHEN SQLCODE = ZERO                                              
00741              CONTINUE                                                     
00742          WHEN OTHER                                                       
00743              MOVE 'M650-OPEN-DETAIL'                                      
00744                                  TO PV-PARAGRAPH-NAME                     
00745              MOVE 'OPEN CURSOR TSPPCDT TABLE '                            
00746                                  TO PV-DB2-OPERATION-ATTEMPTED            
00747              PERFORM Z950-SQL-ABEND                                       
00748      END-EVALUATE.                                                        
00749 *                                                                         
00750  EJECT                                                                    
00751 ******************************************************************        
00752 *M700-CLOSE-COMPLETED-CURSOR                                     *        
00753 *  ==> PROCESSES:                                                *        
00754 *    - CLOSES COMPLETED DOCUMENTS CURSOR                         *        
00755 *  ==> PERFORMED FROM:                                           *        
00756 ******************************************************************        
00757  M700-CLOSE-COMPLETED-CURSOR.                                             
00758                                                                           
00759      EXEC SQL                                                             
00760          CLOSE HEADER-CURSOR                                              
00761      END-EXEC.                                                            
00762 *                                                                         
00763      EVALUATE TRUE                                                        
00764          WHEN SQLCODE = ZERO                                              
00765              CONTINUE                                                     
00766          WHEN OTHER                                                       
00767              MOVE 'M700-CLOSE-COMPLETED-CURSOR'                           
00768                                  TO PV-PARAGRAPH-NAME                     
00769              MOVE 'CLOSE CP/CA HEADER CURSOR'                             
00770                                  TO PV-DB2-OPERATION-ATTEMPTED            
00771              PERFORM Z950-SQL-ABEND                                       
00772      END-EVALUATE.                                                        
00773 *                                                                         
00774  EJECT                                                                    
00775 ******************************************************************        
00776 *M750-CLOSE-DETAIL                                               *        
00777 *  ==> PROCESSES:                                                *        
00778 *    - CLOSED DETAIL TABLE CURSOR                                *        
00779 *  ==> PERFORMED FROM:                                           *        
00780 ******************************************************************        
00781  M750-CLOSE-DETAIL.                                                       
00782                                                                           
00783       EXEC SQL                                                            
00784           CLOSE DETAIL-CURSOR                                             
00785       END-EXEC.                                                           
00786                                                                           
00787      EVALUATE TRUE                                                        
00788          WHEN SQLCODE = ZERO                                              
00789              CONTINUE                                                     
00790          WHEN OTHER                                                       
00791              MOVE 'M750-CLOSE-DETAIL'                                     
00792                                  TO PV-PARAGRAPH-NAME                     
00793              MOVE 'CLOSE CURSOR TSPPCDT TABLE '                           
00794                                  TO PV-DB2-OPERATION-ATTEMPTED            
00795              PERFORM Z950-SQL-ABEND                                       
00796      END-EVALUATE.                                                        
00797 *                                                                         
00798  EJECT                                                                    
00799 ******************************************************************        
00800 *X900-LOCK-TABLES                                                *        
00801 *  ==> PROCESSES:                                                *        
00802 *    - LOCKS ALL TABLES THAT WILL BE UPDATED IN THE PROCESS FOR  *        
00803 *      ONE  DOCUMENT                                             *        
00804 *  ==> PERFORMED FROM:                                           *        
00805 ******************************************************************        
00806  X900-LOCK-TABLES.                                                        
00807                                                                           
00808      EXEC SQL                                                             
00809         LOCK TABLE TSTRPPC                                                
00810             IN EXCLUSIVE MODE                                             
00811      END-EXEC.                                                            
00812      EVALUATE TRUE                                                        
00813          WHEN SQLCODE = ZERO                                              
00814              CONTINUE                                                     
00815          WHEN OTHER                                                       
00816              MOVE 'X900-LOCK-TABLES '                                     
00817                                  TO PV-PARAGRAPH-NAME                     
00818              MOVE 'LOCK TSTRPPC TABLE '                                   
00819                                  TO PV-DB2-OPERATION-ATTEMPTED            
00820              PERFORM Z950-SQL-ABEND                                       
00821      END-EVALUATE.                                                        
00822 *                                                                         
00823      EXEC SQL                                                             
00824         LOCK TABLE TSPPCDT                                                
00825             IN EXCLUSIVE MODE                                             
00826      END-EXEC.                                                            
00827      EVALUATE TRUE                                                        
00828          WHEN SQLCODE = ZERO                                              
00829              CONTINUE                                                     
00830          WHEN OTHER                                                       
00831              MOVE 'X900-LOCK-TABLES '                                     
00832                                  TO PV-PARAGRAPH-NAME                     
00833              MOVE 'LOCK TSPPCDT TABLE '                                   
00834                                  TO PV-DB2-OPERATION-ATTEMPTED            
00835              PERFORM Z950-SQL-ABEND                                       
00836      END-EVALUATE.                                                        
00837 *                                                                         
00838      EXEC SQL                                                             
00839         LOCK TABLE TSTRPCH                                                
00840             IN EXCLUSIVE MODE                                             
00841      END-EXEC.                                                            
00842      EVALUATE TRUE                                                        
00843          WHEN SQLCODE = ZERO                                              
00844              CONTINUE                                                     
00845          WHEN OTHER                                                       
00846              MOVE 'X900-LOCK-TABLES '                                     
00847                                  TO PV-PARAGRAPH-NAME                     
00848              MOVE 'LOCK TSTRPCH TABLE '                                   
00849                                  TO PV-DB2-OPERATION-ATTEMPTED            
00850              PERFORM Z950-SQL-ABEND                                       
00851      END-EVALUATE.                                                        
00852 *                                                                         
00853      EXEC SQL                                                             
00854         LOCK TABLE TSPCDTH                                                
00855             IN EXCLUSIVE MODE                                             
00856      END-EXEC.                                                            
00857      EVALUATE TRUE                                                        
00858          WHEN SQLCODE = ZERO                                              
00859              CONTINUE                                                     
00860          WHEN OTHER                                                       
00861              MOVE 'X900-LOCK-TABLES '                                     
00862                                  TO PV-PARAGRAPH-NAME                     
00863              MOVE 'LOCK TSPCDTH TABLE '                                   
00864                                  TO PV-DB2-OPERATION-ATTEMPTED            
00865              PERFORM Z950-SQL-ABEND                                       
00866      END-EVALUATE.                                                        
00867 *                                                                         
00868  EJECT                                                                    
00869 ******************************************************************        
00870 *Y100-CALL-CALENDAR                                              *        
00871 *  ==> PROCESSES:                                                *        
00872 *    - PERFORMS THE CALL TO THE DATE CALCULATION MODULE, DPKUT500*        
00873 *  ==> PERFORMED FROM:                                           *        
00874 *                 C115-CURR-DATE-TO-DATE-BEFORE                  *        
00875 ******************************************************************        
00876  Y100-CALL-CALENDAR.                                                      
00877                                                                           
00878                                                                           
00879      CALL PC-CALENDAR-PGM USING DPG51 DPG52                               
00880                                 DPG53 DPG54                               
00881                                 DPG55 DPG56.                              
00882                                                                           
00883      EVALUATE TRUE                                                        
00884          WHEN DPG54-SEVERE-ERROR                                          
00885          WHEN DPG54-DATE-INVALID                                          
00886              DISPLAY '** ABEND ** '                                       
00887              DISPLAY '** PROGRAM ** = ' PC-CURRENT-PROGRAM-NAME           
00888              DISPLAY ' CALENDAR ERROR :' DPG54-ERROR-MESSAGE              
00889              SET ABEND-4008-DATA-ERROR TO TRUE                            
00890              CALL 'ILBOABN0' USING ABEND-CODE                             
00891          WHEN OTHER                                                       
00892              CONTINUE                                                     
00893      END-EVALUATE.                                                        
00894 *                                                                         
00895  EJECT                                                                    
00896 ******************************************************************        
00897 *Z940-DISPLAY-CONTROL-INFO                                       *        
00898 *  ==> PROCESSES:                                                *        
00899 *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
00900 *  ==> PERFORMED FROM:                                           *        
00901 *                     B100-MAINLINE                              *        
00902 *                     Z950-SQL-ABEND                             *        
00903 ******************************************************************        
00904  Z940-DISPLAY-CONTROL-INFO.                                               
00905                                                                           
00906      MOVE RC-STRPPC-ROW-READ-CNTR                                         
00907                                  TO DI-STRPPC-ROW-READ-CNTR.              
00908      MOVE RC-SPPCDT-ROW-READ-CNTR                                         
00909                                  TO DI-SPPCDT-ROW-READ-CNTR.              
00910      MOVE RC-STRPCH-ROW-INSERT-CNTR                                       
00911                                  TO DI-STRPCH-ROW-INSERT-CNTR.            
00912      MOVE RC-SPCDTH-ROW-INSERT-CNTR                                       
00913                                  TO DI-SPCDTH-ROW-INSERT-CNTR.            
00914                                                                           
00915      DISPLAY '************** CONTROL DATA START **************'.          
00916      DISPLAY '************** CONTROL DATA START **************'.          
00917      DISPLAY 'DI-STRPPC-ROW-READ-CNTR'.                                   
00918      DISPLAY '===>' DISPLAY DI-STRPPC-ROW-READ-CNTR.                      
00919      DISPLAY 'DI-SPPCDT-ROW-READ-CNTR'.                                   
00920      DISPLAY '===>' DISPLAY DI-SPPCDT-ROW-READ-CNTR.                      
00921      DISPLAY 'DI-STRPCH-ROW-INSERT-CNTR'.                                 
00922      DISPLAY '===>' DISPLAY DI-STRPCH-ROW-INSERT-CNTR.                    
00923      DISPLAY 'DI-SPCDTH-ROW-INSERT-CNTR'.                                 
00924      DISPLAY '===>' DISPLAY DI-SPCDTH-ROW-INSERT-CNTR.                    
00925      DISPLAY 'PV-CURRNET-DATE: ' PV-CURRENT-DATE.                         
00926      DISPLAY 'DATE-60-DAYS-AGO: ' PV-DATE-60-DAYS-AGO.                    
00927      DISPLAY '************** CONTROL DATA END **************'.            
00928      DISPLAY '************** CONTROL DATA END **************'.            
00929 *                                                                         
00930  EJECT                                                                    
00931 ******************************************************************        
00932 *Z950-SQL-ABEND. *                                                        
00933 *  ==> PROCESSES:                                                *        
00934 *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE *                         
00935 *  ==> PERFORMED FROM:                                           *        
00936 *                    BY ANY PARA THAT MAKES AN EXEC SQL CALL     *        
00937 *                    AND HAS AN UNEXPECTED RETURN CODE           *        
00938 ******************************************************************        
00939  Z950-SQL-ABEND.                                                          
00940                                                                           
00941      DISPLAY '** ABEND **'.                                               
00942      DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
00943      DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
00944      DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
00945                                                                           
00946      PERFORM Z940-DISPLAY-CONTROL-INFO.                                   
00947                                                                           
00948 * FORMAT THE ABEND MESSAGE INTO A DISPLAYABLE FORMAT                      
00949      COPY DPPD004.                                                        
00950                                                                           
00951                                                                           
00952      EXEC SQL                                                             
00953          ROLLBACK                                                         
00954      END-EXEC.                                                            
00955                                                                           
00956      SET ABEND-4013-DB2-ERROR TO TRUE.                                    
00957      CALL 'ILBOABN0' USING ABEND-CODE.                                    
