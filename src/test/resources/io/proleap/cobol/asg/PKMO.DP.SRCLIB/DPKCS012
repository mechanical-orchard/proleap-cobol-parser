      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.                DPKCS012.                                     
       AUTHOR.                    DWYNE WALLACE.                                
       INSTALLATION.              KOHLS DEPARTMENT STORES.                      
       DATE-WRITTEN.              OCTOBER 1990.                                 
       DATE-COMPILED.                                                           
       SKIP2                                                                    
      ******************************************************************        
      *  CICS PROGRAM - DPKCS012                                       *        
      *  TRANSACTION ID. - Z012                                        *        
      *                                                                *        
      *                ADD A MENU ITEM PROGRAM                         *        
      *                                                                *        
      *  THIS TRANSACTION PROCESSES ALL ADD REQUESTS FOR THE TMENUITEM *        
      *  DB2 DATABASE. THIS DATABASE IS USED TO CONTROL NAVIGATION     *        
      *  WITHIN CICS.                                                  *        
      *                                                                *        
      *  PF KEY PROCESSING:                                            *        
      *                                                                *        
      *         ENTER - EDIT SCREEN                                    *        
      *         PA2   - TOGGLE FUNCTION KEY LINES                      *        
      *         PF5   - REFRESH LIST FROM DATABASE                     *        
      *         PF13  - ADD TO DATABASE                                *        
      *         CLEAR - RESET SCREEN TO LAST DISPLAY                   *        
      *                                                                *        
      *  NAVIGATION PF KEY PROCESSING:                                 *        
      *                                                                *        
      *         *SEE MNEMONIC FILE FOR NAVIGATION PF KEY DEFINITIONS.  *        
      *                                                                *        
      *    03/14/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED THIS PROGRAM TO UPDATE THE   *        
      *                          DB2 TMNEMONIC TABLE'S UPD-VSAM-IND.   *        
      *                          * THIS COLUMN WILL BE UPDATED FOR:    *        
      *                            THE MENU ROW (NOT THE MENUITEM ROW).*        
      *                          * THIS COLUMN WILL BE UPDATED WHEN:   *        
      *                            A MENUITEM IS UPDATED ON THIS MENU. *        
      *                          SEE PARA: A400-PROCESS-ADD.           *        
      *                                    X980-UPDT-TMNEMONIC-ROW.    *        
981110*    11/10/98  TKMA464  SPACE OUT SEQUENCE INSTEAD OF ZERO OUT.  *        
      *                                                                *        
      ******************************************************************        
       SKIP2                                                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       EJECT                                                                    
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       SKIP2                                                                    
       WORKING-STORAGE SECTION.                                                 
       SKIP1                                                                    
      *********************                                                     
      * PROGRAM CONSTANTS *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-CONSTANT-AREA.                                               
           05  PC-MAXIMUM-COMMAREA-LENGTH                                       
                                       PIC S9(4)      VALUE +4072               
                                                      COMP SYNC.                
           05  PC-CURRENT-MAP-NAME     PIC X(8)       VALUE 'DP012A  '.         
           05  PC-CURRENT-MAPSET-NAME  PIC X(8)       VALUE 'DPKM012 '.         
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)       VALUE 'DPKCS012'.         
           05  PC-NAVIGATOR-TRAN-ID    PIC X(4)       VALUE 'Z008'.             
           05  PC-MENU-TRAN-ID         PIC X(4)       VALUE 'Z007'.             
           05  PC-LEAVE-SYSTEM-LENGTH  PIC S9(4)      VALUE +80                 
                                                      COMP SYNC.                
           05  PC-FORMAT-CODE          PIC X(4)       VALUE 'MITM'.             
           05  PC-ACKNWLDGE-CODE       PIC X(4)       VALUE 'MITM'.             
           05  PC-Y                    PIC X(1)       VALUE 'Y'.                
           05  PC-TSYMSG-00005         PIC 9(5)       VALUE 00005.              
           05  PC-TSYMSG-00007         PIC 9(5)       VALUE 00007.              
           05  PC-TSYMSG-00008         PIC 9(5)       VALUE 00008.              
           05  PC-TSYMSG-00034         PIC 9(5)       VALUE 00034.              
           05  PC-TSYMSG-00117         PIC 9(5)       VALUE 00117.              
           05  PC-TSYMSG-00148         PIC 9(5)       VALUE 00148.              
           05  PC-TSYMSG-00259         PIC 9(5)       VALUE 00259.              
           05  PC-TSYMSG-00266         PIC 9(5)       VALUE 00266.              
           05  PC-TSYMSG-00268         PIC 9(5)       VALUE 00268.              
       SKIP2                                                                    
      *********************                                                     
      * PROGRAM VARIABLES *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-VARIABLE-AREA.                                               
           05  PV-CICS-RESPONSE        PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-HOLD-SQLCODE         PIC S9(9)      COMP-4.                   
           05  PV-TERMINAL             PIC X(4)       VALUE SPACES.             
           05  PV-MESSAGE              PIC X(78)      VALUE SPACES.             
           05  PV-LEAVE-SYSTEM-MSG     PIC X(80)      VALUE SPACES.             
           05  PV-HOLD-MESSAGE         PIC X(78)      VALUE SPACES.             
           05  PV-HOLD-SEVERITY-IND    PIC X(1)       VALUE SPACES.             
           05  PV-MNEMONIC             PIC X(8)       VALUE SPACES.             
           05  PV-DESC-NULL-IND        PIC S9(4)      COMP.                     
               88  PV-DESC-NOT-NULL                   VALUE ZERO.               
               88  PV-DESC-NULL                       VALUE -1.                 
           05  PV-SEQUENCE-NUMBER-ALPHA                                         
                                       PIC X(4).                                
           05  PV-SEQUENCE-NUMBER-NUMERIC                                       
                                       REDEFINES                                
               PV-SEQUENCE-NUMBER-ALPHA                                         
                                       PIC 9(4).                                
           05  PV-FORMAT-CODES         PIC X(4)       VALUE SPACES.             
               88  PV-ACCEPTED-FORMAT-CODE            VALUE 'CMIA'              
                                                            'FKEY'              
                                                            'MITM'.             
           05  PV-DISPLAY-INFO.                                                 
               10  PV-DISP-MENU-NAME   PIC X(8).                                
               10  FILLER              PIC X(2)       VALUE ', '.               
               10  PV-DISP-ITEM        PIC X(8).                                
               10  PV-DISP-FIELD2                                               
                   REDEFINES                                                    
                   PV-DISP-ITEM.                                                
                   15  PV-DISP-SEQ     PIC 9(4).                                
                   15  FILLER          PIC X(4).                                
       EJECT                                                                    
      ********************                                                      
      * PROGRAM SWITCHES *                                                      
      ********************                                                      
       SKIP1                                                                    
       01  PROGRAM-SWITCHES.                                                    
           05  PS-MAP-SEND-TYPE        PIC X          VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET           VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN     VALUE '2'.                
               88  PS-SEND-DATA-ONLY-FRSET            VALUE '3'.                
               88  PS-SEND-DATA-ONLY                  VALUE '4'.                
               88  PS-SEND-DATA-ONLY-ERASEAUP         VALUE '5'.                
               88  PS-SEND-MAP-ONLY-FRSET             VALUE '6'.                
           05  PS-LOCAL-FUNCTION-KEY-IND                                        
                                       PIC X          VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-NO               VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-YES              VALUE 'Y'.                
           05  PS-ROW-FOUND-IND        PIC X          VALUE 'N'.                
               88  PS-ROW-FOUND-NO                    VALUE 'N'.                
               88  PS-ROW-FOUND-YES                   VALUE 'Y'.                
           05  PS-EDIT-ERROR-SW        PIC X          VALUE 'N'.                
               88  PS-EDIT-ERROR-NO                   VALUE 'N'.                
               88  PS-EDIT-ERROR-YES                  VALUE 'Y'.                
           05  PS-WARNING-SW           PIC X          VALUE 'N'.                
               88  PS-NO-WARNING                      VALUE 'N'.                
               88  PS-WARNING                         VALUE 'Y'.                
           05  PS-NEW-MNEMONIC-ENTERED-IND                                      
                                       PIC X          VALUE 'N'.                
               88  PS-NEW-MNEMONIC-ENTERED-NO         VALUE 'N'.                
               88  PS-NEW-MNEMONIC-ENTERED-YES        VALUE 'Y'.                
       EJECT                                                                    
      ******************                                                        
      * ERROR MESSAGES *                                                        
      ******************                                                        
       SKIP1                                                                    
       01  ERROR-MESSAGES.                                                      
           05  INVALID-NEXT-APPL-MSG   PIC X(78)      VALUE                     
               'NEXT APPLICATION CODE INVALID                                   
      -        '                      '.                                        
       EJECT                                                                    
      **************************                                                
      * DB2 COMMUNICATION AREA *                                                
      **************************                                                
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       SKIP2                                                                    
      ***********************************                                       
      * DB2 SQL RETURN CODE DEFINITIONS *                                       
      *    USING '88' LEVEL             *                                       
      ***********************************                                       
       SKIP1                                                                    
       01  SQL-RETURN-CODE             PIC S9(9)      COMP-4.                   
           88  SQL-SUCCESSFUL                         VALUE +0.                 
           88  SQL-ROW-FOUND                          VALUE +0.                 
           88  SQL-ROW-NOT-FOUND                      VALUE +100.               
           88  SQL-WARNING                            VALUE +100                
                                                            +558                
                                                            +560.               
           88  SQL-DUPLICATE-ROW                      VALUE -803.               
           88  SQL-MORE-THAN-1-ROW                    VALUE -811.               
           88  SQL-NOT-AVAILABLE                      VALUE -911.               
           88  SQL-DB2-DOWN                           VALUE -923.               
           88  SQL-NULL-VALUE                         VALUE -305.               
           88  SQL-DEPENDENT-ROW                      VALUE -532.               
       EJECT                                                                    
      **************************************                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DFHAID.                                                         
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005   *         
      *---------------------------------------------------------------*         
           COPY DPWS006.                                                        
       EJECT                                                                    
      **************************************************************            
      * WORKING-STORAGE LAYOUT USED TO LOAD THE FUNCTION KEY LINES *            
      **************************************************************            
           COPY DPWS009.                                                        
       EJECT                                                                    
      ***************************                                               
      * COMMON ABEND DATA AREAS *                                               
      ***************************                                               
       SKIP1                                                                    
           COPY DPWS013.                                                        
       EJECT                                                                    
      *********************************************************                 
      * KOHL'S DEFINED:                                       *                 
      * STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST *                 
      *********************************************************                 
       SKIP1                                                                    
           COPY DPWS015.                                                        
       EJECT                                                                    
      **************************************                                    
      * KOHL'S:                            *                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DPWS016.                                                        
       EJECT                                                                    
      ***************************************************************           
      * WORKING-STORAGE LAYOUT USED TO GET THE SYSTEM DATE AND TIME *           
      ***************************************************************           
           COPY DPWS017.                                                        
       EJECT                                                                    
      *******************************                                           
      * STANDARD COMMUNICATION AREA *                                           
      *******************************                                           
       SKIP1                                                                    
           COPY DPWS020.                                                        
           05  FILLER                  REDEFINES                                
               DP020-VARIABLE-COMMAREA.                                         
      ***************************************                                   
      * CICSARC INTER APPLICATION DATA AREA *                                   
      ***************************************                                   
               10  INTER-APPL-COMM-AREA                                         
                                       PIC X(200).                              
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      **********************************                                        
      * APPLICATION SPECIFIC COMM AREA *                                        
      **********************************                                        
               10  APPL-SPECIFIC-COMM-AREA                                      
                                       PIC X(2865).                             
               10  DP012-APPL-SPECIFIC-COMM-AREA                                
                                       REDEFINES                                
                   APPL-SPECIFIC-COMM-AREA.                                     
                   15  FILLER          PIC X(02).                               
                   15  DP012-CURRENT-SCREEN-IMAGE.                              
                       20  DP012-SCREEN-TRAN-ID                                 
                                       PIC X(04).                               
                       20  DP012-SCREEN-MNEMONIC                                
                                       PIC X(08).                               
                       20  DP012-SCREEN-SEQUENCE-NUMBER                         
                                       PIC X(04).                               
                       20  DP012-SCREEN-MENU-TITLE                              
                                       PIC X(30).                               
                       20  DP012-SCREEN-MENU-NAME                               
                                       PIC X(08).                               
                       20  DP012-SCREEN-FORMAT                                  
                                       PIC X(01).                               
                       20  DP012-SCREEN-PANEL-TITLE-NAME                        
                                       PIC X(30).                               
                       20  DP012-SCREEN-MENU-ITEM-TITLE                         
                                       PIC X(30).                               
                   15  DP012-TRAN-DEFINED-CICS                                  
                                       PIC X.                                   
                   15  DP012-MENU-TRAN-SW                                       
                                       PIC X.                                   
                       88  DP012-A-NON-MENU-TRAN          VALUE 'N'.            
                       88  DP012-A-MENU-TRAN              VALUE 'Y'.            
                   15  DP012-SELECT-MAINT-RESULT                                
                                       PIC X.                                   
                       88  DP012-RESULT-IS-CREATE      VALUE '1'.               
                       88  DP012-RESULT-IS-UPDATE      VALUE '2'.               
                       88  DP012-RESULT-IS-DELETE      VALUE '3'.               
                       88  DP012-RESULT-IS-READ        VALUE '4'.               
                       88  DP012-RESULT-IS-ERROR       VALUE '5'.               
                   15  DP012-INTER-APPL-COMMAREA       PIC X(200).              
                   15  FILLER  REDEFINES  DP012-INTER-APPL-COMMAREA.            
                       20  DP012-INTER-APPL-FORMAT-CODE                         
                                           PIC X(04).                           
                       20  DP012-INTER-APPL-ACKNLG-CODE                         
                                           PIC X(04).                           
981110                 20  FILLER          PIC X(192).                          
                   15  DP012-TIMESTAMP     PIC X(26).                           
                   15  DP012-HOLD-FORMAT   PIC X(01).                           
981110             15  FILLER              PIC X(2518).                         
       EJECT                                                                    
      **************************************************************            
      * PARAMETER LIST USED TO CALL THE 'TRANSLATE AID' SUBROUTINE *            
      **************************************************************            
           COPY DPWS045.                                                        
       EJECT                                                                    
      **************************************************************            
      * WORK AREA FOR THE STANDARD ERROR MESSAGES.                 *            
      **************************************************************            
           COPY DPRD507I.                                                       
        EJECT                                                                   
      ********************************                                          
      * DSECT FOR THE DPKM021 MAPSET *                                          
      ********************************                                          
       SKIP1                                                                    
           COPY DPKM012.                                                        
       SKIP2                                                                    
      ************************************************                          
      * REDEFINES OF DP012AI MAPSET OF DETAIL FIELDS *                          
      ************************************************                          
       01  MD-MAP-DATA-AREA            REDEFINES                                
981110     DP012AO                     PIC X(548).                              
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMENUITEM *                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TMNUITM                                                  
           END-EXEC.                                                            
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMNEMONIC *                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TCICSROUTE*                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
       EJECT                                                                    
       LINKAGE SECTION.                                                         
       SKIP2                                                                    
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * A000-ADD-MENU-ITEM                                             *        
      * (Z012-00 ADD MENU ITEM)                                        *        
      * MAINLINE SECTION.                                              *        
      ******************************************************************        
                                                                                
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *****************************************************************         
      * THE VARIABLE 'RETURN-WITH-TRAN-ID' WILL BE SET TO 'Y' IN      *         
      * U13-CHECK-COMMAREA IF A BAD COMMAREA OR NO COMMAREA WAS       *         
      * ENCOUNTERED. IF THAT HAPPENS U13-CHECK-COMMAREA STARTS THE    *         
      * NAVIGATION TRANSACTION AND SETS THIS INDICATOR TO 'Y'. CHECK- *         
      * ING FOR A 'Y' HERE WILL PREVENT THIS TRANSACTION FROM CONTINU-*         
      * ING TO EXECUTE IF THE NAVIGATOR HAS BEEN REQUESTED TO START.  *         
      *****************************************************************         
                                                                                
           IF  DP020-SRC-RETURN-W-TRAN-ID                                       
               PERFORM A050-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A050-CONTROL-PROCESSING                                        *        
      * (Z012-01 CONTROL PROCESSING)                                   *        
      ******************************************************************        
                                                                                
       A050-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE APPL-SPECIFIC-COMM-AREA                           
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A450-EDIT-SCREEN                                     
                   MOVE PV-HOLD-MESSAGE                                         
                                       TO DP020-MSG-MESSAGE                     
                   MOVE PV-HOLD-SEVERITY-IND                                    
                                       TO DP020-MSG-SEVERITY-IND                
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A100-PROCESS-MAP                                     
                                                                                
               WHEN OTHER                                                       
                   SET  DP013-OTHER-ABEND                                       
                                       TO TRUE                                  
                   MOVE 'A050-CONTROL-PROCESSING       '                        
                                       TO DP013-PARAGRAPH                       
                   MOVE INVALID-NEXT-APPL-MSG                                   
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                       TO DP013-MESSAGE-TEXT (2)                
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A100-PROCESS-MAP                                               *        
      * (Z012-03 PROCESS-MAP)                                          *        
      ******************************************************************        
                                                                                
       A100-PROCESS-MAP.                                                        
                                                                                
           IF  EIBAID  =  DFHPA2                                                
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
               MOVE DP016-PA2          TO DP020-SRC-AID                         
               SET PS-SEND-DATA-ONLY                                            
                                       TO TRUE                                  
               MOVE -1                 TO ANAVKEYL                              
               PERFORM DP009-0000-LOAD-FK-LINES                                 
               PERFORM DP018-0000-GET-CURR-TIME                                 
               PERFORM DP008-0000-WRITE-PANEL                                   
           ELSE                                                                 
               IF  EIBAID  =  DFHCLEAR                                          
                   MOVE DP016-CLEAR      TO DP020-SRC-AID                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                         TO TRUE                                
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
               ELSE                                                             
                   PERFORM DP007-0000-GET-PANEL                                 
                   IF  DP020-SRC-AID  NOT =  DP016-CLEAR                        
                       PERFORM A650-MOVE-SCREEN-TO-COMM                         
                   END-IF                                                       
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A300-CHECK-LOCAL-PF-KEYS                             
                   IF  PS-LOCAL-FUNCTION-NO                                     
                       PERFORM DP015-1000-START-NAVIGATOR                       
                   ELSE                                                         
                       IF  ((DP020-SRC-AID  =  DP016-ENTER)    AND              
                            (DP020-NAV-MNEMONIC  NOT =  SPACES  AND             
                                                        LOW-VALUES))            
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       ELSE                                                     
                           IF  PS-NEW-MNEMONIC-ENTERED-NO                       
      *****************************************************************         
      * THIS SWITCH IS CHECKED TO PREVENT THE PROGRAM FROM EDITTING   *         
      * AND PROCESSING THE SCREEN AFTER A NEW SCREEN HAS BEEN SENT TO *         
      * THE TERMINAL. THIS SWITCH IS SET IN MODULE Z012-13 TO 'Y' IF  *         
      * THE USER HAS ENTERED A NEW ITEM IN THE NAVIGATOR KEY FIELD ON *         
      * THE SCREEN. OTHERWISE IT IS ALWAYS SET TO 'N'.                *         
      *****************************************************************         
                               PERFORM A350-LOCAL-PF-KEY-CONTROL                
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A200-PROCESS-INTER-APPL-COMM                                   *        
      * (Z012-05 PROCESS-INTER-APPL COMMAREA)                          *        
      * THE PURPOSE OF THIS MODULE IS TO MANIPULATE THE INTER-APPL-    *        
      * COMMAREA UNDER THE FOUR POSSIBLE CYCLES OF THIS TRANSACTION:   *        
      *                                                                *        
      *        1. DP020-NEXT-ACT-INITIAL                               *        
      *        2. DP020-NEXT-ACT-RETURN                                *        
      *        3. DP020-NEXT-ACT-READ-MAP                              *        
      *        4. DP020-NEXT-ACT-ERROR-RETURN                          *        
      *                                                                *        
      * THREE DATA AREAS ARE NEEDED TO CORRECTLY USE THE INTER-APPL-   *        
      * COMMAREA:                                                      *        
      *                                                                *        
      *        1. THE INTER-APPL-COMMAREA PORTION OF THE STANDARD-COMM-*        
      *           AREA                                                 *        
      *        2. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO STORE THE CONTENTS OF THE INTER-APPL-COMMAREA SENT*        
      *           TO THIS PROGRAM. (NOTE: ONLY SAVE THOSE FIELDS       *        
      *           RELAVENT TO THIS PROGRAM).                           *        
      *        3. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO HOLD THE DATA RELEVENT TO THE CURRENT OBJECT BEING*        
      *           PROCESSED BY THIS PROGRAM.                           *        
      ******************************************************************        
                                                                                
       A200-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           IF  DP020-NEXT-ACT-INITIAL                                           
               MOVE INTER-APPL-COMM-AREA                                        
                                     TO  DP012-INTER-APPL-COMMAREA              
               MOVE DP055-MENU-NAME  TO  DP012-SCREEN-MENU-NAME                 
           ELSE                                                                 
               IF  DP020-NEXT-ACT-RETURN                                        
               MOVE DP012-INTER-APPL-COMMAREA                                   
                                     TO  INTER-APPL-COMM-AREA                   
               ELSE                                                             
                   IF  DP020-NEXT-ACT-READ-MAP                                  
                       IF  DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                  
                           PERFORM A800-SETUP-INTER-APPL                        
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A250-SEND-INITIAL-SCREEN                                       *        
      * (Z012-06 SEND INITIAL SCREEN)                                  *        
      * RETRIEVE THE DATA FROM THE DATABASE, FORMAT DATA ON SCREEN, AND*        
      * DISPLAY THE SCREEN TO THE USER.                                *        
      ******************************************************************        
                                                                                
       A250-SEND-INITIAL-SCREEN.                                                
                                                                                
           PERFORM A500-RETRIEVE-DATA-FOR-SCREEN.                               
                                                                                
           PERFORM A550-DISPLAY-PANEL.                                          
       EJECT                                                                    
      ******************************************************************        
      * A300-CHECK-LOCAL-PF-KEYS                                       *        
      * (Z012-09 CHECK FOR LOCAL PF KEYS)                              *        
      *                                                                *        
      * LOCAL FUNCTION KEYS:                                           *        
      *      PF5   (DP020-SRC-AID = 05)  - REFRESH SCREEN              *        
      *      PF13  (DP020-SRC-AID = 13)  - ADD TO TMENUITEM TABLE      *        
      *      PA2   (DP020-SRC-AID = 26)  - TOGGLE THE FUNCTION KEY     *        
      *                                    LINES ON SCREEN             *        
      *      CLEAR (DP020-SRC-AID = 27)  - REFRESH SCREEN              *        
      *      ENTER (DP020-SRC-AID = 28)  - EDIT SCREEN                 *        
      ******************************************************************        
                                                                                
       A300-CHECK-LOCAL-PF-KEYS.                                                
                                                                                
           SET PS-LOCAL-FUNCTION-NO    TO TRUE.                                 
                                                                                
           IF (DP020-FK-LOCAL-KEY (DP020-SRC-AID)  OR                           
               DP020-FK-CONFIRM (DP020-SRC-AID)    OR                           
               DP020-FK-REFRESH (DP020-SRC-AID)    OR                           
               DP020-FK-PREV-PAGE (DP020-SRC-AID)  OR                           
               DP020-FK-NEXT-PAGE (DP020-SRC-AID)  OR                           
               DP020-FK-FIRST-PAGE (DP020-SRC-AID) OR                           
               DP020-FK-DESELECT (DP020-SRC-AID))                               
               SET PS-LOCAL-FUNCTION-YES                                        
                                       TO TRUE                                  
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A350-LOCAL-PF-KEY-CONTROL                                      *        
      * (Z012-10 LOCAL PF KEY CONTROL)                                 *        
      ******************************************************************        
                                                                                
       A350-LOCAL-PF-KEY-CONTROL.                                               
                                                                                
           EVALUATE DP020-SRC-AID                                               
                                                                                
               WHEN  DP016-PF5                                                  
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN  DP016-PF13                                                 
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A450-EDIT-SCREEN                                     
                   IF  PS-EDIT-ERROR-NO                                         
                       MOVE -1         TO AMNEMONL                              
                       PERFORM A400-PROCESS-ADD                                 
                   ELSE                                                         
                       MOVE -1         TO ANAVKEYL                              
                   END-IF                                                       
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN  DP016-PA2                                                  
                   MOVE -1             TO ANAVKEYL                              
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP018-0000-GET-CURR-TIME                             
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN  DP016-CLEAR                                                
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   MOVE -1             TO ANAVKEYL                              
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A540-RESET-ATTRIBUTES                                
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A400-PROCESS-ADD                                               *        
      * (Z012-12 PROCESS ADD TO DATABASE)                              *        
      * ALL DB2 DATABASE MAINTENANCE USES 'TIMESTAMP' VALIDATION SYNTAX*        
      * TO ENSURE THAT THE SET OF DATA BEING UPDATED IS IN THE SAME    *        
      * CONDITION AT THE TIME OF UPDATE THAT IT WAS IN WHEN THE SET OF *        
      * DATA WAS RETRIEVED FROM THE DATABASE FOR PROCESSING. THIS IS A *        
      * RECOGNITION OF THE POTENTIAL FOR CONCURRENT UPDATING ON ALL    *        
      * RELATIONS WITHIN THE DB2 ENVIRONMENT.                          *        
      ******************************************************************        
                                                                                
       A400-PROCESS-ADD.                                                        
                                                                                
           MOVE DP012-SCREEN-MENU-NAME TO TMENUITEM-MENU-NAME.                  
           MOVE DP012-SCREEN-MNEMONIC  TO TMENUITEM-ITEM-MNEMONIC-CODE.         
           MOVE DP012-SCREEN-FORMAT    TO TMENUITEM-IN-APLCTN-COMM-CDE.         
           MOVE DP012-SCREEN-SEQUENCE-NUMBER                                    
                                       TO PV-SEQUENCE-NUMBER-ALPHA.             
           MOVE PV-SEQUENCE-NUMBER-NUMERIC                                      
                                       TO TMENUITEM-SEQUENCE-NMBR.              
           IF  PV-DESC-NOT-NULL                                                 
               MOVE DP012-SCREEN-MENU-ITEM-TITLE                                
                                       TO TMENUITEM-DESC                        
           END-IF.                                                              
                                                                                
           PERFORM X940-ADD-TMENUITEM-ROW.                                      
                                                                                
           IF  SQL-SUCCESSFUL                                                   
               MOVE PC-TSYMSG-00034    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-INFORMATIONAL                                      
                                       TO TRUE                                  
               SET DP012-RESULT-IS-CREATE                                       
                                       TO TRUE                                  
               SET DP055-CREATED       TO TRUE                                  
               PERFORM X980-UPDT-TMNEMONIC-ROW                                  
               PERFORM A540-RESET-ATTRIBUTES                                    
               INITIALIZE ATRANIDO                                              
                          AMNEMONO                                              
                          ASEQNUMO                                              
                          AFORMATO                                              
                          ATITTLEO                                              
                          APANELO                                               
                          DP012-SCREEN-MNEMONIC                                 
                          DP012-SCREEN-PANEL-TITLE-NAME                         
                          DP012-SCREEN-TRAN-ID                                  
                          DP012-SCREEN-SEQUENCE-NUMBER                          
                          DP012-SCREEN-FORMAT                                   
                          DP012-SCREEN-MENU-ITEM-TITLE                          
           ELSE                                                                 
               MOVE PC-TSYMSG-00117    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               SET DP012-RESULT-IS-ERROR                                        
                                       TO TRUE                                  
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A450-EDIT-SCREEN                                               *        
      * (Z012-11 EDIT SCREEN)                                          *        
      * EDITS WILL OCCUR FROM THE BOTTOM OF THE SCREEN TO THE TOP      *        
      ******************************************************************        
                                                                                
       A450-EDIT-SCREEN.                                                        
                                                                                
           SET PS-EDIT-ERROR-NO        TO TRUE.                                 
                                                                                
      **********************************************                            
      * VALIDATE SCREEN MENU ITEM TITLE            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      *       (NO VALIDATION NECCESSARY)           *                            
      **********************************************                            
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF  TO ATITTLEA.                             
           MOVE DP015-UNDERLINE        TO ATITTLEH.                             
           MOVE DP015-GREEN            TO ATITTLEC.                             
                                                                                
           MOVE DP012-SCREEN-MNEMONIC                                           
                                       TO TMNEMONIC-MNEMONIC-CODE.              
           PERFORM X900-READ-TMNEMONIC-ROW.                                     
           MOVE SQL-RETURN-CODE        TO PV-HOLD-SQLCODE.                      
                                                                                
           MOVE DP015-GREEN            TO  AFORMATC.                            
           MOVE DP015-UNDERLINE        TO  AFORMATH.                            
           IF  DP012-SCREEN-FORMAT GREATER THAN SPACES                          
               SET PS-NO-WARNING       TO TRUE                                  
               PERFORM X960-READ-TCICSROUTE-ROW                                 
               PERFORM X970-READ-TMENUITEM-ROW                                  
               IF  PS-WARNING                                                   
                   MOVE PC-TSYMSG-00259                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-WARNING TO TRUE                                
                   MOVE -1             TO AFORMATL                              
                   MOVE DP015-YELLOW   TO AFORMATC                              
                   MOVE DP015-REVERSE  TO AFORMATH                              
               END-IF                                                           
           END-IF.                                                              
      **********************************************                            
      * VALIDATE SCREEN SEQUENCE NUMBER            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           EXEC CICS                                                            
               BIF                                                              
                 DEEDIT FIELD (DP012-SCREEN-SEQUENCE-NUMBER)                    
                       LENGTH (4)                                               
           END-EXEC                                                             
           IF  DP012-SCREEN-SEQUENCE-NUMBER  NUMERIC                            
               IF  DP012-SCREEN-SEQUENCE-NUMBER  >  ZEROS                       
                   MOVE DP012-SCREEN-MENU-NAME                                  
                                       TO TMENUITEM-MENU-NAME                   
                   MOVE DP012-SCREEN-SEQUENCE-NUMBER                            
                                       TO PV-SEQUENCE-NUMBER-ALPHA              
                   MOVE PV-SEQUENCE-NUMBER-NUMERIC                              
                                       TO TMENUITEM-SEQUENCE-NMBR               
                   PERFORM X920-READ-TMENUITEM-ROW                              
                   IF  (   SQL-ROW-FOUND                                        
                       OR  SQL-MORE-THAN-1-ROW                                  
                       )                                                        
                       SET PS-EDIT-ERROR-YES                                    
                                       TO TRUE                                  
                       MOVE PC-TSYMSG-00268                                     
                                       TO DP020-MSG-NUMBER                      
                       SET DP020-MSG-FATAL                                      
                                       TO TRUE                                  
                       MOVE -1            TO ASEQNUML                           
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                       TO ASEQNUMA                              
                       MOVE DP015-RED  TO ASEQNUMC                              
                       MOVE DP015-REVERSE                                       
                                       TO ASEQNUMH                              
                   ELSE                                                         
                       MOVE DP015-UNP-ALP-NOR-OFF                               
                                       TO ASEQNUMA                              
                       MOVE DP015-UNDERLINE                                     
                                       TO ASEQNUMH                              
                       MOVE DP015-GREEN                                         
                                       TO ASEQNUMC                              
                   END-IF                                                       
               ELSE                                                             
                   SET PS-EDIT-ERROR-YES TO TRUE                                
                   MOVE PC-TSYMSG-00005                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-FATAL TO TRUE                                  
                   MOVE -1             TO ASEQNUML                              
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                       TO ASEQNUMA                              
                   MOVE DP015-RED      TO ASEQNUMC                              
                   MOVE DP015-REVERSE  TO ASEQNUMH                              
               END-IF                                                           
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00008    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ASEQNUML                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO ASEQNUMA                              
               MOVE DP015-RED          TO ASEQNUMC                              
               MOVE DP015-REVERSE      TO ASEQNUMH                              
           END-IF.                                                              
                                                                                
      **********************************************                            
      * VALIDATE SCREEN MNEMONIC                   *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
      **********************************************                            
                                                                                
           IF  DP012-SCREEN-MNEMONIC  =  SPACES                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00007    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AMNEMONL                              
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                       TO AMNEMONA                              
               MOVE DP015-RED          TO AMNEMONC                              
               MOVE DP015-REVERSE      TO AMNEMONH                              
           ELSE                                                                 
               MOVE PV-HOLD-SQLCODE    TO SQL-RETURN-CODE                       
               IF  SQL-ROW-FOUND                                                
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                       TO DP012-SCREEN-TRAN-ID                  
                   MOVE TMNEMONIC-CMAREA-FRMT-CODE                              
                                       TO DP012-HOLD-FORMAT                     
                   MOVE DP012-SCREEN-MENU-NAME                                  
                                       TO TMENUITEM-MENU-NAME                   
                   MOVE DP012-SCREEN-MNEMONIC                                   
                                       TO TMENUITEM-ITEM-MNEMONIC-CODE          
                   PERFORM X910-READ-TMENUITEM-ROW                              
                   IF  SQL-ROW-FOUND                                            
                       SET PS-EDIT-ERROR-YES                                    
                                       TO TRUE                                  
                       MOVE PC-TSYMSG-00117                                     
                                       TO DP020-MSG-NUMBER                      
                       SET DP020-MSG-FATAL                                      
                                       TO TRUE                                  
                       MOVE -1         TO AMNEMONL                              
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                       TO AMNEMONA                              
                       MOVE DP015-RED  TO AMNEMONC                              
                       MOVE DP015-REVERSE                                       
                                       TO AMNEMONH                              
                   ELSE                                                         
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                       TO AMNEMONA                              
                       MOVE DP015-GREEN                                         
                                       TO AMNEMONC                              
                       MOVE DP015-UNDERLINE                                     
                                       TO AMNEMONH                              
                   END-IF                                                       
               ELSE                                                             
                   SET PS-EDIT-ERROR-YES                                        
                                       TO TRUE                                  
                   MOVE PC-TSYMSG-00148                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-FATAL TO TRUE                                  
                   MOVE -1             TO AMNEMONL                              
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                       TO AMNEMONA                              
                   MOVE DP015-RED      TO AMNEMONC                              
                   MOVE DP015-REVERSE  TO AMNEMONH                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DP015-PRO-NOR-OFF      TO ATRANIDA.                             
           MOVE DP015-BLUE             TO ATRANIDC.                             
           IF  PS-EDIT-ERROR-NO                                                 
               MOVE -1                 TO ANAVKEYL                              
           END-IF.                                                              
      **********************************************                            
      * VALIDATE THAT A MENU HAS BEEN ENTERED      *                            
      **********************************************                            
           IF DP012-A-NON-MENU-TRAN                                             
               INITIALIZE DP012-SCREEN-TRAN-ID                                  
                          DP012-SCREEN-MNEMONIC                                 
                          DP012-SCREEN-SEQUENCE-NUMBER                          
                          DP012-SCREEN-PANEL-TITLE-NAME                         
                          DP012-SCREEN-MENU-ITEM-TITLE                          
               MOVE DP015-PRO-NOR-OFF  TO AMNUNAMA                              
               MOVE DP015-REVERSE      TO AMNUNAMH                              
               MOVE DP015-RED          TO AMNUNAMC                              
               MOVE DP015-PRO-NOR-OFF  TO AMNUTTLA                              
               MOVE DP015-HL-OFF       TO AMNUTTLH                              
               MOVE DP015-BLUE         TO AMNUTTLC                              
               MOVE DP015-PRO-NOR-OFF  TO ATRANIDA                              
               MOVE DP015-HL-OFF       TO ATRANIDH                              
               MOVE DP015-BLUE         TO ATRANIDC                              
               MOVE 0                  TO ATRANIDL                              
               MOVE DP015-PRO-NOR-OFF  TO AMNEMONA                              
               MOVE DP015-HL-OFF       TO AMNEMONH                              
               MOVE DP015-BLUE         TO AMNEMONC                              
               MOVE 0                  TO AMNEMONL                              
               MOVE DP015-PRO-NOR-OFF  TO ATITTLEA                              
               MOVE DP015-HL-OFF       TO ATITTLEH                              
               MOVE DP015-BLUE         TO ATITTLEC                              
               MOVE 0                  TO ATITTLEL                              
               MOVE DP015-PRO-NOR-OFF  TO ASEQNUMA                              
               MOVE DP015-HL-OFF       TO ASEQNUMH                              
               MOVE DP015-BLUE         TO ASEQNUMC                              
               MOVE 0                  TO ASEQNUML                              
               SET PS-ROW-FOUND-NO     TO TRUE                                  
               MOVE PC-TSYMSG-00266    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ANAVCMDL                              
           END-IF.                                                              
       EJECT                                                                    
      *****************************************************************         
      *     EVERY TIME IN SET THE COMMAND FIELD TO EITHER RED         *         
      *     OR GREEN. THIS IS BECAUSE IT IS UNEDITTED IN THE          *         
      *     PROGRAM AND IT NEEDS RESETTING.                           *         
      *****************************************************************         
       A470-SET-COMMAND-ATTRIBUTES.                                             
           IF DP020-NEXT-ACT-ERROR                                              
               MOVE DP020-MSG-MESSAGE                                           
                                       TO PV-HOLD-MESSAGE                       
               MOVE DP020-MSG-SEVERITY-IND                                      
                                       TO PV-HOLD-SEVERITY-IND                  
               IF DP020-NAV-MNEMONIC NOT EQUAL TO SPACES                        
                   MOVE DP015-UNP-ALP-BRT-MDT                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-RED                                               
                                       TO ANAVCMDC                              
                   MOVE DP015-REVERSE                                           
                                       TO ANAVCMDH                              
               ELSE                                                             
                   MOVE SPACES         TO ANAVCMDO                              
                                          DP020-NAV-MNEMONIC                    
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           ELSE                                                                 
               IF (EIBAID NOT EQUAL TO DFHPA2 AND                               
                                       DFHCLEAR)                                
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A500-RETRIEVE-DATA-FOR-SCREEN                                  *        
      * (Z012-07 RETRIEVE DATA FOR SCREEN                              *        
      ******************************************************************        
                                                                                
       A500-RETRIEVE-DATA-FOR-SCREEN.                                           
                                                                                
           MOVE DP012-SCREEN-MENU-NAME TO TMNEMONIC-MNEMONIC-CODE.              
           SET DP012-A-NON-MENU-TRAN TO TRUE.                                   
                                                                                
           PERFORM X900-READ-TMNEMONIC-ROW.                                     
                                                                                
           IF  SQL-ROW-FOUND                                                    
               IF TMNEMONIC-TRANS-ID-CODE = PC-MENU-TRAN-ID                     
                   SET DP012-A-MENU-TRAN   TO TRUE                              
                   MOVE TMNEMONIC-PANEL-TITLE-NAME                              
                                           TO DP012-SCREEN-MENU-TITLE           
                   MOVE SPACES             TO DP012-SCREEN-MNEMONIC             
                   MOVE SPACES             TO DP012-SCREEN-TRAN-ID              
                   MOVE LOW-VALUES         TO                                   
                                           DP012-SCREEN-MENU-ITEM-TITLE         
                                           DP012-SCREEN-PANEL-TITLE-NAME        
                   MOVE SPACES             TO                                   
                                           DP012-SCREEN-SEQUENCE-NUMBER         
                   SET PS-ROW-FOUND-YES    TO TRUE                              
                   MOVE -1                 TO AMNEMONL                          
               ELSE                                                             
                   SET DP012-A-NON-MENU-TRAN TO TRUE                            
                   INITIALIZE DP012-SCREEN-TRAN-ID                              
                              DP012-SCREEN-MNEMONIC                             
                              DP012-SCREEN-SEQUENCE-NUMBER                      
                              DP012-SCREEN-PANEL-TITLE-NAME                     
                              DP012-SCREEN-MENU-ITEM-TITLE                      
                   MOVE DP015-REVERSE      TO AMNUNAMH                          
                   MOVE DP015-RED          TO AMNUNAMC                          
                   SET PS-ROW-FOUND-NO     TO TRUE                              
                   MOVE PC-TSYMSG-00266    TO DP020-MSG-NUMBER                  
                   SET DP020-MSG-FATAL     TO TRUE                              
                   MOVE -1                 TO ANAVCMDL                          
               END-IF                                                           
           ELSE                                                                 
               INITIALIZE DP012-CURRENT-SCREEN-IMAGE                            
               SET PS-ROW-FOUND-NO     TO TRUE                                  
               MOVE PC-TSYMSG-00148    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ANAVCMDL                              
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A540-RESET-ATTRIBUTES                                          *        
      ******************************************************************        
       A540-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                   TO AMNEMONA.                                 
           MOVE DP015-GREEN        TO AMNEMONC.                                 
           MOVE DP015-UNDERLINE    TO AMNEMONH.                                 
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                   TO ATITTLEA.                                 
           MOVE DP015-GREEN        TO ATITTLEC.                                 
           MOVE DP015-UNDERLINE    TO ATITTLEH.                                 
           MOVE DP015-UNP-ALP-NOR-OFF                                           
                                   TO ASEQNUMA.                                 
           MOVE DP015-GREEN        TO ASEQNUMC.                                 
           MOVE DP015-UNDERLINE    TO ASEQNUMH.                                 
           MOVE DP015-GREEN        TO AFORMATC.                                 
           MOVE DP015-UNDERLINE    TO AFORMATH.                                 
       EJECT                                                                    
      ******************************************************************        
      * A550-DISPLAY-PANEL                                             *        
      * (Z012-02 DISPLAY PANEL)                                        *        
      ******************************************************************        
                                                                                
       A550-DISPLAY-PANEL.                                                      
                                                                                
           IF  PS-SEND-DATA-ONLY-FRSET                                          
               PERFORM DP018-0000-GET-CURR-TIME                                 
           ELSE                                                                 
               PERFORM DP017-0000-GET-CURR-DATE-TIME                            
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
           END-IF.                                                              
                                                                                
           IF  PS-SEND-MAP-N-DATA-FRSET                                         
               PERFORM A600-LOAD-DETAIL-DATA                                    
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           IF  (    PS-SEND-MAP-N-DATA-FRSET                                    
               OR   DP020-SRC-AID  =  DP016-PA2                                 
               )                                                                
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
               PERFORM DP009-0000-LOAD-FK-LINES                                 
           END-IF.                                                              
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
       EJECT                                                                    
      ******************************************************************        
      * A600-LOAD-DETAIL-DATA                                          *        
      * (Z012-08 LOAD DETAIL DATA)                                     *        
      ******************************************************************        
                                                                                
       A600-LOAD-DETAIL-DATA.                                                   
                                                                                
           MOVE DP012-SCREEN-MNEMONIC  TO AMNEMONO                              
                                          TMNEMONIC-MNEMONIC-CODE.              
           MOVE DP012-SCREEN-TRAN-ID   TO ATRANIDO.                             
                                                                                
           IF  DP012-SCREEN-PANEL-TITLE-NAME  =  LOW-VALUES                     
               PERFORM X900-READ-TMNEMONIC-ROW                                  
               IF  SQL-ROW-FOUND                                                
                   MOVE TMNEMONIC-PANEL-TITLE-NAME                              
                                       TO DP012-SCREEN-PANEL-TITLE-NAME         
               ELSE                                                             
                   MOVE SPACES         TO DP012-SCREEN-PANEL-TITLE-NAME         
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE DP012-SCREEN-PANEL-TITLE-NAME                                   
                                       TO APANELO.                              
           MOVE DP012-SCREEN-MENU-ITEM-TITLE                                    
                                       TO ATITTLEO.                             
           MOVE DP012-SCREEN-SEQUENCE-NUMBER                                    
                                       TO ASEQNUMO.                             
           MOVE DP012-SCREEN-MENU-NAME TO AMNUNAMO.                             
           MOVE DP012-SCREEN-MENU-TITLE                                         
                                       TO AMNUTTLO.                             
           MOVE DP012-SCREEN-FORMAT    TO AFORMATO.                             
       EJECT                                                                    
      ******************************************************************        
      * A650-MOVE-SCREEN-TO-COMM                                       *        
      * (Z012-13 MOVE SCREEN TO COMMAREA)                              *        
      * IN ORDER TO MINIMIZE DATA TRANSMISSIONS, ALL SCREEN VARIABLES  *        
      * WILL BE STORED IN THE COMMAREA. ALL EDITING AND DATABASE I/O   *        
      * WILL BE FROM THESE FIELDS IN THE APPLICATION SPECIFIC COMMAREA.*        
      * ONLY ATTRIBUTE CHANGES WILL BE SENT TO THE TERMINAL AS LONG AS *        
      * THE CURRENT MAP IS STILL BEING DISPLAYED. THIS MODULE OVERLAYS *        
      * ANY COMMAREA SCREEN VARIABLE THAT HAS CHANGED (EITHER A LENGTH *        
      * GREATER THAN ZERO OR EOF HAS BEEN PRESSED) WITH THE INPUT FIELD*        
      * FROM THE SCREEN.                                               *        
      ******************************************************************        
                                                                                
       A650-MOVE-SCREEN-TO-COMM.                                                
                                                                                
           SET PS-NEW-MNEMONIC-ENTERED-NO                                       
                                       TO TRUE                                  
                                                                                
           IF  (    ANAVKEYL       >  ZEROS                                     
               AND  ANAVCMDL       =  ZEROS                                     
               AND  DP020-SRC-AID  =  DP016-ENTER                               
               )                                                                
               MOVE LOW-VALUES         TO ANAVKEYO                              
           END-IF.                                                              
                                                                                
           IF  DP020-FK-REFRESH (DP020-SRC-AID)    OR                           
               DP020-SRC-AID  =  DP016-CLEAR                                    
               CONTINUE                                                         
           ELSE                                                                 
               IF  AMNEMONL  >  ZERO                                            
                   MOVE AMNEMONI       TO DP012-SCREEN-MNEMONIC                 
               ELSE                                                             
                   IF  AMNEMONF  =  DP015-ERASE-EOF                             
                       MOVE SPACES         TO DP012-SCREEN-MNEMONIC             
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF  ATITTLEL  >  ZERO                                            
                   MOVE ATITTLEI       TO DP012-SCREEN-MENU-ITEM-TITLE          
                   SET PV-DESC-NOT-NULL TO TRUE                                 
               ELSE                                                             
                   IF  (    ATITTLEF  =  DP015-ERASE-EOF                        
                       OR   DP012-SCREEN-MENU-ITEM-TITLE  =  SPACES             
                       OR   DP012-SCREEN-MENU-ITEM-TITLE  =  LOW-VALUES         
                       )                                                        
                       MOVE SPACES     TO DP012-SCREEN-MENU-ITEM-TITLE          
                       SET PV-DESC-NULL                                         
                                       TO TRUE                                  
                       MOVE DP012-SCREEN-MNEMONIC                               
                                       TO TMNEMONIC-MNEMONIC-CODE               
                       PERFORM X900-READ-TMNEMONIC-ROW                          
                       IF  SQL-ROW-FOUND                                        
                           MOVE TMNEMONIC-PANEL-TITLE-NAME                      
                                       TO DP012-SCREEN-PANEL-TITLE-NAME         
                       ELSE                                                     
                           MOVE SPACES TO DP012-SCREEN-PANEL-TITLE-NAME         
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
981110         IF  ASEQNUML  >  ZERO                                            
981110             MOVE ASEQNUMI       TO DP012-SCREEN-SEQUENCE-NUMBER          
981110         ELSE                                                             
981110             IF  ASEQNUMF  =  DP015-ERASE-EOF                             
981110                 MOVE SPACES     TO DP012-SCREEN-SEQUENCE-NUMBER          
981110             END-IF                                                       
981110         END-IF                                                           
                                                                                
               IF  AFORMATL  >  ZERO                                            
                   MOVE AFORMATI       TO DP012-SCREEN-FORMAT                   
               ELSE                                                             
                   IF  AFORMATF  =  DP015-ERASE-EOF                             
                       MOVE SPACE      TO DP012-SCREEN-FORMAT                   
                   END-IF                                                       
               END-IF                                                           
                                                                                
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A800-SETUP-INTER-APPL                                          *        
      * (Z012-16 SETUP INTER-APPL)                                     *        
      *                                                                *        
      * WHENEVER THIS TRANSACTION IS ABOUT TO NAVIGATE TO ANOTHER      *        
      * TRANSACTION, THIS MODULE WILL FORMAT THE INTER APPLICATION     *        
      * COMMAREA WITH THE DATA SAVED FOR THE CURRENT OBJECT BEING PRO- *        
      * CESSED BY THIS TRANSACTION.                                    *        
      ******************************************************************        
                                                                                
       A800-SETUP-INTER-APPL.                                                   
                                                                                
           MOVE INTER-APPL-COMM-AREA   TO DP012-INTER-APPL-COMMAREA.            
                                                                                
           MOVE DP012-SCREEN-MNEMONIC  TO DP055-MNEMONIC.                       
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A850-RESET-INITIAL-INTER-APPL                                  *        
      * (Z012-17 RESET INITIAL INTER-APPL)                             *        
      *                                                                *        
      * WHEN THIS MODULE IS ABOUT TO RETURN TO THE MODULE THAT INVOKED *        
      * IT, IT WILL RESET THE COMMAREA TO THE CONTENTS THAT WERE SENT  *        
      * TO IT ALONG WITH ANY UPDATES TO THE OBJECT SENT TO IT, ALONG   *        
      * WITH AN ACKNOWLEDGEMENT CODE.                                  *        
      ******************************************************************        
                                                                                
       A850-RESET-INITIAL-INTER-APPL.                                           
                                                                                
           MOVE DP012-INTER-APPL-COMMAREA                                       
                                       TO INTER-APPL-COMM-AREA.                 
                                                                                
       EJECT                                                                    
      ************************************                                      
      * FETCH ROW FROM TABLE TMNEMONIC   *                                      
      *    USING TMNEMONIC-MNEMONIC-CODE *                                      
      ************************************                                      
                                                                                
       X900-READ-TMNEMONIC-ROW.                                                 
       SKIP2                                                                    
           EXEC SQL                                                             
               SELECT       MNEMONIC_CODE,                                      
                            TRANS_ID_CODE,                                      
                            CMAREA_FRMT_CODE,                                   
                            PANEL_TITLE_NAME                                    
                     INTO   :TMNEMONIC-MNEMONIC-CODE,                           
                            :TMNEMONIC-TRANS-ID-CODE,                           
                            :TMNEMONIC-CMAREA-FRMT-CODE,                        
                            :TMNEMONIC-PANEL-TITLE-NAME                         
                     FROM   TMNEMONIC                                           
                     WHERE  MNEMONIC_CODE  =  :TMNEMONIC-MNEMONIC-CODE          
           END-EXEC.                                                            
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF SELECT *                                               
      ***************************                                               
                                                                                
           MOVE SQLCODE                TO SQL-RETURN-CODE.                      
           IF  SQL-ROW-NOT-FOUND                                                
               CONTINUE                                                         
           ELSE                                                                 
               IF  NOT SQL-SUCCESSFUL                                           
                   SET  DP013-DB2-ABEND                                         
                                         TO TRUE                                
                   MOVE 'X900-READ-TMNEMONIC-ROW       '                        
                                         TO DP013-PARAGRAPH                     
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TMNEMONIC         '                                    
                                         TO DP013-DB2-TABLE-NAME (1)            
                   MOVE 'READ FAILED FOR: '                                     
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE TMNEMONIC-MNEMONIC-CODE                                 
                                         TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TMENUITEM *                                        
      **********************************                                        
                                                                                
       X910-READ-TMENUITEM-ROW.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT       MENU_NAME,                                          
                            ITEM_MNEMONIC_CODE,                                 
                            DESC,                                               
                            SEQUENCE_NMBR,                                      
                            LAST_UPDATE_TMESTP                                  
                     INTO   :TMENUITEM-MENU-NAME,                               
                            :TMENUITEM-ITEM-MNEMONIC-CODE,                      
                            :TMENUITEM-DESC:PV-DESC-NULL-IND,                   
                            :TMENUITEM-SEQUENCE-NMBR,                           
                            :TMENUITEM-LAST-UPDATE-TMESTP                       
                     FROM   TMENUITEM                                           
                     WHERE  MENU_NAME           =                               
                                :TMENUITEM-MENU-NAME                            
                            AND                                                 
                            ITEM_MNEMONIC_CODE  =                               
                                  :TMENUITEM-ITEM-MNEMONIC-CODE                 
           END-EXEC.                                                            
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF SELECT *                                               
      ***************************                                               
                                                                                
           MOVE SQLCODE                TO SQL-RETURN-CODE.                      
           IF  SQL-ROW-NOT-FOUND                                                
               CONTINUE                                                         
           ELSE                                                                 
             IF  NOT SQL-SUCCESSFUL                                             
                 SET  DP013-DB2-ABEND                                           
                                       TO TRUE                                  
                 MOVE 'X910-READ-TMENUITEM-ROW       '                          
                                       TO DP013-PARAGRAPH                       
                 MOVE SQLCA            TO DP013-SQLCA                           
                 MOVE 'TMENUITEM         '                                      
                                       TO DP013-DB2-TABLE-NAME (1)              
                 MOVE 'READ FAILED FOR: '                                       
                                       TO DP013-MESSAGE-TEXT (1)                
                 MOVE TMENUITEM-MENU-NAME                                       
                                       TO PV-DISP-MENU-NAME                     
                 MOVE TMENUITEM-ITEM-MNEMONIC-CODE                              
                                       TO PV-DISP-ITEM                          
                 MOVE PV-DISPLAY-INFO  TO DP013-MESSAGE-TEXT (2)                
                 PERFORM DP013-0000-PROCESS-ABEND                               
             END-IF                                                             
           END-IF.                                                              
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TMENUITEM *                                        
      * TO VERIFY SEQUENCE NUMBER      *                                        
      **********************************                                        
                                                                                
       X920-READ-TMENUITEM-ROW.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT         MENU_NAME,                                        
                              SEQUENCE_NMBR                                     
                     INTO     :TMENUITEM-MENU-NAME,                             
                              :TMENUITEM-SEQUENCE-NMBR                          
                     FROM     TMENUITEM                                         
                     WHERE    MENU_NAME        =                                
                                :TMENUITEM-MENU-NAME                            
                              AND                                               
                              SEQUENCE_NMBR    =                                
                                  :TMENUITEM-SEQUENCE-NMBR                      
           END-EXEC.                                                            
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF SELECT *                                               
      ***************************                                               
                                                                                
           MOVE SQLCODE                TO SQL-RETURN-CODE.                      
           IF  (    SQL-ROW-FOUND                                               
               OR   SQL-ROW-NOT-FOUND                                           
               OR   SQL-MORE-THAN-1-ROW                                         
               )                                                                
               CONTINUE                                                         
           ELSE                                                                 
               IF  NOT SQL-SUCCESSFUL                                           
                   SET  DP013-DB2-ABEND                                         
                                         TO TRUE                                
                   MOVE 'X920-READ-TMENUITEM-ROW       '                        
                                         TO DP013-PARAGRAPH                     
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TMENUITEM         '                                    
                                         TO DP013-DB2-TABLE-NAME (1)            
                   MOVE 'READ FAILED FOR: '                                     
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE TMENUITEM-MENU-NAME                                     
                                         TO PV-DISP-MENU-NAME                   
                   MOVE SPACES           TO PV-DISP-FIELD2                      
                   MOVE TMENUITEM-SEQUENCE-NMBR                                 
                                         TO PV-DISP-SEQ                         
                   MOVE PV-DISPLAY-INFO  TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************                                            
      * ADD ROW TO TABLE TMENUITEM *                                            
      ******************************                                            
                                                                                
       X940-ADD-TMENUITEM-ROW.                                                  
                                                                                
             EXEC SQL                                                           
                 INSERT INTO  TMENUITEM                                         
                       (MENU_NAME,                                              
                        ITEM_MNEMONIC_CODE,                                     
                        IN_APLCTN_COMM_CDE,                                     
                        DESC,                                                   
                        SEQUENCE_NMBR,                                          
                        LAST_UPDATE_TMESTP)                                     
                   VALUES                                                       
                       (:TMENUITEM-MENU-NAME,                                   
                        :TMENUITEM-ITEM-MNEMONIC-CODE,                          
                        :TMENUITEM-IN-APLCTN-COMM-CDE,                          
                        :TMENUITEM-DESC:PV-DESC-NULL-IND,                       
                        :TMENUITEM-SEQUENCE-NMBR,                               
                        CURRENT TIMESTAMP)                                      
             END-EXEC.                                                          
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF UPDATE *                                               
      ***************************                                               
                                                                                
           MOVE SQLCODE                TO SQL-RETURN-CODE                       
           IF  (    SQL-ROW-FOUND                                               
               OR   SQL-DUPLICATE-ROW                                           
               )                                                                
               CONTINUE                                                         
           ELSE                                                                 
               IF  NOT SQL-SUCCESSFUL                                           
                   SET  DP013-DB2-ABEND  TO TRUE                                
                   MOVE 'X940-ADD-TMENUITEM-ROW        '                        
                                         TO DP013-PARAGRAPH                     
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TMENUITEM         '                                    
                                         TO DP013-DB2-TABLE-NAME (1)            
                   MOVE 'INSERT FAILED FOR: '                                   
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE TMENUITEM-MENU-NAME                                     
                                         TO PV-DISP-MENU-NAME                   
                   MOVE TMENUITEM-ITEM-MNEMONIC-CODE                            
                                         TO PV-DISP-ITEM                        
                   MOVE PV-DISPLAY-INFO  TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TCICSROUTE*                                        
      * TO OBTAIN INTERAPPL FORMAT CODE*                                        
      **********************************                                        
                                                                                
       X960-READ-TCICSROUTE-ROW.                                                
                                                                                
           EXEC SQL                                                             
               SELECT       IN_APLCTN_COMM_CDE                                  
                     INTO   :TCICSROUTE-IN-APLCTN-COMM-CDE                      
                     FROM   TCICSROUTE                                          
                     WHERE  DEST_MNEMONIC_CODE      =                           
                                  :DP012-SCREEN-MNEMONIC                        
                      AND                                                       
                            IN_APLCTN_COMM_CDE      =                           
                                  :DP012-SCREEN-FORMAT                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = -811                                              
                    SET PS-WARNING     TO TRUE                                  
                                                                                
               WHEN OTHER                                                       
                   SET DP013-DB2-ABEND TO TRUE                                  
                   SET DP013-ROLLBACK  TO TRUE                                  
                                                                                
                   MOVE 'X960-READ-TCICSROUTE-ROW' TO                           
                        DP013-PARAGRAPH                                         
                   MOVE SQLCA                        TO                         
                        DP013-SQLCA                                             
                   MOVE 'TCICSROUTE - SELECT FAILED FOR' TO                     
                        DP013-MESSAGE-TEXT(1)                                   
                   MOVE DP012-SCREEN-MNEMONIC                                   
                                         TO PV-DISP-MENU-NAME                   
                   MOVE DP012-SCREEN-FORMAT                                     
                                         TO PV-DISP-ITEM                        
                   MOVE PV-DISPLAY-INFO  TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TCICSROUTE*                                        
      * TO OBTAIN INTERAPPL FORMAT CODE*                                        
      **********************************                                        
                                                                                
       X970-READ-TMENUITEM-ROW.                                                 
                                                                                
           EXEC SQL                                                             
               SELECT   IN_APLCTN_COMM_CDE                                      
               INTO     :TMENUITEM-IN-APLCTN-COMM-CDE                           
               FROM     TMENUITEM                                               
               WHERE    (ITEM_MNEMONIC_CODE =                                   
                                  :DP012-SCREEN-MNEMONIC                        
                      AND                                                       
                            IN_APLCTN_COMM_CDE      =                           
                                  :DP012-SCREEN-FORMAT)                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN SQLCODE = +100                                              
                    CONTINUE                                                    
               WHEN SQLCODE = +0                                                
               WHEN SQLCODE = -811                                              
                    SET PS-WARNING     TO TRUE                                  
                                                                                
               WHEN OTHER                                                       
                   SET DP013-DB2-ABEND TO TRUE                                  
                   SET DP013-ROLLBACK  TO TRUE                                  
                                                                                
                   MOVE 'X970-READ-TMENUITEM-ROW ' TO                           
                        DP013-PARAGRAPH                                         
                   MOVE SQLCA                        TO                         
                        DP013-SQLCA                                             
                   MOVE 'TMENUITEM - SELECT FAILED FOR' TO                      
                        DP013-MESSAGE-TEXT(1)                                   
                   MOVE DP012-SCREEN-MNEMONIC                                   
                                         TO PV-DISP-MENU-NAME                   
                   MOVE DP012-SCREEN-FORMAT                                     
                                         TO PV-DISP-ITEM                        
                   MOVE PV-DISPLAY-INFO  TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      ************************************                                      
      * UPDATE ROW FROM TABLE TMNEMONIC  *                                      
      *    FOR THIS MENU.                *                                      
      ************************************                                      
                                                                                
       X980-UPDT-TMNEMONIC-ROW.                                                 
       SKIP2                                                                    
           EXEC SQL                                                             
                 UPDATE TMNEMONIC                                               
                    SET UPD_VSAM_IND       = :PC-Y,                             
                        LAST_UPDATE_TMESTP = CURRENT TIMESTAMP                  
                 WHERE  MNEMONIC_CODE      = :DP012-SCREEN-MENU-NAME            
           END-EXEC.                                                            
                                                                                
      ***************************                                               
      * CHECK SQLCODE OF UPDATE *                                               
      ***************************                                               
                                                                                
           MOVE SQLCODE                TO SQL-RETURN-CODE.                      
           IF  SQLCODE NOT EQUAL ZERO                                           
               SET  DP013-DB2-ABEND  TO TRUE                                    
               MOVE 'X980-UPDT-TMNEMONIC-ROW       '                            
                                       TO DP013-PARAGRAPH                       
               MOVE SQLCA              TO DP013-SQLCA                           
               MOVE 'TMNEMONIC         '                                        
                                       TO DP013-DB2-TABLE-NAME (1)              
               MOVE 'UPDATE OF UPD-VSAM-IND FAILED FOR: '                       
                                       TO DP013-MESSAGE-TEXT (1)                
               MOVE DP012-SCREEN-MENU-NAME                                      
                                       TO DP013-MESSAGE-TEXT (2)                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
                                                                                
      **************************************                                    
      * COPY DP002-0000-FORMAT-TITLE-LINES *                                    
      **************************************                                    
           COPY DPPD022.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP003-0000-RETURN-TO-CICS *                                        
      **********************************                                        
           COPY DPPD003.                                                        
       EJECT                                                                    
      ************************************                                      
      * COPY DP005-0000-FORMAT-MSG-LINES *                                      
      ************************************                                      
           COPY DPPD005.                                                        
       EJECT                                                                    
      *****************************                                             
      * COPY DP007-0000-GET-PANEL *                                             
      *****************************                                             
           COPY DPPD007.                                                        
       EJECT                                                                    
      *******************************                                           
      * COPY DP008-0000-WRITE-PANEL *                                           
      *******************************                                           
           COPY DPPD008.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP009-0000-LOAD-FK-LINES *                                         
      *********************************                                         
           COPY DPPD009.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP013-0000-PROCESS-ABEND *                                         
      *********************************                                         
           COPY DPPD013.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP015-0000-CHECK-COMMAREA *                                        
      **********************************                                        
           COPY DPPD015.                                                        
       EJECT                                                                    
      **************************************                                    
      * COPY DP017-0000-GET-CURR-DATE-TIME *                                    
      **************************************                                    
           COPY DPPD017.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP018-0000-GET-CURR-TIME *                                         
      *********************************                                         
           COPY DPPD018.                                                        
