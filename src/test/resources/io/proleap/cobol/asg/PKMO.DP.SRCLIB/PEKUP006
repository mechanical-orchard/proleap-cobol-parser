000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.    PEKUP006.                                                 
000600 AUTHOR.        AMY REILAND.                                              
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  09/20/93.                                                 
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  IN-STORE PRICE CHANGE DOCUMENT COMPLETION                              
001300*                                                                         
001400*  THIS PROGRAM WILL PROCESS ALL IN-STORE PRICE CHANGE DOCUMENTS          
001500*  ON A DAILY BASIS TO MOVE DOCUMENTS TO A NEW PHASE IF                   
001600*  WARRENTED BY THE CURRENT STATE OF THE DOCUMENT.                        
001700*  DOCUMENTS IN COUNTS ENTERED STATUS WILL BE CHANGED TO                  
001800*  COMPLETED.                                                             
001900*                                                                         
002000*  DOCUMENTS IN AVAILABLE STATUS WILL BE CHANGED TO DELINQUENT            
002100*  IF IT IS 'X' DAYS PAST DATE ISSUED, OR THE DATE IS 'X' DAYS            
002200*  BEFORE MONTH END ('X' DAYS BEING THE 2 DELINQUENT NUMBER OF            
002300*  DAYS ON THE CONTROL TABLE - #28 AND #31).                              
002400*                                                                         
002500*  AN EXCESSIVE UNITS FILE WILL BE PRODUCED FOR ALL DOCUMENTS             
002600*  WHICH WERE COMPLETED AND HAD A SQU WHICH HAD A QUANTITY OVER A         
002700*  SPECIFIED AMOUNT. THE AMOUNT IS A VALUE ON THE CONTROL TABLE.          
002800*                                                                         
002900*  ALL DAMAGED MARKDOWN RECORDS (CONVERTED REASON CODE 7), WILL BE        
003000*  WRITTEN TO THE DAMAGED MARKDOWN FILE.  THE RECORDS WRITTEN WILL        
003100*  BE IN THE SKU-ADJUSTMENT FORMAT.  ONCE IN FASHION, THE ONHANDS         
003200*  WILL BE DECREASED FOR THOSE SKUS WRITTEN.                              
003300*  THOSE DAMAGED MARKDOWN DOCUMENTS WILL BE CHANGED FROM A REASON         
003400*  CODE 7 TO A REASON CODE 4 AND WRITTEN TO THE MARKDOWN FILE.            
003500*                                                                         
003600*                                                                         
003700*  TABLES ACCESSED ARE:-                                                  
003800*      TSTRPPC - IN-STORE PERM PRICE CHANGE HEADER                        
003900*      TSPPCDT - IN-STORE PERM PRICE CHANGE DETAIL                        
004000*      TPPCCTL - PERMANENT PRICE CHANGE CONTROLS                          
004100*  TABLES UPDATED ARE:-                                                   
004200*      TSTRPPC - IN-STORE PERM PRICE CHANGE HEADER                        
004300*  RECORDS WRITTEN ARE;-                                                  
004400*      ML012O MARKDOWN/MARKUP RECORD                                      
004500*      PE004  EXCESSIVE UNITS                                             
004600*      KDC1802 - D210-SKU-ADJUST                                          
004700******************************************************************        
004800*                                                                         
004900*                                                                         
005000*                                                                         
005100******************************************************************        
005200*  AMENDMENT HISTORY                                                      
005300*                                                                         
005400*    DATE     CHANGE NUMBER   DESCRIPTION OF CHANGE                       
005500*  ========   =============   ================================            
005600*   12/1/93   WR 2195         MODIFIED THE PROGRAM TO USE THE             
005700*                             RETICKET DATE AS THE DATE TO PASS           
005800*                             TO MERCH LEDGER.  ALSO CHANGED THE          
005900*                             EXCESSIVE UNITS REPORT TO ALSO              
006000*                             INCLUDE EXCESSIVE PRICE DIFFERENCES.        
006100*                                                                         
006200*   03/25/94  WR 2718         MODIFIED THE PROGRAM TO NOT CHANGE          
006300*                             THE DOCUMENT TO DELINQUENT IF IT            
006400*                             WAS CREATED ON THE FRIDAY BEFORE            
006500*                             FISCAL MONTH END.  THIS DOCUMENT            
006600*                             WAS REALLY MEANT FOR THE NEXT               
006700*                             FISCAL MONTH.                               
006800*                                                                         
006900*   01/16/95  WR 2663         ADDED ROW NUMBER TO THE LEDGER OUTPU        
007000*                             FILE (MLRD012O) FOR USE BY THE PHYSI        
007100*                             INVENTORY SYSTEM WHEN CREATING MISSE        
007200*                             MARKDOWN ADJUSTMENTS. NEW FIELD WAS         
007300*                             ADDED TO A FILLER AREA.                     
007400*                                                                         
007500*   08/21/97  WR 5408         REPLACED ALL ++INCLUDE REFERENCES TO        
007600*             KARI SUTTON     COPY, PER STANDARDS.  ADDED CODE TO         
007700*                             PASS ALONG THE REASON CODE IN THE           
007800*                             MARKDOWN/MARKUP FILE.                       
007810*                                                                         
007820*   10/04/2000 WR 21558       REMOVED DATE LOGIC TO SET NEW               
007830*             JACK KRAMER     DOCUMENTS DELINQUENT AT FISCAL MONTH        
007840*                             END.                                        
007840*                                                                         
W24472*   03/04/2003 WR 24472       REPLACED DEPT/CLASS/SEQ WITH 8 DIGIT        
W24472*             ALBERT A VINOD  SKU NUMBER. INCREASED RECORD LENGHT         
W24472*                             OF MARKDOWN AND MARKUP FILE TO 100.         
W24472*                             REDUCED RECORD LENGTH OF DAMAGED            
W24472*                             MARKDOWN FILE TO 200. REPLACED              
W24472*                             COPYBOOK MLRD012O WITH MLRD017 AND          
W24472*                             KDC1802 WITH FSRD035.                       
007900******************************************************************        
      * 05/04/04 - TODD C. MICHALEK -  WR NO. WR26602                           
      *    - CHANGED THE PROGRAM TO WRITE OUT THE NEW 120 (FORMALLY 100)        
      *      BYTE MLRD017 RECORD.  THIS NEW RECORD EXPANDS THE STORE            
      *      FIELD FROM 3 TO 5 BYTES AND ADDS 20 NEW BYTES OF DATA TO           
      *      THE END OF THE RECORD.  THE PROGRAM HAS ALSO BEEN CHANGED          
      *      TO INITIALIZE, INSTEAD OF INITIALLY SPACING OUT, THE               
      *      ML017-PRICE-CHANGE-RECORD SO THE NEWLY ADDED FIELDS ARE            
      *      PROPERLY SET TO THEIR APPROPRIATE ALPHA/NUMERIC DEFAULT            
      *      VALUE.                                                             
      *    - Z930-CALL-DATE-CALC-MODULE ERROR PROCESSING HAS ALSO BEEN          
      *      ENHANCED TO BETTER IDENTIFY THE RECORD CAUSING AN ERROR            
      *      CONDITION, WHEN THIS CONDITION ARISES.                             
      ******************************************************************        
      * 01/27/05 - TCM -  WR NO. 27559                                          
      *    - CHANGED ALL STORE NUMBER LENGTH REFERENCES FROM 3 TO 4             
      *      DIGITS IN LENGTH, REMOVING ANY UNNECESSARY STORE NUMBER            
      *      WORKING-STORAGE FIELDS                                             
      *    - REMOVED THE OPERCO_NBR FIELD FROM ALL IN-STORE PERM TABLE          
      *      CALLS                                                              
      ******************************************************************        
      * 03/23/05 - TCM -  WR NO. 27559                                          
      *    - REMOVE THE SELECT OF TPPCCTL KEY=02                                
      *      (PE048-CURR-FISCAL-YM-DATE-NBR) AND THE ASSOCIATED                 
      *      WORKING-STORAGE FIELDS (PV-CURRENT-FISCAL-YM-DATE-N AND            
      *      ITS REDEFINITION).  THESE FIELDS ARE NO LONGER USED IN THE         
      *      PROGRAM.                                                           
      ******************************************************************        
      * 11/01/05 - TCM -  WR NO. 28048                                          
      *    - IN PEWS049 ADDED THE NEW REASON CODE ("89") FOR                    
      *      "MARK OUT-OF-STOCK - SALVAGE" IN-STORE DOCUMENTS                   
      ******************************************************************        
      * 11/23/05 - TCM -  WR NO. 29145                                          
      *    - CHANGED THE PROGRAM TO WRITE OUT AN EXPANDED, 200 BYTES,           
      *      MLRD017 RECORD                                                     
      ******************************************************************        
SR3403* 12/29/02 - JALAJ- WR NO. SR3403                                         
SR3403*      CHANGES FOR LARGE RETAIL PROJECT                                   
CHG959******************************************************************        
CHG959* CHG0262959 10/21/2021 MODIFIED PROGRAM TO LOOK UP ITEM NUMBER           
CHG959*                       IN TSKXREF TABLE WHEN THE STORE/SKU               
CHG959*                       COMBINATION IS NOT PRESENT IN TSKST TABLE         
CHG959*                       CHANGES ARE MADE TO                               
CHG959*                       I500-DAMAGED-PROCESSING AND                       
CHG959*                       I600-SELECT-TSKXREF PARAGRAPHS.                   
CHG959*                       ALL CHANGES ARE TAGGED TO CHG959.                 
CHG959******************************************************************        
008000*                                                                         
008100*****************************************************************         
008200 ENVIRONMENT DIVISION.                                                    
008300*****************************************************************         
008400*                                                                         
008500*                                                                         
008600 CONFIGURATION SECTION.                                                   
008700                                                                          
008800 SOURCE-COMPUTER. IBM-3090.                                               
008900 OBJECT-COMPUTER. IBM-3090.                                               
009000*                                                                         
009100*                                                                         
009200 INPUT-OUTPUT SECTION.                                                    
009300                                                                          
009400 FILE-CONTROL.                                                            
009500                                                                          
009600     SELECT MARKUP-FILE                                                   
009700         ASSIGN TO OUT01.                                                 
009800                                                                          
009900     SELECT MARKDOWN-FILE                                                 
010000         ASSIGN TO OUT02.                                                 
010100                                                                          
010200     SELECT EXCESSIVE-UNITS-FILE                                          
010300         ASSIGN TO OUT03.                                                 
010400                                                                          
010500     SELECT DAMAGED-MARKDOWN-FILE                                         
010600         ASSIGN TO OUT04.                                                 
010700                                                                          
010800*                                                                         
010900 EJECT                                                                    
011000*                                                                         
011100*                                                                         
011200*                                                                         
011300*****************************************************************         
011400 DATA DIVISION.                                                           
011500*****************************************************************         
011600*                                                                         
011700*                                                                         
011800 FILE SECTION.                                                            
011900                                                                          
012000 FD  MARKDOWN-FILE                                                        
012100     RECORDING MODE IS F                                                  
012200     LABEL RECORDS ARE STANDARD                                           
012300     BLOCK  CONTAINS 0 RECORDS.                                           
W24472 01  MARKDOWN-RECORD                     PIC X(200).                      
012800*                                                                         
012900 FD  MARKUP-FILE                                                          
013000     RECORDING MODE IS F                                                  
013100     LABEL RECORDS ARE STANDARD                                           
013200     BLOCK  CONTAINS 0 RECORDS.                                           
W24472 01  MARKUP-RECORD                       PIC X(200).                      
013700*                                                                         
013800 FD  EXCESSIVE-UNITS-FILE                                                 
013900     RECORDING MODE IS F                                                  
014000     LABEL RECORDS ARE STANDARD                                           
014100     BLOCK  CONTAINS 0 RECORDS                                            
014200     RECORD CONTAINS 60 CHARACTERS                                        
014300     DATA RECORD IS EXCESSIVE-UNITS-RECORD.                               
014400                                                                          
014500 01  EXCESSIVE-UNITS-RECORD              PIC X(60).                       
014600*                                                                         
014700 FD  DAMAGED-MARKDOWN-FILE                                                
014800     RECORDING MODE IS F                                                  
014900     LABEL RECORDS ARE STANDARD                                           
015000     BLOCK  CONTAINS 0 RECORDS                                            
W24472     RECORD CONTAINS 200 CHARACTERS                                       
015200     DATA RECORDS ARE DAMAGED-MARKDOWN-RECORD.                            
015300                                                                          
W24472 01  DAMAGED-MARKDOWN-RECORD             PIC X(200).                      
015500*                                                                         
015600 EJECT                                                                    
015700*                                                                         
015800*                                                                         
015900*                                                                         
016000 WORKING-STORAGE SECTION.                                                 
016100                                                                          
016200*****************************************************************         
016300*    FASHION SALES MAINTENANCE LAYOUT FOR DAMAGED MERCHANDISE             
016400*****************************************************************         
W24472     COPY FSRD035.                                                        
016600*                                                                         
016700*****************************************************************         
016800*    PERM CONTROL NUMBERS                                                 
016900*****************************************************************         
017000     COPY PEWS048.                                                        
017100*                                                                         
017200 EJECT                                                                    
017300*****************************************************************         
017400*    PERM CONSTANTS                                                       
017500*****************************************************************         
017600     COPY PEWS049.                                                        
017700*                                                                         
017800 EJECT                                                                    
017900*****************************************************************         
018000*    MARKDOWN / MARKUP FILE LAYOUT                                        
018100*****************************************************************         
018200                                                                          
W24472     COPY MLRD017.                                                        
018400*                                                                         
018500 EJECT                                                                    
018600*****************************************************************         
018700*    EXCESSIVE UNITS FILE LAYOUT                                          
018800*****************************************************************         
018900                                                                          
019000     COPY PERD004.                                                        
019100*                                                                         
019200 EJECT                                                                    
019300*****************************************************************         
019400*    DB2 COMMUNICATION AREA                                               
019500*****************************************************************         
019600                                                                          
019700     COPY DPWS004.                                                        
019800*                                                                         
019900 EJECT                                                                    
020000*****************************************************************         
020100*    INCLUDE DATE ROUTINE COPY MEMBER                                     
020200*****************************************************************         
020300     COPY DPWS500.                                                        
020400*                                                                         
020500 EJECT                                                                    
020600 01  DK-DB2-KEYS.                                                         
020700     05  DK-CONTROL-NUMBER              PIC S9(03)   VALUE 0              
020800                                                     COMP-3.              
020900     05  DK-DOC-NBR                     PIC S9(07)   VALUE +0             
021000                                                     COMP-3.              
021100     05  DK-OPERATING-COMPANY           PIC  X(02)   VALUE '03'.          
021200     05  DK-STR-NBR                     PIC S9(04)   VALUE ZEROS          
                                                           COMP.                
021300*                                                                         
021400 01  PC-PROGRAM-CONSTANTS.                                                
021500     05  PC-DAMAGED-REDUCE-ONHAND-RSN   PIC  X(01)   VALUE '7'.           
021600     05  PC-DAMAGED-RSN                 PIC  X(01)   VALUE '4'.           
021700     05  PC-DFSMSA70-RECORD-CODE        PIC  X(08)   VALUE                
021800                                                     'DFSMSA70'.          
021900     05  PC-DOT                         PIC  X(01)   VALUE '.'.           
022000     05  PC-NOON                        PIC  9(02)   VALUE 12.            
022100     05  PC-ROW-NOT-FOUND               PIC S9(04)   VALUE +100.          
022200     05  PC-MAX-PR-CHG-QTY              PIC  9(4)    VALUE 9999.          
022300     05  PC-CALENDAR-PGM                PIC  X(8)    VALUE                
022400                                                     'DPKUT500'.          
022500*                                                                         
022600 01  PV-PROGRAM-VARIABLES.                                                
022700     05  PV-ACTL-PR-CHG-QTY             PIC  9(9)    VALUE ZERO.          
022800     05  PV-CONVERTED-RSN-CODE          PIC  X(01)   VALUE SPACES.        
022900     05  PV-FULL-RSN-CODE.                                                
023000         10  PV-FULL-RSN-CODE-TYPE      PIC  X(01)   VALUE SPACES.        
023100         10  PV-FULL-RSN-CODE-RSN       PIC  X(02)   VALUE SPACES.        
023600     05  PV-CURRENT-TIME                PIC  X(08)   VALUE SPACES.        
023700     05  FILLER  REDEFINES  PV-CURRENT-TIME.                              
023800         10  PV-HR                      PIC  9(02).                       
023900         10  PV-DOT-1                   PIC  X.                           
024000         10  PV-MN                      PIC  9(02).                       
024100         10  PV-DOT-2                   PIC  X.                           
024200         10  PV-SC                      PIC  9(02).                       
024300     05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
024400     05  PV-DELINQUENT-CUTOFF-DATE      PIC  X(10)   VALUE SPACES.        
024500     05  PV-DOCS-CHG-TO-CP-CNTR         PIC S9(08)   VALUE +0             
024600                                                     COMP-3.              
024700     05  PV-DOCS-CHG-TO-DQ-CNTR         PIC S9(08)   VALUE +0             
024800                                                     COMP-3.              
024900     05  PV-FRI-BEFORE-CREATE-MTHEND    PIC  X(10)   VALUE SPACES.        
025600     05  PV-HOLD-CLASS-NBR.                                               
025700         10  FILLER                     PIC  9(01)   VALUE ZEROES.        
025800         10  PV-HOLD-2DIGIT-CLASS       PIC  9(02)   VALUE ZEROES.        
025900     05  PV-INCR-DECR-DAYS              PIC  9(3)    VALUE ZEROES.        
026000     05  PV-ISO-CRE-MONTH-END-DATE      PIC  X(10)   VALUE SPACES.        
026100     05  PV-ISO-CURRENT-DATE            PIC  X(10)   VALUE SPACES.        
026200     05  FILLER  REDEFINES  PV-ISO-CURRENT-DATE.                          
026300         10  PV-CC                      PIC  9(02).                       
026400         10  PV-YY                      PIC  9(02).                       
026500         10  PV-DASH-1                  PIC  X.                           
026600         10  PV-MM                      PIC  9(02).                       
026700         10  PV-DASH-2                  PIC  X.                           
026800         10  PV-DD                      PIC  9(02).                       
026900     05  PV-ISO-MONTH-END-DATE          PIC  X(10)   VALUE SPACES.        
027000     05  PV-EFF-DATE.                                                     
027100         10  PV-EFF-CC                  PIC  9(02).                       
027200         10  PV-EFF-YY                  PIC  9(02).                       
027300         10  PV-EFF-DASH-1              PIC  X.                           
027400         10  PV-EFF-MM                  PIC  9(02).                       
027500         10  PV-EFF-DASH-2              PIC  X.                           
027600         10  PV-EFF-DD                  PIC  9(02).                       
W24472     05  PV-FSRD035-RECS-WRITTEN-CNTR   PIC S9(08)   VALUE +0             
027800                                                     COMP-3.              
027900     05  PV-LINE-ITEM                   PIC  999     VALUE ZERO.          
028000     05  PV-MARKDOWN-RECS-WRITTEN-CNTR  PIC S9(08)   VALUE +0             
028100                                                     COMP-3.              
028200     05  PV-MARKUP-RECS-WRITTEN-CNTR    PIC S9(08)   VALUE +0             
028300                                                     COMP-3.              
028400     05  PV-NEW-RETAIL                  PIC  9(7)V99 VALUE ZERO.          
028500     05  PV-OLD-RETAIL                  PIC  9(7)V99 VALUE ZERO.          
028600     05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
028700     05  PV-PERD004-RECS-WRITTEN-CNTR   PIC S9(08)   VALUE +0             
028800                                                     COMP-3.              
SR3403     05  PV-PRICE-DIFF                  PIC  9(5)V99 VALUE ZERO.          
029000     05  PV-STORE-NBR-5X.                                                 
               10  FILLER                     PIC  X(01)   VALUE ZEROS.         
               10  PV-STORE-NBR               PIC  X(04)   VALUE ZEROS.         
           05  PV-STORE-NBR-4X                PIC  X(04)   VALUE ZEROS.         
           05  PV-STORE-NBR-4N REDEFINES PV-STORE-NBR-4X                        
                                              PIC  9(04).                       
029300     05  PV-TODAYS-DATE.                                                  
029400         10  PV-TODAYS-YY               PIC  9(02)   VALUE ZEROES.        
029500         10  PV-TODAYS-MM               PIC  9(02)   VALUE ZEROES.        
029600         10  PV-TODAYS-DD               PIC  9(02)   VALUE ZEROES.        
029700     05  PV-TODAYS-TIME.                                                  
029800         10  PV-TODAYS-HR               PIC  9(02)   VALUE ZEROES.        
029900         10  PV-TODAYS-MN               PIC  9(02)   VALUE ZEROES.        
030000         10  PV-TODAYS-SC               PIC  9(02)   VALUE ZEROES.        
030100         10  FILLER                     PIC  9(02)   VALUE ZEROES.        
030200     05  PV-TOTAL-PR-CHG-AMT            PIC S9(7)V9(2)                    
030300                                                     VALUE +0             
030400                                                     COMP-3.              
030500     05  PV-TOTAL-PR-CHG-AMT-NI         PIC S9(04)   COMP                 
030600                                                     VALUE +0.            
030700     05  PV-TOTAL-PR-CHG-QTY            PIC S9(9)V   VALUE ZERO           
030800                                                     COMP-3.              
030900     05  PV-TOTAL-PR-CHG-QTY-NI         PIC S9(04)   COMP                 
031000                                                     VALUE +0.            
031100     05  PV-UNFORMAT-CURRENT-DATE.                                        
031200         10  PV-UNFORMAT-MM             PIC  9(02)   VALUE ZEROES.        
031300         10  PV-UNFORMAT-DD             PIC  9(02)   VALUE ZEROES.        
031400         10  PV-UNFORMAT-YY             PIC  9(02)   VALUE ZEROES.        
W24472     05  PV-SKU                         PIC  9(08)   VALUE ZEROES.        
W24472     05  PV-DEPT-NBR-9                  PIC  9(04)   VALUE ZEROES.        
W24472     05  PV-CLASS-NBR-9                 PIC  9(04)   VALUE ZEROES.        
W24472     05  PV-MAJOR-CLASS-9               PIC  9(04)   VALUE ZEROES.        
W24472     05  PV-MAJ-CLASS-X.                                                  
W24472         10 FILLER                      PIC  9(02)   VALUE ZEROS.         
W24472         10 PV-MAJ-CL-3-DIGIT           PIC  9.                           
W24472         10 FILLER                      PIC  9       VALUE ZERO.          
W24472     05  PV-MAJ-CLASS REDEFINES PV-MAJ-CLASS-X                            
W24472                                        PIC  9(04).                       
031500*                                                                         
031600  01 PS-PROGRAM-SWITCHES.                                                 
031700     05  PS-END-INSTR-DOCS-SWITCH       PIC  X(01)   VALUE 'N'.           
031800         88  PS-END-OF-INSTR-DOCS                    VALUE 'Y'.           
031900         88  PS-NOT-END-OF-INSTR-DOCS                VALUE 'N'.           
032000     05  PS-END-OF-DETAIL-LINES-SWITCH  PIC  X(01)   VALUE 'N'.           
032100         88  PS-END-OF-DETAIL-LINES                  VALUE 'Y'.           
032200         88  PS-NOT-END-OF-DETAIL-LINES              VALUE 'N'.           
032300     05  PS-EXCESSIVE-UNITS-SWITCH      PIC  X(01)   VALUE 'N'.           
032400         88  PS-EXCESSIVE-UNITS                      VALUE 'Y'.           
032500         88  PS-NO-EXCESSIVE-UNITS                   VALUE 'N'.           
032600     05  PS-EXCESSIVE-PRICE-SWITCH      PIC  X(01)   VALUE 'N'.           
032700         88  PS-EXCESSIVE-PRICE                      VALUE 'Y'.           
032800         88  PS-NO-EXCESSIVE-PRICE                   VALUE 'N'.           
032900*                                                                         
033000 01  ABEND-CODE                         PIC S9(04)   VALUE +0.            
033100*                                                                         
033200 01  RW-RECORDS-WRITTEN-CNTRS.                                            
033300     05  RW-CHG-CP-RECS-UPDATED-CNTR    PIC  Z(07)9 VALUE ZEROS.          
033400     05  RW-CHG-DQ-RECS-UPDATED-CNTR    PIC  Z(07)9 VALUE ZEROS.          
W24472     05  RW-FSRD035-RECS-WRITTEN-CNTR   PIC  Z(07)9 VALUE ZEROS.          
033600     05  RW-MARKDOWN-RECS-WRITTEN-CNTR  PIC  Z(07)9 VALUE ZEROS.          
033700     05  RW-MARKUP-RECS-WRITTEN-CNTR    PIC  Z(07)9 VALUE ZEROS.          
033800     05  RW-PERD004-RECS-WRITTEN-CNTR   PIC  Z(07)9 VALUE ZEROS.          
033900*                                                                         
034000 EJECT                                                                    
034100*****************************************************************         
034200*    DB2 IN-STORE PERMANENT PRICE CHANGE HEADER TABLE                     
034300*****************************************************************         
034400     EXEC SQL                                                             
034500         INCLUDE TSTRPPC                                                  
034600     END-EXEC.                                                            
034700                                                                          
W24472*****************************************************************         
W24472*    DB2 SKU TABLE                                                        
W24472*****************************************************************         
W24472     EXEC SQL                                                             
W24472         INCLUDE TSKXREF                                                  
W24472     END-EXEC.                                                            
034700                                                                          
034800     EXEC SQL                                                             
034900       DECLARE DOC-HDR-CSR CURSOR FOR                                     
035000           SELECT DEST_LOC_NBR                                            
035100                , DOC_NBR                                                 
035200                , DOC_TYP_CDE                                             
035300                , EFF_DTE                                                 
035400                , RSN_CDE                                                 
035500                , TYP_CDE                                                 
035600                , STATUS_CDE                                              
035700                , CRE_DTE                                                 
035800             FROM TSTRPPC                                                 
036000         WHERE (STATUS_CDE  = :PE049-IS-CNTS-ENTER-STATUS-CDE             
036100            OR  STATUS_CDE  = :PE049-IS-AVAILABLE-STATUS-CDE              
036200            OR  STATUS_CDE  = :PE049-IS-GENERATED-STATUS-CDE)             
036300         ORDER BY DEST_LOC_NBR                                            
036400                , DOC_NBR                                                 
036500     END-EXEC.                                                            
036600*                                                                         
036700 EJECT                                                                    
036800*****************************************************************         
036900*    DB2 IN-STORE PERMANENT PRICE CHANGE DETAIL TABLE                     
037000*****************************************************************         
037100                                                                          
037200     EXEC SQL                                                             
037300         INCLUDE TSPPCDT                                                  
037400     END-EXEC.                                                            
037500                                                                          
037600     EXEC SQL                                                             
037700       DECLARE DOC-DTL-CSR CURSOR FOR                                     
W24472         SELECT SKU_NBR                                                   
038100              , ROW_NBR                                                   
038200              , ACTL_PR_CHG_QTY                                           
038300              , OLD_PR                                                    
038400              , NEW_PR                                                    
038500              , ACTL_PR_CHG_AMT                                           
038600         FROM TSPPCDT                                                     
W24472         WHERE (DEST_LOC_NBR     = :SPPCDT-DEST-LOC-NBR                   
W24472           AND  DOC_NBR          = :SPPCDT-DOC-NBR)                       
039000         ORDER BY ROW_NBR                                                 
039100     END-EXEC.                                                            
039200*                                                                         
039300 EJECT                                                                    
039400*****************************************************************         
039500*    DB2 CENTRAL PERMANENT PRICE CHANGE CONTROLS TABLE                    
039600*****************************************************************         
039700                                                                          
039800     EXEC SQL                                                             
039900         INCLUDE TPPCCTL                                                  
040000     END-EXEC.                                                            
040100*                                                                         
040200 EJECT                                                                    
040300*****************************************************************         
040400*    DB2 COMMUNICATION AREA.                                              
040500*****************************************************************         
040600                                                                          
040700     EXEC SQL                                                             
040800         INCLUDE SQLCA                                                    
040900     END-EXEC.                                                            
041000*                                                                         
041100 EJECT                                                                    
041200*****************************************************************         
041300 PROCEDURE DIVISION.                                                      
041400*****************************************************************         
041500                                                                          
041600******************************************************************        
041700*A100-MAIN-MODULE                                                *        
041800*  ==> PROCESSES:                                                *        
041900*    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
042000******************************************************************        
042100 A100-MAIN-MODULE.                                                        
042200                                                                          
042300     PERFORM B100-PRE-PROCESSING.                                         
042400                                                                          
042500     PERFORM B200-MAINLINE                                                
042600             UNTIL PS-END-OF-INSTR-DOCS.                                  
042700                                                                          
042800     PERFORM Z940-DISPLAY-CONTROL-INFO.                                   
042900                                                                          
043000     PERFORM B300-EOJ-PROCESSING.                                         
043100                                                                          
043200     STOP RUN.                                                            
043300*                                                                         
043400 EJECT                                                                    
043500******************************************************************        
043600*B100-PRE-PROCESSING                                             *        
043700*  ==> PROCESSES:                                                *        
043800*    - OPENS FILES, OPENS CURSOR FOR HEADER IN-STORE DOCUMENTS   *        
043900*      AND CONTROLS READING THE FIRST DOC. PEFORMS DATE          *        
044000*      CALCULATION PARA.                                         *        
044100*  ==> PERFORMED FROM:                                           *        
044200*                     A100-MAIN-MODULE.                          *        
044300******************************************************************        
044400 B100-PRE-PROCESSING.                                                     
044500                                                                          
044600     OPEN OUTPUT MARKUP-FILE                                              
044700                 MARKDOWN-FILE                                            
044800                 EXCESSIVE-UNITS-FILE                                     
044900                 DAMAGED-MARKDOWN-FILE.                                   
045000                                                                          
045100     EXEC SQL                                                             
045200         OPEN DOC-HDR-CSR                                                 
045300     END-EXEC.                                                            
045400                                                                          
045500     EVALUATE TRUE                                                        
045600         WHEN SQLCODE NOT = 0                                             
045700         WHEN SQLWARN0    = 'W'                                           
045800             MOVE 'B100-PRE-PROCESSING'                                   
045900                                   TO PV-PARAGRAPH-NAME                   
046000             MOVE 'OPEN CURSOR FOR TSTRPPC TABLE '                        
046100                                   TO PV-DB2-OPERATION-ATTEMPTED          
046200             PERFORM Z950-SQL-ABEND                                       
046300     END-EVALUATE.                                                        
046400                                                                          
046500     PERFORM M100-READ-INSTR-HDR.                                         
046600                                                                          
046700     PERFORM E100-GET-CALCULATE-DATES.                                    
046800*                                                                         
046900     MOVE 'SELECT FROM TPPCCTL KEY=28'                                    
047000                                   TO PV-DB2-OPERATION-ATTEMPTED.         
047100     MOVE PE048-INSTR-ME-DQ-TIMEFR-NBR                                    
047200                                   TO DK-CONTROL-NUMBER.                  
047300     PERFORM M400-GET-CONTROL-INFORMATION.                                
047400     MOVE PPCCTL-PARM-AMT          TO                                     
047500                                   PE048-INSTR-ME-DQ-TIMEFR-AMT.          
047600*                                                                         
047700     MOVE 'SELECT FROM TPPCCTL KEY=30'                                    
047800                                   TO PV-DB2-OPERATION-ATTEMPTED.         
047900     MOVE PE048-INSTR-EXCESSIVE-UNIT-NBR                                  
048000                                   TO DK-CONTROL-NUMBER.                  
048100     PERFORM M400-GET-CONTROL-INFORMATION.                                
048200     MOVE PPCCTL-PARM-AMT          TO                                     
048300                                  PE048-INSTR-EXCESSIVE-UNIT-AMT.         
048400*                                                                         
048500     MOVE 'SELECT FROM TPPCCTL KEY=31'                                    
048600                                   TO PV-DB2-OPERATION-ATTEMPTED.         
048700     MOVE PE048-INSTR-ISS-DQ-TIMEFR-NBR                                   
048800                                   TO DK-CONTROL-NUMBER.                  
048900     PERFORM M400-GET-CONTROL-INFORMATION.                                
049000     MOVE PPCCTL-PARM-AMT          TO                                     
049100                                  PE048-INSTR-ISS-DQ-TIMEFR-AMT.          
049200*                                                                         
050100     MOVE 'SELECT FROM TPPCCTL KEY=33'                                    
050200                                   TO PV-DB2-OPERATION-ATTEMPTED.         
050300     MOVE PE048-INSTR-EXCESSIVE-DLR-NBR                                   
050400                                   TO DK-CONTROL-NUMBER.                  
050500     PERFORM M400-GET-CONTROL-INFORMATION.                                
050600     MOVE PPCCTL-PARM-AMT          TO                                     
050700                                  PE048-INSTR-EXCESSIVE-DLR-AMT.          
050800*                                                                         
050900 EJECT                                                                    
051000******************************************************************        
051100*B200-MAINLINE                                                   *        
051200*  ==> PROCESSES:                                                *        
051300*    - CHECK THAT A DOCUMENT TYPE IS = 'S' AND PROCESS THE       *        
051400*      DOCUMENT IF IT IS.                                        *        
051500*    - AFTER PROCESSING THE DOCUMENT THE NEXT DOCUMENT IS READ   *        
051600*      AND PROCESSED                                             *        
051700*  ==> PERFORMED FROM:  A100-MAIN-MODULE                         *        
051800******************************************************************        
051900                                                                          
052000 B200-MAINLINE.                                                           
052100                                                                          
052200     IF STRPPC-DOC-TYP-CDE = PE049-STORE-DOC-TYPE-CDE                     
052300         MOVE STRPPC-DEST-LOC-NBR  TO DK-STR-NBR                          
052400         MOVE STRPPC-DOC-NBR       TO DK-DOC-NBR                          
052500                                                                          
052600         PERFORM C100-PROCESS-INSTR-DOCS                                  
052700                                                                          
052800     END-IF.                                                              
052900                                                                          
053000     PERFORM M100-READ-INSTR-HDR.                                         
053100*                                                                         
053200 EJECT                                                                    
053300******************************************************************        
053400*B300-EOJ-PROCESSING                                             *        
053500*  ==> PROCESSES:                                                *        
053600*    - PERFORMS CLOSE OF FILES AND IN-STORE HEADER CURSOR        *        
053700*  ==> PERFORMED FROM:  A100-MAIN-MODULE                         *        
053800******************************************************************        
053900                                                                          
054000 B300-EOJ-PROCESSING.                                                     
054100                                                                          
054200     EXEC SQL                                                             
054300         CLOSE DOC-HDR-CSR                                                
054400     END-EXEC.                                                            
054500                                                                          
054600     EVALUATE TRUE                                                        
054700         WHEN SQLCODE NOT = 0                                             
054800         WHEN SQLWARN0    = 'W'                                           
054900             MOVE 'B300-EOJ-PROCESSING'                                   
055000                                   TO PV-PARAGRAPH-NAME                   
055100             MOVE 'CLOSE CURSOR FOR TSTRPPC TABLE'                        
055200                                   TO PV-DB2-OPERATION-ATTEMPTED          
055300             PERFORM Z950-SQL-ABEND                                       
055400     END-EVALUATE.                                                        
055500                                                                          
055600     CLOSE MARKDOWN-FILE                                                  
055700           MARKUP-FILE                                                    
055800           EXCESSIVE-UNITS-FILE                                           
055900           DAMAGED-MARKDOWN-FILE.                                         
056000*                                                                         
056100 EJECT                                                                    
056200******************************************************************        
056300*C100-PROCESS-INSTR-DOCS                                         *        
056400*  ==> PROCESSES:                                                *        
056500*    - CONTROLS PROCESSING OF THE IN-STORE PRICE CHANGE DOCUMENTS*        
056600*    - ONLY THOSE DOCUMENTS IN AV AND GE STATUS CAN BE CHANGED   *        
056700*      TO DELINQUENT IF THE DOCUMENT MEETS THE CRITERIA.         *        
056800*  ==> PERFORMED FROM:  B200-MAINLINE                            *        
056900******************************************************************        
057000 C100-PROCESS-INSTR-DOCS.                                                 
057100     EVALUATE TRUE                                                        
057200         WHEN STRPPC-STATUS-CDE = PE049-IS-AVAILABLE-STATUS-CDE           
057300         WHEN STRPPC-STATUS-CDE = PE049-IS-GENERATED-STATUS-CDE           
057400                                                                          
057500             PERFORM C300-DETERMINE-IF-DELINQUENT                         
057600                                                                          
057700         WHEN STRPPC-STATUS-CDE = PE049-IS-CNTS-ENTER-STATUS-CDE          
057800                                                                          
057900             PERFORM C200-PROCESS-DOCUMENT                                
058000                                                                          
058100         WHEN OTHER                                                       
058200                                                                          
058300             CONTINUE                                                     
058400                                                                          
058500     END-EVALUATE.                                                        
058600*                                                                         
058700 EJECT                                                                    
058800******************************************************************        
058900*C200-PROCESS-DOCUMENT                                           *        
059000*  ==> PROCESSES:                                                *        
059100*    - CREATES THE MARKDOWN/MARKUP RECORDS FOR EACH DETAIL LINE  *        
059200*    - WRITES ANY 'EXCESSIVE UNIT' SKUS TO THE OUTPUT FILE.      *        
059300*    - ACCUMULATES THE ACTL PRICE CHANGE AMT FROM THE DETAIL TBL *        
059400*    - PROCESSES THE 'COUNT ENTERED' DOCUMENTS AND CHANGES THEM  *        
059500*      TO A COMPLETED STATUS, UPDATING THE HEADER TABLE WITH     *        
059600*      TOTAL PRICE CHANGE AMT.                                   *        
059700*  ==> PERFORMED FROM:  C100-PROCESS-INSTR-DOCS                  *        
059800******************************************************************        
059900 C200-PROCESS-DOCUMENT.                                                   
060000     SET PS-NOT-END-OF-DETAIL-LINES                                       
060100                                   TO TRUE.                               
060200     MOVE PE049-INSTR-RSN-VALUE-TABLE                                     
060300                                   TO PE049-INSTR-RSN-TABLE.              
060400     SET PE049-TYPE-IDX                                                   
060500         PE049-RSN-IDX             TO 1.                                  
060600     SEARCH PE049-TYPE-CODE-TABLE                                         
060700         AT END                                                           
060800             DISPLAY '** ABEND - PROGRAM PEKUP006 **'                     
060900             DISPLAY '** BAD TYPE CODE ON THE TSTRPPC TABLE'              
061000             DISPLAY 'C200-PROCESS-DOCUMENT'                              
061100             DISPLAY 'DOCUMENT: ' STRPPC-DOC-NBR                          
061200             DISPLAY 'TYPE CDE: ' STRPPC-TYP-CDE                          
061300             MOVE +4008 TO ABEND-CODE                                     
061400             CALL 'ILBOABN0' USING ABEND-CODE                             
061500         WHEN PE049-TYPE-CODE (PE049-TYPE-IDX) = STRPPC-TYP-CDE           
061600             CONTINUE                                                     
061700     END-SEARCH.                                                          
061800     SEARCH PE049-REASON-CODE-TABLE                                       
061900         AT END                                                           
062000             DISPLAY '** ABEND - PROGRAM PEKUP006 **'                     
062100             DISPLAY '** BAD RSN CODE ON THE TSTRPPC TABLE'               
062200             DISPLAY 'C200-PROCESS-DOCUMENT'                              
062300             DISPLAY 'DOCUMENT: ' STRPPC-DOC-NBR                          
062400             DISPLAY 'RSN CDE: ' STRPPC-RSN-CDE                           
062500             MOVE +4008 TO ABEND-CODE                                     
062600             CALL 'ILBOABN0' USING ABEND-CODE                             
062700         WHEN PE049-REASON-CODE (PE049-TYPE-IDX PE049-RSN-IDX)            
062800                                               = STRPPC-RSN-CDE           
062900             MOVE PE049-CONVERT-CODE                                      
063000                                   (PE049-TYPE-IDX PE049-RSN-IDX)         
063100                                   TO PV-CONVERTED-RSN-CODE               
063200             MOVE PE049-TYPE-CODE (PE049-TYPE-IDX)                        
063300                                   TO PV-FULL-RSN-CODE-TYPE               
063400             MOVE PE049-REASON-CODE (PE049-TYPE-IDX PE049-RSN-IDX)        
063500                                   TO PV-FULL-RSN-CODE-RSN                
063600     END-SEARCH.                                                          
063700                                                                          
063800     PERFORM I400-BUILD-MARKDOWN-MARKUP.                                  
063900     PERFORM M300-TOTAL-DETAIL-AMT.                                       
064000     MOVE STRPPC-STATUS-CDE        TO STRPPC-PREV-STATUS-CDE.             
064100     MOVE PE049-IS-COMPLETED-STATUS-CDE                                   
064200                                   TO STRPPC-STATUS-CDE.                  
064300     PERFORM M150-UPDATE-INSTR-HDR-COMPLETE.                              
064400                                                                          
064500*                                                                         
064600 EJECT                                                                    
064700******************************************************************        
064800*C300-DETERMINE-IF-DELINQUENT                                    *        
064900*  ==> PROCESSES:                                                *        
065000*    - A DOCUMENT IS CONSIDERED DELINQUENT IF IT IS IN AVAILABLE *        
065100*      OR GENERATED STATUS AND THE CURRENT DATE IS 'X' DAYS      *        
065200*      PAST THE CREATE DATE ('X' IS THE ISSUED DELINQUENT NUMBER *        
065300*      OF DAYS ON THE CONTROL TABLE.                             *        
065400*    OR                                                          *        
065411*-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*        
065420*  THE FOLLOWING CONDITION FOR DELINQUENCY WAS REMOVED 10/04/00  *        
065421*-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=*        
065500*    - THE DOCUMENT IS IN AVAILABLE OR GENERATED STATUS AND IS   *        
065600*      'X' NUMBER OF DAYS OR LESS BEFORE MONTH END ('X' IS THE   *        
065700*      MONTH END DELINQUENT NUMBER OF DAYS ON THE CONTROL TABLE).*        
065800*      IF THE DOCUMENT WAS CREATED ON THE LAST FRIDAY OF THE     *        
065900*      FISCAL MONTH, THE DOCUMENT WILL NOT BE CHANGED TO         *        
066000*      DELINQUENT.  IT REALLY IS TO BE PROCESSED IN THE NEXT     *        
066100*      FISCAL MONTH.                                             *        
066200*  ==> PERFORMED FROM:  C100-PROCESS-INSTR-DOCS                  *        
066300******************************************************************        
066400 C300-DETERMINE-IF-DELINQUENT.                                            
066500     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
066600         DPG51-INCREMENT-DATE                                             
066700         DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
066800     MOVE STRPPC-CRE-DTE           TO DPG52-LK-DATE-INPUT.                
066900     MOVE PE048-INSTR-ISS-DQ-TIMEFR-AMT                                   
067000                                   TO PV-INCR-DECR-DAYS.                  
067100                                                                          
           MOVE 'C300-DETERMINE-IF-DELINQUENT'                                  
                                         TO PV-PARAGRAPH-NAME.                  
067200     PERFORM Z930-CALL-DATE-CALC-MODULE.                                  
067300                                                                          
067400     IF DPG55-DB2-ISO-DATE-FMT <= PV-ISO-CURRENT-DATE                     
067500*                                                                         
067600*               7 DAYS PAST CREATE DATE - CHANGE TO DELINQUENT            
067700*                                                                         
067800         MOVE STRPPC-STATUS-CDE    TO STRPPC-PREV-STATUS-CDE              
067900         MOVE PE049-IS-DELINQUENT-STATUS-CDE                              
068000                                   TO STRPPC-STATUS-CDE                   
068100         PERFORM M125-UPDATE-INSTR-HDR-STATUS                             
068200                                                                          
073400     END-IF.                                                              
073500*                                                                         
073600 EJECT                                                                    
073700******************************************************************        
073800*E100-GET-CALCULATE-DATES                                        *        
073900*  ==> PROCESSES:                                                *        
074000*    - CONTROLS SET UP OF DATES USED BY THE PROGRAM              *        
074100*  ==> PERFORMED FROM:  B100-PRE-PROCESSING                      *        
074200******************************************************************        
074300 E100-GET-CALCULATE-DATES.                                                
074400                                                                          
074500     ACCEPT PV-TODAYS-DATE         FROM DATE.                             
074600     MOVE PV-TODAYS-DATE           TO DPG51-SYSTEM-DATE.                  
074700                                                                          
074800     ACCEPT PV-TODAYS-TIME         FROM TIME.                             
074900                                                                          
075000     MOVE PV-TODAYS-YY             TO PV-UNFORMAT-YY.                     
075100     MOVE PV-TODAYS-MM             TO PV-UNFORMAT-MM.                     
075200     MOVE PV-TODAYS-DD             TO PV-UNFORMAT-DD.                     
075300                                                                          
075400     MOVE PV-TODAYS-HR             TO PV-HR.                              
075500     MOVE PV-TODAYS-MN             TO PV-MN.                              
075600     MOVE PV-TODAYS-SC             TO PV-SC.                              
075700     MOVE PC-DOT                   TO PV-DOT-1                            
075800                                      PV-DOT-2.                           
075900                                                                          
076000     IF  PV-TODAYS-HR < PC-NOON                                           
076100                                                                          
076200         PERFORM E200-CURR-DATE-TO-DAY-BEFORE                             
076300                                                                          
076400     ELSE                                                                 
076500         SET DPG51-FISCAL-454-CALENDAR                                    
076600             DPG51-DO-NOT-INCR-DECR-DATE                                  
076700             DPG52-LK-DTE-GREG     TO TRUE                                
076800         MOVE PV-UNFORMAT-CURRENT-DATE                                    
076900                                   TO DPG52-LK-DATE-INPUT                 
077000         MOVE 0                    TO PV-INCR-DECR-DAYS                   
077100                                                                          
074300         MOVE 'E100-GET-CALCULATE-DATES'                                  
                                         TO PV-PARAGRAPH-NAME                   
077200         PERFORM Z930-CALL-DATE-CALC-MODULE                               
077300                                                                          
077400         MOVE DPG55-DB2-ISO-DATE-FMT                                      
077500                                   TO PV-ISO-CURRENT-DATE                 
077600         MOVE DPG56-LAST-FP-DB2-ISO-FMT                                   
077700                                   TO PV-ISO-MONTH-END-DATE               
077800     END-IF.                                                              
077900*                                                                         
078000 EJECT                                                                    
078100******************************************************************        
078200*E200-CURR-DATE-TO-DAY-BEFORE                                    *        
078300*  ==> PROCESSES:                                                *        
078400*    - SETS CURRENT DATE TO DAY BEFORE IF RUN AFTER MIDNIGHT     *        
078500*  ==> PERFORMED FROM:  E100-GET-CALCULATE-DATES                 *        
078600******************************************************************        
078700 E200-CURR-DATE-TO-DAY-BEFORE.                                            
078800                                                                          
078900     SET DPG51-FISCAL-454-CALENDAR                                        
079000         DPG51-DECREMENT-DATE                                             
079100         DPG52-LK-DTE-GREG         TO TRUE.                               
079200     MOVE PV-UNFORMAT-CURRENT-DATE TO DPG52-LK-DATE-INPUT.                
079300     MOVE 1                        TO PV-INCR-DECR-DAYS.                  
079400                                                                          
           MOVE 'E200-CURR-DATE-TO-DAY-BEFORE'                                  
                                         TO PV-PARAGRAPH-NAME.                  
079500     PERFORM Z930-CALL-DATE-CALC-MODULE.                                  
079600                                                                          
079700     MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-ISO-CURRENT-DATE.                
079800     MOVE DPG56-LAST-FP-DB2-ISO-FMT                                       
079900                                   TO PV-ISO-MONTH-END-DATE.              
080000*                                                                         
080100 EJECT                                                                    
080200******************************************************************        
080300*I400-BUILD-MARKDOWN-MARKUP                                      *        
080400*  ==> PROCESSES:                                                *        
080500*    - BUILDS MARKDOWN AND MARKUP RECORDS                        *        
080600*    - DETERMINES IF ANY OF THE DETAIL LINES HAVE AN EXCESSIVE   *        
080700*      UNIT AMOUNT ON THE DOCUMENT TO BE COMPLETED.              *        
080800*      THE NUMBER WHICH MAKES A SKU 'EXCESSIVE' IS ON THE        *        
080900*      CONTROL TABLE.                                            *        
081000*   -  THOSE SKUS WHICH ARE EXCESSIVE ARE WRITTEN TO AN OUTPUT   *        
081100*      FILE.                                                     *        
081200*  ==> PERFORMED FROM:  B100-PRE-PROCESSING                      *        
081300******************************************************************        
081400 I400-BUILD-MARKDOWN-MARKUP.                                              
W24472     INITIALIZE ML017-PRICE-CHANGE-RECORD.                                
081600     EVALUATE TRUE                                                        
081700         WHEN STRPPC-TYP-CDE = PE049-MARKDOWN-TYPE-CDE                    
W24472             SET ML017-MKDOWN                                             
W24472                 ML017-MARKDOWN TO TRUE                                   
082000         WHEN STRPPC-TYP-CDE = PE049-MARKDOWN-CAN-TYPE-CDE                
W24472             SET ML017-MKDOWN                                             
W24472                 ML017-MARKDOWN-CANCEL                                    
082300                                 TO TRUE                                  
082400         WHEN STRPPC-TYP-CDE = PE049-MARKUP-TYPE-CDE                      
W24472             SET ML017-MKUP                                               
W24472                 ML017-MARKUP   TO TRUE                                   
082700         WHEN STRPPC-TYP-CDE = PE049-MARKUP-CAN-TYPE-CDE                  
W24472             SET ML017-MKUP                                               
W24472                 ML017-MARKUP-CANCEL TO TRUE                              
083000     END-EVALUATE.                                                        
W24472     MOVE STRPPC-DOC-NBR         TO ML017-PC-DOC-7-NBR-PIC9.              
W24472     MOVE ML017-PC-DOC-NBR       TO ML017-PC-DOC-5-NBR.                   
W24472     MOVE STRPPC-DEST-LOC-NBR    TO PV-STORE-NBR.                         
W24472     MOVE PV-STORE-NBR-5X        TO ML017-PC-STORE-NBR.                   
083400     MOVE STRPPC-EFF-DTE         TO PV-EFF-DATE.                          
W24472     MOVE PV-EFF-MM              TO ML017-PC-DTE-MM                       
W24472     MOVE PV-EFF-DD              TO ML017-PC-DTE-DD.                      
083700*                                                                         
083800     SET DPG51-FISCAL-454-CALENDAR                                        
083900         DPG51-DO-NOT-INCR-DECR-DATE                                      
084000         DPG52-LK-DTE-ISO-FMT    TO TRUE                                  
084100     MOVE PV-EFF-DATE            TO DPG52-LK-DATE-INPUT.                  
084200     MOVE 0                      TO PV-INCR-DECR-DAYS.                    
084300                                                                          
081400     MOVE 'I400-BUILD-MARKDOWN-MARKUP'                                    
                                       TO PV-PARAGRAPH-NAME.                    
084400     PERFORM Z930-CALL-DATE-CALC-MODULE.                                  
084500                                                                          
084600     MOVE DPG56-FISCAL-PERIOD-NBR                                         
W24472                                 TO ML017-PC-KOHLS-MM.                    
084800*                                                                         
W24472     MOVE PV-CONVERTED-RSN-CODE  TO ML017-PC-RSN-CDE.                     
085000     IF PV-CONVERTED-RSN-CODE = PC-DAMAGED-REDUCE-ONHAND-RSN              
W24472         MOVE PC-DAMAGED-RSN     TO ML017-PC-RSN-CDE                      
085200     END-IF.                                                              
085300     MOVE PE049-INSTR-MKDN-MKUP-BTCH-NBR                                  
W24472                                 TO ML017-PC-BATCH-NBR.                   
W24472     MOVE PV-FULL-RSN-CODE       TO ML017-PC-MKDN-MKUP-RSN-CDE.           
085600     PERFORM I410-DETERMINE-LINE-ITEMS.                                   
085700*                                                                         
085800 EJECT                                                                    
085900******************************************************************        
086000*I410-DETERMINE-LINE-ITEMS                                       *        
086100*  ==> PROCESSES:                                                *        
086200*    - BUILDS LINE ITEMS FOR STORE DOCUMENT                      *        
086300*  ==> PERFORMED FROM:  I400-BUILD-MARKDOWN-MARKUP               *        
086400******************************************************************        
086500 I410-DETERMINE-LINE-ITEMS.                                               
086600     EXEC SQL                                                             
086700         SELECT SUM(ACTL_PR_CHG_QTY)                                      
086800           INTO :PV-TOTAL-PR-CHG-QTY:PV-TOTAL-PR-CHG-QTY-NI               
086900           FROM TSPPCDT                                                   
087100           WHERE (DEST_LOC_NBR = :DK-STR-NBR                              
087200             AND  DOC_NBR      = :DK-DOC-NBR)                             
087300     END-EXEC.                                                            
087400*                                                                         
087500     EVALUATE TRUE                                                        
087600         WHEN SQLCODE = 0                                                 
087700             MOVE 1              TO PV-LINE-ITEM                          
087800             IF PV-TOTAL-PR-CHG-QTY-NI < ZERO                             
087900                 MOVE +0         TO PV-TOTAL-PR-CHG-QTY                   
088000             END-IF                                                       
088100             IF PV-TOTAL-PR-CHG-QTY NOT = ZERO                            
088200                 PERFORM I411-BUILD-LINE-ITEMS                            
088300             ELSE                                                         
088400                 PERFORM I412-BUILD-DUMMY-LINE-ITEM                       
088500             END-IF                                                       
088600         WHEN OTHER                                                       
088700             MOVE 'I410-DETERMINE-LINE-ITEMS'                             
088800                                 TO PV-PARAGRAPH-NAME                     
088900             MOVE 'SUM ACTL PR CHGE AMT -TSPPCDT '                        
089000                                 TO PV-DB2-OPERATION-ATTEMPTED            
089100             PERFORM Z950-SQL-ABEND                                       
089200     END-EVALUATE.                                                        
089300*                                                                         
089400 EJECT                                                                    
089500******************************************************************        
089600*I411-BUILD-LINE-ITEMS.                                          *        
089700*  ==> PROCESSES:                                                *        
089800*    - BUILDS LINE ITEMS FOR ANY ROW ON THE DETAIL TABLE THAT    *        
089900*      HAS A QUANTITY GREATER THAN ZERO                          *        
090000*  ==> PERFORMED FROM:  I410-DETERMINE-LINE-ITEMS                *        
090100******************************************************************        
090200 I411-BUILD-LINE-ITEMS.                                                   
090300     PERFORM M200-OPEN-DETAIL-CURSOR.                                     
090400                                                                          
090500     SET PS-NOT-END-OF-DETAIL-LINES                                       
090600                                   TO TRUE.                               
090700                                                                          
090800     PERFORM                                                              
090900         UNTIL PS-END-OF-DETAIL-LINES                                     
091000                                                                          
091100         PERFORM M250-FETCH-DETAIL-CURSOR                                 
091200                                                                          
091300         EVALUATE TRUE                                                    
091400           WHEN SQLCODE = 0                                               
091500               IF SPPCDT-ACTL-PR-CHG-QTY NOT = 0                          
091600                   PERFORM I413-MOVE-LINE-ITEMS                           
091700                   MOVE ZERO       TO PV-TOTAL-PR-CHG-QTY                 
091800               ELSE                                                       
091900                   ADD +1          TO PV-LINE-ITEM                        
092000               END-IF                                                     
092100           WHEN SQLCODE = PC-ROW-NOT-FOUND                                
092200                 SET PS-END-OF-DETAIL-LINES                               
092300                                   TO TRUE                                
092400           WHEN OTHER                                                     
092500                 MOVE 'I411-BUILD-LINE-ITEMS'                             
092600                                   TO PV-PARAGRAPH-NAME                   
092700                 MOVE 'READ DTL TSPPCDT TABLE'                            
092800                                   TO PV-DB2-OPERATION-ATTEMPTED          
092900                 PERFORM Z950-SQL-ABEND                                   
093000         END-EVALUATE                                                     
093100     END-PERFORM.                                                         
093200                                                                          
093300     PERFORM M225-CLOSE-DETAIL-CURSOR.                                    
093400*                                                                         
093500 EJECT                                                                    
093600******************************************************************        
093700*I412-BUILD-DUMMY-LINE-ITEM.                                     *        
093800*  ==> PROCESSES:                                                *        
093900*    - BUILDS A LINE ITEM WITH THE FIRST DEPT, CLASS, SEQUENCE   *        
094000*      NUMBER WITH A COUNT OF ZERO                               *        
094100*    - SQLCODE OF 100 IS CHECKED SINCE A DOCUMENT MAY NOT HAVE   *        
094200*      DETAIL LINES.                                             *        
094300*  ==> PERFORMED FROM:                                           *        
094400*                     I410-DETERMINE-LINE-ITEMS                  *        
094500******************************************************************        
094600 I412-BUILD-DUMMY-LINE-ITEM.                                              
094700     PERFORM M200-OPEN-DETAIL-CURSOR.                                     
094800                                                                          
094900     PERFORM M250-FETCH-DETAIL-CURSOR.                                    
095000                                                                          
095100     EVALUATE TRUE                                                        
095200         WHEN SQLCODE = ZERO                                              
095300             PERFORM I413-MOVE-LINE-ITEMS                                 
095400             PERFORM M225-CLOSE-DETAIL-CURSOR                             
095500         WHEN SQLCODE = +100                                              
095600             PERFORM M225-CLOSE-DETAIL-CURSOR                             
095700         WHEN OTHER                                                       
095800             MOVE 'I412-BUILD-DUMMY-LINE-ITEMS'                           
095900                                   TO PV-PARAGRAPH-NAME                   
096000             MOVE 'READ DTL TSPPCDT TABLE'                                
096100                                   TO PV-DB2-OPERATION-ATTEMPTED          
096200             PERFORM Z950-SQL-ABEND                                       
096300     END-EVALUATE.                                                        
096400*                                                                         
096500 EJECT                                                                    
096600******************************************************************        
096700*I413-MOVE-LINE-ITEMS.                                           *        
096800*  ==> PROCESSES:                                                *        
096900*    - MOVE FIELDS TO THE MARKDOWN / MARKUP RECORD AND CONTROLS  *        
097000*      THE WRITE OF THE RECORD TO CORRECT FILE                   *        
097100*  ==> PERFORMED FROM:  I411-BUILD-LINE-ITEMS                    *        
097200*                       I412-BUILD-DUMMY-LINE-ITEM               *        
097300******************************************************************        
097400 I413-MOVE-LINE-ITEMS.                                                    
097500     SET PS-NO-EXCESSIVE-UNITS                                            
097600         PS-NO-EXCESSIVE-PRICE     TO TRUE.                               
097700     IF SPPCDT-ACTL-PR-CHG-QTY > PE048-INSTR-EXCESSIVE-UNIT-AMT           
097800         MOVE DK-STR-NBR           TO PE004-STORE-NBR                     
097900         MOVE STRPPC-EFF-DTE       TO PE004-EFF-DTE                       
098000         MOVE DK-DOC-NBR           TO PE004-DOC-NBR                       
W24472         MOVE SPPCDT-SKU-NBR       TO PV-SKU                              
W24472         MOVE PV-SKU               TO PE004-SKU-NBR                       
098500         MOVE SPPCDT-ACTL-PR-CHG-QTY                                      
098600                                   TO PE004-QUANTITY                      
098700         MOVE SPPCDT-ACTL-PR-CHG-AMT                                      
098800                                   TO PE004-PR-CHG-AMT                    
098900         MOVE SPPCDT-NEW-PR        TO PE004-NEW-PRICE                     
099000         MOVE SPPCDT-OLD-PR        TO PE004-OLD-PRICE                     
099100         SET PS-EXCESSIVE-UNITS    TO TRUE                                
099200     END-IF.                                                              
099300                                                                          
W24472     MOVE SPPCDT-SKU-NBR           TO PV-SKU.                             
W24472     MOVE PV-SKU                   TO ML017-PC-SKU.                       
W24472     IF PV-SKU (6:3) = '000' OR '999'                                     
W24472          MOVE PV-SKU (1:3)        TO ML017-PC-DEPT-NBR                   
W24472          MOVE PV-SKU (4:2)        TO ML017-PC-CLASS-NBR                  
W24472          MOVE PV-SKU (4:1)        TO PV-MAJ-CL-3-DIGIT                   
W24472          MOVE PV-MAJ-CLASS        TO ML017-PC-MAJ-CLASS                  
W24472     ELSE                                                                 
W24472          PERFORM I600-SELECT-TSKXREF                                     
W24472          MOVE PV-DEPT-NBR-9(2:3)  TO ML017-PC-DEPT-NBR                   
W24472          MOVE PV-CLASS-NBR-9(3:2) TO ML017-PC-CLASS-NBR                  
W24472          MOVE PV-MAJOR-CLASS-9    TO ML017-PC-MAJ-CLASS                  
W24472     END-IF.                                                              
W24472     MOVE SPPCDT-OLD-PR            TO ML017-PC-OLD-PR-AMT.                
W24472     MOVE SPPCDT-NEW-PR            TO ML017-PC-NEW-PR-AMT.                
W24472     MOVE PV-TOTAL-PR-CHG-QTY      TO ML017-PC-TOT-DOC-QTY.               
W24472     MOVE SPPCDT-ROW-NBR           TO ML017-PC-LINE-NBR.                  
100200     ADD +1                        TO PV-LINE-ITEM.                       
W24472     SET ML017-STORE-MKDN-MKON     TO TRUE.                               
100400                                                                          
100500     MOVE SPPCDT-NEW-PR            TO PV-NEW-RETAIL.                      
100600                                                                          
100700     MOVE SPPCDT-OLD-PR            TO PV-OLD-RETAIL.                      
100800                                                                          
100900     COMPUTE PV-PRICE-DIFF = PV-OLD-RETAIL - PV-NEW-RETAIL.               
101000                                                                          
101100     MOVE PV-PRICE-DIFF            TO ML017-PC-DIFFER-AMT.                
101200                                                                          
101300     IF PV-PRICE-DIFF > PE048-INSTR-EXCESSIVE-DLR-AMT                     
101400         IF PS-NO-EXCESSIVE-UNITS                                         
101500             MOVE DK-STR-NBR       TO PE004-STORE-NBR                     
101600             MOVE STRPPC-EFF-DTE   TO PE004-EFF-DTE                       
101700             MOVE DK-DOC-NBR       TO PE004-DOC-NBR                       
W24472             MOVE SPPCDT-SKU-NBR   TO PV-SKU                              
W24472             MOVE PV-SKU           TO PE004-SKU-NBR                       
102300             MOVE SPPCDT-ACTL-PR-CHG-QTY                                  
102400                                   TO PE004-QUANTITY                      
102500             MOVE SPPCDT-ACTL-PR-CHG-AMT                                  
102600                                   TO PE004-PR-CHG-AMT                    
102700             MOVE SPPCDT-NEW-PR    TO PE004-NEW-PRICE                     
102800             MOVE SPPCDT-OLD-PR    TO PE004-OLD-PRICE                     
102900             SET PS-EXCESSIVE-PRICE    TO TRUE                            
103000         ELSE                                                             
103100             SET PS-EXCESSIVE-PRICE    TO TRUE                            
103200         END-IF                                                           
103300     END-IF.                                                              
103400                                                                          
103500     IF (PS-EXCESSIVE-UNITS                                               
103600      OR PS-EXCESSIVE-PRICE)                                              
103700         IF (PS-EXCESSIVE-UNITS                                           
103800         AND PS-EXCESSIVE-PRICE)                                          
103900             SET PE004-EXCESSIVE-UNITS-PRICE   TO TRUE                    
104000         ELSE                                                             
104100             IF PS-EXCESSIVE-UNITS                                        
104200                 SET PE004-EXCESSIVE-UNITS     TO TRUE                    
104300             ELSE                                                         
104400                 SET PE004-EXCESSIVE-PRICE-DIFF                           
104500                                               TO TRUE                    
104600             END-IF                                                       
104700         END-IF                                                           
104800         PERFORM W700-WRITE-EXCESSIVE-UNITS                               
104900     END-IF                                                               
105000                                                                          
105100     MOVE SPPCDT-ACTL-PR-CHG-QTY   TO PV-ACTL-PR-CHG-QTY.                 
105200     IF PV-ACTL-PR-CHG-QTY > PC-MAX-PR-CHG-QTY                            
W24472         MOVE PC-MAX-PR-CHG-QTY    TO ML017-PC-QTY                        
105400         PERFORM                                                          
105500             UNTIL PV-ACTL-PR-CHG-QTY <= PC-MAX-PR-CHG-QTY                
105600             EVALUATE TRUE                                                
W24472                WHEN ML017-MARKDOWN                                       
105800                    IF PV-CONVERTED-RSN-CODE =                            
105900                                   PC-DAMAGED-REDUCE-ONHAND-RSN           
106000                        PERFORM I500-DAMAGED-PROCESSING                   
106100                    END-IF                                                
106200                    PERFORM W500-WRITE-MARKDOWN-REC                       
W24472                WHEN ML017-MARKDOWN-CANCEL                                
106400                    PERFORM W500-WRITE-MARKDOWN-REC                       
W24472                WHEN ML017-MARKUP                                         
W24472                WHEN ML017-MARKUP-CANCEL                                  
106700                    PERFORM W600-WRITE-MARKUP-REC                         
106800             END-EVALUATE                                                 
106900             SUBTRACT PC-MAX-PR-CHG-QTY FROM PV-ACTL-PR-CHG-QTY           
W24472             IF ML017-PC-TOT-DOC-QTY NOT = 0                              
W24472                 MOVE 0          TO ML017-PC-TOT-DOC-QTY                  
107200                                    PV-TOTAL-PR-CHG-QTY                   
107300             END-IF                                                       
107400         END-PERFORM                                                      
W24472         MOVE PV-ACTL-PR-CHG-QTY TO ML017-PC-QTY                          
107600     ELSE                                                                 
107700         MOVE SPPCDT-ACTL-PR-CHG-QTY                                      
W24472                                 TO ML017-PC-QTY                          
107900     END-IF.                                                              
108000     EVALUATE TRUE                                                        
W24472         WHEN ML017-MARKDOWN                                              
108200             IF PV-CONVERTED-RSN-CODE =                                   
108300                                   PC-DAMAGED-REDUCE-ONHAND-RSN           
W24472                 IF ML017-PC-QTY > 0                                      
108500                     PERFORM I500-DAMAGED-PROCESSING                      
108600                 END-IF                                                   
108700             END-IF                                                       
108800             PERFORM W500-WRITE-MARKDOWN-REC                              
W24472         WHEN ML017-MARKDOWN-CANCEL                                       
109000             PERFORM W500-WRITE-MARKDOWN-REC                              
W24472         WHEN ML017-MARKUP                                                
W24472         WHEN ML017-MARKUP-CANCEL                                         
109300             PERFORM W600-WRITE-MARKUP-REC                                
109400     END-EVALUATE.                                                        
109500*                                                                         
109600 EJECT                                                                    
109700******************************************************************        
109800*I500-DAMAGED-PROCESSING.                                        *        
109900*  ==> PROCESSES:                                                *        
110000*    - DETERMINE IF A DAMAGED MARKDOWN RECORD NEEDS TO BE        *        
110100*      WRITTEN.(REASON CODE = 7)                                 *        
110200*    - IF THE DOCUMENT IS A DAMAGED MARKDOWN, CONVERT THE REASON *        
110300*      CODE 7 TO 4 FOR THE MARKDOWN FILE.                        *        
110400*  ==> PERFORMED FROM:  I413-MOVE-LINE-ITEMS                     *        
110500******************************************************************        
110600 I500-DAMAGED-PROCESSING.                                                 
W24472     INITIALIZE FS035-ADJUSTMENT-RECORD.                                  
W24472     PERFORM I600-SELECT-TSKXREF.                                         
CHG959      IF SKXREF-ITM-NBR NOT EQUAL ZEROES                                  
W24472         MOVE SKXREF-ITM-NBR     TO FS035-ITM-NBR                         
W24472         MOVE ML017-PC-STORE-NBR TO PV-STORE-NBR-5X                       
               MOVE PV-STORE-NBR       TO PV-STORE-NBR-4X                       
W24472         MOVE PV-STORE-NBR-4N    TO FS035-LOC-NBR                         
W24472          SET FS035-OH-ADJ                                                
W24472              FS035-ACTN-NEG      TO TRUE                                 
W24472         MOVE ML017-PC-QTY       TO FS035-ADJ-QTY                         
W24472         MOVE ML017-PC-BATCH-NBR TO FS035-BTCH-NBR                        
W24472         MOVE 'PEKUP006'         TO FS035-ADJ-SRC-PGM-NM                  
W24472        PERFORM W800-WRITE-DAMAGED-MARKDOWN                               
CHG959      ELSE                                                                
CHG959         CONTINUE                                                         
CHG959      END-IF.                                                             
112000*                                                                         
W24472******************************************************************        
W24472*I600-SELECT-TSKXREF.                                                     
W24472******************************************************************        
W24472 I600-SELECT-TSKXREF.                                                     
W24472     INITIALIZE DCLTSKXREF.                                               
W24472     MOVE PV-SKU TO SKXREF-SKU.                                           
W24472                                                                          
W24472     EXEC SQL                                                             
W24472          SELECT                                                          
W24472              DEPT,                                                       
W24472              SUBCLASS,                                                   
W24472              ITM_NBR,                                                    
W24472              CLASS                                                       
W24472          INTO                                                            
W24472              :SKXREF-DEPT,                                               
W24472              :SKXREF-SUBCLASS,                                           
W24472              :SKXREF-ITM-NBR,                                            
W24472              :SKXREF-CLASS                                               
W24472          FROM                                                            
W24472              TSKXREF                                                     
W24472          WHERE                                                           
W24472              SKU = :SKXREF-SKU                                           
W24472     END-EXEC.                                                            
W24472                                                                          
W24472     EVALUATE TRUE                                                        
W24472        WHEN SQLCODE = 0                                                  
W24472            MOVE SKXREF-DEPT TO PV-DEPT-NBR-9                             
W24472            MOVE SKXREF-SUBCLASS TO PV-CLASS-NBR-9                        
W24472            MOVE SKXREF-CLASS TO PV-MAJOR-CLASS-9                         
W24472            CONTINUE                                                      
W24472        WHEN SQLCODE = +100                                               
W24472            MOVE ZEROES TO PV-DEPT-NBR-9                                  
W24472                           PV-CLASS-NBR-9                                 
W24472                           PV-MAJOR-CLASS-9                               
CHG959            IF SKXREF-ITM-NBR = 0                                         
CHG959               DISPLAY 'ITEM NBR IS 0 FOR SKU - ' SKXREF-SKU              
CHG959            ELSE                                                          
CHG959               DISPLAY 'ITEM NUMBER IS NOT FOUND FOR THIS SKU - '         
CHG959                        SKXREF-SKU                                        
CHG959            END-IF                                                        
CHG959            CONTINUE                                                      
W24472        WHEN OTHER                                                        
W24472            MOVE 'I600-SELECT-TSKXREF' TO PV-PARAGRAPH-NAME               
W24472            MOVE 'FETCH ROW FROM TSKXREF TABLE'                           
W24472                                 TO PV-DB2-OPERATION-ATTEMPTED            
W24472            PERFORM Z950-SQL-ABEND                                        
W24472     END-EVALUATE.                                                        
112100 EJECT                                                                    
112200******************************************************************        
112300*M100-READ-INSTR-HDR                                             *        
112400*  ==> PROCESSES:                                                *        
112500*    - READS THE IN-STORE PRICE CHANGE DOCUMENT HEADER           *        
112600*  ==> PERFORMED FROM:                                           *        
112700*                     B100-PRE-PROCESSING                        *        
112800*                     B200-MAINLINE.                             *        
112900******************************************************************        
113000                                                                          
113100 M100-READ-INSTR-HDR.                                                     
113200     EXEC SQL                                                             
113300         FETCH DOC-HDR-CSR                                                
113400           INTO  :STRPPC-DEST-LOC-NBR,                                    
113500                 :STRPPC-DOC-NBR,                                         
113600                 :STRPPC-DOC-TYP-CDE,                                     
113700                 :STRPPC-EFF-DTE,                                         
113800                 :STRPPC-RSN-CDE,                                         
113900                 :STRPPC-TYP-CDE,                                         
114000                 :STRPPC-STATUS-CDE,                                      
114100                 :STRPPC-CRE-DTE                                          
114200     END-EXEC.                                                            
114300                                                                          
114400     EVALUATE TRUE                                                        
114500         WHEN SQLCODE = 0                                                 
114600             CONTINUE                                                     
114700                                                                          
114800         WHEN SQLCODE  = PC-ROW-NOT-FOUND                                 
114900             SET PS-END-OF-INSTR-DOCS  TO TRUE                            
115000                                                                          
115100         WHEN OTHER                                                       
115200             MOVE 'M100-READ-INSTR-HDR'                                   
115300                                   TO PV-PARAGRAPH-NAME                   
115400             MOVE 'FETCH ROW FROM TSTRPPC TABLE  '                        
115500                                   TO PV-DB2-OPERATION-ATTEMPTED          
115600             PERFORM Z950-SQL-ABEND                                       
115700     END-EVALUATE.                                                        
115800*                                                                         
115900 EJECT                                                                    
116000******************************************************************        
116100*M125-UPDATE-INSTR-HDR-STATUS                                    *        
116200*  ==> PROCESSES:                                                *        
116300*    - UPDATES THE IN-STORE DOCUMENT HEADER STATUS TO DELINQUENT *        
116400*      AND THE PREV-STATUS-CDE TO WHATEVER THE STATUS CODE WAS.  *        
116500*  ==> PERFORMED FROM:  C300-DETERMINE-IF-DELINQUENT             *        
116600******************************************************************        
116700 M125-UPDATE-INSTR-HDR-STATUS.                                            
116800     EXEC SQL                                                             
116900       UPDATE  TSTRPPC                                                    
117000           SET STATUS_CDE      = :STRPPC-STATUS-CDE,                      
117100               PREV_STATUS_CDE = :STRPPC-PREV-STATUS-CDE                  
117300         WHERE (DEST_LOC_NBR   = :DK-STR-NBR                              
117400           AND  DOC_NBR        = :DK-DOC-NBR)                             
117500     END-EXEC.                                                            
117600                                                                          
117700     EVALUATE TRUE                                                        
117800         WHEN SQLCODE = 0                                                 
117900             ADD +1                TO PV-DOCS-CHG-TO-DQ-CNTR              
118000                                                                          
118100         WHEN OTHER                                                       
118200             MOVE 'M125-UPDATE-INSTR-HDR-STATUS'                          
118300                                   TO PV-PARAGRAPH-NAME                   
118400             MOVE 'UPD STATUS CODES ON TSTRPPC TABLE'                     
118500                                   TO PV-DB2-OPERATION-ATTEMPTED          
118600             PERFORM Z950-SQL-ABEND                                       
118700     END-EVALUATE.                                                        
118800*                                                                         
118900 EJECT                                                                    
119000******************************************************************        
119100*M150-UPDATE-INSTR-HDR-COMPLETE                                  *        
119200*  ==> PROCESSES:                                                *        
119300*    - UPDATES THE IN-STORE DOCUMENT HEADER STATUS TO COMPLETE.  *        
119400*      AND THE PREV-STATUS-CDE TO WHATEVER THE STATUS CODE WAS.  *        
119500*    - UPDATES THE COMPLETE DATE AND TIME, AND THE PRICE         *        
119600*      CHANGE AMOUNT.                                            *        
119700*  ==> PERFORMED FROM:  C200-PROCESS-DOCUMENT                    *        
119800******************************************************************        
119900 M150-UPDATE-INSTR-HDR-COMPLETE.                                          
120000     EXEC SQL                                                             
120100       UPDATE  TSTRPPC                                                    
120200           SET STATUS_CDE          = :STRPPC-STATUS-CDE,                  
120300               PREV_STATUS_CDE     = :STRPPC-PREV-STATUS-CDE,             
120400               CPL_DTE             = :PV-ISO-CURRENT-DATE,                
120500               CPL_TIME            = :PV-CURRENT-TIME,                    
120600               TOT_ACT_PR_CHG_AMT  = :PV-TOTAL-PR-CHG-AMT                 
120800         WHERE (DEST_LOC_NBR   = :DK-STR-NBR                              
120900           AND  DOC_NBR        = :DK-DOC-NBR)                             
121000     END-EXEC.                                                            
121100                                                                          
121200     EVALUATE TRUE                                                        
121300         WHEN SQLCODE = 0                                                 
121400             ADD +1                TO PV-DOCS-CHG-TO-CP-CNTR              
121500                                                                          
121600         WHEN OTHER                                                       
121700             MOVE 'M150-UPDATE-INSTR-HDR-COMPLETE'                        
121800                                   TO PV-PARAGRAPH-NAME                   
121900             MOVE 'UPD STORE/DOC ON TSTRPPC TABLE TO CP'                  
122000                                   TO PV-DB2-OPERATION-ATTEMPTED          
122100             PERFORM Z950-SQL-ABEND                                       
122200     END-EVALUATE.                                                        
122300*                                                                         
122400 EJECT                                                                    
122500******************************************************************        
122600*M200-OPEN-DETAIL-CURSOR                                         *        
122700*  ==> PROCESSES:                                                *        
122800*    - OPENS THE DETAIL CURSOR                                   *        
122900*  ==> PERFORMED FROM:  C200-PROCESS-DOCUMENT                    *        
123000******************************************************************        
123100 M200-OPEN-DETAIL-CURSOR.                                                 
123200                                                                          
W24472     MOVE DK-STR-NBR           TO SPPCDT-DEST-LOC-NBR.                    
W24472     MOVE DK-DOC-NBR           TO SPPCDT-DOC-NBR.                         
123200                                                                          
123300     EXEC SQL                                                             
123400         OPEN DOC-DTL-CSR                                                 
123500     END-EXEC.                                                            
123600                                                                          
123700     EVALUATE TRUE                                                        
123800         WHEN SQLCODE NOT = 0                                             
123900         WHEN SQLWARN0    = 'W'                                           
124000             MOVE 'M200-OPEN-DETAIL-CSR'                                  
124100                                   TO PV-PARAGRAPH-NAME                   
124200             MOVE 'OPEN CURSOR FOR TSPPCDT TABLE'                         
124300                                   TO PV-DB2-OPERATION-ATTEMPTED          
124400             PERFORM Z950-SQL-ABEND                                       
124500     END-EVALUATE.                                                        
124600*                                                                         
124700 EJECT                                                                    
124800******************************************************************        
124900*M225-CLOSE-DETAIL-CURSOR                                        *        
125000*  ==> PROCESSES:                                                *        
125100*    - CLOSES THE DETAIL CURSOR                                  *        
125200*  ==> PERFORMED FROM:  C200-PROCESS-DOCUMENT                    *        
125300******************************************************************        
125400 M225-CLOSE-DETAIL-CURSOR.                                                
125500     EXEC SQL                                                             
125600         CLOSE DOC-DTL-CSR                                                
125700     END-EXEC.                                                            
125800                                                                          
125900     EVALUATE TRUE                                                        
126000         WHEN SQLCODE NOT = 0                                             
126100         WHEN SQLWARN0    = 'W'                                           
126200             MOVE 'M225-CLOSE-DETAIL-CSR'                                 
126300                                   TO PV-PARAGRAPH-NAME                   
126400             MOVE 'CLOSE CURSOR FOR TSPPCDT TABLE'                        
126500                                   TO PV-DB2-OPERATION-ATTEMPTED          
126600             PERFORM Z950-SQL-ABEND                                       
126700     END-EVALUATE.                                                        
126800*                                                                         
126900 EJECT                                                                    
127000******************************************************************        
127100*M250-FETCH-DETAIL-CURSOR                                        *        
127200*  ==> PROCESSES:                                                *        
127300*    - GETS THE NEXT ROW ON THE DETAIL TABLE FOR A SPECIFIC      *        
127400*      STORE/DOCUMENT                                            *        
127500*  ==> PERFORMED FROM:  C200-PROCESS-DOCUMENT                    *        
127600******************************************************************        
127700 M250-FETCH-DETAIL-CURSOR.                                                
127800     EXEC SQL                                                             
127900         FETCH DOC-DTL-CSR                                                
W24472           INTO  :SPPCDT-SKU-NBR,                                         
128300                 :SPPCDT-ROW-NBR,                                         
128400                 :SPPCDT-ACTL-PR-CHG-QTY,                                 
128500                 :SPPCDT-OLD-PR,                                          
128600                 :SPPCDT-NEW-PR,                                          
128700                 :SPPCDT-ACTL-PR-CHG-AMT                                  
128800     END-EXEC.                                                            
128900                                                                          
129000     EVALUATE TRUE                                                        
129100         WHEN SQLCODE = 0                                                 
129200             CONTINUE                                                     
129300                                                                          
129400         WHEN SQLCODE  = PC-ROW-NOT-FOUND                                 
129500             SET PS-END-OF-DETAIL-LINES                                   
129600                                   TO TRUE                                
129700                                                                          
129800         WHEN OTHER                                                       
129900             MOVE 'M250-FETCH-DETAIL-CURSOR'                              
130000                                   TO PV-PARAGRAPH-NAME                   
130100             MOVE 'FETCH ROW FROM TSPPCDT TABLE'                          
130200                                   TO PV-DB2-OPERATION-ATTEMPTED          
130300             PERFORM Z950-SQL-ABEND                                       
130400     END-EVALUATE.                                                        
130500*                                                                         
130600 EJECT                                                                    
130700******************************************************************        
130800*M300-TOTAL-DETAIL-AMT.                                          *        
130900*  ==> PROCESSES:                                                *        
131000*    - TOTALS THE ACTL-PR-CHG-AMT ON THE DETAIL TABLE FOR A      *        
131100*      SPECIFIC STORE/DOC.                                       *        
131200*  ==> PERFORMED FROM:  C200-PROCESS-DOCUMENT                    *        
131300******************************************************************        
131400 M300-TOTAL-DETAIL-AMT.                                                   
131500     EXEC SQL                                                             
131600         SELECT SUM(ACTL_PR_CHG_AMT)                                      
131700           INTO :PV-TOTAL-PR-CHG-AMT:PV-TOTAL-PR-CHG-AMT-NI               
131800           FROM TSPPCDT                                                   
132000          WHERE (DEST_LOC_NBR = :DK-STR-NBR                               
132100            AND  DOC_NBR      = :DK-DOC-NBR)                              
132200     END-EXEC.                                                            
132300                                                                          
132400     EVALUATE TRUE                                                        
132500         WHEN SQLCODE = 0                                                 
132600             IF PV-TOTAL-PR-CHG-AMT-NI < ZERO                             
132700                 MOVE +0           TO PV-TOTAL-PR-CHG-AMT                 
132800             END-IF                                                       
132900                                                                          
133000         WHEN OTHER                                                       
133100             MOVE 'M300-TOTAL-DETAIL-AMT'                                 
133200                                   TO PV-PARAGRAPH-NAME                   
133300             MOVE 'SUM ROWS ON TSPPCDT TABLE'                             
133400                                   TO PV-DB2-OPERATION-ATTEMPTED          
133500             PERFORM Z950-SQL-ABEND                                       
133600     END-EVALUATE.                                                        
133700*                                                                         
133800 EJECT                                                                    
133900******************************************************************        
134000*M400-GET-CONTROL-INFORMATION.                                   *        
134100*  ==> PROCESSES:                                                *        
134200*    - GETS SPECIFIC CONTROL PARMS FROM THE CONTROL TABLE.       *        
134300*  ==> PERFORMED FROM:                                           *        
134400*                    E500-CHECK-INACTIVE-DOCS                    *        
134500******************************************************************        
134600 M400-GET-CONTROL-INFORMATION.                                            
134700                                                                          
134800     EXEC SQL                                                             
134900        SELECT PARM_AMT                                                   
135000          INTO :PPCCTL-PARM-AMT                                           
135100          FROM TPPCCTL                                                    
135200         WHERE (OPERCO_NBR = :DK-OPERATING-COMPANY                        
135300         AND    PPCCTL_NBR = :DK-CONTROL-NUMBER)                          
135400     END-EXEC.                                                            
135500                                                                          
135600     EVALUATE TRUE                                                        
135700         WHEN SQLCODE = 0                                                 
135800             CONTINUE                                                     
135900                                                                          
136000         WHEN OTHER                                                       
136100              MOVE 'M400-GET-CONTROL-INFORMATION'                         
136200                                   TO PV-PARAGRAPH-NAME                   
136300             PERFORM Z950-SQL-ABEND                                       
136400     END-EVALUATE.                                                        
136500*                                                                         
136600 EJECT                                                                    
136700******************************************************************        
136800*W500-WRITE-MARKDOWN-REC                                         *        
136900*  ==> PROCESSES:                                                *        
137000*    - WRITE MARKDOWN RECORD                                     *        
137100*  ==> PERFORMED FROM:                                           *        
137200*                     I413-MOVE-LINE-ITEMS                       *        
137300******************************************************************        
137400 W500-WRITE-MARKDOWN-REC.                                                 
137500                                                                          
137600      WRITE MARKDOWN-RECORD                                               
W24472         FROM ML017-PRICE-CHANGE-RECORD                                   
137800                                                                          
137900      ADD 1                        TO                                     
138000                                   PV-MARKDOWN-RECS-WRITTEN-CNTR.         
138100*                                                                         
138200 EJECT                                                                    
138300******************************************************************        
138400*W600-WRITE-MARKUP-REC                                           *        
138500*  ==> PROCESSES:                                                *        
138600*    - WRITE MARKUP RECORD                                       *        
138700*  ==> PERFORMED FROM:                                           *        
138800*                     I413-MOVE-LINE-ITEMS                       *        
138900******************************************************************        
139000 W600-WRITE-MARKUP-REC.                                                   
139100                                                                          
139200      WRITE MARKUP-RECORD                                                 
W24472         FROM ML017-PRICE-CHANGE-RECORD                                   
139400                                                                          
139500      ADD 1                        TO                                     
139600                                   PV-MARKUP-RECS-WRITTEN-CNTR.           
139700*                                                                         
139800 EJECT                                                                    
139900******************************************************************        
140000*W700-WRITE-EXCESSIVE-UNITS                                      *        
140100*  ==> PROCESSES:                                                *        
140200*    - WRITE THE EXCESSIVE UNITS RECORD                          *        
140300*  ==> PERFORMED FROM:                                           *        
140400******************************************************************        
140500 W700-WRITE-EXCESSIVE-UNITS.                                              
140600                                                                          
140700      WRITE EXCESSIVE-UNITS-RECORD                                        
140800         FROM PE004-EXCESSIVE-UNITS-RECORD.                               
140900                                                                          
141000      ADD 1                        TO                                     
141100                                   PV-PERD004-RECS-WRITTEN-CNTR.          
141200*                                                                         
141300 EJECT                                                                    
141400******************************************************************        
141500*W800-WRITE-DAMAGED-MARKDOWN                                     *        
141600*  ==> PROCESSES:                                                *        
141700*    - WRITE THE DAMAGED MARKDOWN RECORD                         *        
141800*  ==> PERFORMED FROM: I500-DAMAGED-PROCESSING                   *        
141900******************************************************************        
142000 W800-WRITE-DAMAGED-MARKDOWN.                                             
142100                                                                          
142200      WRITE DAMAGED-MARKDOWN-RECORD                                       
W24472         FROM FS035-ADJUSTMENT-RECORD.                                    
142400                                                                          
142500      ADD 1                        TO                                     
W24472                                   PV-FSRD035-RECS-WRITTEN-CNTR.          
142700*                                                                         
142800 EJECT                                                                    
142900******************************************************************        
143000*Z930-CALL-DATE-CALC-MODULE                                      *        
143100*  ==> PROCESSES:                                                *        
143200*    - PERFORMS THE CALL TO THE DATE CALCULATION MODULE TO       *        
143300*      DPKUT500                                                  *        
143400*  ==> PERFORMED FROM:                                           *        
143500*                     C115-CURR-DATE-TO-DATE-BEFORE              *        
143600*                     C145-CALCULATE-INACTIVE-DEL-DAYS           *        
143700*                     C155-CALCULATE-STORE-COMP-DAYS             *        
143800******************************************************************        
143900 Z930-CALL-DATE-CALC-MODULE.                                              
144000     MOVE PV-TODAYS-DATE         TO DPG51-SYSTEM-DATE.                    
144100     MOVE PV-INCR-DECR-DAYS      TO DPG51-INCR-DECR-DAYS-9.               
144200                                                                          
144300     CALL PC-CALENDAR-PGM USING DPG51 DPG52                               
144400                                DPG53 DPG54                               
144500                                DPG55 DPG56.                              
144600                                                                          
144700     EVALUATE TRUE                                                        
144800         WHEN DPG54-SEVERE-ERROR                                          
144900         WHEN DPG54-DATE-INVALID                                          
145000             DISPLAY '** ABEND - PROGRAM PEKUP006 **'                     
145100             DISPLAY '** BAD CALL TO THE CALENDAR MODULE DPKUT500'        
145200             DISPLAY PV-PARAGRAPH-NAME                                    
145300             DISPLAY DPG54-ERROR-MESSAGE                                  
                   DISPLAY 'INPUT DATE: ' DPG52-LK-DATE-INPUT                   
                   DISPLAY 'DOCUMENT: ' STRPPC-DOC-NBR                          
145400             MOVE +4008 TO ABEND-CODE                                     
145500             CALL 'ILBOABN0' USING ABEND-CODE                             
145600         WHEN OTHER                                                       
145700             CONTINUE                                                     
145800     END-EVALUATE.                                                        
145900*                                                                         
146000 EJECT                                                                    
146100******************************************************************        
146200*Z940-DISPLAY-CONTROL-INFO                                       *        
146300*  ==> PROCESSES:                                                *        
146400*    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
146500*  ==> PERFORMED FROM:                                           *        
146600*                     B100-MAINLINE                              *        
146700*                     Z950-SQL-ABEND                             *        
146800******************************************************************        
146900 Z940-DISPLAY-CONTROL-INFO.                                               
147000                                                                          
147100                                                                          
147200     MOVE PV-PERD004-RECS-WRITTEN-CNTR                                    
147300          TO RW-PERD004-RECS-WRITTEN-CNTR.                                
147400     MOVE PV-MARKDOWN-RECS-WRITTEN-CNTR                                   
147500          TO RW-MARKDOWN-RECS-WRITTEN-CNTR.                               
147600     MOVE PV-MARKUP-RECS-WRITTEN-CNTR                                     
147700          TO RW-MARKUP-RECS-WRITTEN-CNTR.                                 
W24472     MOVE PV-FSRD035-RECS-WRITTEN-CNTR                                    
W24472          TO RW-FSRD035-RECS-WRITTEN-CNTR.                                
148000     MOVE PV-DOCS-CHG-TO-DQ-CNTR                                          
148100          TO RW-CHG-DQ-RECS-UPDATED-CNTR.                                 
148200     MOVE PV-DOCS-CHG-TO-CP-CNTR                                          
148300          TO RW-CHG-CP-RECS-UPDATED-CNTR.                                 
148400                                                                          
148500     DISPLAY '*************************************************'.         
148600     DISPLAY '******* CONTROL DATA STARTS FOR PEKUP006 ********'.         
148700     DISPLAY '*************************************************'.         
148800     DISPLAY 'RW-PERD004-RECS-WRITTEN-CNTR'.                              
148900     DISPLAY '===>' DISPLAY RW-PERD004-RECS-WRITTEN-CNTR.                 
149000     DISPLAY 'RW-MARKDOWN-RECS-WRITTEN-CNTR'.                             
149100     DISPLAY '===>' DISPLAY RW-MARKDOWN-RECS-WRITTEN-CNTR.                
149200     DISPLAY 'RW-MARKUP-RECS-WRITTEN-CNTR'.                               
149300     DISPLAY '===>' DISPLAY RW-MARKUP-RECS-WRITTEN-CNTR.                  
W24472     DISPLAY 'RW-FSRD035-RECS-WRITTEN-CNTR'.                              
W24472     DISPLAY '===>' DISPLAY RW-FSRD035-RECS-WRITTEN-CNTR.                 
149600     DISPLAY 'RW-CHG-DQ-RECS-UPDATED-CNTR'.                               
149700     DISPLAY '===>' DISPLAY RW-CHG-DQ-RECS-UPDATED-CNTR.                  
149800     DISPLAY 'RW-CHG-CP-RECS-UPDATED-CNTR'.                               
149900     DISPLAY '===>' DISPLAY RW-CHG-CP-RECS-UPDATED-CNTR.                  
150000     DISPLAY 'PV-TODAYS-TIME ' PV-TODAYS-TIME.                            
150100     DISPLAY 'PV-ISO-CURRENT-DATE ' PV-ISO-CURRENT-DATE                   
150200     DISPLAY '*************************************************'.         
150300     DISPLAY '******* CONTROL DATA ENDS   FOR PEKUP006 ********'.         
150400     DISPLAY '*************************************************'.         
150500*                                                                         
150600 EJECT                                                                    
150700******************************************************************        
150800*Z950-SQL-ABEND. *                                                        
150900*  ==> PROCESSES:                                                *        
151000*    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE *                         
151100*  ==> PERFORMED FROM:                                           *        
151200*                    BY ANY PARA THAT MAKES AN EXEC SQL CALL     *        
151300*                    AND HAS AN UNEXPECTED RETURN CODE           *        
151400******************************************************************        
151500 Z950-SQL-ABEND.                                                          
151600     DISPLAY '** ABEND **'.                                               
151700     DISPLAY 'PEKUP006 - ABEND'.                                          
151800     DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
151900     DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
152000     DISPLAY 'SQLCODE   = ' SQLCODE.                                      
152100     DISPLAY 'SQLWARN0  = ' SQLWARN0.                                     
152200                                                                          
152300     PERFORM Z940-DISPLAY-CONTROL-INFO.                                   
152400                                                                          
152500     COPY DPPD004.                                                        
152600*                                                                         
152700**   DISPLAY SQLCA INFORMATION IN A READABLE FORMAT                       
152800*                                                                         
152900     CALL 'DSNTIAR' USING SQLCA                                           
153000                          DP004-FORMATTED-MESSAGE                         
153100                          DP004-ERROR-LINE-LENGTH.                        
153200*                                                                         
153300     DISPLAY DP004-ASTERISK-LINE.                                         
153400     PERFORM                                                              
153500         VARYING DP004-MSG-IDX                                            
153600         FROM 1 BY 1                                                      
153700         UNTIL DP004-MSG-IDX > DP004-MAX-MESSAGE-LINES                    
153800             DISPLAY '**'                                                 
153900                     DP004-MESSAGE-LINE (DP004-MSG-IDX)                   
154000                     ' **'                                                
154100     END-PERFORM.                                                         
154200     DISPLAY DP004-ASTERISK-LINE.                                         
154300*END INCLUDE++                                                            
154400                                                                          
154500                                                                          
154600     EXEC SQL                                                             
154700         ROLLBACK                                                         
154800     END-EXEC.                                                            
154900                                                                          
155000     MOVE +4013 TO ABEND-CODE.                                            
155100     CALL 'ILBOABN0' USING ABEND-CODE.                                    
155200                                                                          
