000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT003.                                                 
000600 AUTHOR.        LARRY SCHLEICHER - STRATAGEM                              
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   SEPTEMBER, 1996.                                          
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FROM THE INBOUND          
001300*  EDI PACKSLIP/VENDOR SHIPMENT UNIT ASSOCIATION TABLE (TINPKSU).         
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000     SELECT TIMESTAMP-FILE                                                
003100         ASSIGN TO INP01.                                                 
003200                                                                          
003300     SELECT PKSLIP-SHIP-UNIT-FILE                                         
003400         ASSIGN TO OUT02.                                                 
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  TIMESTAMP-FILE                                                       
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  TIMESTAMP-RECORD           PIC  X(50).                               
004700                                                                          
004800 FD  PKSLIP-SHIP-UNIT-FILE                                                
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  PKSLIP-SHIP-UNIT-REC       PIC  X(016).                              
005400*                                                                         
005500*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005900*                                                                         
006000*                                                                         
006500*                                                                         
006600*                                                                         
006700******************************************************************        
006800*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
006900******************************************************************        
007000     COPY RCRD011.                                                        
007100*                                                                         
007200*                                                                         
007300*                                                                         
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
007900*                                                                         
008000*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008700*                                                                         
008800*                                                                         
008900******************************************************************        
009000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE ?????????????????          
009100*  TABLE                                                                  
009200******************************************************************        
009300     EXEC SQL                                                             
009400          INCLUDE TCOCNTL                                                 
009500     END-EXEC.                                                            
009600                                                                          
009700******************************************************************        
009800*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE PACKSLIP/SHIPMENT          
009900*  UNIT ASSOCIATION FILE                                                  
010000******************************************************************        
010100     EXEC SQL                                                             
010200          INCLUDE TINPKSU                                                 
010300     END-EXEC.                                                            
010400******************************************************************        
010500*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
010600*  LOCATION TABLE                                                         
010700******************************************************************        
010800     EXEC SQL                                                             
010900          INCLUDE TSHUNLO                                                 
011000     END-EXEC.                                                            
011100*                                                                         
011200 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-PACKSLIP-SU-CSR-SW   PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-PACKSLIP-SU-CSR                VALUE 'Y'.             
011800         88  PS-MORE-PACKSLIP-SU-CSR               VALUE 'N'.             
011900                                                                          
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'SUKUT003'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
013800     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
013900                                                   COMP SYNC.             
014000     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
014100                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014500                                                                          
014600     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014800                                                                          
014900                                                                          
015000 01  SD-SYSTEM-DATE.                                                      
015100     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
015200     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
015300     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
015400                                                                          
015500*                                                                         
015600*  CURSOR DECLARATION FOR ALL EXCEPTION ACTIVITY ON THE                   
015700*  UPC CERTIFICATION ACTIVITY LOG TABLE                                   
015800*                                                                         
015900                                                                          
016000     EXEC SQL                                                             
016100          DECLARE PACKSLIP_SU_CURSOR CURSOR FOR                           
016200           SELECT DISTINCT                                                
016300             A.SHIPT_UNT_ID,                                              
016400             A.PO_NBR,                                                    
016500             A.SEQ_NBR                                                    
016600             FROM TINPKSU A                                               
016700                , TSHUNLO B                                               
016800            WHERE A.SHIPT_UNT_ID   = B.SHIPT_UNT_ID                       
016900              AND B.CRE_TMST       < :PC-CURRENT-TIMESTAMP                
P41441*             AND B.SCN_LOC_ID   IN ('RECV','NSCN')                       
P41441              AND B.SCN_LOC_ID   IN ('RECV','NSCN','WRCV')                
016920              AND B.ACTV_CDE          = 'A'                               
017000                                                                          
017100     END-EXEC.                                                            
017200                                                                          
017300                                                                          
017400******************************************************************        
017500 PROCEDURE DIVISION.                                                      
017600******************************************************************        
017700                                                                          
017800 0000-PROGRAM-MAINLINE.                                                   
017900******************************************************************        
018000* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
018100*      PROGRAM.                                                           
018200******************************************************************        
018300                                                                          
018400     PERFORM 1000-INITIALIZE.                                             
018500     IF PS-MORE-PACKSLIP-SU-CSR                                           
018600         PERFORM 2000-PROCESS-PACKSLIP-SU                                 
018700            UNTIL PS-EOF-PACKSLIP-SU-CSR                                  
018800     END-IF.                                                              
018900     PERFORM 9000-EOJ-ROUTINE.                                            
019000                                                                          
019100     STOP RUN.                                                            
019200                                                                          
019300 1000-INITIALIZE.                                                         
019400******************************************************************        
019500*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
019600*    -- OPEN OUTPUT FILE                                                  
019700*    -- OPEN CURSOR                                                       
019800*    -- FETCH CURSOR                                                      
019900******************************************************************        
020000                                                                          
020100     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
020200                                                                          
020300     OPEN OUTPUT PKSLIP-SHIP-UNIT-FILE                                    
020400          INPUT  TIMESTAMP-FILE.                                          
020500                                                                          
020600     PERFORM 1100-READ-TIMESTAMP-FILE.                                    
020700     PERFORM 7000-OPEN-PACKSLIP-SU-CURSOR.                                
020800     PERFORM 7100-FETCH-PACKSLIP-SU-CURSOR.                               
020900                                                                          
021000 1100-READ-TIMESTAMP-FILE.                                                
021100      READ TIMESTAMP-FILE INTO                                            
021200          RC011-CONTROL-RECORD.                                           
021300      MOVE RC011-CONTROL-TMST TO PC-CURRENT-TIMESTAMP.                    
021400     DISPLAY 'SUKUT003 CONTROL CARD -- ' RC011-CONTROL-TMST.              
021500                                                                          
021600 2000-PROCESS-PACKSLIP-SU.                                                
021700******************************************************************        
021800*  PROCESS THE CERTIFICATION REQUESTS (LOOP THROUGH THE RESULTS           
021900*  TABLE).  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT             
022000*  MEET THE CRITERIA.                                                     
022100******************************************************************        
022200                                                                          
022300     PERFORM 8000-WRITE-PACKSLIP-SU-RECORD.                               
022400     PERFORM 7100-FETCH-PACKSLIP-SU-CURSOR.                               
022500                                                                          
022600 7000-OPEN-PACKSLIP-SU-CURSOR.                                            
022700******************************************************************        
022800*  OPEN THE CURSOR.                                                       
022900******************************************************************        
023000     PERFORM WITH TEST AFTER                                              
023100       VARYING PV-OPEN-COUNTER FROM 1 BY 1                                
023200         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                          
023300                OR                                                        
023400                SQLCODE = ZERO)                                           
023500                                                                          
023600         EXEC SQL                                                         
023700              OPEN PACKSLIP_SU_CURSOR                                     
023800         END-EXEC                                                         
023900                                                                          
024000         EVALUATE TRUE                                                    
024100             WHEN SQLCODE = -904                                          
024200             WHEN SQLCODE = -911                                          
024300                 CONTINUE                                                 
024400             WHEN SQLWARN0 NOT = SPACES                                   
024500             WHEN SQLCODE < ZERO                                          
024600             WHEN SQLCODE > ZERO                                          
024700                 MOVE '7000-OPEN-PACKSLIP-SU-CURSOR    '                  
024800                                 TO PV-PARAGRAPH-NAME                     
024900                 MOVE 'OPEN THE PACKSLIP_SU_CURSOR    '                   
025000                                 TO PV-DB2-OPERATION-ATTEMPTED            
025100                 PERFORM 9100-SQL-ABEND                                   
025200             WHEN SQLCODE = ZERO                                          
025300                 CONTINUE                                                 
025400         END-EVALUATE                                                     
025500     END-PERFORM.                                                         
025600                                                                          
025700     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                                  
025800         MOVE '7000-OPEN-PACKSLIP-SU-CURSOR   '                           
025900                                 TO PV-PARAGRAPH-NAME                     
026000         MOVE 'OPEN MAX RETRIES EXCEEDED     '                            
026100                                 TO PV-DB2-OPERATION-ATTEMPTED            
026200         PERFORM 9100-SQL-ABEND                                           
026300     END-IF.                                                              
026400                                                                          
026500 7100-FETCH-PACKSLIP-SU-CURSOR.                                           
026600******************************************************************        
026700*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
026800******************************************************************        
026900     EXEC SQL                                                             
027000          FETCH PACKSLIP_SU_CURSOR                                        
027100           INTO :INPKSU-SHIPT-UNT-ID,                                     
027200                :INPKSU-PO-NBR,                                           
027300                :INPKSU-SEQ-NBR                                           
027400     END-EXEC.                                                            
027500                                                                          
027600     EVALUATE TRUE                                                        
027700         WHEN SQLCODE = ZERO                                              
027800             CONTINUE                                                     
027900         WHEN SQLCODE = +100                                              
028000             SET PS-EOF-PACKSLIP-SU-CSR                                   
028100                                 TO TRUE                                  
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7100-FETCH-PACKSLIP-SU-CURSOR  '                       
028600                                 TO PV-PARAGRAPH-NAME                     
028700             MOVE 'FETCH THE PACKSLIP-SU-CURSOR   '                       
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900             PERFORM 9100-SQL-ABEND                                       
029000     END-EVALUATE.                                                        
029100                                                                          
029200 8000-WRITE-PACKSLIP-SU-RECORD.                                           
029300******************************************************************        
029400*  WRITE THE PKSLIP-SHIP-SU-REC                                           
029500******************************************************************        
029600     INITIALIZE PKSLIP-SHIP-UNIT-REC.                                     
029700     WRITE PKSLIP-SHIP-UNIT-REC                                           
029800         FROM DCLTINPKSU.                                                 
029900     ADD +1 TO PV-WRITE-COUNTER.                                          
030000                                                                          
030100 8100-WRITE-TIMESTAMP-RECORD.                                             
030200******************************************************************        
030300*  WRITE THE TIMESTAMP RECORD                                             
030400******************************************************************        
030500     WRITE TIMESTAMP-RECORD                                               
030600         FROM RC011-CONTROL-RECORD.                                       
030700                                                                          
030800                                                                          
030900 9000-EOJ-ROUTINE.                                                        
031000******************************************************************        
031100*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
031200******************************************************************        
031300     CLOSE PKSLIP-SHIP-UNIT-FILE                                          
031400           TIMESTAMP-FILE.                                                
031500                                                                          
031600     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
031700     DISPLAY 'TOTAL PKSLIP SU RECORDS SELECTED ' PV-DISPLAY-COUNT.        
031800                                                                          
031900     PERFORM WITH TEST AFTER                                              
032000       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                               
032100         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                         
032200                OR                                                        
032300                SQLCODE = ZERO)                                           
032400                                                                          
032500         EXEC SQL                                                         
032600              CLOSE PACKSLIP_SU_CURSOR                                    
032700         END-EXEC                                                         
032800                                                                          
032900         EVALUATE TRUE                                                    
033000             WHEN SQLCODE = -904                                          
033100             WHEN SQLCODE = -911                                          
033200                 CONTINUE                                                 
033300             WHEN SQLWARN0 NOT = SPACES                                   
033400             WHEN SQLCODE < ZERO                                          
033500             WHEN SQLCODE > ZERO                                          
033600                 MOVE '9000-EOJ-ROUTINE               '                   
033700                                 TO PV-PARAGRAPH-NAME                     
033800                 MOVE 'CLOSE THE PACKSLIP_SU_CURSOR    '                  
033900                                 TO PV-DB2-OPERATION-ATTEMPTED            
034000                 PERFORM 9100-SQL-ABEND                                   
034100             WHEN SQLCODE = ZERO                                          
034200                 CONTINUE                                                 
034300         END-EVALUATE                                                     
034400     END-PERFORM.                                                         
034500                                                                          
034600     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                                 
034700         MOVE '9000-EOJ-ROUTINE               '                           
034800                                 TO PV-PARAGRAPH-NAME                     
034900         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                           
035000                                 TO PV-DB2-OPERATION-ATTEMPTED            
035100         PERFORM 9100-SQL-ABEND                                           
035200     END-IF.                                                              
035300                                                                          
035400 9100-SQL-ABEND.                                                          
035500******************************************************************        
035600*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
035700*  ERROR OR WARNING CODE OCCURS.                                          
035800******************************************************************        
035900     DISPLAY '9100-SQL-ABEND'.                                            
036000     DISPLAY '** ABEND     **'.                                           
036100     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
036200     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
036300     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
036400                                                                          
036500******************************************************************        
036600* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
036700* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
036800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
036900* FORMAT.                                                                 
037000******************************************************************        
037100     COPY DPPD004.                                                        
037200                                                                          
037300     MOVE +4013                  TO ABEND-CODE.                           
037400     CALL 'ILBOABN0' USING ABEND-CODE.                                    
