00001  ID  DIVISION.                                                    06/16/95
00002  PROGRAM-ID.         ETKUT035.                                    ETKUT035
00003  AUTHOR.             STEVE FLUNKER.                                  LV001
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     ETKUT035
00005  DATE-WRITTEN.       MAY 1995.                                    ETKUT035
00006  DATE-COMPILED.                                                   ETKUT035
00007                                                                   ETKUT035
00008 ******************************************************************ETKUT035
00009 *  ETKUT035 - PO/UPC RECYCLCE UTILITY (TRAN PREP FOR DELETE)      ETKUT035
00010 *  WORK REQUEST #  - 3044                                         ETKUT035
00011 ******************************************************************ETKUT035
00012 *  THIS PROGRAM WILL CREATE A FILE WHICH HAS DATA TO BE DELETED   ETKUT035
00013 *  BY THE NEXT PROGRAM.  THIS WILL ALLOW FOR CHECKPOINT RESTART   ETKUT035
00014 *  ON THE DELETE.THIS IS THE REVALIDATION PORTION OF THE RECYCLE. ETKUT035
00015 ******************************************************************ETKUT035
260107*  DATE  08/19/21                                                         
260107*  CHANGE MADE:                                                           
260107*   ADD COPYBOOK DPWS990 TO MAKE CALL TO DPKUT900 DYNAMIC                 
260107*  MADE BY: JIM HUNTER              WR# : CHG0260107                      
00016 *                                                                 ETKUT035
00017  ENVIRONMENT DIVISION.                                            ETKUT035
00018 *                                                                 ETKUT035
00019  CONFIGURATION SECTION.                                           ETKUT035
00020 *                                                                 ETKUT035
00021  SOURCE-COMPUTER.        IBM-3090.                                ETKUT035
00022  OBJECT-COMPUTER.        IBM-3090.                                ETKUT035
00023 *                                                                 ETKUT035
00024  INPUT-OUTPUT SECTION.                                            ETKUT035
00025  FILE-CONTROL.                                                    ETKUT035
00026      SELECT  PO-INPUT-FILE                                        ETKUT035
00027          ASSIGN TO INP01.                                         ETKUT035
00028                                                                   ETKUT035
00029      SELECT  PO-OUTPUT-FILE                                       ETKUT035
00030          ASSIGN TO OUT02.                                         ETKUT035
00031                                                                   ETKUT035
00032  DATA DIVISION.                                                   ETKUT035
00033  FILE SECTION.                                                    ETKUT035
00034                                                                   ETKUT035
00035  FD  PO-INPUT-FILE                                                ETKUT035
00036      LABEL RECORDS ARE STANDARD                                   ETKUT035
00037      RECORDING MODE IS F                                          ETKUT035
00038      BLOCK CONTAINS 0 RECORDS                                     ETKUT035
00039      RECORD CONTAINS 385 CHARACTERS                               ETKUT035
00040      DATA RECORD IS PO-INPUT-RECORD.                              ETKUT035
00041                                                                   ETKUT035
00042  01  PO-INPUT-RECORD             PIC X(385).                      ETKUT035
00043                                                                   ETKUT035
00044  FD  PO-OUTPUT-FILE                                               ETKUT035
00045      LABEL RECORDS ARE STANDARD                                   ETKUT035
00046      RECORDING MODE IS F                                          ETKUT035
00047      BLOCK CONTAINS 0 RECORDS                                     ETKUT035
00048      RECORD CONTAINS 350 CHARACTERS                               ETKUT035
00049      DATA RECORD IS PO-OUTPUT-RECORD.                             ETKUT035
00050                                                                   ETKUT035
00051  01  PO-OUTPUT-RECORD             PIC X(350).                     ETKUT035
00052                                                                   ETKUT035
00053  WORKING-STORAGE SECTION.                                         ETKUT035
00054 *                                                                 ETKUT035
00055  01  WS-SAVE-AREA.                                                ETKUT035
00056      05 WS-SAVE-TP-DKEY          PIC S9(9) COMP.                  ETKUT035
00057      05 WS-SAVE-TP-DKEY-TMST     PIC X(26) VALUE SPACES.          ETKUT035
00058      05 WS-SAVE-H-PO-NBR         PIC 9(9).                        ETKUT035
00059      05 WS-SAVE-H-DEPT-NBR       PIC X(03).                       ETKUT035
00060 *                                                                 ETKUT035
00061  01  PO-UPD-OR-DEL-RECORD.                                        ETKUT035
00062      05 UD-TABLE-NAME             PIC X(07).                      ETKUT035
00063      05 UD-TP-DKEY                PIC S9(9) COMP.                 ETKUT035
00064      05 UD-TP-DKEY-TMST           PIC X(26) VALUE SPACES.         ETKUT035
00065      05 UD-DATA-AREA              PIC X(354).                     ETKUT035
00066      05 UD-TFGTRHL-AREA REDEFINES UD-DATA-AREA.                   ETKUT035
00067         10  UD-TRN-SET-CDE        PIC X(3).                       ETKUT035
00068         10  UD-TRN-DTL-TXT        PIC 9(9).                       ETKUT035
00069         10  UD-RLSE-IND           PIC X(01).                      ETKUT035
00070         10  UD-RLSE-DTE           PIC X(10).                      ETKUT035
00071         10  UD-USER-ID            PIC X(08).                      ETKUT035
00072         10  FILLER                PIC X(323).                     ETKUT035
00073      05 UD-TTRPOHL-AREA REDEFINES UD-DATA-AREA.                   ETKUT035
00074         10  UD-PO-NBR             PIC 9(9).                       ETKUT035
00075         10  UD-DEPT-NBR           PIC X(03).                      ETKUT035
00076         10  UD-PO-RLSE-IND        PIC X(01).                      ETKUT035
00077         10  FILLER                PIC X(341).                     ETKUT035
00078      05 UD-TFGTRDT-AREA REDEFINES UD-DATA-AREA.                   ETKUT035
00079         10  UD-TRN-SET-SEQ-NBR    PIC S9(9) COMP.                 ETKUT035
00080         10  UD-TRN-SET-DATA       PIC X(350).                     ETKUT035
00081      05 UD-UPD-DEL-FLAG           PIC X(01) VALUE ' '.            ETKUT035
00082         88  UD-UPDATE-DB                          VALUE 'U'.      ETKUT035
00083         88  UD-DELETE-DB                          VALUE 'D'.      ETKUT035
00084         88  UD-NO-DB-ACTION                       VALUE ' '.      ETKUT035
00085                                                                   ETKUT035
00086  01  WS-WORKING-STORAGE.                                          ETKUT035
00087      05  WS-NOSKU.                                                ETKUT035
00088          10  FILLER                 PIC X(05) VALUE 'NOSKU'.      ETKUT035
00089          10  WS-NOSKU-CNT           PIC 9(03) VALUE ZERO.         ETKUT035
00090                                                                   ETKUT035
00091      05  WS-HEADER-RECORD.                                        ETKUT035
00092          10  WS-PO-NBR              PIC 9(09) VALUE ZERO.         ETKUT035
00093          10  WS-LINE-NBR            PIC 9(06) VALUE ZERO.         ETKUT035
00094          10  WS-DEPT-NBR            PIC X(03) VALUE SPACES.       ETKUT035
00095          10  WS-STATUS              PIC X(01) VALUE SPACE.        ETKUT035
00096          10  FILLER                 PIC X(28) VALUE SPACES.       ETKUT035
00097          10  WS-SHIP-DATE           PIC X(06) VALUE SPACES.       ETKUT035
00098          10  WS-INTERCHANGE-ID-QUAL PIC X(02) VALUE SPACES.       ETKUT035
00099          10  WS-INTERCHANGE-ID      PIC X(15) VALUE SPACES.       ETKUT035
00100          10  FILLER                 PIC X(280) VALUE SPACES.      ETKUT035
00101      05  WS-TP-ID-QUAL              PIC X(02) VALUE SPACES.       ETKUT035
00102      05  WS-TP-ID                   PIC X(15) VALUE SPACES.       ETKUT035
00103      05  WS-PO003-UD-UPC            PIC S9(15)V COMP-3.           ETKUT035
00104 *                                                                 ETKUT035
00105  01  PV-PROGRAM-VARIABLES.                                        ETKUT035
00106      05  PV-CHECKPOINT-TYPE           PIC  X     VALUE SPACE.     ETKUT035
00107          88  CONTROL-BREAK                       VALUE 'C'.       ETKUT035
00108          88  LOGICAL-TRANSACTION                 VALUE 'L'.       ETKUT035
00109          88  TIME-INTERVAL                       VALUE 'T'.       ETKUT035
00110          88  REQUESTED-BY-PROGRAM                VALUE 'R'.       ETKUT035
00111          88  NO-COMMIT                           VALUE ' '.       ETKUT035
00112      05  PV-DB2-KEYS.                                             ETKUT035
00113          10  PV-DB2-STR-NBR           PIC  X(03).                 ETKUT035
00114          10  PV-DB2-DOC-NBR           PIC S9(07) COMP-3.          ETKUT035
00115          10  PV-DB2-DEPT-NBR          PIC  X(03).                 ETKUT035
00116          10  PV-DB2-CL-NBR            PIC  X(03).                 ETKUT035
00117          10  FILLER                   REDEFINES                   ETKUT035
00118              PV-DB2-CL-NBR.                                       ETKUT035
00119              15  PV-DB2-CL-NBR-1      PIC  X.                     ETKUT035
00120              15  PV-DB2-CL-NBR-2-3    PIC  X(02).                 ETKUT035
00121          10  PV-DB2-SEQ-NBR           PIC  X(03).                 ETKUT035
00122          10  PV-DB2-ROW-NBR           PIC S9(04) COMP.            ETKUT035
00123          10  PV-DB2-INV-DKEY          PIC S9(09) COMP.            ETKUT035
00124      05  PV-SQL-SUB                   PIC  9(03) VALUE ZERO.      ETKUT035
00125      05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACE.     ETKUT035
00126      05  PV-DB2-OPER-ATTEMPTED        PIC  X(30) VALUE SPACE.     ETKUT035
00127                                                                   ETKUT035
00128 ******************************************************************ETKUT035
00129 * TRANSACTION PREPARATION MODULE LINKAGE SECTION.                 ETKUT035
00130 ******************************************************************ETKUT035
00131                                                                   ETKUT035
260107     COPY DPWS990.                                                ETKUT035
00131                                                                   ETKUT035
00132      COPY DPLS000.                                                ETKUT035
00133                                                                   ETKUT035
00134 *                                                                 ETKUT035
00135  01  PC-PROGRAM-CONSTANTS.                                        ETKUT035
00136      05  PC-MAX-RETRIES          PIC S9(03)   VALUE +03           ETKUT035
00137                                               COMP-3.             ETKUT035
00138      05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'ETKUT035'.ETKUT035
00139      05  PC-TRAN-PREP-FUNC-CODES.                                 ETKUT035
00140          10  PC-TRAN-PREP-FUNC-OPEN   PIC  X(05) VALUE 'OPEN '.   ETKUT035
00141          10  PC-TRAN-PREP-FUNC-WRITE  PIC  X(05) VALUE 'WRITE'.   ETKUT035
00142          10  PC-TRAN-PREP-FUNC-CLOSE  PIC  X(05) VALUE 'CLOSE'.   ETKUT035
00143      05  PC-JOB-NAME                  PIC  X(08) VALUE 'ETK162  '.ETKUT035
00144      05  PC-CKPT-PLAN-NAME            PIC  X(08) VALUE 'ETKUP035'.ETKUT035
00145      05  PC-OPERCO-NBR                PIC  X(02) VALUE '03'.      ETKUT035
00146 *                                                                 ETKUT035
00147  01  PS-PROGRAM-SWITCHES.                                         ETKUT035
00148      05  PS-END-OF-FILE             PIC X     VALUE 'N'.          ETKUT035
00149          88  PS-EOF                           VALUE 'Y'.          ETKUT035
00150      05  PS-ONE-DEPARTMENT          PIC X     VALUE 'Y'.          ETKUT035
00151          88  ONE-DEPT                         VALUE 'Y'.          ETKUT035
00152          88  MULTIPLE-DEPTS                   VALUE 'N'.          ETKUT035
00153      05  PS-KEEP-THIS-PO            PIC X     VALUE 'Y'.          ETKUT035
00154          88  KEEP-THIS-PO                     VALUE 'Y'.          ETKUT035
00155          88  KICK-OUT-THIS-PO                 VALUE 'N'.          ETKUT035
00156      05  PS-ALL-GOOD-UPCS           PIC X     VALUE 'Y'.          ETKUT035
00157          88  ALL-GOOD-UPCS                    VALUE 'Y'.          ETKUT035
00158          88  NOT-ALL-GOOD-UPCS                VALUE 'N'.          ETKUT035
00159 *                                                                 ETKUT035
00160 ***************************************************************** ETKUT035
00161 *****  TO BE PASSED FILE RECORD LAYOUT.                           ETKUT035
00162 ***************************************************************** ETKUT035
00163 *                                                                 ETKUT035
00164      COPY PORD003.                                                ETKUT035
00165      EJECT                                                        ETKUT035
00166 *                                                                 ETKUT035
00167 ******************************************************************ETKUT035
00168 *  SQL ABEND WORKING STORAGE                                      ETKUT035
00169 ******************************************************************ETKUT035
00170      COPY DPWS004.                                                ETKUT035
00171      EJECT                                                        ETKUT035
00172 *                                                                 ETKUT035
00173 ***************************************************************** ETKUT035
00174 *****  DB2 UPC LOOKUP TABLE DCLGEN                                ETKUT035
00175 ***************************************************************** ETKUT035
00176 *                                                                 ETKUT035
00177      EXEC SQL                                                     ETKUT035
00178          INCLUDE TUNUPCS                                          ETKUT035
00179      END-EXEC.                                                    ETKUT035
00180 *                                                                 ETKUT035
00181 ***************************************************************** ETKUT035
00182 *****  DB2 PO ON HOLD TABLE DCLGEN                                ETKUT035
00183 ***************************************************************** ETKUT035
00184 *                                                                 ETKUT035
00185      EXEC SQL                                                     ETKUT035
00186          INCLUDE TTRPOHL                                          ETKUT035
00187      END-EXEC.                                                    ETKUT035
00188 *                                                                 ETKUT035
00189 ***************************************************************** ETKUT035
00190 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD TABLE                     ETKUT035
00191 ***************************************************************** ETKUT035
00192 *                                                                 ETKUT035
00193      EXEC SQL                                                     ETKUT035
00194          INCLUDE TFGTRHL                                          ETKUT035
00195      END-EXEC.                                                    ETKUT035
00196 *                                                                 ETKUT035
00197 ***************************************************************** ETKUT035
00198 *****  DB2 EDI FUNCTIONAL GROUP ON HOLD DETAIL TABLE              ETKUT035
00199 ***************************************************************** ETKUT035
00200 *                                                                 ETKUT035
00201      EXEC SQL                                                     ETKUT035
00202          INCLUDE TFGTRDT                                          ETKUT035
00203      END-EXEC.                                                    ETKUT035
00204 *                                                                 ETKUT035
00205 ***************************************************************** ETKUT035
00206 *****  DB2 EDI TRADING PARTNER TABLE DCLGEN                       ETKUT035
00207 ***************************************************************** ETKUT035
00208 *                                                                 ETKUT035
00209      EXEC SQL                                                     ETKUT035
00210          INCLUDE TTRDPRT                                          ETKUT035
00211      END-EXEC.                                                    ETKUT035
00212 *                                                                 ETKUT035
00213 ***************************************************************** ETKUT035
00214 *****  DB2 EDI TRADING PARTNER TRANSMISSION TABLE DCLGEN          ETKUT035
00215 ***************************************************************** ETKUT035
00216 *                                                                 ETKUT035
00217      EXEC SQL                                                     ETKUT035
00218          INCLUDE TTRDTRN                                          ETKUT035
00219      END-EXEC.                                                    ETKUT035
00220 *                                                                 ETKUT035
00221 ***************************************************************** ETKUT035
00222 * DB2 COMMUNICATIONS AREA                                         ETKUT035
00223 ***************************************************************** ETKUT035
00224                                                                   ETKUT035
00225      EXEC SQL                                                     ETKUT035
00226          INCLUDE SQLCA                                            ETKUT035
00227      END-EXEC.                                                    ETKUT035
00228 *                                                                 ETKUT035
00229      COPY SQLCA2.                                                 ETKUT035
00230                                                                   ETKUT035
00231  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              ETKUT035
00232                                           COMP SYNC.              ETKUT035
00233      EJECT                                                        ETKUT035
00234  LINKAGE SECTION.                                                 ETKUT035
00235 *                                                                 ETKUT035
00236  PROCEDURE DIVISION.                                              ETKUT035
00237 *                                                                 ETKUT035
00238 ******************************************************************ETKUT035
00239 * MAIN-LINE LOGIC.                                                ETKUT035
00240 ******************************************************************ETKUT035
00241  A100-MAIN-MODULE.                                                ETKUT035
00242 *                                                                 ETKUT035
00243      OPEN INPUT  PO-INPUT-FILE.                                   ETKUT035
00244      OPEN OUTPUT PO-OUTPUT-FILE.                                  ETKUT035
00245 *                                                                 ETKUT035
00246      MOVE PC-TRAN-PREP-FUNC-OPEN TO DP000-FUNC-CODE.              ETKUT035
00247      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT035
00248      MOVE DP000-CKPT-TYPE-CODE TO PV-CHECKPOINT-TYPE.             ETKUT035
00249                                                                   ETKUT035
00250      MOVE SPACES TO PO-UPD-OR-DEL-RECORD.                         ETKUT035
00251      PERFORM Z100-READ-INPUT-FILE.                                ETKUT035
00252 *                                                                 ETKUT035
00253      PERFORM B100-PROCESS-PO-FILE                                 ETKUT035
00254              UNTIL PS-EOF.                                        ETKUT035
00255 *                                                                 ETKUT035
00256      CLOSE PO-INPUT-FILE                                          ETKUT035
00257            PO-OUTPUT-FILE.                                        ETKUT035
00258                                                                   ETKUT035
00259      MOVE PC-TRAN-PREP-FUNC-CLOSE TO DP000-FUNC-CODE.             ETKUT035
00260      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT035
00261 *                                                                 ETKUT035
00262      STOP RUN.                                                    ETKUT035
00263 *                                                                 ETKUT035
00264 ******************************************************************ETKUT035
00265 *  PROCESS ALL PO RECORDS FROM INPUT FILE.                        ETKUT035
00266 ******************************************************************ETKUT035
00267  B100-PROCESS-PO-FILE.                                            ETKUT035
00268 *                                                                 ETKUT035
00269       IF PO003-HEADER-RECORD                                      ETKUT035
00270          IF PO003-NEW-PO-GOOD                                     ETKUT035
00271             MOVE PO003-TP-DKEY       TO WS-SAVE-TP-DKEY           ETKUT035
00272             MOVE PO003-TP-DKEY-TMST  TO WS-SAVE-TP-DKEY-TMST      ETKUT035
00273             MOVE PO003-H-PO-NBR      TO WS-SAVE-H-PO-NBR          ETKUT035
00274             MOVE PO003-H-DEPT-NBR    TO WS-SAVE-H-DEPT-NBR        ETKUT035
00275             PERFORM Z200-WRITE-OUTPUT-FILE                        ETKUT035
00276             SET LOGICAL-TRANSACTION TO TRUE                       ETKUT035
00277             SET UD-DELETE-DB      TO TRUE                         ETKUT035
00278             PERFORM C300-UPD-OR-DEL-TFGTRDT-ROW                   ETKUT035
00279             PERFORM Z300-WRITE-UPD-OR-DEL-RECORD                  ETKUT035
00280 *                                                                 ETKUT035
00281             MOVE SPACES TO PO-UPD-OR-DEL-RECORD                   ETKUT035
00282             PERFORM Z100-READ-INPUT-FILE                          ETKUT035
00283 *                                                                 ETKUT035
00284             PERFORM UNTIL PS-EOF OR PO003-HEADER-RECORD           ETKUT035
00285                PERFORM Z200-WRITE-OUTPUT-FILE                     ETKUT035
00286                SET NO-COMMIT TO TRUE                              ETKUT035
00287                SET UD-DELETE-DB      TO TRUE                      ETKUT035
00288                PERFORM C300-UPD-OR-DEL-TFGTRDT-ROW                ETKUT035
00289                PERFORM Z300-WRITE-UPD-OR-DEL-RECORD               ETKUT035
00290                MOVE SPACES TO PO-UPD-OR-DEL-RECORD                ETKUT035
00291                PERFORM Z100-READ-INPUT-FILE                       ETKUT035
00292             END-PERFORM                                           ETKUT035
00293 *                                                                 ETKUT035
00294             MOVE SPACES TO PO-UPD-OR-DEL-RECORD                   ETKUT035
00295             SET NO-COMMIT TO TRUE                                 ETKUT035
00296             SET UD-DELETE-DB      TO TRUE                         ETKUT035
00297             PERFORM C200-UPD-OR-DEL-TTRPOHL-ROW                   ETKUT035
00298             PERFORM Z300-WRITE-UPD-OR-DEL-RECORD                  ETKUT035
00299 *                                                                 ETKUT035
00300             MOVE SPACES TO PO-UPD-OR-DEL-RECORD                   ETKUT035
00301             SET NO-COMMIT TO TRUE                                 ETKUT035
00302             SET UD-UPDATE-DB      TO TRUE                         ETKUT035
00303             PERFORM C100-UPD-OR-DEL-TFGTRHL-ROW                   ETKUT035
00304             PERFORM Z300-WRITE-UPD-OR-DEL-RECORD                  ETKUT035
00305          ELSE                                                     ETKUT035
00306             IF UPDATE-DB-RECORD                                   ETKUT035
00307                MOVE PO003-TP-DKEY       TO WS-SAVE-TP-DKEY        ETKUT035
00308                MOVE PO003-TP-DKEY-TMST  TO WS-SAVE-TP-DKEY-TMST   ETKUT035
00309                MOVE PO003-H-PO-NBR      TO WS-SAVE-H-PO-NBR       ETKUT035
00310                MOVE PO003-H-DEPT-NBR    TO WS-SAVE-H-DEPT-NBR     ETKUT035
00311                SET LOGICAL-TRANSACTION TO TRUE                    ETKUT035
00312                SET UD-UPDATE-DB      TO TRUE                      ETKUT035
00313                PERFORM C300-UPD-OR-DEL-TFGTRDT-ROW                ETKUT035
00314                PERFORM Z300-WRITE-UPD-OR-DEL-RECORD               ETKUT035
00315                IF PO003-ONE-DEPT                                  ETKUT035
00316                   PERFORM C400-CHECK-DEPT                         ETKUT035
00317                   IF SQLCODE = +000                               ETKUT035
00318                      IF TRPOHL-DEPT-NBR NOT = PO003-H-DEPT-NBR    ETKUT035
00319                         MOVE SPACES TO PO-UPD-OR-DEL-RECORD       ETKUT035
00320                         SET NO-COMMIT TO TRUE                     ETKUT035
00321                         SET UD-UPDATE-DB      TO TRUE             ETKUT035
00322                         PERFORM C200-UPD-OR-DEL-TTRPOHL-ROW       ETKUT035
00323                         PERFORM Z300-WRITE-UPD-OR-DEL-RECORD      ETKUT035
00324                      END-IF                                       ETKUT035
00325                   ELSE                                            ETKUT035
00326                     DISPLAY '** ABEND     **'                     ETKUT035
00327                     DISPLAY '** TTRPOHL RECORD NOT FOUND'         ETKUT035
00328                     DISPLAY '** PROGRAM   ** =  ETKUT035'         ETKUT035
00329                     MOVE 'B100-PROCESS-PO-FILE'                   ETKUT035
00330                                   TO  PV-PARAGRAPH-NAME           ETKUT035
00331                     MOVE 'FIND DEPT ON HELD PO'                   ETKUT035
00332                                   TO  PV-DB2-OPER-ATTEMPTED       ETKUT035
00333                     PERFORM Z900-SQL-ABEND                        ETKUT035
00334                   END-IF                                          ETKUT035
00335                END-IF                                             ETKUT035
00336             END-IF                                                ETKUT035
00337 *                                                                 ETKUT035
00338             MOVE SPACES TO PO-UPD-OR-DEL-RECORD                   ETKUT035
00339             PERFORM Z100-READ-INPUT-FILE                          ETKUT035
00340 *                                                                 ETKUT035
00341             PERFORM UNTIL PO003-HEADER-RECORD OR                  ETKUT035
00342                           PS-EOF                                  ETKUT035
00343                IF PO003-ITEM-RECORD                               ETKUT035
00344                   IF UPDATE-DB-RECORD                             ETKUT035
00345                      SET NO-COMMIT TO TRUE                        ETKUT035
00346                      SET UD-UPDATE-DB      TO TRUE                ETKUT035
00347                      PERFORM C300-UPD-OR-DEL-TFGTRDT-ROW          ETKUT035
00348                      PERFORM Z300-WRITE-UPD-OR-DEL-RECORD         ETKUT035
00349                   END-IF                                          ETKUT035
00350                END-IF                                             ETKUT035
00351                MOVE SPACES TO PO-UPD-OR-DEL-RECORD                ETKUT035
00352                PERFORM Z100-READ-INPUT-FILE                       ETKUT035
00353             END-PERFORM                                           ETKUT035
00354          END-IF                                                   ETKUT035
00355       ELSE                                                        ETKUT035
00356          DISPLAY '**  ABEND     **'                               ETKUT035
00357          DISPLAY '**  RECORDS IN THE INPUT FILE WERE'             ETKUT035
00358          DISPLAY '**  OUT OF SEQUENCE.    '                       ETKUT035
00359          DISPLAY '**  PROGRAM   **  =  ETKUT035'                  ETKUT035
00360          DISPLAY '**  PARAGRAPH **  = B100-PROCESS-FILE'          ETKUT035
00361          MOVE +4007          TO ABEND-CODE                        ETKUT035
00362          CALL 'ILBOABN0'  USING ABEND-CODE                        ETKUT035
00363       END-IF.                                                     ETKUT035
00364 *                                                                 ETKUT035
00365 ******************************************************************ETKUT035
00366 *  WRITE RECORD TO INPUT FILE FOR DELETEION INTO THE TFGTRHL TABLEETKUT035
00367 ******************************************************************ETKUT035
00368  C100-UPD-OR-DEL-TFGTRHL-ROW.                                     ETKUT035
00369 *                                                                 ETKUT035
00370      MOVE 'TFGTRHL'             TO UD-TABLE-NAME.                 ETKUT035
00371      MOVE WS-SAVE-TP-DKEY       TO UD-TP-DKEY.                    ETKUT035
00372      MOVE WS-SAVE-TP-DKEY-TMST  TO UD-TP-DKEY-TMST.               ETKUT035
00373      MOVE '855'                 TO UD-TRN-SET-CDE.                ETKUT035
00374      MOVE WS-SAVE-H-PO-NBR      TO UD-TRN-DTL-TXT.                ETKUT035
00375      MOVE 'Y'                   TO UD-RLSE-IND.                   ETKUT035
00376      MOVE '9999-09-09'          TO UD-RLSE-DTE.                   ETKUT035
00377      MOVE 'ETKUP035'            TO UD-USER-ID.                    ETKUT035
00378 *                                                                 ETKUT035
00379 ******************************************************************ETKUT035
00380 *  WRITE RECORD TO INPUT FILE FOR DELETEION INTO THE TFGTPOL TABLEETKUT035
00381 ******************************************************************ETKUT035
00382  C200-UPD-OR-DEL-TTRPOHL-ROW.                                     ETKUT035
00383 *                                                                 ETKUT035
00384      MOVE 'TTRPOHL'             TO UD-TABLE-NAME.                 ETKUT035
00385      MOVE WS-SAVE-TP-DKEY       TO UD-TP-DKEY.                    ETKUT035
00386      MOVE WS-SAVE-TP-DKEY-TMST  TO UD-TP-DKEY-TMST.               ETKUT035
00387      MOVE WS-SAVE-H-PO-NBR      TO UD-PO-NBR.                     ETKUT035
00388      MOVE WS-SAVE-H-DEPT-NBR    TO UD-DEPT-NBR.                   ETKUT035
00389      MOVE 'N'                   TO UD-PO-RLSE-IND.                ETKUT035
00390 *                                                                 ETKUT035
00391 ******************************************************************ETKUT035
00392 *  WRITE RECORD TO INPUT FILE FOR DELETEION INTO THE TFGTRDT TABLEETKUT035
00393 ******************************************************************ETKUT035
00394  C300-UPD-OR-DEL-TFGTRDT-ROW.                                     ETKUT035
00395 *                                                                 ETKUT035
00396      MOVE 'TFGTRDT'             TO UD-TABLE-NAME.                 ETKUT035
00397      MOVE PO003-TP-DKEY         TO UD-TP-DKEY.                    ETKUT035
00398      MOVE PO003-TP-DKEY-TMST    TO UD-TP-DKEY-TMST.               ETKUT035
00399      MOVE PO003-TRN-SET-SEQ-NBR TO UD-TRN-SET-SEQ-NBR.            ETKUT035
00400      MOVE PO003-EDI855-DATA     TO UD-TRN-SET-DATA.               ETKUT035
00401 *                                                                 ETKUT035
00402 ******************************************************************ETKUT035
00403 *  FIND IF THE UPC REQUEST IS BACK FROM THE CATALOG               ETKUT035
00404 ******************************************************************ETKUT035
00405  C400-CHECK-DEPT.                                                 ETKUT035
00406 *                                                                 ETKUT035
00407      PERFORM                                                      ETKUT035
00408          WITH TEST AFTER                                          ETKUT035
00409          VARYING PV-SQL-SUB                                       ETKUT035
00410          FROM 1 BY 1                                              ETKUT035
00411          UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       ETKUT035
00412                    OR SQLCODE = 0                                 ETKUT035
00413                    OR SQLCODE = +100)                             ETKUT035
00414 *                                                                 ETKUT035
00415          EXEC SQL                                                 ETKUT035
00416              SELECT A.DEPT_NBR                                    ETKUT035
00417              INTO :TRPOHL-DEPT-NBR                                ETKUT035
00418              FROM TTRPOHL A                                       ETKUT035
00419              WHERE A.TP_DKEY = :PO003-TP-DKEY AND                 ETKUT035
00420                    A.TP_DKEY_TMST = :PO003-TP-DKEY-TMST           ETKUT035
00421          END-EXEC                                                 ETKUT035
00422 *                                                                 ETKUT035
00423          EVALUATE TRUE                                            ETKUT035
00424              WHEN SQLCODE = -904                                  ETKUT035
00425              WHEN SQLCODE = -911                                  ETKUT035
00426              WHEN SQLCODE = 100                                   ETKUT035
00427              WHEN SQLCODE = 0                                     ETKUT035
00428                  CONTINUE                                         ETKUT035
00429              WHEN SQLWARN NOT = SPACES                            ETKUT035
00430              WHEN SQLCODE NOT = ZERO                              ETKUT035
00431                  MOVE 'C400-CHECK-DEPT'                           ETKUT035
00432                                TO  PV-PARAGRAPH-NAME              ETKUT035
00433                  MOVE 'FIND DEPT ON HELD PO'                      ETKUT035
00434                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT035
00435                  PERFORM Z900-SQL-ABEND                           ETKUT035
00436          END-EVALUATE                                             ETKUT035
00437      END-PERFORM.                                                 ETKUT035
00438 *                                                                 ETKUT035
00439      IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        ETKUT035
00440          MOVE 'C400-CHECK-DEPT'                                   ETKUT035
00441                                TO  PV-PARAGRAPH-NAME              ETKUT035
00442          MOVE 'VERIFY RETRIES EXCEEDED'                           ETKUT035
00443                                TO  PV-DB2-OPER-ATTEMPTED          ETKUT035
00444          PERFORM Z900-SQL-ABEND                                   ETKUT035
00445      END-IF.                                                      ETKUT035
00446 *                                                                 ETKUT035
00447 ******************************************************************ETKUT035
00448 * Z100-READ-INPUT-FILE                                            ETKUT035
00449 ******************************************************************ETKUT035
00450  Z100-READ-INPUT-FILE.                                            ETKUT035
00451 *                                                                 ETKUT035
00452      READ PO-INPUT-FILE                                           ETKUT035
00453           INTO PO003-EDI855-RECORD                                ETKUT035
00454             AT END SET PS-EOF TO TRUE.                            ETKUT035
00455 *                                                                 ETKUT035
00456 ******************************************************************ETKUT035
00457 * Z200-WRITE-OUTPUT-FILE                                          ETKUT035
00458 ******************************************************************ETKUT035
00459  Z200-WRITE-OUTPUT-FILE.                                          ETKUT035
00460 *                                                                 ETKUT035
00461      WRITE PO-OUTPUT-RECORD FROM                                  ETKUT035
00462           PO003-EDI855-DATA.                                      ETKUT035
00463 *                                                                 ETKUT035
00464 ******************************************************************ETKUT035
00465 * Z300-WRITE-UPD-OR-DEL-RECORD                                    ETKUT035
00466 ******************************************************************ETKUT035
00467  Z300-WRITE-UPD-OR-DEL-RECORD.                                    ETKUT035
00468 *                                                                 ETKUT035
00469 *    *--------------------------------------------------------    ETKUT035
00470 *    * THERE ARE NO CONTROL BREAKS IN THIS PROGRAM.  TAKING       ETKUT035
00471 *    * CHECKPOINTS BY CONTROL BREAK HAS THE SAME EFFECT AS        ETKUT035
00472 *    * TAKING CHECKPOINTS BY LOGICAL TRANSACTION.                 ETKUT035
00473 *    *--------------------------------------------------------    ETKUT035
00474      IF LOGICAL-TRANSACTION                                       ETKUT035
00475      OR CONTROL-BREAK                                             ETKUT035
00476          MOVE 'Y' TO DP000-CHECKPOINT-IND                         ETKUT035
00477      ELSE                                                         ETKUT035
00478          MOVE 'N' TO DP000-CHECKPOINT-IND                         ETKUT035
00479      END-IF.                                                      ETKUT035
00480                                                                   ETKUT035
00481      MOVE LENGTH OF PO-UPD-OR-DEL-RECORD                          ETKUT035
00482                                   TO DP000-TRANS-REC-LENGTH.      ETKUT035
00483      MOVE PO-UPD-OR-DEL-RECORD    TO DP000-TRANS-REC.             ETKUT035
00484      MOVE PC-TRAN-PREP-FUNC-WRITE TO DP000-FUNC-CODE.             ETKUT035
00485      PERFORM Z400-BUILD-COMM-AREA-TRAN-PREP.                      ETKUT035
00486                                                                   ETKUT035
00487 ******************************************************************ETKUT035
00488 * BUILD THE COMMUNICATION AREA FOR A CALL TO THE TRANSACTION      ETKUT035
00489 * PREPARATION MODULE.                                             ETKUT035
00490 ******************************************************************ETKUT035
00491  Z400-BUILD-COMM-AREA-TRAN-PREP.                                  ETKUT035
00492                                                                   ETKUT035
00493      MOVE PC-JOB-NAME             TO DP000-JOB-NAME.              ETKUT035
00494      MOVE PC-CKPT-PLAN-NAME       TO DP000-PLAN-NAME.             ETKUT035
00495      PERFORM Z900-CALL-TRANS-PREP-MODULE.                         ETKUT035
00496                                                                   ETKUT035
00497 ******************************************************************ETKUT035
00498 *     TRANSACTION PREPARATION MODULE CALL AND ERROR ROUTINES.     ETKUT035
00499 ******************************************************************ETKUT035
00500                                                                   ETKUT035
00501      COPY DPPD000.                                                ETKUT035
00502                                                                   ETKUT035
00503 *                                                                 ETKUT035
00504 ******************************************************************ETKUT035
00505 *  STANDARD DB2 ERROR ABEND ROUTINE                               ETKUT035
00506 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             ETKUT035
00507 ******************************************************************ETKUT035
00508  Z900-SQL-ABEND.                                                  ETKUT035
00509                                                                   ETKUT035
00510      MOVE +4013                 TO ABEND-CODE.                    ETKUT035
00511      DISPLAY '**  ABEND     **'.                                  ETKUT035
00512      DISPLAY '**  PROGRAM   **  =  ETKUT035'.                     ETKUT035
00513      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            ETKUT035
00514      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        ETKUT035
00515                                                                   ETKUT035
00516 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         ETKUT035
00517 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        ETKUT035
00518                                                                   ETKUT035
00519      COPY DPPD004.                                                ETKUT035
00520                                                                   ETKUT035
00521      CALL 'ILBOABN0'  USING ABEND-CODE.                           ETKUT035
