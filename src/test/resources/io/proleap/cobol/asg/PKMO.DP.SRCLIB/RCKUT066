000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             RCKUT066.                                        
000400 AUTHOR.                 DENNIS STEFFEN - OMNI RESOURCES.                 
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY, 1999.                                  
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*  THIS PROGRAM WILL DIVIDE A CHARACTER STRING INTO PARTS                 
001000*  WHILE KEEPING THE WORDS INTACT.                                        
001100                                                                          
001200 ENVIRONMENT DIVISION.                                                    
001300 CONFIGURATION SECTION.                                                   
001400 SOURCE-COMPUTER.                        IBM-3090.                        
001500 OBJECT-COMPUTER.                        IBM-3090.                        
001600 INPUT-OUTPUT SECTION.                                                    
001700 DATA DIVISION.                                                           
001800/                                                                         
001900 WORKING-STORAGE SECTION.                                                 
001910 01  PT-INPUT-TABLE.                                                      
001920     05  PT-INPUT-CHAR                PIC X(01)                           
001930                           OCCURS 500 TIMES.                              
001940 01  PV-SUBSCRIPTS.                                                       
001950     05  PV-START-SUB                 PIC S9(03) COMP-3                   
001951                                          VALUE ZERO.                     
001952     05  PV-POINT-SUB                 PIC S9(03) COMP-3                   
001953                                          VALUE ZERO.                     
001954     05  PV-END-SUB                   PIC S9(03) COMP-3                   
001955                                          VALUE ZERO.                     
001956     05  PV-OUT-SUB                   PIC S9(03) COMP-3                   
001957                                          VALUE ZERO.                     
001960     05  PV-LAST-SPACE-SUB            PIC S9(03) COMP-3                   
001961                                          VALUE ZERO.                     
001962     05  PV-BOUNDARY-SUB              PIC S9(03) COMP-3                   
001963                                          VALUE ZERO.                     
001964 01  PV-STUFF.                                                            
001965     05  PV-SEGMENT-COUNT             PIC S9(03) COMP-3                   
001966                                          VALUE ZERO.                     
001967 01  PS-SWITCHES.                                                         
001968     05  PS-BLANK-SWITCH              PIC X(01) VALUE 'Y'.                
001969         88  PS-ALL-BLANKS                      VALUE 'Y'.                
001970         88  PS-NOT-ALL-BLANKS                  VALUE 'N'.                
001971     05  PS-VERSION-SWITCH            PIC X(01) VALUE 'Y'.                
001972         88  PS-DISPLAY-COMPILE-DATE            VALUE 'Y'.                
001973         88  PS-DONT-DISPLAY-COMPILE-DATE       VALUE 'N'.                
001974 01  PROGRAM-CONSTANTS.                                                   
001975*  THESE ARE CARRIAGE CONTROL CHARACTERS WHICH CAUSE ERRATIC              
001976*  PRINTING IF NOT CHANGED TO SPACE.                                      
001977*  X'0D' IS CARRIAGE RETURN AND X'25' IS LINE FEED                        
001978     05  PC-HEX-0D                    PIC X(01) VALUE X'0D'.              
001979     05  PC-HEX-25                    PIC X(01) VALUE X'25'.              
001980     COPY RCWS035.                                                        
002000                                                                          
002100 LINKAGE SECTION.                                                         
002110 01  LINK-DATA                        PIC X(2011).                        
002700/                                                                         
002800 PROCEDURE DIVISION USING LINK-DATA.                                      
002900                                                                          
003000 0000-MAINLINE.                                                           
003100                                                                          
003101     IF PS-DISPLAY-COMPILE-DATE                                           
003103         DISPLAY SPACES                                                   
003104         DISPLAY 'RCKUT066 VERSION 3 -- JUNE 28, 1999'                    
003105         DISPLAY SPACES                                                   
003106         SET PS-DONT-DISPLAY-COMPILE-DATE                                 
003107                                      TO TRUE                             
003108     END-IF                                                               
003109                                                                          
003110     MOVE LINK-DATA                   TO RC035-DATA.                      
003111     INSPECT RC035-DATA REPLACING ALL PC-HEX-25                           
003112                            BY SPACES.                                    
003113     INSPECT RC035-DATA REPLACING ALL PC-HEX-0D                           
003114                            BY SPACES.                                    
003115     INITIALIZE PV-SUBSCRIPTS                                             
003116                PV-SEGMENT-COUNT                                          
003117                                                                          
003120     EVALUATE TRUE                                                        
003200     WHEN RC035-STRING-LENGTH  = ZERO                                     
003300         DISPLAY '****   NO INPUT STRING TO PROCESS  ****'                
003400         PERFORM THE-DUMPER                                               
003410     WHEN RC035-STRING-LENGTH  > 500                                      
003510         DISPLAY '****   INPUT LONGER THAN 500 BYTES ****'                
003520         PERFORM THE-DUMPER                                               
003600     END-EVALUATE.                                                        
003700                                                                          
003800     MOVE RC035-STRING                TO PT-INPUT-TABLE                   
003910     MOVE 1                           TO PV-START-SUB                     
003911                                         PV-OUT-SUB                       
003912                                                                          
003913     PERFORM 100-PROCESS-STRING                                           
003914         UNTIL PV-END-SUB >= RC035-STRING-LENGTH.                         
003915                                                                          
003916     MOVE RC035-DATA                  TO LINK-DATA.                       
003917                                                                          
003918 THE-DUMPER.                                                              
003919     GOBACK.                                                              
003920/                                                                         
003921 100-PROCESS-STRING.                                                      
003922                                                                          
003923     SET PS-ALL-BLANKS                TO TRUE                             
003924     COMPUTE PV-END-SUB = (PV-START-SUB + RC035-TARGET-SIZE) - 1          
003925     ADD 1                            TO PV-SEGMENT-COUNT                 
003926     COMPUTE PV-BOUNDARY-SUB = RC035-TARGET-SIZE *                        
003927                               PV-SEGMENT-COUNT                           
003928                                                                          
003929     IF PV-END-SUB >= RC035-STRING-LENGTH                                 
003930         MOVE RC035-STRING-LENGTH     TO PV-LAST-SPACE-SUB                
003931         PERFORM 150-MOVE-STRING-TO-TARGET                                
003945     ELSE                                                                 
003947         PERFORM VARYING PV-POINT-SUB FROM                                
003948             PV-START-SUB BY 1                                            
003949             UNTIL PV-POINT-SUB > PV-END-SUB                              
003950             IF PT-INPUT-CHAR(PV-POINT-SUB) = SPACE OR '-' OR ','         
003953                 MOVE PV-POINT-SUB    TO PV-LAST-SPACE-SUB                
003954             ELSE                                                         
003955                 IF PT-INPUT-CHAR(PV-POINT-SUB + 1) = SPACE               
003956                                           OR '-' OR ','                  
003959                     MOVE PV-POINT-SUB TO PV-LAST-SPACE-SUB               
003960                 END-IF                                                   
003961             END-IF                                                       
003962         END-PERFORM                                                      
003963         PERFORM 150-MOVE-STRING-TO-TARGET                                
003965         ADD 1 TO PV-LAST-SPACE-SUB GIVING PV-START-SUB                   
003966     END-IF.                                                              
003967                                                                          
003968* COUNT ONLY THE SEGMENTS THAT CONTAIN NON-BLANK CHARACTERS               
003969     IF PS-NOT-ALL-BLANKS                                                 
003970         MOVE PV-SEGMENT-COUNT        TO RC035-SEGMENT-COUNT              
003980     END-IF.                                                              
003990                                                                          
003991 150-MOVE-STRING-TO-TARGET.                                               
003992                                                                          
003993     PERFORM VARYING PV-POINT-SUB FROM PV-START-SUB BY 1                  
003994         UNTIL PV-POINT-SUB > PV-LAST-SPACE-SUB                           
003995         MOVE PT-INPUT-CHAR(PV-POINT-SUB)                                 
003996                                      TO                                  
003997                                RC035-OUT-CHAR(PV-OUT-SUB)                
003998         ADD 1                        TO PV-OUT-SUB                       
003999         IF PT-INPUT-CHAR(PV-POINT-SUB) NOT = SPACE                       
004000             SET PS-NOT-ALL-BLANKS    TO TRUE                             
004001         END-IF                                                           
004002     END-PERFORM.                                                         
004003                                                                          
004004     PERFORM UNTIL PV-OUT-SUB > PV-BOUNDARY-SUB                           
004010         MOVE SPACES                  TO                                  
004100                                RC035-OUT-CHAR(PV-OUT-SUB)                
004210         ADD 1                        TO PV-OUT-SUB                       
004300     END-PERFORM.                                                         
