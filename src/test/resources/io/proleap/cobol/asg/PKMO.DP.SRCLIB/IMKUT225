      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    IMKUT225.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-COMPILED. 07/29/2003.                                               
      *                                                                         
      ******************************************************************        
      *      IMKUT225 - ONHAND VALIDATION FOR STYLE DELETES            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT STYLE-DELETE-IN                                               
               ASSIGN TO INP01.                                                 
           SELECT STYLE-DELETE-OUT                                              
               ASSIGN TO OUT01.                                                 
           SELECT INVALID-DELETE-OUT                                            
               ASSIGN TO OUT02.                                                 
      *                                                                         
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
      *                                                                         
       FD  STYLE-DELETE-IN                                                      
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STYLE-DELETE-IN-REC             PIC X(100).                          
      *                                                                         
       FD  STYLE-DELETE-OUT                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STYLE-DELETE-OUT-REC            PIC X(100).                          
      *                                                                         
       FD  INVALID-DELETE-OUT                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-DELETE-OUT-REC          PIC X(100).                          
      *                                                                         
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)  VALUE ZERO               
                                                       COMP SYNC.               
      *                                                                         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORDS-READ             PIC 9(07)   VALUE ZERO.              
           05  PA-VALID-RECS-WRITTEN       PIC 9(07)   VALUE ZERO.              
           05  PA-INVALID-RECS-WRITTEN     PIC 9(07)   VALUE ZERO.              
           05  PA-COUNT                    PIC S9(7)   VALUE ZERO               
                                                        COMP-3.                 
      *                                                                         
       01  PA-PROGRAM-CONSTANTS.                                                
           05  PA-PROGRAM-NAME             PIC X(08)   VALUE                    
               'IMKUT225'.                                                      
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW           PIC X(01)  VALUE 'N'.                
               88  PS-END-OF-FILE                     VALUE 'Y'.                
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA-NAME                PIC X(35).                           
      *                                                                         
       01  DL-DISPLAY-LINE1.                                                    
           05  FILLER                      PIC X(15)     VALUE                  
               ' *** NUMBER OF '.                                               
           05  FILLER                      PIC X(21)     VALUE                  
               ' ITEM RECORDS READ = '.                                         
           05  DL-RECORDS-READ             PIC Z,ZZZ,ZZ9 VALUE ZERO.            
       01  DL-DISPLAY-LINE2.                                                    
           05  FILLER                      PIC X(15)     VALUE                  
               ' *** NUMBER OF '.                                               
           05  FILLER                      PIC X(31)     VALUE                  
               'ITEM RECORDS WITH NO ON HANDS '.                                
           05  DL-NO-ONHANDS               PIC Z,ZZZ,ZZ9 VALUE ZERO.            
       01  DL-DISPLAY-LINE3.                                                    
           05  FILLER                      PIC X(15)     VALUE                  
               ' *** NUMBER OF '.                                               
           05  FILLER                      PIC X(28)     VALUE                  
               'ITEM RECORDS WITH ON HANDS '.                                   
           05  DL-ONHANDS                  PIC Z,ZZZ,ZZ9 VALUE ZERO.            
                                                                                
           COPY IMRD020.                                                FSKRP071
      *                                                                         
      *                                                                         
           COPY SQLCA2.                                                         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SYSDUMMY                                                 
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
                INCLUDE TSKST                                                   
           END-EXEC.                                                            
      *                                                                         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
           EJECT                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *                                                                         
           EJECT                                                                
       0000-IMKUT225.                                                           
      ******************************************************************        
      * MAIN PROCESSING ROUTINE                                        *        
      ******************************************************************        
                                                                                
           OPEN INPUT  STYLE-DELETE-IN.                                         
           OPEN OUTPUT STYLE-DELETE-OUT.                                        
           OPEN OUTPUT INVALID-DELETE-OUT.                                      
                                                                                
           PERFORM 4000-READ-STYLE-DELETE-IN.                                   
                                                                                
           PERFORM 2000-PROCESS-DELETIONS                                       
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-END-OF-JOB.                                             
           GOBACK.                                                              
                                                                                
                                                                                
       2000-PROCESS-DELETIONS.                                                  
                                                                                
           MOVE '*2000-PROCESS-DELETIONS.         *' TO PV-PARA-NAME            
      *                                                                         
           PERFORM 6000-CHECK-FOR-ONHANDS.                                      
           IF PA-COUNT = 0                                                      
               PERFORM 5000-WRITE-VALID-DELETE                                  
           ELSE                                                                 
               PERFORM 5200-WRITE-INVALID-DELETE                                
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-STYLE-DELETE-IN.                                   
                                                                                
                                                                                
       3000-END-OF-JOB.                                                         
      ******************************************************************        
      *    DISPLAY EOJ STATS                                           *        
      ******************************************************************        
                                                                                
           MOVE PA-RECORDS-READ TO DL-RECORDS-READ.                             
           DISPLAY DL-DISPLAY-LINE1.                                            
           MOVE PA-VALID-RECS-WRITTEN      TO DL-NO-ONHANDS.                    
           DISPLAY DL-DISPLAY-LINE2.                                            
           MOVE PA-INVALID-RECS-WRITTEN    TO DL-ONHANDS.                       
           DISPLAY DL-DISPLAY-LINE3.                                            
      *                                                                         
           CLOSE STYLE-DELETE-IN                                                
                 STYLE-DELETE-OUT                                               
                 INVALID-DELETE-OUT.                                            
      *                                                                         
      *                                                                         
       4000-READ-STYLE-DELETE-IN.                                               
                                                                                
           READ STYLE-DELETE-IN INTO IM020-STYLE-DELETE-REC                     
              AT END                                                            
                 SET PS-END-OF-FILE TO TRUE.                                    
                                                                                
           IF PS-END-OF-FILE                                                    
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1  TO PA-RECORDS-READ                                         
           END-IF.                                                              
                                                                                
       5000-WRITE-VALID-DELETE.                                                 
                                                                                
           WRITE STYLE-DELETE-OUT-REC FROM IM020-STYLE-DELETE-REC.              
                                                                                
           ADD 1 TO PA-VALID-RECS-WRITTEN.                                      
                                                                                
       5200-WRITE-INVALID-DELETE.                                               
                                                                                
           WRITE INVALID-DELETE-OUT-REC FROM IM020-STYLE-DELETE-REC.            
                                                                                
           ADD 1 TO PA-INVALID-RECS-WRITTEN.                                    
                                                                                
       6000-CHECK-FOR-ONHANDS.                                                  
           MOVE '*6000-CHECK-FOR-ONHANDS           *' TO PV-PARA-NAME           
      ******************************************************************        
      * UPC/SKU ROW                                                    *        
      ******************************************************************        
                                                                                
           MOVE 0 TO PA-COUNT.                                                  
                                                                                
           EXEC SQL                                                             
             SELECT 1                                                           
               INTO :PA-COUNT                                                   
               FROM SYSIBM.SYSDUMMY1 X                                          
              WHERE EXISTS                                                      
                   (SELECT 1                                                    
                       FROM TSKST                                               
                      WHERE ITEM_NMBR  = :IM020-ITM-NBR                         
                        AND ONHAND_QTY <> 0                                     
                        AND LOC_NBR    <> SMALLINT(0)                           
                        AND X.IBMREQD > ' ')                                    
             WITH UR                                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = +0                                                 
              WHEN SQLCODE = +100                                               
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 PERFORM 9998-SQL-ERROR                                         
           END-EVALUATE.                                                        
                                                                                
       9998-SQL-ERROR.                                                          
      ******************************************************************        
      * ABEND ROUTINE FOR BAD SQL CALLS                                *        
      ******************************************************************        
           MOVE SQLCODE                    TO SQLCODE2.                         
           MOVE SQLERRD(3)                 TO SQLERRD3.                         
           DISPLAY '** ABEND **          = SQL ERROR'.                          
           DISPLAY 'ITEM NUMBER          = ' IM020-ITM-NBR.                     
           DISPLAY '** PROGRAM **        = ' PA-PROGRAM-NAME.                   
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.                      
           DISPLAY '** SQLCODE **        = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
           DISPLAY '** SQLERRM **        = ' SQLERRM.                           
           DISPLAY '** SQLERRMC **       = ' SQLERRMC.                          
                                                                                
           DISPLAY '** SQLWARN0 **       = ' SQLWARN0.                          
           DISPLAY '** SQLWARN1 **       = ' SQLWARN1.                          
           DISPLAY '** SQLWARN2 **       = ' SQLWARN2.                          
           DISPLAY '** SQLWARN3 **       = ' SQLWARN3.                          
           DISPLAY '** SQLWARN4 **       = ' SQLWARN4.                          
           DISPLAY '** SQLWARN5 **       = ' SQLWARN5.                          
           DISPLAY '** SQLWARN6 **       = ' SQLWARN6.                          
           DISPLAY '** SQLWARN7 **       = ' SQLWARN7.                          
      *                                                                         
           MOVE +4013                      TO ABEND-CODE.                       
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
