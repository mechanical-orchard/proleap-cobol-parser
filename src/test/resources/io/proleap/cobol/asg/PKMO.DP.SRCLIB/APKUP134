000100 IDENTIFICATION DIVISION.                                         08/21/95
000200 PROGRAM-ID.     APKUP134.                                        APKUP037
000300 AUTHOR.         R BERNOSKI.                                         LV004
000400*DATE-WRITTEN.   NOV 16,1998.                                        CL**1
000500*DATE-COMPILED.                                                      CL**1
000600*************************************************************        CL**1
000700*    COBOL 2  WITH SQL                                      *        CL**1
000800*                                                           *        CL**1
000900*    MAINTAIN POSTING PERIODS ON THE AP CONTROL TABLE.      *        CL**1
001000*                                                           *        CL**1
001100*    THE PURPOSE OF THIS PROGRAM IS TO UPDATE THE CURRENT   *        CL**1
001200*    AND PREVIOUS POSTING PERIOD COLUMNS ON THE AP CONTROL  *        CL**1
001300*    TABLE.  EACH WEEK THE CURRENT POSTING PERIOD NEEDS TO  *        CL**1
001400*    MODIFIED TO REFLECT THE NEW FISCAL WEEK.  THE PREVIOUS *        CL**1
001500*    PERIOD FIELDS WILL ALSO BE UPDATED, EXEPT DURING THE   *        CL**1
001600*    SPAN WHEN THERE ARE MULTIPLE POSTING PERIODS OPEN      *        CL**1
001700*    - DURING THIS TIME, THE PREVIOUS POSTING PERIOD MUST   *        CL**1
001800*    REMAIN COSTANT, SO IT WILL NOT BE UPDATED UNTIL THAT   *        CL**1
001900*    PERIOD HAS BEEN CLOSED AND THE MULTIPLE PERIOD         *        CL**1
002000*    INDICATOR IS TURNED OFF.                               *        CL**1
002100*                                                           *        CL**1
002200*************************************************************        CL**1
002300*                                                           *        CL**1
002400*    TAPCNTL - AP BATCH CONTROL TABLE.                      *        CL**1
002500*                                                           *        CL**1
002600*************************************************************        CL**1
002700* CHANGE LOG:                                               *        CL**1
002800*                                                           *        CL**1
002900* 11/16/98     INITIAL VERSION.                             *        CL**1
003000*                                                           *        CL**1
003100* 07/26/99                                                  *        CL**1
003200*    MODIFIED PROGRAM SO THAT A CONTROL CARD IS ACCEPTED    *        CL**1
003300*    PROVIDING THE NUMBER OF DAYS UNTIL SUNDAY.  THIS       *        CL**1
003400*    DETERMINES THAT THE DAY IS A SUNDAY THROUGHOUT THE     *        CL**1
003500*    PROCESSING.                                            *             
003600*    CHANGE MADE BY:  ROBIN BERNOSKI      WR/PITS#:         *        CL**1
003700*                                                           *        CL**1
003100* 10/11/1999 CHANGE DATE-INVALID TO WARNING-ERROR IN DATE   *        CL**1
003200*        ROUTINE CHECKING                                   *        CL**1
003300*        BY: L SCHALLHORN                           WR 06041*        CL**1
003400*                                                           *        CL**1
003900*************************************************************        CL**1
004000                                                                     CL**1
004100 ENVIRONMENT DIVISION.                                               CL**1
004200                                                                     CL**1
004300 INPUT-OUTPUT SECTION.                                               CL**1
004400                                                                     CL**1
004500 DATA DIVISION.                                                      CL**1
004600                                                                     CL**1
004700 FILE SECTION.                                                       CL**1
004800                                                                     CL**1
004900/                                                                    CL**1
005000 WORKING-STORAGE SECTION.                                            CL**1
005100                                                                     CL**1
005200 01  FILLER                           PIC X(25) VALUE                CL**1
005300                                   'WS FOR APKUP134 BEGINS==>'.      CL**1
005400 01  ABEND-CODE                       PIC S9(4) COMP SYNC            CL**1
005500                                                VALUE ZEROS.         CL**1
005600     88 AP-DB2-ERROR               VALUE +4013.                      CL**1
005700*                                                                    CL**1
005800 01  PV-WORK-AREA.                                                   CL**1
005900     05  PV-PROGRAM-NAME              PIC X(08) VALUE 'APKUP134'.    CL**1
006000     05  PV-CURRENT-PARAGRAPH         PIC X(40)  VALUE SPACES.       CL**1
006100     05  PV-DATE-YYYYMMDD-9           PIC 9(08)  VALUE ZEROS.             
006200     05  PV-YYYYMMDD        REDEFINES PV-DATE-YYYYMMDD-9.                 
006300         10  PV-DATE-YYYY             PIC  X(04).                         
006400         10  PV-DATE-MM               PIC  X(02).                         
006500         10  PV-DATE-DD               PIC  X(02).                         
006600     05  PV-PSTG-FYR-NBR              PIC  X(04) VALUE SPACES.            
006700     05  PV-PSTG-FMN-NBR              PIC  X(02) VALUE SPACES.            
006800     05  PV-PSTG-FWK-NBR              PIC  X(02) VALUE SPACES.            
006900     05  PV-DAY-OF-WEEK               PIC  X(07) VALUE SPACES.            
007000     05  PV-FISCAL-YEAR               PIC  X(04) VALUE SPACES.            
007100     05  PV-FISCAL-MONTH              PIC  X(02) VALUE SPACES.            
007200     05  PV-FISCAL-WEEK               PIC  X(02) VALUE SPACES.            
007300     05  PV-BTCH-DTE-PLUS-DAY         PIC  X(10) VALUE SPACES.            
007400*                                                                    CL**1
007500 01  PC-WORK-AREA.                                                   CL**1
007600     05  PC-SUNDAY                    PIC  X(06) VALUE 'SUNDAY'.          
007700*                                                                         
007800 01  SYSTEM-DATE.                                                    CL**1
007900     05 DATE-IN-YY                    PIC XX.                        CL**1
008000     05 DATE-IN-MM                    PIC XX.                        CL**1
008100     05 DATE-IN-DD                    PIC XX.                        CL**1
008200*                                                                         
008300 01  CONTROL-CARD.                                                   CL**1
008400     05 CC-DAYS                       PIC 9(01).                     CL**1
008500*                                                                         
008600 01  HOLD-DAYS.                                                      CL**1
008700     05 DAYS-TO-SUNDAY                PIC S9(4) COMP.                CL**1
008800     05 FILLER                        PIC X(05) VALUE ' DAYS'.       CL**1
008900*                                                                         
009000 01  PROGRAM-SWITCHES.                                               CL**1
009100     05 VALID-PROCESS-DTE-IND         PIC X     VALUE 'N'.           CL**1
009200        88 VALID-PROCESS-DATE                   VALUE 'Y'.                
009300        88 INVALID-PROCESS-DATE                 VALUE 'N'.                
009400*                                                                         
009500 01  ABEND-AREAS.                                                 03230000
009600     05  PV-ABEND-PROGRAM             PIC X(8)  VALUE 'APKUP134'.    CL**1
009700     05  PV-ABEND-SQLCODE             PIC X(4)  VALUE SPACES.        CL**1
009800     05  PV-ABEND-PARAGRAPH           PIC X(50) VALUE SPACES.        CL**1
009900     05  PV-ABEND-OPERATION           PIC X(50) VALUE SPACES.        CL**1
010000     05  PV-ABEND-STRUCTURE-1         PIC X(8)  VALUE SPACES.        CL**1
010100     05  PV-ABEND-STRUCTURE-2         PIC X(8)  VALUE SPACES.        CL**1
010200     05  PV-ABEND-STATUS              PIC XX    VALUE SPACES.        CL**1
010300*                                                                    CL**1
010400                                                                          
010500     EJECT                                                                
010600*** GREGORIAN/JULIAN DATE CONVERSION                                      
010700     COPY DPWS500.                                                        
010800                                                                          
010900 01  FILLER                           PIC X(35)  VALUE               CL**1
011000     'END OF AP WORK RECORD AREA       **'.                          CL**1
011100                                                                     CL**1
011200     COPY DPWS004.                                                   CL**1
011300                                                                     CL**1
011400******************************************************************   CL**1
011500*  COMMUNICATIONS AREA2 FOR DB2                                      CL**1
011600******************************************************************   CL**1
011700     COPY SQLCA2.                                                    CL**1
011800                                                                     CL**1
011900                                                                     CL**1
012000     EXEC SQL                                                        CL**1
012100          INCLUDE SQLCA                                              CL**1
012200     END-EXEC.                                                       CL**1
012300                                                                     CL**1
012400                                                                     CL**1
012500     EXEC SQL                                                        CL**1
012600          INCLUDE TAPCNTL                                            CL**1
012700     END-EXEC.                                                       CL**1
012800                                                                     CL**1
012900 01  FILLER                        PIC X(25) VALUE                   CL**1
013000          '<====WS FOR APKUP134 ENDS'.                               CL**1
013100                                                                     CL**1
013200*LINKAGE SECTION.                                                    CL**1
013300                                                                     CL**1
013400                                                                     CL**1
013500 PROCEDURE DIVISION.                                                 CL**1
013600                                                                     CL**1
013700 A100-MAINLINE.                                                      CL**1
013800                                                                     CL**1
013900     PERFORM B100-INITIALIZATION.                                    CL**1
014000                                                                     CL**1
014100     IF VALID-PROCESS-DATE                                                
014200        PERFORM B200-PROCESSING                                           
014300     END-IF.                                                              
014400                                                                     CL**1
014500     PERFORM B300-END-OF-JOB.                                        CL**1
014600                                                                     CL**1
014700     GOBACK.                                                         CL**1
014800        EJECT                                                        CL**1
014900                                                                     CL**1
015000                                                                     CL**1
015100 B100-INITIALIZATION.                                                CL**1
015200*                                                                    CL**1
015300*****************************************************************    CL**1
015400*    DISPLAY INITIAL VALUES OF FIELDS                                CL**1
015500*****************************************************************    CL**1
015600*                                                                    CL**1
015700     MOVE 'B100-INITIALIZATION ' TO PV-ABEND-PARAGRAPH.                   
015800                                                                          
015900     ACCEPT CONTROL-CARD.                                                 
016000*    MOVE CC-DAYS TO APCNTL-PAYT-FLOAT-DAY-QTY.                           
016100     MOVE CC-DAYS TO DAYS-TO-SUNDAY.                                      
016200                                                                          
016300     INITIALIZE DCLTAPCNTL.                                          CL**1
016400                                                                          
016500     PERFORM S100-SELECT-TAPCNTL.                                         
016600                                                                     CL**1
016700     DISPLAY '**********************************************'             
016800     DISPLAY '**************  STARTING DATES  **************'             
016900     DISPLAY '**********************************************'             
017000     DISPLAY 'CALCULATED SUNDAY = ' PV-BTCH-DTE-PLUS-DAY                *1
017100     DISPLAY 'CURR-PSTG-FYR-NBR = ' APCNTL-CURR-PSTG-FYR-NBR            *1
017200     DISPLAY 'CURR-PSTG-FMN-NBR = ' APCNTL-CURR-PSTG-FMN-NBR            *1
017300     DISPLAY 'CURR-PSTG-FWK-NBR = ' APCNTL-CURR-PSTG-FWK-NBR            *1
017400                                                                     CL**1
017500     DISPLAY 'PREV-PSTG-FYR-NBR = ' APCNTL-PREV-PSTG-FYR-NBR            *1
017600     DISPLAY 'PREV-PSTG-FMN-NBR = ' APCNTL-PREV-PSTG-FMN-NBR            *1
017700     DISPLAY 'PREV-PSTG-FWK-NBR = ' APCNTL-PREV-PSTG-FWK-NBR            *1
017800     DISPLAY 'MULT-PSTG-PER-IND = ' APCNTL-MULT-PSTG-PER-IND            *1
017900                                                                          
018000     PERFORM D100-DATE-ROUTINE.                                           
018100                                                                          
018200 B200-PROCESSING.                                                    CL**1
018300*                                                                    CL**1
018400*****************************************************************    CL**1
018500*     MAIN PROCESSING OF PROGRAM                                     CL**1
018600*     -- ROUTE TAKEN BASED ON INITIAL VALUE OF FIELDS                   *1
018700*****************************************************************    CL**1
018800*                                                                    CL**1
018900     MOVE 'B200-PROCESSING   '   TO PV-ABEND-PARAGRAPH.                 06
019000                                                                        06
019100     PERFORM U100-UPDATE-CURR-PERIOD.                                     
019200                                                                          
019300     IF APCNTL-MULT-PSTG-PER-IND = 'Y' THEN                               
019400        CONTINUE                                                          
019500     ELSE                                                                 
019600        IF APCNTL-MULT-PSTG-PER-IND = 'N'                                 
019700           PERFORM U200-PREV-PERIOD-UPDATE                                
019800        END-IF                                                            
019900     END-IF.                                                              
020000                                                                          
020100     PERFORM S200-SELECT-NEW-CURR-WEEK.                                   
020200                                                                          
020300     IF APCNTL-CURR-PSTG-FWK-NBR = '01' THEN                              
020400           PERFORM U300-UPDATE-MULT-NBR                                   
020500     END-IF.                                                              
020600                                                                          
020700                                                                          
020800 B300-END-OF-JOB.                                                    CL**1
020900*                                                                    CL**1
021000*****************************************************************    CL**1
021100*     END JOB                                                           *1
021200*****************************************************************    CL**1
021300*                                                                    CL**1
021400     MOVE 'B300-END-OF-JOB   '   TO PV-ABEND-PARAGRAPH.                 06
021500                                                                          
021600     DISPLAY '************************************************'.     CL**1
021700     DISPLAY ' PROGRAM APKUP134 ENDS NORMALLY '.                          
021800                                                                     CL**1
021900     PERFORM S100-SELECT-TAPCNTL.                                         
022000                                                                          
022100     DISPLAY '**********************************************'             
022200     DISPLAY '***************  ENDING DATES  ***************'             
022300     DISPLAY '**********************************************'             
022400     DISPLAY 'CURR-PSTG-FYR-NBR = ' APCNTL-CURR-PSTG-FYR-NBR.        CL**1
022500     DISPLAY 'CURR-PSTG-FMN-NBR = ' APCNTL-CURR-PSTG-FMN-NBR.        CL**1
022600     DISPLAY 'CURR-PSTG-FWK-NBR = ' APCNTL-CURR-PSTG-FWK-NBR.        CL**1
022700                                                                     CL**1
022800     DISPLAY 'PREV-PSTG-FYR-NBR = ' APCNTL-PREV-PSTG-FYR-NBR.        CL**1
022900     DISPLAY 'PREV-PSTG-FMN-NBR = ' APCNTL-PREV-PSTG-FMN-NBR.        CL**1
023000     DISPLAY 'PREV-PSTG-FWK-NBR = ' APCNTL-PREV-PSTG-FWK-NBR.        CL**1
023100                                                                          
023200                                                                          
023300 D100-DATE-ROUTINE.                                                  CL**1
023400*                                                                    CL**1
023500*****************************************************************    CL**1
023600*     DETERMINE FISCAL DATES FROM DATE ROUTINE                       CL**1
023700*****************************************************************    CL**1
023800*                                                                    CL**1
023900     MOVE 'D100-DATE-ROUTINE '   TO PV-ABEND-PARAGRAPH.                   
024000                                                                          
024100     INITIALIZE                     DPG51 DPG52 DPG53                     
024200                                    DPG54 DPG55 DPG56.                    
024300                                                                          
024400     SET DPG51-FISCAL-454-CALENDAR   TO TRUE.                             
024500     SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                             
024600     SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
024700                                                                          
024800     MOVE PV-BTCH-DTE-PLUS-DAY   TO DPG52-LK-DATE-INPUT.                  
024900     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
025000                              USING  DPG51 DPG52 DPG53                    
025100                                     DPG54 DPG55 DPG56.                   
025200     IF (DPG54-SEVERE-ERROR OR                                            
025300         DPG54-WARNING-ERROR)                                             
025400        DISPLAY 'LK-DATE         ' DPG52-LK-DATE-INPUT                    
025500        DISPLAY 'CALENDAR-TYPE   ' DPG54-CALENDAR-TYPE-IND                
025600        DISPLAY 'DATE-VALID      ' DPG54-DATE-VALID-IND                   
025700        DISPLAY 'ERROR-IND       ' DPG54-ERROR-IND                        
025800        DISPLAY                    DPG54-ERROR-MESSAGE                    
025900                                                                          
026000        MOVE 'B100-PROCESS -'        TO PV-ABEND-PARAGRAPH                
026100        MOVE 'UNSUCCESSFUL DATE CONVERSION CONTROL  DATE  '               
026200            TO PV-ABEND-OPERATION                                         
026300        PERFORM Z997-ABEND                                                
026400     ELSE                                                                 
026500        MOVE DPG55-ALPHA-WEEK-DAY     TO PV-DAY-OF-WEEK                   
026600        MOVE DPG56-FISCAL-YEAR-CCYY   TO PV-FISCAL-YEAR                   
026700        MOVE DPG56-FISCAL-PERIOD-NBR  TO PV-FISCAL-MONTH                  
026800        MOVE DPG56-FISCAL-PERIOD-WEEK TO PV-FISCAL-WEEK                   
026900                                                                          
027000        DISPLAY 'WEEK DAY        ' DPG55-ALPHA-WEEK-DAY                   
027100        DISPLAY 'YEAR            ' DPG56-FISCAL-YEAR-CCYY                 
027200        DISPLAY 'MONTH           ' DPG56-FISCAL-PERIOD-NBR                
027300        DISPLAY 'WEEK            ' DPG56-FISCAL-PERIOD-WEEK               
027400     END-IF.                                                              
027500                                                                          
027600     IF DPG55-ALPHA-WEEK-DAY = PC-SUNDAY                                  
027700        SET VALID-PROCESS-DATE TO TRUE                                    
027800     ELSE                                                                 
027900        DISPLAY 'LK-DATE         ' DPG52-LK-DATE-INPUT                    
028000        DISPLAY 'CALENDAR-TYPE   ' DPG54-CALENDAR-TYPE-IND                
028100        DISPLAY 'DATE-VALID      ' DPG54-DATE-VALID-IND                   
028200        DISPLAY 'ERROR-IND       ' DPG54-ERROR-IND                        
028300        DISPLAY                    DPG54-ERROR-MESSAGE                    
028400                                                                          
028500        MOVE 'B100-PROCESS -'        TO PV-ABEND-PARAGRAPH                
028600        MOVE 'CALCULATED DATE IS NOT A SUNDAY DATE        '               
028700            TO PV-ABEND-OPERATION                                         
028800        PERFORM Z997-ABEND                                                
028900                                                                          
029000     END-IF.                                                              
029100                                                                          
029200                                                                          
029300 S100-SELECT-TAPCNTL.                                                CL**1
029400*                                                                    CL**1
029500*****************************************************************    CL**1
029600*     SELECT INFORMATION FROM TAPCNTL TABLE                          CL**1
029700*****************************************************************    CL**1
029800*                                                                    CL**1
029900     MOVE 'S100-SELECT-TAPCNTL'  TO PV-ABEND-PARAGRAPH.                 06
030000                                                                          
030100****     SELECT   DATE(DAYS(BTCH_PRC_DTE) +                               
030200***             :APCNTL-PAYT-FLOAT-DAY-QTY)                               
030300     EXEC SQL                                                             
030400         SELECT (BTCH_PRC_DTE + :DAYS-TO-SUNDAY DAYS)                     
030500                , CURR_PSTG_FYR_NBR                                       
030600                , CURR_PSTG_FMN_NBR                                       
030700                , CURR_PSTG_FWK_NBR                                       
030800                , PREV_PSTG_FYR_NBR                                       
030900                , PREV_PSTG_FMN_NBR                                       
031000                , PREV_PSTG_FWK_NBR                                       
031100                , MULT_PSTG_PER_IND                                       
031200         INTO    :PV-BTCH-DTE-PLUS-DAY                                    
031300                ,:APCNTL-CURR-PSTG-FYR-NBR                                
031400                ,:APCNTL-CURR-PSTG-FMN-NBR                                
031500                ,:APCNTL-CURR-PSTG-FWK-NBR                                
031600                ,:APCNTL-PREV-PSTG-FYR-NBR                                
031700                ,:APCNTL-PREV-PSTG-FMN-NBR                                
031800                ,:APCNTL-PREV-PSTG-FWK-NBR                                
031900                ,:APCNTL-MULT-PSTG-PER-IND                                
032000         FROM   TAPCNTL                                                   
032100     END-EXEC.                                                            
032200                                                                          
032300     EVALUATE TRUE                                                09760000
032400         WHEN SQLCODE = ZERO                                      09770000
032500              MOVE APCNTL-CURR-PSTG-FYR-NBR TO PV-PSTG-FYR-NBR    09780000
032600              MOVE APCNTL-CURR-PSTG-FMN-NBR TO PV-PSTG-FMN-NBR    09790000
032700              MOVE APCNTL-CURR-PSTG-FWK-NBR TO PV-PSTG-FWK-NBR            
032800         WHEN SQLWARN0 NOT = SPACES                               09800000
032900         WHEN SQLCODE  NOT = ZERO                                 09810000
033000         MOVE 'UNSUCCESSFUL SELECT OF PAYMENT CONTROL TBL'        09840000
033100                            TO PV-ABEND-OPERATION                 09850000
033200         MOVE 'TAPCNTL'     TO PV-ABEND-STRUCTURE-1               09860000
033300         MOVE SPACES        TO PV-ABEND-STRUCTURE-2               09870000
033400         DISPLAY 'BATCH-PLUS-DAY' PV-BTCH-DTE-PLUS-DAY            09890000
033500         PERFORM Z999-SQL-ERROR                                   09900000
033600     END-EVALUATE.                                                09910000
033700                                                                     CL**1
033800                                                                          
033900 S200-SELECT-NEW-CURR-WEEK.                                          CL**1
034000*                                                                    CL**1
034100*****************************************************************    CL**1
034200*     SELECT INFORMATION FROM TAPCNTL TABLE                          CL**1
034300*****************************************************************    CL**1
034400*                                                                    CL**1
034500     MOVE 'S200-SELECT-NEW-CURR-WEEK' TO PV-ABEND-PARAGRAPH.            06
034600                                                                          
034700     EXEC SQL                                                             
034800         SELECT   CURR_PSTG_FWK_NBR                                       
034900         INTO    :APCNTL-CURR-PSTG-FWK-NBR                                
035000         FROM   TAPCNTL                                                   
035100     END-EXEC.                                                            
035200                                                                          
035300     EVALUATE TRUE                                                09760000
035400         WHEN SQLCODE = ZERO                                      09770000
035500              CONTINUE                                            09780000
035600         WHEN SQLWARN0 NOT = SPACES                               09800000
035700         WHEN SQLCODE  NOT = ZERO                                 09810000
035800         MOVE 'UNSUCCESSFUL SELECT OF PAYMENT CONTROL TBL'        09840000
035900                            TO PV-ABEND-OPERATION                 09850000
036000         MOVE 'TAPCNTL'     TO PV-ABEND-STRUCTURE-1               09860000
036100         MOVE SPACES        TO PV-ABEND-STRUCTURE-2               09870000
036200         PERFORM Z999-SQL-ERROR                                   09900000
036300     END-EVALUATE.                                                09910000
036400                                                                     CL**1
036500                                                                          
036600 U100-UPDATE-CURR-PERIOD.                                            CL**1
036700*                                                                    CL**1
036800*****************************************************************    CL**1
036900*     UPDATE THE TAPCNTL TABLE (CURR_PSTG COLUMNS)                   CL**1
037000*****************************************************************    CL**1
037100*                                                                    CL**1
037200     MOVE 'U100-UPDATE-CURR-PERIOD' TO PV-ABEND-PARAGRAPH.              06
037300                                                                          
037400     EXEC SQL                                                             
037500         UPDATE TAPCNTL                                                   
037600         SET                                                              
037700             CURR_PSTG_FYR_NBR = :PV-FISCAL-YEAR                          
037800           , CURR_PSTG_FMN_NBR = :PV-FISCAL-MONTH                         
037900           , CURR_PSTG_FWK_NBR = :PV-FISCAL-WEEK                          
038000     END-EXEC.                                                            
038100                                                                          
038200     EVALUATE TRUE                                                        
038300         WHEN SQLCODE = ZERO                                              
038400              CONTINUE                                                    
038500                                                                          
038600         WHEN SQLWARN0 NOT = SPACES                                       
038700         WHEN SQLCODE  NOT = ZERO                                         
038800              MOVE 'U100-UPDATE-CURR-PERIOD'                              
038900                              TO PV-ABEND-PARAGRAPH                       
039000              MOVE 'UNSUCCESSFUL UPDATE APCNTL  ROW'                      
039100                              TO PV-ABEND-OPERATION                       
039200              MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1                     
039300              PERFORM Z999-SQL-ERROR                                      
039400     END-EVALUATE.                                                        
039500                                                                     CL**1
039600     EVALUATE TRUE                                                   CL**1
039700        WHEN SQLCODE = ZEROS                                         CL**1
039800             CONTINUE                                                CL**1
039900        WHEN OTHER                                                   CL**1
040000             MOVE 'U100-UPDATE-CURR-PERIOD       ' TO                CL**1
040100                                       PV-ABEND-PARAGRAPH            CL**1
040200             MOVE 'UNSUCCESSFUL UPDATE  APCNTL ROW'                  CL**1
040300                             TO PV-ABEND-OPERATION                   CL**1
040400             MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1                 CL**1
040500             PERFORM Z999-SQL-ERROR                                  CL**1
040600     END-EVALUATE.                                                   CL**1
040700                                                                     CL**1
040800                                                                          
040900 U200-PREV-PERIOD-UPDATE.                                            CL**1
041000*                                                                    CL**1
041100*****************************************************************    CL**1
041200*     UPDATE THE TAPCNTL TABLE (PREV_PSTG COLUMNS)                   CL**1
041300*****************************************************************    CL**1
041400*                                                                    CL**1
041500     MOVE 'U200-PREV-PERIOD-UPDATE' TO PV-ABEND-PARAGRAPH.              06
041600                                                                     CL**1
041700      EXEC SQL                                                            
041800          UPDATE TAPCNTL                                                  
041900          SET                                                             
042000              PREV_PSTG_FYR_NBR = :PV-PSTG-FYR-NBR                        
042100            , PREV_PSTG_FMN_NBR = :PV-PSTG-FMN-NBR                        
042200            , PREV_PSTG_FWK_NBR = :PV-PSTG-FWK-NBR                        
042300      END-EXEC                                                            
042400                                                                          
042500      EVALUATE TRUE                                                       
042600          WHEN SQLCODE = ZERO                                             
042700               CONTINUE                                                   
042800                                                                          
042900          WHEN SQLWARN0 NOT = SPACES                                      
043000          WHEN SQLCODE  NOT = ZERO                                        
043100               MOVE 'U200-PREV-PERIOD-UPDATE'                             
043200                               TO PV-ABEND-PARAGRAPH                      
043300               MOVE 'UNSUCCESSFUL UPDATE APCNTL  ROW'                     
043400                               TO PV-ABEND-OPERATION                      
043500               MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1                    
043600               PERFORM Z999-SQL-ERROR                                     
043700      END-EVALUATE                                                        
043800                                                                          
043900      EVALUATE TRUE                                                       
044000         WHEN SQLCODE = ZEROS                                             
044100              CONTINUE                                                    
044200         WHEN OTHER                                                       
044300              MOVE 'U200--PREV-PERIOD-UPDATE      ' TO                    
044400                                        PV-ABEND-PARAGRAPH                
044500              MOVE 'UNSUCCESSFUL UPDATE  APCNTL ROW'                      
044600                              TO PV-ABEND-OPERATION                       
044700              MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1                     
044800              PERFORM Z999-SQL-ERROR                                      
044900      END-EVALUATE.                                                       
045000                                                                          
045100                                                                          
045200 U300-UPDATE-MULT-NBR.                                               CL**1
045300*                                                                    CL**1
045400*****************************************************************    CL**1
045500*     DETERMINE WHETHER MULT_PSTG_PER_NBR NEEDS TO BE UPDATED        CL**1
045600*     BASED ON THE VALUE OF CURR_PSTG_FWK_NBR                        CL**1
045700*****************************************************************    CL**1
045800*                                                                    CL**1
045900     MOVE 'U300-UPDATE-MULT-NBR' TO PV-ABEND-PARAGRAPH.                   
046000                                                                          
046100           EXEC SQL                                                       
046200               UPDATE TAPCNTL                                             
046300               SET                                                        
046400                   MULT_PSTG_PER_IND = 'Y'                                
046500           END-EXEC                                                       
046600                                                                          
046700           EVALUATE TRUE                                                  
046800               WHEN SQLCODE = ZERO                                        
046900                    CONTINUE                                              
047000                                                                          
047100               WHEN SQLWARN0 NOT = SPACES                                 
047200               WHEN SQLCODE  NOT = ZERO                                   
047300                    MOVE 'U100-UPDATE-CURR-PERIOD'                        
047400                                    TO PV-ABEND-PARAGRAPH                 
047500                    MOVE 'UNSUCCESSFUL UPDATE APCNTL  ROW'                
047600                                    TO PV-ABEND-OPERATION                 
047700                    MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1               
047800                    PERFORM Z999-SQL-ERROR                                
047900           END-EVALUATE                                                   
048000                                                                          
048100           EVALUATE TRUE                                                  
048200              WHEN SQLCODE = ZEROS                                        
048300                   CONTINUE                                               
048400              WHEN OTHER                                                  
048500                   MOVE 'U100-UPDATE-CURR-PERIOD       ' TO               
048600                                             PV-ABEND-PARAGRAPH           
048700                   MOVE 'UNSUCCESSFUL UPDATE  APCNTL ROW'                 
048800                                   TO PV-ABEND-OPERATION                  
048900                   MOVE 'TAPCNTL ' TO PV-ABEND-STRUCTURE-1                
049000                   PERFORM Z999-SQL-ERROR                                 
049100           END-EVALUATE.                                                  
049200                                                                          
049300                                                                          
049400 Z997-ABEND.                                                              
049500*                                                                    CL**1
049600*****************************************************************    CL**1
049700*     PROCESS ABENDS                                                 CL**1
049800*****************************************************************    CL**1
049900*                                                                    CL**1
050000                                                                          
050100     DISPLAY PV-ABEND-PROGRAM.                                    APKUT106
050200     DISPLAY PV-ABEND-PARAGRAPH.                                  APKUT106
050300     DISPLAY PV-ABEND-OPERATION.                                        06
050400                                                                          
050500     CALL 'ILBOABN0' USING ABEND-CODE.                                    
050600                                                                          
050700                                                                          
050800 Z999-SQL-ERROR.                                                     CL**1
050900*                                                                    CL**1
051000*****************************************************************    CL**1
051100*     PROCESS DB2 ABENDS                                             CL**1
051200*****************************************************************    CL**1
051300*                                                                    CL**1
051400     DISPLAY '*** DB2 ERROR '.                                       CL**1
051500     DISPLAY '*** SQL CODE = '  PV-ABEND-SQLCODE.                    CL**1
051600     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                    CL**1
051700     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                  CL**1
051800     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                  CL**1
051900     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                CL**1
052000     IF PV-ABEND-STRUCTURE-2 > SPACES                                CL**1
052100         DISPLAY '*** TABLE 2   = ' PV-ABEND-STRUCTURE-2.            CL**1
052200                                                                     CL**1
052300     COPY DPPD004.                                                   CL**1
052400                                                                     CL**1
052500     MOVE +4013                    TO ABEND-CODE.                    CL**1
052600                                                                     CL**1
052700     CALL 'ILBOABN0' USING ABEND-CODE.                               CL**1
