      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT107.                                                
       AUTHOR.         BETTY DORST.                                             
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/31/05.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM GROUP PRICING DOMAIN EXTRACT REFORMAT PROGRAM                     
      *  THIS PROGRAM WILL CONVERT THE RMS COMMON EXTRACT COMP-3 AMTS  *        
      *  TO DISPLAY FORMAT.                                            *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT RMS-FILE                                                      
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RMS-DISPLAY-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  RMS-FILE                                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-CURRENT-RECORD                  PIC X(62).                       
                                                                                
       FD  RMS-DISPLAY-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  RMS-DISPLAY-REC                 PIC   X(48).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT107'.            
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-RMS                PIC  X(1)       VALUE SPACES.          
               88  EOF-RMS                               VALUE 'Y'.             
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR RMS GROUP PRICE EXTRACT FILE                      
      ******************************************************************        
           COPY IMRD039.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW GROUP PRICE EXTRACT FILE                      
      ******************************************************************        
           COPY CWRD107.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL EOF-RMS.                                                   
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  RMS-FILE.                                                
           OPEN OUTPUT RMS-DISPLAY-FILE.                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-RMS-INPUT-FILE.                                    
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 3000-REFORMAT-RMS-DATA.                                      
           PERFORM 4000-WRITE-REFORMATED-REC.                                   
           PERFORM 8000-READ-RMS-INPUT-FILE.                                    
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE CW107-GROUP-PRICE-EXTRACT-REC.                            
           MOVE IM039-GP-PRIC-ID      TO  CW107-GP-PRIC-ID.                     
           MOVE IM039-GP-PRIC-DESC    TO  CW107-GP-PRIC-DESC.                   
           MOVE IM039-GP-RTL-QTY      TO  CW107-GP-RTL-QTY.                     
           MOVE IM039-GP-RTL-AMT      TO  CW107-GP-RTL-AMT.                     
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       4000-WRITE-REFORMATED-REC.                                               
                                                                                
           WRITE  RMS-DISPLAY-REC FROM CW107-GROUP-PRICE-EXTRACT-REC.           
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
      ******************************************************************        
      *  READ THE RMS INPUT FILE.                                      *        
      ******************************************************************        
       8000-READ-RMS-INPUT-FILE.                                                
                                                                                
           READ RMS-FILE INTO IM039-GROUP-PRICE-EXTRACT-REC                     
               AT END                                                           
                   SET EOF-RMS TO TRUE                                          
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
                                                                                
           CLOSE RMS-FILE                                                       
                 RMS-DISPLAY-FILE.                                              
      ******************************************************************        
      ******************************************************************        
