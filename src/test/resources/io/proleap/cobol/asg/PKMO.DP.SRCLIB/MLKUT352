       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT352.                                                 
       AUTHOR.        VL YANG.                                                  
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/13/2007.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS IS A MODIFIED VERSION OF MLKUT212.                                 
      * MODIFICATIONS ARE: ADD LOOKUP FOR MONTHLY DEPT CMU% AND EIC             
      *                    ADD OF DBV CMU% RANGE AND OVERRIDE                   
      * THIS PROGRAM WILL FORMAT A COMMA DELIMITED FILE FOR DOWNLOADING         
      * THE RESULTS OF SMOOTHING FOR RECONCILEMENT.                             
      * THE INPUT IS IN MLKRD070 FORMAT                                         
      * PROCESSING WILL BE DIFFERENT BASED UPON OUTLYER INDICATOR.              
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
I28582* 28582   03-19-2007 ROD JANSSEN ADD TEST ID FOR SMOOTHING LEVEL          
257901* 257901  06-01-2021 SRINIVASAN  INCREASED THE LENGTH OF LABEL SEQ        
257901*                                NUMBER FROM 4 TO 5 TO AVOID THE          
257901*                                TRUNCATION OF LABEL SEQ NUMBER           
257901*                                WHICH IS GREATER THAN 4 DIGITS.          
257901*                                CHANGE NUMBER : CHG0257901               
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DETAIL-IN-FILE                                                
               ASSIGN TO INP01.                                                 
           SELECT OPTIONAL OVERRIDE-CC-FILE                                     
               ASSIGN TO INP02.                                                 
           SELECT RECONCILE-OUT-FILE                                            
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  DETAIL-IN-FILE                                                       
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DETAIL-IN-REC                   PIC X(400).                          
                                                                                
       FD  OVERRIDE-CC-FILE                                                     
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OVERRIDE-CC-REC                 PIC X(080).                          
                                                                                
       FD  RECONCILE-OUT-FILE                                                   
           RECORDING  MODE IS F                                                 
           BLOCK CONTAINS 0 RECORDS.                                            
I28582 01  RECONCILE-OUT-REC               PIC X(0900).                         
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                      PIC S9(4)       VALUE ZEROES         
                               COMP SYNC.                                       
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
                                                                                
           05  PA-CONTROL-CNT              PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-IN-CNT            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-OUT-CNT           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RECORD-BYPASSED-CNT      PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DKEY-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-BRND-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-VNDR-NOT-FOUND           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DBV-CMU-CNT              PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DPT-CMU-CNT              PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-CRP-CMU-CNT              PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-CMU-NOT-FOUND            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DPT-RANGE-CNT            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-CRP-RANGE-CNT            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-RANGE-NOT-FOUND          PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DPT-LEDGER-CNT           PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-DPT-NOT-FOUND            PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PA-WORK-CNT                 PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05 PC-PROGRAM-NAME              PIC X(08)   VALUE 'MLKUT352'.        
           05 PC-UNKNOWN                   PIC X(08)   VALUE '????????'.        
           05 PC-4090                      PIC S9(08)      VALUE +4090          
                               COMP SYNC.                                       
           05 PC-4095                      PIC S9(08)      VALUE +4095          
                               COMP SYNC.                                       
           05 PC-3                         PIC S9(05)      VALUE +3             
                               COMP-3.                                          
                                                                                
       01  PT-DETAIL-OUT-HEADER.                                                
           05  PT-MAIN-SECTION-HDR.                                             
               10  FILLER                  PIC X(12)       VALUE                
                   'FISCAL DATE,'.                                              
               10  FILLER                  PIC X(05)       VALUE                
                   'DEPT,'.                                                     
               10  FILLER                  PIC X(06)       VALUE                
                   'BRAND,'.                                                    
               10  FILLER                  PIC X(11)       VALUE                
                   'BRAND NAME,'.                                               
               10  FILLER                  PIC X(07)       VALUE                
                   'VENDOR,'.                                                   
               10  FILLER                  PIC X(12)       VALUE                
                   'VENDOR NAME,'.                                              
               10  FILLER                  PIC X(05)       VALUE                
                   'DKEY,'.                                                     
               10  FILLER                  PIC X(11)       VALUE                
                   'LOOKUP KEY,'.                                               
               10  FILLER                  PIC X(14)       VALUE                
                   'SMOOTHING IND,'.                                            
               10  FILLER                  PIC X(19)       VALUE                
                   'CURR RCPT NET COST,'.                                       
               10  FILLER                  PIC X(14)       VALUE                
                   'CURR RCPT RTL,'.                                            
               10  FILLER                  PIC X(19)       VALUE                
                   'CURR PADJ NET COST,'.                                       
               10  FILLER                  PIC X(14)       VALUE                
                   'CURR PADJ RTL,'.                                            
               10  FILLER                  PIC X(13)       VALUE                
                   'CURR SLS RTL,'.                                             
               10  FILLER                  PIC X(14)       VALUE                
                   'CURR SHNK RTL,'.                                            
               10  FILLER                  PIC X(14)       VALUE                
                   'CURR MKND RTL,'.                                            
               10  FILLER                  PIC X(17)       VALUE                
                   'CURR INV ADJ RTL,'.                                         
               10  FILLER                  PIC X(17)       VALUE                
                   'CURR TRANSFER IN,'.                                         
               10  FILLER                  PIC X(18)       VALUE                
                   'CURR TRANSFER OUT,'.                                        
               10  FILLER                  PIC X(24)       VALUE                
                   'BOP COST (AFTER SMOOTH),'.                                  
               10  FILLER                  PIC X(08)       VALUE                
                   'BOP RTL,'.                                                  
               10  FILLER                  PIC X(09)       VALUE                
                   'CURR EIR,'.                                                 
               10  FILLER                  PIC X(15)       VALUE                
                   'PRE-SMOOTH EIC,'.                                           
               10  FILLER                  PIC X(14)       VALUE                
                   'PRE-SMOOTH GM,'.                                            
               10  FILLER                  PIC X(11)       VALUE                
                   'COST ADJ 1,'.                                               
               10  FILLER                  PIC X(17)       VALUE                
                   'INTERMEDIATE EIC,'.                                         
               10  FILLER                  PIC X(11)       VALUE                
                   'COST ADJ 2,'.                                               
               10  FILLER                  PIC X(13)       VALUE                
                   'ADJUSTED EIC,'.                                             
               10  FILLER                  PIC X(18)       VALUE                
                   'ADJUSTED SLS COST,'.                                        
               10  FILLER                  PIC X(18)       VALUE                
                   'ADJUSTED GRO MRGN,'.                                        
               10  FILLER                  PIC X(09)       VALUE                
                   'SECT BRK,'.                                                 
      *    05  PT-SEAS-SECTION-HDR.                                             
      *        10  FILLER                  PIC X(17)       VALUE                
      *            'BEG SEAS INV RTL,'.                                         
      *        10  FILLER                  PIC X(18)       VALUE                
      *            'BEG SEAS INV COST,'.                                        
      *        10  FILLER                  PIC X(15)       VALUE                
      *            'TOTAL SHNK RTL,'.                                           
      *        10  FILLER                  PIC X(19)       VALUE                
      *            'TOTAL SHNK SLS RTL,'.                                       
      *        10  FILLER                  PIC X(16)       VALUE                
      *            'SEAS COST ADJ 1,'.                                          
      *        10  FILLER                  PIC X(16)       VALUE                
      *            'SEAS COST ADJ 2,'.                                          
      *        10  FILLER                  PIC X(19)       VALUE                
      *            'CUM TO DTE PUR RTL,'.                                       
      *        10  FILLER                  PIC X(20)       VALUE                
      *            'CUM TO DTE PUR COST,'.                                      
      *        10  FILLER                  PIC X(09)       VALUE                
      *            'SECT BRK,'.                                                 
           05  PT-CALC-SECTION-HDR.                                             
               10  FILLER                  PIC X(35)       VALUE                
                   'PRE-SMOOTH RECALC (TRUNCATED) CMU%,'.                       
      *        10  FILLER                  PIC X(28)       VALUE                
      *            'RECALC (NON-TRUNCATED) CMU%,'.                              
               10  FILLER                  PIC X(22)       VALUE                
                   'SMOOTHED (TRUNC) CMU%,'.                                    
      *        10  FILLER                  PIC X(30)       VALUE                
      *            'SMOOTHED (NON-TRUNCATED) CMU%,'.                            
               10  FILLER                  PIC X(09)       VALUE                
                   'SECT BRK,'.                                                 
           05  PT-DBV-SECTION-HDR.                                              
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT MIN CMU%,'.                                            
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT MAX CMU%,'.                                            
               10  FILLER                  PIC X(13)       VALUE                
                   'DBV OVERRIDE,'.                                             
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT OVERRIDE,'.                                            
               10  FILLER                  PIC X(09)       VALUE                
                   'SECT BRK,'.                                                 
           05  PT-DEPT-SECTION-HDR.                                             
               10  FILLER                  PIC X(19)       VALUE                
                   'DEPT RCPT NET COST,'.                                       
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT RCPT RTL,'.                                            
               10  FILLER                  PIC X(19)       VALUE                
                   'DEPT PADJ NET COST,'.                                       
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT PADJ RTL,'.                                            
               10  FILLER                  PIC X(13)       VALUE                
                   'DEPT SLS RTL,'.                                             
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT SHNK RTL,'.                                            
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT MKND RTL,'.                                            
               10  FILLER                  PIC X(17)       VALUE                
                   'DEPT INV ADJ RTL,'.                                         
               10  FILLER                  PIC X(17)       VALUE                
                   'DEPT TRANSFER IN,'.                                         
               10  FILLER                  PIC X(18)       VALUE                
                   'DEPT TRANSFER OUT,'.                                        
               10  FILLER                  PIC X(10)       VALUE                
                   'DEPT CMU%,'.                                                
               10  FILLER                  PIC X(09)       VALUE                
                   'DEPT EIC,'.                                                 
               10  FILLER                  PIC X(09)       VALUE                
                   'DEPT EIR,'.                                                 
               10  FILLER                  PIC X(14)       VALUE                
                   'DEPT SLS COST,'.                                            
               10  FILLER                  PIC X(08)       VALUE                
                   'DEPT GM,'.                                                  
               10  FILLER                  PIC X(09)       VALUE                
                   'SECT BRK,'.                                                 
           05  FILLER                      PIC X(88)       VALUE SPACES.        
           05  PT-SMOOTHING-RULE-HDR.                                           
               10  FILLER                  PIC X(05)       VALUE                
                   'SMRL,'.                                                     
I28582     05  PT-SMOOTHING-ADJ-IND-HDR.                                        
I28582         10 FILLER                   PIC X(05)   VALUE 'ADJ1,'.   01340000
I28582         10 FILLER                   PIC X(05)   VALUE 'ADJ2,'.   01340000
I28582         10 FILLER                   PIC X(05)   VALUE 'ADJ3,'.   01340000
I28582         10 FILLER                   PIC X(05)   VALUE 'ADJ4,'.   01340000
I28582     05  FILLER                      PIC X(30)   VALUE SPACES.            
                                                                                
       01  PT-DETAIL-OUT-REC.                                                   
           05  PT-MAIN-SECTION-DTL.                                             
               10  PT-FSCL-END-DTE         PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DEPT-NBR             PIC 9(04)       VALUE ZEROS.         
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-LBL-SEQ-NBR          PIC 9(09)       VALUE ZEROS.         
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-BRAND-NAME           PIC X(20)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-ENT-ID               PIC 9(09)       VALUE ZEROS.         
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-VENDOR-NAME          PIC X(30)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-ML-LWST-ID           PIC 9(09)       VALUE ZEROS.         
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-LKEY.                                                     
                   15  PT-LKEY-DATE        PIC X(10).                           
                   15  FILLER              PIC X(01)       VALUE '-'.           
                   15  PT-LKEY-DEPT        PIC 9(04).                           
                   15  FILLER              PIC X(01)       VALUE '-'.           
                   15  PT-LKEY-DKEY        PIC 9(09).                           
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-OUTLYER-CANDI-DESC   PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-RCPT-NET-COST   PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-RCPT-RTL        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-PADJ-NET-COST   PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-PADJ-RTL        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-SLS-RTL         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-SHNK-RTL        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-MKDN-RTL        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-INV-ADJ-RTL     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-XFER-IN-RTL     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-XFER-OUT-RTL    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-PREV-END-INV-COST    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-PREV-END-INV-RTL     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-END-INV-RTL     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-END-INV-COST    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-GRO-MRGN        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-INV-COST-ADJ1        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-END-INV-COST-AMT1    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-INV-COST-ADJ2        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-END-INV-COST-AMT2    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-CURR-SLS-COST        PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-ADJ-GRO-MRGN         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  FILLER                  PIC X(01)       VALUE ','.           
      *    05  PT-SEAS-SECTION-DTL.                                             
      *        10  PT-BEG-SEAS-INV-RTL     PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-BEG-SEAS-INV-COST    PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-TOTAL-SHNK-RTL       PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-TOTAL-SHNK-SLS-RTL   PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-SEAS-INV-COST-ADJ1   PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-SEAS-INV-COST-ADJ2   PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-CUM-TO-DATE-PURCH-RTL                                     
      *                                    PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  PT-CUM-TO-DATE-PURCH-COST                                    
      *                                    PIC -9(11).99   VALUE ZEROES.        
      *        10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  FILLER                  PIC X(01)       VALUE ','.           
           05  PT-CALC-SECTION-DTL.                                             
               10  PT-CURR-CUM-MKUP-PCT    PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-TGT-CUM-MKUP-PCT     PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
      *        10  FILLER                  PIC X(01)       VALUE ','.           
               10  FILLER                  PIC X(01)       VALUE ','.           
           05  PT-DBV-SECTION-DTL.                                              
               10  PT-DPT-MIN-CUM-MKUP-PCT                                      
                                           PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-MAX-CUM-MKUP-PCT                                      
                                           PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DBV-OVRID-CUM-MKUP-PCT                                    
                                           PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-OVRID-CUM-MKUP-PCT                                    
                                           PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  FILLER                  PIC X(01)       VALUE ','.           
           05  PT-DEPT-SECTION-DTL.                                             
               10  PT-DPT-RCPT-NET-COST    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-RCPT-RTL         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-PADJ-NET-COST    PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-PADJ-RTL         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-SLS-RTL          PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-SHNK-RTL         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-MKDN-RTL         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-INV-ADJ-RTL      PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-XFER-IN-RTL      PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-XFER-OUT-RTL     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-CUM-MKUP-PCT     PIC -9(04).9(07).                    
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-END-INV-COST     PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-END-INV-RTL      PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-SLS-COST         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  PT-DPT-GRO-MRGN         PIC -9(11).99   VALUE ZEROES.        
               10  FILLER                  PIC X(01)       VALUE ','.           
               10  FILLER                  PIC X(01)       VALUE ','.           
           05  FILLER                      PIC X(48)       VALUE SPACES.        
           05  PT-SMOOTHING-RULE-DTL.                                           
               10  PT-SMOOTHING-RULE       PIC 9(04)       VALUE ZEROES.        
                   88  PT-UNKNOWN-RULE                     VALUE 91.            
                   88  PT-NEGATIVE-EIC-RULE                VALUE 92.            
                   88  PT-ZERO-GM-RULE                     VALUE 93.            
                   88  PT-GM-RANGE-RULE                    VALUE 94.            
                   88  PT-INACTIVE-DEPT-RULE               VALUE 95.            
                   88  PT-PLACE-HOLDER                     VALUE 999.           
                   88  PT-OTHER-RULE                       VALUE 99.            
               10  FILLER                  PIC X(01)       VALUE ','.           
I28582     05  PT-SMOOTHING-ADJ-IND-DTL.                                        
I28582         10  PT-ADJ-IND1             PIC X(01)   VALUE SPACE.     01340000
I28582         10  FILLER                  PIC X(01)   VALUE ','.               
I28582         10  PT-ADJ-IND2             PIC X(01)   VALUE SPACE.     01340000
I28582         10  FILLER                  PIC X(01)   VALUE ','.               
I28582         10  PT-ADJ-IND3             PIC X(01)   VALUE SPACE.     01340000
I28582         10  FILLER                  PIC X(01)   VALUE ','.               
I28582         10  PT-ADJ-IND4             PIC X(01)   VALUE SPACE.     01340000
I28582         10  FILLER                  PIC X(01)   VALUE ','.               
I28582     05  FILLER                      PIC X(42)   VALUE SPACES.            
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-DETAIL-IN-EOF-SW         PIC X(01)       VALUE 'N'.           
               88  PS-DETAIL-IN-NOT-EOF                    VALUE 'N'.           
               88  PS-DETAIL-IN-EOF                        VALUE 'Y'.           
           05  PS-OVERRIDE-EOF-SW         PIC X(01)        VALUE 'N'.           
               88  PS-OVERRIDE-NOT-EOF                     VALUE 'N'.           
               88  PS-OVERRIDE-EOF                         VALUE 'Y'.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DEPT-NBR-9               PIC 9(04)       VALUE ZEROES.        
           05  PV-DEPT-NBR-X REDEFINES PV-DEPT-NBR-9                            
                                           PIC X(04).                           
           05  FILLER REDEFINES PV-DEPT-NBR-9.                                  
               10  FILLER                  PIC X(01).                           
               10  PV-DEPT-NBR-3X          PIC X(03).                           
           05  PV-ML-LWST-ID-9             PIC 9(09)       VALUE ZEROES.        
           05  PV-LBL-SEQ-NBR              PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
257901     05  PV-LBL-SEQ-NBR-9            PIC 9(05)       VALUE ZEROES.        
           05  PV-ENT-ID                   PIC S9(09)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ENT-ID-9                 PIC 9(09)       VALUE ZEROES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-DB2-OPER-ATTEMPTED       PIC X(30)       VALUE SPACES.        
           05  PV-WORK-AMT                 PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DBV-OVRID-CUM-MKUP-PCT   PIC S9(4)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
           05  PV-DPT-OVRID-CUM-MKUP-PCT   PIC S9(4)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
                                                                                
      ******************************************************************        
      *  OPTIONAL CONTROL CARD                                                  
      ******************************************************************        
       01  PV-CC-OPTIONAL-CONTROL.                                              
           05  FILLER                      PIC X(01)       VALUE SPACES.        
               88  PV-CC-COMMENT                           VALUE '*'.           
               88  PV-CC-NOT-COMMENT                       VALUE SPACES.        
           05  PV-CC-OVERRIDE-KEY-DATE     PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES PV-CC-OVERRIDE-KEY-DATE.                        
               10  PV-CC-OVERRIDE-YYYY     PIC X(04).                           
               10  FILLER                  PIC X(01).                           
                   88  PV-CC-OVERRIDE-DASH-1               VALUE '-'.           
               10  PV-CC-OVERRIDE-MM       PIC X(02).                           
               10  FILLER                  PIC X(01).                           
                   88  PV-CC-OVERRIDE-DASH-2               VALUE '-'.           
               10  PV-CC-OVERRIDE-DD       PIC X(02).                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  PV-CC-PROCESS-OPTION        PIC X(10)       VALUE SPACES.        
               88  PV-CC-NO-DEPT-SECTION                   VALUE                
                   'NO DEPT'.                                                   
           05  FILLER                      PIC X(58)       VALUE SPACES.        
                                                                                
      ******************************************************************        
      *  MLRD070 DETAIL IN RECORD                                               
      ******************************************************************        
      *01  ML070-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070.                                                        
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *************************************************************             
      *    DB2 TVSCHVL (GET BRAND NAME)                           *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 XMT_VND (GET VENDOR NAME)                          *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE XMT00000                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 TMNDPT  (GET MONTHLY DEPT INFORMATION)             *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TMNDPT                                                   
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 TDTATTR (GET DATE INFORMATION FROM DATE ATTRIBUTE) *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_CUM_MKUP_OVRID (GET CMU% OVERRIDE)             *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00012                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_CUM_MKUP_TGT   (GET CMU% TARGET RANGE)         *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00013                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 MLT_CORP_GLBL_DFLT (GET CMU% CORP TARGET RANGE)    *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE MLT00014                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 - MLV_BRND_VND.                                                     
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE MLV00000                                                 
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 COMMUNICATION AREA                                 *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 1100-PROCESS-INPUT                                           
               UNTIL PS-DETAIL-IN-EOF.                                          
                                                                                
           PERFORM 9000-END-PROCESSING.                                         
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *  PERFORM THE NECESSARY INITIALIZATION STEPS                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN  INPUT DETAIL-IN-FILE                                           
                       OVERRIDE-CC-FILE                                         
                OUTPUT RECONCILE-OUT-FILE.                                      
                                                                                
           PERFORM 8000-READ-INPUT.                                             
                                                                                
           PERFORM 8100-READ-CONTROL.                                           
           PERFORM                                                              
               UNTIL PS-OVERRIDE-EOF                                            
                   OR PV-CC-NOT-COMMENT                                         
               DISPLAY ' ' PV-CC-OPTIONAL-CONTROL                               
               ADD +1 TO PA-CONTROL-CNT                                         
               PERFORM 8100-READ-CONTROL                                        
           END-PERFORM.                                                         
           IF PV-CC-OPTIONAL-CONTROL = SPACES                                   
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' ' PV-CC-OPTIONAL-CONTROL                               
               IF PV-CC-OVERRIDE-YYYY IS NUMERIC                                
                   AND PV-CC-OVERRIDE-MM IS NUMERIC                             
                   AND PV-CC-OVERRIDE-DD IS NUMERIC                             
                   AND PV-CC-OVERRIDE-DASH-1                                    
                   AND PV-CC-OVERRIDE-DASH-2                                    
                   DISPLAY ' * USING ' PV-CC-OVERRIDE-KEY-DATE                  
                           ' FOR BUILDING LOOKUP KEY *'                         
               ELSE                                                             
                   MOVE SPACES TO PV-CC-OVERRIDE-KEY-DATE                       
               END-IF                                                           
               IF PV-CC-NO-DEPT-SECTION                                         
                   MOVE SPACES        TO PT-DEPT-SECTION-HDR                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ZEROES                TO MLT00012-DEPT-NBR                      
                                         MLT00012-LBL-SEQ-NBR                   
                                         MLT00012-ENT-ID                        
                                         MLT00013-DEPT-NBR.                     
                                                                                
      ******************************************************************        
      *  PROCESS THE INPUT FILES.                                               
      ******************************************************************        
       1100-PROCESS-INPUT.                                                      
                                                                                
           ADD +1 TO PA-RECORD-IN-CNT.                                          
                                                                                
           INITIALIZE PT-DETAIL-OUT-REC.                                        
                                                                                
      * IS THE RECORD A PLACE HOLDER?                                           
           IF ML070-FSCL-END-DTE = SPACES                                       
               SET PT-PLACE-HOLDER                 TO TRUE                      
               PERFORM 2000-PROCESS-COMMON                                      
           ELSE                                                                 
               PERFORM 2000-PROCESS-COMMON                                      
               PERFORM 3000-PROCESS-EXTRA                                       
               IF PT-LBL-SEQ-NBR = 2230                                         
                   AND PT-ENT-ID = 5161                                         
                   SET PT-UNKNOWN-RULE             TO TRUE                      
               ELSE                                                             
                   IF ML070-CURR-END-INV-COST-AMT <= ZEROES                     
                       SET PT-NEGATIVE-EIC-RULE    TO TRUE                      
                   ELSE                                                         
                       IF ML070-CURR-GRO-MRGN-AMT = ZEROES                      
                           SET PT-ZERO-GM-RULE     TO TRUE                      
                       ELSE                                                     
                           IF ML070-CURR-GRO-MRGN-AMT >= -15000                 
                               AND ML070-CURR-GRO-MRGN-AMT <= 15000             
                               SET PT-GM-RANGE-RULE    TO TRUE                  
                           ELSE                                                 
                               IF MLT00013-MIN-CUM-MKUP-PCT = -999              
                                   AND MLT00013-MAX-CUM-MKUP-PCT = 999          
                                   SET PT-INACTIVE-DEPT-RULE TO TRUE            
                               ELSE                                             
                                   SET PT-OTHER-RULE         TO TRUE            
                               END-IF                                           
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8500-WRITE-RECONCILE.                                        
                                                                                
           PERFORM 8000-READ-INPUT.                                             
                                                                                
      ******************************************************************        
      *  2000-PROCESS-COMMON (COMMON FORMATTING)                                
      ******************************************************************        
       2000-PROCESS-COMMON.                                                     
                                                                                
           MOVE ML070-OUTLYER-CANDI-IND   TO  PT-OUTLYER-CANDI-DESC.            
           MOVE ML070-FSCL-END-DTE        TO  PT-FSCL-END-DTE.                  
           IF PV-CC-OVERRIDE-KEY-DATE = SPACES                                  
               MOVE ML070-FSCL-END-DTE    TO  PT-LKEY-DATE                      
           ELSE                                                                 
               MOVE PV-CC-OVERRIDE-KEY-DATE      TO  PT-LKEY-DATE               
           END-IF.                                                              
           MOVE ML070-DEPT-NBR            TO  PT-DEPT-NBR                       
                                              PT-LKEY-DEPT                      
                                              PV-DEPT-NBR-9.                    
                                                                                
           PERFORM 2100-GET-BV-VIEW.                                            
                                                                                
           PERFORM 2200-GET-BRAND-NAME.                                         
           PERFORM 2300-GET-VENDOR-NAME.                                        
                                                                                
           MOVE PV-LBL-SEQ-NBR            TO PT-LBL-SEQ-NBR.                    
           MOVE PV-ENT-ID                 TO PT-ENT-ID.                         
           MOVE ML070-ML-LWST-ID          TO PT-ML-LWST-ID                      
                                             PT-LKEY-DKEY.                      
           MOVE ML070-CURR-RCPT-NET-COST-AMT TO PT-CURR-RCPT-NET-COST.          
           MOVE ML070-CURR-RCPT-RTL-AMT   TO PT-CURR-RCPT-RTL.                  
           MOVE ML070-CURR-PADJ-NET-COST-AMT TO PT-CURR-PADJ-NET-COST.          
           COMPUTE PV-WORK-AMT =                                                
               ML070-CURR-PADJ-RTL-AMT + ML070-CURR-MKUP-RTL-AMT.               
           MOVE PV-WORK-AMT               TO PT-CURR-PADJ-RTL.                  
           MOVE ML070-CURR-SLS-RTL-AMT    TO PT-CURR-SLS-RTL.                   
           MOVE ML070-CURR-SHNK-RTL-AMT   TO PT-CURR-SHNK-RTL.                  
           MOVE ML070-CURR-MKDN-RTL-AMT   TO PT-CURR-MKDN-RTL.                  
           MOVE ML070-CURR-INV-ADJ-RTL-AMT  TO PT-CURR-INV-ADJ-RTL.             
           MOVE ML070-CURR-XFER-IN-RTL-AMT  TO PT-CURR-XFER-IN-RTL.             
           MOVE ML070-CURR-XFER-OUT-RTL-AMT TO PT-CURR-XFER-OUT-RTL.            
           MOVE ML070-PREV-END-INV-COST-AMT TO PT-PREV-END-INV-COST             
           MOVE ML070-PREV-END-INV-RTL-AMT  TO PT-PREV-END-INV-RTL              
           MOVE ML070-CURR-END-INV-RTL-AMT  TO PT-CURR-END-INV-RTL              
           MOVE ML070-CURR-END-INV-COST-AMT TO PT-CURR-END-INV-COST.            
           MOVE ML070-CURR-GRO-MRGN-AMT   TO PT-CURR-GRO-MRGN.                  
           MOVE ML070-INV-COST-ADJ1       TO PT-INV-COST-ADJ1.                  
           MOVE ML070-END-INV-COST-AMT1   TO PT-END-INV-COST-AMT1.              
           MOVE ML070-INV-COST-ADJ2       TO PT-INV-COST-ADJ2.                  
           MOVE ML070-END-INV-COST-AMT2   TO PT-END-INV-COST-AMT2.              
           MOVE ML070-CURR-SLS-COST-AMT   TO PT-CURR-SLS-COST.                  
           MOVE ML070-ADJ-GRO-MRGN-AMT    TO PT-ADJ-GRO-MRGN.                   
                                                                                
      *    MOVE ML070-BEG-SEAS-INV-RTL-AMT  TO PT-BEG-SEAS-INV-RTL.             
      *    MOVE ML070-BEG-SEAS-INV-COST-AMT TO PT-BEG-SEAS-INV-COST.            
      *    MOVE ML070-SHNK-RTL-AMT        TO PT-TOTAL-SHNK-RTL.                 
      *    MOVE ML070-SHNK-SLS-RTL-AMT    TO PT-TOTAL-SHNK-SLS-RTL.             
      *    MOVE ML070-SEAS-INV-COST-ADJ1  TO PT-SEAS-INV-COST-ADJ1.             
      *    MOVE ML070-SEAS-INV-COST-ADJ2  TO PT-SEAS-INV-COST-ADJ2.             
      *    MOVE ML070-CUM-TO-DATE-PURCH-RTL                                     
      *        TO PT-CUM-TO-DATE-PURCH-RTL.                                     
      *    MOVE ML070-CUM-TO-DATE-PURCH-COST                                    
      *        TO PT-CUM-TO-DATE-PURCH-COST.                                    
                                                                                
           MOVE ML070-CURR-CUM-MKUP-PCT   TO PT-CURR-CUM-MKUP-PCT.              
           MOVE ML070-TGT-CUM-MKUP-PCT    TO PT-TGT-CUM-MKUP-PCT.               
                                                                                
I28582     MOVE ML070-LVL1-SMOOTH-IND     TO PT-ADJ-IND1.               08160014
I28582     MOVE ML070-LVL2-SMOOTH-IND     TO PT-ADJ-IND2.               08160014
I28582     MOVE ML070-LVL3-SMOOTH-IND     TO PT-ADJ-IND3.               08160014
I28582     MOVE ML070-LVL4-SMOOTH-IND     TO PT-ADJ-IND4.               08160014
I28582                                                                          
      ******************************************************************        
      *  2100-GET-BV-VIEW FROM MLV_BRND_VND                                     
      ******************************************************************        
       2100-GET-BV-VIEW.                                                        
                                                                                
           EXEC SQL                                                             
              SELECT   LBL_SEQ_NBR                                              
                      ,ENT_ID                                                   
                INTO  :MLV00000-LBL-SEQ-NBR,                                    
                      :MLV00000-ENT-ID                                          
                FROM   MLV_BRND_VND                                             
               WHERE   ML_LO_MDSE_LVL_ID = :ML070-ML-LWST-ID                    
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               MOVE MLV00000-LBL-SEQ-NBR TO PV-LBL-SEQ-NBR                      
               MOVE MLV00000-ENT-ID      TO PV-ENT-ID                           
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1                  TO PA-DKEY-NOT-FOUND                 
                   MOVE ML070-ML-LWST-ID   TO PV-ML-LWST-ID-9                   
                   DISPLAY 'UNKNOWN BV VIEW FOR ' PV-ML-LWST-ID-9               
                   MOVE PC-UNKNOWN         TO PT-BRAND-NAME                     
                   MOVE PC-UNKNOWN         TO PT-VENDOR-NAME                    
                   MOVE ZEROS              TO PV-LBL-SEQ-NBR                    
                   MOVE ZEROS              TO PV-ENT-ID                         
                   MOVE PC-4090            TO RETURN-CODE                       
           ELSE                                                                 
               MOVE 'SELECT MLV_BRND_VND ' TO PV-DB2-OPER-ATTEMPTED             
               MOVE '2100-GET-BV-VIEW   ' TO PV-PARAGRAPH-NAME                  
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  2200-GET-BRAND-NAME FROM TVSCHVL                                       
      ******************************************************************        
       2200-GET-BRAND-NAME.                                                     
                                                                                
           EXEC SQL                                                             
              SELECT   CHARTC_VAL_DESC                                          
                INTO  :VSCHVL-CHARTC-VAL-DESC                                   
                FROM   TVSCHVL                                                  
               WHERE   CHARTC_CDE = :PC-3                                       
                 AND   CHARTC_SEQ_NBR = :PV-LBL-SEQ-NBR                         
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               INSPECT VSCHVL-CHARTC-VAL-DESC                                   
                                REPLACING ALL ',' BY ' '                        
               MOVE VSCHVL-CHARTC-VAL-DESC TO PT-BRAND-NAME                     
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1                  TO PA-BRND-NOT-FOUND                 
                   MOVE PC-UNKNOWN         TO PT-BRAND-NAME                     
                   MOVE PV-LBL-SEQ-NBR     TO PV-LBL-SEQ-NBR-9                  
                   DISPLAY 'UNKNOWN BRAND FOR ' PV-LBL-SEQ-NBR-9                
                   MOVE PC-4090            TO RETURN-CODE                       
           ELSE                                                                 
               MOVE 'SELECT TVSCHVL     ' TO PV-DB2-OPER-ATTEMPTED              
               MOVE '2200-GET-BRAND-NAME' TO PV-PARAGRAPH-NAME                  
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  2300-GET-VENDOR-NAME FROM XMT_VND                                      
      ******************************************************************        
       2300-GET-VENDOR-NAME.                                                    
                                                                                
           EXEC SQL                                                             
              SELECT   PRIM_VND_NM                                              
                INTO  :XMT00000-PRIM-VND-NM                                     
                FROM   XMT_VND                                                  
               WHERE   ENT_ID = :PV-ENT-ID                                      
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               INSPECT XMT00000-PRIM-VND-NM                                     
                                      REPLACING ALL ',' BY ' '                  
               MOVE XMT00000-PRIM-VND-NM   TO PT-VENDOR-NAME                    
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1                  TO PA-VNDR-NOT-FOUND                 
                   MOVE PC-UNKNOWN         TO PT-VENDOR-NAME                    
                   MOVE PV-ENT-ID          TO PV-ENT-ID-9                       
                   DISPLAY 'UNKNOWN VENDOR FOR ' PV-ENT-ID-9                    
                   MOVE PC-4090            TO RETURN-CODE                       
           ELSE                                                                 
               MOVE 'SELECT XMT_VND     ' TO PV-DB2-OPER-ATTEMPTED              
               MOVE '2300-GET-VENDOR-NAME ' TO PV-PARAGRAPH-NAME                
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  2400-GET-CMU-OVERRIDE FROM MLT_CUM_MKUP_OVRID                          
      ******************************************************************        
       2400-GET-CMU-OVERRIDE.                                                   
                                                                                
           EXEC SQL                                                             
              SELECT   OVRID_CUM_MKUP_PCT                                       
                INTO  :MLT00012-OVRID-CUM-MKUP-PCT                              
                FROM   MLT_CUM_MKUP_OVRID                                       
               WHERE   DEPT_NBR    = :MLT00012-DEPT-NBR                         
                 AND   LBL_SEQ_NBR = :MLT00012-LBL-SEQ-NBR                      
                 AND   ENT_ID      = :MLT00012-ENT-ID                           
W28582           AND  :MLT00012-EFF-BEG-DTE BETWEEN                             
W28582                   EFF_BEG_DTE AND                                        
W28582                   VALUE(EFF_END_DTE, '9999-12-31')                       
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               CONTINUE                                                         
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   MOVE ZEROES         TO MLT00012-OVRID-CUM-MKUP-PCT           
           ELSE                                                                 
               MOVE 'SELECT MLT_CUM_MKUP_OVRID' TO PV-DB2-OPER-ATTEMPTED        
               MOVE '2400-GET-CMU-OVERRIDE' TO PV-PARAGRAPH-NAME                
W28582         DISPLAY ' DEPT:    ' MLT00012-DEPT-NBR                           
W28582                 ' LBL:     ' MLT00012-LBL-SEQ-NBR                        
W28582                 ' ENT-ID:  ' MLT00012-ENT-ID                             
W28582                 ' EFF-DTE: ' MLT00012-EFF-BEG-DTE                        
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  2500-GET-TGT-RANGE FROM MLT_CUM_MKUP_TGT                               
      ******************************************************************        
       2500-GET-TGT-RANGE.                                                      
                                                                                
           MOVE ML070-DEPT-NBR                TO MLT00013-DEPT-NBR.             
W28582     MOVE ML070-FSCL-END-DTE            TO MLT00013-EFF-BEG-DTE.          
           EXEC SQL                                                             
              SELECT   MIN_CUM_MKUP_PCT                                         
                      ,MAX_CUM_MKUP_PCT                                         
                INTO  :MLT00013-MIN-CUM-MKUP-PCT                                
                     ,:MLT00013-MAX-CUM-MKUP-PCT                                
                FROM   MLT_CUM_MKUP_TGT                                         
               WHERE   DEPT_NBR    = :MLT00013-DEPT-NBR                         
W28582           AND  :MLT00013-EFF-BEG-DTE BETWEEN                             
                         EFF_BEG_DTE AND                                        
                         VALUE(EFF_END_DTE, '9999-12-31')                       
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               ADD +1                         TO PA-DPT-RANGE-CNT               
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   DISPLAY ' ** CANNOT FIND TARGET/RANGE FOR'                   
W28582                     ' DEPT:    ' MLT00013-DEPT-NBR                       
W28582                     ' EFF-DTE: ' MLT00013-EFF-BEG-DTE                    
      *                    ', WILL TRY CORP **'                                 
      *            EXEC SQL                                                     
      *               SELECT   MIN_CUM_MKUP_PCT                                 
      *                       ,MAX_CUM_MKUP_PCT                                 
      *                 INTO  :MLT00013-MIN-CUM-MKUP-PCT                        
      *                      ,:MLT00013-MAX-CUM-MKUP-PCT                        
      *                 FROM   MLT_CORP_GLBL_DFLT                               
      *                WHERE                                                    
      *                        CURRENT DATE BETWEEN                             
      *                          EFF_BEG_DTE AND                                
      *                          VALUE(EFF_END_DTE, '9999-12-31')               
      *               WITH UR                                                   
      *            END-EXEC                                                     
      *            IF SQLCODE = +0                                              
      *                ADD +1                 TO PA-CRP-RANGE-CNT               
      *            ELSE                                                         
      *                IF SQLCODE = +100                                        
                           ADD +1          TO PA-RANGE-NOT-FOUND                
      *                    DISPLAY ' ** CANNOT FIND CORP TGT/RANGE **'          
                           MOVE ZEROES     TO MLT00013-MIN-CUM-MKUP-PCT         
                                              MLT00013-MAX-CUM-MKUP-PCT         
           ELSE                                                                 
               MOVE 'SELECT MLT_CUM_MKUP_TGT'   TO PV-DB2-OPER-ATTEMPTED        
               MOVE '2500-GET-TGT-RANGE   ' TO PV-PARAGRAPH-NAME                
W28582         DISPLAY ' DEPT:    ' MLT00013-DEPT-NBR                           
W28582                 ' EFF-DTE: ' MLT00013-EFF-BEG-DTE                        
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  2600-GET-TMNDPT-INFO FROM TMNDPT                                       
      ******************************************************************        
       2600-GET-TMNDPT-INFO.                                                    
                                                                                
           MOVE PV-DEPT-NBR-3X                TO MNDPT-DEPT-NBR.                
           MOVE ML070-FSCL-END-DTE            TO DTATTR-KY-DTE.                 
           EXEC SQL                                                             
              SELECT                                                            
                       RCPT_NET_COST_AMT                                        
                      ,RCPT_RTL_AMT                                             
                      ,PADJ_RTL_AMT                                             
                      ,MKUP_RTL_AMT                                             
                      ,PADJ_NET_COST_AMT                                        
                      ,SLS_RTL_AMT                                              
                      ,SHNK_RTL_AMT                                             
                      ,MKDN_RTL_AMT                                             
                      ,INV_ADJ_RTL_AMT                                          
                      ,XFER_IN_RTL_AMT                                          
                      ,XFER_OUT_RTL_AMT                                         
                      ,END_INV_COST_AMT                                         
                      ,END_INV_RTL_AMT                                          
                      ,SLS_COST_AMT                                             
                      ,GRO_MRGN_AMT                                             
                      ,CUM_MKUP_PCT                                             
                INTO                                                            
                      :MNDPT-RCPT-NET-COST-AMT                                  
                     ,:MNDPT-RCPT-RTL-AMT                                       
                     ,:MNDPT-PADJ-RTL-AMT                                       
                     ,:MNDPT-MKUP-RTL-AMT                                       
                     ,:MNDPT-PADJ-NET-COST-AMT                                  
                     ,:MNDPT-SLS-RTL-AMT                                        
                     ,:MNDPT-SHNK-RTL-AMT                                       
                     ,:MNDPT-MKDN-RTL-AMT                                       
                     ,:MNDPT-INV-ADJ-RTL-AMT                                    
                     ,:MNDPT-XFER-IN-RTL-AMT                                    
                     ,:MNDPT-XFER-OUT-RTL-AMT                                   
                     ,:MNDPT-END-INV-COST-AMT                                   
                     ,:MNDPT-END-INV-RTL-AMT                                    
                     ,:MNDPT-SLS-COST-AMT                                       
                     ,:MNDPT-GRO-MRGN-AMT                                       
                     ,:MNDPT-CUM-MKUP-PCT                                       
                FROM                                                            
                       TMNDPT   A                                               
                      ,TDTATTR  B                                               
               WHERE   DEPT_NBR    = :MNDPT-DEPT-NBR                            
                 AND   KY_DTE = :DTATTR-KY-DTE                                  
                 AND   B.FSCL_MN_NBR     = A.FSCL_MN_NBR                        
                 AND   B.FSCL_MN_END_DTE = A.FSCL_MN_END_DTE                    
              WITH UR                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
               ADD +1                         TO PA-DPT-LEDGER-CNT              
           ELSE                                                                 
               IF SQLCODE = +100                                                
                   ADD +1          TO PA-DPT-NOT-FOUND                          
                   DISPLAY ' ** MONTHLY DEPT INFO NOT FOUND FOR'                
                           ' DEPT:   ' MNDPT-DEPT-NBR                           
                           ' DATE: ' DTATTR-KY-DTE                              
                           ' **'                                                
                   MOVE ZEROES     TO MNDPT-RCPT-NET-COST-AMT                   
                                      MNDPT-RCPT-RTL-AMT                        
                                      MNDPT-PADJ-RTL-AMT                        
                                      MNDPT-MKUP-RTL-AMT                        
                                      MNDPT-PADJ-NET-COST-AMT                   
                                      MNDPT-SLS-RTL-AMT                         
                                      MNDPT-SHNK-RTL-AMT                        
                                      MNDPT-MKDN-RTL-AMT                        
                                      MNDPT-INV-ADJ-RTL-AMT                     
                                      MNDPT-XFER-IN-RTL-AMT                     
                                      MNDPT-XFER-OUT-RTL-AMT                    
                                      MNDPT-END-INV-COST-AMT                    
                                      MNDPT-END-INV-RTL-AMT                     
                                      MNDPT-SLS-COST-AMT                        
                                      MNDPT-GRO-MRGN-AMT                        
                                      MNDPT-CUM-MKUP-PCT                        
           ELSE                                                                 
               MOVE 'SELECT TMNDPT      '   TO PV-DB2-OPER-ATTEMPTED            
               MOVE '2600-GET-TMNDPT-INFO ' TO PV-PARAGRAPH-NAME                
               DISPLAY ' DEPT:   ' MNDPT-DEPT-NBR                               
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 3000-PROCESS-EXTRA.                                                     
      ******************************************************************        
       3000-PROCESS-EXTRA.                                                      
                                                                                
           IF ML070-DEPT-NBR = MLT00012-DEPT-NBR                                
               AND PV-LBL-SEQ-NBR = MLT00012-LBL-SEQ-NBR                        
               AND PV-ENT-ID = MLT00012-ENT-ID                                  
               CONTINUE                                                         
           ELSE                                                                 
               MOVE ML070-DEPT-NBR        TO MLT00012-DEPT-NBR                  
               MOVE PV-LBL-SEQ-NBR        TO MLT00012-LBL-SEQ-NBR               
               MOVE PV-ENT-ID             TO MLT00012-ENT-ID                    
W28582         MOVE ML070-FSCL-END-DTE    TO MLT00012-EFF-BEG-DTE               
               PERFORM 2400-GET-CMU-OVERRIDE                                    
               MOVE MLT00012-OVRID-CUM-MKUP-PCT                                 
                   TO PV-DBV-OVRID-CUM-MKUP-PCT                                 
               IF SQLCODE = +0                                                  
                   ADD +1                 TO PA-DBV-CMU-CNT                     
               END-IF                                                           
      *        ELSE                                                             
      *            DISPLAY ' ** CANNOT FIND OVERRIDE FOR'                       
      *                    ' DEPT: '   MLT00012-DEPT-NBR                        
      *                    ' BRAND: '  MLT00012-LBL-SEQ-NBR                     
      *                    ' VENDOR: ' MLT00012-ENT-ID                          
      *                    ', WILL TRY DEPT LEVEL **'                           
                   MOVE ZEROES            TO MLT00012-LBL-SEQ-NBR               
                                             MLT00012-ENT-ID                    
                   PERFORM 2400-GET-CMU-OVERRIDE                                
                   MOVE MLT00012-OVRID-CUM-MKUP-PCT                             
                       TO PV-DPT-OVRID-CUM-MKUP-PCT                             
                   IF SQLCODE = +0                                              
                       ADD +1             TO PA-DPT-CMU-CNT                     
                   ELSE                                                         
      *                DISPLAY ' ** CANNOT FIND OVERRIDE FOR'                   
      *                        ' DEPT: '   MLT00012-DEPT-NBR                    
      *                        ', WILL TRY CORP LEVEL **'                       
      *                MOVE ZEROES        TO MLT00012-DEPT-NBR                  
      *                PERFORM 2400-GET-CMU-OVERRIDE                            
      *                IF SQLCODE = +0                                          
      *                    ADD +1         TO PA-CRP-CMU-CNT                     
      *                ELSE                                                     
                           ADD +1         TO PA-CMU-NOT-FOUND                   
                           MOVE ZEROES    TO MLT00012-OVRID-CUM-MKUP-PCT        
      *                END-IF                                                   
                   END-IF                                                       
      *        END-IF                                                           
           END-IF.                                                              
           MOVE PV-DBV-OVRID-CUM-MKUP-PCT                                       
               TO PT-DBV-OVRID-CUM-MKUP-PCT.                                    
           MOVE PV-DPT-OVRID-CUM-MKUP-PCT                                       
               TO PT-DPT-OVRID-CUM-MKUP-PCT.                                    
                                                                                
           IF ML070-DEPT-NBR = MLT00013-DEPT-NBR                                
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2500-GET-TGT-RANGE                                       
           END-IF.                                                              
           MOVE MLT00013-MIN-CUM-MKUP-PCT                                       
               TO PT-DPT-MIN-CUM-MKUP-PCT.                                      
           MOVE MLT00013-MAX-CUM-MKUP-PCT                                       
               TO PT-DPT-MAX-CUM-MKUP-PCT.                                      
                                                                                
           IF PV-DEPT-NBR-3X = MNDPT-DEPT-NBR                                   
               AND ML070-FSCL-END-DTE = DTATTR-KY-DTE                           
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2600-GET-TMNDPT-INFO                                     
           END-IF.                                                              
                                                                                
           MOVE MNDPT-RCPT-NET-COST-AMT   TO PT-DPT-RCPT-NET-COST.              
           MOVE MNDPT-RCPT-RTL-AMT        TO PT-DPT-RCPT-RTL.                   
           MOVE MNDPT-PADJ-NET-COST-AMT   TO PT-DPT-PADJ-NET-COST.              
           COMPUTE PV-WORK-AMT = MNDPT-PADJ-RTL-AMT                             
                               + MNDPT-MKUP-RTL-AMT.                            
           MOVE PV-WORK-AMT               TO PT-DPT-PADJ-RTL.                   
           MOVE MNDPT-SLS-RTL-AMT         TO PT-DPT-SLS-RTL.                    
           MOVE MNDPT-SHNK-RTL-AMT        TO PT-DPT-SHNK-RTL.                   
           MOVE MNDPT-MKDN-RTL-AMT        TO PT-DPT-MKDN-RTL.                   
           MOVE MNDPT-INV-ADJ-RTL-AMT     TO PT-DPT-INV-ADJ-RTL.                
           MOVE MNDPT-XFER-IN-RTL-AMT     TO PT-DPT-XFER-IN-RTL.                
           MOVE MNDPT-XFER-OUT-RTL-AMT    TO PT-DPT-XFER-OUT-RTL.               
           MOVE MNDPT-CUM-MKUP-PCT        TO PT-DPT-CUM-MKUP-PCT.               
           MOVE MNDPT-END-INV-COST-AMT    TO PT-DPT-END-INV-COST.               
           MOVE MNDPT-END-INV-RTL-AMT     TO PT-DPT-END-INV-RTL                 
           MOVE MNDPT-SLS-COST-AMT        TO PT-DPT-SLS-COST.                   
           MOVE MNDPT-GRO-MRGN-AMT        TO PT-DPT-GRO-MRGN.                   
                                                                                
                                                                                
      ******************************************************************        
      * 8000-READ-INPUT.                                                        
      ******************************************************************        
       8000-READ-INPUT.                                                         
                                                                                
           READ DETAIL-IN-FILE INTO ML070-COMMON-RECALC-EXTRACT                 
               AT END                                                           
                   SET PS-DETAIL-IN-EOF TO TRUE.                                
                                                                                
                                                                                
      ******************************************************************        
      * 8100-READ-CONTROL.                                                      
      ******************************************************************        
       8100-READ-CONTROL.                                                       
                                                                                
           READ OVERRIDE-CC-FILE INTO PV-CC-OPTIONAL-CONTROL                    
               AT END                                                           
                   SET PS-OVERRIDE-EOF TO TRUE.                                 
                                                                                
      ******************************************************************        
      * 8500-WRITE-RECONCILE.                                                   
      ******************************************************************        
       8500-WRITE-RECONCILE.                                                    
                                                                                
           IF PA-RECORD-OUT-CNT = 0                                             
               WRITE RECONCILE-OUT-REC  FROM PT-DETAIL-OUT-HEADER               
               ADD +1 TO PA-RECORD-OUT-CNT                                      
           END-IF.                                                              
                                                                                
           IF PV-CC-NO-DEPT-SECTION                                             
               MOVE SPACES        TO PT-DEPT-SECTION-DTL                        
           END-IF.                                                              
           WRITE RECONCILE-OUT-REC FROM PT-DETAIL-OUT-REC.                      
                                                                                
           ADD +1 TO PA-RECORD-OUT-CNT.                                         
                                                                                
      ******************************************************************        
      *  PERFORM THE NECESSARY END PROCESSING STEPS                             
      ******************************************************************        
                                                                                
       9000-END-PROCESSING.                                                     
                                                                                
           CLOSE DETAIL-IN-FILE                                                 
                 RECONCILE-OUT-FILE.                                            
                                                                                
           DISPLAY '*************************************************'          
           DISPLAY ' RECORD IN:      ' PA-RECORD-IN-CNT.                        
           DISPLAY ' RECORD IGNORED: ' PA-RECORD-BYPASSED-CNT.                  
           DISPLAY ' RECORD OUT:     ' PA-RECORD-OUT-CNT.                       
           COMPUTE PA-WORK-CNT = PA-RECORD-OUT-CNT                              
                               + PA-RECORD-BYPASSED-CNT - 1.                    
           IF PA-WORK-CNT = PA-RECORD-IN-CNT                                    
               DISPLAY '  IN BALANCE'                                           
           ELSE                                                                 
               DISPLAY '  OUT OF BALANCE'                                       
               MOVE PC-4095 TO RETURN-CODE                                      
           END-IF.                                                              
           DISPLAY ' '.                                                         
           DISPLAY ' DKEY NOT FOUND:         ' PA-DKEY-NOT-FOUND.               
           DISPLAY ' BRAND NAME NOT FOUND:   ' PA-BRND-NOT-FOUND.               
           DISPLAY ' VENDOR NAME NOT FOUND:  ' PA-VNDR-NOT-FOUND.               
           DISPLAY ' '.                                                         
           DISPLAY ' DEPT RANGE/TARGET:      ' PA-DPT-RANGE-CNT.                
           DISPLAY ' CORP RANGE/TARGET:      ' PA-CRP-RANGE-CNT                 
           DISPLAY ' RANGE/TARGET NOT FOUND: ' PA-RANGE-NOT-FOUND.              
           DISPLAY ' '.                                                         
           DISPLAY ' DEPT/BRND/VND OVERRIDE: ' PA-DBV-CMU-CNT.                  
           DISPLAY ' DEPT LEVEL OVERRIDE:    ' PA-DPT-CMU-CNT.                  
           DISPLAY ' CORP LEVEL OVERRIDE:    ' PA-CRP-CMU-CNT.                  
           DISPLAY ' OVERRIDE NOT FOUND:     ' PA-CMU-NOT-FOUND.                
           DISPLAY ' '.                                                         
           DISPLAY ' DEPT LEDGER LOOKUP:     ' PA-DPT-LEDGER-CNT.               
           DISPLAY ' DEPT LEDGER NOT FOUND:  ' PA-DPT-NOT-FOUND.                
           DISPLAY '*************************************************'.         
                                                                                
                                                                                
      ***************************************************************           
      *    SQL ABEND PROCESSING                                     *           
      ***************************************************************           
       9950-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   = ' PC-PROGRAM-NAME.                           
           DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                         
           DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
                                                                                
       9999-OTHER-ABEND.                                                        
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '************************************************'.          
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
