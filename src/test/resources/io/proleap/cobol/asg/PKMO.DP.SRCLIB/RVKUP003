000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.   RVKUP003.                                                  
000400 AUTHOR.       VIRGENE LENNEMAN.                                          
000500 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000600 DATE-WRITTEN. MAY 2008.                                                  
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*  MARK OUT OF STOCK FROM RTV UPDATES TO IN STORE PERM TABLES             
001100******************************************************************        
001200*                                                                         
001500*-----------------------------------------------------------------        
001600* THIS PROGRAM HAS CHECKPOINT RESTART.                                    
002900*                                                                         
002900* THIS PROGRAM RUNS UNDER THE DB2 CHECKPOINT RESTART SYSTEM.              
002900* PRIOR TO THIS PROGRAM RUNNING THE MOS TRANSACTION FILE IS               
002900* PROCESSED BY A TRANSACTION PREPARATION PROGRAM (RVKUT033)               
002900* WHICH READS THE RECORDS, APPENDS ADDITIONAL INFORMATION ONTO            
002900* EACH TRANSACTION BASED ON HOW THE CHECKPOINTS ARE TO BE TAKEN           
002900* BY THIS PROGRAM AND PASSES THE MODIFIED RECORD TO THE                   
002900* TO THE TRANSACTION PREPARATION MODULE (DPKUT900).  A UNIQUE             
002900* SEQUENCE NUMBER IS THEN ASSIGNED TO EACH TRANSACTION AND THE            
002900* RECORDS ARE WRITTEN OUT TO A SEQUENTIAL FILE. AFTER GOING               
002900* THROUGH THE PREPARATION PROCESS THE FILE IS READY TO BE                 
002900* PROCESSED BY THIS PROGRAM WHILE RUNNING UNDER THE DB2 CHECKPOINT        
002900* RESTART SYSTEM.                                                         
002900*                                                                         
003000* A UNIT OF WORK IS THE COMPLETE LOADING OF A HEADER RECORD AND           
003100* ALL DETAIL RECORDS FOR A SPECIFIC DOCUMENT NUMBER TO                    
003100* TSTRPPC (HEADER) & TSPPCDT (DETAIL).                                    
003200*                                                                         
006400******************************************************************        
007200*                                                                         
007300 ENVIRONMENT DIVISION.                                                    
007400*                                                                         
007500 CONFIGURATION SECTION.                                                   
007600 SOURCE-COMPUTER.        IBM-3090.                                        
007700 OBJECT-COMPUTER.        IBM-3090.                                        
007800 INPUT-OUTPUT SECTION.                                                    
007900 FILE-CONTROL.                                                            
006500     SELECT  RV-HDR-DTL-FILE      ASSIGN TO INP01.                        
006600                                                                          
008100 DATA DIVISION.                                                           
008200 FILE SECTION.                                                            
007800 FD  RV-HDR-DTL-FILE                                                      
007900     RECORDING MODE IS F                                                  
008000     LABEL RECORDS ARE STANDARD                                           
008100     BLOCK CONTAINS 0 RECORDS.                                            
008200 01  RV-HDRDTL-RECORD                PIC  X(260).                         
008300*                                                                         
008400 WORKING-STORAGE SECTION.                                                 
008500                                                                          
015300 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
015400                                                   COMP SYNC.             
015500 01  PA-PROGRAM-ACCUMULATORS.                                             
015600     05  PA-TOTALS.                                                       
015700         10 PA-TOT-RECS-READ          PIC S9(09) VALUE +0 COMP-3.         
015800         10 PA-TOT-PPC-INS            PIC S9(09) VALUE +0 COMP-3.         
015900         10 PA-TOT-PPCDTL-INS         PIC S9(09) VALUE +0 COMP-3.         
016000         10 PA-TOT-UNITS-OF-WORK      PIC S9(09) VALUE +0 COMP-3.         
016200         10 PA-LAST-ORD-ID-COMMITTED  PIC S9(09) VALUE +0 COMP-3.         
016300         10 PA-LAST-DOC-NBR-COMMITTED PIC S9(09) VALUE +0 COMP-3.         
016400         10 PA-TOT-COMMITS-TAKEN      PIC S9(09) VALUE +0 COMP-3.         
016500                                                                          
016600 01  PS-PROGRAM-SWITCHES.                                                 
017000     05  PS-END-OF-FILE-SW             PIC  X(01)    VALUE 'N'.           
017100         88  PS-END-OF-FILE-YES                      VALUE 'Y'.           
017200         88  PS-END-OF-FILE-NO                       VALUE 'N'.           
017600                                                                          
017700 01  PC-PROGRAM-CONSTANTS.                                                
017900     05  PC-CKPT-JOB-NAME            PIC X(08)  VALUE 'RVK020  '.         
017900     05  PC-CKPT-PLAN-NAME           PIC X(08)  VALUE 'RVKUP003'.         
003700     05  PC-TRAN-FUNC-CODES.                                              
003800         10  PC-TRAN-FUNC-OPEN       PIC X(05)  VALUE 'OPEN '.            
004000         10  PC-TRAN-FUNC-TRANS      PIC X(05)  VALUE 'TRANS'.            
004200         10  PC-TRAN-FUNC-CLOSE      PIC X(05)  VALUE 'CLOSE'.            
004000         10  PC-TRAN-FUNC-RSTRT      PIC X(05)  VALUE 'RSTRT'.            
017900     05  PC-CURRENT-PROGRAM-NAME     PIC X(08)  VALUE 'RVKUP003'.         
018800                                                                          
020300 01  PV-PROGRAM-VARIABLES.                                                
020400     05  PV-CKPT-STAT-CODE             PIC X(01)  VALUE SPACE.            
024600     05  PV-DISPLAY-NO-COMMA           PIC ZZZZZZZ9-.                     
024700     05  PV-PARAGRAPH-NAME             PIC  X(30) VALUE SPACE.            
024800     05  PV-ABEND-MESSAGE              PIC  X(30) VALUE SPACE.            
024900     05  PV-SQLCODE                    PIC  S9(9) VALUE ZEROES.           
025000     05  PV-DB2-TABLE                  PIC X(10) VALUE SPACES.            
025100                                                                          
025200 01  PV-ABEND-VARIABLES.                                                  
025300     05  PV-ABEND-CODE              PIC S9(04) VALUE +0 COMP SYNC.        
026400                                                                          
015500******************************************************************        
015600*  TRANSACTION PREPARATION MODULE COMMUNICATIONS AREA                     
015700******************************************************************        
260107     COPY DPWS991.                                                        
015900                                                                          
008500*  TRANSACTION PRESENTATION MODULE PARAMENTERS                            
008500     COPY DPLS001.                                                        
008500     05  WORK-STRG-AREA              PIC X(200).                          
008500                                                                          
015500*  HEADER-DETAIL RECORD                                                   
015600     COPY RVRD056.                                                        
015700                                                                          
014900*  DB2 FORMATTED MESSAGE AREA                                             
015000     COPY DPWS004.                                                        
015200                                                                          
011900* PE-PROPOSED IN-STORE PRICE CHANGE                                       
012000     EXEC SQL                                                             
012100          INCLUDE TSTRPPC                                                 
012200     END-EXEC.                                                            
012300                                                                          
012400*  PE-STORE PRICE CHANGE CURRENT DETAIL                                   
012500     EXEC SQL                                                             
012600          INCLUDE TSPPCDT                                                 
012700     END-EXEC.                                                            
012800                                                                          
008600*  COMMUNICATIONS AREA FOR DB2                                            
008700     EXEC SQL                                                             
008800          INCLUDE SQLCA                                                   
008900     END-EXEC.                                                            
008900     COPY SQLCA2.                                                         
009000                                                                          
037200                                                                          
037300 LINKAGE SECTION.                                                         
037400                                                                          
037500 PROCEDURE DIVISION.                                                      
040600******************************************************************        
040600**   MAIN PROGRAM PROCESING                                               
040600******************************************************************        
040700 0000-MAINLINE.                                                           
037800     PERFORM 1000-INITIALIZE.                                             
037900                                                                          
038000     PERFORM 2000-PROCESS                                                 
038100        UNTIL PS-END-OF-FILE-YES.                                         
039100                                                                          
039300     PERFORM 9000-EOJ-ROUTINE.                                            
039400                                                                          
039500     DISPLAY '************  END OF RVKUP003  ***************'             
039600                                                                          
039700     GOBACK.                                                              
039800                                                                          
039900******************************************************************        
040000* INITIALIZE VARIABLES.                                                   
040100* OPEN INPUT FILES.                                                       
040600******************************************************************        
040700 1000-INITIALIZE.                                                         
040900     DISPLAY '************ START OF RVKUP003 ***************'             
041000                                                                          
041100     INITIALIZE   PA-PROGRAM-ACCUMULATORS                                 
041300                  PV-PROGRAM-VARIABLES                                    
041600                  DCLTSTRPPC                                              
041700                  DCLTSPPCDT.                                             
042300                                                                          
042300     MOVE PC-TRAN-FUNC-OPEN        TO DP001-FUNC-CODE.                    
042300     PERFORM 8000-READ-MOS-FILE.                                          
042300     MOVE PC-TRAN-FUNC-TRANS       TO DP001-FUNC-CODE.                    
042300                                                                          
042300     IF PS-END-OF-FILE-YES                                                
042300         DISPLAY '* * * * * * * * * * * * * '                             
042300         DISPLAY '   INPUT FILE IS EMPTY'                                 
042300         DISPLAY '* * * * * * * * * * * * * '                             
042300     END-IF.                                                              
042300                                                                          
051200 2000-PROCESS.                                                            
051000*********   DETERMINE WHAT RECORD TYPE                                    
051000********    MOVE DATA TO DCLGEN                                           
051000********    PERFORM INSERT OF APPROPRIATE TABLE                           
051000     IF RV056-HEADER                                                      
051000         PERFORM 3000-LOAD-DCLTSTRPPC                                     
051000         PERFORM 3500-INSERT-TSTRPPC                                      
051000     ELSE                                                                 
051000*************  DETAIL RECORD                                              
129200         PERFORM 4000-LOAD-DCLTSPPCDT                                     
129200         PERFORM 4500-INSERT-TSPPCDT                                      
051000     END-IF.                                                              
051000                                                                          
051000     PERFORM 8000-READ-MOS-FILE.                                          
051000                                                                          
121100******************************************************************        
121200* 3000- SETUP DCLGEN FOR TSTRPPC                                          
121300******************************************************************        
121400 3000-LOAD-DCLTSTRPPC.                                                    
109400     INITIALIZE DCLTSTRPPC.                                               
121600     MOVE RV056-DEST-LOC-NBR       TO STRPPC-DEST-LOC-NBR.                
121700     MOVE RV056-DOC-NBR            TO STRPPC-DOC-NBR.                     
121800     MOVE RV056-DOC-TYP-CDE        TO STRPPC-DOC-TYP-CDE.                 
121900     MOVE RV056-EFF-DTE            TO STRPPC-EFF-DTE.                     
122100     MOVE RV056-PR-CHG-CPL-NAME    TO STRPPC-PR-CHG-CPL-NAME.             
122200     MOVE RV056-RSN-CDE            TO STRPPC-RSN-CDE.                     
122400     MOVE RV056-TYP-CDE            TO STRPPC-TYP-CDE.                     
122600     MOVE RV056-STATUS-CDE         TO STRPPC-STATUS-CDE.                  
122600     MOVE RV056-PREV-STATUS-CDE    TO STRPPC-PREV-STATUS-CDE.             
122700     MOVE RV056-DEPT-AREA-DEST     TO STRPPC-DEPT-AREA-DEST.              
123300     MOVE RV056-ORGN-LOC-NBR       TO STRPPC-ORGN-LOC-NBR.                
123400** USE DB2 CURRENT DATE            TO STRPPC-CRE-DTE.                     
123500     MOVE PC-CURRENT-PROGRAM-NAME  TO STRPPC-CRE-ID.                      
123600     MOVE RV056-CAN-DTE            TO STRPPC-CAN-DTE.                     
123700     MOVE RV056-CAN-ID             TO STRPPC-CAN-ID.                      
123800** USE DB2 CURRENT DATE CCYY-MM-DD TO STRPPC-ENTR-DTE                     
123900** USE DB2 CURRENT TIME HH.MM.SS   TO STRPPC-ENTR-TIME                    
124000     MOVE PC-CURRENT-PROGRAM-NAME  TO STRPPC-ENTR-ID.                     
124100     MOVE RV056-MGMT-APPRVL-DTE    TO STRPPC-MGMT-APPRVL-DTE.             
124200     MOVE RV056-MGMT-APPRVL-ID     TO STRPPC-MGMT-APPRVL-ID.              
124300     MOVE RV056-MGMT-APPRVL-NAME   TO STRPPC-MGMT-APPRVL-NAME.            
124400     MOVE RV056-REJ-DTE            TO STRPPC-REJ-DTE.                     
124500     MOVE RV056-REJ-ID             TO STRPPC-REJ-ID.                      
124600     MOVE RV056-CPL-DTE            TO STRPPC-CPL-DTE.                     
124700     MOVE RV056-CPL-TIME           TO STRPPC-CPL-TIME.                    
124900     MOVE RV056-TOT-ACT-PR-CHG-AMT TO STRPPC-TOT-ACT-PR-CHG-AMT.          
125000     MOVE RV056-PGS-TO-PRT-NBR     TO STRPPC-PGS-TO-PRT-NBR.              
125100                                                                          
108900******************************************************************        
109000* INSERT A PARENT PPC REC INTO TSTRPPC                                    
109100******************************************************************        
109200 3500-INSERT-TSTRPPC.                                                     
110800     EXEC SQL                                                             
110900         INSERT INTO TSTRPPC                                              
111000             ( DEST_LOC_NBR                                               
111100              ,DOC_NBR                                                    
111200              ,DOC_TYP_CDE                                                
111300              ,EFF_DTE                                                    
111400              ,PR_CHG_CPL_NAME                                            
111500              ,RSN_CDE                                                    
111600              ,TYP_CDE                                                    
111700              ,STATUS_CDE                                                 
111800              ,PREV_STATUS_CDE                                            
111900              ,DEPT_AREA_DEST                                             
112000              ,ORGN_LOC_NBR                                               
112100              ,CRE_DTE                                                    
112200              ,CRE_ID                                                     
112300              ,CAN_DTE                                                    
112400              ,CAN_ID                                                     
112500              ,ENTR_DTE                                                   
112600              ,ENTR_TIME                                                  
112700              ,ENTR_ID                                                    
112800              ,MGMT_APPRVL_DTE                                            
112900              ,MGMT_APPRVL_ID                                             
113000              ,MGMT_APPRVL_NAME                                           
113100              ,REJ_DTE                                                    
113200              ,REJ_ID                                                     
113300              ,CPL_DTE                                                    
113400              ,CPL_TIME                                                   
113500              ,TOT_ACT_PR_CHG_AMT                                         
113600              ,PGS_TO_PRT_NBR                                             
113700             )                                                            
113800            VALUES                                                        
113900             ( :STRPPC-DEST-LOC-NBR                                       
114000              ,:STRPPC-DOC-NBR                                            
114100              ,:STRPPC-DOC-TYP-CDE                                        
114200              ,:STRPPC-EFF-DTE                                            
114300              ,:STRPPC-PR-CHG-CPL-NAME                                    
114400              ,:STRPPC-RSN-CDE                                            
114500              ,:STRPPC-TYP-CDE                                            
114600              ,:STRPPC-STATUS-CDE                                         
114700              ,:STRPPC-PREV-STATUS-CDE                                    
114800              ,:STRPPC-DEPT-AREA-DEST                                     
114900              ,:STRPPC-ORGN-LOC-NBR                                       
115000              ,CURRENT DATE                                               
115100              ,:STRPPC-CRE-ID                                             
115200              ,:STRPPC-CAN-DTE                                            
115300              ,:STRPPC-CAN-ID                                             
115400              ,CURRENT DATE                                               
115500              ,CURRENT TIME                                               
115600              ,:STRPPC-ENTR-ID                                            
115700              ,:STRPPC-MGMT-APPRVL-DTE                                    
115800              ,:STRPPC-MGMT-APPRVL-ID                                     
115900              ,:STRPPC-MGMT-APPRVL-NAME                                   
116000              ,:STRPPC-REJ-DTE                                            
116100              ,:STRPPC-REJ-ID                                             
116200              ,:STRPPC-CPL-DTE                                            
116300              ,:STRPPC-CPL-TIME                                           
116400              ,:STRPPC-TOT-ACT-PR-CHG-AMT                                 
116500              ,:STRPPC-PGS-TO-PRT-NBR                                     
116600             )                                                            
116700     END-EXEC                                                             
116800                                                                          
116900     EVALUATE TRUE                                                        
117000         WHEN SQLCODE = ZERO                                              
117100             ADD +1       TO PA-TOT-PPC-INS                               
117900                                                                          
118000         WHEN SQLCODE = -904                                              
118200         WHEN SQLCODE = -913                                              
118300             CONTINUE                                                     
118400                                                                          
118500         WHEN SQLWARN0 NOT EQUAL SPACE                                    
118600         WHEN SQLCODE NOT EQUAL ZERO                                      
118600             MOVE '3500-INSERT-TSTRPPC' TO PV-PARAGRAPH-NAME              
118600             MOVE 'TSTRPPC'             TO PV-DB2-TABLE                   
118600             MOVE 'INSERT INTO TSTRPPC' TO PV-ABEND-MESSAGE               
118600             MOVE SQLCODE               TO PV-SQLCODE                     
118700             DISPLAY '**************************************'             
118800             DISPLAY '** ABEND                            **'             
118900             DISPLAY '** PROGRAM = RVKUP003               **'             
119000             DISPLAY '** PARAGRAPH = 3500-INSERT-TSTRPPC  **'             
119100             DISPLAY '** INSERT INTO TSTRPPC              **'             
119200             DISPLAY '** DOC NBR    :' RV056-DOC-NBR                      
119300             DISPLAY '**************************************'             
119400             PERFORM 9100-SQL-ABEND                                       
119500     END-EVALUATE.                                                        
119700                                                                          
138700******************************************************************        
138800* LOAD THE TSPPCDT DCLGEN                                                 
138900******************************************************************        
139000 4000-LOAD-DCLTSPPCDT.                                                    
129000     INITIALIZE DCLTSPPCDT.                                               
139600     MOVE RV056-DEST-LOC-NBR       TO SPPCDT-DEST-LOC-NBR.                
139700     MOVE RV056-DOC-NBR            TO SPPCDT-DOC-NBR.                     
141100     MOVE RV056-ROW-NBR            TO SPPCDT-ROW-NBR.                     
139900     MOVE RV056-SKU-NBR            TO SPPCDT-SKU-NBR                      
141300     MOVE RV056-ACTL-PR-CHG-QTY    TO SPPCDT-ACTL-PR-CHG-QTY.             
141400     MOVE RV056-OLD-PR             TO SPPCDT-OLD-PR.                      
141500     MOVE RV056-NEW-PR             TO SPPCDT-NEW-PR.                      
141700     MOVE RV056-ACTL-PR-CHG-AMT    TO SPPCDT-ACTL-PR-CHG-AMT.             
141800     MOVE RV056-UPC-NBR            TO SPPCDT-UPC-NBR.                     
141900     MOVE RV056-ITEM-NBR           TO SPPCDT-ITEM-NBR.                    
142000     MOVE RV056-INV-DKEY           TO SPPCDT-INV-DKEY.                    
142200     MOVE RV056-DEPT-NBR           TO SPPCDT-DEPT-NBR.                    
143500     MOVE RV056-MAJ-CL-NBR         TO SPPCDT-MAJ-CL-NBR.                  
143700     MOVE RV056-SUB-CL-NBR         TO SPPCDT-SUB-CL-NBR.                  
143900                                                                          
128500******************************************************************        
128600* INSERT THE DTL REC INTO TSPPCDT                                         
128700******************************************************************        
128800 4500-INSERT-TSPPCDT.                                                     
130300     EXEC SQL                                                             
130400         INSERT INTO TSPPCDT                                              
130500             ( DEST_LOC_NBR                                               
130600              ,DOC_NBR                                                    
130700              ,SKU_NBR                                                    
130800              ,ROW_NBR                                                    
130900              ,ACTL_PR_CHG_QTY                                            
131000              ,OLD_PR                                                     
131100              ,NEW_PR                                                     
131200              ,ACTL_PR_CHG_AMT                                            
131300              ,UPC_NBR                                                    
131400              ,ITEM_NBR                                                   
131500              ,INV_DKEY                                                   
131600              ,DEPT_NBR                                                   
131700              ,MAJ_CL_NBR                                                 
131800              ,SUB_CL_NBR                                                 
131900             )                                                            
132000            VALUES                                                        
132100             ( :SPPCDT-DEST-LOC-NBR                                       
132200              ,:SPPCDT-DOC-NBR                                            
132300              ,:SPPCDT-SKU-NBR                                            
132400              ,:SPPCDT-ROW-NBR                                            
132500              ,:SPPCDT-ACTL-PR-CHG-QTY                                    
132600              ,:SPPCDT-OLD-PR                                             
132700              ,:SPPCDT-NEW-PR                                             
132800              ,:SPPCDT-ACTL-PR-CHG-AMT                                    
132900              ,:SPPCDT-UPC-NBR                                            
133000              ,:SPPCDT-ITEM-NBR                                           
133100              ,:SPPCDT-INV-DKEY                                           
133200              ,:SPPCDT-DEPT-NBR                                           
133300              ,:SPPCDT-MAJ-CL-NBR                                         
133400              ,:SPPCDT-SUB-CL-NBR                                         
133500             )                                                            
133600     END-EXEC                                                             
133700                                                                          
133800     EVALUATE TRUE                                                        
133900         WHEN SQLCODE = ZERO                                              
134000             ADD +1       TO PA-TOT-PPCDTL-INS                            
134100*HELPFUL FOR MATCHING RTV REC TO PERM REC                                 
134200*            DISPLAY '********************************'                   
134300*            DISPLAY '4500-INSERT-TSPPCDT'                                
134400*            DISPLAY '>>>>> DTL <<<<<'                                    
134700*            DISPLAY 'PERM DOC : ' RV056-DOC-NBR                          
134800*            DISPLAY 'ROW NUM  : ' RV056-ROW-NBR                          
134800*            DISPLAY 'UPC NUM  : ' RV056-UPC-NBR                          
134800*            DISPLAY 'ITEM NBR : ' RV056-ITEM-NBR                         
134900*            DISPLAY 'DEPT NBR :' RV056-DEPT-NBR                          
135000*            DISPLAY '------------------'                                 
135100                                                                          
135200         WHEN SQLCODE = -904                                              
135400         WHEN SQLCODE = -913                                              
135500             CONTINUE                                                     
135600                                                                          
135700         WHEN SQLWARN0 NOT EQUAL SPACE                                    
135800         WHEN SQLCODE NOT EQUAL ZERO                                      
118600             MOVE '4500-INSERT-TSPPCDT' TO PV-PARAGRAPH-NAME              
118600             MOVE 'TSPPCDT'             TO PV-DB2-TABLE                   
118600             MOVE 'INSERT INTO TSPPCDT' TO PV-ABEND-MESSAGE               
118600             MOVE SQLCODE               TO PV-SQLCODE                     
135900             DISPLAY '**************************************'             
136000             DISPLAY '** ABEND                            **'             
136100             DISPLAY '** PROGRAM = RVKUP003               **'             
136200             DISPLAY '** PARAGRAPH = 4500-INSERT-TSPPCDT  **'             
136300             DISPLAY '** INSERT INTO TSPPCDT              **'             
134700             DISPLAY 'PERM DOC : ' RV056-DOC-NBR                          
134800             DISPLAY 'ROW NUM  : ' RV056-ROW-NBR                          
134800             DISPLAY 'UPC NUM  : ' RV056-UPC-NBR                          
134800             DISPLAY 'ITEM NBR : ' RV056-ITEM-NBR                         
134900             DISPLAY 'DEPT NBR :'  RV056-DEPT-NBR                         
136900             DISPLAY '**************************************'             
137000             PERFORM 9100-SQL-ABEND                                       
137100     END-EVALUATE.                                                        
103200                                                                          
167100******************************************************************        
167200* 8000- READ THE EXTRACT FILE                                             
167500******************************************************************        
167600 8000-READ-MOS-FILE.                                                      
103200     MOVE LENGTH OF                                                       
103200                 RV056-HDR-DTL-RECORD TO DP001-WORK-STRG-LENGTH.          
103200     MOVE PC-CKPT-JOB-NAME            TO DP001-JOB-NAME.                  
103200     MOVE PC-CKPT-PLAN-NAME           TO DP001-PLAN-NAME.                 
103200                                                                          
103200     PERFORM Z900-CALL-TRANS-PRES-MODULE.                                 
103200                                                                          
103200     MOVE DP001-TRANS-RECORD          TO RV056-HDR-DTL-RECORD.            
103200                                                                          
103200     IF DP001-PREP-TRANS-EOF                                              
103200         SET PS-END-OF-FILE-YES       TO TRUE                             
103200     ELSE                                                                 
103200         ADD 1                        TO PA-TOT-RECS-READ                 
103200     END-IF.                                                              
103200                                                                          
167100******************************************************************        
167200* 9000-  FINISH UP                                                        
167500******************************************************************        
167600 9000-EOJ-ROUTINE.                                                        
167720                                                                          
167730     MOVE PC-TRAN-FUNC-CLOSE       TO DP001-FUNC-CODE.                    
167750     PERFORM 8000-READ-MOS-FILE                                           
167760                                                                          
169600     DISPLAY '***********************************************'.           
169700     DISPLAY '**    * RVKUP003 SUMMARY STATISTICS *        **'.           
169800     DISPLAY '**                                           **'.           
169900     DISPLAY '**      ---------------------------          **'.           
170000     DISPLAY '**        MOS LOADS TO PERM TABLES           **'.           
170100     DISPLAY '**      ---------------------------          **'.           
170400     DISPLAY '** TOT RECS READ       :' PA-TOT-RECS-READ.                 
170500     DISPLAY '** TOT PPC INS         :' PA-TOT-PPC-INS.                   
170600     DISPLAY '** TOT PPCDTL INS      :' PA-TOT-PPCDTL-INS.                
171100     DISPLAY '***********************************************'.           
171200                                                                          
171300******************************************************************        
171400* PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL    *        
171500* ERROR OR WARNING CODE OCCURS.                                  *        
171600******************************************************************        
171700 9100-SQL-ABEND.                                                          
171800******************************************************************        
171900* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
172000* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
172100* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
172200* FORMAT.                                                        *        
172300******************************************************************        
172400                                                                          
172500     COPY DPPD004.                                                        
172600                                                                          
174000     DISPLAY '***********************************************'.           
174100     DISPLAY '** ABEND PARAGRAPH **  ' PV-PARAGRAPH-NAME.                 
174200     DISPLAY '** ERROR MESSAGE   **  ' PV-ABEND-MESSAGE.                  
174210     MOVE PV-SQLCODE                TO PV-DISPLAY-NO-COMMA.               
174300     DISPLAY '** SQLCODE         **  ' PV-DISPLAY-NO-COMMA.               
174400     DISPLAY '** TABLE NAME      **  ' PV-DB2-TABLE.                      
174500     DISPLAY '***********************************************'.           
174600     DISPLAY ' '.                                                         
174700     MOVE +4013 TO ABEND-CODE.                                            
174800     CALL 'ILBOABN0' USING ABEND-CODE.                                    
174900                                                                          
177500******************************************************************        
177600* PRESENTATION MODULE - CHECKPOINT RESTART                                
177700******************************************************************        
174900     COPY DPPD001.                                                        
174900                                                                          
177500******************************************************************        
177600*                       END OF PROGRAM                                    
177700******************************************************************        
