       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ETKUT157.                                                 
       AUTHOR.        WADE GROVE.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/19/07.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM WILL CREATE THE FLAT FILE USED IN ETK0030.                 
      * THE PROGRAM WILL REPLACE THE MANUAL PROCESS PERFORMED BY THE            
      * EDI TEAM TO MAKE CHANGES TO PKMO.ET.ITEM.SELECT.  THE DATA WILL         
      * BE CREATED FROM DB2 TABLES UPDATED BY THE NEW SALES HISTORY             
      * SCREENS.                                                                
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ITEM-SELECT-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ITEM-SELECT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ITEM-SELECT-RECORD.                                   
                                                                                
       01  ITEM-SELECT-RECORD                   PIC X(80).                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
                                                                                
           05  PC-PROGRAM-NAME                  PIC X(08)  VALUE                
                                                           'ETKUT157'.          
       01  PV-PROGRAM-VARIABLES.                                                
                                                                                
           05  PV-PARAGRAPH-NAME                PIC X(30)  VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED       PIC X(30)  VALUE SPACES.        
                                                                                
       01  ABEND-CODE                           PIC S9(04) VALUE +0             
                                                           COMP SYNC.           
                                                                                
      *****************************************************************         
      *    ITEM SELECT RECORD.                                                  
      *****************************************************************         
           COPY ETRD004.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
      *    SQL COMMUNICATIONS AREA                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      *    TVNDDPT TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDDPT                                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *    TVNDAGR TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TVNDAGR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    VNT00004 TABLE LAYOUT                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE VNT00004                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TEXTENT TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TEXTENT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TENTNME TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TENTNME                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TTRDTRN TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TTRDTRN                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *    TTRDPRT TABLE LAYOUT                                                 
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TTRDPRT                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *     SELECT TRADING PARTNERS                                             
      *                                                                         
      *****************************************************************         
            EXEC SQL                                                            
               DECLARE VNT_MDSE_VND CURSOR FOR                                  
               SELECT DISTINCT A.ENT_ID,                                        
                      B.DUN_NBR,                                                
                      C.ENT_NAME_DESC,                                          
                      A.SLS_RPT_FREQ_CDE,                                       
                      A.SLS_RPT_OH_IND,                                         
                      A.SLS_RPT_ONORD_IND,                                      
                      A.SLS_RPT_SUM_CDE,                                        
                      A.SLS_RPT_DOW_CDE                                         
               FROM   VNT_MDSE_VND A,                                           
                      TEXTENT      B,                                           
                      TENTNME      C,                                           
                      TTRDTRN      D,                                           
                      TTRDPRT      E                                            
               WHERE  A.ENT_ID   = B.ENT_ID                                     
               AND    B.ENT_ID   = C.ENT_ID                                     
               AND    C.ENT_ID   = E.ENT_ID                                     
               AND    E.TP_DKEY  = D.TP_DKEY                                    
               AND    C.TYPE_CDE = '01'                                         
               AND    E.INACTV_DTE > '9999-01-01'                               
               AND    D.INACTV_DTE > '9999-01-01'                               
               AND    D.TRAN_SET_CDE = '852'                                    
               ORDER  BY C.ENT_NAME_DESC                                        
            END-EXEC.                                                           
                                                                                
      *****************************************************************         
      *     SELECT DEPARTMENT INFORMATION FOR THE TRADING PARTNER               
      *                                                                         
      *****************************************************************         
            EXEC SQL                                                            
               DECLARE DEPARTMENT_INFO CURSOR FOR                               
               SELECT   A.ENT_ID,                                               
                        A.DEPT_NBR,                                             
                        A.INHOUSE_NBR                                           
               FROM     TVNDDPT A                                               
               WHERE NOT EXISTS (SELECT   *                                     
                                 FROM     TVNDAGR B                             
                                 WHERE    A.DEPT_NBR = B.DEPT_NBR               
                                 AND      A.ENT_ID   = B.ENT_ID                 
                                 AND      AGRMT_TYP_ID = '4'                    
                                 AND      TYP_CDE = 9)                          
               AND      VND_DEPT_STAT_CD IN ('A','I')                           
               AND      A.ENT_ID = : VNT00004-ENT-ID                            
               ORDER BY ENT_ID,DEPT_NBR                                         
            END-EXEC.                                                           
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      ******************************************************************        
      *    OPEN ALL FILES,                                                      
      *    PERFORM INITIALIZATIONS                                              
      *    PROCESS ALL TRADING PARTNERS IN TABLE VNT_MDSE_VND TABLE             
      *    CLOSE ALL FILES                                                      
      ******************************************************************        
                                                                                
           MOVE 'A100-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           OPEN OUTPUT ITEM-SELECT-FILE.                                        
                                                                                
           PERFORM D100-OPEN-VNT-MDSE-VND-CURSOR.                               
                                                                                
           PERFORM D200-FETCH-VNT-MDSE-VND-RECS                                 
                   UNTIL SQLCODE = +100.                                        
                                                                                
           PERFORM D300-CLOSE-VNT-MDSE-VND-CURSOR.                              
                                                                                
           CLOSE ITEM-SELECT-FILE.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       D100-OPEN-VNT-MDSE-VND-CURSOR.                                           
      ******************************************************************        
      ******************************************************************        
      *    THE PARAGRAPH OPENS THE VNT_MDSE_VND CURSOR                          
      ******************************************************************        
                                                                                
           MOVE 'D100-OPEN-VNT-MDSE-VND-CURSOR' TO                              
                PV-PARAGRAPH-NAME.                                              
                                                                                
           EXEC SQL                                                             
                OPEN VNT_MDSE_VND                                               
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'OPEN VNT_MDSE_VND CURSOR'           TO                     
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
       D200-FETCH-VNT-MDSE-VND-RECS.                                            
      ******************************************************************        
                                                                                
           MOVE 'D200-FETCH-VNT-MDSE-VND-RECS' TO PV-PARAGRAPH-NAME.            
                                                                                
           EXEC SQL                                                             
               FETCH VNT_MDSE_VND                                               
               INTO  :VNT00004-ENT-ID,                                          
                     :EXTENT-DUN-NBR,                                           
                     :ENTNME-ENT-NAME-DESC,                                     
                     :VNT00004-SLS-RPT-FREQ-CDE,                                
                     :VNT00004-SLS-RPT-OH-IND,                                  
                     :VNT00004-SLS-RPT-ONORD-IND,                               
                     :VNT00004-SLS-RPT-SUM-CDE,                                 
                     :VNT00004-SLS-RPT-DOW-CDE                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
                                                                                
              PERFORM F100-WRITE-VENDOR-RECORD                                  
                                                                                
              PERFORM D400-OPEN-DEPARTMENT-INFO-CSR                             
                                                                                
              PERFORM D500-FETCH-DEPARTMENT-INFO-CSR                            
                      UNTIL SQLCODE = +100                                      
                                                                                
              PERFORM D600-CLOSE-DEPARTMENT-INFO-CSR                            
                                                                                
           ELSE                                                                 
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH-VNT-MDSE-VND-RECORDS'  TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-ABEND                                           
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
       D300-CLOSE-VNT-MDSE-VND-CURSOR.                                          
      ******************************************************************        
                                                                                
           MOVE 'D300-CLOSE-VNT-MDSE-VND-CURSOR' TO PV-PARAGRAPH-NAME.          
                                                                                
           EXEC SQL                                                             
                CLOSE VNT_MDSE_VND                                              
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'CLOSE VNT_MDSE_VND'    TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
       D400-OPEN-DEPARTMENT-INFO-CSR.                                           
      *****************************************************************         
                                                                                
                                                                                
           MOVE 'D400-OPEN-DEPARTMENT-INFO-CSR' TO                              
                PV-PARAGRAPH-NAME.                                              
                                                                                
           EXEC SQL                                                             
                OPEN DEPARTMENT_INFO                                            
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'OPEN DEPARTMENT_INFO'    TO                                
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
       D500-FETCH-DEPARTMENT-INFO-CSR.                                          
      *****************************************************************         
                                                                                
           MOVE 'D500-FETCH-DEPARTMENT-INFO-CSR' TO                             
                PV-PARAGRAPH-NAME.                                              
                                                                                
           EXEC SQL                                                             
               FETCH DEPARTMENT_INFO                                            
               INTO  :VNDDPT-ENT-ID,                                            
                     :VNDDPT-DEPT-NBR,                                          
                     :VNDDPT-INHOUSE-NBR                                        
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +0                                                      
                                                                                
              PERFORM F200-WRITE-DEPARTMENT-RECORD                              
                                                                                
           ELSE                                                                 
                                                                                
               IF (SQLCODE NOT = +100) OR                                       
                  (SQLWARN0    = 'W')                                           
                                                                                
                   MOVE 'FETCH-VNT-MDSE-VND-RECORDS'  TO                        
                        PV-DB2-OPERATION-ATTEMPTED                              
                                                                                
                   PERFORM Z999-ABEND                                           
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
       D600-CLOSE-DEPARTMENT-INFO-CSR.                                          
      *****************************************************************         
                                                                                
           MOVE 'D600-CLOSE-DEPARTMENT-INFO-CSR' TO                             
                PV-PARAGRAPH-NAME.                                              
                                                                                
           EXEC SQL                                                             
                CLOSE DEPARTMENT_INFO                                           
           END-EXEC.                                                            
                                                                                
           IF (SQLCODE NOT = +0)  OR                                            
              (SQLWARN0 = 'W')                                                  
                                                                                
               MOVE 'CLOSE DEPARTMENT_INFO' TO                                  
                    PV-DB2-OPERATION-ATTEMPTED                                  
                                                                                
               PERFORM Z999-ABEND                                               
                                                                                
           END-IF.                                                              
                                                                                
      *****************************************************************         
       F100-WRITE-VENDOR-RECORD.                                                
      *****************************************************************         
                                                                                
           INITIALIZE ITEM-SELECT-RECORD.                                       
           INITIALIZE ET004-VENDOR-REC.                                         
                                                                                
           MOVE EXTENT-DUN-NBR       TO ET004-VENDOR-DUNNS.                     
           MOVE '1'                  TO ET004-VENDOR-REC-TYPE.                  
           MOVE ENTNME-ENT-NAME-DESC TO ET004-VENDOR-NAME.                      
                                                                                
           WRITE ITEM-SELECT-RECORD FROM ET004-VENDOR-REC.                      
                                                                                
           INITIALIZE ET004-VENDOR-REC.                                         
                                                                                
      *****************************************************************         
       F200-WRITE-DEPARTMENT-RECORD.                                            
      *****************************************************************         
                                                                                
           INITIALIZE ITEM-SELECT-RECORD.                                       
           INITIALIZE ET004-DPT-CL-INHSE-REC.                                   
                                                                                
           MOVE EXTENT-DUN-NBR     TO ET004-DPT-CL-INHSE-DUNNS.                 
           MOVE '2'                TO ET004-DPT-CL-INHSE-REC-TYPE.              
           MOVE VNDDPT-DEPT-NBR    TO ET004-DEPT.                               
           MOVE VNDDPT-INHOUSE-NBR TO ET004-DEPT-INHOUSE.                       
                                                                                
      ***                                                                       
      *    IF VNT00004-SLS-RPT-ONORD-IND = 'Y'                                  
      *       MOVE 'A' TO ET004-ALL-INDIV-SALES-IND                             
      *    ELSE                                                                 
      *       IF VNT00004-SLS-RPT-ONORD-IND = 'N'                               
      *          MOVE 'I' TO ET004-ALL-INDIV-SALES-IND                          
      *       ELSE                                                              
      *          MOVE 'ON ORDER INCORRECT' TO                                   
      *               PV-DB2-OPERATION-ATTEMPTED                                
      *          PERFORM Z999-ABEND.                                            
      ***                                                                       
           MOVE 'A' TO ET004-ALL-INDIV-SALES-IND.                               
                                                                                
           IF VNT00004-SLS-RPT-OH-IND = 'Y' OR                                  
              VNT00004-SLS-RPT-OH-IND = 'N'                                     
              MOVE VNT00004-SLS-RPT-OH-IND TO ET004-ONHAND-IND                  
           ELSE                                                                 
              MOVE 'ON HAND INCORRECT' TO                                       
                   PV-DB2-OPERATION-ATTEMPTED                                   
              PERFORM Z999-ABEND.                                               
                                                                                
           IF VNT00004-SLS-RPT-DOW-CDE = '1'                                    
              MOVE 'X      ' TO                                                 
                   ET004-DAY-OF-WEEK-TRANSMIT                                   
           ELSE                                                                 
              IF VNT00004-SLS-RPT-DOW-CDE = '2'                                 
                 MOVE ' X     ' TO                                              
                      ET004-DAY-OF-WEEK-TRANSMIT                                
              ELSE                                                              
                 IF VNT00004-SLS-RPT-DOW-CDE = '3'                              
                    MOVE '  X    ' TO                                           
                         ET004-DAY-OF-WEEK-TRANSMIT                             
                 ELSE                                                           
                    IF VNT00004-SLS-RPT-DOW-CDE = '4'                           
                       MOVE '   X   ' TO                                        
                            ET004-DAY-OF-WEEK-TRANSMIT                          
                    ELSE                                                        
                       IF VNT00004-SLS-RPT-DOW-CDE = '5'                        
                          MOVE '    X  ' TO                                     
                               ET004-DAY-OF-WEEK-TRANSMIT                       
                       ELSE                                                     
                          IF VNT00004-SLS-RPT-DOW-CDE = '6'                     
                             MOVE '     X ' TO                                  
                                  ET004-DAY-OF-WEEK-TRANSMIT                    
                          ELSE                                                  
                             IF VNT00004-SLS-RPT-DOW-CDE = '7'                  
                                MOVE '      X' TO                               
                                     ET004-DAY-OF-WEEK-TRANSMIT                 
                             ELSE                                               
                                MOVE 'DAY OF WEEK INCORRECT'  TO                
                                     PV-DB2-OPERATION-ATTEMPTED                 
                                PERFORM Z999-ABEND.                             
                                                                                
           IF VNT00004-SLS-RPT-FREQ-CDE = 'WKY'                                 
              MOVE 'W' TO ET004-DAILY-WEEKLY-IND                                
           ELSE                                                                 
              IF VNT00004-SLS-RPT-FREQ-CDE = 'DLY'                              
                 MOVE 'D' TO ET004-DAILY-WEEKLY-IND                             
                 MOVE 'XXXXXXX' TO ET004-DAY-OF-WEEK-TRANSMIT                   
              ELSE                                                              
                 MOVE 'FREQUENCY INCORRECT'  TO                                 
                       PV-DB2-OPERATION-ATTEMPTED                               
                 PERFORM Z999-ABEND.                                            
                                                                                
           IF VNT00004-SLS-RPT-SUM-CDE = 'STR'                                  
              MOVE SPACES TO ET004-STORE-CORP-IND                               
              MOVE SPACES TO ET004-ITEM-SUMMARY-IND                             
           ELSE                                                                 
              IF VNT00004-SLS-RPT-SUM-CDE = 'CRP'                               
                 MOVE 'CORP' TO ET004-STORE-CORP-IND                            
                 MOVE 'SUM'  TO ET004-ITEM-SUMMARY-IND                          
              ELSE                                                              
                 IF VNT00004-SLS-RPT-SUM-CDE = 'SUM'                            
                    MOVE SPACES TO ET004-STORE-CORP-IND                         
                    MOVE 'SUM'  TO ET004-ITEM-SUMMARY-IND                       
                 ELSE                                                           
                    MOVE 'STORE OR ITEM SUMMARY INDICATORS INCORRECT'           
                         TO PV-DB2-OPERATION-ATTEMPTED                          
                    PERFORM Z999-ABEND.                                         
                                                                                
           WRITE ITEM-SELECT-RECORD FROM ET004-DPT-CL-INHSE-REC.                
                                                                                
           INITIALIZE ET004-DPT-CL-INHSE-REC.                                   
                                                                                
      *****************************************************************         
       Z999-ABEND.                                                              
      *****************************************************************         
      *****************************************************************         
                                                                                
           MOVE 'Z999-ABEND' TO PV-PARAGRAPH-NAME.                              
           DISPLAY '  '.                                                        
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ETKUT157 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'ENT-ID    = ' VNT00004-ENT-ID.                              
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
