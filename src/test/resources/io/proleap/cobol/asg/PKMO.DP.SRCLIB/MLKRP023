000010 IDENTIFICATION DIVISION.                                                 
000030 PROGRAM-ID.    MLKRP023.                                                 
000040 AUTHOR.        TESS BESTE.                                               
000050 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000060 DATE-WRITTEN.  04-18-94.                                                 
000070 DATE-COMPILED.                                                           
000080                                                                          
000090******************************************************************        
000100*           MLKRP023 - ACCUM PRICE CHANGES                                
000110*                                                                         
000120* THIS PROGRAM ACCUMULATES THE AMOUNTS FROM A FILE OF PRICE CHANGE        
000130* TRANSACTIONS IN THE MLRD023 FORMAT. THE TOTAL RETAIL AMOUNT IS          
000140* DISPLAYED.                                                              
000150*                                                                         
000160******************************************************************        
000170                                                                 *        
000190 ENVIRONMENT DIVISION.                                                    
000210 CONFIGURATION SECTION.                                                   
000230 SOURCE-COMPUTER.    IBM-3090.                                            
000240 OBJECT-COMPUTER.    IBM-3090.                                            
000270 INPUT-OUTPUT SECTION.                                                    
000280                                                                          
000290 FILE-CONTROL.                                                            
000300     SELECT INPUT-FILE                                                    
000310         ASSIGN TO INP01.                                                 
000320                                                                          
000330                                                                          
000340                                                                          
000350 DATA DIVISION.                                                           
000360                                                                          
000370 FILE SECTION.                                                            
000380                                                                          
000390 FD  INPUT-FILE                                                           
000400     LABEL RECORDS ARE STANDARD                                           
000410     BLOCK CONTAINS 0 RECORDS                                             
000420     RECORDING  MODE IS F.                                                
000430**1  INPUT-RECORD                PIC X(130).                              
000431     COPY MLRD023.                                                        
000440                                                                          
000450 WORKING-STORAGE SECTION.                                                 
000460                                                                          
000470******************************************************************        
000480* INPUT RECORD LAYOUT                                                     
000490******************************************************************        
000500                                                                          
000510*++INCLUDE MLRD023                                                        
000520                                                                          
000530 01  PS-PROGRAM-SWITCHES.                                                 
000540     05  PS-END-OF-INPUT-FILE-SW      PIC X         VALUE 'N'.            
000550         88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.            
000560         88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.            
000570                                                                          
000580 01  PV-PROGRAM-VARIABLES.                                                
000590     05  PV-MKDN-RECORD-COUNT         PIC S9(13)    VALUE +0              
000600                                                    COMP-3.               
000610     05  PV-MKDN-ACCUM-AMT            PIC S9(13)V99 VALUE +0              
000620                                                    COMP-3.               
000630     05  PV-MKUP-RECORD-COUNT         PIC S9(13)    VALUE +0              
000640                                                    COMP-3.               
000650     05  PV-MKUP-ACCUM-AMT            PIC S9(13)V99 VALUE +0              
000660                                                    COMP-3.               
000670     05  PV-DISPLAY-COUNT             PIC ZZZ,ZZZ,ZZZ,ZZZ-.               
000680     05  PV-DISPLAY-AMT               PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.            
000690 EJECT                                                                    
000700 PROCEDURE DIVISION.                                                      
000710                                                                          
000720                                                                          
000730 A100-PROGRAM-MAINLINE.                                                   
000740                                                                          
000750     OPEN INPUT INPUT-FILE.                                               
000760     READ INPUT-FILE NEXT                                                 
000770         AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
000780                                                                          
000790     PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
000800         IF ML023-PC-TRANS-CDE = '40'                                     
000810             ADD +1 TO PV-MKDN-RECORD-COUNT                               
000820             COMPUTE PV-MKDN-ACCUM-AMT                                    
000830                   = PV-MKDN-ACCUM-AMT                                    
000840                   + ML023-PC-EXTD-RTL-AMT                                
000850         ELSE                                                             
000860             ADD +1 TO PV-MKUP-RECORD-COUNT                               
000870             COMPUTE PV-MKUP-ACCUM-AMT                                    
000880                   = PV-MKUP-ACCUM-AMT                                    
000890                   + ML023-PC-EXTD-RTL-AMT                                
000900         END-IF                                                           
000910         READ INPUT-FILE NEXT                                             
000920             AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
000930         END-READ                                                         
000940     END-PERFORM.                                                         
000950                                                                          
000960     MOVE PV-MKDN-RECORD-COUNT TO PV-DISPLAY-COUNT.                       
000970     DISPLAY 'MARKDOWN TRANSACTION COUNT: ' PV-DISPLAY-COUNT.             
000980     MOVE PV-MKDN-ACCUM-AMT   TO PV-DISPLAY-AMT.                          
000990     DISPLAY 'MARKDOWN TOTAL RETAIL:      ' PV-DISPLAY-AMT.               
001000     DISPLAY ' '.                                                         
001010     MOVE PV-MKUP-RECORD-COUNT TO PV-DISPLAY-COUNT.                       
001020     DISPLAY 'MARUP TRANSACTION COUNT:    ' PV-DISPLAY-COUNT.             
001030     MOVE PV-MKUP-ACCUM-AMT   TO PV-DISPLAY-AMT.                          
001040     DISPLAY 'MARKUP TOTAL RETAIL:        ' PV-DISPLAY-AMT.               
001050                                                                          
001060     CLOSE INPUT-FILE.                                                    
001070     STOP RUN.                                                            
